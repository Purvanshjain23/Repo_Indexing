**FREE
//------------------------------------------------------------------------------------ //
//Created By  :  Programmers.io @ 2025                                                 //
//Created Date:  2025/10/14                                                            //
//Developer   :  Naman Ramawat                                                         //
//Description :  Warehouse Master Maintenance program                                  //
//                                                                                     //
//PROCEDURE LOG:                                                                       //
//------------------------------------------------------------------------------------ //
//Procedure Name           | Procedure Description                                     //
//-------------------------|---------------------------------------------------------- //
//No Procedure             |                                                           //
//------------------------------------------------------------------------------------ //
//                                                                                     //
//MODIFICATION LOG:                                                                    //
//------------------------------------------------------------------------------------ //
//Date    | Mod_ID | Developer  | Case and Description                                 //
//--------|--------|------------|----------------------------------------------------- //
//25/10/14|        | NAMAN      |  New program created.                                //
//------------------------------------------------------------------------------------ //
ctl-opt option(*nodebugio: *srcstmt: *nounref);

//------------------------------------------------------------------------------------ //
//File Definitions
//------------------------------------------------------------------------------------ //
dcl-f WRHMAST usage(*input:*output:*update) keyed;
dcl-f WRHDSPF workstn indds(IND_DS);

//------------------------------------------------------------------------------------ //
//Data Structure
//------------------------------------------------------------------------------------ //
dcl-ds IND_DS;
    Exit ind pos(03);
    Refresh ind pos(05);
    ProCur  ind pos(10);
    InvWarHId ind pos(11);
    WarHIdNotFnd Ind pos(13);
    Cancel ind pos(12);
end-ds;

//------------------------------------------------------------------------------------ //
//Work Variables
//------------------------------------------------------------------------------------ //
dcl-s uwWareHouID CHAR(10);

//------------------------------------------------------------------------------------ //
//Constants
//------------------------------------------------------------------------------------ //
dcl-c True  const(*ON);
dcl-c False const(*OFF);
dcl-c One const(1);
dcl-c Two const(2);

//------------------------------------------------------------------------------------ //
//Main Line
//------------------------------------------------------------------------------------ //
dow Exit = False;
  // Main menu
  ProCur = true;
  write MAINMENU;
  exfmt MAINMENU;
  ProCur = False;
  ErrInfo = *blanks;
  //Process the Options
  select;
    when Exit = True;
    //If Opt2, Process to add the item details
    when Refresh = True;
         Opt = *zeros;
         Refresh = False;
    //If Opt2, Process to add the item details
    when Opt = One;
         Exsr AddDetails;
    //Display Items
    when Opt = Two;
         Exsr DisplayDtl;
    when Opt <> *zero;
      // Invalid option, redisplay menu
    ErrInfo = 'Invalid Option';
  endsl;

enddo;
*inlr = True;
//--------------------------------------------------------------------------------------//
//Display the Item Details.                                                             //
//--------------------------------------------------------------------------------------//
   Begsr DisplayDtl;
      //Display Item
      WHSID = *Blanks;
      ErrMsg = *blanks;
      dow Cancel = False and Exit = False;
        Exfmt DSPWHRH;
        ErrMsg = *blanks;
        WarHIdNotFnd = False;
        if Exit = True OR
           Cancel = True;
          leave;
        endif;
        if Refresh = True;
           WHSID = *blanks;
           ErrMsg = *blanks;
           Refresh = False;
           iter;
        Endif;
        if WHSID = *blanks;
           ErrMsg = 'Warehouse ID cannot be Blank!';
           WarHIdNotFnd = True;
           ITER;
        ENDIF;
        // Search for item
        chain WHSID WRHMAST;
        if %found(WRHMAST);
          // Display found record
          DOW EXIT = FALSE and
              CANCEL = FALSE;
          exfmt DSPREC;
          Enddo;
          CANCEL = FALSE;
        else;
          ErrMsg = 'Warehouse ID not found!';
          WarHIdNotFnd = True;
        endif;
      enddo;
      Cancel = False;
endsr;

//-------------------------------------------------------------------------------/
//Add the Details into Warehouse Master file
//-------------------------------------------------------------------------------/
   Begsr AddDetails;
      dow Exit = False and Cancel = False;
        IF InvWarHId  = False;
           exsr ClearFlds;
        Endif;
        exfmt ADDREC;
        ErrMsg = *blanks;
        InvWarHId = False;
        if Exit = True;
          leave;
        elseif Cancel = True;
          leave;
        elseif Refresh = True;
               Refresh = False;
        iter;
        endif;
        if WHSID = *blanks;
           ErrMsg = 'Warehouse ID cannot be Blank!';
           InvWarHId = True;
           iter;
        else;
           uwWareHouID = *blanks;
           uwWareHouID = WHSID;
        endif;
        // Check for duplicate
        chain WHSID WRHMAST;
        if %found(WRHMAST);
          exsr CLEARFLDS;
          ErrMsg = 'Warehouse ID already exists!';
          WHSID = uwWareHouID;
          InvWarHId = True;
          iter;
        endif;
        // Write new record
        write RWRHMAST;
        ErrMsg = 'Warehouse details added successfully!';
      enddo;
      Cancel = False;
   endsr;
//-------------------------------------------------------------------------------/
//Clear the qork fields to avoid retained values
//-------------------------------------------------------------------------------/
   Begsr CLEARFLDS;

         WHSID   = *blanks;
         WHLOCATN = *blanks;
         WHCAPCTY = *zero;
         WHEMAIL = *blanks;

   endsr;
