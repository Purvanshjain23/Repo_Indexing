**FREE
//------------------------------------------------------------------------------------ //
//Created By  :  Programmers.io @ 2025                                                 //
//Created Date:  2025/10/13                                                            //
//Developer   :  Naman Ramawat                                                         //
//Description :  Item Master Maintenance program                                       //
//                                                                                     //
//PROCEDURE LOG:                                                                       //
//------------------------------------------------------------------------------------ //
//Procedure Name           | Procedure Description                                     //
//-------------------------|---------------------------------------------------------- //
//No Procedure             |                                                           //
//------------------------------------------------------------------------------------ //
//                                                                                     //
//MODIFICATION LOG:                                                                    //
//------------------------------------------------------------------------------------ //
//Date    | Mod_ID | Developer  | Case and Description                                 //
//--------|--------|------------|----------------------------------------------------- //
//25/10/13|        | NAMAN      |  New program created.                                //
//------------------------------------------------------------------------------------ //
ctl-opt option(*nodebugio: *srcstmt: *nounref);

//------------------------------------------------------------------------------------ //
//File Definitions
//------------------------------------------------------------------------------------ //
dcl-f ITMMAST usage(*input:*output:*update) keyed;
dcl-f MASTDSP workstn indds(IND_DS);

//------------------------------------------------------------------------------------ //
//Data Structure
//------------------------------------------------------------------------------------ //
dcl-ds IND_DS;
    Exit ind pos(03);
    Refresh ind pos(05);
    ProCur  ind pos(10);
    InvProdId ind pos(11);
    ProdIdNotFnd Ind pos(13);
    Cancel ind pos(12);
end-ds;

dcl-ds Item extname('ITMMAST') qualified end-ds;

//------------------------------------------------------------------------------------ //
//Work Variables
//------------------------------------------------------------------------------------ //
dcl-s Title char(40) inz('Item Master Maintenance');
dcl-s uwItmProdId zoned(5:0);

//------------------------------------------------------------------------------------ //
//Constants
//------------------------------------------------------------------------------------ //
dcl-c True  const(*ON);
dcl-c False const(*OFF);
dcl-c One const(1);
dcl-c Two const(2);

//------------------------------------------------------------------------------------ //
//Main Line
//------------------------------------------------------------------------------------ //
dow Exit = False;
  // Main menu
  ProCur = true;
  write MAINMENU;
  exfmt MAINMENU;
  ProCur = False;
  ErrInfo = *blanks;
  //Process the Options
  select;
    when Exit = True;
    //If Opt2, Process to add the item details
    when Refresh = True;
         Opt = *zeros;
         Refresh = False;
    //If Opt2, Process to add the item details
    when Opt = One;
         Exsr AddDetails;
    //Display Items
    when Opt = Two;
         Exsr DisplayDtl;
    when Opt <> *zero;
      // Invalid option, redisplay menu
    ErrInfo = 'Invalid Option';
  endsl;

enddo;
*inlr = True;
//--------------------------------------------------------------------------------------//
//Display the Item Details.                                                             //
//--------------------------------------------------------------------------------------//
   Begsr DisplayDtl;
      //Display Item
      ITMPRODID = *zero;
      ErrMsg = *blanks;
      dow Cancel = False and Exit = False;
        Exfmt DSPITMNO;
        ErrMsg = *blanks;
        ProdIdNotFnd = False;
        if Exit = True OR
           Cancel = True;
          leave;
        endif;
        if Refresh = True;
           ITMPRODID = *zero;
           ErrMsg = *blanks;
           Refresh = False;
           iter;
        Endif;
        // Search for item
        chain ITMPRODID ITMMAST;
        if %found(ITMMAST);
          // Display found record
          DOW EXIT = FALSE and
              CANCEL = FALSE;
          exfmt DSPREC;
          Enddo;
          CANCEL = FALSE;
        else;
          ErrMsg = 'Item not found!';
          ProdIdNotFnd = True;
        endif;
      enddo;
      Cancel = False;
endsr;

//-------------------------------------------------------------------------------/
//Add the Details into Item Master file
//-------------------------------------------------------------------------------/
   Begsr AddDetails;
      dow Exit = False and Cancel = False;
        IF InvProdId = False;
           exsr ClearFlds;
        Endif;
        exfmt ADDREC;
        ErrMsg = *blanks;
        InvProdId = False;
        if Exit = True;
          leave;
        elseif Cancel = True;
          leave;
        elseif Refresh = True;
               Refresh = False;
        iter;
        endif;
        if ITMPRODID = *zeros;
           ErrMsg = 'Product ID cannot be zero!';
           InvProdId = True;
           iter;
        else;
           uwItmProdId = *zero;
           uwItmProdId = ITMPRODID;
        endif;
        // Check for duplicate
        chain ITMPRODID ITMMAST;
        if %found(ITMMAST);
          exsr CLEARFLDS;
          ErrMsg = 'Product ID already exists!';
          ITMPRODID = uwItmProdId;
          InvProdId = True;
          iter;
        endif;
        // Write new record
        write RITMMAST;
        ErrMsg = 'Item added successfully!';
      enddo;
      Cancel = False;
   endsr;
//-------------------------------------------------------------------------------/
//Clear the qork fields to avoid retained values
//-------------------------------------------------------------------------------/
   Begsr CLEARFLDS;

         ITMPRODID = *zero;
         ITMNAME = *blanks;
         ITMDESC = *blanks;
         ITMQNTY = *zero;
         ITMPRICE = *zero;
         ITMCATGRY = *blanks;
         ITMSUPPID = *blanks;

   endsr;
