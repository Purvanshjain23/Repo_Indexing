/********************************************************************/
/*                                                                  */
/* PLARUPC   - PRT SBM LD NOT CNFRM RPCL                            */
/* SYNOPSIS  - PROMPT JOB CONTROL FOR PRINTER OVERRIDE INFORMATION  */
/*             TO PRINT THE LOADS NOT CONFIRMED EXCEPTION REPORT    */
/*                                                                  */
/* SYSTEM    - ORDER MANAGEMENT SYSTEM                              */
/* DATE      - 08/22/2006                                           */
/* PROGRAMMER- LARA BUSER                                           */
/*                                                                  */
/* PARAMETERS RECEIVED:                                             */
/*   &RETURN     - RETURN CODE                                      */
/*   &APPLIC     - APPLICATION                                      */
/*   &COMPNYN    - COMPANY NUMBER - BLANK FOR ALL                   */
/*   &SHPDATN    - PROCESS ORDERS FOR REQUESTED SHIP DATE           */
/*   &OUTQ       - OUTPUT QUEUE                                     */
/*   &HOLD       - HOLD PRINT                                       */
/*   &SAVE       - SAVE PRINT                                       */
/*   &NQSTAT     - NIGHT QUEUE STATUS                               */
/*   &NIGHTQ     - NIGHT OUTPUT QUEUE                               */
/*   &COPYN      - NUMBER OF COPIES                                 */
/********************************************************************/
/*  MODIFICATIONS:                                                  */
/* ---------------------------------------------------------------- */
/*                                                                  */
/********************************************************************/
 PLARUPC:    PGM        PARM(&RETURN &APPLIC &COMPNYN &SHPDATN &OUTQ +
                          &HOLD &SAVE &COPYN &NQSTAT &NIGHTQ)

/* PARMS                                                                      */
             DCL        VAR(&RETURN) TYPE(*CHAR) LEN(7) /* RETURN +
                          CODE              */
             DCL        VAR(&APPLIC) TYPE(*CHAR) LEN(6) /* +
                          APPLICATION              */
             DCL        VAR(&COMPNYN) TYPE(*DEC) /* Company Number +
                          (Passed)   */
             DCL        VAR(&SHPDATN) TYPE(*DEC) /* For Ship Date +
                          (Passed)   */
             DCL        VAR(&OUTQ) TYPE(*CHAR) LEN(10) /* Output +
                          Queue              */
             DCL        VAR(&HOLD) TYPE(*CHAR) LEN(4) /* Hold +
                          Status               */
             DCL        VAR(&SAVE) TYPE(*CHAR) LEN(4) /* Save +
                          Status               */
             DCL        VAR(&COPYN) TYPE(*DEC) /* No. of Copies  +
                          (Passed)   */
             DCL        VAR(&NQSTAT) TYPE(*CHAR) LEN(1) /* If MUST +
                          run in night q.   */
             DCL        VAR(&NIGHTQ) TYPE(*CHAR) LEN(10) /* Night +
                          Job Queue           */

             DCL        VAR(&COPY) TYPE(*DEC) LEN(2 0) /* NUMBER OF +
                          COPIES         */
             DCL        VAR(&DESC) TYPE(*CHAR) LEN(25) VALUE('Loads +
                          Not Confirmed') /* PMT PGM +
                          DESCRIPTION      */

/* ERROR MESSAGE FIELDS                                              */
             DCL        VAR(&MSG) TYPE(*CHAR) LEN(512) /* +
                          MESSAGE                  */
             DCL        VAR(&MSGID) TYPE(*CHAR) LEN(7) /* MESSAGE +
                          ID               */
             DCL        VAR(&MSGL) TYPE(*DEC) LEN(5 0)

/********************************************************************/
/*   INITIALIZE VARIABLES                                           */
/********************************************************************/
             CHGVAR     VAR(&RETURN) VALUE('       ')
             CHGVAR     VAR(&COPY) VALUE(&COPYN)

/********************************************************************/
/*   MONITOR FOR ALL MESSAGES AT PROGRAM LEVEL                      */
/********************************************************************/
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(MSGTRAP))

/********************************************************************/
/* PROMPT FOR PRINTER OVERRIDE INFORMATION                          */
/********************************************************************/
 PRINTER:    CALL       PGM(PDG0PVR) PARM(&RETURN &DESC &OUTQ &HOLD +
                          &SAVE &COPY &NQSTAT &NIGHTQ &APPLIC)
             IF         COND(&RETURN *NE ' ') THEN(GOTO +
                          CMDLBL(USERQUIT))

             CHGVAR     VAR(&COPYN) VALUE(&COPY)

/*********************************************************************/
/* CALL THE PROGRAM TO SUBMIT THE EXCEPTION REPORT                   */
/*********************************************************************/
             SBMJOB     CMD(CALL PGM(PLASUPC) PARM(&RETURN &APPLIC +
                          &COMPNYN &SHPDATN &OUTQ &HOLD &SAVE +
                          &COPYN &NQSTAT &NIGHTQ)) JOB(LDNOTCNFRM) +
                          OUTQ(&NIGHTQ)

             GOTO       CMDLBL(END)

/*-------------------------USER QUIT--------------------------------*/
/* USER QUIT REQUESTED                                              */
/*------------------------------------------------------------------*/
 USERQUIT:   CHGVAR     VAR(&RETURN) VALUE('Y2U9999')
             GOTO       CMDLBL(END)

/*----------------------RECEIVE MESSAGES----------------------------*/

 MSGTRAP:    RCVMSG     MSGTYPE(*EXCP) MSGDTA(&MSG) MSGDTALEN(&MSGL) +
                          MSGID(&MSGID)
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(END))

             CHGJOB     LOG(4 20 *SECLVL) LOGCLPGM(*YES)

             SNDPGMMSG  MSGID(&MSGID) MSGF(QCPFMSG) MSGDTA(%SST(&MSG +
                          1 &MSGL)) MSGTYPE(*ESCAPE)
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(END))

 END:        ENDPGM
