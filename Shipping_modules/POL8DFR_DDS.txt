     A*%%TS  SD  20010717  103012  ISLRAMS     REL-V4R4M0  5769-PW1
     A*****************************************************************
     A*  SYSTEM:        ORDER MANAGEMENT SYSTEM
     A*  DISPLAY FILE:  POL8DFR#
     A*  TITLE:         VIEW ITEM LOADS DISPLAY
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  DATE:          07/16/01
     A*****************************************************************
     A*  RMC 06/07/04  PKTR01 REMOVE COMPANY FROM SCREEN
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      HELP(28 'HELP')
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A                                      CF05(05 'REFRESH')
     A*
     A*
     A*---------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*---------------------------------------------------------------
     A          R OM7001S1                  SFL
     A*%%TS  SD  20010717  093835  ISLRAMS     REL-V4R4M0  5769-PW1
     A  85                                  SFLNXTCHG
     A            HDRRN1         4Y 0H
     A            D1MDY          6Y 0O 12  2EDTWRD('  /  /  ')
     A            D1LOADID  R        O 12 13REFFLD(@BECPFI/BERMNB *LIBL/OPBFCPP)
     A            D1ITCD    R        O 12 25REFFLD(@BGWKP/OMHGCD *LIBL/OPBGWKP)
     A                                      EDTCDE(Z)
     A            D1ORNO    R        O 12 34REFFLD(@BECPFI/BEC4NB *LIBL/OPBFCPP)
     A                                      EDTCDE(Z)
     A            D1ORST    R        O 12 44REFFLD(@BECPFI/BEGWST *LIBL/OPBFCPP)
     A            D1SCQT         5Y 0O 12 48EDTCDE(K)
     A            D1SCWT         5Y 0O 12 56EDTCDE(K)
     A            D1SHQT         5Y 0O 12 64EDTCDE(K)
     A            D1SHWT         5Y 0O 12 72EDTCDE(K)
     A            D1LOADST  R        O 12 22REFFLD(@FJCPBP/FJPKST *LIBL/OMFJCPP)
     A*---------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*---------------------------------------------------------------
     A          R OM7001C1                  SFLCTL(OM7001S1)
     A*%%TS  SD  20010717  094829  ISLRAMS     REL-V4R4M0  5769-PW1
     A                                      SFLSIZ(0250)
     A                                      SFLPAG(0010)
     A                                      OVERLAY
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 26'Order Management System'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  2 29'View Item Loads'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A*                                 4  2'Company Number .............:'
     A*           C1AIC3    R        O  4 32REFFLD(@BECPFI/BEAIC3 *LIBL/OPBFCPP)
     A                                  5  2'Scheduled Ship Date Range ..:'
     A            C1FRMDY        6Y 0O  5 32EDTWRD('  /  /  ')
     A            C1TOMDY        6Y 0O  5 42EDTWRD('  /  /  ')
     A                                  6  2'Warehouse Code .............:'
     A            C1WHCD    R        O  6 32REFFLD(@BGWKP/OMAJCD *LIBL/OPBGWKP)
     A                                  7  2'Item Code ..................:'
     A            C1INITCD  R        O  7 32REFFLD(@BGWKP/OMHGCD *LIBL/OPBGWKP)
     A                                      EDTCDE(Z)
     A                                  9  2'Scheduled'
     A                                      DSPATR(HI)
     A                                  9 22'Ld'
     A                                      DSPATR(HI)
     A                                  9 36'Order'
     A                                      DSPATR(HI)
     A                                  9 43'Or'
     A                                      DSPATR(HI)
     A                                  9 47'-- Scheduled --'
     A                                      DSPATR(HI)
     A                                  9 64'-- Shipped  --'
     A                                      DSPATR(HI)
     A                                 10  2'Ship Date'
     A                                      DSPATR(HI)
     A                                 10 13'Load ID'
     A                                      DSPATR(HI)
     A                                 10 22'St'
     A                                      DSPATR(HI)
     A                                 10 28'Item'
     A                                      DSPATR(HI)
     A                                 10 35'Number'
     A                                      DSPATR(HI)
     A                                 10 43'St'
     A                                      DSPATR(HI)
     A                                 10 51'Qty'
     A                                      DSPATR(HI)
     A                                 10 56'Weight'
     A                                      DSPATR(HI)
     A                                 10 67'Qty'
     A                                      DSPATR(HI)
     A                                 10 72'Weight'
     A                                      DSPATR(HI)
     A            C1LOADID  R        B 11 13REFFLD(@BECPFI/BERMNB *LIBL/OPBFCPP)
     A                                      DSPATR(HI)
     A                                      EDTCDE(M)
     A            C1LOADST  R        B 11 22REFFLD(@FJCPBP/FJPKST *LIBL/OMFJCPP)
     A                                      DSPATR(HI)
     A            C1ITCD    R        B 11 25REFFLD(@BGWKP/OMHGCD *LIBL/OPBGWKP)
     A                                      DSPATR(HI)
     A            C1ORNO    R        B 11 34REFFLD(@BECPFI/BEC4NB *LIBL/OPBFCPP)
     A                                      EDTCDE(M)
     A                                      DSPATR(HI)
     A            C1ORST    R        B 11 44REFFLD(@BECPFI/BEGWST *LIBL/OPBFCPP)
     A                                      DSPATR(HI)
     A            C1SCQT         5Y 0B 11 48EDTCDE(K)
     A                                      DSPATR(HI)
     A            C1SCWT         5Y 0B 11 56EDTCDE(K)
     A                                      DSPATR(HI)
     A            C1SHQT         5Y 0B 11 64EDTCDE(K)
     A                                      DSPATR(HI)
     A            C1SHWT         5Y 0B 11 72EDTCDE(K)
     A                                      DSPATR(HI)
     A*
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R OM7001K1
     A*%%TS  SD  20010717  103012  ISLRAMS     REL-V4R4M0  5769-PW1
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A                                 23 11'F5=Refresh'
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *---------------------------------------------------------------
     A          R OM7001ES                  SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE CONTROL FORMAT
      *---------------------------------------------------------------
     A          R OM7001EC                  SFLCTL(OM7001ES)
      *
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
