      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Triumph Foods
      * PROGRAM:     TF230 - Eopfees: Carrier Invoice Close--Load Header Update
      * PROGRAMMER:  LeAnne Ramsey
      * CREATED:     06/23/06
      *
      * Function:    This program is a preliminary step in "freezing" the load data.
      *              It retrieves unpaid Load Header records for carrier Seaboard
      *              Transportation and marks them as "paid".  It also prints a
      *              listing of the updated records and e-mails it.
      *
      ****************************************************************************************
      * MODIFICATIONS:
      ****************************************************************************************
      * DATE      PROGRAMMER
      *
      * 08/15/06  LeAnne Fedor
      *           Added an "absorbed freight" column and total. Just FYI, many of the 960
      *           loads are "transfers" to a warehouse. Since these do not have records in
      *           Sales History, their absorbed freight will be zero.
      *
      * 12/01/06  LeAnne Ramsey
      *           Replaced call to the generic program that formatted Synon dates
      *           into CCYYMMDD and MMDDYY formats with ILE date logic.
      *
      * 12/05/06  LeAnne Ramsey
      *           A new customer (Gordon Food Service) is demanding that all product be
      *           shipped to them from the Triumph (St. Joe) location. This has required
      *           the Synon Systems to have both an Accounting Company and a Shipping
      *           Company. To simplify the logic, we will now retrieve Sales History
      *           records by "Load" only---instead of Company/Load.
      *
      * 04/06/09  LeAnne Ramsey
      *           TIME was not printing in the heading; made it print.
      /eject
      ****************************************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************************************
      *
     Fomfjcplh  uf   e           k disk
      *    Load header
      *
      *
     Fomhstpn8  if   e           k disk
      *    Sales history
      *
      *
     Ftfp099    if   e           k disk
      *    Function control
      *
      *
     Fprint132  o    f  132        printer oflind(*inof)
      *
      /eject
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      *
      ****************************************************************
      * ARRAYS AND TABLES
      ****************************************************************
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Standard fields
      *
     D dash97          s             97    inz(*all'-')
     d rtime           s              6  0
      *
      * Control fields
      *
     d first           s              1    inz('Y')
      *
      *
      * Date manipulation
      *
     D wkisodate       s               d   datfmt(*iso)
      *
      *
      * Print fields
      *
     d h1cbmdy         s                   like(fncbmdy)
     d h1cemdy         s                   like(fncemdy)
     d h1cyr           s                   like(fncyr)
     d h1cpe           s                   like(fncpe)
      *
     d l1asdtmdy       s              6  0
     d l1absfam        s                   like(f4ulva)
     d t1absfam        s                   like(f4ulva)
      *
      *
      * Parm fields
      *
     D xxsynenddt      s              7  0
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      **********************************************************************************
      * Mainline
      **********************************************************************************
      *
      * Print headings.
     C                   exsr      $h1hdr
      *
      * Process Load Header records where:
      *       1) Carrier code = 'STI' (Seaboard Transportation)
      *       2) Actual Ship Date is on/before Period End Date
      *       3) Carrier Payment Status is blank
      *       4) Load Status is I=Invoiced
      *
      * Update each record with:
      *       1) P=Paid in the Carrier Payment Status
      *       2) Period End Date in the Payment Date
      *
     C     'STI'         setll     omfjcplh
      *
     C                   dou       *in90 = *on                                  Main do
     C     'STI'         reade     omfjcplh                               90
     C                   if        *in90 = *off and                             If not EOF
     C                             fjf9dt <= xxsynenddt and
     C                             fjrnst = ' ' and
     C                             fjpkst = 'I'
      *
     C                   move      no            first
      *
     C                   move      'P'           fjrnst
     C                   z-add     xxsynenddt    fjpydt
     C                   update    @fjcppf
      *
      * Retrieve Absorbed Freight for this Load Header
      * Then, print the load header line
      *
     C                   exsr      $absfam
     C                   exsr      $l1dtl
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Main do
      *
      * EOF processing
      *
     C                   if        first = yes
     C                   except    nodata
     C                   else
     C                   exsr      $t1tot
     C                   endif
      *
     C                   seton                                        lr
      /eject
      *-------------------------------------------------------------------
      * Retrieve Absorbed Freight for a Load from the Sales History file
      *-------------------------------------------------------------------
      *
     C     $absfam       begsr
      *
     C     fjrmnb        setll     omhstpn8
      *
     C                   dou       *in91 = *on                                  Do frt
     C     fjrmnb        reade     omhstpn8                               91
     C                   if        *in91 = *off                                 If not EOF
     C                   add       f4ulva        l1absfam
     C                   add       f4ulva        t1absfam
     C                   endif                                                  If not EOF
     C                   enddo                                                  Do frt
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Print record
      *---------------------------------------------------------------
      *
     C     $l1dtl        begsr
      *
     C                   if        *inof = *on
     C                   exsr      $h1hdr
     C                   endif
      *
      * Get Actual Ship Date into MMDDYY format for printing
      *
     C                   z-add     0             l1asdtmdy
     C                   if        fjf9dt <> 0                                  If date
     C     *cymd         test(d)                 fjf9dt                 92
     C                   if        *in92 = *off                                 If OK date
     C     *cymd         move      fjf9dt        wkisodate
     C     *mdy          move      wkisodate     l1asdtmdy
     C                   endif                                                  If OK date
     C                   endif                                                  If date
      *
      * Print detail line
      *
     C                   except    l1dtl
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Print report headings
      *---------------------------------------------------------------
      *
     C     $h1hdr        begsr
      *
     C                   except    h1hdr
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Print report total
      *---------------------------------------------------------------
      *
     C     $t1tot        begsr
      *
     C                   if        *inof = *on
     C                   exsr      $h1hdr
     C                   endif
      *
     C                   except    t1tot
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Parm lists
      *
     C     *entry        plist
     C                   parm                    xxsynenddt
      *
     C                   time                    rtime
      *
      * Retrieve the "Current" period data from the Control file for:
      *   1) the EOP function
      *
     C     'EOPFEES   '  chain     tfp099                             92
     C                   if        *in92 = *off
     C                   z-add     fncyr         h1cyr
     C                   z-add     fncpe         h1cpe
     C                   z-add     fncbmdy       h1cbmdy
     C                   z-add     fncemdy       h1cemdy
     C                   endif
      *
     C                   endsr
      /eject
      *-------------------------------------------------------------
      * Report headings
      *-------------------------------------------------------------
      *
     Oprint132  e            h1hdr          1 01
     O                       sdpgm               10
     O                                           52 'STI LOADS UPDATED AS PAID'
     O                                           87 'DATE'
     O                       udate         y     97
      *
     O          e            h1hdr          1
     O                       sdusr               10
     O                                           87 'TIME'
     O                       rtime               97 '  :  :  '
      *
      *
     O          e            h1hdr          2
     O                                           87 'PAGE'
     O                       page          z     97
      *
      *
     O          e            h1hdr          1
     O                                           18 'Year/Period .....:'
     O                       h1cyr         z     24
     O                       h1cpe         z     28
      *
     O          e            h1hdr          1
     O                                           18 'Period begin date:'
     O                       h1cbmdy             28 '  /  /  '
      *
     O          e            h1hdr          2
     O                                           18 'Period end date .:'
     O                       h1cemdy             28 '  /  /  '
      *
      *
      *-------------------------------------------------------------
      * Column headings
      *-------------------------------------------------------------
      *
     O          e            h1hdr       0  1
     O                                           50 'Absorbed'
      *
     O          e            h1hdr       0  1
     O                                            7 'Company'
     O                                           15 'Load'
     O                                           24 'Carrier'
     O                                           35 'Actual'
     O                                           50 'Freight'
      *
      *
     O          e            h1hdr       0  1
     O                                            6 'Number'
     O                                           14 'ID'
     O                                           23 'Code'
     O                                           35 'Ship Date'
     O                                           50 'Amount'
      *
      *
     O          e            h1hdr          0
     O                       dash97              97
      *
      *-------------------------------------------------------------
      * Detail line
      *-------------------------------------------------------------
      *
     O          e            l1dtl       1
     O                       fjaic3        z      5
     O                       fjrmnb        z     15
     O                       fjbzna              22
     O                       l1asdtmdy           35 '  /  /  '
     O                       l1absfam      kb    51
      *
      *-------------------------------------------------------------
      * Total line
      *-------------------------------------------------------------
      *
     O          e            t1tot       1
     O                                           50 '--------------'
      *
     O          e            t1tot       1
     O                                           35 'Total:'
     O                       t1absfam      kb    51
      *
      *-------------------------------------------------------------
      * Blank line
      *-------------------------------------------------------------
      *
     O          e            blank       1
      *
      *-------------------------------------------------------------
      * No data message line
      *-------------------------------------------------------------
      *
     O          e            nodata      1
     O                                           17 'No STI loads were'
     O                                           26 ' updated.'
      /eject
