/***************************************************************** */
/*                                                                 */
/*  PROGRAM NUMBER:  HP204CLSD                                     */
/*  PROGRAM NAME:    BUILD JDE UPLOAD FILE: HPS SEMEN DOSES SHIPPED*/
/*  PROGRAMMER:      LEANNE FEDOR                                  */
/*  CREATE DATE:     05/22/03                                      */
/*                                                                 */
/*  FUNCTION:        THIS CL IS SUBMITTED FROM THE DRIVER CL       */
/*                   HP404CL.                                      */
/*                   OPTIONS ARE PASSED THROUGH THE LDA.           */
/*                                                                 */
/*******************************************************************/
/* MODIFICATIONS:                                                  */
/*                                                                 */
/* 06/02/03  LEANNE FEDOR                                          */
/*           ADDED LOGIC TO OMIT GDU FARMS.                        */
/* 10/10/18  Danny Nguyen  (S13682)                                */
/*           Retrieve Company Code from LDA. Change OPNQRYF logic  */
/*           for HSL018I to select by Company. Increase &QDATA     */
/*           from 44 to 60.                                        */
/*                                                                 */
/*******************************************************************/

             PGM

             DCL        VAR(&QDATA) TYPE(*CHAR) LEN(60)

             DCL        VAR(&LDACYR) TYPE(*DEC) LEN(4)
             DCL        VAR(&LDACPE) TYPE(*DEC) LEN(2)
             DCL        VAR(&LDBPDT) TYPE(*DEC) LEN(8)
             DCL        VAR(&LDEPDT) TYPE(*DEC) LEN(8)
             DCL        VAR(&LDCOCD) TYPE(*CHAR) LEN(3)

             CHGVAR     VAR(&LDACYR) VALUE(%SST(*LDA 1 4))
             CHGVAR     VAR(&LDACPE) VALUE(%SST(*LDA 5 2))
             CHGVAR     VAR(&LDBPDT) VALUE(%SST(*LDA 7 8))
             CHGVAR     VAR(&LDEPDT) VALUE(%SST(*LDA 15 8))
             CHGVAR     VAR(&LDCOCD) VALUE(%SST(*LDA 25 3))


/*********************************************************************/
/*  SETUP/RUN OPEN QUERIES                                           */
/*********************************************************************/

/* ALWAYS SELECT ONLY FARMS THAT HAVE A COST CENTER VALUE         */
/* ALWAYS OMIT GDU FARMS                                          */

             CHGVAR     VAR(&QDATA) VALUE(' ')
             CHGVAR     VAR(%SST(&QDATA 1 13)) VALUE('FSCJD *NE " "')
             CHGVAR     VAR(%SST(&QDATA 15 23)) VALUE('*AND FSTFCD +
                          *NE "GDU  "')

/*  S13682-ALWAYS SELECT BY COMPANY CODE.                        */

             CHGVAR     VAR(%SST(&QDATA 39 17)) VALUE('*AND FSCOCD +
                          *EQ "')
             CHGVAR     VAR(%SST(&QDATA 56 3)) VALUE(&LDCOCD)
             CHGVAR     VAR(%SST(&QDATA 59 1)) VALUE('"')

             OVRDBF     FILE(HSL018I) SHARE(*YES)
             OPNQRYF    FILE((HSL018I)) QRYSLT(&QDATA) KEYFLD(*FILE) +
                          SEQONLY(*YES)


/*  SELECT ALL SIRE LINE DOSES RECORDS WITHIN THE DATE RANGE        */

             CHGVAR     VAR(&QDATA) VALUE(' ')
             CHGVAR     VAR(%SST(&QDATA 1 10)) VALUE('SWWEDT *GE')
             CHGVAR     VAR(%SST(&QDATA 12 8)) VALUE(&LDBPDT)
             CHGVAR     VAR(%SST(&QDATA 21 15)) VALUE('*AND SWWEDT +
                          *LE')
             CHGVAR     VAR(%SST(&QDATA 37 8)) VALUE(&LDEPDT)

             OVRDBF     FILE(HSL081B) SHARE(*YES)
             OPNQRYF    FILE((HSL081B)) QRYSLT(&QDATA) KEYFLD(*FILE) +
                          SEQONLY(*YES)

/*********************************************************************/
/*  CALL PROGRAM TO BUILD INTERFACE FILE OF HPS SEMEN DOSES SHIPPED  */
/*********************************************************************/

             CALL       PGM(HP204SS)

/*******************************************************************/
/*  DELETE FILE OVERRIDES                                          */
/*******************************************************************/

             CLOF       OPNID(HSL018I)
             DLTOVR     FILE(HSL018I)

             CLOF       OPNID(HSL081B)
             DLTOVR     FILE(HSL081B)

             ENDPGM

