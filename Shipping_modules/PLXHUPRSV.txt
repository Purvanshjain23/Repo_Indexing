      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Order Managment/M3 Interface
      * PROGRAM:     PLXHUPR
      * TITLE:       Invoice Post List by Actual Ship Date
      * PROGRAMMER:  ROSE CENTONZE
      * CREATED:     06/26/15
      *
      * FUNCTION:  Batch program to print a listing of workfile records.
      *
      *    ******* REPLACES OMINVJDE4 ****************
      *
      ****************************************************************
      * MODIFICATIONS:
     A*  RMC 1/20/17  Project  E8287
     A*               Changed LF OML300B to OML300H  SEL LIVE WITH M3 GL =Y,P FOR  Yes or Parallel
     A*  RMC 6/05/17  Project  E9083
     A*               get m3 acct with cgl code if CC/OBJ/Sub is blank
      ****************************************************************
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     F*oml300b   if   e           k disk
     Foml300h   if   e           k disk
      *   Workfile: OM Invoicing to M3
      *
      *
     Fprint198  o    f  198        printer oflind(*inof)
      *
      /EJECT
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Standard fields
      *
     D return          s              7
     D prtfl           s              1    inz('N')
     D dash            s            198    inz(*all'-')
     d rtime           s              6  0
      *
      *
      * Print fields
      *
     D h1numco         s              3  0
     D h1conm          s             30
      *
     D l1custnm        s             20
     D l1slsnm         s             20
     D l1mdy           s              6  0
     D l1itxt          s             17
      *
      *
      * Control fields
      *
     D first           s              1    inz('Y')
     D svord#          s                   like(omord#)
     D svasdt          s                   like(omasdt)
     D ttamt$          s                   like(omamt)
      *
      *
      * Workfields for date manipulation
      *
     d wkisodate       s               d   datfmt(*iso)
      *
      *
      * Parm fields
      *
     d xxerror         s              1
     d xxalphco        s              3
     d xxnumco         s                   like(omco#)
     d xxcust          s                   like(omcust)
     d xxcustnm        s             30
     d xxglcode        s              8
     d xxsls           s                   like(omsls#)
     d xxslsnm         s             30
     d xxtype          s              1
     d xxsynondt       s              7  0
     d xxccyymmdd      s              8  0
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * MAINLINE
      ****************************************************************
      *
      * Prepare to read the workfile and print headings.
      *
     C                   exsr      $h1hdr
     C     *LOVAL        setll     oml300h
      *
     C                   dou       *in90 = *on
     C                   read      oml300h                                90
     C                   if        *in90 = *off
      *
      * Control break logic:
      *   1) First time logic
      *   2) New order logic
      *   3) Actual Ship Date Total
      *
     C                   select
     C                   when      first = yes
     C                   exsr      $newasdt
     C                   exsr      $neworder
     C                   move      no            first
      *
     C                   when      omasdt <> svasdt
     C                   exsr      $newasdt
     C                   exsr      $neworder
      *
     C                   when      omord# <> svord#
     C                   exsr      $neworder
     C                   endsl
      *
      * Print detail line
     C                   exsr      $l1dtl
      *
     C                   endif
     C                   enddo
      *
      *---------------------------------------------------------------
      * EOF processing
      *---------------------------------------------------------------
      * If the file was empty, print the standard 'no data...'
      * error message
      *  ELSE print the last act ship date total
      *
     C                   if        prtfl = no
     C                   except    nodata
     C                   else
     C                   exsr      $newasdt
     C                   endif
      *
     C                   seton                                        lr
      /eject
      *-------------------------------------------------------------------------
      * Print order detail line
      *-------------------------------------------------------------------------
      *
     C     $l1dtl        begsr
      *
      * Move some fields to shortened print fields
      *
     C                   movel     omitxt        l1itxt
      *
      *
      * Accum seq 000 for Actual Ship Date total - this is the receivable acct,
      *
     C                   if        omseq# = 0
     C                   add       omamt         ttamt$
     C                   endif
      *
      * If the Account Id is blank, set on the indicator to print the words
      * 'Invalid Account' instead of printing the cost center/obj/sub.
      *
     C*                  if        omaid = *blank
     C*                  seton                                        89
     C*                  else
     C*                  setoff                                       89
     C*                  endif
     C***********************************************************
     C*  GOTO    JDE-M3 XREF TABLE FOR DIMS
     C***********************************************************
     C*                  MOVE      *off          *in89
     c     ommcu         ifne      *blanks
     C                   CALL      'PLXGXFR'
     C                   PARM                    Return
     C                   PARM                    RTNCDE            7
     C                   PARM                    OMMCU
     C                   PARM                    OMOBJ
     C                   PARM                    OMSUB
     C                   PARM                    DSDIM1            8
     C                   PARM                    DSDIM2            8
     C                   PARM                    DSDIM3            8
     C                   PARM                    DSDIM4            8
     C                   PARM                    DSDIM5            8
     C                   PARM                    DSDIM6            8
     C                   PARM                    DSDIM7            8
     C                   PARM                    wkades           30
     C     rtncde        IFEQ      'Y2U0005'                                    m3 DIMs not found
     C                   MOVE      *on           *in89
     c                   else
     C                   MOVE      *off          *in89
     C                   ENDIF
     c                   else
     c* Get DIMs with CGL Code
     c     omaid         ifne      *blanks
     C                   CALL      'PLXZXFR'
     C                   PARM                    RTNCDE            7
     C                   PARM                    OMAID
     C                   PARM                    DSDIM1            8
     C                   PARM                    DSDIM2            8
     C                   PARM                    DSDIM3            8
     C                   PARM                    DSDIM4            8
     C                   PARM                    DSDIM5            8
     C                   PARM                    DSDIM6            8
     C                   PARM                    DSDIM7            8
     C                   PARM                    wkades           30
     C     rtncde        IFEQ      'Y2U0005'                                    m3 DIMs not found
     C                   MOVE      *on           *in89
     c                   else
     C                   MOVE      *off          *in89
     C                   ENDIF
     C                   ENDIF
     C                   ENDIF
     C     omsbl         IFne      *blanks                                      m3 DIMs not found
     c                   evalr     dsdim7=%subst(omsbl:%check('0':omsbl))       left adj, remv 00-
     C                   eval      dsdim7= %triml(dsdim7)
     C                   ENDIF
     C********************************************************************************************
      *
      * Set indicators to not print some text on the second detail line unless y
      * actually have a value in the field. (This is just to make the listing mo
      * readable.)
      *
     C                   if        omftyp <> *blank
     C                   seton                                        87
     C                   else
     C                   setoff                                       87
     C                   endif
      *
     C                   if        omcode <> *blank
     C                   seton                                        86
     C                   else
     C                   setoff                                       86
     C                   endif
      *
     C                   if        omtext <> *blank
     C                   seton                                        85
     C                   else
     C                   setoff                                       85
     C                   endif
      *
      *
      * Print headings on overflow
      *
     C                   if        *inof = *on
     C                   exsr      $h1hdr
     C                   endif
      *
      * Print detail line
     C                   except    l1dtl
     C                   move      yes           prtfl
      *
      * Set off indicator that controls printing of 'order header' line.
      *
     C                   setoff                                       88
      *
     C                   endsr
      /eject
      *-------------------------------------------------------------------------
      * New Actual Ship Date
      *-------------------------------------------------------------------------
      *
     C     $newasdt      begsr
      *
      * If not first time ...
     C                   if        first = no
      *
      * Print headings on overflow
      *
     C                   if        *inof = *on
     C                   exsr      $h1hdr
     C                   endif
      *
      * Print total line
     C                   except    l2ttl
     C                   endif
      *
      * Save control fields
      *
     C                   z-add     omasdt        svasdt
     C                   z-add     0             ttamt$
      *
      * Get Actual Ship Date from the SYNON format into MMDDYY format for printi
      *
     C                   exsr      $date
     C*
     C                   endsr
      *-------------------------------------------------------------------------
      * Retrieve Names for Customer and Salesman
      *-------------------------------------------------------------------------
      *
     C     $neworder     begsr
      *
      * Save control fields
      *
     C                   z-add     omord#        svord#
      *
      * Get Actual Ship Date from the SYNON format into MMDDYY format for printi
      *
     C*                  exsr      $date
      *
      * Retrieve Customer and Salesman Names (move into truncated print fields).
      *
     C                   call      'PDZFXFR'
     C                   parm                    return
     C                   parm      h1numco       xxnumco
     C                   parm      omcust        xxcust
     C                   parm      *blank        xxtype
     C                   parm      *blank        xxglcode
     C     l1custnm      parm      *blank        xxcustnm
     C                   parm      omsls#        xxsls
     C     l1slsnm       parm      *blank        xxslsnm
     C                   parm      *blank        xxerror
      *
      *
      * Set on indicator to control printing of 'order header' line.
      *
     C                   seton                                        88
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Get the Synon date into MMDDYY format
      *---------------------------------------------------------------
      *
     C     $date         begsr
      *
      * Get Synon Actual Ship Date into CCYYMMDD format
      *
     C                   call      'HP8011'
     C                   parm      omasdt        xxsynondt
     C                   parm      0             xxccyymmdd
      *
      * Flip CCYYMMDD to MMDDYY for printing.
      *
     C     *iso          test(d)                 xxccyymmdd             92
     C                   if        *in92 = *off
     C                   move      xxccyymmdd    wkisodate
     C     *mdy          move      wkisodate     l1mdy
     C                   endif
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Print report headings
      *---------------------------------------------------------------
      *
     C     $h1hdr        begsr
      *
     C                   except    h1hdr
      *
      * Set on indicator that controls printing of 'order header' line.
      *
     C                   seton                                        88
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Parm lists
      *
     C     *entry        plist
     C                   parm                    return
     C                   parm                    xxalphco
      *
      *
      * Get the incoming alpha Company Number into a numeric print field.
      *
     C*************      move      xxalphco      h1numco
      *
      * Retrieve Company Name
      *
     C                   call      'PDGZXFR'
     C                   parm                    return
     C                   parm                    h1numco
     C                   parm      *blank        h1conm
     C                   parm                    xxerror
     C*
      * Retrieve time for report heading.
      *
     C                   time                    rtime
      *
     C                   endsr
      /EJECT
      *-------------------------------------------------------------
      * Report heading lines
      *-------------------------------------------------------------
      *
     Oprint198  e            h1hdr          1 01
     O                       sdpgm               10
     O                                          107 'ORDER MANAGEMENT SYSTEM'
     O                                          188 'DATE'
     O                       udate         y    198
      *
     O          e            h1hdr          1
     O                       sdusr               10
     O                                           94 'INVOICE POST LIST'
     O                                          114 ' BY ACTUAL SHIP DATE'
     O                                          188 'TIME'
     O                       rtime              198 '  :  :  '
      *
     O          e            h1hdr          2
     O                                          188 'PAGE'
     O                       page          z    198
      *
      *
     O***       e            h1hdr          2
     O***                                         8 'Company:'
     O***                    h1numco       z     13
     O***                    h1conm              45
      *
      *-------------------------------------------------------------
      * Column headings
      *-------------------------------------------------------------
      *
     O          e            h1hdr          1
     O*                                         186 'SUB'
     O*                                         197 'SUB'
     O*                                         181 'SUB'
      *
     O          e            h1hdr          1
     O                                            7 'ORDER'
     O                                           12 'ORD'
     O                                           22 'ACTUAL'
     O                                           33 'DOCUMENT'
     O                                           41 'INVOICE'
     O                                           64 '-------- A/R CUSTOMER'
     O                                           72 ' -------'
     O                                           85 'SALESPERSON'
      *
     O*                                         104 'SHIPPED'
     O*                                         189 'LEDGER'
     O*                                         198 'LEDGER'
     O*                                         184 'LEDGER'
      *
     O          e            h1hdr          1
     O                                            7 'NUMBER'
     O                                           12 'TYP'
     O                                           23 'SHIP DATE'
     O                                           32 'NUMBER'
     O                                           41 'NUMBER'
     O                                           50 'NUMBER'
     O                                           56 'NAME'
     O                                           77 'NBR'
     O                                           83 'NAME'
      *
     O                                           84 'SEQ'
     O                                           92 'ITEM CD'
     O*                                          98 'GRP'
     O*                                         103 'CLS'
     O                                          104 'DESCRIPTION'
      *
     O                                          120 'UNITS'
     O                                          134 'AMOUNT'
     O*                                         163 'COST CENTER'
     O*                                         171 'OBJECT'
     O*                                         181 'SUB CODE'
     O*                                         187 'CODE'
     O*                                         198 'ERROR'
     O                                          159 'DIM1     DIM2     DIM3'
     O                                          186 'DIM4     DIM5     DIM6'
     O                                          195 'DIM7'
      *
     O          e            h1hdr          1
     O                       dash               198
      *
      *
      *-------------------------------------------------------------
      * Detail line
      *-------------------------------------------------------------
      *
     O          e    88      l1dtl       1  1
      *
     O                       omord#        z      7
     O                       omotyp              11
      *
     O                       l1mdy               23 '  /  /  '
     O                       omdoc#        z     32
     O                       ominv#        z     41
     O                       omcust        z     50
     O                       l1custnm            72
     O                       omsls#              77
     O                       l1slsnm             99
      *
     O          e            l1dtl          1
      *
     O               87                          24 'Fin type:'
     O                       omftyp              26
     O               86                          37 'Fin code:'
     O                       omcode              41
     O               85                          48 'Text:'
     O                       omtext              79
      *
     O                       omseq#        z     84
     O                       omsitm        z     92
     O*                      omigrp        z     98
     O*                      omicls        z    103
     O*                      l1itxt             122
     O*                      omunit        k    134
     O*                      omamt         k    149
      *
     O                       l1itxt             110
     O                       omunit        k    121
     O                       omamt         k    135
     O*             n89      ommcu              163
     O*             n89      omobj              171
     O*             n89      omsub              181
     O               89                         161 'Invalid Account'
     O*                      omsbl              191
     O*                      omsblty            193
     O*                      omsblfl            196
     O              N89      dsdim1             145
     O              N89      dsdim2             154
     O              N89      dsdim3             163
     O              N89      dsdim4             172
     O              N89      dsdim5             181
     O              N89      dsdim6             190
     O              N89      dsdim7             198
      *
      *
      *
      *-------------------------------------------------------------
      * Total Amt Line
      *-------------------------------------------------------------
      *
     O          e            l2ttl          1
      *
     O                                           23 '--------'
     O                                          136 '------------'
     O          e            l2ttl          1
      *
     O                                           13 'A/R Total:'
     O                       l1mdy               23 '  /  /  '
     O                       ttamt$        k    135
      *
      *-------------------------------------------------------------
      * No data message line
      *-------------------------------------------------------------
      *
     O          e            nodata      1
     O                                           19 'NO DATA MEETS YOUR'
     O                                           38 'SELECTION CRITERIA'
