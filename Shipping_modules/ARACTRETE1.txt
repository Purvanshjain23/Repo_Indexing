     Z* CRTBNDRPG
     Z* DFTACTGRP(*NO) BNDDIR(YBNDDIR) DBGVIEW(*SOURCE)
     Z* CVTOPT(*DATETIME) ACTGRP(*CALLER) OPTIMIZE(*BASIC)
      *****************  RPG PROGRAM HEADING  ************************
      *
      * PROGRAM NUMBER..... -  ARACTRETE1
      *          DATE....... -  3/13/2020
      *          AUTHOR..... - Brad Baden    SDN465   TFS# 16169
      *          DESCRIPTION - THIS PROGRAM IS CALLED FROM THE A/R
      *                        HOG SYSTEM.  IT IS USED TO RETRIEVE
      *                        THE COST CENTER, OBJECT, SUB AND
      *                        DESCRIPTION FROM JDE.  THE ACCOUNT
      *                        I.D. IS PASSED IN.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      *  Date      Programmer
      *
      *  06/29/96  Barb Gutierrez
      *            Recompiled for JDE Release A7.1 only
      *
      *  12/26/05  Rose Centonze
      *            Passed out CO
      *
      *  12/29/05  LeAnne Fedor
      *            Rewrote as ILE.
      *
      *  02/23/10  Barb Gutierrez
      *            Need to pass out the posting edit code associated with the
      *            account number.
      *
      *  06/06/17  Barb Gutierrez
      *            For the new STF plant (comapny 440), if the account id doesn't
      *            exist in F0901, retrieve the accounting company from
      *            pkcwrep to pass back out to calling program.
      *
      *  3/10/20  Brad Baden    SDN465  TFS# 16169
      *           This is a copy of program ARACTRET from JFOODCUST.
      *           Changed file name from F0901LA to E10901LA.  Did
      *           not change this E1 version to a Synon EXTFUN because
      *           of the Return Code position and because of PKCWREL7
      *           file.
      *
      /eject
      *****************************************************************
      * File specifications
      ****************************************************************
      *
     F*f0901la   if   e           k disk
     Fe10901la  if   e           k disk
      * JDE Account master
      *
     Fpkcwrel7  if   e           k disk
      * g/l cross reference
      *
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      *---------------------------------------------------------------------
      * Standalone fields
      *---------------------------------------------------------------------
      *
     D wkco            s              3  0 inz(440)
      *
      * Standard fields
      *
     d return          s              7
      *
      *
      * Parm fields
      *
     d xxmcu           s                   like(gmmcu)
     d xxobj           s                   like(gmobj)
     d xxsub           s                   like(gmsub)
     d xxco            s              3  0
     d xxactid         s              8
     d xxdesc          s             30
     d xxpec           s              1
      *
      ****************************************************************
      * Data structures
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      *
      /eject
      *----------------------------------------------------------------------------------
      * Mainline
      *----------------------------------------------------------------------------------
      *
      *  Retrieve Account ID info from the Account Master
      *
     C*    xxactid       chain     f0901la                            92
     c     xxactid       chain     e10901la                           92
      *
      * If you find a record and it's inactive or a non-posting status,
      * set the Return Code to indicate an error.
      *
     C                   if        *in92 = *off                                 have record
     C                   if        gmpec = 'I' or                               non-post or inactive
     C                             gmpec = 'N'
      *
     C                   movel     'ERR'         return
     C                   move      *blank        xxmcu
     C                   move      *blank        xxobj
     C                   move      *blank        xxsub
     C                   move      *blank        xxdesc
     C                   move      *blank        xxco
     C                   move      *blank        xxpec
     C                   else
      *
      * If all good, set parms to pass back out with JDE values.
      *
     C                   move      *blank        return
     C                   movel     gmmcu         xxmcu
     C                   movel     gmobj         xxobj
     C                   movel     gmsub         xxsub
     C                   movel     gmdl01        xxdesc
     C                   move      gmco          xxco
     C                   move      gmpec         xxpec
     C                   endif                                                  non-post or inactive
     C                   endif                                                  have record
      *
     c* If no record in JDE, we need to check to see if we have a value in the g/l account
      * reference table.
      *
     C                   if        *in92 = *on
      *
      * chain to cross ref table with the account id passed in and company 440 to retrieve the
      * G/L account company.
      *
     C                   if        xxactid <> *blank
     C     key           chain     pkcwrel7                           93
     C                   if        *in93 = *off                                 have record
     C                   move      cwj1a1        xxco
     C                   move      *blank        return
     C                   movel     *blank        xxmcu
     C                   movel     *blank        xxobj
     C                   movel     *blank        xxsub
     C                   movel     *blank        xxdesc
     C                   move      *blank        xxpec
     C                   else                                                   If no good
      *
     C                   movel     'ERR'         return
     C                   movel     xxactid       xxmcu
     C                   move      *blank        xxobj
     C                   move      *blank        xxsub
     C                   move      *blank        xxdesc
     C                   move      *blank        xxco
     C                   move      *blank        xxpec
     C                   endif
      *
     C                   else
     C                   movel     'ERR'         return
     C                   movel     xxactid       xxmcu
     C                   move      *blank        xxobj
     C                   move      *blank        xxsub
     C                   move      *blank        xxdesc
     C                   move      *blank        xxco
     C                   move      *blank        xxpec
     C                   endif                                                  acct id <> blank
     C                   endif                                                  92 = on
      *
     C                   seton                                        lr
      /eject
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
     C     *entry        plist
     C                   parm                    xxactid
     C                   parm                    xxmcu
     C                   parm                    xxobj
     C                   parm                    xxsub
     C                   parm                    xxdesc
     C                   parm                    xxco
     C                   parm                    xxpec
     C                   parm                    return
      *
      *
     C     key           klist
     C                   kfld                    wkco
     C                   kfld                    xxactid
     C                   endsr
