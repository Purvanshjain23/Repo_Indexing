/***************************************************************** */
/*                                                                 */
/*  SYSTEM:          TRIUMPH FOODS                                 */
/*  PROGRAM NUMBER:  TF429CL                                       */
/*  PROGRAM NAME:    UNPAID LOADS PREVIOUSLY POSTED TO TFS AS PAID */
/*  PROGRAMMER:      LEANNE RAMSEY                                 */
/*  CREATE DATE:     04/08/08                                      */
/*                                                                 */
/*  FUNCTION:        THIS FUNCTION IS: 1) SUBMITTED ON DEMAND      */
/*                   2) SUBMITTED FROM THE JOB SCHEDULER NIGHTLY   */
/*                   WHEN SUBMITTED FROM THE JOB SCHEDULER, LOGIC  */
/*                   IN THE PRINT PROGRAM DETERMINES WHETHER IT    */
/*                   IS THE LAST WEEK OF A PERIOD. IF SO, IT       */
/*                   GENERATES THE REPORT.                         */
/*                                                                 */
/*******************************************************************/
/* MODIFICATIONS:                                                  */
/*                                                                 */
/* 05/06/09  LEANNE RAMSEY                                         */
/*           CHANGED PRINT LOGIC TO MATCH MEAT COSTING.            */
/*******************************************************************/

             PGM

             DCL        VAR(&OUTQ) TYPE(*CHAR) LEN(10)
             DCL        VAR(&HOLD) TYPE(*CHAR) LEN(4)
             DCL        VAR(&SAVE) TYPE(*CHAR) LEN(4)
             DCL        VAR(&COPY) TYPE(*DEC)  LEN(1)

             DCL        VAR(&EMAIL) TYPE(*CHAR) LEN(38)
             DCL        VAR(&TSTPRDFL) TYPE(*CHAR) LEN(1)

             DCL        VAR(&LDDEMAND) TYPE(*CHAR) LEN(1)
             DCL        VAR(&GOTDATA)  TYPE(*CHAR) LEN(1)

             CHGVAR     VAR(&GOTDATA) VALUE('N')
             CHGVAR     VAR(&LDDEMAND) VALUE(%SST(*LDA 400 1))


/*----------------------------------------------------------------*/
/* Set up print parms                                             */
/*----------------------------------------------------------------*/

 OUTQ:       CHGVAR     VAR(&OUTQ) VALUE(%SST(*LDA 401 10))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(*JOB))

 HOLD:       CHGVAR     VAR(&HOLD) VALUE(%SST(*LDA 411 4))
             IF         COND(&LDDEMAND *NE 'D') THEN(CHGVAR +
                          VAR(&HOLD) VALUE(*YES))
             IF         COND(&HOLD *NE '*YES' *AND &HOLD *NE '*NO ') +
                          THEN(CHGVAR VAR(&HOLD) VALUE(*NO))

 SAVE:       CHGVAR     VAR(&SAVE) VALUE(%SST(*LDA 415 4))
             IF         COND(&SAVE *NE '*YES' *AND &SAVE *NE '*NO ') +
                          THEN(CHGVAR VAR(&SAVE) VALUE(*NO))

 COPY:       IF         COND(&LDDEMAND *NE 'D') THEN(CHGDTAARA +
                          DTAARA(*LDA (419 1)) VALUE('1'))
             CHGVAR     VAR(&COPY) VALUE(%SST(*LDA 419 1))
             IF         COND(&COPY = 0) THEN(CHGVAR VAR(&COPY) +
                          VALUE(1))

/*----------------------------------------------------------------*/
/* Override the print file                                        */
/*----------------------------------------------------------------*/

             OVRPRTF    FILE(PRINT132) OUTQ(&OUTQ) COPIES(&COPY) +
                          HOLD(&HOLD) SAVE(&SAVE)

/*----------------------------------------------------------------*/
/* Run the Report                                                 */
/*----------------------------------------------------------------*/

             CALL       PGM(TF629) PARM(&GOTDATA)

/*------------------------------------------------------------------*/
/* E-mail the Report                                                */
/*------------------------------------------------------------------*/
/* We only email when:                                              */
/* 1) it is not On-Demand (it was kicked off from the Job Scheduler */
/* 2) we actually generated a report with loads                     */

             IF         COND(&GOTDATA *EQ 'Y' *AND &LDDEMAND *NE 'D') +
                          THEN(DO)

/* Default EMAIL to "TEST" Distribution List.                     */
/* Then, override if you are running in Production.               */

             CHGVAR     VAR(&EMAIL) VALUE('TF Test')
             RTVDTAARA  DTAARA(DATSTPRD (1 1)) RTNVAR(&TSTPRDFL)

             IF         COND(&TSTPRDFL *EQ 'P') THEN(CHGVAR +
                          VAR(&EMAIL) VALUE('TF EOP Paid Loads Now +
                          Unpaid'))

             ESNDMAIL   RECIPIENT(&EMAIL) SUBJECT('Unpaid Loads that +
                          were previously posted to TFS as Paid') +
                          MSG('Distribution List: TF EOP Paid Loads +
                          Now Unpaid') ATTLIST((* *PDF *N PRINT132 *))
             MONMSG     MSGID(CPF0000)
             ENDDO

             ENDPGM

