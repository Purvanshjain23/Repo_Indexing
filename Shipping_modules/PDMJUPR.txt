/********************************************************************/
/*                                                                  */
/*  SYNOPSIS -  JOB CONTROL PROMPT FOR PRINTER OVERRIDE INFORMATION */
/*              GENERIC PROMPTING FUNCTION. CAUSE PRINTER OVERRIDE  */
/*              PROMPTING AND PASSES COMPANY, FROM DATE, THROUGH    */
/*              DATE, AND TWO FLAGS.                                */
/*                                                                  */
/*  SYSTEM    - ORDER MANAGEMENT SYSTEM                             */
/*  PROGRAM   - PRINT REPORTS                                       */
/*  DATE      - 06/08/95                                            */
/*                                                                  */
/*      PARAMETERS:                                                 */
/*                                                                  */
/*     O    &RETURN     - RETURN CODE                               */
/*     I    &APPLIC     - APPLICATION CODE                          */
/*     I    &CLP        - CL PROGRAM TO SUBMIT                      */
/*     I    &PGM        - RPG PROGRAM TO SUBMIT                     */
/*     I    &DESC       - PRINT PROGRAM DESCRIPTION                 */
/*     B    &OUTQ       -  OUTPUT QUEUE                             */
/*     B    &HOLD       -  HOLD STATUS                              */
/*     B    &SAVE       -  SAVE STATUS                              */
/*     B    &NQSTAT     -  INDICATOR WHETHER MUST RUN IN NIGHT Q    */
/*     B    &NIGHTQ     -  NIGHT JOB QUEUE                          */
/*     B    &COMPNYN    - COMPANY NUMBER (PASSED)                   */
/*     B    &PCUST      - PARENT CUSTOMER(PASSED)                   */
/*     B    &FRMDATN    - FROM DATE      (PASSED)                   */
/*     B    &THRDATN    - THRU DATE      (PASSED)                   */
/*     B    &ARCUST     - A/R CUSTOMER   (PASSED)                   */
/*     B    &COPYN      - NO. OF COPIES  (PASSED)                   */
/*     B    &SCUST      - SHIPTO CUSTOMER(PASSED)                   */
/*     B    &FLAG1      - FLAG           (PASSED)                   */
/*     B    &FLAG2      - FLAG           (PASSED)                   */
/*     B    &CARRIER    - CARRIER CODE   (PASSED)                   */
/*     B    &PAIDYN     - CARRIER PAID IN FULL Y/N                  */
/* &FLAG1 OPTIONS WILL CALL:                                        */
/*    &CLP     &PGM        &CLP     &PGM        &CLP     &PGM       */
/*   -------- -------     -------- -------     -------- -------     */
/* 1=OMNBPCLP OMNBPFR   2=PDMOPCLP PDMOPFR   3=PDXWPCLP PDXWPFR     */
/* 4=POOHPCLP POOHPFR   5=POOVPCLP POOVPFR   6=POR9PCLP POR9PFR     */
/* 7=PMVFPCLP PMVFPFR                                               */
/*********************************************************************/
/*  RMC 2/11/02 ADDED 2 PARMS FOR SELECTION: &EXCLD =EXCLUDE CARRIER */
/*              PAID, &EXCLCT=EXCLUDE COLLECT TRUCKS                 */
/*  RMC 9/17/02 ADDED 2 PARMS: SALES ROUTE, DETAIL/SUMMARY(&DTLSUM)  */
/*  RMC 11/15/06 ADDED LOAD ID PARM -- USED IN REPORT 1              */
/*  LJB 11/23/09 CHANGE EXCLUDE CARRIER PAID TO "TYPE INC/EXC STS"   */
/*  LJB 12/10/09 E000322   ADD EMAIL ADDRESS PARM FOR REPORT 2       */
/*  RMC 08/14/13 E2719     ADD DOCUMENT NAME IN DOC8 PART           */
/*********************************************************************/
 PDMJUPR:    PGM        PARM(&RETURN &APPLIC &CLP &PGM &DESC &OUTQ +
                          &HOLD &SAVE &NQSTAT &NIGHTQ &COMPNYN +
                          &PCUST &FRMDATN &THRDATN &ARCUST &COPYN +
                          &DTLSUM &FLAG1 &SLSRTE &SCUST &PAIDYN +
                          &EXCLCT &FLAG2 &CARRIER &LOADN &DOC8 &EMAILADDR)
/* PARMS                                                                      */
        DCL    &RETURN     *CHAR   (7)           /* Return Code               */
        DCL    &APPLIC     *CHAR   (06)          /* Application Code          */
        DCL    &CLP        *CHAR   (10)          /* CL to Submit              */
        DCL    &PGM        *CHAR   (10)          /* RPG TO SUBMIT             */
        DCL    &DESC       *CHAR   (25)          /* PRT Pgm Description       */
        DCL    &COPYN      *DEC                  /* No. of Copies             */
        DCL    &OUTQ       *CHAR   (10)          /* Output Queue              */
        DCL    &HOLD       *CHAR   (4)           /* Hold Status               */
        DCL    &SAVE       *CHAR   (4)           /* Save Status               */
        DCL    &NQSTAT     *CHAR   (01)          /* If MUST run in Night Q    */
        DCL    &NIGHTQ     *CHAR   (10)          /* Night Job Queue           */
        DCL    &COMPNYN    *DEC                  /* Company Number (Passed)   */
        DCL    &FRMDATN    *DEC                  /* FROM DATE      (Passed)   */
        DCL    &THRDATN    *DEC                  /* THROUGH DATE   (Passed)   */
        DCL    &PCUST      *DEC                  /* PARENT CUSTOMER           */
        DCL    &ARCUST     *DEC                  /* A/R CUSTOMER              */
        DCL    &SCUST      *DEC                  /* SHIPTO CUSTOMER           */
        DCL    &LOADN      *DEC                  /* LOAD ID                   */
        DCL    &FLAG1      *CHAR   (1)           /* FLAG                      */
        DCL    &FLAG2      *CHAR   (1)           /* FLAG                      */
        DCL    &CARRIER    *CHAR   (3)           /* CARRIER CODE              */
        DCL    &PAIDYN     *CHAR   (1)           /* CARRIER PAID Y/N/ALL      */
        DCL    &EXCLCT     *CHAR   (1)           /* EXCL CARRIER TRUCKS       */
        DCL    &DTLSUM     *CHAR   (1)           /* DTL/SUMM                  */
        DCL    &SLSRTE     *CHAR   (3)           /* SALES ROUTE               */
        DCL    &DOC8       *CHAR   (8)          /* FTP DOCUMENT NM           */
        DCL    &EMAILADDR  *CHAR   (50)          /* EMAIL ADDRESS             */

/* INTERNAL FIELDS                                                            */
        DCL    &COPY       *DEC    (2 0)         /* Number of copies          */

/* ERROR MESSAGE FIELDS                                                       */
        DCL    &MSG        *CHAR   (512)         /* message                   */
        DCL    &MSGID      *CHAR   (7)           /* message id                */
        DCL    &MSGL       *DEC    (5 0)

/********************************************************************/
/*   INITIALIZE VARIABLES                                           */
/********************************************************************/
             CHGVAR     VAR(&RETURN) VALUE('       ')
             CHGVAR     VAR(&COPY) VALUE(&COPYN)

/********************************************************************/
/*   MONITOR FOR ALL MESSAGES AT PROGRAM LEVEL                      */
/********************************************************************/
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(MSGTRAP))

/********************************************************************/
/*   CALL PROMPT PROGRAM                                            */
/********************************************************************/
             CALL       PGM(PDG0PVR) PARM(&RETURN &DESC &OUTQ &HOLD +
                          &SAVE &COPY &NQSTAT &NIGHTQ &APPLIC)
             CHGVAR     VAR(&COPYN) VALUE(&COPY)
             IF         COND(&RETURN *NE ' ') THEN(GOTO +
                          CMDLBL(USERQUIT))

/********************************************************************/
/*   SUBMIT PRINT CL PROGRAMS                                       */
/********************************************************************/
 /* SUBMIT CL FOR RPT BY SALES ROUTE- TYPE 6                       */
 SUBMIT6:    IF         COND(&FLAG1 *EQ '6') THEN(DO)
             SBMJOB     CMD(CALL PGM(&CLP) PARM(&PGM &COMPNYN +
                          &FRMDATN &THRDATN &FLAG1 &FLAG2 &SLSRTE +
                          &PCUST &ARCUST &SCUST &PAIDYN &EXCLCT +
                          &DTLSUM &OUTQ &HOLD &SAVE &COPYN &NIGHTQ +
                          &LOADN &DOC8 &EMAILADDR)) JOB(FRTCOMP) +
                          JOBQ(&NIGHTQ)
             GOTO       CMDLBL(END)
                          ENDDO

/* FOR ALL OTHER REPORTS                                            */
 SUBMIT:     SBMJOB     CMD(CALL PGM(&CLP) PARM(&PGM &COMPNYN +
                          &FRMDATN &THRDATN &FLAG1 &FLAG2 &CARRIER +
                          &PCUST &ARCUST &SCUST &PAIDYN &EXCLCT +
                          &DTLSUM &OUTQ &HOLD &SAVE &COPYN &NIGHTQ +
                          &LOADN &DOC8 &EMAILADDR)) JOB(FRTCOMP) +
                          JOBQ(&NIGHTQ)
             GOTO       CMDLBL(END)

/*-------------------------USER QUIT--------------------------------*/
 USERQUIT:   CHGVAR     VAR(&RETURN) VALUE('Y2U0005')
             GOTO       CMDLBL(END)

/*----------------------RECEIVE MESSAGES----------------------------*/

 MSGTRAP:    RCVMSG     MSGTYPE(*EXCP) MSGDTA(&MSG) MSGDTALEN(&MSGL) +
                          MSGID(&MSGID)
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(END))

             SNDPGMMSG  MSGID(&MSGID) MSGF(QCPFMSG) MSGDTA(%SST(&MSG +
                          1 &MSGL)) MSGTYPE(*ESCAPE)
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(END))

 END:        ENDPGM

