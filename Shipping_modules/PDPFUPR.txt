/********************************************************************/
/*                                                                  */
/*  SYNOPSIS -  JOB CONTROL PROMPT FOR PRINTER OVERRIDE INFORMATION */
/*              PROMPTING FUNCTION USED TO PROCESS ORDER STATUS     */
/*              INQUIRY PRINT. PROVIDES  PRINTER OVERRIDE PROMPTING */
/*              AND PASSES COMPANY, SCHED SHIP DATE, ACTUAL SHIP DT,*/
/*              ORDER NO., LOAD NO., WAREHOUSE, ORDER STATUS, PRICE */
/*              AUDIT, PRICE STATUS, SALES ROUTE, MODIFIED FLAG,    */
/*              LOAD STATUS.                                        */
/*                                                                  */
/*  SYSTEM    - ORDER MANAGEMENT SYSTEM                             */
/*  PROGRAM   - PRT ORDER STATUS INQUIRY                            */
/*  DATE      -  9/16/96                                            */
/*                                                                  */
/*      PARAMETERS:                                                 */
/*                                                                  */
/*     O    &RETURN     - RETURN CODE                               */
/*     I    &APPLIC     - APPLICATION CODE                          */
/*     I    &PGM        - CL PROGRAM TO SUBMIT                      */
/*     I    &DESC       - PRINT PROGRAM DESCRIPTION (FOR PROMPT     */
/*                         SCREEN)                                  */
/*     B    &OUTQ       -  OUTPUT QUEUE                             */
/*                         IF THIS IS BLANK AT START, PROMPT        */
/*                         PGM IS CALLED; OTHERWISE, THE PRINT CL   */
/*                         IS SUBMITTED WITHOUT PROMPTING           */
/*     B    &HOLD       -  HOLD STATUS                              */
/*     B    &SAVE       -  SAVE STATUS                              */
/*     B    &NQSTAT     -  INDICATOR WHETHER MUST RUN IN NIGHT Q    */
/*     B    &NIGHTQ     -  NIGHT JOB QUEUE                          */
/*     B    &COMPNYN    - COMPANY NUMBER (PASSED)                   */
/*     B    &CUSTN      - CUSTOMER NUMBER (PASSED)                  */
/*     B    &SCHDATN    - SCHED SHIP DATE(PASSED)                   */
/*     B    &ACTDATN    - ACTUAL SHIPPED DATE (PASSED)              */
/*     B    &ORDERN     - ORDER NUMBER   (PASSED)                   */
/*     B    &LOADN      - LOAD NUMBER    (PASSED)                   */
/*     B    &COPYN      - NO. OF COPIES  (PASSED)                   */
/*     B    &WHSCDE     - WAREHOUSE CODE (PASSED)                   */
/*     B    &ORDSTS     - HEADER STATUS   (PASSED)                  */
/*     B    &PRCAUD     - PRICE AUDIT    (PASSED)                   */
/*     B    &PRCSTS     - PRICE STATUS   (PASSED)                   */
/*     B    &SLSRTE     - SALES ROUTE     (PASSED)                  */
/*     B    &MODFLG     - MODIFIED FLAG  (PASSED)                   */
/*     B    &LOADSTS    - LOAD STATUS    (PASSED)                   */
/*     B    &RPTSEL     - REPORT SELECTION  (PASSED)                */
/*     B    &DTLSUM     - DETAIL OR SUMMARY (PASSED)                */
/********************************************************************/
/* MODIFICATIONS:                                                   */
/* 04/13/2015 LJB E004119 LOAD STATUS=BLANK WHEN LIVE W/TPM         */
/* 10/03/2006 LJB L61002C ADD PRINT Y/N FLAG TO CONTROL PRINT       */
/*                        OF PRICING INFORMATION ON THE REPORT      */
/* 10/03/2006 LJB L61002C ADD PRICE METHOD CODE                     */
/*                                                                  */
/********************************************************************/
 PDPFUPR:    PGM        PARM(&RETURN &APPLIC &PGM &DESC &OUTQ &HOLD +
                          &SAVE &NQSTAT &NIGHTQ &COMPNYN &CUSTN +
                          &SCHDATN &ACTDATN &ORDERN &LOADN &COPYN +
                          &WHSCDE &DTLSUM &RPTSEL &ORDSTS &PRCAUD +
                          &PRCSTS &SLSRTE &MODFLG &BLKORD &LOADSTS +
                          &PRINTSTS &PMTH)
/* PARMS                                                                      */
        DCL    &RETURN     *CHAR   (7)           /* Return Code               */
        DCL    &APPLIC     *CHAR   (06)          /* Application Code          */
        DCL    &PGM        *CHAR   (10)          /* CL to Submit              */
        DCL    &DESC       *CHAR   (25)          /* PRT Pgm Description       */
        DCL    &OUTQ       *CHAR   (10)          /* Output Queue              */
        DCL    &HOLD       *CHAR   (4)           /* Hold Status               */
        DCL    &SAVE       *CHAR   (4)           /* Save Status               */
        DCL    &NQSTAT     *CHAR   (01)          /* If MUST run in Night Q    */
        DCL    &NIGHTQ     *CHAR   (10)          /* Night Job Queue           */
        DCL    &COMPNYN    *DEC                  /* Company Number (Passed)   */
        DCL    &CUSTN      *DEC                  /* CUSTOMER NO.   (Passed)   */
        DCL    &SCHDATN    *DEC                  /* SCHED DELIV DT (Passed)   */
        DCL    &ACTDATN    *DEC                  /* ACTUAL    DATE (Passed)   */
        DCL    &ORDERN     *DEC                  /* ORDER NO.      (Passed)   */
        DCL    &LOADN      *DEC                  /* LOAD NO.                  */
        DCL    &COPYN      *DEC                  /* No. of Copies             */
        DCL    &WHSCDE     *CHAR   (3)           /* WAREHOUSE CODE            */
        DCL    &ORDSTS     *CHAR   (1)           /* LTL FLAG                  */
        DCL    &PRCAUD     *CHAR   (1)           /* PRICE AUDIT               */
        DCL    &PRCSTS     *CHAR   (1)           /* PRICE STATUS              */
        DCL    &SLSRTE     *CHAR   (3)           /* SALES ROUTE               */
        DCL    &MODFLG     *CHAR   (1)           /* MODIFIED FLAG             */
        DCL    &BLKORD     *CHAR   (1)           /* BLOCK ORDER FLAG          */
        DCL    &LOADSTS    *CHAR   (1)           /* LOAD STATUS               */
        DCL    &DTLSUM     *CHAR   (1)           /* DETAIL/SUMMARY            */
        DCL    &RPTSEL     *CHAR   (1)           /* REPORT SELECTION          */
        DCL    &PRINTSTS   *CHAR   (1)           /* PRINT STATUS              */
        DCL    &PMTH       *CHAR   (3)           /* PRICE METHOD CODE         */

/* INTERNAL FIELDS                                                            */
        DCL    &COPY       *DEC    (2 0)         /* Number of copies          */

/* ERROR MESSAGE FIELDS                                                       */
        DCL    &MSG        *CHAR   (512)         /* message                   */
        DCL    &MSGID      *CHAR   (7)           /* message id                */
        DCL    &MSGL       *DEC    (5 0)

/********************************************************************/
/*   INITIALIZE VARIABLES                                           */
/********************************************************************/
             CHGVAR     VAR(&RETURN) VALUE('       ')
             CHGVAR     VAR(&COPY) VALUE(&COPYN)

/********************************************************************/
/*   MONITOR FOR ALL MESSAGES AT PROGRAM LEVEL                      */
/********************************************************************/
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(MSGTRAP))

/********************************************************************/
/*   CALL PROMPT PROGRAM                                            */
/********************************************************************/
             CALL       PGM(PDG0PVR) PARM(&RETURN &DESC &OUTQ &HOLD +
                          &SAVE &COPY &NQSTAT &NIGHTQ &APPLIC)
             CHGVAR     VAR(&COPYN) VALUE(&COPY)
             IF         COND(&RETURN *NE ' ') THEN(GOTO +
                          CMDLBL(USERQUIT))

/********************************************************************/
/*   SUBMIT PRINT CL PROGRAM - PDPDCLP                              */
/********************************************************************/
 SUBMIT:     SBMJOB     CMD(CALL PGM(&PGM) PARM(&COMPNYN &CUSTN +
                          &SCHDATN &ACTDATN &ORDERN &LOADN &WHSCDE +
                          &DTLSUM &RPTSEL &ORDSTS &PRCAUD &PRCSTS +
                          &SLSRTE &MODFLG &BLKORD &LOADSTS &PMTH +
                          &OUTQ &HOLD &SAVE &COPYN &NIGHTQ +
                          &PRINTSTS)) JOB(&PGM) JOBQ(&NIGHTQ)
             GOTO       CMDLBL(END)

/*-------------------------USER QUIT--------------------------------*/
 USERQUIT:   CHGVAR     VAR(&RETURN) VALUE('Y2U0005')
             GOTO       CMDLBL(END)

/*----------------------RECEIVE MESSAGES----------------------------*/

 MSGTRAP:    RCVMSG     MSGTYPE(*EXCP) MSGDTA(&MSG) MSGDTALEN(&MSGL) +
                          MSGID(&MSGID)
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(END))

             SNDPGMMSG  MSGID(&MSGID) MSGF(QCPFMSG) MSGDTA(%SST(&MSG +
                          1 &MSGL)) MSGTYPE(*ESCAPE)
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(END))

 END:        ENDPGM
