/********************************************************************/
/*                                                                  */
/*  SYNOPSIS -  JOB CONTROL PROMPT FOR PRINTER OVERRIDE INFORMATION */
/*              PROMPTING FUNCTION. CAUSE PRINTER OVERRIDE          */
/*              PROMPTING AND PASSES COMPANY, DATE RANGE            */
/*                                                                  */
/*                                                                  */
/*  SYSTEM    - ORDER MANAGEMENT SYSTEM                             */
/*  PROGRAM   - PRINT LOADS NOT ON YARD REPORT                      */
/*  DATE      - 01/29/98                                            */
/*                                                                  */
/*      PARAMETERS:                                                 */
/*                                                                  */
/*     O    &RETURN     - RETURN CODE                               */
/*     I    &APPLIC     - APPLICATION CODE                          */
/*     I    &PGM        - CL PROGRAM TO SUBMIT                      */
/*     I    &DESC       - PRINT PROGRAM DESCRIPTION (FOR PROMPT     */
/*                         SCREEN)                                  */
/*     B    &OUTQ       -  OUTPUT QUEUE                             */
/*                         IF THIS IS BLANK AT START, PROMPT        */
/*                         PGM IS CALLED; OTHERWISE, THE PRINT CL   */
/*                         IS SUBMITTED WITHOUT PROMPTING           */
/*     B    &HOLD       -  HOLD STATUS                              */
/*     B    &SAVE       -  SAVE STATUS                              */
/*     B    &NQSTAT     -  INDICATOR WHETHER MUST RUN IN NIGHT Q    */
/*     B    &NIGHTQ     -  NIGHT JOB QUEUE                          */
/*     B    &COMPNYN    - COMPANY NUMBER (PASSED)                   */
/*     B    &FRMDATN    - FROM DATE      (PASSED)                   */
/*     B    &THRDATN    - TO   DATE      (PASSED)                   */
/*     B    &COPYN      - NO. OF COPIES  (PASSED)                   */
/*     B    &CARRIER    - CARRIER CODE                              */
/*     B    &EMAIL      - EMAIL ADDRESS                             */
/*                                                                  */
/********************************************************************/
/* MODIFICATIONS:                                                   */
/* 99/99/9999 XXX X999999                                           */
/********************************************************************/
 PDHKUPC:    PGM        PARM(&RETURN &APPLIC &PGM &DESC &OUTQ &HOLD +
                          &SAVE &NQSTAT &NIGHTQ &COMPNYN &FRMDATN +
                          &THRDATN &COPYN &CARRIER &EMAIL)
/* PARMS                                                                      */
        DCL    &RETURN     *CHAR   (7)           /* Return Code               */
        DCL    &APPLIC     *CHAR   (06)          /* Application Code          */
        DCL    &PGM        *CHAR   (10)          /* CL to Submit              */
        DCL    &DESC       *CHAR   (25)          /* PRT Pgm Description       */
        DCL    &OUTQ       *CHAR   (10)          /* Output Queue              */
        DCL    &HOLD       *CHAR   (4)           /* Hold Status               */
        DCL    &SAVE       *CHAR   (4)           /* Save Status               */
        DCL    &NQSTAT     *CHAR   (01)          /* If MUST run in Night Q    */
        DCL    &NIGHTQ     *CHAR   (10)          /* Night Job Queue           */
        DCL    &DESC       *CHAR   (25)          /* PRT Pgm Description       */
        DCL    &PAPER      *CHAR   (1) ('L')     /* LEGAL, LETTER */
        DCL    &COPYN      *DEC                  /* No. of Copies             */
        DCL    &COMPNYN    *DEC                  /* Company Number (Passed)   */
        DCL    &FRMDATN    *DEC                  /* FROM DATE      (Passed)   */
        DCL    &THRDATN    *DEC                  /* TO   DATE      (Passed)   */
        DCL    &CARRIER    *CHAR   (03)          /* CARRIER CODE              */
        DCL    &EMAIL      *CHAR   (50)          /* EMAIL ADDRESS             */
        DCL    &EMAIL1     *CHAR   (25)          /* EMAIL ADDRESS             */
        DCL    &EMAIL2     *CHAR   (25)          /* EMAIL ADDRESS             */

/* INTERNAL FIELDS                                                            */
        DCL    &COPY       *DEC    (2 0)         /* Number of copies          */

/* ERROR MESSAGE FIELDS                                                       */
        DCL    &MSG        *CHAR   (512)         /* message                   */
        DCL    &MSGID      *CHAR   (7)           /* message id                */
        DCL    &MSGL       *DEC    (5 0)

/********************************************************************/
/*   MONITOR FOR ALL MESSAGES AT PROGRAM LEVEL                      */
/********************************************************************/
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(MSGTRAP))

/********************************************************************/
/*   INITIALIZE VARIABLES                                           */
/********************************************************************/
             CHGVAR     VAR(&RETURN) VALUE('       ')
             CHGVAR     VAR(&COPY) VALUE(&COPYN)
             CHGVAR     VAR(&EMAIL1) VALUE(%SST(&EMAIL 1 25))
             CHGVAR     VAR(&EMAIL2) VALUE(%SST(&EMAIL 26 25))

/********************************************************************/
/*   CALL PROMPT PROGRAM FOR PRINTER PARMS                          */
/********************************************************************/
             CALL       PGM(PDV8PVR) PARM(&RETURN &DESC &OUTQ &HOLD +
                          &SAVE &COPY &NQSTAT &NIGHTQ &APPLIC &PAPER)
             CHGVAR     VAR(&COPYN) VALUE(&COPY)
             IF         COND(&RETURN *NE ' ') THEN(GOTO +
                          CMDLBL(USERQUIT))

/*****       GOTO CALL           FOR TESTING *****/
/********************************************************************/
/*   SUBMIT PRINT CL PROGRAM TO PRINT LOADS NOT ON YARD REPORT      */
/********************************************************************/
 SUBMIT:     SBMJOB     CMD(CALL PGM(PDHKUCLP) PARM(&COMPNYN +
                          &FRMDATN &THRDATN &OUTQ &HOLD &SAVE +
                          &COPYN &CARRIER &EMAIL1 &EMAIL2)) +
                          JOB(LDNOTONYRD) JOBQ(&NIGHTQ)
             GOTO       CMDLBL(END)

 CALL:       CALL       PGM(PDHKUCLP) PARM(&COMPNYN &FRMDATN +
                          &THRDATN &OUTQ &HOLD &SAVE &COPYN +
                          &CARRIER &EMAIL1 &EMAIL2)
             GOTO       CMDLBL(END)

/*-------------------------USER QUIT--------------------------------*/
 USERQUIT:   CHGVAR     VAR(&RETURN) VALUE('Y2U0005')
             GOTO       CMDLBL(END)

/*----------------------RECEIVE MESSAGES----------------------------*/
 MSGTRAP:    RCVMSG     MSGTYPE(*EXCP) MSGDTA(&MSG) MSGDTALEN(&MSGL) +
                          MSGID(&MSGID)
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(END))

             SNDPGMMSG  MSGID(&MSGID) MSGF(QCPFMSG) MSGDTA(%SST(&MSG +
                          1 &MSGL)) MSGTYPE(*ESCAPE)
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(END))

 END:        ENDPGM
