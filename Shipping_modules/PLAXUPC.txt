/*********************************************************************/
/*     Program: PLAXUPC                                              */
/*       Title: Prompt for printer overrides, then submit report.    */
/*        Date: 08/23/2006                                           */
/* Description: - Calls print options prompt program                 */
/*              - Submits COMPANY CARRIER REPORT                     */
/*                                                                   */
/*  Parameters:                                                      */
/*  Usage   Field       Field Description                            */
/*  -----   ---------   ------------------------------------------   */
/*    O     &RETURN     Return code                                  */
/*    I     &APPLIC     Application Code                             */
/*    I     &COMPNYN    Company number PASS                          */
/*    I     &CARRIER    CARRIER CODE                                 */
/*    I     &OUTQ                                                    */
/*    I     &HOLD                                                    */
/*    I     &SAVE                                                    */
/*    I     &NQSTAT                                                  */
/*    I     &NIGHTQ                                                  */
/*    I     &COPIES                                                  */
/*                                                                   */
/*********************************************************************/
/* Modifications:                                                    */
/*                                                                   */
/*   Date     Pgmr     Description                                   */
/* -------- ---------- --------------------------------------------  */
/*                                                                   */
/*********************************************************************/
 PLAXUPC:    PGM        PARM(&RETURN &APPLIC &OUTQ &HOLD &SAVE +
                          &NQSTAT &NIGHTQ &COMPNYN &COPYN &CARRIER)

/*-------------------------------------------------------------------*/
/* Declare Variables                                                 */
/*-------------------------------------------------------------------*/

             DCL  VAR(&RETURN)  TYPE(*CHAR) LEN(7)
             DCL  VAR(&APPLIC)  TYPE(*CHAR) LEN(6)
             DCL  VAR(&COMPNYN) TYPE(*DEC)
             DCL  VAR(&COMPANY) TYPE(*DEC) LEN(3 0)
             DCL  VAR(&CARRIER) TYPE(*CHAR) LEN(3)
             DCL  VAR(&DESC)    TYPE(*CHAR) LEN(25)
             DCL  VAR(&OUTQ)    TYPE(*CHAR) LEN(10)
             DCL  VAR(&HOLD)    TYPE(*CHAR) LEN(4)
             DCL  VAR(&SAVE)    TYPE(*CHAR) LEN(4)
             DCL  VAR(&COPYN)   TYPE(*DEC)
             DCL  VAR(&COPY)    TYPE(*DEC)  LEN(2 0)
             DCL  VAR(&NQSTAT)  TYPE(*CHAR) LEN(01)
             DCL  VAR(&NIGHTQ)  TYPE(*CHAR) LEN(10)
             DCL  VAR(&MSG)     TYPE(*CHAR) LEN(512)
             DCL  VAR(&MSGID)   TYPE(*CHAR) LEN(7)
             DCL  VAR(&MSGL)    TYPE(*DEC)  LEN(5 0)

/*-------------------------------------------------------------------*/
/* Monitor for all message at the program level                      */
/*-------------------------------------------------------------------*/
             MONMSG MSGID(CPF0000) EXEC(GOTO CMDLBL(MSGTRAP))

/*-------------------------------------------------------------------*/
/* Call Print Options Prompt Program                                 */
/*-------------------------------------------------------------------*/
             CHGVAR     VAR(&RETURN) VALUE(' ')
             CHGVAR     VAR(&DESC) VALUE('COMPANY CARRIER REPORT')
             CHGVAR     VAR(&COPY) VALUE(&COPYN)

/* Call print option prompt                                          */
             CALL       PGM(PDG0PVR) PARM(&RETURN &DESC &OUTQ &HOLD +
                          &SAVE &COPY &NQSTAT &NIGHTQ &APPLIC)

/* Convert number of copies & handle return code                     */
             IF         COND(&RETURN *NE ' ') THEN(GOTO +
                          CMDLBL(USERQUIT))

             CHGVAR     VAR(&COPYN) VALUE(&COPY)
/*-------------------------------------------------------------------*/
/* SUBMIT THE PRINT CL PROGRAM                                       */
/*-------------------------------------------------------------------*/
             CHGVAR     VAR(&COMPANY) VALUE(&COMPNYN)
             SBMJOB     CMD(CALL PGM(PLAYUPC) PARM(&RETURN &APPLIC +
                          &OUTQ &HOLD &SAVE &NQSTAT &NIGHTQ +
                          &COMPNYN &COPYN &CARRIER)) JOB(CMPCARLIST)

             GOTO       CMDLBL(END)

/*-------------------------------------------------------------------*/
/* User Quit                                                         */
/*-------------------------------------------------------------------*/
 USERQUIT:   CHGVAR     VAR(&RETURN) VALUE('Y2U9999')
             GOTO       CMDLBL(END)

/*-------------------------------------------------------------------*/
/* Trap errors and send message                                      */
/*-------------------------------------------------------------------*/
 MSGTRAP:    RCVMSG     MSGTYPE(*EXCP) MSGDTA(&MSG) MSGDTALEN(&MSGL) +
                          MSGID(&MSGID)
             MONMSG MSGID(CPF0000) EXEC(GOTO CMDLBL(END))
             SNDPGMMSG MSGID(&MSGID) MSGF(QCPFMSG) +
                       MSGDTA(%SST(&MSG 1 &MSGL)) +
                       MSGTYPE(*ESCAPE)
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(END))

/*-------------------------------------------------------------------*/
/* End of Program                                                    */
/*-------------------------------------------------------------------*/
 END:        ENDPGM
