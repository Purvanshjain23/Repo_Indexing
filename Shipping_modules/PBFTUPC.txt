/********************************************************************/
/*  NAME      - PBFTUPC                                             */
/*  SYNOPSIS  - JOB CONTROL PROMPT FOR PRINTER OVERRIDE INFORMATION */
/*              PROMPTING FUNCTION FOR PRINT TPM LOCATION MASTER    */
/*              CUSTOMER INFORMATION                                */
/*                                                                  */
/*  SYSTEM    - ORDER MANAGEMENT SYSTEM                             */
/*  PROGRAM   - PRINT TPM LOCATION MASTER REPORT                    */
/*  DATE      - 10/05/2012 L. BUSER                                 */
/*                                                                  */
/*  PARAMETERS:                                                     */
/*  I    &DESCR      - PROGRAM DESCRIPTION                          */
/*  B    &OUTQ       - OUTPUT QUEUE                                 */
/*  B    &HOLD       - HOLD STATUS                                  */
/*  B    &SAVE       - SAVE STATUS                                  */
/*  B    &COPY       - NO. OF COPIES  (2.0)                         */
/*  B    &NQSTAT     - NIGHT Q STATUS                               */
/*  B    &NIGHTQ     - NIGHT JOB QUEUE                              */
/*  I    &PGM        - RPT PROGRAM CALLED BY THE SUBMITTED PGM      */
/*  I    &APPLIC     - APPLICATION CODE                             */
/*  I    &FACILITY   - TPM FACILITY CODE (WAREHOUSE, CUSTOMER, ETC) */
/********************************************************************/
/* MAINTENANCE                                                      */
/* 99/99/99 XXX X999999                                             */
/*                                                                  */
/********************************************************************/
 PBFTUPC:    PGM        PARM(&DESC &OUTQ &HOLD &SAVE &NQSTAT &NIGHTQ +
                          &PGM &APPLIC &COPYN &FACILITY)
/* PARMS                                                                      */
             DCL        VAR(&RETURN) TYPE(*CHAR) LEN(7)
             DCL        VAR(&DESC) TYPE(*CHAR) LEN(25)
             DCL        VAR(&FACILITY) TYPE(*CHAR) LEN(24)
             DCL        VAR(&APPLIC) TYPE(*CHAR) LEN(3)
             DCL        VAR(&PGM) TYPE(*CHAR) LEN(10)
             DCL        VAR(&COPYN) TYPE(*DEC)
             DCL        VAR(&OUTQ) TYPE(*CHAR) LEN(10)
             DCL        VAR(&HOLD) TYPE(*CHAR) LEN(4)
             DCL        VAR(&SAVE) TYPE(*CHAR) LEN(4)
             DCL        VAR(&NQSTAT) TYPE(*CHAR) LEN(01)
             DCL        VAR(&NIGHTQ) TYPE(*CHAR) LEN(10)
             DCL        VAR(&COPY) TYPE(*DEC) LEN(2 0)
             DCL        VAR(&COPYN) TYPE(*DEC)

/* ERROR MESSAGE FIELDS                                                       */
             DCL        VAR(&MSG) TYPE(*CHAR) LEN(512)
             DCL        VAR(&MSGID) TYPE(*CHAR) LEN(7)
             DCL        VAR(&MSGL) TYPE(*DEC) LEN(5 0)

/*   MONITOR FOR ALL MESSAGES AT PROGRAM LEVEL                      */
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(MSGTRAP))

/*   INITIALIZE VARIABLES                                           */
             CHGVAR     VAR(&RETURN) VALUE('       ')
             CHGVAR     VAR(&COPY) VALUE(&COPYN)

/*   CALL PROMPT PROGRAM                                            */
             CALL       PGM(PDG0PVR) PARM(&RETURN &DESC &OUTQ &HOLD +
                          &SAVE &COPY &NQSTAT &NIGHTQ &APPLIC)
             IF         COND(&RETURN *NE ' ') THEN(GOTO +
                          CMDLBL(USERQUIT))
             CHGVAR     VAR(&COPYN) VALUE(&COPY)

/* SUBMIT PRINT CL PROGRAM - PBFUUPC CALLS PBFSPFR AND PASSES THE   */
/* FACILITY TYPE. THIS IS WHAT CONTROLS THE INFO THAT IS PRINTED.   */
 SUBMIT:     SBMJOB     CMD(CALL PGM(PBFUUPC) PARM(&DESC &OUTQ &HOLD +
                          &SAVE &NQSTAT &NIGHTQ &PGM &COPYN +
                          &FACILITY)) JOB(TPMLOCMSTR)

             GOTO       CMDLBL(END)

/*-------------------------USER QUIT--------------------------------*/
 USERQUIT:   CHGVAR     VAR(&RETURN) VALUE('Y2U0005')
             GOTO       CMDLBL(END)

/*----------------------RECEIVE MESSAGES----------------------------*/

 MSGTRAP:    RCVMSG     MSGTYPE(*EXCP) MSGDTA(&MSG) MSGDTALEN(&MSGL) +
                          MSGID(&MSGID)
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(END))

             SNDPGMMSG  MSGID(&MSGID) MSGF(QCPFMSG) MSGDTA(%SST(&MSG +
                          1 &MSGL)) MSGTYPE(*ESCAPE)
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(END))

 END:        ENDPGM
