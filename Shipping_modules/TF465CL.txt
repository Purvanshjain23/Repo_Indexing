/***************************************************************** */
/*                                                                 */
/*  SYSTEM:          TRIUMPH FOODS                                 */
/*  PROGRAM NUMBER:  TF465CL                                       */
/*  PROGRAM NAME:    EOPFEES: UNPAID LOADS LISTING                 */
/*  PROGRAMMER:      LEANNE RAMSEY                                 */
/*  CREATE DATE:     04/03/09                                      */
/*                                                                 */
/*  FUNCTION: THIS IS THE "ON-DEMAND" VERSION OF:                  */
/*            TF223-VERIFICATION FOR FREIGHT VARIANCE-UNPAID LOADS */
/*            GARY MARTIN WANTED TF223 TO BE ON-DEMAND; WE COULD   */
/*            NOT MAKE IT ON-DEMAND SINCE IT RAN OVER THE TFS      */
/*            PERIOD FILES. SO, WE WROTE TF465/TF465CL/TF645 AS    */
/*            THE ON-DEMAND LISTING FOR GARY.                      */
/*                                                                 */
/*******************************************************************/
/* MODIFICATIONS                                                   */
/*                                                                 */
/* 05/06/09  LeAnne Ramsey                                         */
/*           Changed print logic to match Meat Costing.            */
/*******************************************************************/

             PGM

             DCL        VAR(&OUTQ) TYPE(*CHAR) LEN(10)
             DCL        VAR(&HOLD) TYPE(*CHAR) LEN(4)
             DCL        VAR(&SAVE) TYPE(*CHAR) LEN(4)
             DCL        VAR(&COPY) TYPE(*DEC)  LEN(1)

/*----------------------------------------------------------------*/
/* SET PRINT PARAMETERS FROM LDA                                  */
/*----------------------------------------------------------------*/

 OUTQ:       CHGVAR     VAR(&OUTQ) VALUE(%SST(*LDA 401 10))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(*JOB))

 HOLD:       CHGVAR     VAR(&HOLD) VALUE(%SST(*LDA 411 4))
             IF         COND(&HOLD *NE '*YES' *AND &HOLD *NE '*NO ') +
                          THEN(CHGVAR VAR(&HOLD) VALUE(*NO))

 SAVE:       CHGVAR     VAR(&SAVE) VALUE(%SST(*LDA 415 4))
             IF         COND(&SAVE *NE '*YES' *AND &SAVE *NE '*NO ') +
                          THEN(CHGVAR VAR(&SAVE) VALUE(*NO))

 COPY:       CHGVAR     VAR(&COPY) VALUE(%SST(*LDA 419 1))
             IF         COND(&COPY = 0) THEN(CHGVAR VAR(&COPY) +
                          VALUE(1))

/*---------------------------------------------------------------------*/
/* PRINT THE LISTING                                                   */
/*---------------------------------------------------------------------*/

             OVRDBF     FILE(OMFJCPMC) SHARE(*YES)
             OPNQRYF    FILE((OMFJCPMC)) OPTION(*INP) +
                          KEYFLD((FJAIC3) (FJBZNA) (FJRMNB)) +
                          SEQONLY(*YES)

             OVRPRTF    FILE(PRINT132) OUTQ(&OUTQ) COPIES(&COPY) +
                          HOLD(&HOLD) SAVE(&SAVE)

             CALL       PGM(TF665)

             CLOF       OPNID(OMFJCPMC)
             DLTOVR     FILE(*ALL)

             ENDPGM

