/*********************************************************************/
/* PROGRAM : PUJJUPC                                                 */
/* DESC    : PRT MANFST DTL EDI RCP CL EXECUTE USER PROGRAM          */
/* DATE    : 09/14/2018                                              */
/* PROJECT : T11041 - 3PL EDI TRANSACTION                            */
/* BY      : DANNY NGUYEN                                            */
/* PURPOSE : Build WF EDI Whs Stk Txfr Diff (PUA1CPP) file in QTEMP  */
/*           & print  Detail report for Warehouse Transfer           */
/*           Orders (EDI 944) Shipped vs Received Qty differences.   */
/*           Call SEQUEL to print report as an excel & send via      */
/*           ESEND.                                                  */
/*********************************************************************/
/* MAINTENANCE LOG:                                                  */
/*-------------------------------------------------------------------*/
/* DATE     BY  CHANGE DESCRIPTION                                   */
/* -------- --- ---------------------------------------------------- */
/* 11/08/18 DN  S14071 - Added 'Select Ship/Rcvd Diff' (&SHPRCV)     */
/*    flag as input parm. Changed PUJKXFR to send &SHPRCV flag as    */
/*    input parm.                                                    */
/*                                                                   */
/*********************************************************************/

             PGM        PARM(&FMDTEA &TODTEA &COMP &WHSTO &EMAIL +
                          &SHPRCV)

             DCL        VAR(&FMDTEA) TYPE(*CHAR) LEN(7)
             DCL        VAR(&FMDTEA8) TYPE(*CHAR) LEN(8)
             DCL        VAR(&FMDTE) TYPE(*DEC) LEN(7 0)
             DCL        VAR(&TODTEA) TYPE(*CHAR) LEN(7)
             DCL        VAR(&TODTEA8) TYPE(*CHAR) LEN(8)
             DCL        VAR(&TODTE) TYPE(*DEC) LEN(7 0)

             DCL        VAR(&RTNCDE) TYPE(*CHAR) LEN(7)
             DCL        VAR(&COMP) TYPE(*DEC) LEN(3 0)
             DCL        VAR(&COMPA) TYPE(*CHAR) LEN(3)
             DCL        VAR(&WHSTO) TYPE(*CHAR) LEN(3)
             DCL        VAR(&RTNLIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&EMAIL) TYPE(*CHAR) LEN(50)
             DCL        VAR(&EMLMSG) TYPE(*CHAR) LEN(170)
             DCL        VAR(&SHPRCV) TYPE(*CHAR) LEN(1)

    /* Format Data */
             CHGVAR     VAR(&FMDTE) VALUE(&FMDTEA)
             CHGVAR     VAR(&TODTE) VALUE(&TODTEA)
             CHGVAR     VAR(&COMPA) VALUE(&COMP)

    /* Convert Date to CYYMMDD to MM/DD/YY */
             CALL       PGM(PUJLXFR) PARM('' &FMDTE &FMDTEA8) /* From Date */
             CALL       PGM(PUJLXFR) PARM('' &TODTE &TODTEA8) /* To Date */

/*-------------------------------------------------------------------*/
/* Build WF EDI Whs Stk Txfr Diff (PUA1CPP) file in QTEMP.           */
/*-------------------------------------------------------------------*/
             CRTDUPOBJ  OBJ(PUA1CPP) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)
             MONMSG     MSGID(CPF2130) EXEC(CLRPFM FILE(QTEMP/PUA1CPP))
             CRTDUPOBJ  OBJ(PUA1CPL0) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)
             MONMSG     MSGID(CPF2130) /* Object duplicated/not +
                          duplicated */
             CRTDUPOBJ  OBJ(PUA1CPL1) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)
             MONMSG     MSGID(CPF2130) /* Object duplicated/not +
                          duplicated */
             CRTDUPOBJ  OBJ(PUA1CPL2) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)
             MONMSG     MSGID(CPF2130) /* Object duplicated/not +
                          duplicated */

             OVRDBF     FILE(PUA1CPP) TOFILE(QTEMP/PUA1CPP)
             OVRDBF     FILE(PUA1CPL0) TOFILE(QTEMP/PUA1CPL0)
             OVRDBF     FILE(PUA1CPL1) TOFILE(QTEMP/PUA1CPL1)
             OVRDBF     FILE(PUA1CPL2) TOFILE(QTEMP/PUA1CPL2)

             CALL       PGM(PUJKXFR) PARM(&RTNCDE &FMDTE &TODTE +
                          &COMP &WHSTO &SHPRCV)

/*-------------------------------------------------------------------*/
/* Create Excel Spreadsheet with SEQUEL Viewpoint and email to       */
/* receipient.                                                       */
/*-------------------------------------------------------------------*/

    /* Print Order Detail Receipt-EDI 944 */
             CHGVAR     VAR(&EMLMSG) VALUE('Manifest Order Items +
                          Detail Shipped vs Received Differences +
                          EDI: TRANSFRDIF Export to Excel')

             CHGVAR     VAR(&EMLMSG) VALUE(&EMLMSG *BCAT '- From +
                          Actual Shipped Date:' *BCAT &FMDTEA8 +
                          *BCAT '-' *BCAT &TODTEA8 *BCAT 'to Whs' +
                          *BCAT &WHSTO)

    /* If TRANSFRDIF return library not PRKPLIB then set 'TEST' in Email Msg */
             RTVOBJD    OBJ(TRANSFRDIF) OBJTYPE(*USRSPC) +
                          RTNLIB(&RTNLIB)
             IF         COND(&RTNLIB *NE 'PRKPLIB') THEN(DO)
             CHGVAR     VAR(&EMLMSG) VALUE(&EMLMSG *BCAT '- TEST')
             ENDDO

    /* Call SEQUEL to Email Shipped vs Received Differences Report */
             EXECUTE    VIEW(TRANSFRDIF) PCFMT(*XLS) +
                          RECIPIENT(&EMAIL) EMLMSG(&EMLMSG)


             DLTOVR     FILE(*ALL)
             DLTF       FILE(QTEMP/PUA1CP*)

 ENDPGM:
             ENDPGM
