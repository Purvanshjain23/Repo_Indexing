/********************************************************************/
/*                                                                  */
/*  SYNOPSIS - PRINT PROGRAM WITH SELECTIONS FOR LOADS SHIPPED RPT. */
/*             COMPANY, FROM DATE, THROUGH DATE                     */
/*                                                                  */
/*  SYSTEM    - ORDER MANAGEMENT SYSTEM                             */
/*  PROGRAM   - PRINT LOADS SHIPPED                                 */
/*  DATE      -  5/17/95                                            */
/*                                                                  */
/*      PARAMETERS RECEIVED:                                        */
/*                                                                  */
/*          &COMPNYN    - COMPANY NUMBER - BLANK FOR ALL            */
/*          &FRMDATN    - FROM SHIP DATE                            */
/*          &THRDATN    - THROUGH SHIP DATE                         */
/*          &OUTQ       - OUTPUT QUEUE                              */
/*          &HOLD       - HOLD PRINT                                */
/*          &SAVE       - SAVE PRINT                                */
/*          &COPYN      - NUMBER OF COPIES                          */
/*          &NIGHTQ     - NIGHT OUTPUT QUEUE                        */
/********************************************************************/
/* RMC  05/20/04  PKTR01 - USE LF OMFJCPLL INSTEAD OF OMFJCPL0      */
/* PIO  05/18/23 - REPLACE OPNQRYF WITH OPNSQLF FOR OMFJCPP FILE    */
/*                 ADDED NEEDED OVRDBF AROUND THE COMMAND           */
/*                 CHANGE *AND TO AND , *EQ TO =, *GE TO >=, *LE TO */
/*                 <=, *GT TO > IN &D                               */
/********************************************************************/

 OMH6PCLP:   PGM        PARM(&COMPNYN &FRMDATN &THRDATN &OUTQ +
                          &HOLD &SAVE &COPYN &NIGHTQ)

/* PARMS                                                                      */
        DCL    &COMPNYN    *DEC                  /* Company Number (Passed)   */
        DCL    &FRMDATN    *DEC                  /* FROM SHIP DATE (Passed)   */
        DCL    &THRDATN    *DEC                  /* THRU SHIP DATE (PASSED)   */
        DCL    &DESC       *CHAR   (25)          /* DESCRIPTION               */
        DCL    &COPYN      *DEC                  /* No. of Copies             */
        DCL    &OUTQ       *CHAR   (10)          /* Output Queue              */
        DCL    &HOLD       *CHAR   (4)           /* Hold Status               */
        DCL    &SAVE       *CHAR   (4)           /* Save Status               */
        DCL    &NIGHTQ     *CHAR   (10)          /* Night Job Queue           */
        DCL    &COMMAND    *CHAR   (512)

/* INTERNAL FIELDS                                                            */
        DCL    &COPY       *DEC    (2 0)         /* Number of copies          */
        DCL    &COMPANY    *DEC    (3 0)         /* Company Number            */
        DCL    &FRMDAT     *DEC    (7 0)         /* FROM SHIP DATE            */
        DCL    &THRDAT     *DEC    (7 0)         /* THRU SHIP DATE            */

/* OPENQRYF FIELDS                                                            */
        DCL    &COMPA      *CHAR    3            /* ACK COMPLETE              */
        DCL    &FRMDATA    *CHAR    7            /* FROM SHIP DATE (Passed)   */
        DCL    &THRDATA    *CHAR    7            /* THRU SHIP DATE (Passed)   */

        DCL    &RPTCO     *CHAR   14              /* REPORT/CO MSG  */
        DCL     &A        *CHAR   20              /* QRY FROM DATE  */
        DCL     &B        *CHAR   25              /* QRY TO DATE    */
        DCL     &C        *CHAR   25              /* QRY COMPANY    */
        DCL     &D        *CHAR   70              /* QRY ALL        */
/********************************************************************/
/* CHANGE VARIABLES FOR USE IN PROGRAM                              */
/********************************************************************/

             CHGVAR     VAR(&COMPANY) VALUE(&COMPNYN)
             CHGVAR     VAR(&COMPA) VALUE(&COMPANY)
             CHGVAR     VAR(&FRMDAT) VALUE(&FRMDATN)
             CHGVAR     VAR(&FRMDATA) VALUE(&FRMDAT)
             CHGVAR     VAR(&THRDAT) VALUE(&THRDATN)
             CHGVAR     VAR(&THRDATA) VALUE(&THRDAT)
             CHGVAR     VAR(&COPY) VALUE(&COPYN)

/*********************************************************************/
/*   CREATE OPEN QUERY FILE TO BE USED FOR INVOICING REPORTS         */
/*********************************************************************/

  /*PIO      CHGVAR     VAR(&A) VALUE('FJAODT *GE ' *CAT + +
                         &FRMDATA)                         */
  /*PIO*/    CHGVAR     VAR(&A) VALUE('FJAODT >=  ' *CAT +
                         &FRMDATA)

  /*PIO      CHGVAR     VAR(&B) VALUE('*AND FJAODT *LE ' *CAT + +
                         &THRDATA)                            */
  /*PIO*/    CHGVAR     VAR(&B) VALUE(' AND FJAODT <=  ' *CAT +
                         &THRDATA)

             IF         (&COMPA *NE '000') THEN(DO)
  /*PIO      CHGVAR     VAR(&C) VALUE('*AND FJAIC3 *EQ ' *CAT + +
                         &COMPA)                             */
  /*PIO*/    CHGVAR     VAR(&C) VALUE(' AND FJAIC3  =  ' *CAT +
                         &COMPA)
             ENDDO

             CHGVAR     VAR(&D) VALUE(&A *BCAT &B *BCAT &C)
             CHGVAR     (%SST(&RPTCO 1 3)) VALUE(&COMPA)

/*  RUN QUERY TO READ LOAD HEADER BY DATE RANGE  */

    /*PIO    OVRDBF     FILE(OMFJCPLL) SHARE(*YES) +
             OPNQRYF    FILE(OMFJCPLL) +           +
                        QRYSLT(&D) +               +
                        KEYFLD(*FILE)              */

    /*PIO*/  OVRDBF     FILE(OMFJCPP) TOFILE(OMFJCPP) LVLCHK(*NO)
             OPNSQLF    SQL('Select * from OMFJCPP Where' *BCAT &D +
                          *BCAT ' ORDER BY FJRMNB ASC') +
                          OPNID(OMFJCPLL) OPTION(*ALL) SEQONLY(*NO) +
                          RCDFMT(@FJCPSB) ALWCPY(*IFRQD) /*PIO*/
    /*PIO*/  OVRDBF     FILE(OMFJCPLL) TOFILE(OMFJCPP) LVLCHK(*NO) +
                          OVRSCOPE(*JOB) SHARE(*YES) OPNSCOPE(*JOB)

/********************************************************************/
/* OVERRIDE PRINTER FILE WITH USER SPECIFIED SELECTION VALUES       */
/********************************************************************/

             SBDPRTOVR  CMDVAR(&COMMAND) FILE(OMH6PFR$) OUTQ(&OUTQ) +
                          COPIES(&COPY) HOLD(&HOLD) SAVE(&SAVE)

             CALL       PGM(QCMDEXC) PARM(&COMMAND 512)


/*********************************************************************/
/*   CALL PRINT PROGRAM -
/*********************************************************************/

             CALL       PGM(OMH6PFR) PARM(' ' &COMPANY &FRMDAT +
                          &THRDAT)

 END:        ENDPGM
