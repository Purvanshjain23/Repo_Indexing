      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Triumph Foods
      * PROGRAM:     TF629 - Eopfees: Unpaid Loads Previously Posted to TFS as Paid Loads
      * PROGRAMMER:  LeAnne Ramsey
      * CREATED:     04/08/08
      *
      * Function:    This program extracts unpaid loads.
      *
      ****************************************************************************************
      * MODIFICATIONS:
      ****************************************************************************************
      * DATE      PROGRAMMER
      *
      * 07/16/08  LeAnne Ramsey
      *           Recompile only. Field FDAACTFAM was renamed FDALFAM.
      *           Text was changed from 'Allocated Actual Freight Amount' to
      *                                 'Allocated Load Total Freight Amount'.
      *
      * 03/10/09  LeAnne Ramsey
      *           Recompile only.
      *           The users want to revert back to Allocated Actual Freight Amount
      *           from Allocated Load Total Freight Amount.
      *           This will reverse the change of 07/16/08 above!
      *           We have decided to keep both of the fields in file TFP027-Freight
      *           Variance Detail:
      *              FDALFAM    Allocated Load Total Freight Amount
      *              FDAACTFAM  Allocated Actual Freight Amount
      /eject
      ****************************************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************************************
      *
     Fomfjcplc  if   e           k disk
      *    Load header (logical file selects only Invoice records)
      *
      *
     Ftfl027c   if   e           k disk
      *    Freight variance detail (LF selects only Paid records)
      *
      *
     Ftfp099    if   e           k disk
      *    Function control
      *
      *
     Fprint132  o    f  132        printer oflind(*inof)
      *
      /eject
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      *
      ****************************************************************
      * ARRAYS AND TABLES
      ****************************************************************
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Standard fields
      *
     D dash132         s            132    inz(*all'-')
     d rtime           s              6  0
      *
      *
      * Control fields
      *
     D procfl          s              1    inz('N')
      *
      *
      * Date manipulation
      *
     D wkisodate       s               d   datfmt(*iso)
     D wkcedt          s                   like(fncedt)
      *
      *
      * Parms
      *
     D xxgotdata       s              1
      *
      *
      * Print fields
      *
     D h1cyr           s                   like(fncyr)
     D h1cpe           s                   like(fncpe)
     D l1asdtmdy       s              6  0
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *---------------------------------------------------------------
      * Local data area
      *---------------------------------------------------------------
      *
     d lda            uds                  dtaara(*lda)
      *
     d  lddemand             400    400
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      **********************************************************************************
      * Mainline
      **********************************************************************************
      *
      * If the report has been submitted 1) On=Demand or 2) from the job scheduler and
      * it is the week prior to period end, then generate the report.
      *
     C                   if        procfl = yes                                 If process
      *
     C                   exsr      $h1hdr
      *
      * Process records in the Load Header file that:
      *          1) Have a Load Status of I=Invoiced (the logical file selects these recs)
      *          2) Are Prepaid (have a 'P' in the first position of Shipping Method)
      *          3) Have an LH Carrier Payment Status of ' '=Unpaid
      *
     C     *loval        setll     omfjcplc
      *
     C                   dou       *in91 = *on                                  Do unpaid
     C                   read      omfjcplc                               91
     C                   if        *in91 = *off and                             If not EOF
     C                             %subst(fjq2cd:1:1) = 'P' and
     C                             fjrnst = ' '
      *
      * If this Load had a status of "paid" in a previous TFS Period, print.
      * (LF selects only Paid records.)
      *
     C     fjrmnb        chain     tfl027c                            92
     C                   if        *in92 = *off                                 If prev paid
     C                   exsr      $l1dtl
     C                   endif                                                  If prev paid
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Do unpaid
      *
     C                   if        xxgotdata = no
     C                   except    nodata
     C                   endif
      *
     C                   endif                                                  If process
      *
      * EOF processing
      *
     C                   seton                                        lr
      /eject
      *---------------------------------------------------------------
      * Print detail line (load)
      *---------------------------------------------------------------
      *
     C     $l1dtl        begsr
      *
      * Flip Actual Ship Date to MMDDYY format
      *
     C     *cymd         test(d)                 fjf9dt                 92
     C                   if        *in92 = *off                                 If OK date
     C     *cymd         move      fjf9dt        wkisodate
     C     *mdy          move      wkisodate     l1asdtmdy
     C                   endif                                                  If OK date
      *
     C                   if        *inof = *on
     C                   exsr      $h1hdr
     C                   endif
      *
     C                   except    l1dtl
      *
     C                   move      yes           xxgotdata
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Print report headings
      *---------------------------------------------------------------
      *
     C     $h1hdr        begsr
      *
     C                   except    h1hdr
      *
     C                   endsr
      /eject
      *-----------------------------------------------------------------------------------
      * Determine if it is time to generate the report
      *-----------------------------------------------------------------------------------
      *
     C     $timetogen    begsr
      *
      * You will NOT be in this subroutine if it is On-Demand.
      * When this program is called from a Job Scheduler submission,
      * we want to determine here whether to actually generate the report or not.
      *
      * The REVENUE close function in TFS is run each Monday for the prior week.
      * The "freeze" of freight loads happens as part of this Revenue function when
      * the last week of the period is being closed.
      * We want to find the "once were paid/now are unpaid" loads prior to this "freeze".
      *
      * So, we kick this report off from the Job Scheduler every night. BUT, we only
      * generate the report on the nights when it is the last week of the period.
      *
      * We will determine "when it is the last week of the period" by:
      * Retrieving/comparing the "Current End Dates" from the Control file for:
      *   1) the Revenue function
      *   2) the EOP function
      *
     C     'REVENUE   '  chain     tfp099                             92
     C                   if        *in92 = *off                                 If rev hit
     C                   z-add     fncedt        wkcedt
      *
     C     'EOPFEES   '  chain     tfp099                             92
     C                   if        *in92 = *off and                             If freeze time
     C                             fncedt = wkcedt
     C                   move      yes           procfl
     C                   endif                                                  If freeze time
     C                   endif                                                  If rev hit
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Parm Lists
      *
     C     *entry        plist
     C                   parm                    xxgotdata
      *
      * Key lists
      *    None
      *
     C                   time                    rtime
      *
      * Retrieve the current EOPFEES "period" from the Control file to print in
      * the Report Heading.
      *
     C     'EOPFEES   '  chain     tfp099                             92
     C                   if        *in92 = *off
     C                   z-add     fncyr         h1cyr
     C                   z-add     fncpe         h1cpe
     C                   endif
      *
      * If the report has been submitted from the job scheduler (it is NOT on-demand),
      * we have to determine whether it is time to generate the report or not.
      *
     C                   if        lddemand = 'D'
     C                   move      yes           procfl
     C                   else
     C                   exsr      $timetogen
     C                   endif
      *
     C                   endsr
      /eject
      *-------------------------------------------------------------
      * Report heading lines
      *-------------------------------------------------------------
      *
     Oprint132  e            h1hdr          1 01
     O                       sdpgm               10
     O                                           47 'UNPAID LOADS PREVIOUSLY '
     O                                           69 'POSTED TO TFS AS PAID '
     O                                           74 'LOADS'
     O                                          122 'DATE'
     O                       udate         y    132
      *
     O          e            h1hdr          1
     O                       sdusr               10
     O                                          122 'TIME'
     O                       rtime              132 '  :  :  '
      *
     O          e            h1hdr          1
     O                                          122 'PAGE'
     O                       page          z    132
      *
     O          e            h1hdr          2
     O                                           12 'Year/Period:'
     O                       h1cyr         z     17
     O                       h1cpe         z     20
      *
      *-------------------------------------------------------------
      * Column headings
      *-------------------------------------------------------------
      *
     O          e            h1hdr          1
     O                                           44 'TFS'
     O                                           54 'TFS'
      *
     O          e            h1hdr          1
     O                                            7 'Load'
     O                                           16 'Company'
     O                                           25 'Carrier'
     O                                           35 'Actual'
     O                                           45 'Posted'
     O                                           55 'Posted'
      *
      *
     O          e            h1hdr       0  1
     O                                            6 'ID'
     O                                           16 'Number'
     O                                           23 'Code'
     O                                           35 'Ship Date'
     O                                           44 'Year'
     O                                           55 'Period'
      *
     O          e            h1hdr          1
     O                       dash132            132
      /eject
      *-------------------------------------------------------------
      * Detail line
      *-------------------------------------------------------------
      *
     O          e            l1dtl          1
     O                       fjrmnb        z      7
     O                       fjaic3        z     14
     O                       fjbzna              22
     O                       l1asdtmdy           35 '  /  /  '
     O                       fdyr          z     44
     O                       fdpe          z     53
      *
      /eject
      *-------------------------------------------------------------
      * No data message line
      *-------------------------------------------------------------
      *
     O          e            nodata      1
     O                                           20 'There are no unpaid '
     O                                           36 'loads that were '
     O                                           62 'previously posted as paid.'
      *
      /eject
