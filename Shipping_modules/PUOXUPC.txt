/*********************************************************************/
/* PROGRAM : PUOXUPC                                                 */
/* DESC    : PRT EDI 856 ASN ALERT CL  EXECUTE USER PROGRAM          */
/* DATE    : 11/04/2019                                              */
/* PROJECT : R15305 - EDI 856 ADVANCED SHIPPING NOTICE (ASN)         */
/* BY      : DANNY NGUYEN                                            */
/* PURPOSE : Build WF EDI Whs Stk Txfr Diff (PUA1CPP) file in QTEMP  */
/*           & print Detail report for Bill To Customer's who are    */
/*           setup to receive the EDI 856 ASN V2 & where the Ship To */
/*           Customer does NOT have a Resource No Type of 'PI'       */
/*           (Purchaser Item Code) either in the Customer Item       */
/*           Reference (POBIREP) or the Customer Item Ref for EDI    */
/*           (PUBQREP) file.                                         */
/*                                                                   */
/*           Call SEQUEL to print report as an excel & send via      */
/*           ESEND.                                                  */
/*                                                                   */
/*           Called from:                                            */
/*             Job Schedule                                          */
/*********************************************************************/
/* MAINTENANCE LOG:                                                  */
/*-------------------------------------------------------------------*/
/* DATE     BY  CHANGE DESCRIPTION                                   */
/* -------- --- ---------------------------------------------------- */
/* 12/16/19 RMC ADDED EDI ALERTS COMING FROM EDI PO HDR/DTL        */
/*              WHICH WILL ADD TO WF PBUSCPP IF THE SBD ITEM CANT BE FOUND */
/*********************************************************************/

             PGM

             DCL        VAR(&RTNCDE) TYPE(*CHAR) LEN(7)

             DCL        VAR(&RTNLIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&EMAIL) TYPE(*CHAR) LEN(50)
             DCL        VAR(&EMLMSG) TYPE(*CHAR) LEN(170)

             DCL        VAR(&FRDATE) TYPE(*CHAR) LEN(8)
             DCL        VAR(&TODATE) TYPE(*CHAR) LEN(8)


/*-------------------------------------------------------------------*/
/* Build WF EDI Whs Stk Txfr Diff (PUA1CPP) file in QTEMP.           */
/*-------------------------------------------------------------------*/
             CRTDUPOBJ  OBJ(PUA1CPP) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)
             MONMSG     MSGID(CPF2130) EXEC(CLRPFM FILE(QTEMP/PUA1CPP))
             CRTDUPOBJ  OBJ(PUA1CPL0) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)
             MONMSG     MSGID(CPF2130) /* Object duplicated/not +
                          duplicated */
             CRTDUPOBJ  OBJ(PUA1CPL1) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)
             MONMSG     MSGID(CPF2130) /* Object duplicated/not +
                          duplicated */
             CRTDUPOBJ  OBJ(PUA1CPL2) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)
             MONMSG     MSGID(CPF2130) /* Object duplicated/not +
                          duplicated */

             OVRDBF     FILE(PUA1CPP) TOFILE(QTEMP/PUA1CPP)
             OVRDBF     FILE(PUA1CPL0) TOFILE(QTEMP/PUA1CPL0)
             OVRDBF     FILE(PUA1CPL1) TOFILE(QTEMP/PUA1CPL1)
             OVRDBF     FILE(PUA1CPL2) TOFILE(QTEMP/PUA1CPL2)

             CALL       PGM(PUOYXFR) PARM(&RTNCDE &FRDATE &TODATE)

/*-------------------------------------------------------------------*/
/* Create Excel Spreadsheet with SEQUEL Viewpoint and email to       */
/* receipient.                                                       */
/*-------------------------------------------------------------------*/

             CHGVAR     VAR(&EMAIL) VALUE('EDI_856_ALERT')

    /* Print EDI 856 ASN Alert Report */
             CHGVAR     VAR(&EMLMSG) VALUE('EDI 856 ASN Alert: +
                          EDI856ALRT Export to Excel')

             CHGVAR     VAR(&EMLMSG) VALUE(&EMLMSG *BCAT '- From +
                          Scheduled Ship Date:' *BCAT &FRDATE +
                          *BCAT '-' *BCAT &TODATE)

    /* If EDI856ALRT Return Library Not PRKPLIB Then Set 'TEST' in Email Msg */
             RTVOBJD    OBJ(EDI856ALRT) OBJTYPE(*USRSPC) +
                          RTNLIB(&RTNLIB)
             IF         COND(&RTNLIB *NE 'PRKPLIB') THEN(DO)
             CHGVAR     VAR(&EMLMSG) VALUE(&EMLMSG *BCAT '- TEST')
             CHGVAR     VAR(&EMAIL) VALUE('EDI_856_ALERT_TST')
             ENDDO

    /* Call SEQUEL to Email EDI 856 ASN Alert Report */
             EXECUTE    VIEW(EDI856ALRT) PCFMT(*XLS) +
                          RECIPIENT(&EMAIL) EMLMSG(&EMLMSG)


             DLTOVR     FILE(*ALL)
             DLTF       FILE(QTEMP/PUA1CP*)

 EDIALL:
             CLRPFM     FILE(PUBSCPP)
             CALL       PGM(PUOEXFR) PARM('' X'0000000F')
             EXECUTE    VIEW(EDIALERT) PCFMT(*XLS) RECIPIENT(&EMAIL) +
                          EMLMSG('EDI ALERT - SBD CROSS REF MISSING')
             MONMSG     MSGID(CPF0000)
 ENDPGM:
             ENDPGM
