/***************************************************************** */
/*                                                                 */
/*  PROGRAM NUMBER ... HPHELPCL                                    */
/*  PROGRAM NAME ..... DISPLAY HELP TEXT                           */
/*  PROGRAMMER ....... LEANNE FEDOR                                */
/*  DATE ............. 02/08/00                                    */
/*                                                                 */
/*  PROGRAM PURPOSE -> THIS CL CONTROLS THE DISPLAYING OF HELP     */
/*                     TEXT.                                       */
/*                                                                 */
/***************************************************************** */
/* MODIFICATIONS                                                   */
/***************************************************************** */

             PGM        PARM(&PGM)

             DCL        VAR(&PGM) TYPE(*CHAR) LEN(10)
             DCL        VAR(&DOC) TYPE(*CHAR) LEN(8)
             DCL        VAR(&FLDR) TYPE(*CHAR) LEN(17)
             DCL        VAR(&LBL) TYPE(*CHAR) LEN(10)

/* CALL THE PROGRAM TO RETRIEVE THE NAME OF THE DOCUMENT, FOLDER   */
/* AND LABEL ASSOCIATED WITH THE PROGRAM THE USER WAS IN WHEN      */
/* THEY PRESSED HELP.  THIS INFO MUST BE SET UP IN FILE PKA0REL.   */
/* TO SET UP THE DATA, CALL PKMGDFR PASSING IN A PARM OF ' '.      */

             CALL       PGM(PKJGXFR) PARM('       ' &PGM &DOC &FLDR +
                          &LBL)

             IF         COND(&DOC *NE ' ' & &FLDR *NE ' ' & &LBL *NE +
                          ' ') THEN(DO)
             DSPHLPDOC  DOC(&DOC) FLR(&FLDR) HLPLBL(&LBL)

/* TO PREVENT THE CL FROM ABENDING WHEN THE RESOLVED DOCUMENT DOES */
/* NOT EXIST ON THE AS/400, MONITOR FOR MESSAGE.                   */

             MONMSG     MSGID(CPF0000) EXEC(DO)
             SNDPGMMSG  MSGID(CPF9897) MSGF(QCPFMSG) MSGDTA('Help +
                          does not exist') TOPGMQ(*EXT) +
                          MSGTYPE(*STATUS)
             DLYJOB     DLY(2)
             SNDPGMMSG  MSGID(CPF9897) MSGF(QCPFMSG) MSGDTA(' ') +
                          TOPGMQ(*EXT) MSGTYPE(*STATUS)
             ENDDO
             ENDDO

/* THE DOC/FOLDER/LABEL INFO HAS NOT BEEN SET UP IN FILE PKA0REL.  */

             ELSE       CMD(DO)
             SNDPGMMSG  MSGID(CPF9897) MSGF(QCPFMSG) MSGDTA('Help +
                          does not exist') TOPGMQ(*EXT) +
                          MSGTYPE(*STATUS)
             DLYJOB     DLY(2)
             SNDPGMMSG  MSGID(CPF9897) MSGF(QCPFMSG) MSGDTA(' ') +
                          TOPGMQ(*EXT) MSGTYPE(*STATUS)
             ENDDO

             ENDPGM
