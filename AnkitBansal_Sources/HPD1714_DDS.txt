     A*%%TS  SD  20040802  095759  ISLRAMS     REL-V5R2M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD1714
     A*  TITLE:         CUSTOMIZE ITEMS FOR FARM BUDGET
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  CREATED:       02/03/04
     A*
     A*  INDICATOR 60 IS ON WHEN IN REVISE MODE
     A*  INDICATOR 61 IS ON WHEN IN CREATE MODE
     A*
     A*  INDICATOR 63 IS ON WHEN THE SUBFILE FIELDS ARE NON-DISPLAY
     A*               BECAUSE THE RECORD WAS SUCCESSFULLY DELETED
     A*               (THE HIDDEN SUBFILE FIELD HDNDFL = YES)
     A*
     A*  INDICATOR 64 IS ON WHEN A SUBFILE RECORD FIELDS MUST BE PROTECTED.
     A*               (THE HIDDEN SUBFILE FIELD HDPFL = YES). FIELDS ARE
     A*               PROTECTED IF:
     A*               1) THE SUBFILE RECORD WAS SUCCESSFULLY CREATED
     A*
     A*  INDICATOR 67 IS ON WHEN A BUDGET IS 'INACTIVE' AND YOU, THEREFORE, HAVE
     A*               NO 'BUDGET AMOUNTS' AND DO NOT WANT TO DISPLAY THE
     A*               'AMOUNT' TEXT.
     A*
     A*  INDICATOR 90 IS ON WHEN THE RECORD THE USER SELECTED
     A*               FROM THE WORK WITH HAS BEEN DELETED BY ANOTHER USER
     A**************************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A N90                                  CF04(04 'PROMPT')
     A N90                                  CF05(05 'REFRESH')
     A N90                                  CF10(10 'CHANGE MODES')
     A*
     A*---------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP1714S1                  SFL
     A*%%TS  SD  20040802  095759  ISLRAMS     REL-V5R2M0  5722-WDS
     A  85                                  SFLNXTCHG
     A            HDRRN1         4Y 0H
     A            HDPFL     R        H      REFFLD(@@FLAG)
     A            HDNDFL    R        H      REFFLD(@@FLAG)
     A            D1OPT          2A  B 12  2CHGINPDFT(UL)
     A  61
     AO 64
     AO 90                                  DSPATR(PR)
     A  61
     AO 64
     AO 90                                  DSPATR(ND)
     A  30                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A N30N61N64
     A N90                                  DSPATR(HI)
     A  60N30N64
     A N90                                  DSPATR(UL)
     A            D1BGIT    R        B 12  5REFFLD(HSRREF/@@BGIT *LIBL/HSPREF)
     A  60
     AO 64
     AO 90                                  DSPATR(PR)
     A  63                                  DSPATR(ND)
     A  33
     AO 53                                  DSPATR(PC)
     A  33                                  DSPATR(RI)
     A N33N60N64
     A N90                                  DSPATR(HI)
     A N33N60N64
     A N90                                  DSPATR(UL)
     A            D1DESC        37A  O 12 31
     A            D1ITTYCD  R        O 12 74REFFLD(HSRREF/@@ITTYCD *LIBL/HSPREF)
     A  63                                  DSPATR(ND)
     A                                 13 10'qty:'
     A                                      COLOR(PNK)
     A            D1BGQT    R        B 13 15REFFLD(HSRREF/@@BGQT *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A  64
     AO 90                                  DSPATR(PR)
     A  63                                  DSPATR(ND)
     A  34                                  DSPATR(PC)
     A  34                                  DSPATR(RI)
     A N34N64N90                            DSPATR(HI)
     A N34N64N90                            DSPATR(UL)
     A                                 13 31'amount:'
     A  67                                  DSPATR(ND)
     A                                      COLOR(PNK)
     A            D1FBAM    R        O 13 39REFFLD(HSRREF/@@FBAM *LIBL/HSPREF)
     A  67                                  DSPATR(ND)
     A                                      EDTCDE(K)
     A                                 13 53'uom:'
     A                                      DSPATR(HI)
     A                                      COLOR(PNK)
     A            D1UOM     R        O 13 58REFFLD(HSRREF/@@UOM *LIBL/HSPREF)
     A  63                                  DSPATR(ND)
     A                                 13 67'uow:'
     A                                      DSPATR(HI)
     A                                      COLOR(PNK)
     A            D1UOW     R        O 13 72REFFLD(HSRREF/@@UOW *LIBL/HSPREF)
     A  63                                  DSPATR(ND)
     A*
     A*---------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*---------------------------------------------------------------
     A*
     A          R HP1714C1                  SFLCTL(HP1714S1)
     A*%%TS  SD  20040225  113426  ISLRAMS     REL-V5R2M0  5722-WDS
     A                                      SFLSIZ(0050)
     A                                      SFLPAG(0005)
     A                                      SETOF(30)
     A                                      SETOF(33)
     A                                      SETOF(34)
     A                                      SETOF(50)
     A                                      SETOF(53)
     A                                      SETOF(54)
     A                                      SETOF(63)
     A N82N90                               ROLLUP(25 'PAGE FORWARD')
     A                                      SFLCSRRRN(&HDRR1)
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A                                      OVERLAY
     A                                      RTNCSRLOC(&RCD &FLD)
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            HDRR1          5S 0H
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 25'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A            C1MODE         6A  O  2  2DSPATR(HI)
     A                                  2 20'CUSTOMIZE ITEMS FOR FARM BUDGET'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  3  7'(Customizations last ONLY for the -
     A                                      duration of this Budget Number)'
     A                                      DSPATR(HI)
     A                                  5  2'Farm site .:'
     A            C1FSCD    R        O  5 15REFFLD(HSRREF/@@FSCD *LIBL/HSPREF)
     A                                      EDTCDE(Z)
     A            C1FSNM    R        O  5 22REFFLD(HSRREF/FSFSNM *LIBL/HSPREF)
     A                                  5 51'Budget number:'
     A            C1FBSN    R        O  5 66REFFLD(HSRREF/@@FBSN *LIBL/HSPREF)
     A                                      EDTCDE(Z)
     A                                  6  2'Template ..:'
     A            C1BTCD    R        O  6 15REFFLD(HSRREF/@@BTCD *LIBL/HSPREF)
     A            C1BTDS    R        O  6 22REFFLD(HSRREF/@@BTDS *LIBL/HSPREF)
     A                                  6 51'Status ......:'
     A            C1FBSDS   R        O  6 66REFFLD(HSRREF/@@FBSDS *LIBL/HSPREF)
     A N61N90                           8  2'4=Delete'
     A N90N61                           8 76'?'
     A                                      DSPATR(HI)
     A N90                              9 10'?'
     A                                      DSPATR(HI)
     A                                  9 74'Item'
     A                                      DSPATR(HI)
     A                                 10  2'Op'
     A                                      DSPATR(HI)
     A                                 10  5'Budget Item'
     A                                      DSPATR(HI)
     A                                 10 31'Description'
     A                                      DSPATR(HI)
     A                                 10 74'Type'
     A                                      DSPATR(HI)
     A N90        C1BGIT    R        B 11  5REFFLD(HSRREF/@@BGIT *LIBL/HSPREF)
     A  50                                  DSPATR(PC)
     A                                      DSPATR(HI)
     A  61                                  DSPATR(ND)
     A  61                                  DSPATR(PR)
     A N90        C1DESC        37A  B 11 31DSPATR(HI)
     A  61                                  DSPATR(ND)
     A  61                                  DSPATR(PR)
     A N90        C1ITTYCD  R        B 11 74REFFLD(HSRREF/@@ITTYCD *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A  61                                  DSPATR(ND)
     A  61                                  DSPATR(PR)
     A  54                                  DSPATR(PC)
     A*
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R HP1714K1
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A                                      DSPATR(HI)
     A                                 23  2'F3=Exit'
     A N90                             23 11'F4=Prompt'
     A N90                             23 22'F5=Refresh'
     A  60N90                          23 34'F10=Go to Create'
     A  61N90                          23 34'F10=Go to Revise'
     A*
     A*---------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE FORMAT
     A*---------------------------------------------------------------
     A          R HP1714ES                  SFL SFLMSGRCD(24)
     A*
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
     A*
     A*---------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*---------------------------------------------------------------
     A          R HP1714EC                  SFLCTL(HP1714ES)
      *
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
