     A*%%TS  SD  20011227  110445  ISLRAMS     REL-V4R4M0  5769-PW1
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION
     A*  DISPLAY FILE:  HPD581 - SELECT ACTIVE RATION
     A*  RPG PROGRAM:   HP581  - SELECT ACTIVE RATION
     A*  CREATED BY:    LEANNE FEDOR
     A*  DATE:          04/05/94
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA12(12 'PREVIOUS')
     A N82                                  ROLLUP(25 'PAGE FORWARD')
     A*----------------------------------------------------------------
     A* REFERENCE WINDOW RECORD FORMAT 1
     A*----------------------------------------------------------------
     A          R HP581R1
     A*%%TS  SD  19941216  141015  ISLRAMS     REL-V3R0M5  5763-PW1
     A                                      TEXT('Reference window')
     A                                      WINDOW(*DFT 16 53)
     A                                      WDWBORDER((*DSPATR RI) (*CHAR '    -
     A                                          '))
     A*----------------------------------------------------------------
     A* RECORD FORMAT ONE
     A*----------------------------------------------------------------
     A          R HP581R2
     A                                      TEXT('Written in RPG pgm to prevent-
     A                                       screen from clearing')
     A                                      CLRL(*NO)
     A                                      OVERLAY
     A*----------------------------------------------------------------
     A* RECORD FORMAT TWO
     A*----------------------------------------------------------------
     A* THIS FORMAT IS NOT WRITTEN IN THE RPG PGM.  IT PREVENTS THE
     A* CALLING_PROGRAM_SCREEN FROM BEING CLEARED.
     A*----------------------------------------------------------------
     A          R HP581R3
     A*%%TS  SD  19940315  135528  ISRCENT     REL-V2R2M0  5738-PW1
     A                                      TEXT('Dummy format not written in R-
     A                                      PG pgm.')
     A                                      ASSUME
     A                                 11  1' '
     A*---------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP581S1                   SFL
     A*%%TS  SD  20011227  110445  ISLRAMS     REL-V4R4M0  5769-PW1
     A                                      TEXT('subfile record format one')
     A            HDRRN1         4S 0H
     A            DFRNCD    R        O  6  1REFFLD(RNRNCD)
     A            DFRNDS    R        O  6 10REFFLD(RNRNDS)
     A            DFPPCD    R        O  6 33REFFLD(RNPPCD)
     A            DFMAXDAY  R        O  6 46REFFLD(HSRREF/@@MAXDAY *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A*---------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP581C1                   SFLCTL(HP581S1)
     A*%%TS  SD  20011227  110445  ISLRAMS     REL-V4R4M0  5769-PW1
     A                                      SFLSIZ(0050)
     A                                      SFLPAG(0009)
     A                                      WINDOW(HP581R1)
     A                                      TEXT('control format one')
     A                                      OVERLAY
     A                                      SFLCSRRRN(&HDRRN)
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND(*MORE)
     A                                      RMVWDW
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            HDRRN          5S 0H
     A                                  1 16'SELECT ACTIVE RATION'
     A                                      DSPATR(HI)
     A                                  2  1'Position Cursor, Press Enter'
     A                                  3 30'Production'
     A                                      DSPATR(HI)
     A                                  3 44'Maximum'
     A                                      DSPATR(HI)
     A                                  4  1'Ration'
     A                                      DSPATR(HI)
     A                                  4 10'Description'
     A                                      DSPATR(HI)
     A                                  4 33'Phase'
     A                                      DSPATR(HI)
     A                                  4 43'Age (Days)'
     A                                      DSPATR(HI)
     A            C1RNCD    R        B  5  1REFFLD(RNRNCD)
     A                                      DSPATR(HI)
     A            C1PPCD    R        B  5 33REFFLD(RNPPCD)
     A                                      DSPATR(HI)
     A          R HP581K1
     A                                      WINDOW(HP581R1)
     A                                      OVERLAY
     A                                 15  1'F12=Previous'
