/***************************************************************** */
/*                                                                 */
/*  PROGRAM NUMBER:  PLJMUPC                                       */
/*  PROGRAM NAME:    TRUCK START ENTER MOVEMENT NUMBER WO WEIGHT   */
/*                   Truck Gate processing                         */
/*                                                                 */
/*  PROGRAMMER:      PURVA DROGE AND SUSAN MASON CLONE             */
/*  CREATE DATE:     01/05/99 CLONE 6/5/2007  FROM PKPUUPR         */
/*                                                                 */
/*  FUNCTION:        THIS CL STARTS TO PROGRAM FOR ENTERING        */
/*                   THE MOVEMENT NUMBER FOR TRUCKS WITH OUT       */
/*                   ANY WEIGHT.  PKPUUPR IS WITH WEIGHT AND       */
/*                   THIS FUNCTION IS WITH OUT WEIGHT              */
/*                                                                 */
/*******************************************************************/
/*  Modification History                                           */
/*  8/28/2007 SUSAN MASON REMOVE OVRRIDES TO DEVICE FILES          */
/*                   THAT WERE USED FOR GUYMON AND NOT NEEDED      */
/*                   FOR TRIUMPH FOODS.                            */
/*                   This cl was just review to looking for        */
/*                   outq informaiton on the snd url procedure     */
/*                                                                 */
/*                   FILE(PKQEUPC#) TO                             */
/*                     WAITRCD(30)                                 */
/*                   FILE(PKKMSRR#) TO                             */
/*                     WAITRCD(30)                                 */
/*                   FILE(PKYJSRR#) TO                             */
/*******************************************************************/
             PGM        PARM(&CO)

/*-----------------------------------------------------------------*/
/*                      PARAMETER                                  */
/*-----------------------------------------------------------------*/
/* Print Options                                                   */

/* Function Specific                                               */
             DCL        VAR(&CO) TYPE(*DEC) LEN(3)
             DCL        VAR(&VALCDE) TYPE(*CHAR) LEN(10) +
                          VALUE('HPETRKSCL')
             DCL        VAR(&VALUE) TYPE(*CHAR) LEN(40)
             DCL        VAR(&DEVICE) TYPE(*CHAR) LEN(10)
             DCL        VAR(&JOB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&USER) TYPE(*CHAR) LEN(10)
             DCL        VAR(&NBR) TYPE(*CHAR) LEN(6)
             DCL        VAR(&DTAARA) TYPE(*CHAR) LEN(26)
             DCL        VAR(&MSGDATA) TYPE(*CHAR) LEN(512)
             DCL        VAR(&MSGID) TYPE(*CHAR) LEN(7)

/*-----------------------------------------------------------------*/
/*                    PROGRAM VARIABLES                            */
/*-----------------------------------------------------------------*/

/*-----------------------------------------------------------------*/
/*                     INITIALIZATION                              */
/*-----------------------------------------------------------------*/

             CHGJOB     LOG(4 00 *SECLVL) INQMSGRPY(*DFT)

/* TRY TO ALLOCATE THE DATAAREA EXCLUSIVELY.  IF IS ALREADY        */
/* ALLOCATED WITH *SHRRD TO ANOTHER JOB, THEN GO TO THE ERROR      */
/* PROCESSING BECAUSE THERE IS ALREADY ANOTHER JOB RUNNING.        */

             ALCOBJ     OBJ((HPETFGATE *DTAARA *EXCL))
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(ALCOBJ))

/* ALLOCATE THE DATAAREA TO THIS JOB SO ANOTHER JOB CANNOT GET     */
/* EXCLUSIVE LOCK ON THE DATAAREA                                  */

             ALCOBJ     OBJ((HPETFGATE *DTAARA *SHRRD))

/* RELEASE EXCLUSIVE LOCK ON THE DATAAREA.  IF THIS IS THE ONLY    */
/* JOB, THEN THE LOCK NEEDS TO *SHRRD.                             */
             DLCOBJ     OBJ((HPETFGATE *DTAARA *EXCL))

/*-----------------------------------------------------------------*/
/*                           MAIN                                  */
/*-----------------------------------------------------------------*/
             CALL       PGM(PDGKXFR) PARM(' ' &CO &VALCDE &VALUE)

             CHGVAR     VAR(&DEVICE) VALUE(&VALUE)

             OVRDSPF    FILE(PLJLPVR#) TOFILE(PLJLPVR#) DEV(&DEVICE)
/******************************************************/
/********    OVRDSPF    FILE(PKQEUPC#) TOFILE(PKQEUPC#) DEV(&DEVICE) + */
/********                 WAITRCD(30)        *********/
/********    OVRDSPF    FILE(PKKMSRR#) TOFILE(PKKMSRR#) DEV(&DEVICE) + */
/********                 WAITRCD(30)        *********/
/********    OVRDSPF    FILE(PKYJSRR#) TOFILE(PKYJSRR#) DEV(&DEVICE) + */
/********                 WAITRCD(30)        *********/
/******************************************************/

             RTVJOBA    JOB(&JOB) USER(&USER) NBR(&NBR)
             CHGVAR     VAR(%SST(&DTAARA  1 10)) VALUE(&JOB)
             CHGVAR     VAR(%SST(&DTAARA 11 10)) VALUE(&USER)
             CHGVAR     VAR(%SST(&DTAARA 21  6)) VALUE(&NBR)
             CHGDTAARA  DTAARA(HPETFGATE) VALUE(&DTAARA)

LOOP:
             CALL PLJLPVR ''
             MONMSG     MSGID(RPG0000)

             DLYJOB     DLY(120)
             GOTO       CMDLBL(LOOP)

/*-----------------------------------------------------------------*/
/*                     ERROR MESSAGES                              */
/*-----------------------------------------------------------------*/

 ALCOBJ:
             /* RECEIVE THE MESSAGE THAT INDICATES WHO SUBMITTED */
             /* THIS JOB AND SEND THEM A MESSAGE.                */
             RCVMSG     PGMQ(*EXT) RMV(*NO) MSGDTA(&MSGDATA) +
                          MSGID(&MSGID)
             IF         COND(&MSGID *EQ 'CPI1125') THEN(DO)
             CHGVAR     VAR(&USER) VALUE(%SST(&MSGDATA 37 10))
             SNDUSRMSG  MSG('Truck Gate Job is already running.  +
                          Please take the option to End Truck Gate +
                          Device from the Menu.  After the Job has +
                          ended, take the option to Start Truck +
                          Gate Device.') TOUSR(&USER)
             GOTO       CMDLBL(ENDPGM)
             ENDDO
             GOTO       CMDLBL(ALCOBJ)

 ERROR2:

/*-----------------------------------------------------------------*/
/*                      PROGRAM END                                */
/*-----------------------------------------------------------------*/
 ENDPGM:
             ENDPGM
