      *****************  RPG PROGRAM HEADING  ************************
      *
      * ENVIRONMENT: Pork Division
      * SYSTEM:      Datamart--Sales
      * PROGRAM:     SH222 - Datamart Sales: Fresh-to-Frozen Rework
      * PROGRAMMER:  LeAnne Ramsey
      * CREATED:     03/26/14
      *
      *           This only runs for Seaboard...not Dailys.
      *
      *           On each run, we will use the AS400 system date less 3 days
      *           to get a processing date. We will delete any Datamart records
      *           we already have with a Transaction Date on/after this processing date.
      *           Then, we will extract the Inventory Transaction Journal records
      *           and write them to our Datamart for this date forward.
      *
      *           This file will be used for trending the Items that were remade/relabeled
      *           from a Fresh Item Code to a Frozen Item Code. They have just started
      *           doing this on the AS400.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * Date      Programmer
      *
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fcab7cpm5  if   e           k disk
      *  Item transaction detail (aka: Inventory Transaction Journal)
      *  (LF selects only: B7W9ST-Inventory Transaction ID=R
      *                    B7BSST-Inventory Transaction Type=RR
      *                    B7CQCD-Reason Code=RWZ
      *   This gives us the Fresh-to-Frozen rework records only.
      *
      *
     Fshp222    uf a e           k disk
      *  Datamart: Item transaction detail
      *
      /EJECT
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      *---------------------------------------------------------------
      *  Named Constants
      *---------------------------------------------------------------
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      *---------------------------------------------------------------
      * Standalone fields
      *---------------------------------------------------------------
      *
      * Workfields
      *
     D wk80tndt        s                   like(ff80tndt)
     D wksyndt         s                   like(b7b4dt)
     D wkcymdiso       s               D   datfmt(*iso)
     D wknulldt        s                   like(fftndt)   inz(D'0001-01-01')
      *
      ****************************************************************
      * Data Structures
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * Mainline
      ****************************************************************
      *
      * Read all the records for the processing date forward...the LF
      * selects out the Fresh-to-Frozen rework records.
      *
     C     wksyndt       setll     cab7cpm5
     C                   dou       *in90 = *on
     C                   read      cab7cpm5                               90
     C                   if        *in90 = *off
     C                   exsr      $wrt222
     C                   endif
     C                   enddo
      *
     C                   seton                                        lr
      /eject
      *---------------------------------------------------------------
      * Write a Datamart record
      *---------------------------------------------------------------
      *
     C     $wrt222       begsr
      *
     C                   move      'S'           ffcorg
     C                   z-add     b7aic3        ffcono
     C                   z-add     b7hgcd        ffitcd
     C                   move      b7ajcd        ffwhcd
     C                   move      b7hvcd        ffuom
     C                   z-add     b7a5qt        fftrqt
     C                   z-add     b7ahwg        fftrwt
     C                   z-add     b7ayva        ffexam
      *
      * Transaction date
     C     *cymd         test(d)                 b7b4dt                 92
     C                   if        *in92 = *off
     C     *cymd         move      b7b4dt        fftndt
     C     *iso          move      fftndt        ff80tndt
     C                   move      fftndt        fftndtmd
     C                   else
     C                   move      wknulldt      fftndt
     C                   move      wknulldt      fftndt
     C                   z-add     0             ff80tndt
     C                   endif
      *
      * Production date
     C     *cymd         test(d)                 b7eldt                 92
     C                   if        *in92 = *off
     C     *cymd         move      b7eldt        ffpddt
     C     *iso          move      ffpddt        ff80pddt
     C                   move      ffpddt        ffpddtmd
     C                   else
     C                   move      wknulldt      ffpddt
     C                   move      wknulldt      ffpddt
     C                   z-add     0             ff80pddt
     C                   endif
      *
     C                   write     ffrec
     C                   clear                   ffrec
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      *  Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Use the System Date less 3 days to come up with a "processing" date.
      *
     C     *mdy          movel     udate         wkcymdiso
     C     wkcymdiso     subdur    3:*days       wkcymdiso
     C                   movel     wkcymdiso     wk80tndt
     C     *cymd         movel     wkcymdiso     wksyndt
      *
      * Delete all Datmart records with a Transaction Date
      * on/after the date calc'd above.
      *
     C     wk80tndt      setll     shp222
     C                   dou       *in92 = *on
     C                   read      shp222                                 92
     C                   if        *in92 = *off
     C                   delete    ffrec
     C                   endif
     C                   enddo
      *
     C                   endsr
      /EJECT
