     A*%%TS  SD  20020829  115235  ISLRAMS     REL-V5R1M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD4078
     A*  TITLE:         SPECIFY OPTIONS FOR ROOM AVAILABILITY REPORT
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  CREATED:       08/29/02
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A N99                                  CF04(04 'PROMPT')
     A  44N99                               CF08(08 'SUBMIT')
     A N99                                  CF17(17 'PRINT OPTIONS')
     A N99                                  CF20(20 'WRKSPLF')
     A*
     A*---------------------------------------------------------------
     A* RECORD FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP4078R1
     A*%%TS  SD  20020829  115235  ISLRAMS     REL-V5R1M0  5722-WDS
     A                                      SETOF(31)
     A                                      SETOF(32)
     A                                      SETOF(33)
     A                                      SETOF(34)
     A                                      SETOF(51)
     A                                      SETOF(52)
     A                                      OVERLAY
     A                                      RTNCSRLOC(&RCD &FLD)
     A            RCD       R        H      REFFLD(@@RCD)
     A            FLD       R        H      REFFLD(@@FLD)
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 25'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USR)
     A                                      DSPATR(HI)
     A                                  2 14'SPECIFY OPTIONS FOR ROOM AVAILABIL-
     A                                      ITY REPORT'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@JOB)
     A                                      DSPATR(HI)
     A                                  4  2'As of date .........'
     A N99        D1MDY     R        B  4 25REFFLD(HSRREF/@@DT06 *LIBL/HSPREF)
     A                                      EDTWRD('  /  /  ')
     A N33                                  DSPATR(HI)
     A  33                                  DSPATR(PC)
     A  33                                  DSPATR(RI)
     A                                  5  2'Production phase ...'
     A N99                              5 23'?'
     A N99        D1PPCD    R        B  5 25REFFLD(HSRREF/@@PPCD *LIBL/HSPREF)
     A N31                                  DSPATR(HI)
     A  51
     AO 31                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A            D1PPDS    R        O  5 34REFFLD(PPREC/PPPPDS *LIBL/HSP011)
     A                                  5 65'(Blank for All)'
     A                                  6  2'Cell ...............'
     A N99        D1CELL    R        B  6 25REFFLD(HSRREF/@@CELL *LIBL/HSPREF)
     A                                      EDTCDE(M)
     A N34                                  DSPATR(HI)
     A  34                                  DSPATR(PC)
     A  34                                  DSPATR(RI)
     A                                  6 65'(Blank for All)'
     A                                  7  2'Farm site ..........'
     A N99                              7 23'?'
     A N99        D1FSCD    R        B  7 25REFFLD(@@FSCD)
     A                                      EDTCDE(M)
     A N32                                  DSPATR(HI)
     A  52
     AO 32                                  DSPATR(PC)
     A  32                                  DSPATR(RI)
     A            D1FSNM    R        O  7 34REFFLD(FSFSNM)
     A                                  7 65'(Blank for All)'
     A*
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R HP4078K1
     A*%%TS  SD  19980601  163600  ISLRAMS     REL-V3R7M0  5716-PW1
     A  43                             21  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A N99                             23 11'F4=Prompt'
     A  44N99                          23 22'F8=Submit Listing'
     A N99                             23 41'F17=Print Options'
     A N99                             23 60'F20=Spool Files'
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *---------------------------------------------------------------
     A          R HP4078ES                  SFL SFLMSGRCD(24)
      *
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
     A*---------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*---------------------------------------------------------------
     A*
     A          R HP4078EC                  SFLCTL(HP4078ES)
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
