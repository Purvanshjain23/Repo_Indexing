      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      HOG PRODUCTION
      * PROGRAM:     HPJDEVE1
      * TITLE:       VALIDATE J.D. EDWAREDS ADDRESS NUMBER AND RETRIEVE
      *               NAME
      * PROGRAMMER:  Brad Baden   SDN465   TFS# 16169
      * CREATED:      3/19/2020
      *
      * FUNCTION:  THIS PROGRAM IS CALLED TO VALIDATE JDE ADDRESS
      *            NUMBERS USED IN THE HOG PRODUCTION SYSTEM
      *            AGAINST THE JDE ADDRESS BOOK FILE.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      * 07/15/94  R. CENTONZE    JDE DATABASE CHANGES FOR 7/24:
      *                          THE STATE CODE IS 3A INSTEAD OF 2A,
      *                          (ABADDS)
      *
      * 05/08/96  LEANNE FEDOR
      *           REMOVED PARM FIELDS FOR ADDRESS.
      * 05/20/96  JONI MILLER
      *           JDE RELEASE A7.1 CHANGES
      *
      * 3/12/2020 JBB E16169 - JDE World to JDE Enterprise 1
      *       Project SDN465   CVTRPGSRC for JFOODCUST/QRPGSRC for
      *                        member HPJDEV to E1IDEVGEN/QRPGLESEC
      *                        member HPJDEVE1.
      *
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     F*F0101LA   IF   E           K DISK
     fe10101la  if   e           k disk
      *    JDE ADDRESS BOOK  (KEY: ABAN8)
      *
      /EJECT
      *---------------------------------------------------------------
      *  STANDARD MESSAGE ARRAYS
      *---------------------------------------------------------------
      *
      *---------------------------------------------------------------
      * STANDARD ERROR MESSAGE HANDLING DATA STRUCTURES
      *---------------------------------------------------------------
      *
      * THIS DATA STRUCTURE SUPPLIES THE NAME OF THE MESSAGE FILE TO
      * THE MESSAGE HANDLING CL PROGRAM.  THE FIELD NAME MSGFIL MUST BE
      * CONSTANT. THE VALUE IN QUOTES IS THE NAME OF THE SPECIFIC
      * MESSAGE FILE CONTAINING THE USER DEFINED MESSAGES.
      *
     D                 DS                  INZ
     D  MSGFIL                 1     10    INZ('HSMSGF    ')
      *
      * THE FOLLOWING 3 DATA STRUCTURES ARE USED TO SPEED MESSAGE
      * HANDLING SINCE IT IS FASTER TO CLEAR DATA STRUCTURES THAN
      * ARRAYS.  EACH IS ASSOCIATED WITH A STANDARD MESSAGE ARRAY.
      *
     D MGI             DS           140
     D  MGID                   1    140
     D                                     DIM(20)                              MSG ID ARRAY
      *
     D MGD             DS          1000
     D  MGDT                   1   1000
     D                                     DIM(20)                              MSG PARMS
      *
     D MGWK            DS            50    INZ
     D  MGW                    1     50
     D                                     DIM(50)                              MSG WORK PARMS
      *
      *---------------------------------------------------------------
      * STANDARD PROGRAM STATUS DATA STRUCTURE
      *---------------------------------------------------------------
      *    EXTERNALLY DEFINED AS UTPGFR (RECORD FORMAT: PGMDSR)
     D PGMDS         ESDS                  EXTNAME(UTPGFR)
      *
      *---------------------------------------------------------------
      * STANDARD WORKSTATION INFORMATION DATA STRUCTURE
      *---------------------------------------------------------------
      *    EXTERNALLY DEFINED AS UTWSFR (RECORD FORMAT: UTIDFRR)
     D IOFEED        E DS                  EXTNAME(UTWSFR)
      *
      *---------------------------------------------------------------
      * STANDARD DATABASE FILE INFORMATION DATA STRUCTURE
      *---------------------------------------------------------------
      *    EXTERNALLY DEFINED AS UTDBFR (RECORD FORMAT: FDBCKD)
     D DBFEED        E DS                  EXTNAME(UTDBFR)
      *
      *---------------------------------------------------------------
      *  NAMED CONSTANTS
      *---------------------------------------------------------------
      *
     D YES             C                   CONST('Y')
     D NO              C                   CONST('N')
      *
     D                 DS
     D  DUMMYD                 1      1
      *
      *  THE PRECEDING DUMMY DATA STRUCTURE IS ONLY TO KEEP THE I-SPEC
      *  INFO FROM RUNNING OVER INTO THE 'CALC SPECS' ON THE COMPILE
      *  LISTING.  IT SERVES NO OTHER PURPOSE AND CAN BE DELETED
      *  AT ANY TIME!!!
      /EJECT
      ****************************************************************
      * MAIN LINE
      ****************************************************************
      *
      * CHAIN TO THE JDE ADDRESS BOOK TO VALIDATE THE JDE NUMBER.
      *
     C*    INAJD         CHAIN     F0101LA                            92
     c     inajd         chain     e10101la                           92
     C     *IN92         IFEQ      *ON                                          IF NOT FOUND
     C                   MOVEL     YES           JDEFL
     C     E             IFLT      MAXMSG
     C                   ADD       1             E
     C                   CLEAR                   MGW
     C                   MOVEL     INAJD         XXAJD
     C                   MOVEA     XXAJD         MGW(1)
     C                   MOVEA     MGW           MGDT(E)
     C                   MOVEL     'HS00135'     MGID(E)
     C                   ENDIF
     C                   ELSE
      *
      * VERIFY THE ADDRESS TYPE RETRIEVED AGAINST THE INCOMING TYPE.
      * IF THEY DON'T MATCH, MOVE THE ERROR MESSAGE TO THE ARRAY.
      *
     C     ABAT1         IFEQ      INAT1                                        IF SAME TYPE
     C                   MOVEL     ABALPH        OUALPH
     C                   ELSE
      *
     C                   MOVEL     YES           JDEFL
     C     E             IFLT      MAXMSG
     C                   ADD       1             E
     C                   CLEAR                   MGW
     C                   MOVEL     INAJD         XXAJD
     C                   MOVEA     XXAJD         MGW(1)
     C                   MOVEA     MGW           MGDT(E)
     C                   MOVEL     'HS00136'     MGID(E)
     C                   ENDIF
     C                   ENDIF                                                  IF SAME TYPE
     C                   ENDIF                                                  IF NOT FOUND
      *
     C                   SETON                                        LR
      /EJECT
      *---------------------------------------------------------------
      * *INZSR - INITIALIZATION SUBROUTINE
      *---------------------------------------------------------------
      *
     C     *INZSR        BEGSR
      *
      * PARM LISTS
      *
     C     *ENTRY        PLIST
     C                   PARM                    MGI                            MSG ID'S
     C                   PARM                    MGD                            MSG DATA FLD
     C                   PARM                    E                 2 0          # ERR MSGS
      * INCOMING PARMS
     C                   PARM                    INAJD                          ADDR # JDE
     C                   PARM                    INAT1                          ADDR # TYPE
      * OUTGOING PARMS
     C                   PARM                    JDEFL                          ERRORS      OR FLG
     C                   PARM                    OUALPH                         NAME
      *
      * THE FOLLOWING STANDARD CODE MUST BE INCLUDED TO MAKE THE
      * STANDARD ERROR MESSAGE HANDLING PROGRAM FUNCTION PROPERLY.
      * THIS CODE SETS AND CLEARS THE PROGRAM MESSAGE QUEUE.
      *
     C                   Z-ADD     20            MAXMSG            2 0
      *
      * SET UP PARM FIELDS
      *
     C                   MOVEL     NO            JDEFL             1
     C     *LIKE         DEFINE    ABAN8         INAJD
     C     *LIKE         DEFINE    ABALPH        OUALPH
     C                   MOVE      *BLANK        XXAJD             8
      *
     C     *LIKE         DEFINE    ABAT1         INAT1
      *
     C                   ENDSR
      /EJECT
