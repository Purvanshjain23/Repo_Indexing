     A*%%TS  SD  19950516  095408  ISLRAMS     REL-V3R0M5  5763-PW1
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD149
     A*  TITLE:         MAINTAIN REJECTED HEAD REASONS
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  CREATED:       05/15/95
     A*
     A*  INDICATOR 60 IS ON WHEN IN REVISE MODE
     A*  INDICATOR 61 IS ON WHEN IN CREATE MODE
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A                                      CF04(04 'PROMPT')
     A                                      CF05(05 'REFRESH')
     A N99                                  CF09(09 'ACCEPT')
     A                                      CA12(12 'PREVIOUS')
     A*
     A*---------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP149S1                   SFL
     A*%%TS  SD  19950516  095408  ISLRAMS     REL-V3R0M5  5763-PW1
     A  85                                  SFLNXTCHG
     A            HDRRN1         4Y 0H
     A            DFRJHD    R        B 13 24REFFLD(RJRJHD)
     A                                      EDTCDE(K)
     A  34                                  DSPATR(PC)
     A  34                                  DSPATR(RI)
     A N34                                  DSPATR(HI)
     A N34                                  DSPATR(UL)
     A            DFDRCD    R        B 13 34REFFLD(KDDRCD)
     A  37
     AO 57                                  DSPATR(PC)
     A  37                                  DSPATR(RI)
     A N37                                  DSPATR(HI)
     A N37                                  DSPATR(UL)
     A            DFDRDS    R        O 13 42REFFLD(DRDRDS)
     A*
     A*---------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP149C1                   SFLCTL(HP149S1)
     A                                      SETOF(31)
     A                                      SETOF(34)
     A                                      SETOF(37)
     A                                      SETOF(57)
     A                                      ROLLUP(25 'PAGE FORWARD')
     A                                      SFLSIZ(0100)
     A                                      SFLPAG(0009)
     A                                      SFLCSRRRN(&HDRR1)
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A                                      OVERLAY
     A                                      RTNCSRLOC(&RCD &FLD)
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            HDRR1          5S 0H
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 28'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A            DFMODE         6A  O  2  2DSPATR(HI)
     A                                  2 23'MAINTAIN REJECTED HEAD REASONS'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  4  2'Farm site .........:'
     A            C1FSCD    R        O  4 24REFFLD(FSFSCD)
     A                                      EDTCDE(M)
     A            C1FSNM    R        O  4 33REFFLD(FSFSNM)
     A                                  5  2'Hog group .........:'
     A            C1HGCD    R        O  5 24REFFLD(HGHGCD)
     A                                  6  2'Movement ..........:'
     A            C1MVSN    R        O  6 24REFFLD(RJMVSN)
     A                                      EDTCDE(M)
     A                                  7  2'Rejected head .....:'
     A            C1RJHD    R     Y  O  7 24REFFLD(RJRJHD)
     A                                      EDTCDE(K)
     A                                  9  2'Accumulated head ..:'
     A            C1HD      R     Y  O  9 24REFFLD(RJRJHD)
     A  31                                  DSPATR(RI)
     A                                      EDTCDE(K)
     A                                 10 36'?'
     A                                      DSPATR(HI)
     A                                 11 26'Head'
     A                                      DSPATR(HI)
     A                                 11 34'Reason'
     A                                      DSPATR(HI)
     A*
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R HP149K1
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A                                      DSPATR(HI)
     A                                 23  2'F3=Exit'
     A                                 23 11'F4=Prompt'
     A                                 23 22'F5=Refresh'
     A    N99                          23 34'F9=Accept'
     A                                 23 45'F12=Previous'
     A*
     A*---------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE FORMAT
     A*---------------------------------------------------------------
     A          R HP149ES                   SFL SFLMSGRCD(24)
     A*
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
     A*---------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*---------------------------------------------------------------
     A          R HP149EC                   SFLCTL(HP149ES)
     A*
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
