      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      HOG PRODUCTION
      * PROGRAM:     HP679
      * TITLE:       INVENTORY REPORT BY AGE OR BY WEEKS/DAYS IN PHASE
      * PROGRAMMER:  MARY PALMER
      * CREATED:     9/20/94
      *
      *  FUNCTION:   THIS PROGRAM PRINTS A SINGLE LINE REPORT
      *              OF THE INVENTORY COUNTS OF A HOG GROUP FOR A
      *              SPECIFIED CUT-OFF DATE. REPORT OPTIONS ARE LOADED
      *              INTO THE LDA IN SPECIFY PROGRAM HP469.
      *              HP669CL RUNS OPNQRY ON HOG GROUP FILE AND CALLS
      *              THIS PROGRAM. A WORKFILE IS BUILT IN HP369.
      *
      *              THIS PROGRAM CAN GENERATE EITHER THE:
      *                   INVENTORY REPORT BY AGE
      *                   INVENTORY REPORT BY WEEKS/DAYS IN PHASE
      *
      *              THE ONLY DIFFERENCE BETWEEN THE TWO REPORTS IS
      *              THE ONE 'BY AGE' PRINTS THE AGE OF THE GROUP
      *              IN THE FIRST COLUMN WHILE THE 'BY PHASE' PRINTS
      *              THE WEEKS AND DAYS IN THE PHASE IN THE FIRST
      *              COLUMN.  THE VALUES FOR THE COLUMN ARE BUILT IN
      *              THE WORKFILE PROGRAM.
      *
      *              THE ONLY LOGIC REQUIRED IS THIS PROGRAM IS TO
      *              SET AN INDICATOR TO PRINT THE APPROPRIATE REPORT
      *              TITLE AND COLUMN HEADINGS.
      *
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 12/20/94  MARY PALMER - BREAK TO A NEW PAGE ON A CHANGE IN PROD
      *                         PHASE.
      *
      * 01/20/95  LEANNE FEDOR
      *           ADDED THE WORD 'INVENTORY' TO THE COLUMN HEADING FOR
      *           ADJUSTMENTS.
      *
      * 02/07/95  LEANNE FEDOR
      *           ADDED THE DESCRIPTION FIELDS TO THE LDA TO ELIMINATE
      *           REDUNDANT RETRIEVAL IN THIS PRINT PROGRAM
      *
      * 04/24/95  LEANNE FEDOR
      *           CHANGED FARM SITE CODE FROM ALPHA TO NUMERIC.
      *
      * 07/17/95  LEANNE FEDOR
      *           CHANGED FOR MOVEMENT REWRITE.
      *
      * 08/30/95  LEANNE FEDOR
      *           USERS MAY NOW SELECT 'ALL' BUSINES OFFICES. CHANGED
      *           REPORT HEADINGS TO SAY 'ALL'.
      *
      * 03/22/96  LEANNE FEDOR
      *           THIS PROGRAM CAN NOT GENERATE TWO REPORTS:
      *                BY AGE
      *                BY WEEKS/DAYS IN PHASE
      *
      *              THE ONLY DIFFERENCE BETWEEN THE TWO REPORTS IS
      *              THE ONE 'BY AGE' PRINTS THE AGE OF THE GROUP
      *              IN THE FIRST COLUMN WHILE THE 'BY PHASE' PRINTS
      *              THE WEEKS AND DAYS IN THE PHASE IN THE FIRST
      *              COLUMN.  THE VALUES FOR THE COLUMN ARE BUILT IN
      *              THE WORKFILE PROGRAM.
      *
      *              THE ONLY LOGIC REQUIRED IS THIS PROGRAM IS TO
      *              SET AN INDICATOR TO PRINT THE APPROPRIATE REPORT
      *              TITLE AND COLUMN HEADINGS.
      *
      * 03/28/96  LEANNE FEDOR
      *           AS REQUESTED BY DAVE O'SULLIVAN, THE 'BY AGE' REPORT
      *           WILL NOT SORT OR BREAK ON PRODUCTION PHASE.  THE 'BY
      *           WEEKS/DAYS IN PHASE' REPORT WILL INCLUDE SORTING AND
      *           A BREAK ON PHASE.
      *
      * 07/19/96  LEANNE FEDOR
      *           ADDED 3 PERCENT FIELDS: CULL SALE, DEAD/DOA, MARKET
      *           SALES. ADDED FEED POUNDS PER HEAD TRANSFERRED IN AND
      *           AVERAGE POUNDS IN.
      *
      * 05/31/07  LeAnne Ramsey
      *           Some users are having printing problems. So, we are switching
      *           from QPRINT to PRINT198 for the printer file and replacing
      *           John Erikson's customized printer override command in the CL with
      *           the standard override.
      *
      * 10/16/13  LeAnne Ramsey (E2831)
      *           Recompile only. Added field 'MTech Reference'.
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     FHSP010    IF   E           K DISK
      *    PRODUCTION TYPE         (KEY: PTPTCD)
      *
     FHSP011    IF   E           K DISK
      *    PRODUCTION PHASE         (KEY: PPPPCD)
      *
     FHSP018    IF   E           K DISK
      *    FARM SITE                 (KEY: FSFSCD)
      *
     FHSL369A   IF   E           K DISK
      *    GROUP WORKFILE
      *    (KEY: WGPTCD WGPPCD WGWK WGDAY WGFSCD WGHGCD)
      *
     FHSL369B   IF   E           K DISK
     F                                     RENAME(WGREC:WGRECB)
      *    GROUP WORKFILE
      *    (KEY: WGPTCD WGWK WGDAY WGFSCD WGHGCD)
      *
     Fprint198  o    f  198        printer oflind(*inof)
      *
      /EJECT
      ****************************************************************
      * INPUT SPECS
      ****************************************************************
      *
      *---------------------------------------------------------------
      * STANDARD PROGRAM STATUS DATA STRUCTURE
      *---------------------------------------------------------------
      *    EXTERNALLY DEFINED AS UTPGFR (RECORD FORMAT: PGMDSR)
     D PGMDS         ESDS                  EXTNAME(UTPGFR)
      *
      *---------------------------------------------------------------
      *  NAMED CONSTANTS
      *---------------------------------------------------------------
      *
     D YES             C                   CONST('Y')
     D NO              C                   CONST('N')
     D UNKNWN          C                   CONST('UNKNOWN')
     D ALL             C                   CONST('ALL')
      *
      *----------------------------------------------------------------
      * LDA - REPORT SELECTIONS PASSED FROM SPECIFY HP469
      *----------------------------------------------------------------
      *
     D                UDS
     D  LDFSBO                 1      5
     D  LDCYMD                 6     13  0
     D  LDPTCD                14     18
     D  LDPPCD                19     23
     D  LDFSCD                24     28  0
     D  LDHGSN                29     35  0
     D  LDRPFL                36     36
     D  LDCMDY                37     42  0
     D  LDPBFL                43     43
     D  LDENFL                44     44
     D  LDCELL                45     46  0
     D  LDGTCD                47     47
      *
     D  LDBODS                72    101
     D  LDPTDS               102    111
     D  LDPPDS               112    141
     D  LDFSNM               142    166
     D  LDHGCD               167    173
     D  LDGTDS               174    188
      *
     D  LDOUTU               401    410
     D  LDHOLD               411    411
     D  LDCOPY               412    412
     D  LDOUTQ               413    422
      /EJECT
      ****************************************************************
      * MAIN LINE
      ****************************************************************
      *
      * PROCESS ALL RECORDS IN THE WORKFILE.  THE SORT
      * SEQUENCE FOR THE 'PHAGE' AND THE 'AGE' REPORT IS
      * DIFFERENT--SO READ THE APPROPRIATE LOGICAL FILE.
      *
     C     *IN90         DOUEQ     *ON                                          MAIN DO
      *
     C                   SELECT
     C     LDRPFL        WHENEQ    'P'
     C                   READ      HSL369A                                90
     C     LDRPFL        WHENEQ    'A'
     C                   READ      HSL369B                                90
     C                   ENDSL
      *
     C     *IN90         IFEQ      *OFF                                         IF NOT EOF
      *
     C     FIRST         IFEQ      YES                                          IF FIRST
     C                   Z-ADD     WGWK          SVWK
     C                   MOVEL     NO            FIRST
     C                   ENDIF                                                  IF FIRST
      *
      * BREAK ON PRODUCTION TYPE
      *
     C     WGPTCD        IFNE      SVPTCD                                       IF NEW TYPE
     C                   EXSR      $PTDS
     C                   EXSR      $PPDS
     C                   EXCEPT    HRHDR
     C                   ENDIF                                                  IF NEW TYPE
      *
      * BREAK ON PRODUCTION PHASE (ONLY FOR THE REPORT BY PHASE)
      *
     C     LDRPFL        IFEQ      'P'                                          IF NEW PHASE
     C     WGPPCD        ANDNE     SVPPCD
     C                   EXSR      $PPDS
     C                   EXCEPT    HRHDR
     C                   ENDIF                                                  IF NEW PHASE
      *
      * PRINT A BLANK LINE WHEN THE WEEK CHANGES
      *
     C     WGWK          IFNE      SVWK
     C                   EXCEPT    BLANK
     C                   Z-ADD     WGWK          SVWK
     C                   ENDIF
      *
      * RETRIEVE FARM SITE NAME FOR PRINTING ON DETAIL LINE
      *
     C     WGFSCD        IFNE      SVFSCD
     C                   EXSR      $FSNM
     C                   ENDIF
      *
      *
      * CALCULATE CULL PERCENT AND DEAD PERCENT
      *
     C     WGISHD        ADD       WGIDHD        WKHD
     C     WKHD          IFNE      0
     C     WGCUHD        DIV       WKHD          WKPR
     C     WKPR          MULT(H)   100           DTCUPR
     C     WGDDHD        DIV(H)    WKHD          WKPR
     C     WKPR          MULT(H)   100           DTDDPR
     C     WGSLHD        DIV(H)    WKHD          WKPR
     C     WKPR          MULT(H)   100           DTSLPR
     C                   ENDIF
      *
     C     *INOF         IFEQ      *ON
     C                   EXCEPT    HRHDR
     C                   ENDIF
     C                   EXCEPT    DTLINE
      *
     C                   ENDIF                                                  IF NOT EOF
     C                   ENDDO                                                  MAIN DO
      *
      *--------------------------------------------------------------
      * EOF PROCESSING
      *--------------------------------------------------------------
      *  IF NO DETAIL RECORDS MATCHED THE SELECTION CRITERIA, THEN
      *  PRINT THE STANDARD 'NO DATA...' ERROR MESSAGE
      *
     C     FIRST         IFEQ      YES                                          IF NO RECS
     C                   EXCEPT    HRHDR
     C                   EXCEPT    NODATA
     C                   ENDIF                                                  IF NO RECS
      *
     C                   SETON                                        LR
      /EJECT
      *****************************************************************
      * $FSNM - RETRIEVE THE FARM SITE NAME
      *****************************************************************
      *
     C     $FSNM         BEGSR
      *
     C     WGFSCD        CHAIN     HSP018                             91
     C     *IN91         IFEQ      *OFF                                         IF FOUND
     C                   MOVEL     FSFSNM        DTFSNM
     C                   ELSE
     C                   MOVEL(P)  UNKNWN        DTFSNM
     C                   ENDIF                                                  IF FOUND
      *
     C                   Z-ADD     WGFSCD        SVFSCD
      *
     C                   ENDSR
      /EJECT
      *****************************************************************
      * $PTDS - RETRIEVE THE PRODUCTION TYPE DESCRIPTION
      *****************************************************************
      *
     C     $PTDS         BEGSR
      *
     C     WGPTCD        CHAIN     HSP010                             91
     C     *IN91         IFEQ      *OFF                                         IF FOUND
     C                   MOVEL     PTPTDS        HRPTDS
     C                   ELSE
     C                   MOVEL(P)  UNKNWN        HRPTDS
     C                   ENDIF                                                  IF FOUND
      *
     C                   MOVEL     WGPTCD        SVPTCD
      *
     C                   ENDSR
      /EJECT
      *****************************************************************
      * $PPDS - RETRIEVE THE PRODUCTION PHASE DESCRIPTION
      *****************************************************************
      *
     C     $PPDS         BEGSR
      *
     C     WGPPCD        CHAIN     HSP011                             91
     C     *IN91         IFEQ      *OFF                                         IF FOUND
     C                   MOVEL     PPPPDS        HRPPDS
     C                   ELSE
     C                   MOVEL(P)  UNKNWN        HRPPDS
     C                   ENDIF                                                  IF FOUND
      *
     C                   MOVEL     WGPPCD        SVPPCD
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * *INZSR - INITIALIZATION SUBROUTINE
      *---------------------------------------------------------------
      *
     C     *INZSR        BEGSR
      *
      * REPORT PRINT FIELDS FOR REPORT HEADING LINE
      *
     C     *LIKE         DEFINE    PTPTDS        HRPTDS
     C     *LIKE         DEFINE    PPPPDS        HRPPDS
     C                   MOVE      *BLANK        HRGTCD            3
      *
      * DEFINE PRINT FIELDS FOR DETAIL LINE
      *
     C                   MOVE      *BLANK        DTFSNM           18
     C                   Z-ADD     0             DTCUPR            5 2
     C                   Z-ADD     0             DTDDPR            5 2
     C                   Z-ADD     0             DTSLPR            5 2
      *
      *
      * WORKFIELDS
     C                   Z-ADD     0             WKHD             15 0
     C                   Z-ADD     0             WKPR             15 7
      *
      * DEFINE SAVE FIELDS FOR CONTROL BREAKS
      *
     C     *LIKE         DEFINE    WGWK          SVWK
     C     *LIKE         DEFINE    WGFSCD        SVFSCD
     C     *LIKE         DEFINE    WGPPCD        SVPPCD
     C     *LIKE         DEFINE    WGPTCD        SVPTCD
      *
      * SET UP REPORT HEADINGS
      *
      * 'BY AGE' REPORT
      *
     C     LDRPFL        IFEQ      'A'
     C                   SETON                                        87
     C                   ENDIF
      * BUSINESS OFFICE
      *
     C     LDFSBO        IFEQ      *BLANK
     C                   MOVEL(P)  ALL           LDFSBO
     C                   ENDIF
      * PRODUCTION TYPE
      *
     C     LDPTCD        IFEQ      *BLANK
     C                   MOVEL(P)  ALL           LDPTCD
     C                   ENDIF
      *
      * PRODUCTION PHASE
      *
     C     LDPPCD        IFEQ      *BLANK
     C                   MOVEL(P)  ALL           LDPPCD
     C                   ENDIF
      *
      *
      * ALL GROUP TYPES
      *
     C     LDGTCD        IFEQ      *BLANK
     C                   MOVEL(P)  ALL           HRGTCD
     C                   ELSE
     C                   MOVEL     LDGTCD        HRGTCD
     C                   ENDIF
      * ALL CELLS
     C     LDCELL        IFEQ      0
     C                   SETON                                        88
     C                   ENDIF
      *
      * ALL FARMS
      *
     C     LDFSCD        IFEQ      0
     C                   SETON                                        89
     C                   ENDIF
      *
      * ALL GROUPS
      *
     C     LDHGCD        IFEQ      *BLANK
     C                   MOVEL     ALL           LDHGCD
     C                   ENDIF
      *
      * SET UP A FIRST TIME FLAG
      * SET UP HEADING UNDERLINE FIELD FOR PRINT
      * RETRIEVE TIME FOR REPORT HEADING
      *
     C                   MOVEL     YES           FIRST             1
     C                   MOVEL     *ALL'-'       DASH            198
     C                   TIME                    RTIME             6 0
      *
     C                   ENDSR
      /EJECT
      *-------------------------------------------------------------
      * REPORT HEADING LINES
      *-------------------------------------------------------------
      *
     OPRINT198  E            HRHDR          1 01
     O                       SDPGM               10
     O                                          109 'HOG PRODUCTION SYSTEM'
     O                                          189 'DATE'
     O                       UDATE         Y    198
      *
     O          E            HRHDR          1
     O                       SDUSR               10
     O               87                         110 'INVENTORY REPORT BY AGE'
     O              N87                         100 'INVENTORY REPORT BY'
     O              N87                        +  1 'WEEKS/DAYS IN PHASE'
     O                                          189 'TIME'
     O                       RTIME              198 '  :  :  '
      *
     O          E            HRHDR          1
     O                                          189 'PAGE'
     O                       PAGE          Z    198
      *
     O          E            HRHDR          1
     O                                           16 'BUSINESS OFFICE '
     O                                           22 '.....'
     O                       LDFSBO              29
     O                       LDBODS              63
      *
     O          E            HRHDR          1
     O                                           13 'CUT-OFF DATE '
     O                                           22 '........'
     O                       LDCMDY        Y     32
      *
     O          E            HRHDR          1
     O                                           22 'PRODUCTION TYPE ......'
     O                       LDPTCD              29
     O                       LDPTDS              43
      *
     O          E            HRHDR          1
     O                                           22 'PRODUCTION PHASE .....'
     O                       LDPPCD              29
     O                       LDPPDS              63
      *
     O          E            HRHDR          1
     O                                           22 'GROUP TYPE ...........'
     O                       HRGTCD              27
     O                       LDGTDS              48
      *
     O          E            HRHDR          1
     O                                           22 'CELL .................'
     O              N88      LDCELL        M     27
     O               88                          27 'ALL'
      *
      *
     O          E            HRHDR          1
     O                                           22 'FARM SITE ............'
     O              N89      LDFSCD        M     30
     O               89                          27 'ALL'
     O              N89      LDFSNM              58
      *
     O          E            HRHDR          2
     O                                           22 'HOG GROUP ............'
     O                       LDHGCD              31
      *
      *-------------------------------------------------------------
      * PRODUCTION TYPE / PRODUCTION PHASE
      *-------------------------------------------------------------
      *
     O          E            HRHDR          2
     O                       HRPTDS              15
     O              N87                          17 '-'
     O              N87      HRPPDS              48
      *
      *-------------------------------------------------------------
      * COLUMN HEADING LINES
      *-------------------------------------------------------------
      *
     O          E            HRHDR          1
     O                                           88 'TRANSFER IN HEAD'
     O                                           95 'AVG'
     O                                          104 'FEED'
     O                                          137 'DAILY DEAD'
     O                                          158 'TRANSFER OUT HEAD'
      *
     O          E            HRHDR          1
     O                                            7 'WK-DAYS'
     O                                           13 'HOG'
     O                                           24 'GROUP'
     O                                           31 'FARM'
     O                                           68 'GEN'
      *
     O                                           79 'WITHIN'
     O                                           87 'PRIOR'
     O                                           95 'LBS'
     O                                          105 'LBS PER'
     O                                          121 'CULLS SOLD'
     O                                          135 'AND DOA'
     O                                          148 'WITHIN'
     O                                          157 'NEXT'
     O                                          174 'MARKET SALES'
     O                                          187 'INVENTORY'
      *
     O          E            HRHDR          1
     O               87                           5 'AGE'
     O              N87                           8 'IN PHASE'
     O                                           15 'GROUP'
     O                                           23 'TYPE'
     O                                           31 'SITE'
     O                                           42 'FARM NAME'
     O                                           57 'BLDG'
     O                                           63 'ROOM'
     O                                           69 'LINE'
     O                                           79 'PHASE'
     O                                           87 'PHASE'
      *
     O                                           95 'IN'
     O                                          105 'HEAD IN'
     O                                          113 'HEAD'
     O                                          122 'PERCENT'
     O                                          130 'HEAD'
     O                                          139 'PERCENT'
     O                                          148 'PHASE'
     O                                          157 'PHASE'
     O                                          166 'HEAD'
     O                                          175 'PERCENT'
     O                                          187 'ADJUSTMENT'
     O                                          198 'INVENTORY'
      *
     O          E            HRHDR          1
     O                       DASH               198
      *
      *-------------------------------------------------------------
      * DETAIL LINE
      *-------------------------------------------------------------
      *
     O          E            DTLINE         1
     O                       WGWK          ZB     2
     O                                            4 '-'
     O                       WGDAY         ZB     6
     O                       WGHGCD         B    17
     O                       WGGTCD         B    22
     O                       WGFSCD        MB    32
     O                       DTFSNM              51
     O                       WGBLCD         B    58
     O                       WGRMCD         B    64
     O                       WGGNCD         B    70
      *
     O                       WGISHD        KB    80
     O                       WGIDHD        KB    88
      *
     O                       WGIALB        KB    96
     O                       WGFALB        KB   106
      *
     O                       WGCUHD        KB   114
     O                       DTCUPR        KB   123
     O                       WGDDHD        KB   131
     O                       DTDDPR        KB   139
      *
     O                       WGOSHD        KB   149
     O                       WGODHD        KB   158
      *
     O                       WGSLHD        KB   167
     O                       DTSLPR        KB   176
     O                       WGAJHD        KB   186
     O                       WGEIHD        KB   198
      *
      *-------------------------------------------------------------
      * PRINT A BLANK LINE
      *-------------------------------------------------------------
      *
     O          E            BLANK          1
      *
      *-------------------------------------------------------------
      * NO DATA MESSAGE LINE
      *-------------------------------------------------------------
      *
     O          E            NODATA         1
     O                                           19 'NO DATA MEETS YOUR'
     O                                           38 'SELECTION CRITERIA'
