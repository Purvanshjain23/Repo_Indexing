/*********************************************************************/
/*     Program: PMVSUPC                                              */
/*       Title: Prompt for printer overrides, then submit report.    */
/*        Date: 06/16/2008                                           */
/* Description: - Calls print options prompt program                 */
/*              - Submits CUSTOMER PRICE CODE REPORTS                */
/*              - PRT CUST PRICE CODES   CL                          */
/*                                                                   */
/*  Parameters:                                                      */
/*  FIELD       FIELD DESCRIPTION                                    */
/*  ---------   ------------------------------------------           */
/*  &RETURN     RETURN CODE                                          */
/*  &APPLIC     APPLICATION CODE                                     */
/*  &OUTQ       OUTPUT QUEUE                                         */
/*  &HOLD       HOLD YES OR NO (YES FOR EMAILS)                      */
/*  &SAVE       SAVE YES OR NO (YES FOR EMAILS)                      */
/*  &NQSTAT     SUBMIT TO NIGHT QUEUE YES OR NO                      */
/*  &NIGHTQ     WHICH NIGHT QUEUE IF YES                             */
/*  &COPIES     NUMBER OF COPIES (DEFAULTS TO 1 INITIALLY)           */
/*  &CUSTN      CUSTOMER NUMBER 15.5                                 */
/*  &FDATEN     START DATE 15.5                                      */
/*  &TDATEN     END DATE 15.5                                        */
/*  &PRCDE      PRICE CODE 15.5                                      */
/*  &USER       ENTERED BY USER PROFILE                              */
/*  &SELECT     SELECTED REPORT TYPE                                 */
/*********************************************************************/
/* Modifications:                                                    */
/*   Date     Pgmr     Description                                   */
/* -------- ---------- --------------------------------------------  */
/*                                                                   */
/*********************************************************************/
 PMVSUPC:    PGM        PARM(&RETURN &APPLIC &OUTQ &HOLD &SAVE +
                          &NQSTAT &NIGHTQ &CUSTN &FDATEN &TDATEN +
                          &COPYN &FPRCDEN &USER &SELECT &TPRCDEN)

             DCL  VAR(&RETURN)  TYPE(*CHAR) LEN(7)
             DCL  VAR(&APPLIC)  TYPE(*CHAR) LEN(6)
             DCL  VAR(&DESC)    TYPE(*CHAR) LEN(25)
             DCL  VAR(&OUTQ)    TYPE(*CHAR) LEN(10)
             DCL  VAR(&HOLD)    TYPE(*CHAR) LEN(4)
             DCL  VAR(&SAVE)    TYPE(*CHAR) LEN(4)
             DCL  VAR(&COPYN)   TYPE(*DEC)
             DCL  VAR(&COPY)    TYPE(*DEC)  LEN(2 0)
             DCL  VAR(&NQSTAT)  TYPE(*CHAR) LEN(01)
             DCL  VAR(&NIGHTQ)  TYPE(*CHAR) LEN(10)
             DCL  VAR(&CUSTN)   TYPE(*DEC)
             DCL  VAR(&FDATEN)  TYPE(*DEC)
             DCL  VAR(&TDATEN)  TYPE(*DEC)
             DCL  VAR(&FPRCDEN) TYPE(*DEC)
             DCL  VAR(&TPRCDEN) TYPE(*DEC)
             DCL  VAR(&USER)    TYPE(*CHAR) LEN(10)
             DCL  VAR(&SELECT)  TYPE(*CHAR) LEN(1)

             DCL  VAR(&MSG)     TYPE(*CHAR) LEN(512)
             DCL  VAR(&MSGID)   TYPE(*CHAR) LEN(7)
             DCL  VAR(&MSGL)    TYPE(*DEC)  LEN(5 0)

/*-------------------------------------------------------------------*/
/* Monitor for all message at the program level                      */
/*-------------------------------------------------------------------*/
             MONMSG MSGID(CPF0000) EXEC(GOTO CMDLBL(MSGTRAP))

/*-------------------------------------------------------------------*/
/* Call Print Options Prompt Program                                 */
/*-------------------------------------------------------------------*/
             CHGVAR     VAR(&RETURN) VALUE(' ')
             CHGVAR     VAR(&DESC) VALUE('Customer Price Code Reports')
             CHGVAR     VAR(&COPY) VALUE(&COPYN)

/* Call print option prompt                                          */
             CALL       PGM(PDG0PVR) PARM(&RETURN &DESC &OUTQ &HOLD +
                          &SAVE &COPY &NQSTAT &NIGHTQ &APPLIC)

/* Convert number of copies & handle return code                     */
             IF         COND(&RETURN *NE ' ') THEN(GOTO +
                          CMDLBL(USERQUIT))

             CHGVAR     VAR(&COPYN) VALUE(&COPY)
/*-------------------------------------------------------------------*/
/* SUBMIT THE PRINT CL PROGRAM - SBM CUST PRICE CODES   CL           */
/*-------------------------------------------------------------------*/
             SBMJOB     CMD(CALL PGM(PMVTUPC) PARM(&RETURN &APPLIC +
                          &OUTQ &HOLD &SAVE &NQSTAT &NIGHTQ &CUSTN +
                          &FDATEN &TDATEN &COPYN &FPRCDEN &USER +
                          &SELECT &TPRCDEN)) JOB(CUSTPRICE)

             GOTO       CMDLBL(END)

/*-------------------------------------------------------------------*/
/* User Quit                                                         */
/*-------------------------------------------------------------------*/
 USERQUIT:   CHGVAR     VAR(&RETURN) VALUE('Y2U9999')
             GOTO       CMDLBL(END)

/*-------------------------------------------------------------------*/
/* Trap errors and send message                                      */
/*-------------------------------------------------------------------*/
 MSGTRAP:    RCVMSG     MSGTYPE(*EXCP) MSGDTA(&MSG) MSGDTALEN(&MSGL) +
                          MSGID(&MSGID)
             MONMSG MSGID(CPF0000) EXEC(GOTO CMDLBL(END))
             SNDPGMMSG MSGID(&MSGID) MSGF(QCPFMSG) +
                       MSGDTA(%SST(&MSG 1 &MSGL)) +
                       MSGTYPE(*ESCAPE)
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(END))

/*-------------------------------------------------------------------*/
/* End of Program                                                    */
/*-------------------------------------------------------------------*/
 END:        ENDPGM
