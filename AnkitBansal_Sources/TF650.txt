      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Triumph Foods
      * PROGRAM:     TF650
      * TITLE:       Kill/Cut Data Listing
      * PROGRAMMER:  LeAnne Ramsey
      * CREATED:     03/27/07
      *
      * FUNCTION:  Batch program to print a record(s) in the Kill/Cut Data file.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 12/01/08  LeAnne Ramsey
      *           File TFP050 was renamed from Cold Carcass Pounds to Kill/Cut Data.
      *           Fields were added to the file and the 2-letter prefix was changed
      *           from "CP" to "KC". The Head fields were increased from 5,0 to 7,0.
      *
      * 11/09/17  Danny Nguyen    - R12011-Kill Cut Data for STF
      *           File TFP050 was changed to add 4 fields for STF.
      *           Replace PRINT132 with PRINT198 to support printing of the 4 fields.
      *
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Ftfp050    if   e           k disk
      *  Kill/cut data   (records selected by open query)
      *
      *
12011F**print132  o    f  132        printer oflind(*inof)
12011Fprint198  o    f  186        printer oflind(*inof)
      *
      /EJECT
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Standard fields
      *
12011D**dash            s            132    inz(*all'-')
12011D dash            s            185    inz(*all'-')
     d rtime           s              6  0
      *
      *
      * Control fields
      *
     D first           s              1    inz('Y')
      *
      *
      * Workfields for date manipulation
      *
     D wkisodate       s               D   datfmt(*iso)
      *
      *
      * Print fields
      *
     D l1wemdy         s              6  0
      *
      ****************************************************************
      * ARRAYS AND TABLES
      ****************************************************************
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *
      *---------------------------------------------------------------
      * Local data area.
      *---------------------------------------------------------------
      *
     Dlda             uds                  dtaara(*lda)
     D  ldfwedt                1      8  0
     D  ldfwemdy               9     14  0
      *
     D  ldtwedt               15     22  0
     D  ldtwemdy              23     28  0
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ***************************************************************************************
      * MAINLINE
      ***************************************************************************************
      *
      * Print headings
     C                   exsr      $h1hdr
      *
      * Read/print each record selected by the open query.
      *
     C                   dou       *in90 = *on                                  Main do
     C                   read      tfp050                                 90
     C                   if        *in90 = *off                                 If not EOF
     C                   move      no            first
      * Print record
     C                   exsr      $l1dtl
     C                   endif                                                  If not EOF
     C                   enddo                                                  Main do
      *
      *---------------------------------------------------------------
      * EOF processing
      *---------------------------------------------------------------
      * If the file was empty, print the standard 'no data...' message
      *
     C                   if        first = yes
     C                   except    nodata
     C                   endif
      *
     C                   seton                                        lr
      /EJECT
      *---------------------------------------------------------------
      * Print detail line
      *---------------------------------------------------------------
      *
     C     $l1dtl        begsr
      *
     C                   if        *inof = *on
     C                   exsr      $h1hdr
     C                   endif
      *
      * Flip Week-ending Date to MMDDYY format.
      *
     C     *iso          test(d)                 kcwedt                 92
     C                   if        *in92 = *off                                 If OK date
     C                   move      kcwedt        wkisodate
     C     *mdy          move      wkisodate     l1wemdy
     C                   endif                                                  If OK date
      *
     C                   except    l1dtl
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Print report heading
      *---------------------------------------------------------------
      *
     C     $h1hdr        begsr
      *
     C                   except    h1hdr
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Retrieve time for report heading.
      *
     C                   time                    rtime
      *
     C                   endsr
      /EJECT
      *-------------------------------------------------------------
      * Report heading lines
      *-------------------------------------------------------------
      *
12011O**print132  e            h1hdr          1 01
12011Oprint198  e            h1hdr          1 01
     O                       sdpgm               10
     O                                           84 'KILL/CUT DATA LISTING'
12011O**                                        122 'DATE'
  |  O                                          175 'DATE'
  |  O**                     udate         y    132
12011O                       udate         y    185
      *
     O          e            h1hdr          1
     O                       sdusr               10
12011O**                                        122 'TIME'
  |  O                                          175 'TIME'
  |  O**                     rtime              132 '  :  :  '
12011O                       rtime              185 '  :  :  '
      *
      *
     O          e            h1hdr          1
12011O**                                        122 'PAGE'
  |  O                                          175 'PAGE'
  |  O**                     page          z    132
12011O                       page          z    185
      *
     O          e            h1hdr          1
     O                                           22 'From week-ending date:'
     O                       ldfwemdy            32 '  /  /  '
      *
      *
     O          e            h1hdr          2
     O                                           22 'To week-ending date:'
     O                       ldtwemdy            32 '  /  /  '
      *
      *
      *-------------------------------------------------------------
      * Column headings
      *-------------------------------------------------------------
      *
     O          e            h1hdr       0  1
     O                                            6 'Week'
     O                                           34 'SBF'
     O                                           50 'SBF'
     O                                           61 'SBF'
     O                                           77 'SBF'
      *
     O                                           88 'TF'
     O                                          104 'TF'
     O                                          115 'TF'
     O                                          131 'TF'
      *
12011O                                          142 'STF'
  |  O                                          158 'STF'
  |  O                                          169 'STF'
12011O                                          185 'STF'
      *
     O          e            h1hdr       0  1
     O                                            7 'Ending'
     O                                           34 'Killed'
     O                                           50 'Hot Weight'
     O                                           61 'Cut'
     O                                           77 'Cold Carcass'
      *
     O                                           88 'Killed'
     O                                          104 'Hot Weight'
     O                                          115 'Cut'
     O                                          131 'Cold Carcass'
      *
12011O                                          142 'Killed'
  |  O                                          158 'Hot Weight'
  |  O                                          169 'Cut'
12011O                                          185 'Cold Carcass'
      *
     O          e            h1hdr       0  1
     O                                            6 'Date'
     O                                           15 'Year'
     O                                           22 'Week'
      *
     O                                           34 'Head'
     O                                           50 'Pounds'
     O                                           61 'Head'
     O                                           77 'Pounds'
      *
     O                                           88 'Head'
     O                                          104 'Pounds'
     O                                          115 'Head'
     O                                          131 'Pounds'
      *
12011O                                          142 'Head'
  |  O                                          158 'Pounds'
  |  O                                          169 'Head'
12011O                                          185 'Pounds'
      *
     O          e            h1hdr          1
12011O**                     dash               132
12011O                       dash               185
      *
      *
      *-------------------------------------------------------------
      * Detail line
      *-------------------------------------------------------------
      *
     O          e            l1dtl          1
     O                       l1wemdy              8 '  /  /  '
     O                       kcyr          m     16
     O                       kcwk          m     22
      *
     O                       kcsklhd       k     35
     O                       kcshclb       k     51
     O                       kcscthd       k     62
     O                       kcscclb       k     78
      *
     O                       kctklhd       k     89
     O                       kcthclb       k    105
     O                       kctcthd       k    116
     O                       kctcclb       k    132
      *
12011O                       kcxklhd       k    143
  |  O                       kcxhclb       k    159
  |  O                       kcxcthd       k    170
12011O                       kcxcclb       k    186
      *
      *-------------------------------------------------------------
      * No data message line
      *-------------------------------------------------------------
      *
     O          e            nodata      1
     O                                           19 'No data meets your'
     O                                           39 'selection criteria.'
