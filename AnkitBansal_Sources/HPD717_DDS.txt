     A*%%TS  SD  19991104  085135  ISLRAMS     REL-V4R2M0  5769-PW1
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION
     A*  DISPLAY FILE:  HPD717
     A*  TITLE:         VIEW LAST UPDATE INFORMATION FOR MOVEMENTS
     A*  CREATED BY:    LEANNE FEDOR
     A*  DATE:          11/04/99
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A*
     A*----------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*----------------------------------------------------------------
     A          R HP717S1                   SFL
     A*%%TS  SD  19991104  085135  ISLRAMS     REL-V4R2M0  5769-PW1
     A  85                                  SFLNXTCHG
     A            HDRRN1         4Y 0H
     A            DFMVSN    R        O  9  2REFFLD(@@MVSN)
     A                                      EDTCDE(Z)
     A            DFMTCD    R        O  9 13REFFLD(@@MTCD)
     A            DFUPDTMDY R     Y  O  9 17REFFLD(@@DT06)
     A                                      EDTCDE(Y)
     A            DFUPTM    R     Y  O  9 27REFFLD(@@TM04)
     A                                      EDTWRD('  :  ')
     A            DFUSCD    R        O  9 34REFFLD(@@USCD)
     A            DFUSNM        35A  O  9 45
     A                                 10 23'Function:'
     A            DFPGM     R        O 10 34REFFLD(MEREC/MEPGM *LIBL/HSP058)
     A            DFFNDS    R        O 10 45REFFLD(FNREC/FNFNDS *LIBL/HSP002)
     A*
     A*----------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*----------------------------------------------------------------
     A          R HP717C1                   SFLCTL(HP717S1)
     A*%%TS  SD  19991104  085135  ISLRAMS     REL-V4R2M0  5769-PW1
     A                                      SFLSIZ(0050)
     A                                      SFLPAG(0006)
     A                                      CF03(03 'EXIT')
     A N99                                  CF04(04 'PROMPT')
     A N99                                  CF05(05 'REFRESH')
     A                                      CF12(12 'PREVIOUS')
     A N99                                  CF13(13 'FOLD/DROP')
     A N82N99                               ROLLUP(25 'PAGE FORWARD')
     A                                      SETOF(51)
     A                                      SETOF(54)
     A                                      SETOF(55)
     A                                      RTNCSRLOC(&RCD &FLD)
     A                                      OVERLAY
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A  88                                  SFLFOLD(CF13)
     A  89                                  SFLDROP(CF13)
     A                                      SFLMODE(&FOLD)
     A            FOLD           1A  H
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            RCD           10A  H
     A            FLD           10A  H
     A                                  1  2'HP717'
     A                                      DSPATR(HI)
     A                                  1 26'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  2 16'VIEW LAST UPDATE INFORMATION FOR M-
     A                                      OVEMENTS'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A N99                              4 13'?'
     A                                      DSPATR(HI)
     A N99                              4 38'?'
     A                                      DSPATR(HI)
     A                                  5 17'------- Last Update -------'
     A                                      DSPATR(HI)
     A                                  6  2'Movemnt'
     A                                      DSPATR(HI)
     A                                  6 11'Type'
     A                                      DSPATR(HI)
     A                                  6 19'Date'
     A                                      DSPATR(HI)
     A                                  6 27'Time'
     A                                      DSPATR(HI)
     A                                  6 37'User'
     A                                      DSPATR(HI)
     A            C1MVSN    R        B  7  2REFFLD(@@MVSN)
     A                                      EDTCDE(Z)
     A                                      DSPATR(HI)
     A  99                                  DSPATR(ND)
     A  99                                  DSPATR(PR)
     A            C1MTCD    R        B  7 13REFFLD(@@MTCD)
     A                                      DSPATR(HI)
     A  51                                  DSPATR(PC)
     A  99                                  DSPATR(ND)
     A  99                                  DSPATR(PR)
     A            C1UPDTMDY R     Y  B  7 17REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  ')
     A                                      DSPATR(HI)
     A  99                                  DSPATR(ND)
     A  99                                  DSPATR(PR)
     A            C1USCD    R        B  7 34REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A  55                                  DSPATR(PC)
     A  99                                  DSPATR(ND)
     A  99                                  DSPATR(PR)
     A*
     A*
     A*----------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*----------------------------------------------------------------
     A          R HP717K1
     A*%%TS  SD  19991104  085135  ISLRAMS     REL-V4R2M0  5769-PW1
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  1'F3=Exit'
     A N99                             23 10'F4=Prompt'
     A N99                             23 21'F5=Refresh'
     A                                 23 33'F12=Previous'
     A N99                             23 47'F13=Fold/Unfold Function'
      *
      *----------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *----------------------------------------------------------------
     A          R HP717ES                   SFL SFLMSGRCD(24)
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
      *----------------------------------------------------------------
      * ERROR MESSAGE SUBFILE CONTROL FORMAT
      *----------------------------------------------------------------
     A          R HP717EC                   SFLCTL(HP717ES)
      *
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
