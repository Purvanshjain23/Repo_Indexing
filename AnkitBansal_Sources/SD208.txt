      *****************  RPG PROGRAM HEADING  ************************
     h option(*SRCSTMT:*NODEBUGIO)
      *
      * ENVIRONMENT: Pork Division
      * SYSTEM:      Margin Management
      * PROGRAM:     SD208 - Signal Demand: Build Commodity Futures
      * PROGRAMMER:  LeAnne Ramsey
      * CREATED:     04/24/08
      *
      * NOTE: After the initial loads for Signal Demand, we will only send the
      *       "last 10 days" of data. If we need to resend all the data:
      *       1) reinstate the commented out code in the Initialization Subroutine
      *          so that we get all data from 2003 forward
      *       2) change the hardcoding of D=Delta to F=Full
      *       3) change the FTP CL (SD201CL) for SDP208/SPD209 to say "TOTAL" instead
      *          "DELTA". After the run, you must revert to the "10 day" code.)
      *
      **************************************************************************************
      * MODIFICATIONS:
      **************************************************************************************
      * Date      Programmer
      *
      /eject
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fomf0cpl5  if   e           k disk
      *   Commodity market price
      *
      *
     Fsdp208    uf a e           k disk
      *    Signal Demand: Commodity futures header
      *    (Signal Demand refers to this as: Commodity Futures Report)
      *
      *
     Fsdp209    o    e           k disk
      *    Signal Demand: Commodity futures detail
      *    (Signal Demand refers to this as: Commodity Futures Price)
      *
      /EJECT
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      *---------------------------------------------------------------
      *  Named Constants
      *---------------------------------------------------------------
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      *---------------------------------------------------------------
      *  Standalone field
      *---------------------------------------------------------------
      *
      * Control/save fields
      *
     D sveqdt          s                   like(f0eqdt)
      *
      *
      * Workfields
      *
     D wkmkcd          s                   like(f0lqcd)
     D wkmon           s              3
     D wkyy            s              2
     D wkccyy          s              4
      *
      *
      * Workfields for date manipulation
      *
     D wktime          s              6  0
     D wkisodate       s               d   datfmt(*iso)
     D wkdt            s              7  0
      *
      ****************************************************************
      * Data Structures
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ***************************************************************************************
      * Mainline
      ***************************************************************************************
      *
      * Set up key field
     C                   move      'CME'         wkmkcd
     C     key01         setll     omf0cpl5
      *
     C                   dou       *in90 = *on                                  Main do
     C     wkmkcd        reade     omf0cpl5                               90
     C                   if        *in90 = *off                                 If not EOF
      *
      * Write a "header" record when the Market Price Date changes.
      * FYI: Signal Demand calls this the Period Start Date.
      *
     C                   if        f0eqdt <> sveqdt
     C                   exsr      $wrt208
     C                   endif
      *
      * Write a Detail Record for each record that you read:
      *
      * Populate Report Instance Date with the value from the Header record.
      *
     C                   z-add     fhridt        fdridt
      *
      * Contract Price/Lb (aka: Market Price/lb)
      *
     C                   z-add     f0a5pr        fdconpr
      *
      * Extract Month from the Commodity Price Group field.
      *
     C                   exsr      $month
      *
      * Extract Year from Commodity Price Group field and concatenate with
      * Century to come up with a Contract Year in CCYY format.
      *
     C                   eval      wkyy = %subst(f0lrcd:4:2)
     C                   eval      wkccyy = '20' + wkyy
     C                   move      wkccyy        fdconyr
      *
      * Write detail rec
     C                   write     fdrec
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Main do
      *
     C                   seton                                        lr
      /EJECT
      *-----------------------------------------------------------------------------------
      * Hardcode a numeric month
      *-----------------------------------------------------------------------------------
      *
     C     $month        begsr
      *
      * Extract Month from Commodity Price Group field and hardcode to a number.
      * (Note: There are no edits on what Damon Ginther keys in positions 1-3
      *        of this field.)
      *
     C                   eval      wkmon = %subst(f0lrcd:1:3)
      *
     C                   select
     C                   when      wkmon = 'JAN'
     C                   z-add     1             fdconmon
      *
     C                   when      wkmon = 'FEB'
     C                   z-add     2             fdconmon
      *
     C                   when      wkmon = 'MAR'
     C                   z-add     3             fdconmon
      *
     C                   when      wkmon = 'APR'
     C                   z-add     4             fdconmon
      *
     C                   when      wkmon = 'MAY'
     C                   z-add     5             fdconmon
      *
     C                   when      wkmon = 'JUN'
     C                   z-add     6             fdconmon
      *
     C                   when      wkmon = 'JLY' or wkmon = 'JUL'
     C                   z-add     7             fdconmon
      *
     C                   when      wkmon = 'AUG'
     C                   z-add     8             fdconmon
      *
     C                   when      wkmon = 'SEP'
     C                   z-add     9             fdconmon
      *
     C                   when      wkmon = 'OCT'
     C                   z-add     10            fdconmon
      *
     C                   when      wkmon = 'NOV'
     C                   z-add     11            fdconmon
      *
     C                   when      wkmon = 'DEC'
     C                   z-add     12            fdconmon
     C                   other
      *
      * The month abbreviation did not fit any of the values above; so, put
      * it in a goofy "month" that, hopefully, someone will notice.
      *
     C                   z-add     13            fdconmon
     C                   endsl
      *
     C                   endsr
      /EJECT
      *-----------------------------------------------------------------------------------
      * Write Header
      *-----------------------------------------------------------------------------------
      *
      * All fields in the Header record are constants except the "date" fields.
      * Populate both 1) Period Start Date and 2) Report Instance Date with our
      * Market Price Date.  (The "constant" fields are populated once in the
      * Initialization subroutine.
      *
     C     $wrt208       begsr
      *
     C                   z-add     0             fhpsdt
     C                   z-add     0             fhridt
      *
     C                   z-add     f0eqdt        sveqdt
      *
     C     *cymd         test(d)                 f0eqdt                 92
     C                   if        *in92 = *off
     C     *cymd         move      f0eqdt        wkisodate
     C     *iso          move      wkisodate     fhpsdt
     C                   endif
      *
     C     1000000       mult      fhpsdt        fhridt
     C                   time                    wktime
     C                   add       wktime        fhridt
      *
     C                   write     fhrec
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Initialization
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Key lists
      *
     C     key01         klist
     C                   kfld                    wkmkcd
     C                   kfld                    wkdt
      *
      * Find the "date" that is 10 days prior to the System Date.
      * Put it into a Synon format (CYYMMDD)
      *
     C                   move      *date         wkisodate
     C     wkisodate     subdur    10:*d         wkisodate
     C     *cymd         move      wkisodate     wkdt
      *
      *
      * Set up fields that are constants on the header/detail records.
      *
     C                   move      'F'           fhldtype
     C                   move      'F'           fdldtype
      *
     C                   move      'seaboard'    fhxscd
     C                   move      'seaboard'    fdxscd
      *
     C                   move      'hog'         fhcmcd
     C                   move      'hog'         fdcmcd
      *
     C                   move      'cme'         fhrpcd
     C                   move      'cme'         fdrpcd
      *
      *
      * When/if we must send the entire file (instead of just the last 10 days),
      * reinstate the line of code below. (You must also change the FTP CL to say
      * "TOTAL" instead of "DELTA".) (If/when you are sending only the last 10 days,
      * you will need to comment out the line below.)
      *
     C                   z-add     1030101       wkdt
      *
     C                   endsr
      /EJECT
