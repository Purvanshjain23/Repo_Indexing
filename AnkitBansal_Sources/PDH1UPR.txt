/********************************************************************/
/*                                                                  */
/*  SYNOPSIS -  JOB CONTROL PROMPT FOR PRINTER OVERRIDE INFORMATION */
/*              PROMPTING FUNCTION                                  */
/*                                                                  */
/*  SYSTEM    - ORDER MANAGEMENT SYSTEM                             */
/*  PROGRAM   - SUBMIT PRINT LOADS NOT PICKED UP                    */
/*  DATE      - 03/03/95                                            */
/*                                                                  */
/*      PARAMETERS:                                                 */
/*                                                                  */
/*     O    &RETURN     - RETURN CODE                               */
/*     I    &APPLIC     - APPLICATION CODE                          */
/*     I    &PGM        - CL PROGRAM TO SUBMIT                      */
/*     B    &COMPNYN    - COMPANY NUMBER (PASSED)                   */
/*     B    &COPYN      -  NO. OF COPIES (PASSED)                   */
/*     I    &DESC       - PRINT PROGRAM DESCRIPTION (FOR PROMPT     */
/*                         SCREEN)                                  */
/*     B    &OUTQ       -  OUTPUT QUEUE                             */
/*                         IF THIS IS BLANK AT START, PROMPT        */
/*                         PGM IS CALLED; OTHERWISE, THE PRINT CL   */
/*                         IS SUBMITTED WITHOUT PROMPTING           */
/*     B    &HOLD       -  HOLD STATUS                              */
/*     B    &SAVE       -  SAVE STATUS                              */
/*     B    &NQSTAT     -  INDICATOR WHETHER MUST RUN IN NIGHT Q    */
/*     B    &NIGHTQ     -  NIGHT JOB QUEUE                          */
/*     I    &CARRIER    - CARRIER CODE                              */
/*     I    &EMAIL      - EMAIL ADDRESS                             */
/********************************************************************/
/* MODIFIED:                                                        */
/* 04/24/2015 LJB E004076 ADDED OPTIONAL EMAIL ADDRESS              */
/* 01/30/2025 SG  S0245656 PASSED EMLFMT PARAMETER TO PDUEPCLP      */
/********************************************************************/
 PDH1UPR:    PGM        PARM(&RETURN &APPLIC &PGM &COMPNYN &COPYN +
                          &DESC &OUTQ &HOLD &SAVE &NQSTAT &NIGHTQ +
                          &CARRIER &EMAIL)
/* PARMS                                                                      */
        DCL    &RETURN     *CHAR   (7)           /* Return Code               */
        DCL    &APPLIC     *CHAR   (06)          /* Application Code          */
        DCL    &PGM        *CHAR   (10)          /* CL to Submit              */
        DCL    &COMPNYN    *DEC                  /* Company Number (Passed)   */
        DCL    &DESC       *CHAR   (25)          /* PRT Pgm Description       */
        DCL    &COPYN      *DEC                  /* No. of Copies             */
        DCL    &OUTQ       *CHAR   (10)          /* Output Queue              */
        DCL    &HOLD       *CHAR   (4)           /* Hold Status               */
        DCL    &SAVE       *CHAR   (4)           /* Save Status               */
        DCL    &NQSTAT     *CHAR   (01)          /* If MUST run in Night Q    */
        DCL    &NIGHTQ     *CHAR   (10)          /* Night Job Queue           */
        DCL    &CARRIER    *CHAR   (3 )          /*                           */
        DCL    &EMAIL      *CHAR   (50)          /*                           */
        DCL    &EMAIL1     *CHAR   (25)          /*                           */
        DCL    &EMAIL2     *CHAR   (25)          /*                           */
        /* S0245656 - DECLARE EMLFMT FOR PDUEPCLP           */
        DCL    &EMLFMT     *CHAR   (5)           /*  EMAIL FORMAT             */

/* INTERNAL FIELDS                                                            */
        DCL    &COPY       *DEC    (2 0)         /* Number of copies          */

/* ERROR MESSAGE FIELDS                                                       */
        DCL    &MSG        *CHAR   (512)         /* message                   */
        DCL    &MSGID      *CHAR   (7)           /* message id                */
        DCL    &MSGL       *DEC    (5 0)

/********************************************************************/
/*   INITIALIZE VARIABLES                                           */
/********************************************************************/

             CHGVAR     VAR(&RETURN) VALUE('       ')
             CHGVAR     VAR(&COPY) VALUE(&COPYN)
             CHGVAR     VAR(&EMAIL1) VALUE(%SST(&EMAIL 1 25))
             CHGVAR     VAR(&EMAIL2) VALUE(%SST(&EMAIL 26 25))
/********************************************************************/
/*   MONITOR FOR ALL MESSAGES AT PROGRAM LEVEL                      */
/********************************************************************/
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(MSGTRAP))

/********************************************************************/
/*   CALL PROMPT PROGRAM                                            */
/********************************************************************/
             CALL       PGM(PDG0PVR) PARM(&RETURN &DESC &OUTQ &HOLD +
                          &SAVE &COPY &NQSTAT &NIGHTQ &APPLIC)
             CHGVAR     VAR(&COPYN) VALUE(&COPY)
             IF         COND(&RETURN *NE ' ') THEN(GOTO +
                          CMDLBL(USERQUIT))
/*****   GOTO CALL              TEST   ****/
/********************************************************************/
/*   SUBMIT PRINT CL PROGRAM TO PRINT LOADS NOT PICKED UP REPORT    */
/********************************************************************/
     /* S0245656 - PASS MISSING PARAMETER EMLFMT TO PDUEPCLP  */
 SUBMIT:     SBMJOB     CMD(CALL PGM(PDUEPCLP) PARM(&COMPNYN &OUTQ +
                          &HOLD &SAVE &COPYN &NIGHTQ &CARRIER +
                          &EMAIL1 &EMAIL2 &EMLFMT)) JOB(LDNOTPKDUP)
      /* S0245656         &EMAIL1 &EMAIL2)) JOB(LDNOTPKDUP)     */
             GOTO       CMDLBL(END)

     /* S0245656 - PASS MISSING PARAMETER EMLFMT TO PDUEPCLP  */
 CALL:       CALL       PGM(PDUEPCLP) PARM(&COMPNYN &OUTQ &HOLD +
                          &SAVE &COPYN &NIGHTQ &CARRIER &EMAIL1 +
                          &EMAIL2 &EMLFMT)
      /* S0245656         &EMAIL2)                              */
             GOTO       CMDLBL(END)

/*-------------------------USER QUIT--------------------------------*/
 USERQUIT:   CHGVAR     VAR(&RETURN) VALUE('Y2U0005')
             GOTO       CMDLBL(END)

/*----------------------RECEIVE MESSAGES----------------------------*/
 MSGTRAP:    RCVMSG     MSGTYPE(*EXCP) MSGDTA(&MSG) MSGDTALEN(&MSGL) +
                          MSGID(&MSGID)
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(END))

             SNDPGMMSG  MSGID(&MSGID) MSGF(QCPFMSG) MSGDTA(%SST(&MSG +
                          1 &MSGL)) MSGTYPE(*ESCAPE)
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(END))

 END:        ENDPGM
