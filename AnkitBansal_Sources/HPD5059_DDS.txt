     A*%%TS  SD  20060817  164415  ISLRAMS     REL-V5R3M0  5722-WDS
     A*****************************************************************
     A*
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD5059 SELECT BINS ON A FARM
     A*  CREATED BY:    LEANNE RAMSEY
     A*  DATE:          08/17/06
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      PRINT
     A                                      CF12(12 'PREVIOUS')
     A N82                                  ROLLUP(25 'PAGE FORWARD')
     A*---------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP5059S1                  SFL
     A*%%TS  SD  20060817  163525  ISLRAMS     REL-V5R3M0  5722-WDS
     A            HDRRN1    R        H      REFFLD(@@RRN)
     A            D1BNCD    R        O  9  1REFFLD(@@BNCD)
     A            D1RNCD    R        O  9 15REFFLD(HSRREF/@@RNCD *LIBL/HSPREF)
     A*
     A*---------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP5059C1                  SFLCTL(HP5059S1)
     A*%%TS  SD  20060817  164415  ISLRAMS     REL-V5R3M0  5722-WDS
     A                                      SFLSIZ(0100)
     A                                      SFLPAG(0009)
     A                                      WINDOW(HP5059EC)
     A                                      OVERLAY
     A                                      SFLCSRRRN(&HDRRN)
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A            HDPAG1    R        H      REFFLD(@@PAG)
     A                                      SFLRCDNBR
     A            HDRRN     R        H      REFFLD(@@RRN)
     A            SDPGM     R        O  1  1REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 18'SELECT BIN'
     A                                      DSPATR(HI)
     A                                  3  1'Farm:'
     A            C1FSCD    R        O  3  7REFFLD(@@FSCD)
     A                                      EDTCDE(Z)
     A            C1FSNM    R        O  3 14REFFLD(FSFSNM)
     A                                  5  1'Position Cursor, Press Enter'
     A                                      DSPATR(HI)
     A                                  7  1'Code'
     A                                      DSPATR(HI)
     A                                  7 12'Last Ration'
     A                                      DSPATR(HI)
     A            C1BNCD    R        B  8  1REFFLD(@@BNCD)
     A                                      DSPATR(HI)
     A*---------------------------------------------------------------
     A* COMMAND LINE LINE
     A*---------------------------------------------------------------
     A          R HP5059K1
     A                                      WINDOW(HP5059EC)
     A                                      OVERLAY
     A  43                             18  1'Message program failed. Call IS.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 19  1'F12=Previous'
      *
     A*-------------------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
     A*-------------------------------------------------------------------------
     A          R HP5059ES                  SFL SFLMSGRCD(19)
      *
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
     A*
     A*-------------------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*-------------------------------------------------------------------------
     A*
     A          R HP5059EC                  SFLCTL(HP5059ES)
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A                                      WINDOW(*DFT 20 40)
     A                                      WDWBORDER((*DSPATR RI) (*CHAR '    -
     A                                          '))
     A            MSGPGM                    SFLPGMQ(10)
     A*
     A*---------------------------------------------------------------
     A* ASSUME FORMAT -- REQUIRED FOR WINDOWS
     A*---------------------------------------------------------------
     A          R ASSUME
     A                                      ASSUME
     A                                      OVERLAY
     A                                 24 79' '
