/********************************************************************/
/*                                                                  */
/*  SYNOPSIS -  PRINT PROGRAM WITH COMPANY, WAREHOUSE, ITEM, ITEM   */
/*              TYPE, ITEM LEVEL, QUALITY LEVEL                     */
/*              ITEM STR TYPE, ITEM STR GROUP, ITEM STR CLASS       */
/*              NON-ZERO QUANTITY ONLY, AS OF DATE, REPORT TYPE AND */
/*              DETAIL OR SUMMARY.                                  */
/*              OPEN QUERY FILE OVER IC ITEM BALANCE                */
/*              STOCK STATUS                                        */
/*                                                                  */
/*  COMPANY   - SEABOARD CORP                                       */
/*  SYSTEM    - INVENTORY CONTROL                                   */
/*  DATE      - 06/20/95                                            */
/*                                                                  */
/*      PARAMETERS RECEIVED:                                        */
/*                                                                  */
/*          &PGM        - PROGRAM NAME                              */
/*          &COMPL      - COMPANY NUMBER                            */
/*          &WHSE       - WAREHOUSE CODE                            */
/*          &ITEM       - ITEM CODE                                 */
/*          &ITEMTP     - ITEM TYPE                                 */
/*          &ITEMLV     - ITEM LEVEL                                */
/*          &QLTYLV     - QUALITY LEVEL                             */
/*          &ITMTYP     - ITEM STR TYPE                             */
/*          &ITMGRP     - ITEM STR GROUP                            */
/*          &ITMCLS     - ITEM STR CLASS                            */
/*          &ASDATEL    - AS OF DATE                                */
/*          &NONZER     - NON-ZERO QUANTITY ONLY                    */
/*          &DTLSUM     - DETAIL OR SUMMARY                         */
/*          &RPTTYP     - REPORT TYPE                               */
/*          &OUTQ       - OUTPUT QUEUE                             */
/*          &HOLD       - HOLD                                     */
/*          &SAVE       - SAVE                                     */
/*          &COPIESL    - NUMBER OF COPIES                          */
/*          &NIGHTQ     - NIGHT QUEUE                               */
/********************************************************************/
/* 05/25/22 RMC DDSDDL REPLACE OPNQRYF WITH OPNSQLF        */
/*                     ADDED NEEDED OVRDBF AROUND THE COMMAND.              */
/*                     CHANGE *AND TO AND IN &G, CHG *EQ TO = IN QRY  */
/********************************************************************/

 PDX3CLP:    PGM        PARM(&PGM &COMPL &WHSE &ITEML +
                        &ITEMTP &ITEMLV &QLTYLV &ITMTYPL &ITMGRPL +
                        &ITMCLSL &ASDATEL &NONZER &DTLSUM &RPTTYP +
                        &OUTQ &HOLD &SAVE &COPIESL &NIGHTQ)

             DCL        &COPIESL    *DEC
             DCL        &COPIES     *DEC     LEN(3 0)
             DCL        &COMPL      *DEC         /*  CO. #       */
             DCL        &COMP       *DEC     LEN(3 0)
             DCL        &ASDATEL    *DEC         /* AS OF DATE   */
             DCL        &ASDATE     *DEC     LEN(7 0)
             DCL        &ITEML      *DEC         /*  ITEM         */
             DCL        &ITEM       *DEC     LEN(7 0)
             DCL        &ITEMTP     *CHAR    3   /* ITEM TYPE    */
             DCL        &ITEMLV     *CHAR    2   /* ITEM LEVEL   */
             DCL        &QLTYLV     *CHAR    2   /* QUALITY LEVEL*/
             DCL        &ITMTYPL    *DEC         /*  ITEM TYPE    */
             DCL        &ITMTYP     *DEC     LEN(3 0)
             DCL        &ITMGRPL    *DEC         /*  ITEM GROUP   */
             DCL        &ITMGRP     *DEC     LEN(3 0)
             DCL        &ITMCLSL    *DEC         /*  ITEM CLASS   */
             DCL        &ITMCLS     *DEC     LEN(3 0)
             DCL        &NONZER     *CHAR    1   /* NON-ZERO QTY */
             DCL        &DTLSUM     *CHAR    1   /* DETAIL/SUM   */
             DCL        &RPTTYP     *CHAR    1   /* REPORT TYPE  */
             DCL        &OUTQ       *CHAR    10  /* OUTPUT QUEUE */
             DCL        &NIGHTQ     *CHAR   10  /*  NIGHT JOB QUEUE*/
             DCL        &HOLD       *CHAR   4   /* HOLD        */
             DCL        &SAVE       *CHAR   4   /* SAVE        */
             DCL        &PGM        *CHAR    10  /*  PGM NAME    */
             DCL        &COMPA      *CHAR    3  /* CO. # FOR MSG */
             DCL        &ITEMA      *CHAR    7  /* ITEM FOR QRY  */
             DCL        &ITMTYPA    *CHAR    3  /* ITEM TYPE FOR QRY */
             DCL        &ITMGRPA    *CHAR    3  /* ITEM GRP FOR QRY */
             DCL        &ITMCLSA    *CHAR    3  /* ITEM CLS FOR QRY */
             DCL        &WHSE       *CHAR    3  /* WAREHOUSE     */
             DCL        &PGMO       *CHAR    10 /* PRINTER NAME  */
             DCL        &RETURN     *CHAR    7
             DCL        &COMMAND    *CHAR    512
             DCL        &A          *CHAR    25 /* QRY SELECT    */
             DCL        &B          *CHAR    35 /* QRY SELECT    */
             DCL        &C          *CHAR    35 /* QRY SELECT    */
             DCL        &D          *CHAR    35 /* QRY SELECT    */
             DCL        &E          *CHAR    35 /* QRY SELECT    */
             DCL        &F          *CHAR    35 /* QRY SELECT    */
             DCL        &G          *CHAR   200 /* QRY SELECT    */
             DCL        &SELECT     *CHAR    1   /* SELECT       */

             MONMSG     MSGID(CPF9841) /* DLTOVR ERROR  */

/********************************************************************/
/* APPEND $ TO THE PROGRAM NAME                                     */
/********************************************************************/
             IF         COND(&RPTTYP = 'S')  THEN(DO)
             CHGVAR     VAR(&PGM) VALUE('PDR1PFR')
             ENDDO

             IF         COND(&RPTTYP = 'T')  THEN(DO)
             CHGVAR     VAR(&PGM) VALUE('ICQUPFR')
             ENDDO

             IF         COND(&RPTTYP = 'W')  THEN(DO)
             CHGVAR     VAR(&PGM) VALUE('PDVAPFR')
             ENDDO

             CHGVAR     VAR(&PGMO) VALUE(&PGM *TCAT '$')
             CHGVAR     VAR(&COMP) VALUE(&COMPL) /* Change to length +
                          for print program */
             CHGVAR     VAR(&COMPA) VALUE(&COMP) /* Change FOR  +
                          MESSAGES for print program */
             CHGVAR     VAR(&COPIES) VALUE(&COPIESL) /* Change to length +
                          for print program */
             CHGVAR     VAR(&ITEM) VALUE(&ITEML) /* Change to +
                          length for print program */
             CHGVAR     VAR(&ITEMA) VALUE(&ITEM)  /*  FOR QRY +
                          SELECTION  */
             CHGVAR     VAR(&ITMTYP) VALUE(&ITMTYPL) /* Change to +
                          length for print program */
             CHGVAR     VAR(&ITMTYPA) VALUE(&ITMTYP)  /*  FOR QRY +
                          SELECTION  */
             CHGVAR     VAR(&ITMGRP) VALUE(&ITMGRPL) /* Change to +
                          length for print program */
             CHGVAR     VAR(&ITMGRPA) VALUE(&ITMGRP)  /*  FOR QRY +
                          SELECTION  */
             CHGVAR     VAR(&ITMCLS) VALUE(&ITMCLSL) /* Change to +
                          length for print program */
             CHGVAR     VAR(&ITMCLSA) VALUE(&ITMCLS)  /*  FOR QRY +
                          SELECTION  */
             CHGVAR     VAR(&ASDATE) VALUE(&ASDATEL)  /*  FOR QRY +
                          SELECTION  */

/********************************************************************/
/* Override printer file with user specified values                 */
/********************************************************************/

             SBDPRTOVR  CMDVAR(&COMMAND) FILE(&PGMO) OUTQ(&OUTQ) +
                          COPIES(&COPIES) HOLD(&HOLD) SAVE(&SAVE)

             CALL       PGM(QCMDEXC) PARM(&COMMAND 512)

/*********************************************************************/
/*   CREATE OPEN QUERY FILE TO BE USED TO DISPLAY DETAILS            */
/*********************************************************************/

             IF         COND(&COMPA *NE '000') THEN(DO)
             CHGVAR     VAR(&A) VALUE('B0AIC3 = ' *CAT &COMPA)
             CHGVAR     VAR(&SELECT) VALUE('Y')
             ENDDO

             IF         COND(&ITEMA *NE '0000000') THEN(DO)
             IF         COND(&SELECT *EQ 'Y') THEN(DO)
             CHGVAR     VAR(&B) VALUE('AND B0HGCD = ' *CAT &ITEMA)
             ENDDO
             ELSE DO
             CHGVAR     VAR(&B) VALUE('B0HGCD = ' *CAT &ITEMA)
             CHGVAR     VAR(&SELECT) VALUE('Y')
             ENDDO
             ENDDO

             IF         COND(&WHSE *NE '   ') THEN(DO)
             IF         COND(&SELECT *EQ 'Y') THEN(DO)
             CHGVAR     VAR(&C) VALUE('AND B0AJCD = "' *CAT &WHSE +
                        *CAT '"')
             ENDDO
             ELSE DO
             CHGVAR     VAR(&C) VALUE('B0AJCD = "' *CAT &WHSE +
                        *CAT '"')
             CHGVAR     VAR(&SELECT) VALUE('Y')
             ENDDO
             ENDDO

             IF        COND(&ITMTYPA *NE '000') THEN(DO)
             IF         COND(&SELECT *EQ 'Y') THEN(DO)
             CHGVAR    VAR(&D) VALUE('AND B0RGNB = ' *CAT &ITMTYPA)
             ENDDO
             ELSE DO
             CHGVAR    VAR(&D) VALUE('B0RGNB = ' *CAT &ITMTYPA)
             CHGVAR     VAR(&SELECT) VALUE('Y')
             ENDDO
             ENDDO

             IF        COND(&ITMGRPA *NE '000') THEN(DO)
             IF         COND(&SELECT *EQ 'Y') THEN(DO)
             CHGVAR    VAR(&E) VALUE('AND B0RHNB = ' *CAT &ITMGRPA)
             ENDDO
             ELSE DO
             CHGVAR    VAR(&E) VALUE('B0RHNB = ' *CAT &ITMGRPA)
             CHGVAR     VAR(&SELECT) VALUE('Y')
             ENDDO
             ENDDO

             IF        COND(&ITMCLSA *NE '000') THEN(DO)
             IF         COND(&SELECT *EQ 'Y') THEN(DO)
             CHGVAR    VAR(&F) VALUE('AND B0RINB = ' *CAT &ITMCLSA)
             ENDDO
             ELSE DO
             CHGVAR    VAR(&F) VALUE('B0RINB = ' *CAT &ITMCLSA)
             CHGVAR     VAR(&SELECT) VALUE('Y')
             ENDDO
             ENDDO


             CHGVAR     VAR(&G) VALUE(&A *BCAT &B *BCAT &C *BCAT &D +
                                      *BCAT &E *BCAT &F)

/********************  PRINT DETAIL REPORT  **************************/

/*   OPEN QUERY FOR DETAIL PRINT.    */

             IF         (&SELECT = 'Y') THEN(DO)

/*   PRINT STOCK STATUS BY ITEM                   */
             IF         COND(&RPTTYP = 'I')  THEN(DO)
     /*      OVRDBF     FILE(CAB0REL1) SHARE(*YES)                    */
    /*       OPNQRYF    FILE((CAB0REL1)) QRYSLT(&G) KEYFLD(*FILE) +
                           SEQONLY(*YES)                                */
/*   OPEN SQL                                                      */
             OVRDBF     FILE(CAB0REP) TOFILE(CAB0REP) LVLCHK(*NO)
             OPNSQLF    SQL('SELECT * FROM CAB0REP WHERE' *BCAT &G +
                          *BCAT 'ORDER BY  B0AIC3 ASC,B0HGCD +
                          ASC,B0AJCD ASC') OPNID(CAB0REL1) +
                          RCDFMT(@B0REJX) ALWCPY(*IFRQD)
   /*  Fromfile is the file in the program, tofile is 1st file in the opnsqlf */
     /*  For the override to be SEEN BY ALL PROGRAMS, THE OVRDBF +
                command must specify OVRSCOPE(*JOB) and OPNSCOPE(*JOB). */

             OVRDBF     FILE(CAB0REL1) TOFILE(CAB0REP) LVLCHK(*NO) +
                          OVRSCOPE(*JOB) SHARE(*YES) OPNSCOPE(*JOB)
             ENDDO

/*   PRINT STOCK STATUS BY ITEM STRUCTURE          */
             IF         COND(&RPTTYP = 'S')  THEN(DO)
             OVRDBF     FILE(CAB0RELM) SHARE(*YES)
/*           OPNQRYF    FILE((CAB0RELM)) QRYSLT(&G) KEYFLD(*FILE) +
                           SEQONLY(*YES)                             */
/*   OPEN SQL                                                      */
             OVRDBF     FILE(CAB0REP) TOFILE(CAB0REP) LVLCHK(*NO)
             OPNSQLF    SQL('SELECT * FROM CAB0REP WHERE' *BCAT &G +
                          *BCAT 'ORDER BY  B0AIC3 ASC,B0AJCD +
    ASC,B0RGNB ASC,B0RHNB ASC,B0RINB ASC,B0HGCD ASC') OPNID(CAB0RELM) +
                          RCDFMT(@B0REBY) ALWCPY(*IFRQD)
   /*  Fromfile is the file in the program, tofile is 1st file in the opnsqlf */
     /*  For the override to be SEEN BY ALL PROGRAMS, THE OVRDBF +
                command must specify OVRSCOPE(*JOB) and OPNSCOPE(*JOB). */

             OVRDBF     FILE(CAB0RELM) TOFILE(CAB0REP) LVLCHK(*NO) +
                          OVRSCOPE(*JOB) SHARE(*YES) OPNSCOPE(*JOB)
             ENDDO

/*   PRINT STOCK STATUS BY ITEM STRUCTURE TYPE     */
             IF         COND(&RPTTYP = 'T')  THEN(DO)
    /*       OVRDBF     FILE(CAB0RELN) SHARE(*YES)                      */
    /*       OPNQRYF    FILE((CAB0RELN)) QRYSLT(&G) KEYFLD(*FILE) +
                           SEQONLY(*YES)                                */
/*   OPEN SQL                                                      */
             OVRDBF     FILE(CAB0REP) TOFILE(CAB0REP) LVLCHK(*NO)
             OPNSQLF    SQL('SELECT * FROM CAB0REP WHERE' *BCAT &G +
                          *BCAT 'ORDER BY  B0AIC3 ASC,B0RGNB +
                          ASC,B0RHNB ASC, B0RINB ASC, B0HGCD ASC') +
                          OPNID(CAB0RELN) RCDFMT(@B0REBZ) +
                          ALWCPY(*IFRQD)
   /*  Fromfile is the file in the program, tofile is 1st file in the opnsqlf */
     /*  For the override to be SEEN BY ALL PROGRAMS, THE OVRDBF +
                command must specify OVRSCOPE(*JOB) and OPNSCOPE(*JOB). */

             OVRDBF     FILE(CAB0RELN) TOFILE(CAB0REP) LVLCHK(*NO) +
                          OVRSCOPE(*JOB) SHARE(*YES) OPNSCOPE(*JOB)
             ENDDO

/*   PRINT STOCK STATUS BY WAREHOUSE               */
             IF         COND(&RPTTYP = 'W')  THEN(DO)
   /*        OVRDBF     FILE(CAB0RELC) SHARE(*YES)                       */
   /*        OPNQRYF    FILE((CAB0RELC)) QRYSLT(&G) KEYFLD(*FILE) +
                           SEQONLY(*YES)                            */

/*   OPEN SQL                                                      */
             OVRDBF     FILE(CAB0REP) TOFILE(CAB0REP) LVLCHK(*NO)
             OPNSQLF    SQL('SELECT * FROM CAB0REP WHERE' *BCAT &G +
                          *BCAT 'ORDER BY  B0AIC3 ASC,B0AJCD +
                          ASC,B0HGCD ASC') OPNID(CAB0RELC) +
                          RCDFMT(@B0REHD) ALWCPY(*IFRQD)
   /*  Fromfile is the file in the program, tofile is 1st file in the opnsqlf */
     /*  For the override to be SEEN BY ALL PROGRAMS, THE OVRDBF +
                command must specify OVRSCOPE(*JOB) and OPNSCOPE(*JOB). */

             OVRDBF     FILE(CAB0RELC) TOFILE(CAB0REP) LVLCHK(*NO) +
                          OVRSCOPE(*JOB) SHARE(*YES) OPNSCOPE(*JOB)
             ENDDO

             ENDDO

             CALL       PGM(&PGM) PARM(&RETURN &COMP &ITEM &WHSE +
                          &ITMCLS &ITMGRP &ITMTYP &DTLSUM &ITEMTP +
                          &NONZER &ITEMLV &QLTYLV &RPTTYP &ASDATE)

             IF         (&SELECT = 'Y') THEN(DO)

             IF         COND(&RPTTYP = 'I') THEN(DO)
             CLOF       OPNID(CAB0REL1) /* Close query file */
             DLTOVR     FILE(CAB0REL1) /* Delete override */
             ENDDO

             IF         COND(&RPTTYP = 'S') THEN(DO)
             CLOF       OPNID(CAB0RELM) /* Close query file */
             DLTOVR     FILE(CAB0RELM) /* Delete override */
             ENDDO

/*   PRINT STOCK STATUS BY ITEM STRUCTURE TYPE     */
             IF         COND(&RPTTYP = 'T')  THEN(DO)
             CLOF       OPNID(CAB0RELN) /* Close query file */
             DLTOVR     FILE(CAB0RELN) /* Delete override */
             ENDDO

/*   PRINT STOCK STATUS BY WAREHOUSE               */
             IF         COND(&RPTTYP = 'W')  THEN(DO)
             CLOF       OPNID(CAB0RELC) /* Close query file */
             DLTOVR     FILE(CAB0RELC) /* Delete override */
             ENDDO

             ENDDO
