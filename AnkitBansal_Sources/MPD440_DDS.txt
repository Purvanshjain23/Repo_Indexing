     A*%%TS  SD  20140312  104748  ISLRAMS     REL-V6R1M0  5761-WDS
     A*****************************************************************
     A*  ENVIRONMENT:   PORK DIVISION
     A*  SYSTEM:        AS/400
     A*  DISPLAY FILE:  MPD440
     A*  TITLE:         SPECIFY BATCH EDIT LISTING OF RECEIPTS
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  CREATED:       04/29/02
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/MP2REF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A                                      CF04(04 'PROMPT')
     A                                      CF08(08 'SUBMIT')
     A                                      CF17(17 'PRINT OPTIONS')
     A                                      CF20(20 'WRKSPLF')
     A*
     A*---------------------------------------------------------------
     A* RECORD FORMAT ONE
     A*---------------------------------------------------------------
     A          R MP440R1
     A*%%TS  SD  20140312  104748  ISLRAMS     REL-V6R1M0  5761-WDS
     A                                      SETOF(34)
     A                                      OVERLAY
     A                                      RTNCSRLOC(&RCD &FLD)
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM         10A  O  1  2DSPATR(HI)
     A                                  1 26'MP2 INTERFACE TO JDE'
     A                                      DSPATR(HI)
     A            SDUSR         10A  O  1 70DSPATR(HI)
     A                                  2 18'SPECIFY BATCH EDIT LISTING OF RECE-
     A                                      IPTS'
     A                                      DSPATR(HI)
     A            SDJOB         10A  O  2 70DSPATR(HI)
     A                                  5  4'Take F8-Submit to generate an erro-
     A                                      r listing for all receipt records t-
     A                                      hat'
     A                                  6  4'have not yet been posted to JDE.'
     A                                  8  4'This function will not post any re-
     A                                      cords into JDE.'
     A                                 10  4'Accounting company ..'
     A                                 10 26'?'
     A            D1ACONO   R        B 10 28REFFLD(@@ACONO)
     A                                      EDTCDE(K)
     A N34                                  DSPATR(HI)
     A  34                                  DSPATR(PC)
     A  34                                  DSPATR(RI)
     A            D1ACONM       30   O 10 34
     A*
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R MP440K1
     A*%%TS  SD  20140312  104748  ISLRAMS     REL-V6R1M0  5761-WDS
     A  43                             21  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A                                 23 11'F4=Prompt'
     A                                 23 22'F8=Submit'
     A                                 23 33'F17=Print Options'
     A                                 23 52'F20=Spool Files'
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *---------------------------------------------------------------
     A          R MP440ES                   SFL SFLMSGRCD(24)
      *
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
     A*---------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*---------------------------------------------------------------
     A*
     A          R MP440EC                   SFLCTL(MP440ES)
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
