/********************************************************************/
/*                                                                  */
/*  SYNOPSIS -  JOB CONTROL PROMPT FOR PRINTER OVERRIDE INFORMATION */
/*              GENERIC PROMPTING FUNCTION. CAUSE PRINTER OVERRIDE  */
/*              PROMPTING AND PASSES COMPANY, FROM/THRU DATES,      */
/*              MARKET CODE,AND FLAG                                */
/*                                                                  */
/*  SYSTEM    - ORDER MANAGEMENT SYSTEM                             */
/*  PROGRAM   - PRINT REPORTS                                       */
/*  DATE      - 01/02/96                                            */
/*                                                                  */
/*      PARAMETERS:                                                 */
/*                                                                  */
/*     O    &RETURN     - RETURN CODE                               */
/*     I    &APPLIC     - APPLICATION CODE                          */
/*     I    &CLP        - CL PROGRAM TO SUBMIT                      */
/*     I    &DESC       - PRINT PROGRAM DESCRIPTION (FOR PROMPT     */
/*                         SCREEN)                                  */
/*     B    &OUTQ       -  OUTPUT QUEUE                             */
/*                         IF THIS IS BLANK AT START, PROMPT        */
/*                         PGM IS CALLED; OTHERWISE, THE PRINT CL   */
/*                         IS SUBMITTED WITHOUT PROMPTING           */
/*     B    &HOLD       -  HOLD STATUS                              */
/*     B    &SAVE       -  SAVE STATUS                              */
/*     B    &NQSTAT     -  INDICATOR WHETHER MUST RUN IN NIGHT Q    */
/*     B    &NIGHTQ     -  NIGHT JOB QUEUE                          */
/*     B    &COMPNYN    - COMPANY NUMBER (PASSED)                   */
/*     B    &FRMDATN    - FROM WEEK      (PASSED)                   */
/*     B    &THRDATN    - THRU WEEK      (PASSED)                   */
/*     B    &EXPSLS     - EXPORT SALES   (PASSED)                   */
/*     B    &SELRPT     - SELECT REPORT  (PASSED)                   */
/*     B    &SLSPR      - SALESPERSON    (PASSED)                   */
/*     B    &SHPCS      - SHIP TO CUST   (PASSED)                   */
/*     B    &MKTCODE    - MARKET CODE    (PASSED)                   */
/*     B    &ZONE       - SHIPPING ZONE  (PASSED)                   */
/*     B    &FLAG       - FLAG           (PASSED)                   */
/*     B    &FLAG2      - FLAG           (PASSED)                   */
/*     B    &COPYN      - NO. OF COPIES  (PASSED)                   */
/*     B    &FLAG3      - DEFAULT MKT CDE(PASSED)                   */
/********************************************************************/
/* RMC 7/26/00  PASS IN FTP FLAG AND DOCUMENT NAME                */
/* RMC 2/21/02  PASS IN WAREHOUSE TYPE AND WHSE CODE PARMS        */
/* RMC 3/24/04  PASS IN BROKER                                    */
/* SLM 10/02/05 PASS IN ITEM CODE                                 */
/* PKD 01/20/09 PASS IN ACTUAL OR ABSORBED FLAG                   */
/********************************************************************/

 PDJ2UPR:    PGM        PARM(&RETURN &APPLIC &CLP &DESC &OUTQ &HOLD +
                          &SAVE &NQSTAT &NIGHTQ &COMPNYN &FRMDATN +
                          &THRDATN &COPYN &ITMPASS &EXPSLS &SELRPT +
                          &SLSPR &SHPCS &FTP &MKTCODE &ZONE &FLAG +
                 &FLAG2 &FLAG3 &DOCUM &WHSTYP &WHSCOD &ACTABS &BRK)
/* PARMS                                                                      */
        DCL    &RETURN     *CHAR   (7)           /* Return Code               */
        DCL    &APPLIC     *CHAR   (06)          /* Application Code          */
        DCL    &CLP        *CHAR   (10)          /* CL to Submit              */
        DCL    &PGM        *CHAR   (10)          /* RPG TO SUBMIT             */
        DCL    &DESC       *CHAR   (25)          /* PRT Pgm Description       */
        DCL    &OUTQ       *CHAR   (10)          /* Output Queue              */
        DCL    &HOLD       *CHAR   (4)           /* Hold Status               */
        DCL    &SAVE       *CHAR   (4)           /* Save Status               */
        DCL    &NQSTAT     *CHAR   (01)          /* If MUST run in Night Q    */
        DCL    &NIGHTQ     *CHAR   (10)          /* Night Job Queue           */
        DCL    &COMPNYN    *DEC                  /* Company Number (Passed)   */
        DCL    &FRMDATN    *DEC                  /* From Date      (Passed)   */
        DCL    &THRDATN    *DEC                  /* Thru Date      (Passed)   */
        DCL    &COPYN      *DEC                  /* No. of Copies             */
        DCL    &ITMPASS    *DEC                  /* Item code      (Passed)   */
        DCL    &EXPSLS     *CHAR   (1)           /* Export Sales   (Passed)   */
        DCL    &SELRPT     *CHAR   (1)           /* Select Rpt     (Passed)   */
        DCL    &SLSPR      *CHAR   (3)           /* Salesperson    (Passed)   */
        DCL    &SHPCS      *DEC                  /* Ship to Cust   (Passed)   */
        DCL    &MKTCODE    *CHAR   (3)           /* Market Code    (Passed)   */
        DCL    &ZONE       *CHAR   (4)           /* Shipping Zone  (Passed)   */
        DCL    &FLAG       *CHAR   (1)           /*                (Passed)   */
        DCL    &FLAG2      *CHAR   (1)           /*                (Passed)   */
        DCL    &FLAG3      *CHAR   (1)
        DCL    &FTP        *CHAR   (01)          /* FTP RPT TYPE 1            */
        DCL    &DOCUM      *CHAR   (08)          /* DOCUMENT                  */
        DCL    &WHSTYP     *CHAR   (2)           /* WAREHOUSE TYPE            */
        DCL    &WHSCOD     *CHAR   (3)           /* WAREHOUSE CODE            */
        DCL    &ACTABS     *CHAR   (1)           /* ACTUAL OR ABSORBED        */
        DCL    &BRK        *CHAR   (3)           /*                (Passed)   */
/* INTERNAL FIELDS                                                            */
        DCL    &COPY       *DEC    (2 0)         /* Number of copies          */

/* ERROR MESSAGE FIELDS                                                       */
        DCL    &MSG        *CHAR   (512)         /* message                   */
        DCL    &MSGID      *CHAR   (7)           /* message id                */
        DCL    &MSGL       *DEC    (5 0)

/********************************************************************/
/*   INITIALIZE VARIABLES                                           */
/********************************************************************/

             CHGVAR     VAR(&RETURN) VALUE('       ')
             CHGVAR     VAR(&COPY) VALUE(&COPYN)
/********************************************************************/
/*   MONITOR FOR ALL MESSAGES AT PROGRAM LEVEL                      */
/********************************************************************/

             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(MSGTRAP))

/********************************************************************/
/*   CALL PROMPT PROGRAM                                            */
/********************************************************************/

             CALL       PGM(PDG0PVR) PARM(&RETURN &DESC &OUTQ &HOLD +
                          &SAVE &COPY &NQSTAT &NIGHTQ &APPLIC)
             CHGVAR     VAR(&COPYN) VALUE(&COPY)
             IF         COND(&RETURN *NE ' ') THEN(GOTO +
                          CMDLBL(USERQUIT))

/********************************************************************/
/*   SUBMIT PRINT CL PROGRAM                                        */
/********************************************************************/

 SUBMIT:     SBMJOB     CMD(CALL PGM(&CLP) PARM(&COMPNYN &FRMDATN +
                          &THRDATN &ITMPASS &EXPSLS &SELRPT &SLSPR +
                          &SHPCS &MKTCODE &ZONE &OUTQ &HOLD &SAVE +
                          &COPYN &NIGHTQ &FLAG &FLAG2 &FLAG3 &FTP +
                          &DOCUM &WHSTYP &WHSCOD &ACTABS &BRK)) +
                          JOB(&CLP) JOBQ(&NIGHTQ)
             GOTO       CMDLBL(END)

/*-------------------------USER QUIT--------------------------------*/
 USERQUIT:   CHGVAR     VAR(&RETURN) VALUE('Y2U0005')
             GOTO       CMDLBL(END)

/*----------------------RECEIVE MESSAGES----------------------------*/

 MSGTRAP:    RCVMSG     MSGTYPE(*EXCP) MSGDTA(&MSG) MSGDTALEN(&MSGL) +
                          MSGID(&MSGID)
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(END))

             SNDPGMMSG  MSGID(&MSGID) MSGF(QCPFMSG) MSGDTA(%SST(&MSG +
                          1 &MSGL)) MSGTYPE(*ESCAPE)
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(END))

 END:        ENDPGM

