     H/TITLE X0302    - Compute Due Date
     H* ----------------------------------------------------------------
     H*  X
     H*    Copyright (c) 1985,1991
     H*    J. D. Edwards & Company
     H*
     H*        This unpublished material is proprietary to
     H*        J. D. Edwards & Company.  All rights reserved.
     H*        The methods and techniques described herein are
     H*        considered trade secrets and/or confidential.
     H*        Reproduction or distribution, in whole or in part,
     H*        is forbidden except by express written permission
     H*        of J. D. Edwards & Company.
     H*
     H* ----------------------------------------------------------------
     H*                                                                -
     F*
     F*    PROGRAM REVISION LOG
     F*    --------------------
     F*
     F*          Date     Programmer     Nature of Revision
     F*        --------   ----------  ------------------------------------
     F*        08/07/92   KST         SAR # 00228443
     F*        11/18/92   WALSH       SAR # 00545086
     F*        07/30/93   KST         SAR # 00628834
     F*        08/31/93   SW930339    SAR # 00651735
     F*        03/08/94   WALSH       SAR # 00728579
     F*        03/21/96   CHAMBERS    SAR # 01148525
     F*        11/07/99   JP6382337   SAR # 03456450
     F*        04/03/20   BRAD BADEN  SDN465   TFS# 16169
     F*                   Copied source into E1 model and compiled
     F*
     F*****************************************************************
     F*
     F*   PURPOSE
     F*   -------
     F*
     F*   To provide a routine common to all programs that will
     F*   compute the discount date and the net due date for any
     F*   payment terms code.
     F*
     F*   REMARKS
     F*   -------
     F*
     F*   Input fields:
     F*
     F*      PSDDJ   - Transaction Date          (JUL-6 bytes)
     F*      PSPTC   - Payment Terms Code
     F*
     F*   Returned fields:
     F*
     F*      PSDDJ   - Net Due Date
     F*      PSDDDJ  - Discount Due Date
     F*      PSERR   - Error message
     F*      PS0014  - Payment terms record      (Optional)
     F*
     F*    The error return parameter will pass back a four digit
     F*    error number if error occurred.  Otherwise, this value will
     F*    be blank.
     F*
     F*    The return of the Payment terms record parameter is
     F*    determined by the presence of the parameter.  This is an
     F*    optional parameter.
     F*
     F*****************************************************************
     F*
     F*    Payment Terms
     F*
     FF0014     IF   E           K DISK    USROPN
     D*****************************************************************
     D*    PROGRAM TABLES AND ARRAYS
     D*    -------------------------
     D*
     D @SM             S              2  0 DIM(12) CTDATA PERRCD(12)            Days array        00
     D*********************************************************************00078
     D*********************************************************************00078
     D*    PROGRAM INPUT SPECIFICATIONS AND DATA STRUCTURES
     D*    ------------------------------------------------
     D*
     D*/COPY JDECPY,I00DSINX
     D*                                                                    00001
     D*    This data strucure provides the field definitions of all
     D*    possible array index field names useable by all common
     D*    subroutines.
     D*
     D DSINX           DS                  INZ                                   00001
     D*
     D*    Indexes of 1 digit 0 decimals
     D*
     D  #A                     1      1  0
     D  #B                     2      2  0
     D  #C                     3      3  0
     D  #D                     4      4  0
     D  #E                     5      5  0
     D  #F                     6      6  0
     D*
     D*    Indexes of 2 digits 0 decimals
     D*
     D  #G                     7      8  0
     D  #H                     9     10  0
     D  #I                    11     12  0
     D  #J                    13     14  0
     D  #K                    15     16  0
     D  #L                    17     18  0
     D*
     D*    Indexes of 3 digits 0 decimals
     D*
     D  #M                    19     21  0
     D  #N                    22     24  0
     D  #O                    25     27  0
     D  #P                    28     30  0
     D  #Q                    31     33  0
     D  #R                    34     36  0
     D*
     D*    Indexes of 4 digits 0 decimals
     D*
     D  #S                    37     40  0
     D  #T                    41     44  0
     D  #U                    45     48  0
     D  #V                    49     52  0
     D  #W                    53     56  0
     D  #X                    57     60  0
     D  #Y                    61     64  0
     D  #Z                    65     68  0
     D*/COPY JDECPY,I00DSPROG
     D*****************************************************************    00003
     D*
     D*    PROGRAM STATUS DATA STRUCTURE
     D*    -----------------------------
     D*
     D*    Portions of this data structure are loaded at the time the
     D*    program is loaded.  Other portions of this data structure
     D*    are loaded as you perform I/O.
     D*
     D*    PURPOSE
     D*    -------
     D*    This common subroutine is set up to be used with C0000
     D*    (Cost Center Security) common subroutine and C0001(Edit
     D*    Action Code) common subroutine.  Those two subroutines
     D*    will retrieve ##USER for the user name.
     D*
     D*    No program calcs are done in this subroutine.
     D*
     D ##PSDS         SDS
     D*
     D*          Program Name
     D  ##PROG                 1     10
     D*          Status Code(09999=I/O Error)
     D  ##STAT                11     15  0
     D*          Previous Status code
     D  ##PSTA                16     20  0
     D*          RPG Source Statement Sequence Number
     D  ##SEQN                21     28
     D*          RPG Routine in Which Exception/Error Occured
     D  ##ROUT                29     36
     D*          Number of Parameters Passed to This Program
     D  ##PARM                37     39  0
     D*          Exception Type(MCH=Machine, CPF=CPF)
     D  ##ETYP                40     42
     D*          Exception Message Number
     D  ##ENBR                43     46
     D*          Machine Instruction/Object Definition Template Number
     D  ##MINO                47     50
     D*          Work Area for Messages
     D  ##MWRK                51     80
     D*          Name of Library in Which Program is Located
     D  ##PLIB                81     90
     D*          Retrieved Exdeption Data.  CPF Messages.
     D  ##MSG                 91    170
     D*          Identification of Exception That Caused RPG9001
     D  ##9001               171    174
     D*          Unused
     D  ##FLR1               175    200
     D*          Name of File for Last I/O(Only Updated if Error)
     D  ##LFIL               201    208
     D*          Status Info on Last File Used(Only on Error)
     D  ##LFST               209    243
     D*          Status Code on Last File Used(Only on Error)
     D  ##LFS5               209    213
     D*          Job Name
     D  ##JOBN               244    253
     D*          User Name From User Profile
     D  ##USER               254    263
     D*          Job Number
     D  ##JOB#               264    269  0
     D*          Date Job Entered the System(MMDDYY)
     D  ##JDT                270    275  0
     D*          Date of Program Execution(MMDDYY)
     D  ##EDT                276    281  0
     D*          Time of Program Execttion(HHMMSS)
     D  ##ETM                282    287  0
     D*          Date Program Was Compiled
     D  ##CDT                288    293  0
     D*          Time Program Was Compiled
     D  ##CTM                294    299  0
     D*          Level of the Compiler
     D  ##LVL                300    303
     D*          Source File Name
     D  ##SRCN               304    313
     D*          Source Library Name
     D  ##SRCL               314    323
     D*          Source File Member Name
     D  ##SRCM               324    333
     D*          Unused
     D  ##FLR2               334    429
     D**************************************************************
     D DSDATE          DS
     D  $MNTH                  1      2  0
     D  $DAY                   3      4  0
     D  $YEAR                  5      6  0
     D  $DATE                  1      6  0
     D DS0014        E DS                  EXTNAME(F0014)
     I*****************************************************************
     C*********************************************************************00013
     C*
     C*    MAINLINE PROGRAM
     C*    ----------------
     C*
     C*    *INZSR subroutine executed first time.
     C*
     C*    If no parameters, bypass routine.
     C*
     C     ##PARM        CABEQ     *ZERO         EOJ
     C*                    -----          ---
     C*
     C*    Initialize routine
     C*
     C                   EXSR      S999
     C*                    ---- ----
     C     PSERR         CABNE     *BLANKS       EOJ
     C*                    -----          ---
     C*------------------------------------------------------------------
     C*
     C*    If same parameters as last time, skip processing
     C*
B1   C     PSDDJ         IFEQ      $IDDJ                                        trans date
     C     PSPTC         ANDEQ     $IPTC                                        payment term
     C     $0014         ANDEQ     $I0014                                       f0014 retrn
     C*
     C                   MOVEL     $OPTC         PSPTC                          payment term
     C                   MOVEL     $ODDJ         PSDDJ                          net due date
     C                   MOVEL     $ODDDJ        PSDDDJ                         disc due dat
     C                   MOVEL     $OERR         PSERR                          error msg
     C*
B2   C     $0014         IFEQ      '1'
     C                   MOVEL     $O0014        PS0014                         F0014 record
E2   C                   ENDIF
     C*
     C                   GOTO      EOJ
     C*                    ---- ---
E1   C                   ENDIF
     C*------------------------------------------------------------------
     C*
     C*    Move parameters to input work fields
     C*
     C                   MOVEL     PSPTC         $IPTC                          payment term
     C                   MOVEL     PSDDJ         $IDDJ                          trans date
     C                   MOVEL     $0014         $I0014                         f0014 record
     C*------------------------------------------------------------------
     C*
     C*    Determine discount and net due dates.
     C*
     C                   EXSR      S001
     C*                    ---- ----
     C*
     C*------------------------------------------------------------------
     C*
     C*    End of program
     C*
     C     EOJ           TAG
     C*          ---       ---
     C*
     C*    Save values for next return
     C*
B1   C     ##PARM        IFGT      *ZERO
     C                   MOVEL     PSPTC         $OPTC
     C                   MOVEL     PSDDJ         $ODDJ
     C                   MOVEL     PSDDDJ        $ODDDJ
     C                   MOVEL     PSERR         $OERR
B2   C     $0014         IFEQ      '1'
     C                   MOVEL     PS0014        $O0014                         F0014 record
E2   C                   ENDIF
E1   C                   ENDIF
     C*
     C                   RETURN
     C*********************************************************************00002
     C*
     C*     SUBROUTINE S001  - Compute Due Date
     C*     -----------------------------------
     C*
     CSR   S001          BEGSR
     C*          ----      -----
     C*
     C*    Save transaction date.
     C*
     CSR                 MOVE      PSDDJ         $TDDJ
     C*
     C*--------------------------------------------------------------
     C*
     C*    Calculate Net Due Date.
     C*    -----------------------
     C*
B1   CSR                 SELECT
     C*
     C*. . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . .
     C*
     C*   Use net days to pay
     C*
W1   CSR   PNNDTP        WHENGT    *ZEROS
     CSR                 MOVE      PNNDTP        $#X               9
     C*
     C*        - calculate due date
     C*
     CSR                 MOVE      ' '           $ERTST                          invoice dt
     CSR                 MOVE      '*JUL   '     #SIFM1            7
     CSR                 MOVE      '*JUL   '     #SIFM2            7
     CSR                 MOVE      PSDDJ         #SIDA1            6
     CSR                 MOVE      *BLANKS       #SIDA2            6
     CSR                 MOVE      *BLANKS       #SITYE            1
     CSR                 CALL      'X0035'
     C*                    ----  ------
     CSR                 PARM                    #SIDA1
     CSR                 PARM                    #SIDA2
     CSR                 PARM                    #SIFM1
     CSR                 PARM                    #SIFM2
     CSR                 PARM                    #SITYE
     CSR                 PARM                    $ERTST
     CSR                 PARM                    $#X
     CSR                 PARM      *BLANKS       #CTRY             2
     CSR                 PARM      *BLANKS       #CTRY2            2
     CSR                 MOVE      #SIDA2        PSDDJ
     C*
     C*. . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . .
     C*
     C*   Use discount days to pay
     C*
W1   CSR   PNDCD         WHENGT    *ZEROS
     CSR                 MOVE      PNDCD         $#X
     C*
     C*        - calculate due date
     C*
     CSR                 MOVE      ' '           $ERTST                          invoice dt
     CSR                 MOVE      '*JUL   '     #SIFM1            7
     CSR                 MOVE      '*JUL   '     #SIFM2            7
     CSR                 MOVE      PSDDJ         #SIDA1            6
     CSR                 MOVE      *BLANKS       #SIDA2            6
     CSR                 MOVE      *BLANKS       #SITYE            1
     CSR                 CALL      'X0035'
     C*                    ----  ------
     CSR                 PARM                    #SIDA1
     CSR                 PARM                    #SIDA2
     CSR                 PARM                    #SIFM1
     CSR                 PARM                    #SIFM2
     CSR                 PARM                    #SITYE
     CSR                 PARM                    $ERTST
     CSR                 PARM                    $#X
     CSR                 PARM      *BLANKS       #CTRY
     CSR                 PARM      *BLANKS       #CTRY2
     CSR                 MOVE      #SIDA2        PSDDJ
     C*
     C*. . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . .
     C*
     C*   Use due date
     C*
W1   CSR   PNDDJ         WHENNE    *ZEROS                                       If specific
     CSR                 MOVE      PNDDJ         PSDDJ                            date
     C*
     C*. . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . .
     C*
     C*   Use prox date
     C*
W1   CSR                 OTHER
     C*
     CSR                 MOVE      PSDDJ         $DDJ
     CSR                 EXSR      S001P
     C*                    ---- -----
     CSR                 MOVE      $DDJ          PSDDJ
     C*
E1   CSR                 ENDSL
     C*--------------------------------------------------------------
     C*
     C*    Calculate discount due date.
     C*    ----------------------------
     C*
B1   CSR                 SELECT
     C*
     C*. . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . .
     C*
     C*   Use discount days to pay
     C*
W1   CSR   PNDCD         WHENGT    *ZEROS
     CSR                 MOVE      PNDCD         $#X
     C*
     C*        - calculate due date
     C*
     CSR                 MOVE      ' '           $ERTST                          invoice dt
     CSR                 MOVE      '*JUL   '     #SIFM1            7
     CSR                 MOVE      '*JUL   '     #SIFM2            7
     CSR                 MOVE      $TDDJ         #SIDA1            6
     CSR                 MOVE      *BLANKS       #SIDA2            6
     CSR                 MOVE      *BLANKS       #SITYE            1
     CSR                 CALL      'X0035'
     C*                    ----  ------
     CSR                 PARM                    #SIDA1
     CSR                 PARM                    #SIDA2
     CSR                 PARM                    #SIFM1
     CSR                 PARM                    #SIFM2
     CSR                 PARM                    #SITYE
     CSR                 PARM                    $ERTST
     CSR                 PARM                    $#X
     CSR                 PARM      *BLANKS       #CTRY
     CSR                 PARM      *BLANKS       #CTRY2
     CSR                 MOVE      #SIDA2        PSDDDJ
     C*
     C*. . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . .
     C*
     C*   Use net days to pay
     C*
W1   CSR   PNNDTP        WHENGT    *ZEROS
     CSR                 MOVE      PNNDTP        $#X               9
     C*
     C*        - calculate due date
     C*
     CSR                 MOVE      ' '           $ERTST                          invoice dt
     CSR                 MOVE      '*JUL   '     #SIFM1            7
     CSR                 MOVE      '*JUL   '     #SIFM2            7
     CSR                 MOVE      $TDDJ         #SIDA1            6
     CSR                 MOVE      *BLANKS       #SIDA2            6
     CSR                 MOVE      *BLANKS       #SITYE            1
     CSR                 CALL      'X0035'
     C*                    ----  ------
     CSR                 PARM                    #SIDA1
     CSR                 PARM                    #SIDA2
     CSR                 PARM                    #SIFM1
     CSR                 PARM                    #SIFM2
     CSR                 PARM                    #SITYE
     CSR                 PARM                    $ERTST
     CSR                 PARM                    $#X
     CSR                 PARM      *BLANKS       #CTRY
     CSR                 PARM      *BLANKS       #CTRY2
     CSR                 MOVE      #SIDA2        PSDDDJ
     C*
     C*. . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . .
     C*
     C*   Use due date
     C*
W1   CSR   PNDDJ         WHENNE    *ZEROS                                       If specific
     CSR                 MOVE      PNDDJ         PSDDDJ                           date
     C*
     C*. . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . .
     C*
     C*   Use prox date
     C*
W1   CSR                 OTHER
     C*
     CSR                 MOVE      $TDDJ         $DDJ
     CSR                 EXSR      S001P
     C*                    ---- -----
     CSR                 MOVE      $DDJ          PSDDDJ
     C*
E1   CSR                 ENDSL
     C*--------------------------------------------------------------
     C*
     C*   If parm passed, move F0014 to PS0014.
     C*
B1   CSR   $0014         IFEQ      '1'
DBN  CSR                 MOVEL     DS0014        PS0014                         F0014 record
E1   CSR                 ENDIF
     C*
     CSR   END001        ENDSR
     C**************************************************************************
     C*
     C*    SUBROUTINE S001P - Calculate Date Using Prox Date
     C*    -------------------------------------------------
     C*
     C*    Processing:  1.  Calculate Date using Prox Date
     C*
     C*    Input     :  $DDJ   - Due Date (Julian)
     C*                 PNPXDM - Prox Month
     C*                 PNPXDD - Prox Day
     C*
     C*    Output    :  $DDJ   - Due Date (Julian)
     C*
     CSR   S001P         BEGSR
     C*          ----      -----
     C*
     C*        - convert date to MDY
     C*
     CSR                 MOVE      $DDJ          #SIDAT            6
     CSR                 MOVE      *BLANK        #EDAT             8
     CSR                 MOVEL     '*JUL    '    #FFMT             7
     CSR                 MOVEL     '*MDY    '    #TFMT             7
     CSR                 MOVEL     '*NONE   '    #SEP              7
     CSR                 MOVEL     ' '           $ERTST            1
     CSR                 CALL      'X0028   '                           81
     C*                    ---- -----
     CSR                 PARM                    #SIDAT
     CSR                 PARM                    #EDAT
     CSR                 PARM                    #FFMT
     CSR                 PARM                    #TFMT
     CSR                 PARM                    #SEP
     CSR                 PARM                    $ERTST
     C****                 PARM           $CTRY   2        DEL 3456450
     CSR                 PARM      *BLANKS       $CTRY             2            ADD 3456450
     CSR                 MOVEL     #SIDAT        $DATE
     C*
     C*        - calculate month and year
     C*
B1   CSR   PNPXDM        IFGT      *ZEROS                                       If month
     CSR                 ADD       PNPXDM        $MNTH
     C*
B2   CSR   $MNTH         DOWGT     12
     CSR                 SUB       12            $MNTH
     CSR                 ADD       1             $YEAR
     CSR   $YEAR         IFEQ      00
     CSR   $CTRY         ANDEQ     '19'
     CSR                 MOVE      $CTRY         $$CTY             2 0
     CSR   $$CTY         ADD       1             $$CTY
     CSR                 MOVE      $$CTY         $CTRY
     CSR                 ENDIF
E2   CSR                 ENDDO
     C*
     C*    Check for Leap Year
     C*
     CSR                 MOVE      $YEAR         $#YY              2
B1   CSR   $#YY          IFEQ      '00'
     CSR                 MOVE      $#YY          $$CDTM            4 0
     CSR                 MOVEL     $CTRY         $$CDTM
     CSR   $$CDTM        DIV       400           $LYR              4 0
     CSR                 MVR                     $LFLG             4 0
X1   CSR                 ELSE
     CSR                 MOVE      $#YY          $CDTM             2 0
     CSR   $CDTM         DIV       4             $#LYR             2 0
     CSR                 MVR                     $LFLG
E1   CSR                 ENDIF
B1   CSR   $LFLG         IFEQ      *ZEROS
     CSR                 Z-ADD     29            $$LYR             2 0
     CSR                 MOVEA     $$LYR         @SM(2)
     CSR                 ELSE
     CSR                 Z-ADD     28            $$LYR
     CSR                 MOVEA     $$LYR         @SM(2)
E1   CSR                 ENDIF
     C*
     C*
     C*        - calculate day (if overflow)
     C*
B2   CSR   $DAY          IFGT      @SM($MNTH)
     CSR                 Z-ADD     @SM($MNTH)    $DAY
E2   CSR                 ENDIF
     C*
E1   CSR                 ENDIF
     C*
     C*        - calculate day
     C*
B1   CSR   PNPXDD        IFGT      *ZEROS                                       If days
     CSR                 Z-ADD     PNPXDD        $DAY
B2   CSR   $DAY          IFGT      @SM($MNTH)
     CSR                 Z-ADD     @SM($MNTH)    $DAY
E2   CSR                 ENDIF
E1   CSR                 ENDIF
     C*
     C*        - convert date back to Julian
     C*
     CSR                 MOVE      $DATE         #SIDAT            6
     CSR                 MOVE      *BLANK        #EDAT             8
     CSR                 MOVEL     '*MDY    '    #FFMT             7
     CSR                 MOVEL     '*JUL    '    #TFMT             7
     CSR                 MOVEL     '*NONE   '    #SEP              7
     CSR                 MOVEL     ' '           $ERTST            1
     CSR                 CALL      'X0028   '                           81
     C*                    ---- -----
     CSR                 PARM                    #SIDAT
     CSR                 PARM                    #EDAT
     CSR                 PARM                    #FFMT
     CSR                 PARM                    #TFMT
     CSR                 PARM                    #SEP
     CSR                 PARM                    $ERTST
     CSR                 MOVEL     #SIDAT        $DDJ
     C*
     CSR   E001P         ENDSR
     C**************************************************************************
     C**************************************************************************
     C*
     C*    SUBROUTINE S999  - Housekeeping
     C*    -------------------------------
     C*
     C*    Processing:  1.  Receive passed parameters.
     C*                 2.  Initialize data file key list(s).
     C*
     CSR   S999          BEGSR
     C*          ----      -----
     C*
     C*    Required program parameters.
     C*
     CSR   *ENTRY        PLIST
     CSR                 PARM                    PSDDJ             6            net due date
     CSR                 PARM                    PSDDDJ            6            disc due dat
     CSR                 PARM                    PSPTC                          payment terms
     CSR                 PARM                    PSERR             4            error msg   s
     CSR                 PARM                    PS0014                         F0014 records
     C*-----------------------------------------------------------------
     C*
     C*    Validate parameters
     C*
     CSR                 MOVE      *BLANKS       PSERR
     C*
     C*    File open error.
     C*
B1   CSR   $OPEN         IFEQ      '1'
     CSR                 MOVE      '3003'        PSERR
E1   CSR                 ENDIF
     C*
     C*    Return F0014 record value
     C*
B1   CSR   ##PARM        IFGE      5
     CSR                 MOVE      '1'           $0014             1
X1   CSR                 ELSE
     CSR                 MOVE      '0'           $0014
E1   CSR                 ENDIF
     C*
     C*    Validate Payment Terms Code passed.
     C*
     CSR   PSPTC         CHAIN     I0014                              8182
B1   CSR   *IN81         IFEQ      '1'
     CSR                 MOVE      '0130'        PSERR
E1   CSR                 ENDIF
     C*
     C*    Validate transaction date passed.
     C*
     CSR                 MOVE      PSDDJ         #SIDAT            6
     CSR                 MOVE      *BLANK        #EDAT             8
     CSR                 MOVEL     '*JUL    '    #FFMT             7
     CSR                 MOVEL     '*JUL    '    #TFMT             7
     CSR                 MOVEL     '*NONE   '    #SEP              7
     CSR                 MOVEL     ' '           $ERTST            1
     CSR                 CALL      'X0028   '                           81
     C*                    ---- -----
     CSR                 PARM                    #SIDAT
     CSR                 PARM                    #EDAT
     CSR                 PARM                    #FFMT
     CSR                 PARM                    #TFMT
     CSR                 PARM                    #SEP
     CSR                 PARM                    $ERTST
     C****                 PARM           $CTRY   2        DEL 3456450
     CSR                 PARM      *BLANKS       $CTRY             2            ADD 3456450
     C*
     C*    Check for Leap Year
     C*
     CSR                 MOVE      #EDAT         $#YY              2
B1   CSR   $#YY          IFEQ      '00'
     CSR                 MOVE      $#YY          $$CDTM            4 0
     CSR                 MOVEL     $CTRY         $$CDTM
     CSR   $$CDTM        DIV       400           $LYR              4 0
     CSR                 MVR                     $LFLG             4 0
X1   CSR                 ELSE
     CSR                 MOVE      $#YY          $CDTM             2 0
     CSR   $CDTM         DIV       4             $#LYR             2 0
     CSR                 MVR                     $LFLG
E1   CSR                 ENDIF
B1   CSR   $LFLG         IFEQ      *ZEROS
     CSR                 Z-ADD     29            $$LYR             2 0
     CSR                 MOVEA     $$LYR         @SM(2)
     CSR                 ELSE
     CSR                 Z-ADD     28            $$LYR
     CSR                 MOVEA     $$LYR         @SM(2)
E1   CSR                 ENDIF
     C*
B1   CSR   $ERTST        IFEQ      '1'
     CSR                 MOVE      '0004'        PSERR
E1   CSR                 ENDIF
     C*
     C*----------------------------------------------------------------
     CSR   END999        ENDSR
     C*****************************************************************
     C*
     C*    SUBROUTINE S999LK - *LIKE definitions
     C*    -------------------------------------
     C*
     CSR   S999LK        BEGSR
     C*
     C*    Work fields
     C*
     CSR   *LIKE         DEFINE    PSDDJ         $TDDJ
     CSR   *LIKE         DEFINE    PSDDJ         $DDJ
     C*
     C*    Save fields for input parameters
     C*
     CSR   *LIKE         DEFINE    PSDDJ         $IDDJ
     CSR   *LIKE         DEFINE    PNPTC         $IPTC
     CSR   *LIKE         DEFINE    PNPTC         PSPTC
     CSR   *LIKE         DEFINE    $0014         $I0014
     C*
     C*    Save fields for output parameters
     C*
     CSR   *LIKE         DEFINE    PSDDJ         $ODDJ
     CSR   *LIKE         DEFINE    PSDDDJ        $ODDDJ
     CSR   *LIKE         DEFINE    PNPTC         $OPTC
     CSR   *LIKE         DEFINE    PSERR         $OERR
     CSR   *LIKE         DEFINE    DS0014        $O0014
     CSR   *LIKE         DEFINE    DS0014        PS0014
     C*
     CSR   E999LK        ENDSR
     C*****************************************************************
     C*
     C*    SUBROUTINE *INZSR - Initial subroutine - first time only
     C*    --------------------------------------------------------
     C*
     C*    Processing:
     C*                 1.  Open database files
     C*
     CSR   *INZSR        BEGSR
     C*          ------    -----
     C*-----------------------------------------------------------------
     C*
     C*    Open account master files
     C*
     CSR                 OPEN      F0014                                81
     CSR                 MOVE      *BLANK        $OPEN             1
B1   CSR   *IN81         IFEQ      '1'
     CSR                 MOVE      '1'           $OPEN
E1   CSR                 ENDIF
     C*-----------------------------------------------------------------
     C*
     CSR   EINZSR        ENDSR
     C****************************************************************
** @SM   DAYS OF THE MONTH
312831303130313130313031
