     A*%%TS  SD  20080505  162353  ISLRAMS     REL-V5R4M0  5722-WDS
     A*****************************************************************
     A*
     A*  SYSTEM:        TRIUMPH FOODS
     A*  DISPLAY FILE:  TFD405
     A*  TITLE:         WORK WITH CASH DISTRIBUTION HEADERS
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  CREATED:       10/21/04
     A*
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/TFPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A                                      CF04(04 'PROMPT')
     A                                      CF05(05 'REFRESH')
     A  89                                  CF06(06 'CREATE')
     A                                      CF07(07 'LISTING')
     A                                      CF17(17 'PRINT OPTIONS')
     A                                      CF20(20 'WRKSPLF')
     A*
     A*
     A*---------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*---------------------------------------------------------------
     A          R TF405S1                   SFL
     A*%%TS  SD  20050608  143408  ISLRAMS     REL-V5R3M0  5722-WDS
     A  85                                  SFLNXTCHG
     A            HDRRN1         4Y 0H
     A            D1OPT          2A  B 10  2CHGINPDFT(UL)
     A  30                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A N30                                  DSPATR(HI)
     A            D1DMDY    R        O 10  7REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  ')
     A            D1CPAM    R        O 10 17REFFLD(TFREF/@@CPAM *LIBL/TFPREF)
     A                                      EDTCDE(K)
     A            D1BTSN    R        O 10 34REFFLD(TFREF/@@BTSN *LIBL/TFPREF)
     A                                      EDTCDE(Z)
     A            D1CHSTCD  R        O 10 44REFFLD(TFREF/@@CHSTCD *LIBL/TFPREF)
     A            D1YR      R        O 10 47REFFLD(@@YR)
     A                                      EDTCDE(M)
     A            D1WK      R        O 10 54REFFLD(@@WK)
     A                                      EDTCDE(M)
     A            D1WBMDY   R        O 10 60REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  ')
     A            D1WEMDY   R        O 10 70REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  ')
     A*
     A*---------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*---------------------------------------------------------------
     A          R TF405C1                   SFLCTL(TF405S1)
     A*%%TS  SD  20050926  130713  ISLRAMS     REL-V5R3M0  5722-WDS
     A                                      SFLSIZ(0050)
     A                                      SFLPAG(0011)
     A N82                                  ROLLUP(25 'PAGE FORWARD')
     A                                      SETOF(52)
     A                                      RTNCSRLOC(&RCD &FLD)
     A                                      OVERLAY
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 23'WORK WITH CASH DISTRIBUTION HEADER-
     A                                      S'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USR)
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@JOB)
     A                                      DSPATR(HI)
     A  89                              3  2'2=Revise'
     A  89                              3 12'4=Delete'
     A                                  3 22'5=View'
     A                                  3 30' 6=CD Batch Report'
     A  89                              4  2'7=Assign/Unassign Deposits'
     A  89                              4 30'10=Post'
     A                                  6 44'?'
     A                                      DSPATR(HI)
     A                                  6 62'Week'
     A                                      DSPATR(HI)
     A                                  6 72'Week'
     A                                      DSPATR(HI)
     A                                  7  9'Daily'
     A                                      DSPATR(HI)
     A                                  7 19'Cash Payment'
     A                                      DSPATR(HI)
     A                                  7 36'Batch'
     A                                      DSPATR(HI)
     A                                  7 59'Beginning'
     A                                      DSPATR(HI)
     A                                  7 71'Ending'
     A                                      DSPATR(HI)
     A                                  8  2'Opt'
     A                                      DSPATR(HI)
     A                                  8  9'Date'
     A                                      DSPATR(HI)
     A                                  8 22'Amount'
     A                                      DSPATR(HI)
     A                                  8 35'Number'
     A                                      DSPATR(HI)
     A                                  8 43'St'
     A                                      DSPATR(HI)
     A                                  8 47'Year'
     A                                      DSPATR(HI)
     A                                  8 53'Week'
     A                                      DSPATR(HI)
     A                                  8 62'Date'
     A                                      DSPATR(HI)
     A                                  8 72'Date'
     A                                      DSPATR(HI)
     A            C1DMDY    R        B  9  7REFFLD(@@DT06)
     A                                      DSPATR(HI)
     A                                      EDTWRD('  /  /  -')
     A            C1BTSN    R        B  9 34REFFLD(TFREF/@@BTSN *LIBL/TFPREF)
     A                                      EDTCDE(M)
     A                                      DSPATR(HI)
     A            C1CHSTCD  R        B  9 44REFFLD(TFREF/@@CHSTCD *LIBL/TFPREF)
     A                                      DSPATR(HI)
     A  52                                  DSPATR(PC)
     A            C1YR      R        B  9 47REFFLD(@@YR)
     A                                      DSPATR(HI)
     A                                      EDTCDE(M)
     A            C1WK      R        B  9 54REFFLD(@@WK)
     A                                      DSPATR(HI)
     A                                      EDTCDE(M)
     A*
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R TF405K1
     A*%%TS  SD  20080505  162353  ISLRAMS     REL-V5R4M0  5722-WDS
     A  43                             21  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 22  2'F3=Exit'
     A                                 22 11'F4=Prompt'
     A                                 22 22'F5=Refresh'
     A  89                             22 34'F6=Create'
     A                                 22 45'F7=Listing'
     A                                 22 62'F17=Print Options'
     A                                 23 62'F20=Spool Files'
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *---------------------------------------------------------------
     A          R TF405ES                   SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE CONTROL FORMAT
      *---------------------------------------------------------------
     A          R TF405EC                   SFLCTL(TF405ES)
      *
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
