      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Triumph Foods
      * PROGRAM:     TF646
      * TITLE:       Margin: Margin Adjustment Re-Cap Report
      * PROGRAMMER:  LeAnne Ramsey
      * CREATED:     12/12/06
      *
      * FUNCTION:  Batch program to print a record from the Summary files.
      *
      *            This listing is submitted:
      *               1) On-demand
      *               2) From the Margin Adjustment Close function
      *
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 09/26/06  LeAnne Ramsey
      *           Rewritten. We went to two summary files (mix and volume).
      *           So, we had to go to workfile logic to bring the 2 summary files
      *           together for this report.
      *
      * 12/12/06  LeAnne Ramsey
      *           Logic has been changed to handle both "weekly" and "period" data.
      *
      * 03/21/07  LeAnne Ramsey
      *           Users now want the "mix" based on "produced" data instead of "sales"
      *           data. In case they change back, we have kept the "sales" logic in the
      *           system--just added "produced" logic. So, now, for this report, they
      *           can elect what they want the "mix" based on--sales or production data.
      *
      * 07/06/07  LeAnne Ramsey
      *           Changed layout.
      *
      * 08/27/08  LeAnne Ramsey
      *           We no longer allow the users to select whether they want the report
      *           based on Sales vs Production Data. So, I hardcoded the "description"
      *           for the heading.
      *
      * 11/21/08  LeAnne Ramsey
      *           As part of synchronizing the LDAs between the TFS Margin Adjustment Close
      *           and the Meat Costing, we changed the LDA positions.
      *
      * 03/30/09  LeAnne Ramsey
      *           Tom Dye has requested that the "CoOwned Transfer Cost Amount" on this
      *           report be broken out into 2 columns: SBD and TF.
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
      *
     Ftfp346    if   e           k disk
      *    Workfile: Header
      *
      *
     Ftfp3460   if   e           k disk
      *    Workfile: Detail
      *
      *
     Fprint198  o    f  198        printer oflind(*inof)
      *
      /EJECT
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Standard fields
      *
     D dash            s            198    inz(*all'-')
     d rtime           s              6  0
      *
      *
      * Control fields
      *
     D first           s              1    inz('Y')
      *
      *
      * Workfields for date manipulation
      *
     D wkisodate       s               D   datfmt(*iso)
      *
      *
      * Print fields
      *
     d h1demand        s             35
     d h1fwemdy        s              6  0
     d h1twemdy        s              6  0
     D h1ds            s             40
      *
     D t1volam         s                   like(w2volam)
     D t1mixam         s                   like(w2volam)
     D t1adjam         s                   like(w2volam)
     D t1scoam         s                   like(w2volam)
     D t1tcoam         s                   like(w2volam)
     D t1payam         s                   like(w2volam)
      *
      *
      ****************************************************************
      * ARRAYS AND TABLES
      ****************************************************************
      *
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *
      *---------------------------------------------------------------
      * Local data area.
      *---------------------------------------------------------------
      *
      * We have to generate this from the Margin Adjustment Close function. So,
      * if you have to change the LDA, check the LDA in the Close function as well.
      *
     Dlda             uds                  dtaara(*lda)
      *
     D  ldpfcd                 1      1
      *
     D  ldfwedt               85     92  0
      *
     D  ldtwedt               99    106  0
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ***************************************************************************************
      * MAINLINE
      ***************************************************************************************
      *
      * Print headings
     C                   exsr      $h1hdr
      *
      * Read each record in the "header" workfile.
      *
     C     *loval        setll     tfp346
      *
     C                   dou       *in90 = *on                                  Main do
     C                   read      tfp346                                 90
     C                   if        *in90 = *off                                 If not EOF
     C                   exsr      $h2hdr
      *
     C                   move      no            first
      *
      * Print each detail line for this Item Structure Type Code
      *
     C     w1istycd      setll     tfp3460
     C                   dou       *in91 = *on                                  Do detail
     C     w1istycd      reade     tfp3460                                91
     C                   if        *in91 = *off                                 If more dtl
     C                   exsr      $l1dtl
     C                   endif                                                  If more dtl
     C                   enddo                                                  Do detail
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Main do
      *
      *---------------------------------------------------------------
      * EOF processing
      *---------------------------------------------------------------
      * If the file was empty, print the standard 'no data...' message
      * Otherwise, print the Report Total
      *
     C                   if        first = yes
     C                   except    nodata
     C                   else
     C                   exsr      $t1tot
     C                   endif
      *
     C                   seton                                        lr
      /EJECT
      *---------------------------------------------------------------
      * Print detail line
      *---------------------------------------------------------------
      *
     C     $l1dtl        begsr
      *
      * Accumulate values for Report Total
      *
     C                   add       w2volam       t1volam
     C                   add       w2mixam       t1mixam
     C                   add       w2adjam       t1adjam
     C                   add       w2scoam       t1scoam
     C                   add       w2tcoam       t1tcoam
     C                   add       w2payam       t1payam
      *
     C                   if        *inof = *on
     C                   exsr      $h1hdr
     C                   endif
      *
     C                   except    l1dtl
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Print report heading
      *---------------------------------------------------------------
      *
     C     $h1hdr        begsr
      *
     C                   except    h1hdr
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Print heading for Item Structure Type Code
      *---------------------------------------------------------------
      *
     C     $h2hdr        begsr
      *
     C                   if        *inof = *on
     C                   exsr      $h1hdr
     C                   endif
      *
     C                   except    h2hdr
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Total line
      *---------------------------------------------------------------
      *
     C     $t1tot        begsr
      *
     C                   if        *inof = *on
     C                   exsr      $h1hdr
     C                   endif
      *
     C                   except    t1tot
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Retrieve time for report heading.
      *
     C                   time                    rtime
      *
      *
      * This report is generated on-demand and also from the
      * Margin Adjustment Close function.
      *
     C                   select
     C                   when      ldpfcd = 'D'
     C                   eval      %subst(h1demand:13:9) = 'On-Demand'
      *
     C                   when      ldpfcd = 'F'
     C                   eval      %subst(h1demand:4:29) =
     C                             'Final Margin Adjustment Close'
      *
     C                   when      ldpfcd = 'P'
     C                   eval      %subst(h1demand:1:35) =
     C                             'Preliminary Margin Adjustment Close'
     C                   endsl
      *
      *
      * Hardcode the "based on" description for the Mix Adjustment.
      *
     C                   eval      h1ds = 'Mix Adjustment Based on Production +
     C                                     Data'
      *
      * Flip From/To Week-ending Dates into MMDDYY format.
      *
     C     *iso          move      ldfwedt       wkisodate
     C     *mdy          move      wkisodate     h1fwemdy
      *
     C     *iso          move      ldtwedt       wkisodate
     C     *mdy          move      wkisodate     h1twemdy
      *
     C                   endsr
      /EJECT
      *-------------------------------------------------------------------------------------
      * Report heading lines
      *-------------------------------------------------------------------------------------
      *
     Oprint198  e            h1hdr          1 01
     O                       sdpgm               10
     O                                          105 'MARGIN ADJUSTMENT RECAP'
     O                                          188 'DATE'
     O                       udate         y    198
      *
     O          e            h1hdr          1
     O                       sdusr               10
     O                       h1demand           113
     O                                          188 'TIME'
     O                       rtime              198 '  :  :  '
      *
      *
     O          e            h1hdr          1
     O                                          188 'PAGE'
     O                       page          z    198
      *
      *
     O          e            h1hdr          1
     O                                           22 'From week-ending date:'
     O                       h1fwemdy            32 '  /  /  '
      *
     O          e            h1hdr          2
     O                                           22 'To week-ending date:'
     O                       h1twemdy            32 '  /  /  '
      *
     O          e            h1hdr          0
     O                       h1ds                40
      *
      *-------------------------------------------------------------------------------------
      * Column headings
      *-------------------------------------------------------------------------------------
      *
     O          e            h1hdr       0  1
     O                                           69 'Volume'
     O                                           86 'Mix'
     O                                          103 'Total'
     O                                          131 'CoOwned Transfer Cost '
     O                                          137 'Amount'
     O                                          154 'Total'
      *
     O          e            h1hdr       0  1
     O                                           69 'Adjustment'
     O                                           86 'Adjustment'
     O                                          103 'Adjustment'
     O                                          115 '----- '
     O                                          137 'Producing Company ----'
     O                                          154 'Payment'
      *
     O          e            h1hdr       0  1
     O                                           69 'Amount'
     O                                           86 'Amount'
     O                                          103 'Amount'
     O                                          118 'SBF'
     O                                          135 'TF'
     O                                          154 'Amount'
      *
     O          e            h1hdr          0
     O                       dash               198
      *
      *-------------------------------------------------------------------------------------
      * Item Structure Type Code heading
      *-------------------------------------------------------------------------------------
      *
     O          e            h2hdr       2  0
     O                       w1desc              34
      *
     O          e            h2hdr       0  1
     O                       w1desc              34
      *
      *-------------------------------------------------------------------------------------
      * Detail line
      *-------------------------------------------------------------------------------------
      *
     O          e            l1dtl       1
     O                       w2desc              53
     O                       w2volam       k     70
     O                       w2mixam       k     87
     O                       w2adjam       k    104
     O                       w2scoam       k    121
     O                       w2tcoam       k    138
     O                       w2payam       k    155
      *
      *-------------------------------------------------------------------------------------
      * Total line
      *-------------------------------------------------------------------------------------
      *
     O          e            t1tot       1  1
     O                                           69 '--------------'
     O                                           86 '--------------'
     O                                          103 '--------------'
     O                                          120 '--------------'
     O                                          137 '--------------'
     O                                          154 '--------------'
      *
      *
     O          e            t1tot          1
     O                                           53 'Report Total:'
     O                       t1volam       k     70
     O                       t1mixam       k     87
     O                       t1adjam       k    104
     O                       t1scoam       k    121
     O                       t1tcoam       k    138
     O                       t1payam       k    155
      *
      *
      *-------------------------------------------------------------
      * No data message line
      *-------------------------------------------------------------
      *
     O          e            nodata      1
     O                                           19 'No data meets your'
     O                                           39 'selection criteria.'
