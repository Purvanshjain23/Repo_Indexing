      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      HOG PRODUCTION
      * PROGRAM:     HP667 - EXPENSE TICKET ALLOCATION EXCEPTION REPORT
      * PROGRAMMER:  LEANNE FEDOR
      * CREATED:     06/01/94
      *
      * FUNCTION: THIS PROGRAM PRINTS AN EXCEPTION REPORT OF EACH EXPENSE
      *           TICKET THAT CANNOT BE ALLOCATED.
      *
      *           THIS PROGRAM IS SUBMITTED BY HP467CL WHICH IS CALLED
      *           FROM HP467 THROUGH QCMDEXC.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 09/12/94  LEANNE FEDOR
      *           RECOMPILE ONLY. THE FOLLOWING FOUR FIELDS WERE
      *           REMOVED FROM HSP048-EXPENSE TICKET AND THE WORKFILE
      *           HSP367 USED IN TICKET ALLOCATION.
      *                 PIG DAYS ALLOCATED OVER
      *                 ALLOCATED TIME
      *                 G/L POST TIME
      *                 EOP POST TIME
      *
      * 02/01/95  LEANNE FEDOR
      *           CHANGED TO USE NAMING STANDARDS AND ADDED USER'S
      *           NAME TO TOP LEFT OF PAGE.
      *
      * 04/26/95  LEANNE FEDOR
      *           CHANGED FARM SITE CODE FROM ALPHA TO NUMERIC.
      *
      * 12/09/97  LEANNE FEDOR
      *           EXPENSE TICKET NUMBER FIELD RENAMED FROM 'XTNO' TO
      *           'XTSN'.
      *
      * 07/18/01  LeAnne Fedor
      *           Expense ticket amount expanded from 7,2 to 9,2.
      *           Nudged layout accordingly.
      *
      * 02/04/03  LeAnne Fedor
      *           Changed report to an 'exception' report--prints only tickets that
      *           could not be allocated.
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     FHSL367A   IF   E           K DISK
      *    EXPENSE TICKET WORKFILE
      *
     FQPRINT    O    F  198        PRINTER OFLIND(*INOF)
      *
      /EJECT
      ****************************************************************
      * INPUT SPECS
      ****************************************************************
      *
      *---------------------------------------------------------------
      * STANDARD PROGRAM STATUS DATA STRUCTURE
      *---------------------------------------------------------------
      *    EXTERNALLY DEFINED AS UTPGFR (RECORD FORMAT: PGMDSR)
     D PGMDS         ESDS                  EXTNAME(UTPGFR)
      *
      *----------------------------------------------------------------
      * LDA - REPORT SELECTIONS
      *----------------------------------------------------------------
      *
     D                UDS
     D  LDCYMD                 1     08  0
     D  LDEFFL                 9     09
      *
      *---------------------------------------------------------------
      *  NAMED CONSTANTS
      *---------------------------------------------------------------
      *
     D YES             C                   CONST('Y')
     D NO              C                   CONST('N')
     D notalloc        c                   'The following tickets could not be -
     D                                     allocated.'
      *
      *---------------------------------------------------------------
      * STANDALONE FIELDS
      *---------------------------------------------------------------
      *
      * Standard fields
      *
     D first           s              1    inz('Y')
     D rtime           s              6  0
     D dash            s            198    inz(*all'-')
      *
      *
      * Workfields for date manipulation
      *
     D wkisodate       s               D   datfmt(*iso)
      *
      *
      * Counters
      *
     D totalrecs       s              7  0
     D notrecs         s              7  0
      *
      *
      * Print fields
      *
     D href            s              5
     D hrcutmdy        s              6  0
     D hrtext          s             45
      *
     D l1bmdy          s              6  0
     D l1emdy          s              6  0
      *
     D t1recs          s              7  0
     D t1text          s             33
      *
      /EJECT
      ****************************************************************
      * MAIN LINE
      ****************************************************************
      *
      * Read all Expense Tickets, print those that could not be allocated.
      *
     C                   dou       *in90 = *on                                  MAIN DO
     C                   read      hsl367a                                90
     C                   if        *in90 = *off                                 If not EOF
      *
     C                   add       1             totalrecs
      *
      * If the ticket could not be allocated
      *
     C                   if        wtalfl = 'T'                                 If not allocated
      *
     C                   add       1             notrecs
      *
     C                   if        first = yes                                  IF FIRST
     C                   movel     notalloc      hrtext
     C                   except    hrhdr
     C                   movel     no            first
     C                   endif                                                  IF FIRST
      *
      * Flip dates from CCYYMMDD to MMDDYY
      *
     C     *iso          test(d)                 wtxbdt                 92
     C                   if        *in92 = *off                                 If OK date
     C                   move      wtxbdt        wkisodate
     C     *mdy          move      wkisodate     l1bmdy
     C                   endif                                                  If OK date
      *
     C     *iso          test(d)                 wtxedt                 92
     C                   if        *in92 = *off                                 If OK date
     C                   move      wtxedt        wkisodate
     C     *mdy          move      wkisodate     l1emdy
     C                   endif                                                  If OK date
      *
      * Print the expense ticket
      *
     C                   if        *inof = *on
     C                   except    hrhdr
     C                   endif
     C                   except    l1dtl
      *
     C                   endif                                                  If not allocated
     C                   endif                                                  If not EOF
     C                   enddo                                                  MAIN DO
      *
      *----------------------------------------------------------------
      * EOF PROCESSING
      *----------------------------------------------------------------
      *
      * Total tickets
     C                   if        *inof = *on or first = yes
     C                   except    hrhdr
     C                   else
     C                   except    blank
     C                   endif
      *
     C                   z-add     totalrecs     t1recs
     C                   eval      t1text = 'Total tickets'
     C                   except    t1tot
      *
      * Allocated tickets
     C                   if        *inof = *on
     C                   except    hrhdr
     C                   endif
     C     totalrecs     sub       notrecs       t1recs
     C                   eval      t1text = 'Allocated tickets'
     C                   except    t1tot
      *
      * Tickets not allocated
      *
     C                   if        *inof = *on
     C                   except    hrhdr
     C                   endif
     C                   z-add     notrecs       t1recs
     C                   eval      t1text = 'Tickets not allocated'
     C                   except    t1tot
      *
     C                   seton                                        lr
      /EJECT
      *---------------------------------------------------------------
      * INITIALIZATION SUBROUTINE
      *---------------------------------------------------------------
      *
     C     *INZSR        BEGSR
      *
      * SETUP HEADINGS FOR USER'S REPORT SELECTION
      *
     C     LDEFFL        IFEQ      'E'
     C                   MOVEL(P)  'EDIT'        HREF
     C                   ELSE
     C                   MOVEL(P)  'FINAL'       HREF
     C                   ENDIF
      *
      * CONVERT THE USER'S CUTOFF DATE FROM CCYYMMDD TO MMDDYY
      *
     C     *iso          test(d)                 ldcymd                 92
     C                   if        *in92 = *off                                 If OK date
     C                   move      ldcymd        wkisodate
     C     *mdy          move      wkisodate     hrcutmdy
     C                   endif                                                  If OK date
      *
      * Retrieve time for report heading.
      *
     C                   time                    rtime
      *
     C                   ENDSR
      /EJECT
      *-------------------------------------------------------------
      * REPORT HEADING LINES
      *-------------------------------------------------------------
      *
     OQPRINT    E            HRHDR          1 01
     O                       SDPGM               10
     O                                          105 'HOG PRODUCTION SYSTEM'
     O                                          189 'DATE'
     O                       UDATE         Y    198
      *
     O          E            HRHDR          1
     O                       SDUSR               10
     O                                           88 'EXPENSE TICKET'
     O                                          109 'ALLOCATION EXCEPTION'
     O                                          116 ' REPORT'
     O                                          189 'TIME'
     O                       RTIME              198 '  :  :  '
      *
     O          E            HRHDR          2
     O                                           12 'CUTOFF DATE:'
     O                       HRCUTMDY      Y     22
     O                       HREF                97
     O                                          189 'PAGE'
     O                       PAGE          Z    198
      *
      *-------------------------------------------------------------
      * COLUMN HEADING LINES
      *-------------------------------------------------------------
      *
     O          E            HRHDR          1
     O                                           19 'BEGIN DATE'
     O                                           35 'ALLOC'
     O                                          100 'ORIGINATING'
     O                                          119 'ALLOCATED'
     O                                          138 '-PRODUCTION-'
      *
     O          E            HRHDR          1
     O                                            7 'TICKET'
     O                                           19 'END DATE'
     O                                           28 'EXPENSE'
     O                                           36 'METHOD'
     O                                           49 'AMOUNT'
     O                                           57 'HEAD'
     O                                           71 'POUNDS'
     O                                           83 'QUANTITY'
      *
     O                                          102 'BUSINESS OFFICE'
     O                                          122 'BUSINESS OFFICE'
     O                                          137 'PHASE  TYPE'
     O                                          150 'DIVISION'
     O                                          160 'REGION'
     O                                          169 'FARM'
     O                                          181 'BUILDING'
     O                                          197 'COST CENTER'
      *
     O          E            HRHDR          1
     O                       DASH               198
      *
     O          E            HRHDR          0
     O                       HRTEXT              45
      *
      *-------------------------------------------------------------
      * EXPENSE TICKET DETAIL LINE
      *-------------------------------------------------------------
      *
     O          E            L1DTL       2  0
     O                       WTXTSN        M      8
     O                       L1BMDY        Y     19
     O                       WTEXCD              27
     O                       WTAMCD              35
     O                       WTXTAM        K     50
     O                       WTXTHD        K     58
     O                       WTXTLB        K     72
     O                       WTXTQT        K     84
      *
     O                       WTBOBO              97
     O                       WTALBO             117
     O                       WTPPCD             131
     O                       WTPTCD             138
     O                       WTDVCD             149
     O                       WTRGCD             159
     O                       WTFSCD        M    170
     O                       WTBLCD             179
     O                       WTCJD              197
      *
     O          E            L1DTL       1  1
     O                       L1EMDY        Y     19
      *
      *
      *-------------------------------------------------------------
      * Blank line
      *-------------------------------------------------------------
      *
     O          E            blank       1
      *
      *-------------------------------------------------------------
      * Totals
      *-------------------------------------------------------------
      *
     O          E            t1tot       1
     O                       t1recs        j     10
     O                       t1text              45
      *
