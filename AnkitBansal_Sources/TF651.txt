      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Triumph Foods
      * PROGRAM:     TF651
      * TITLE:       Inventory: Sales Timing Allocation
      * PROGRAMMER:  LeAnne Ramsey
      * CREATED:     03/29/07
      *
      * FUNCTION:  Batch program to print a record from the Sales Timing Allocation file.
      *
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 05/21/07  LeAnne Ramsey
      *           We are breaking the Inventory Change Section into 2 sections:
      *             NOT Co-Owned Products
      *             Co-Owned Products
      *           And, adding a new Total Line that combines the 2 sections.
      *
      * 07/17/07  LeAnne Ramsey
      *           Changed Not CoOwned section per Damon Ginther.
      *
      * 08/08/07  LeAnne Ramsey
      *           This function will now exclude all "CoOwned" products.
      *           Removed all CoOwned fields/logic.
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Ftfp051    if   e           k disk
      *  Sales timing allocation
      *
      *
     Fprint132  o    f  132        printer oflind(*inof)
      *
      /EJECT
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Standard fields
      *
     d rtime           s              6  0
      *
      *
      * Workfields for date manipulation
      *
     D wkisodate       s               D   datfmt(*iso)
      *
      *
      * Print fields
      *
     d h1demand        s             27
      *
     D l1wemdy1        s              6  0
     D l1wemdy2        s              6  0
     D l1wemdy3        s              6  0
     D l1wemdy4        s              6  0
      *
      ****************************************************************
      * ARRAYS AND TABLES
      ****************************************************************
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *
      *---------------------------------------------------------------
      * Local data area.
      *---------------------------------------------------------------
      *
     Dlda             uds                  dtaara(*lda)
     D  ldpfcd                 1      1
     D  ldyr                   2      5  0
     D  ldwk                   6      7  0
      *
     D  ldwedt                29     36  0
     D  ldwemdy               44     49  0
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ***************************************************************************************
      * MAINLINE
      ***************************************************************************************
      *
     C                   except    h1hdr
      *
      * This function prints a single record from the file.
      *
     C     ldwedt        chain     tfp051                             92
     C                   if        *in92 = *on                                  If no hit
     C                   except    nodata
     C                   else
      *
      * Set up the 4 week-ending dates and print Cold Carcass Pounds lines.
      *
     C     *iso          test(d)                 tawk1wedt              92
     C                   if        *in92 = *off
     C                   move      tawk1wedt     wkisodate
     C     *mdy          move      wkisodate     l1wemdy1
     C                   endif
      *
     C     *iso          test(d)                 tawk2wedt              92
     C                   if        *in92 = *off
     C                   move      tawk2wedt     wkisodate
     C     *mdy          move      wkisodate     l1wemdy2
     C                   endif
      *
     C     *iso          test(d)                 tawk3wedt              92
     C                   if        *in92 = *off
     C                   move      tawk3wedt     wkisodate
     C     *mdy          move      wkisodate     l1wemdy3
     C                   endif
      *
     C     *iso          test(d)                 tawedt                 92
     C                   if        *in92 = *off
     C                   move      tawedt        wkisodate
     C     *mdy          move      wkisodate     l1wemdy4
     C                   endif
      *
      * Print all lines
     C                   except    l1dtl
      *
     C                   endif                                                  If no hit
      *
     C                   seton                                        lr
      /EJECT
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Retrieve time for report heading.
      *
     C                   time                    rtime
      *
      * This report is generated on-demand and also from the Inventory Close
      * function.
      *
     C                   select
     C                   when      ldpfcd = 'D'
     C                   eval      %subst(h1demand:10:9) = 'On-Demand'
      *
     C                   when      ldpfcd = 'F'
     C                   eval      %subst(h1demand:4:21) =
     C                             'Final Inventory Close'
      *
     C                   when      ldpfcd = 'P'
     C                   eval      %subst(h1demand:1:27) =
     C                             'Preliminary Inventory Close'
     C                   endsl
      *
     C                   endsr
      /EJECT
      *-------------------------------------------------------------
      * Report heading lines
      *-------------------------------------------------------------
      *
     Oprint132  e            h1hdr          1 01
     O                       sdpgm               10
     O                                           57 'SALES TIMING ALLOCATION'
     O                                           65 ' LISTING'
     O                                           92 'DATE'
     O                       udate         y    102
      *
     O          e            h1hdr          1
     O                       sdusr               10
     O                       h1demand            63
     O                                           92 'TIME'
     O                       rtime              102 '  :  :  '
      *
      *
     O          e            h1hdr          1
     O                                           92 'PAGE'
     O                       page          z    102
      *
     O          e            h1hdr          1
     O                                           17 'Week-ending date:'
     O                       ldwemdy             27 '  /  /  '
     O          e            h1hdr          2
     O                                           17 'Year/week:'
     O                       ldyr          z     23
     O                       ldwk          z     27
      *
      *-------------------------------------------------------------
      * Column headings
      *-------------------------------------------------------------
      *
     O          e            h1hdr       2  1
     O                                           56 'COLD CARCASS POUNDS'
     O          e            h1hdr       0  1
     O                                           24 'Week'
     O                                           42 'SBF'
     O                                           58 'TF'
     O                                           74 'TOTAL'
      *
     O          e            h1hdr       0  1
     O                                           42 '--------------'
     O                                           58 '--------------'
     O                                           74 '--------------'
      *
      * Detail line for Cold Carcass Pounds
      *
     O          e            l1dtl          1
     O                       l1wemdy1            26 '  /  /  '
     O                       taswk1cclb    k     43
     O                       tatwk1cclb    k     59
      *
     O          e            l1dtl          1
     O                       l1wemdy2            26 '  /  /  '
     O                       taswk2cclb    k     43
     O                       tatwk2cclb    k     59
      *
     O          e            l1dtl          1
     O                       l1wemdy3            26 '  /  /  '
     O                       taswk3cclb    k     43
     O                       tatwk3cclb    k     59
      *
     O          e            l1dtl          1
     O                       l1wemdy4            26 '  /  /  '
     O                       taswk4cclb    k     43
     O                       tatwk4cclb    k     59
      *
      * Detail lines for remaining values
      *
     O          e            l1dtl       0  1
     O                                           42 '--------------'
     O                                           58 '--------------'
      *
     O          e            l1dtl          1
     O                                           26 'Average'
     O                       tas4avcclb    k     43
     O                       tat4avcclb    k     59
     O                       taa4avcclb    k     75
      *
     O          e            l1dtl          2
     O                       tasccpc             43 '   .    %'
     O                       tatccpc             59 '   .    %'
      *
     O          e            l1dtl          1
     O                                           26 'Inventory Change Actual'
     O                       tasicactam    k     43
     O                       taticactam    k     59
     O                       taaicactam    k     75
      *
     O          e            l1dtl          1
     O                                           26 'Inventory Change Allocated'
     O                       tasicallam    k     43
     O                       taticallam    k     59
      *
     O          e            l1dtl          1
     O                                           42 '--------------'
     O                                           58 '--------------'
      *
     O          e            l1dtl          2
     O                                           26 'Difference'
     O                       tasicdifam    k     43
     O                       taticdifam    k     59
      *
      *-------------------------------------------------------------
      * No data message line
      *-------------------------------------------------------------
      *
     O          e            nodata      1
     O                                           19 'No data meets your'
     O                                           39 'selection criteria.'
