      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      TF Fees & Payments   -- COPIED FROM TF244 TO UPLOAD 440 ITEM STD COSTS
      * PROGRAM:     Margin: Update Costs/Yield in Item Standard Cost
      * PROGRAMMER:  LeAnne Ramsey
      * DATE:        06/01/17
      *
      * Function: This program allows the users to DWL costs from their Spreadsheets
      *           to directly update the cost file PUACCPP  (INSTEAD OF POAXCPP)
      *           The "costs" will come from the user's file/
      *           spreadsheet. They get the costs into file TFP244 which this
      *           program reads updating the SYNON file 'Item Standard Cost'.
      *
      *           There is a submission screen for this program which is on the
      *           menu.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 10/08/07  LeAnne Ramsey
      *           Added logic to conditionally update based on user selection:
      *             M=Meat Cost/Yield %
      *             P=Packaging Cost
      *             L=Labor Cost
      *
      * 01/22/08  Alice Brownfield
      *           comment and text changes only --  this programs was originally
      *             thought to be a temporary program, but we have decided to keep
      *             it permanently based on the enhancements that we have made to it.
      *
      * 03/12/08  Alice Brownfield
      *           Change the Labor upload to update both the Std Incremental Labor
      *           as it was alreadying doing, but also the Std Total Labor which
      *           is something new that we added.
      *
      * 03/18/08  LeAnne Ramsey
      *           Recompile only. Synon file POAXCPP was recompiled.
      *
      * 03/20/08  Alice Brownfield
      *           Recompile only. recreated POAXCPDWL in production.
      *
      * 05/20/08  Alice Brownfield
      *           E00159-Add Ingredients to the STD Cost DWL.
      *                 -Also changed Meat Cost & Packaging to NOT
      *                   update if the field is ZERO.
      *
      * 09/08/08  LeAnne Ramsey
      *           Since this function does not appear to be temporary as expected, we
      *           have changed the upload file to have a DDS that matches, somewhat,
      *           some naming standards.
      *
      * 06/29/17  Danny Nguyen  T10906-Meat Cost on Other Cost
      *           Added Other Cost 'O' to update Std Other Cost/CWT field in
      *           Item Standard Cost NotTFS file.
      /eject
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Ftfp244    if   e           k disk
      *  Uploaded standard costs
      *
      *
     F*poaxcpl1  uf   e           k disk
     Fpuaccpl1  uf   e           k disk
      *  Item standard costs- not tfs company 440
      *
      /eject
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      *---------------------------------------------------------------
      * CONSTANTS
      *---------------------------------------------------------------
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Workfields
      *
     D wkcocd          s                   like(acaic3) inz(440)                     init to 440
     D wkitem          s                   like(achgcd)
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *---------------------------------------------------------------
      *  Local data area
      *---------------------------------------------------------------
      *
     d lda            uds                  dtaara(*lda)
     d  ldupcd                 1      1
      *
      /EJECT
      *************************************************************************************
      * Mainline
      *************************************************************************************
      *
      * Read each record in the Uploaded Standard Costs file.
      * Process records that have an Item value.
      *
     C                   dou       *in90 = *on                                  Main do
     C                   read      tfp244                                 90
     C                   if        *in90 = *off and u1item <> 0                 If not EOF
      *
      * Retrieve the SYNON record using a key of:
      *   Company (hardcoded to 440)
      *   Item    (moved from a 5,0 field in the upload file to a 7,0 workfield)
      *   Date    (from the upload file)
      *
     C                   z-add     u1item        wkitem
     C     key01         chain     puaccpl1                           92
     C                   if        *in92 = *off                                 If hit
      *
     C                   select
      * Meat
     C                   when      ldupcd = 'M'
     C                   if        u1cost <> 0
     C                   z-add     u1cost        acfrpr                         Meat Cost
     C                   endif
     C                   if        u1other <> 0
     C                   z-add     u1other       acfcpc                         Yield %
     C                   endif
      * Packaging
     C                   when      ldupcd = 'P'
     C                   if        u1cost <> 0
     C                   z-add     u1cost        acedpr                         Packaging
     C                   endif
      * Labor
     C                   when      ldupcd = 'L'
      *      Incremental
     C                   if        u1cost <> 0
     C                   z-add     u1cost        aceepr                         Labor - Inc
     C                   endif
      *      Labor Total
     C                   if        u1other <> 0
     C                   z-add     u1other       acfupr                         Labor - Tot
     C                   endif
      * Ingredients
     C                   when      ldupcd = 'I'
     C                   if        u1cost <> 0
     C                   z-add     u1cost        acefpr                         Ingredients
     C                   endif
10906 * Other
  |  C                   when      ldupcd = 'O'
  |  C                   if        u1cost <> 0
  |  C                   z-add     u1cost        acegpr                         Other
10906C                   endif
      *
     C                   endsl
      *
     C                   update    @accpzy
     C                   endif                                                  If hit
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Main do
      *
      * EOF processing
      *
     C                   seton                                        lr
      /eject
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
     C     key01         klist
     C                   kfld                    wkcocd
     C                   kfld                    wkitem
     C                   kfld                    u1syndt
      *
     C                   endsr
      /eject
