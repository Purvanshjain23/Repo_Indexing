      *****************  RPG PROGRAM HEADING  ************************
      * SYSTEM:      HOG PRODUCTION
      * PROGRAM:     HP688
      * TITLE:       CLOSED GROUP PERFORMANCE REPORT -- RANKED
      * PROGRAMMER:  MARY PALMER
      * CREATED:     10/20/94
      *
      *  FUNCTION:   THIS PROGRAM PRINTS, IN USER-SELECTED RANKING, THE
      *              DATA IN WORKFILES THAT ARE CREATED IN HP388:
      *                   HSP387--ONE RECORD FOR EACH HOG GROUP
      *                   HSP389--ONE TOTAL RECORD FOR EACH PHASE
      *
      *           THIS PROGRAM IS SUBMITTED BY HP482CL WHICH IS CALLED
      *           FROM HP482 THROUGH QCMDEXC.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 10/25/94  MARY PALMER
      *           CHANGED AVG LBS SOLD TO AVERAGE POUNDS PER HEAD MOVED
      *
      * 11/21/94  MARY PALMER
      *           REPLACED CLOSED DATE WITH DISPOSED DATE
      *
      * 01/23/95  LEANNE FEDOR
      *           INCREASED ACCUMULATOR FIELD SIZES, ETC.
      *
      * 04/24/95  LEANNE FEDOR
      *           CHANGED FARM SITE CODE FROM ALPHA TO NUMERIC.
      *
      * 08/30/95  LEANNE FEDOR
      *           THE LOGIC FOR REPORT HEADINGS WAS CHANGED TO PRINT
      *           THE NEW REPORT SELECTION OF 'CELL'.
      *
      * 09/12/95  LEANNE FEDOR
      *           CHANGED TO THE COLUMN HEADING 'DEATH LOSS%' TO BE
      *           'MORTALITY %' AND 'MARGIN' TO BE 'MARGIN PER CWT' SO
      *           THAT THEY WOULD BE CONSISTENT WITH THE COMPANION
      *           REPORT HP682-CLOSED GROUP PERFORMANCE REPORT--NOT
      *           RANKED.
      *
      * 11/01/95  LEANNE FEDOR
      *           CHANGED HEADING TO REFLECT THE NEW SELECTION CRITERIA
      *           OF 'GROUP TYPE'.
      *
      * 04/12/96  LEANNE FEDOR
      *           CHANGED HEADING TO REFLECT THE NEW SELECTION CRITERIA
      *           OF 'BREED STOCK'.
      *
      * 08/09/96  LEANNE FEDOR
      *           RECOMPILE ONLY. ADDED TWO NEW FIELDS TO WORKFILES:
      *                ADJUSTED FEED CONVERSION
      *                AVERAGE CALORIES PER POUND OF FEED
      *
      * 08/14/96  LEANNE FEDOR
      *           RECOMPILE ONLY. ADDED A NEW FIELD TO WORKFILES:
      *                ADJUSTED AVERAGE DAILY GAIN
      *
      * 04/13/98  LeAnne Fedor
      *           Recompile only. New field 'state' added to farm site file.
      *
      * 08/17/00  LeAnne Fedor
      *           Recompile only. New field 'adjusted transferred in head' was
      *           added to workfiles. Also, the cull% and mortality% values in
      *           these workfiles are now calculated with the new 'adjusted
      *           transferred in head' instead of the original 'transferred in' head.
      *
      * 06/05/01  LeAnne Fedor
      *           Recompile only. New field 'multisite' added to Farm Site file.
      *
      * 06/26/01  LeAnne Fedor
      *           Three fields (manager/supervisor/multisite) renamed in Farm Site file.
      *           Manager and supervisor fields removed from Hog Group file.
      *
      * 09/04/01  LeAnne Fedor
      *           Recompile only. New 'square feet' fields added to Farm Site file.
      *
      * 10/16/13  LeAnne Ramsey (E2831)
      *           Recompile only. Added field 'MTech Reference'.
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     FHSP009    IF   E           K DISK
      *    BUSINESS OFFICE            (KEY: BOBOBO)
      *
     FHSP010    IF   E           K DISK
      *    PRODUCTION TYPE            (KEY: PTPTCD)
      *
     FHSP011    IF   E           K DISK
      *    PRODUCTION PHASE           (KEY:PPPPCD)
      *
     FHSP018    IF   E           K DISK
      *    FARM SITE                  (KEY: FSFSBO FSFSCD)
      *
     FHSP025    IF   E           K DISK
      *    GROUP TYPE               (KEY: GTGTCD)
      *
     FHSP031    IF   E           K DISK
      *    BREED STOCK              (KEY: BSBSCD)
      *
     FHSP387    IF   E           K DISK
      *    WORKFILE FOR GROUPS
      *    (KEYED IN OPNQRY)
      *
     FHSP389    IF   E           K DISK
      *    WORKFILE OF PHASE TOTALS   (KEY: WPPPCD)
      *
     FQPRINT    O    F  198        PRINTER OFLIND(*INOF)
      /EJECT
      ****************************************************************
      * TABLE AND ARRAY SPECIFICATIONS
      ****************************************************************
      *
      *---------------------------------------------------------------
      *  PERFORMANCE RANKING OPTIONS RELATED TABLE OF CODES AND
      *  DESCRIPTIONS
      *---------------------------------------------------------------
      *
     D TABA            S              1    DIM(7) CTDATA PERRCD(1) ASCEND
     D TABB            S             20    DIM(7) ALT(TABA)
      /EJECT
      ****************************************************************
      * INPUT SPECS
      ****************************************************************
      *
      *---------------------------------------------------------------
      * STANDARD PROGRAM STATUS DATA STRUCTURE
      *---------------------------------------------------------------
      *    EXTERNALLY DEFINED AS UTPGFR (RECORD FORMAT: PGMDSR)
     D PGMDS         ESDS                  EXTNAME(UTPGFR)
      *
      * DATE FORMAT FOR FLIPPING DATES
      *
     D                 DS
     D  WKCYMD                 1      8  0
     D  WKCC                   1      2  0
     D  WKYY                   3      4  0
     D  WKMMDD                 5      8  0
      *
      *---------------------------------------------------------------
      *  LDA -  WITH THE REQUESTED PRINT SELECTIONS
      *---------------------------------------------------------------
     D                UDS
     D  LDFSBO                 1      5
     D  LDFROM                 6     13  0
     D  LDTO                  14     21  0
     D  LDPTCD                22     26
     D  LDPPCD                27     31
     D  LDFSCD                32     36  0
     D  LDOPTN                37     37
     D  LDCELL                50     51  0
     D  LDGTCD                52     52
     D  LDBSCD                53     57
      *
      *---------------------------------------------------------------
      *  NAMED CONSTANTS
      *---------------------------------------------------------------
      *
     D YES             C                   CONST('Y')
     D NO              C                   CONST('N')
     D ALL             C                   CONST('ALL')
      /EJECT
      ****************************************************************
      * MAIN LINE
      ****************************************************************
      *
      * READ THE GROUP WORKFILE IN ORDER OF THE RANKING SELECTED
      * BY THE USER.
      *
     C     *IN99         DOUEQ     *ON                                          MAIN DO
     C                   READ      HSP387                                 99
     C     *IN99         IFEQ      *OFF                                         IF NOT EOF
      *
     C     FIRST         IFEQ      YES
     C                   MOVEL     NO            FIRST
     C                   MOVEL     WGPPCD        SVPPCD
     C                   EXSR      $PPDS
     C                   EXCEPT    HRHDR
     C                   EXCEPT    HPHDR
     C                   ENDIF
      *
      * IF THERE IS A BREAK IN THE PRODUCTION PHASE
      *    PRINT PRODUCTION PHASE TOTALS
      *    GET NEW PRODUCTION PHASE DESCRIPTION
      *    PRINT REPORT HEADINGS
      *    SAVE THE PRODUCTION PHASE
      *
     C     WGPPCD        IFNE      SVPPCD                                       IF PP BREAK
     C                   EXSR      $PPTOT
     C                   EXSR      $PPDS
     C                   EXSR      $OFLOW
     C                   MOVEL     WGPPCD        SVPPCD
     C                   ENDIF                                                  IF PP BREAK
      *
      * FLIP THE DISPOSED DATE TO MDY FROM CCYYMMDD
      *
     C                   Z-ADD     WGDSDT        WKCYMD
     C                   MOVEL     WKMMDD        DTMDY
     C                   MOVE      WKYY          DTMDY
      *
      * TEST FOR DOLLAR VALUE IN NET AMOUNT FIELD TO CONTROL
      * PRINTING
      *
     C     WGN$CW        IFEQ      0
     C                   SETON                                        85
     C                   ELSE
     C                   SETOFF                                       85
     C                   ENDIF
      *
     C     *INOF         IFEQ      *ON
     C                   EXSR      $OFLOW
     C                   ENDIF
      *
      * PRINT HOG GROUP LINE
      *
     C                   EXCEPT    DTLINE
      *
     C                   ENDIF                                                  IF NOT EOF
     C                   ENDDO                                                  MAIN DO
      *
      *---------------------------------------------------------------
      * EOF PROCESSING
      *---------------------------------------------------------------
      *  IF NO DETAIL RECORDS MATCHED THE SELECTION CRITERIA, THEN
      *  PRINT THE STANDARD 'NO DATA...' ERROR MESSAGE
      *
     C     FIRST         IFEQ      YES
     C                   EXCEPT    HRHDR
     C                   EXCEPT    NODATA
     C                   ELSE
      *
      * PRINT TOTALS FOR LAST PRODUCTION PHASE
      *
     C                   EXSR      $PPTOT
     C                   ENDIF
      *
     C                   SETON                                        LR
      /EJECT
      *****************************************************************
      * $PPTOT - CONTROL BREAK IN PRODUCTION PHASE
      *****************************************************************
      *
     C     $PPTOT        BEGSR
      *
      * RETRIEVE PRODUCTION PHASE TOTALS FROM WORKFILE
      *
     C     SVPPCD        CHAIN     HSP389                             91
     C     *IN91         IFEQ      *OFF                                          IF FOUND
      *
      * CHECK TO SEE IF NET SALES ARE ZERO AND PRINT N/A ON REPORT
      *
     C     WPN$CW        IFEQ      0
     C                   SETON                                        87
     C                   ELSE
     C                   SETOFF                                       87
     C                   ENDIF
      *
      * PRINT PRODUCTION PHASE TOTAL
      *
     C     *INOF         IFEQ      *ON
     C                   EXSR      $OFLOW
     C                   ENDIF
      *
     C                   EXCEPT    TPTOT
     C                   ENDIF                                                    IF FOUND
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      *  $OFLOW - OVERFLOW
      *----------------------------------------------------------------
      *
      * AT OVERFLOW, PRINT HEADINGS FOR THE REPORT AND THE PHASE
      *
     C     $OFLOW        BEGSR
      *
     C                   EXCEPT    HRHDR
     C                   EXCEPT    HPHDR
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $PPDS - RETRIEVE PRODUCTION PHASE DESCRIPTION
      *----------------------------------------------------------------
      *
     C     $PPDS         BEGSR
      *
      * SETON INDICATOR 88 TO CONTROL PRINTING
      *
     C     WGPPCD        IFEQ      'NUR  '
     C                   SETON                                        88
     C                   ELSE
     C                   SETOFF                                       88
     C                   ENDIF
      *
     C                   MOVE      *BLANKS       HPPPDS
     C     WGPPCD        CHAIN     HSP011                             91
     C     *IN91         IFEQ      *OFF                                         IF FOUND
     C                   MOVEL(P)  PPPPDS        HPPPDS
     C                   ELSE
     C                   MOVEL(P)  'UNKNOWN'     HPPPDS
     C                   ENDIF                                                  IF FOUND
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $DATE -  MANIPULATE DATES WITH DATE UTILITY
      *----------------------------------------------------------------
      *
     C     $DATE         BEGSR
      *
     C                   CALL      'UT80060R'
     C                   PARM                    PFRM8             8 0
     C                   PARM                    PTO8              8 0
     C                   PARM                    PFRFMT            4
     C                   PARM                    PTOFMT            4
     C                   PARM                    PDAY              4 0
     C                   PARM                    PCODE             1
     C                   PARM                    PRTRN             1
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * *INZSR - INITIALIZATION SUBROUTINE
      *----------------------------------------------------------------
      *
     C     *INZSR        BEGSR
      *
      * DEFINE PRINT FIELDS FOR THE REPORT HEADING
      *
     C     *LIKE         DEFINE    BOBOBO        HRFSBO
     C     *LIKE         DEFINE    BOBODS        HRBODS
     C     *LIKE         DEFINE    PPPPCD        HRPPCD
     C     *LIKE         DEFINE    PPPPDS        HRPPDS
     C     *LIKE         DEFINE    PTPTCD        HRPTCD
     C     *LIKE         DEFINE    PTPTDS        HRPTDS
     C     *LIKE         DEFINE    FSFSNM        HRFSDS
     C     *LIKE         DEFINE    BSBSCD        HRBSCD
     C     *LIKE         DEFINE    BSBSDS        HRBSDS
     C                   MOVE      *BLANK        HROPDS           20
     C                   MOVE      *BLANK        HRGTCD            3
     C     *LIKE         DEFINE    GTGTDS        HRGTDS
      *
      * DEFINE PRINT FIELDS FOR THE PRODUCTION PHASE HEADING
      *
     C     *LIKE         DEFINE    PPPPDS        HPPPDS
      *
      * DEFINE PRINT FIELDS FOR THE DETAIL LINE
      *
     C                   Z-ADD     0             DTMDY             6 0
      *
      * DEFINE SAVE FIELDS FOR CONTROL BREAKS
      *
     C     *LIKE         DEFINE    WGPPCD        SVPPCD
      *
      * FLIP THE 'FROM' DATE TO MDY FORMAT FOR REPORT HEADING
      *
     C                   Z-ADD     LDFROM        PFRM8
     C                   Z-ADD     0             PTO8
     C                   MOVEL     'CYMD'        PFRFMT
     C                   MOVEL     'MDY '        PTOFMT
     C                   Z-ADD     0             PDAY
     C                   MOVE      *BLANK        PCODE
     C                   MOVE      *BLANK        PRTRN
     C                   EXSR      $DATE
     C                   Z-ADD     PTO8          HRFMDY            6 0
      *
      * FLIP 'TO' DATE TO MDY FORMAT FOR REPORT HEADING
      *
     C                   Z-ADD     LDTO          PFRM8
     C                   Z-ADD     0             PTO8
     C                   MOVEL     'CYMD'        PFRFMT
     C                   MOVEL     'MDY '        PTOFMT
     C                   Z-ADD     0             PDAY
     C                   MOVE      *BLANK        PCODE
     C                   MOVE      *BLANK        PRTRN
     C                   EXSR      $DATE
     C                   Z-ADD     PTO8          HRTMDY            6 0
      *
      * GET BUSINESS OFFICE DESCRIPTION
      *
     C     LDFSBO        IFEQ      *BLANK                                       IF ALL BO
     C                   MOVEL     ALL           HRFSBO
     C                   ELSE
     C                   MOVEL     LDFSBO        HRFSBO
     C     LDFSBO        CHAIN     HSP009                             92
     C     *IN92         IFEQ      *OFF                                         IF FOUND
     C                   MOVEL     BOBODS        HRBODS
     C                   ELSE
     C                   MOVEL     'UNKNOWN'     HRBODS
     C                   ENDIF                                                  IF FOUND
     C                   ENDIF                                                  IF ALL BO
      *
      * GET PRODUCTION TYPE DESCRIPTION
      *
     C     LDPTCD        IFEQ      *BLANK                                       IF ALL TYPE
     C                   MOVEL     ALL           HRPTCD
     C                   ELSE
     C     LDPTCD        CHAIN     HSP010                             93
     C                   MOVEL     LDPTCD        HRPTCD
     C     *IN93         IFEQ      *OFF                                         IF FOUND
     C                   MOVEL     PTPTDS        HRPTDS
     C                   ELSE
     C                   MOVEL     'UNKNOWN'     HRPTDS
     C                   ENDIF                                                  IF FOUND
     C                   ENDIF                                                  IF ALL TYPE
      *
      * CELL
     C     LDCELL        IFEQ      0
     C                   SETON                                        81
     C                   ENDIF
      *
      *
      * RETRIEVE FARM SITE NAME
      *
     C     LDFSCD        IFEQ      0                                            IF ALL FARM
     C                   SETON                                        89
     C                   ELSE
     C     LDFSCD        CHAIN     HSP018                             95
     C     *IN95         IFEQ      *OFF                                         IF FOUND
     C                   MOVEL     FSFSNM        HRFSDS
     C                   ELSE
     C                   MOVEL     'UNKNOWN'     HRFSDS
     C                   ENDIF                                                  IF FOUND
     C                   ENDIF                                                  IF ALL FARM
      *
      * GET THE PRODUCTION PHASE DESCRIPTION
      *
     C     LDPPCD        IFEQ      *BLANK                                       IF ALL PHASE
     C                   MOVEL     ALL           HRPPCD
     C                   ELSE
     C                   MOVEL     LDPPCD        HRPPCD
     C     LDPPCD        CHAIN     HSP011                             94
     C     *IN94         IFEQ      *OFF                                         IF FOUND
     C                   MOVEL     PPPPDS        HRPPDS
     C                   ELSE
     C                   MOVEL     'UNKNOWN'     HRPPDS
     C                   ENDIF                                                  IF FOUND
     C                   ENDIF                                                  IF ALL PHASE
      *
      * GET GROUP TYPE DESCRIPTION
      *
     C     LDGTCD        IFNE      *BLANK                                       IF ENTERED
     C     LDGTCD        CHAIN     HSP025                             93
     C                   MOVEL(P)  LDGTCD        HRGTCD
     C     *IN93         IFEQ      *OFF                                         IF FOUND
     C                   MOVEL     GTGTDS        HRGTDS
     C                   ELSE
     C                   MOVEL     'UNKNOWN'     HRGTDS
     C                   ENDIF                                                  IF FOUND
     C                   ELSE
     C                   MOVEL(P)  ALL           HRGTCD
     C                   ENDIF                                                  IF ENTERED
      *
      * GET BREED STOCK DESCRIPTION
      *
     C     LDBSCD        IFNE      *BLANK                                       IF ENTERED
     C     LDBSCD        CHAIN     HSP031                             92
     C                   MOVEL(P)  LDBSCD        HRBSCD
     C     *IN92         IFEQ      *OFF                                         IF FOUND
     C                   MOVEL     BSBSDS        HRBSDS
     C                   ELSE
     C                   MOVEL     'UNKNOWN'     HRBSDS
     C                   ENDIF                                                  IF FOUND
     C                   ELSE
     C                   MOVEL(P)  ALL           HRBSCD
     C                   ENDIF                                                  IF ENTERED
      *
      * RETRIEVE THE PERFORMANCE RANKING OPTION DESCRIPTION
      *
     C     LDOPTN        LOOKUP    TABA          TABB                     94
     C     *IN94         IFEQ      *ON                                          IF FOUND
     C                   MOVEL     TABB          HROPDS
     C                   ELSE
     C                   MOVEL     'UNKNOWN'     HROPDS
     C                   ENDIF                                                  IF FOUND
      *
      * RETRIEVE TIME FOR REPORT HEADING
      * SET UP A FIRST TIME FLAG
      * SET UP A DASHED LINE FOR UNDERLINING THE HEADING
      *
     C                   TIME                    RTIME             6 0
     C                   MOVEL     YES           FIRST             1
     C                   MOVEL     *ALL'-'       DASH            198
      *
     C                   ENDSR
      /EJECT
      *-------------------------------------------------------------
      * REPORT HEADING LINES
      *-------------------------------------------------------------
      *
     OQPRINT    E            HRHDR          1 01
     O                       SDPGM               10
     O                                          110 'HOG PRODUCTION SYSTEM'
     O                                          189 'DATE'
     O                       UDATE         Y    198
      *
     O          E            HRHDR          1
     O                       SDUSR               10
     O                                          108 'CLOSED GROUP PERFORMANCE'
     O                                          115 'REPORT'
     O                                          189 'TIME'
     O                       RTIME              198 '  :  :  '
      *
     O          E            HRHDR          1
     O                                           95 'RANKED BY -'
     O                       HROPDS             116
     O                                          189 'PAGE'
     O                       PAGE          Z    198
      *
     O          E            HRHDR          1
     O                                           16 'BUSINESS OFFICE '
     O                                           22 '.....'
     O                       HRFSBO              29
     O                       HRBODS              63
      *
     O          E            HRHDR          1
     O                                           13 'FROM CLOSED D'
     O                                           22 'ATE......'
     O                       HRFMDY        Y     32
      *
     O          E            HRHDR          1
     O                                           13 'TO CLOSED DAT'
     O                                           22 'E........'
     O                       HRTMDY        Y     32
      *
      *
     O          E            HRHDR          1
     O                                           22 'PRODUCTION TYPE ......'
     O                       HRPTCD              29
     O                       HRPTDS              43
      *
     O          E            HRHDR          1
     O                                           22 'PRODUCTION PHASE .....'
     O                       HRPPCD              29
     O                       HRPPDS              63
      *
     O          E            HRHDR          1
     O                                           22 'CELL .................'
     O              N81      LDCELL        K     27
     O               81                          27 'ALL'
      *
     O          E            HRHDR          1
     O                                           22 'FARM SITE ............'
     O              N89      LDFSCD        M     30
     O               89                          27 'ALL'
     O              N89      HRFSDS              58
      *
     O          E            HRHDR          1
     O                                           22 'GROUP TYPE ...........'
     O                       HRGTCD              27
     O                       HRGTDS              48
      *
     O          E            HRHDR          2
     O                                           22 'BREED STOCK ..........'
     O                       HRBSCD              29
     O                       HRBSDS              53
      *
     O          E            HRHDR          1
     O                                           44 'AVG'
     O                                           81 'MORTALITY'
     O                                           85 'AVG'
     O                                          101 'PER HEAD MOVED'
     O                                          112 'AVG'
     O                                          128 '--COST PER---'
     O                                          134 'AVG'
     O                                          155 '--PER CWT LIVE MO'
     O                                          165 'VED OUT---'
     O                                          173 'NET SLS'
     O                                          180 'MARGIN'
     O              N88                         192 'BACK'
      *
     O          E            HRHDR          1
     O                                           12 'FARM'
     O                                           19 'GENET'
     O                                           28 'GROUP'
     O                                           36 'DATE'
     O                                           45 'WK-DAY'
     O                                           51 'TRANI'
     O                                           58 'HEAD'
     O                                           65 'CULLS'
     O                                           72 'TRANO'
     O                                           80 'PERCENT'
     O                                           85 'LBS'
     O                                          100 'AVG     FEED'
     O                                          107 'FEED'
     O                                          114 'DAILY'
     O                                          128 '-LB OF GAIN--'
     O                                          135 'FEED'
     O                                          143 'TRANI'
     O                                          151 'FEED'
     O                                          158 'OTHER'
     O                                          165 'TOTAL'
     O                                          173 'PER CWT'
     O                                          179 'PER'
     O              N88                         191 'FAT'
     O              N88                         197 'CARC'
      *
     O          E            HRHDR          1
     O                                            4 'B/O'
     O                                           12 'SITE'
     O                                           18 'LINE'
     O                                           22 'SEX'
     O                                           29 'NUMBER'
     O                                           38 'DISPOSED'
     O                                           45 'IN PHS'
     O                                           50 'HEAD'
     O                                           58 'SOLD'
     O                                           64 'SOLD'
     O                                           71 'HEAD'
     O                                           87 'PLACED'
     O                                          100 'LBS      LBS'
     O                                          108 'CONVS'
     O                                          113 'GAIN'
     O                                          121 'PHASE'
     O                                          128 'FEED'
     O                                          137 'CST/TN'
     O                                          141 '$'
     O                                          151 'COST'
     O                                          157 'COST'
     O                                          164 'COST'
     O                                          172 'SOLD'
     O                                          179 'CWT'
     O              N88                         186 'LEAN%'
     O              N88                         192 'INCH'
     O              N88                         198 'YIELD'
      *
     O          E            HRHDR          2
     O                       DASH               198
      *
      *-------------------------------------------------------------
      * PRODUCTION PHASE HEADING
      *-------------------------------------------------------------
      *
     O          E            HPHDR          2
     O                       HPPPDS              30
      *
      *-------------------------------------------------------------
      * DETAIL LINE
      *-------------------------------------------------------------
      *
     O          E            DTLINE         1
     O                       WGFSBO               5
     O                       WGFSCD        M     13
     O                       WGGNCD              19
     O                       WGSXCD              21
     O                       WGHGCD         B    30
     O                       DTMDY         YB    39
     O                       WGWK           B    42
     O                                           43 '-'
     O                       WGDAY          B    44
     O                       WGTIHD        KB    52
     O                       WGSLHD        KB    59
     O                       WGCUHD        KB    66
     O                       WGTOHD        KB    73
     O                       WGDDPR        KB    80
     O                       WGAPLB        KB    87
     O                       WGAMLB        KB    94
     O                       WGAFLB        KB   101
     O                       WGFDCV        KB   108
     O                       WGAGLB        KB   115
     O                       WGP$GN        KB   122
     O                       WGF$GN        KB   129
     O                       WGFC$T        KB   137
     O                       WGTI$M        KB   144
     O                       WGFC$M        KB   152
     O                       WGOC$M        KB   159
     O                       WGTC$M        KB   166
     O              N85      WGN$CW        KB   173
     O               85                         172 'N/A'
     O              N85      WGM$CW        KB   180
     O               85                         179 'N/A'
     O              N88      WGLEAN        2B   186
     O              N88      WGBFIN        2B   192
     O              N88      WGCRYD        2B   198
      *
      *-------------------------------------------------------------
      * PRODUCTION PHASE TOTAL LINE
      *-------------------------------------------------------------
      *
     O          E            TPTOT       2  1
      *
     O                       WPPPCD         B     5
     O                                            8 '**'
     O                       WPTIHD        KB    52
     O                       WPCUHD        KB    66
     O                       WPDDPR        KB    80
     O                       WPAPLB        KB    87
     O                       WPAMLB        KB    94
     O                       WPAFLB        KB   101
     O                       WPFDCV        KB   108
     O                       WPAGLB        KB   115
     O                       WPP$GN        KB   122
     O                       WPF$GN        KB   129
     O                       WPFC$T        KB   137
     O                       WPTI$M        KB   144
     O                       WPFC$M        KB   152
     O                       WPOC$M        KB   159
     O                       WPTC$M        KB   166
     O              N87      WPN$CW        KB   173
     O               87                         172 'N/A'
     O              N87      WPM$CW        KB   180
     O               87                         179 'N/A'
     O              N88      WPLEAN        2B   186
     O              N88      WPCRYD        2B   198
      *
     O          E            TPTOT          1
     O                       WPSLHD        KB    59
     O                       WPTOHD        KB    73
     O              N88      WPBFIN        2B   192
      *
      *-------------------------------------------------------------
      * NO DATA MESSAGE LINE
      *-------------------------------------------------------------
      *
     O          E            NODATA         1
     O                                           19 'NO DATA MEETS YOUR'
     O                                           38 'SELECTION CRITERIA'
      *
**  PERFORMANCE RANKING OPTIONS TABLE
AAVERAGE DAILY GAIN
BBACKFAT INCHES
CCARCASS YIELD
DDEATH LOSS PERCENT
FFEED CONVERSION
LLEAN PERCENT
MMARGIN PER CWT
