/***************************************************************** */
/*                                                                 */
/*  SYSTEM:          TRIUMPH FOODS                                 */
/*  PROGRAM NUMBER:  TF4500CL                                      */
/*  PROGRAM NAME:    SPECIFY KILL/CUT DATA LISTING OPTIONS         */
/*  PROGRAMMER:      LEANNE RAMSEY                                 */
/*  CREATE DATE:     03/27/07                                      */
/*                                                                 */
/*******************************************************************/
/* MODIFICATIONS                                                   */
/*                                                                 */
/* 05/06/09  LeAnne Ramsey                                         */
/*           Changed print logic to match Meat Costing.            */
/* 11/09/17  Danny Nguyen    - R12011-Kill Cut Data for STF        */
/*           File TFP050 was changed. Recompile only.              */
/*******************************************************************/

             PGM

             DCL        VAR(&QDATA) TYPE(*CHAR) LEN(45)

             DCL        VAR(&OUTQ) TYPE(*CHAR) LEN(10)
             DCL        VAR(&HOLD) TYPE(*CHAR) LEN(4)
             DCL        VAR(&SAVE) TYPE(*CHAR) LEN(4)
             DCL        VAR(&COPY) TYPE(*DEC)  LEN(1)

             DCL        VAR(&LDFWEDT) TYPE(*DEC) LEN(8)
             DCL        VAR(&LDTWEDT) TYPE(*DEC) LEN(8)

             CHGVAR     VAR(&LDFWEDT) VALUE(%SST(*LDA  1 8))
             CHGVAR     VAR(&LDTWEDT) VALUE(%SST(*LDA 15 8))

/*----------------------------------------------------------------*/
/* SET PARAMETERS FROM LDA FOR LISTINGS                           */
/*----------------------------------------------------------------*/

 OUTQ:       CHGVAR     VAR(&OUTQ) VALUE(%SST(*LDA 401 10))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(*JOB))

 HOLD:       CHGVAR     VAR(&HOLD) VALUE(%SST(*LDA 411 4))
             IF         COND(&HOLD *NE '*YES' *AND &HOLD *NE '*NO ') +
                          THEN(CHGVAR VAR(&HOLD) VALUE(*NO))

 SAVE:       CHGVAR     VAR(&SAVE) VALUE(%SST(*LDA 415 4))
             IF         COND(&SAVE *NE '*YES' *AND &SAVE *NE '*NO ') +
                          THEN(CHGVAR VAR(&SAVE) VALUE(*NO))

 COPY:       CHGVAR     VAR(&COPY) VALUE(%SST(*LDA 419 1))
             IF         COND(&COPY = 0) THEN(CHGVAR VAR(&COPY) +
                          VALUE(1))


/*---------------------------------------------------------------------*/
/* EXTRACT THE DATA FROM THE KILL/CUT DATA FILE                        */
/*---------------------------------------------------------------------*/

             CHGVAR     VAR(&QDATA) VALUE(' ')

/* FROM DATE                                                            */
             CHGVAR     VAR(%SST(&QDATA 1 10)) VALUE('KCWEDT *GE')
             CHGVAR     VAR(%SST(&QDATA 12 8)) VALUE(&LDFWEDT)

/* TO DATE                                                              */
             CHGVAR     VAR(%SST(&QDATA 21 15)) VALUE('*AND KCWEDT *LE')
             CHGVAR     VAR(%SST(&QDATA 37 8)) VALUE(&LDTWEDT)

/* OVERRIDE PRINT FILE                                                */

             OVRPRTF    FILE(PRINT132) OUTQ(&OUTQ) COPIES(&COPY) +
                          HOLD(&HOLD) SAVE(&SAVE)

/* OVERRIDE KILL/CUT DATA FILE                                          */

             OVRDBF     FILE(TFP050) SHARE(*YES)
             OPNQRYF    FILE((TFP050)) OPTION(*INP) QRYSLT(&QDATA) +
                          KEYFLD(*FILE) SEQONLY(*YES)
             CALL       PGM(TF650)
             CLOF       OPNID(TFP050)
             DLTOVR     FILE(TFP050)

/*---------------------------------------------------------------------*/
/* END OF PROCESSING                                                   */
/*---------------------------------------------------------------------*/

             DLTOVR     FILE(*ALL)

             ENDPGM

