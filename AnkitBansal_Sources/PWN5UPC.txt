/*********************************************************************/
/* PROGRAM : PWN5UPC                                                 */
/* DESC    : EXC QSH UPD CARCASS DTACL EXECUTE USER PROGRAM          */
/* DATE    : 08/18/2021                                              */
/* PROJECT : W76958 - CARCASS HOT WEIGHT TABLEAU DATA SOURCES        */
/* BY      : DANNY NGUYEN                                            */
/* PURPOSE : Execute the QSH workflow from Informatica to update the */
/*           Carcass Hot Weight Tableau Data sources after the HPE   */
/*           Payment Producer Post process.                          */
/*                                                                   */
/*           Program is called from:                                 */
/*           PKTNUPR                                                 */
/*                                                                   */
/*********************************************************************/
/* MAINTENANCE LOG:                                                  */
/*-------------------------------------------------------------------*/
/* DATE     BY  CHANGE DESCRIPTION                                   */
/* -------- --- ---------------------------------------------------- */
/* MM/DD/YY                                                          */
/*                                                                   */
/*********************************************************************/

             PGM        PARM(&COMP)

             DCL        VAR(&COMP) TYPE(*DEC) LEN(3 0)
             DCL        VAR(&COMPA) TYPE(*CHAR) LEN(3)

             DCL        VAR(&JOBNME) TYPE(*CHAR) LEN(10)
             DCL        VAR(&USER) TYPE(*CHAR) LEN(10)
             DCL        VAR(&JOBNBR) TYPE(*CHAR) LEN(6)

             DCL        VAR(&DTALIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&CMD) TYPE(*CHAR) LEN(150)
             DCL        VAR(&PSWD) TYPE(*CHAR) LEN(10)
             DCL        VAR(&WFNAME) TYPE(*CHAR) LEN(20)
             DCL        VAR(&FOLDER) TYPE(*CHAR) LEN(13)

             DCL        VAR(&COVAL) TYPE(*CHAR) LEN(10)
             DCL        VAR(&EMAIL) TYPE(*CHAR) LEN(50)
             DCL        VAR(&SUBJECT) TYPE(*CHAR) LEN(50)
             DCL        VAR(&MESSAGE) TYPE(*CHAR) LEN(256)


             RTVJOBA    JOB(&JOBNME) USER(&USER) NBR(&JOBNBR)

             CHGVAR     VAR(&COVAL) VALUE('HPECSHOTWT')
             CHGVAR     VAR(&COMPA) VALUE(&COMP)

             CHGVAR     VAR(&SUBJECT) VALUE('Carcass Hot Weight +
                          Tableau updated for Company' *BCAT &COMPA)
             CHGVAR     VAR(&MESSAGE) VALUE('Carcass Hot Weight +
                          Tableau updated for Company' *BCAT &COMPA)

/*-------------------------------------------------------------------*/
/* Get Data Library to Determine if in TEST or PROD Env.             */
/*-------------------------------------------------------------------*/
             CALL       PGM(PPJXXFR) PARM(' ' 'DTALIB' &DTALIB)

/*-------------------------------------------------------------------*/
/* Build & Execute the Informatica Workflow Qshell Script.           */
/*-------------------------------------------------------------------*/
             CHGVAR     VAR(&CMD) VALUE(*BLANKS)

    /* Set TEST or PROD Workflow Directory - Is Case Sensitive. */
             IF         COND(&DTALIB *NE 'PRKFLIB') THEN(DO)
             CHGVAR     VAR(&PSWD) VALUE('as400')
             CHGVAR     VAR(&CMD) +
                          VALUE('/QOpenSys/infawf/wf_ERP_Reporting_Ca+
                          rcass_Facts_TST.sh')
             CHGVAR     VAR(&MESSAGE) VALUE(&MESSAGE *BCAT ' - TEST')
             ENDDO
             ELSE       CMD(DO)
    /*ˆIS THIS THE CORRECT PASSWORD FOR PRODUCTION LOGIN?? */
             CHGVAR     VAR(&PSWD) VALUE('as400Log1n')
             CHGVAR     VAR(&CMD) +
                          VALUE('/QOpenSys/infawf/wf_ERP_Reporting_Ca+
                          rcass_Facts_PRD.sh')
    /*                    VALUE('/QOpenSys/infawf/FRESH_POST_INVENTOR+ */
    /*                    Y_RESTORATION_PRD.sh')                       */
             ENDDO

             CHGVAR     VAR(&WFNAME) VALUE('ERP_Reporting')
             CHGVAR     VAR(&FOLDER) VALUE('infawf')

             CHGVAR     VAR(&CMD) VALUE(&CMD *BCAT &PSWD)
             CHGVAR     VAR(&CMD) VALUE(&CMD *BCAT &WFNAME)
             CHGVAR     VAR(&CMD) VALUE(&CMD *BCAT &FOLDER)

    /* Call Qshell Command */
             QSH        CMD(&CMD)

         /* Get Email DL from Company Value for 'HPECSHOTWT'. */
             CALL       PGM(PUH4XFR) PARM(' ' &COMP &COVAL &EMAIL)

         /* Send Email */
             IF         COND(&EMAIL *NE *BLANK) THEN(DO)
             CALL       PGM(PUE8UPC) PARM(&EMAIL &SUBJECT &MESSAGE)
             ENDDO


             ENDPGM
