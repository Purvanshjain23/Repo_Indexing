/***************************************************************** */
/*  PROGRAM NUMBER --> HP240CL                                     */
/*  PROGRAM NAME ----> FOS DOWNLOAD--CREATE/SEND FILES TO THE      */
/*                                   FEED ORDERING SYSTEM          */
/*  PROGRAMMER ------> LEANNE FEDOR                                */
/*  DATE ------------> 08/03/00                                    */
/*                                                                 */
/*  FUNCTION --------> WE PLAN TO RUN THIS CL NIGHTLY TO EXTRACT   */
/*                     DATA FROM HPS AND BUILD 3 FILES THAT WILL   */
/*                     BE ACCESSED TO PROVIDE DATA FOR THE FOS-    */
/*                     FEED ORDERING SYSTEM (A P.C.BASED SYSTEM    */
/*                     DONE IN ACCESS BY BRAD HAMILTON.)           */
/*                                                                 */
/***************************************************************** */
/*  MODIFIED:                                                      */
/* 02/04/02  LEANNE FEDOR                                          */
/*           BRAD HAMILTON REQUESTED THAT WE CHANGED FROM .CSV TO  */
/*           .XLS.  SO, WE DID.                                    */
/*                                                                 */
/* 01/30/07  LEANNE RAMSEY                                         */
/*           JOHN ZIMMERMAN/BOB ULRICH NEEDED/WANTED ADDITIONAL    */
/*           FARM/BIN/GROUP INFO. THEY WILL USE THIS ADDITIONAL    */
/*           INFO FOR BOTH FOS REPORTING AND REPORTING FROM THE    */
/*           MARKET SELECTOR WORKSHEET DOWNLOAD.                   */
/*           SO, WE ELECTED TO CREAT/DOWNLOAD THE NEW FILE IN THIS */
/*           FOS JOBSTREAM.                                        */
/*                                                                 */
/* 08/22/07  LEANNE RAMSEY                                         */
/*           CHANGED KEY OVERRIDES ON HSL038E-FEED TICKET DETAIL.  */
/*                                                                 */
/* 10/01/09  LeAnne Ramsey                                         */
/*           David Weaver has completed the rewrite of the FOS     */
/*           system. He now pulls directly from the download files */
/*           that we create in PRKFLIB. So, we no longer need the  */
/*           FTP logic that put that data onto the server. So,     */
/*           I removed the FTP stuff.                              */
/*******************************************************************/

             PGM

             DCL        VAR(&QDATA) TYPE(*CHAR) LEN(75)

/*-------------------------------------------------------------------*/
/* CLEAR THE EXTRACT FILES                                           */
/*-------------------------------------------------------------------*/

             CLRPFM     FILE(HSP240) /* Group Header */
             CLRPFM     FILE(HSP241) /* Group Rations */
             CLRPFM     FILE(HSP242) /* Farms/Rooms/Bins/Groups */

/*********************************************************************/
/* BUILD THE TWO "GROUP" FILES                                       */
/*********************************************************************/

/* ALWAYS OMIT VOIDED GROUPS                                         */

             CHGVAR     VAR(%SST(&QDATA 1 12)) VALUE('HGGSCD *NE "')
             CHGVAR     VAR(%SST(&QDATA 13 2)) VALUE('VD')
             CHGVAR     VAR(%SST(&QDATA 15 1)) VALUE('"')

/* ALWAYS OMIT BGF GROUPS                                            */

             CHGVAR     VAR(%SST(&QDATA 17 17)) VALUE('*AND HGPPCD *NE "')
             CHGVAR     VAR(%SST(&QDATA 34 3)) VALUE('BGF')
             CHGVAR     VAR(%SST(&QDATA 37 1)) VALUE('"')

/* INITIALLY, WE WILL BE OMITTING CLOSED GROUPS....BUT WE MAY        */
/* CHANGE OUR MINDS AND INCLUDE THEM LATER.                          */

             CHGVAR     VAR(%SST(&QDATA 39 17)) VALUE('*AND HGGSCD +
                          *NE "')
             CHGVAR     VAR(%SST(&QDATA 56 2)) VALUE('CL')
             CHGVAR     VAR(%SST(&QDATA 58 1)) VALUE('"')

/* RUN QUERY OVER THE HOG GROUP FILE                                 */

             OVRDBF     FILE(HSP034) SHARE(*YES)
             OPNQRYF    FILE((HSP034)) QRYSLT(&QDATA) KEYFLD(*FILE) +
                          SEQONLY(*YES)

/* OVERRIDE FEED TICKET DETAIL FILE TO HAVE MILL/RATION AS KEYS      */

             OVRDBF     FILE(HSL038E) SHARE(*YES)
             OPNQRYF    FILE((HSL038E)) KEYFLD((FDHGSN) (FDFMBO) +
                          (FDRNCD)) SEQONLY(*YES)

/* CALL THE PROGRAM TO BUILD THE "GROUP" EXTRACT FILES                */

             CALL       PGM(HP240)

             CLOF       OPNID(HSP034)
             CLOF       OPNID(HSL038E)
             DLTOVR     FILE(HSP034)
             DLTOVR     FILE(HSL038E)


/*********************************************************************/
/* BUILD THE FARMS/ROOMS/BINS/GROUPS FILE                            */
/*********************************************************************/

             CALL       PGM(HP242)


             ENDPGM

