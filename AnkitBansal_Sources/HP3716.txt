      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Hog Production System
      * PROGRAM:     HP3716-Update HPS Item and Item Type Files with Upload Data
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     03/03/04
      *
      * Function:    This program runs when there were NO errors in the Uploaded Data.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 10/13/04  LeAnne Fedor
      *           Recompile only. Cost Per Unit Amount was changed from
      *           9,2 to 9,4.
      /eject
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fhsp220    if   e           k disk
      *    Upload file: Item type
      *
      *
     Fhsp221    if   e           k disk
      *    Upload file: Items
      *
      *
     Fhsl221a   if   e           k disk    rename(u2rec:u2reca)
      *    Upload file: Items
      *
      *
     Fhsp3716   uf a e           k disk
      *    Workfile: Old/new budget items
      *
      *
     Fhsp180    uf a e           k disk
      *    Item types
      *
      *
     Fhsp181    uf a e           k disk
      *    Items
      *
      *
     Fhsl181a   if   e           k disk    rename(imrec:imreca)
      *    Items
      *
      *
     Fhsl181c   if   e           k disk    rename(imrec:imrecc)
      *    Items
      *
      *
     Fhsl186b   uf   e           k disk
      *    Template detail
      *
      *
     Fhsp188    if   e           k disk
      *    Farm budget header
      *
      *
     Fhsj189l   if   e           k disk
      *    Farm budget detail + Farm budget header
      *
      *
     Fhsl189m   uf   e           k disk    rename(jdrec:jdrecm)
      *    Farm budget detail
      *
      *
     Fhsl193a   if   e           k disk
      *    Requisitions
      *
      /eject
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      *----------------------------------------------------------------
      * CONSTANTS
      *----------------------------------------------------------------
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      *----------------------------------------------------------------
      * STANDALONE FIELDS
      *----------------------------------------------------------------
      *
      * Control fields
      *
     D first           s              1    inz('Y')
     D allsamefl       s              1    inz('Y')
      *
      *
      * Save fields
      *
     D svoldbgit       s                   like(w1oldbgit)
     D svnewbgit       s                   like(w1newbgit)
      *
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************************************
      * Mainline
      ****************************************************************************************
      *
      * Item Logic:
      *      process Items that are in the HPS Item Master but not in the Upload file
      *      process all Items in the Upload file and also build a workfile for the next step
      *      process the workfile in an attempt to unravel Budget Item changes
      *      make a final pass to try to clean up templates and inactive budgets
      *
     C                   exsr      $goneitem
     C                   exsr      $items
     C                   exsr      $unravel
     C                   exsr      $cleanup
      *
      * Item Type Logic:
      *      process Item Types that are in the HPS Item Type Master but not in the Upload file
      *      process all Item Types in the Upload file
      *
     C                   exsr      $gonetype
     C                   exsr      $types
      *
     C                   seton                                        lr
      /eject
      *------------------------------------------------------------------------------------
      * Process 'Gone' Items
      *------------------------------------------------------------------------------------
      * When we have an Item in the HPS Item Master that is not in the upload
      * file, we must decide whether to delete the HPS record or just Inactivate
      * it. The rules are:
      *     1) If the Item Code is on a requisition, inactivate it
      *     2) If it is: a) the only record with this 'budget item' value and
      *                  b) the 'budget item' has been used on any closed or
      *                     active Farm Budgets, inactivate it
      *     3) Otherwise, delete it.
      *
     C     $goneitem     begsr
      *
      * Read each record in the HPS Item Master. If the record has an Active
      * status, process it.
      *
     C     *loval        setll     hsp181
     C                   dou       *in91 = *on                                  Do inactivate
     C                   read      hsp181                                 91
     C                   if        *in91 = *off and imaist = 'A'                If not EOF
      *
      * If this item is in the upload file, then you are done here.
      * If it is not in the upload file, continue.
      *
     C     imitcd        chain     hsp221                             92
     C                   if        *in92 = *on                                  If gone
      *
      * Have we used this Item Code on any requisition? If so, just
      * inactivate the record and get out.
      *
     C     imitcd        chain     hsl193a                            92
     C                   if        *in92 = *off                                 If on req
     C                   move      'I'           imaist
     C                   update    imrec
     C                   else
      *
      * If this Item has a Budget Flag of YES, see if you have any records
      * in the upload file with this same Budget Item. If you do, you can
      * just delete this Item record because you will ultimately upload another
      * Item with the same Budget Item value.
      *
     C                   if        imbgfl = yes                                 If budget flag
     C     imbgit        chain     hsl221a                            92
     C                   if        *in92 = *off                                 If will have 1
     C                   delete    imrec
     C                   else
      *
      * You're not going to get any Item record up on this upload with
      * this Budget Item value. SO, see if you have already used this Budget
      * Item on any Closed or Active Farm Budget. If not, you can
      * still delete this record.
      *
     C     imbgit        chain     hsj189l                            92
     C                   if        *in92 = *on                                  If on no budget
     C                   delete    imrec
     C                   else
     C                   move      'I'           imaist
     C                   update    imrec
     C                   endif                                                  If on no budget
      *
     C                   endif                                                  If will have 1
     C                   endif                                                  If budget flag
     C                   endif                                                  If on req
      *
     C                   endif                                                  If gone
     C                   endif                                                  If not EOF
     C                   enddo                                                  Do inactivate
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Process all the Items in the Upload File
      *---------------------------------------------------------------
      *
      * New Items coming up and existing Items that have not had their
      * 'Budget Item' value changed are no problems. But, if the "budget item"
      * is different on existing records, write the old/new values to a
      * workfile for additional processing later.
      *
     C     $items        begsr
      *
     C     *loval        setll     hsp221
     C                   dou       *in91 = *on                                  Do items
     C                   read      hsp221                                 91
     C                   if        *in91 = *off                                 If not EOF
      *
      * If this Item does not exist in the HPS file, just write it to HPS.
      *
     C     u2itcd        chain     hsp181                             92
     C                   select
     C                   when      *in92 = *on
     C                   exsr      $setup
     C                   write     imrec
     C                   other
      *
      * This Item exists in HPS.  Write the old/new Budget Item values
      * to the workfile before updating--if they aren't blank.
      *
     C                   if        imbgit = *blank and u2bgit = *blank
     C                   else
     C                   exsr      $bldwrkf
     C                   endif
      *
     C                   exsr      $setup
     C                   update    imrec
     C                   endsl
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Do items
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Write an Item record
      *---------------------------------------------------------------
      *
     C     $setup        begsr
      *
     C                   move      u2itcd        imitcd
     C                   move      u2itds        imitds
     C                   move      u2uom         imuom
     C                   z-add     u2vol         imvol
     C                   move      u2uow         imuow
     C                   move      u2ittycd      imittycd
     C                   move      u2critfl      imcritfl
     C                   move      u2bgfl        imbgfl
     C                   move      u2bgit        imbgit
     C                   move      u2stkloc      imstkloc
     C                   z-add     u2cpuam       imcpuam
     C                   move      'A'           imaist
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Set up workfile records
      *---------------------------------------------------------------
      *
     C     $bldwrkf      begsr
      *
     C     key01         chain     hsp3716                            92
     C                   if        *in92 = *on                                  If new
     C                   move      imbgit        w1oldbgit
     C                   move      u2bgit        w1newbgit
     C                   write     w1rec
     C                   endif                                                  If new
      *
     C                   endsr
      /eject
      *-------------------------------------------------------------------------------------
      * Try to unravel Budget Item changes between 'old' and 'new'
      *-------------------------------------------------------------------------------------
      *
      * What we are trying to figure out is if a Budget Item was changed on all Items.
      * If Budget Item was changed on ALL records from an old value to a new value
      * (Example: GLOVES was changed to be PINK GLOVES) then we ultimately want to make global
      * changes from GLOVES to PINK GLOVES in HPS.
      *
      * So, we will read each record in the workfile that we built when we were updating
      * the HPS Item Master with the uploaded items. For each unique 'old' budget item,
      * we want to see if the corresponding 'new' item is 1) the same for all the
      * records and 2) different from the 'old' value.
      *
     C     $unravel      begsr
      *
     C     *loval        setll     hsp3716
     C                   dou       *in91 = *on                                  Do unravel
     C                   read      hsp3716                                91
     C                   if        *in91 = *off                                 If not EOF
      * Break logic
     C                   select
     C                   when      first = yes
     C                   move      no            first
     C                   move      w1oldbgit     svoldbgit
     C                   move      w1newbgit     svnewbgit
     C                   move      yes           allsamefl
      *
     C                   when      svoldbgit <> w1oldbgit
     C                   exsr      $chgbgit
     C                   move      w1oldbgit     svoldbgit
     C                   move      w1newbgit     svnewbgit
     C                   move      yes           allsamefl
     C                   endsl
      * Detail logic
     C                   if        w1newbgit <> svnewbgit
     C                   move      no            allsamefl
     C                   endif
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Do unravel
      *
     C                   endsr
      /eject
      *--------------------------------------------------------------------------------------
      * Change the Budget Item globally on Templates and Farm Budgets
      *--------------------------------------------------------------------------------------
      *
      * If Budget Item was changed on ALL records from an old value to a new value
      * (Example: GLOVES was changed to be PINK GLOVES) then we want to make global
      * changes from GLOVES to PINK GLOVES on:
      *      1) all templates and
      *      2) all farm budgets
      *
     C     $chgbgit      begsr
      *
      * If not ALL values were changed or the old/new values are the same, we
      * don't do anything---just get out.
      *
     C                   if        allsamefl = no or                            If no go
     C                             svoldbgit = svnewbgit
     C                   else
      *
      * Farm Budget Detail
      *
     C     svoldbgit     setll     hsl189m
      *
     C                   dou       *in93 = *on                                  Do farm budgets
     C     svoldbgit     reade     hsl189m                                93
     C                   if        *in93 = *off                                 If read
     C                   move      svnewbgit     jdbgit
     C                   update    jdrecm
     C                   endif                                                  If read
     C                   enddo                                                  Do farm budgets
      *
      * Template Detail
      *
     C     svoldbgit     setll     hsl186b
      *
     C                   dou       *in93 = *on                                  Do templates
     C     svoldbgit     reade     hsl186b                                93
     C                   if        *in93 = *off                                 If read
     C                   move      svnewbgit     tdbgit
     C                   update    tdrec
     C                   endif                                                  If read
     C                   enddo                                                  Do templates
      *
     C                   endif                                                  If no go
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Try to clean up Templates and Inactive Budgets
      *---------------------------------------------------------------
      *
     C     $cleanup      begsr
      *
      * Read all Template detail records. If the Budget Item no longer has any
      * 'active' records in the Item Master that have YES in the Budget Flag field,
      * delete the Budget Template Detail record.
      *
     C     *loval        setll     hsl186b
     C                   dou       *in93 = *on                                  Do template dtl
     C                   read      hsl186b                                93
     C                   if        *in93 = *off                                 If read
      * Item Master
      *
     C     tdbgit        chain     hsl181c                            92
     C                   if        *in92 = *on                                  If gone
     C                   delete    tdrec
     C                   endif                                                  If gone
      *
     C                   endif                                                  If read
     C                   enddo                                                  Do template dtl
      *
      *
      * Read all Farm Budget detail records. If the Budget Item no longer has
      * 'active' records in the Item Master that have YES in the Budget Flag
      * field, delete the Budget Detail record.
      *
      *
     C     *loval        setll     hsl189m
     C                   dou       *in93 = *on                                  Do farm dtl
     C                   read      hsl189m                                93
     C                   if        *in93 = *off                                 If read
      *
      * Is this Budget 'Inactive'?
      *
     C     jdfbsn        chain     hsp188                             92
     C                   if        *in92 = *off and jhfbscd = 'I'               If inactive
      * Item Master
      *
     C     jdbgit        chain     hsl181c                            92
     C                   if        *in92 = *on                                  If gone
     C                   delete    jdrecm
     C                   endif                                                  If gone
     C                   endif                                                  If inactive
      *
     C                   endif                                                  If read
     C                   enddo                                                  Do farm dtl
      *
     C                   endsr
      /eject
      *------------------------------------------------------------------------------------
      * Process 'Gone' Types
      *------------------------------------------------------------------------------------
      * When we have an Item Type in the HPS Item Type Master that is not in the upload
      * file, we must decide whether to delete the HPS record or just Inactivate
      * it. The rules are:
      *     1) If the Item Type Code is on an Item record, inactivate it
      *     3) Otherwise, delete it.
      *
     C     $gonetype     begsr
      *
      * Read each record in the HPS Item Type Master. If the record has an Active
      * status, process it.
      *
     C     *loval        setll     hsp180
     C                   dou       *in91 = *on                                  Do inactivate
     C                   read      hsp180                                 91
     C                   if        *in91 = *off and tmaist = 'A'                If not EOF
      *
      * If this Type is in the upload file, then you are done here.
      * If it is not in the upload file, continue.
      *
     C     tmittycd      chain     hsp220                             92
     C                   if        *in92 = *on                                  If gone
      *
      * Do we have this Item Type on any record in the Item Master? If
      * so, inactivate the record; otherwise, delete it.
      *
     C     tmittycd      chain     hsl181a                            92
     C                   if        *in92 = *off                                 If on item
     C                   move      'I'           tmaist
     C                   update    tmrec
     C                   else
     C                   delete    tmrec
     C                   endif                                                  If on item
      *
     C                   endif                                                  If gone
     C                   endif                                                  If not EOF
     C                   enddo                                                  Do inactivate
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Process all the Item Types in the Upload File
      *---------------------------------------------------------------
      *
     C     $types        begsr
      *
     C     *loval        setll     hsp220
     C                   dou       *in91 = *on                                  Do types
     C                   read      hsp220                                 91
     C                   if        *in91 = *off                                 If not EOF
      *
      * Either write a record or update an existing record in the HPS
      * Item Type Master file.
      *
     C     u1ittycd      chain     hsp180                             92
     C                   select
     C                   when      *in92 = *on
     C                   move      u1ittycd      tmittycd
     C                   move      u1ittyds      tmittyds
     C                   move      'A'           tmaist
     C                   write     tmrec
     C                   other
      *
     C                   move      u1ittyds      tmittyds
     C                   move      'A'           tmaist
     C                   update    tmrec
     C                   endsl
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Do types
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Key lists
      *
     C     key01         klist
     C                   kfld                    imbgit
     C                   kfld                    u2bgit
      *
     C                   endsr
      /eject
