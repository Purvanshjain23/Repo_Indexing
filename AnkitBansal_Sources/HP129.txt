      *****************  RPG PROGRAM HEADING  ***********************
      *
      * SYSTEM:      HOG PRODUCTION
      * PROGRAM:     HP129
      * TITLE:       MAINTAIN SCHEDULED TRANSFER
      * PROGRAMMER:  LEANNE FEDOR
      * CREATED:     10/06/95
      *
      * FUNCTION: THIS PROGRAM USES A SUBFILE TO MAINTAIN RECORDS
      *           IN THE TRANSFER FILES (HEADER AND DETAIL)
      *           FOR SCHEDULED TRANSFER MOVEMENTS.
      *
      *           THE PROGRAM WILL CREATE, DELETE AND UPDATE RECORDS
      *           FOR A SPECIFIC MOVEMENT NUMBER.
      *
      *           THIS PROGRAM IS CALLED FROM:
      *                HP429-WORK WITH SCHEDULED TRANSFERS
      *
      *           IT DOES NOT HAVE SELECTORS OR POSITIONERS.
      *           IT HAS F4-PROMPT LOGIC.
      *           IT USES THE STANDARD MESSAGE HANDLING PROGRAM.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 02/06/01  LeAnne Fedor
      *           In preparation for allowing groups on a farm to have different
      *           production types, we added the group 'production type' as output only
      *           in the subfile. Also, we added an edit that all origin groups must have
      *           the same production type and all destination groups must have the same
      *           production type. The production type does not have to match the current
      *           type for the farm in the Farm Master.
      *
      * 06/05/01  LeAnne Fedor
      *           Recompile only. New field 'multisite' added to Farm Site file.
      *
      * 06/26/01  LeAnne Fedor
      *           Recompile only.
      *           Three fields (manager/supervisor/multisite) renamed in Farm Site file.
      *           Manager and supervisor fields removed from Hog Group file.
      *
      * 09/04/01  LeAnne Fedor
      *           Recompile only...'hog capacity' removed from Farm Buildings file.
      *           'Square feet' fields added to Farm Site file.
      *
      * 07/17/02  LeAnne Fedor
      *           In preparation for allowing groups on a farm to have different
      *           production phases, we added the group 'production phase' as output only
      *           in the subfile. Also, we added an edit that all origin groups must have
      *           the same production phase and all destination groups must have the same
      *           production phase. The production phase does not have to match the default
      *           phase for the farm in the Farm Master.
      *
      * 01/07/03  LeAnne Fedor
      *           Added edit that 'trucker' must be active.
      *
      * 05/17/05  LeAnne Fedor
      *           The users want to "temporarily" lift the edit that requires both sides of
      *           a WTF transfer to be WTF groups. When they are ready to put the edit back
      *           into effect, scan for WTF-TEMP to see what code to reinstate.
      *
      * 01/18/06  LeAnne Fedor
      *           Recompile only.
      *           Field "Bin Set" was removed from HSP020-Building Rooms file
      *
      * 10/16/06  LeAnne Ramsey
      *           We reinstated the edit that was temporarily 'lifted' in May 2005 for
      *           WTF groups.
      *
      * 07/02/09  LeAnne Ramsey
      *           Recompile only. Added new field 'Continuous Flow Flag' to Hog Group file.
      *
      * 10/15/13  LeAnne Ramsey (E2831)
      *           Recompile only. Added field 'MTech Reference'.
      *
      * 10/21/13  LeAnne Ramsey (E2831)
      *           Added MTech Reference as a parm on the calls to:
      *               HP538-Select Trucker
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     FHPD129    CF   E             WORKSTN SFILE(HP129S1:HDRRN1)
     F                                     INFDS(IOFEED)
      *
     FHSP018    IF   E           K DISK
      *    FARM SITE
      *
      *
     FHSP019    IF   E           K DISK
      *    FARM BUILDINGS
      *
      *
     Fhsp020    if   e           k disk
      *    Building rooms
      *
      *
     FHSP034    IF   E           K DISK
      *  HOG GROUPS
      *
      *
     FHSL034D   IF   E           K DISK    RENAME(HGREC:HGRECD)
      *  HOG GROUPS
      *
      *
     Fhsl034o   if   e           k disk    rename(hgrec:hgreco)
      *  Hog groups  (open and created groups only)
      *
      *
     Fhsl034p   if   e           k disk    rename(hgrec:hgrecp) prefix(p1)
      *  Hog groups (open groups only)
      *
      *
     FHSP046    IF   E           K DISK
      *    TRUCKER
      *
      *
     FHSP058    UF A E           K DISK
      *  MOVEMENT EVENT
      *
      *
     FHSP074    UF A E           K DISK
      *  TRANSFER HEADER
      *
      *
     FHSP075    UF A E           K DISK
      *  TRANSFER DETAIL
      *
      *
     FHSP080    O    E             DISK
      *  DELETED MOVEMENTS
      *
      /EJECT
      ****************************************************************
      * TABLE AND ARRAY SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * INPUT SPECIFICATIONS
      ****************************************************************
      *
      *---------------------------------------------------------------
      * STANDARD ERROR MESSAGE HANDLING DATA STRUCTURES
      *---------------------------------------------------------------
      *
      * FOR ERROR MESSAGE HANDLING, A PACKED INDEX (E) IS REQUIRED.
      * FOR PROGRAM READABILITY, DEFINE A CORRESPONDING ERROR COUNT
      * FIELD CALLED 'ERROR'. ALSO DEFINE CURSOR LOCATION FIELDS.
      *
     D                 DS                  INZ
     D  ERROR                  1      2P 0
     D  E                      1      2P 0
      *
      * THIS DATA STRUCTURE SUPPLIES THE NAME OF THE MESSAGE FILE TO
      * THE MESSAGE HANDLING CL PROGRAM.  THE FIELD NAME MSGFIL MUST BE
      * CONSTANT. THE VALUE IN QUOTES IS THE NAME OF THE SPECIFIC
      * MESSAGE FILE CONTAINING THE USER DEFINED MESSAGES.
      *
     D                 DS                  INZ
     D  MSGFIL                 1     10    INZ('HSMSGF    ')
      *
      * THE FOLLOWING 3 DATA STRUCTURES ARE USED TO SPEED MESSAGE
      * HANDLING SINCE IT IS FASTER TO CLEAR DATA STRUCTURES THAN
      * ARRAYS.  EACH IS ASSOCIATED WITH A STANDARD MESSAGE ARRAY.
      *
     D MGI             DS           140    INZ
     D  MGID                   1    140
     D                                     DIM(20)                              MSG ID ARRAY
      *
     D MGD             DS          1000    INZ
     D  MGDT                   1   1000
     D                                     DIM(20)                              MSG PARMS
      *
     D MGWK            DS            50    INZ
     D  MGW                    1     50
     D                                     DIM(50)                              MSG WORK PARMS
      *
      *---------------------------------------------------------------
      * STANDARD PROGRAM STATUS DATA STRUCTURE
      *---------------------------------------------------------------
      *    EXTERNALLY DEFINED AS UTPGFR (RECORD FORMAT: PGMDSR)
     D PGMDS         ESDS                  EXTNAME(UTPGFR)
      *
      *---------------------------------------------------------------
      * STANDARD WORKSTATION INFORMATION DATA STRUCTURE
      *---------------------------------------------------------------
      *    EXTERNALLY DEFINED AS UTWSFR (RECORD FORMAT: UTIDFRR)
     D IOFEED        E DS                  EXTNAME(UTWSFR)
      *
      *---------------------------------------------------------------
      * STANDARD DATABASE FILE INFORMATION DATA STRUCTURE
      *---------------------------------------------------------------
      *    EXTERNALLY DEFINED AS UTDBFR (RECORD FORMAT: FDBCKD)
     D DBFEED        E DS                  EXTNAME(UTDBFR)
      *
      *---------------------------------------------------------------
      *  DATE FORMATS
      *---------------------------------------------------------------
      *
      * DATE IN MMDDYY FORMAT
      *
     D                 DS
     D  WKMDY                  1      6  0
     D  WKMM                   1      2  0
     D  WKDD                   3      4  0
     D  WKYY                   5      6  0
      *
      * DATE IN CCYYMMDD FORMAT
      *
     D                 DS
     D  WKCYMD                 1      8  0
     D  WKCEN                  1      2  0
     D  WKYER                  3      4  0
     D  WKMON                  5      6  0
     D  WKDAY                  7      8  0
      *
      *---------------------------------------------------------------
      *  TIME FORMAT
      *---------------------------------------------------------------
      *
     D                 DS
     D  HHMMSS                 1      6  0
     D  HHMM                   1      4  0
      *
      *---------------------------------------------------------------
      *  DATA AREA FOR NEXT MOVEMENT NUMBER
      *---------------------------------------------------------------
      *
     D                 DS
     D  DAMVSN                 1      7  0
      *
      *---------------------------------------------------------------
      *  DEFINITION FOR EXTERNAL DATA AREA 'DAAPER'
      *---------------------------------------------------------------
     D                 DS
     D  DAAPER                 1     24
     D  DACCYY                 1      4
     D  DAPER                  5      6
     D  DABPDT                 7     14  0
     D  DAEPDT                15     22  0
     D  DAPGFL                23     23
     D  DAPPFL                24     24
      *
      *---------------------------------------------------------------
      *  NAMED CONSTANTS
      *---------------------------------------------------------------
      *
     D CREATE          C                   CONST('CREATE')
     D REVISE          C                   CONST('REVISE')
     D DELETE          C                   CONST('DELETE')
     D VIEW            C                   CONST('VIEW  ')
     D SELECT          C                   CONST('SELECT')
     D YES             C                   CONST('Y')
     D NO              C                   CONST('N')
     D SET1            C                   CONST('SET1  ')
     D EDIT1           C                   CONST('EDIT1 ')
     D SCRN1           C                   CONST('SCRN1 ')
     D EXIT            C                   CONST('EXIT  ')
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * MAINLINE
      ****************************************************************
      *
     C     *INLR         DOWEQ     *OFF                                         MAIN DO LOOP
      *
     C     ROUTNE        CASEQ     SET1          $SET1
     C     ROUTNE        CASEQ     SCRN1         $SCRN1
     C     ROUTNE        CASEQ     EDIT1         $EDIT1
      *
     C     ROUTNE        CASEQ     EXIT          $EXIT
     C                   ENDCS
     C                   ENDDO                                                  MAIN DO LOOP
      /EJECT
      *----------------------------------------------------------------
      * $SET1 - SUBROUTINE TO SET ENVIRONMENT FOR SCREEN 1
      *----------------------------------------------------------------
      *
     C     $SET1         BEGSR
      *
      * IF THE USER IS IN CREATE MODE
      *
     C                   SELECT
     C     MODE          WHENEQ    CREATE                                       WH CREATE
     C                   Z-ADD     0             C1MVSN
     C                   MOVEL     'CREATE'      DFMODE
     C                   OTHER
      *
      * IF THE USER IS REVISING, VERIFY THAT THE TRANSFER MOVEMENT
      *    THAT THE USER SELECTED STILL EXISTS AND SETUP THE HEADER
      *    INFO FOR THE CONTROL RECORD
      *
     C     C1MVSN        CHAIN(N)  HSP074                             90
     C     *IN90         IFEQ      *OFF                                         IF FOUND
     C                   EXSR      $HDR
     C                   ELSE
     C                   MOVEL     YES           NOTFLG
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS00592'     MGID(E)
     C                   CLEAR                   MGW
     C                   MOVEL     C1MVSN        XXMVSN
     C                   MOVEA     XXMVSN        MGW(1)
     C                   MOVEA     MGW           MGDT(E)
     C                   ENDIF
     C                   ENDIF                                                  IF FOUND
     C                   ENDSL
      *
      *  CLEAR/FILL THE SUBFILE
      *
     C                   EXSR      $CLR1
     C                   EXSR      $FRSH1
      *
      *  SET ROUTINE TO DISPLAY SCREEN
      *
     C                   MOVEL     SCRN1         ROUTNE
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * $SCRN1 - SUBROUTINE TO PERFORM OPERATOR I/O FOR SCREEN 1
      *---------------------------------------------------------------
      *
     C     $SCRN1        BEGSR
      *
      * SET THE PAGE OF THE SUBFILE TO DISPLAY
      *
     C                   SELECT
     C     WKFLOW        WHENEQ    0
     C     ERROR         ANDEQ     0
     C                   Z-ADD     1             HDPAG1
      *
     C     RESET         WHENEQ    YES
     C     WKFLOW        IFGT      HDRRN1
     C                   ELSE
     C                   Z-ADD     WKFLOW        HDPAG1
     C                   ENDIF
     C                   ENDSL
      *
      * SET THE INDICATORS TO CONTROL SCREEN FIELDS
      *
     C                   EXSR      $IND
      *
      * IF THE USER HAS EDITED THE DATA, THEN ISSUE THE 'ACCEPT
      * ENTRIES' MESSAGE
      *
     C     ERROR         IFEQ      0
     C     EDITFL        ANDEQ     YES
     C                   Z-ADD     1             ERROR
     C                   MOVEL     'HS09026'     MGID(E)
     C                   ENDIF
      *
      * WRITE THE ERROR MESSAGES FROM THE ERROR ARRAYS TO THE
      * ERROR MESSAGE SUBFILE
      *
     C                   EXSR      $WRMSG
      *
      * THIS SUBFILE HAS FOLD/UNFOLD CAPABILITIES.  THE DDS USES THE
      * KEYWORDS (SFLDROP, SFLFOLD) TO DETERMINE WHETHER THE
      * USER IS FOLDED OR UNFOLDED AND TO REDISPLAY THE SCREEN IN THE
      * SAME MODE AS WHEN HE LAST SAW IT.  INDICATORS 88 AND 89 ARE
      * ASSIGNED IN THE DDS TO SFLDROP AND SFLFOLD.
      *
     C                   setoff                                       8889
     C                   if        fold = '0'
     C                   seton                                        88
     C                   else
     C                   seton                                        89
     C                   endif
      *
      *  WRITE SCREEN 1 TO CRT
      *
     C                   WRITE     HP129K1
     C                   WRITE     HP129EC
     C                   EXFMT     HP129C1
      *
      * RETRIEVE THE LOWEST SUBFILE RRN ON THE SCREEN FROM THE DSPF
      * FEEDBACK AREA (IDFLOW) AND MOVE IT TO A WORK FIELD.  THIS VALUE
      * WILL BE USED LATER TO CONTROL THE REPOSITIONING OF THE SUBFILE.
      *
     C                   Z-ADD     IDFLOW        WKFLOW
     C                   MOVEL     NO            RESET
     C                   MOVEL     NO            EDITFL
      *
      * CLEAR ALL MESSAGES
      *
     C                   EXSR      $CLMSG
      *
      * GET USER'S ENTRY AND RESET STANDARD RESPONSE
      *
     C                   SELECT
     C     *IN03         WHENEQ    *ON                                          F3-EXIT
     C                   MOVEL     '03'          RTNCD
     C                   MOVEL     EXIT          ROUTNE
      *
     C     *IN04         WHENEQ    *ON                                          F4-PROMPT
     C                   EXSR      $F4S1
     C                   MOVEL     SCRN1         ROUTNE
      *
     C     *IN05         WHENEQ    *ON                                          F5-REFRESH
     C                   MOVEL     YES           RESET
     C                   MOVEL     SET1          ROUTNE
      *
     C     *IN09         WHENEQ    *ON                                          F9-ACCEPT
      *
      *----------------------------------------------------------------
      * REVISING AN EXISTING TRANSFER MOVEMENT
      *----------------------------------------------------------------
      *
     C     MODE          IFEQ      REVISE
     C                   EXSR      $EDIT1
     C     ERROR         IFEQ      0
      *
      * DELETE EXISTING TRANSFER HEADER AND DETAIL RECORDS
      * DELETE MOVEMENT EVENT RECORD
      *
     C                   EXSR      $DLTTH
     C                   EXSR      $DLTTD
     C                   EXSR      $DLTME
      *
      * CREATE NEW TRANSFER HEADER AND DETAIL RECORDS AND A NEW
      * MOVEMENT EVENT RECORD.
      *
     C                   EXSR      $CRTTH
     C                   EXSR      $CRTTD
      *
     C                   MOVEL     EXIT          ROUTNE
     C                   ELSE
     C                   MOVEL     SCRN1         ROUTNE
     C                   ENDIF
     C                   ENDIF
      *
      *----------------------------------------------------------------
      * CREATING A NEW TRANSER MOVEMENT
      *----------------------------------------------------------------
      *
     C     MODE          IFEQ      CREATE                                       IF CREATE
     C                   EXSR      $EDIT1
     C     ERROR         IFNE      0                                            IF OK CREATE
     C                   MOVEL     SCRN1         ROUTNE
     C                   ELSE
      *
      * 1) CREATE THE TRANSFER HEADER RECORD
      * 2) CREATE ALL DETAIL RECORDS FOR THIS TRANSFER
      *
     C                   EXSR      $CRTTH
     C                   EXSR      $CRTTD
      *
      * ISSUE THE MESSAGE THAT THE TRANSFER WAS CREATED
      *
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS00594'     MGID(E)
     C                   CLEAR                   MGW
     C                   MOVEL     DAMVSN        XXMVSN
     C                   MOVEA     XXMVSN        MGW(1)
     C                   MOVEA     MGW           MGDT(E)
     C                   ENDIF
      *
     C                   MOVEL     SET1          ROUTNE
     C                   ENDIF                                                  IF OK CREATE
     C                   ENDIF                                                  IF CREATE
      *
      *----------------------------------------------------------------
      * DELETING A TRANSFER
      *----------------------------------------------------------------
      *
     C     *IN11         WHENEQ    *ON                                          F11-DELETE
     C                   EXSR      $DLT11
      *
     C     *IN12         WHENEQ    *ON                                          F12-PREVIOUS
     C                   MOVEL     '12'          RTNCD
     C                   MOVEL     EXIT          ROUTNE
      *
     C     *IN25         WHENEQ    *ON                                          F25-ROLLUP
     C     IDFLOW        ADD       WKPLIN        WKDIFF
     C     HDRRN1        IFGE      WKDIFF                                       IF SOMETHING
     C                   ELSE
     C                   Z-ADD     COUNT         HDRRN1
     C                   EXSR      $SETOF
     C                   EXSR      $CLRLN
     C                   DO        WKPLIN                                       DO FILL
     C                   EXSR      $BLANK
     C                   ENDDO                                                  DO FILL
     C                   ENDIF                                                  IF SOMETHING
     C                   MOVEL     SCRN1         ROUTNE
      *
     C     NOTFLG        WHENEQ    YES                                           MOVE GONE
     C                   MOVEL     SET1          ROUTNE
     C                   OTHER
      *
      * USER PRESSED 'ENTER'
      *
     C                   EXSR      $EDIT1
     C                   ENDSL
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * $EDIT1 - SUBROUTINE TO EDIT INPUT FIELDS FOR SCREEN 1
      *---------------------------------------------------------------
      *
     C     $EDIT1        BEGSR
      *
     C                   MOVEL     YES           EDITFL
      *
      * EDIT THE CONTROL RECORD
      *
     C                   EXSR      $EDC1
      *
      * EDIT THE SUBFILE IF THERE ARE NO ERRORS IN THE CONTROL
      * RECORD. IF THERE ARE ERRORS IN THE CONTROL RECORD, YOU
      * NEED TO REWRITE ANY SUBFILE RECORDS WITH THE ERROR INDICATORS
      * OFF.
      *
     C     ERROR         IFEQ      0
     C                   EXSR      $EDS1
     C                   ELSE
     C                   EXSR      $OFFIN
     C                   ENDIF
      *
     C     ERROR         IFEQ      0
     C                   Z-ADD     1             HDPAG1
     C                   ENDIF
     C                   MOVEL     SCRN1         ROUTNE
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * $EDC1 - SUBROUTINE TO EDIT THE CONTROL RECORD INFO ON SCREEN 1
      *---------------------------------------------------------------
      *
     C     $EDC1         BEGSR
      *
      * SCHEDULED SHIP DATE
      *
     C                   EXSR      $SCDT
      * TRUCKER
     C                   EXSR      $TICD
      *
      * ORIGIN FARM SITE
     C                   EXSR      $ORFSCD
      *
      * DESTINATION FARM
     C                   EXSR      $DNFSCD
      *
      * SCHEDULED HEAD TO SHIP
      *
     C     C1SCHD        IFLT      0
     C                   SETON                                        30
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS09000'     MGID(E)
     C                   ENDIF
     C                   ENDIF
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $ORFSCD - EDIT ORIGIN FARM SITE
      *----------------------------------------------------------------
      *
     C     $ORFSCD       BEGSR
      *
     C                   MOVE      *BLANK        c1orfsnm
     C     c1orfscd      IFEQ      0                                            IF NO ORIG
     C                   SETON                                        31
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS09001'     MGID(E)
     C                   ENDIF
     C                   ELSE
      *
      * CHECK THAT FARM SITE EXISTS IN THE FARM SITE MASTER FILE
      *
     C     c1orfscd      CHAIN     HSP018                             92
     C     *IN92         IFEQ      *OFF                                         IF GOOD ORIG
     C                   MOVEL     FSFSNM        c1orfsnm
     C                   MOVEL     FSFSBO        WKORBO
     C                   ELSE
      *
      * INVALID FARM SITE
     C                   SETON                                        31
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS00131'     MGID(E)
     C                   CLEAR                   MGW
     C                   MOVE      c1orfscd      xxalphfscd
     C                   MOVEA     xxalphfscd    MGW(1)
     C                   MOVEA     MGW           MGDT(E)
     C                   ENDIF
     C                   ENDIF                                                  IF GOOD ORIG
     C                   ENDIF                                                  IF NO ORIG
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $DNFSCD - EDIT DESTINATION FARM SITE
      *----------------------------------------------------------------
      *
     C     $DNFSCD       BEGSR
      *
     C                   MOVE      *BLANK        c1dnfsnm
     C     c1dnfscd      IFEQ      0                                            IF NO DEST
     C                   SETON                                        32
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS09001'     MGID(E)
     C                   ENDIF
     C                   ELSE
      *
      * CHECK THAT FARM SITE EXISTS IN THE FARM SITE MASTER FILE
      *
     C     c1dnfscd      CHAIN     HSP018                             92
     C     *IN92         IFEQ      *OFF                                         IF GOOD DEST
     C                   MOVEL     FSFSNM        c1dnfsnm
     C                   MOVEL     FSFSBO        WKDNBO
     C                   ELSE
      *
      * INVALID FARM SITE
     C                   SETON                                        32
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS00131'     MGID(E)
     C                   CLEAR                   MGW
     C                   MOVE      c1dnfscd      xxalphfscd
     C                   MOVEA     xxalphfscd    MGW(1)
     C                   MOVEA     MGW           MGDT(E)
     C                   ENDIF
     C                   ENDIF                                                  IF GOOD DEST
     C                   ENDIF                                                  IF NO DEST
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $SCDT - EDIT SCHEDULED SHIP DATE
      *----------------------------------------------------------------
      *
     C     $SCDT         BEGSR
      *
     C                   SELECT
     C     C1MDY         WHENEQ    0
     C                   SETON                                        39
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVE      'HS00607'     MGID(E)
     C                   ENDIF
     C                   OTHER
      *
      * VALIDATE THAT THE DATE IS VALID AND ADD CENTURY VALUES
      * TO THE WORK FIELD FOR LATER USE IN THE UPDATING ROUTINE.
      *
     C                   Z-ADD     C1MDY         PFRM8
     C                   Z-ADD     0             PTO8
     C                   MOVEL     'MDY '        PFRFMT
     C                   MOVEL     'CYMD'        PTOFMT
     C                   Z-ADD     0             PDAYS
     C                   MOVE      *BLANK        PCODE
     C                   MOVE      *BLANK        PRTRN
      *
     C                   EXSR      $DATE
      *
     C     PRTRN         IFNE      *BLANK                                       IF INVALID
     C                   SETON                                        39
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVE      'HS09004'     MGID(E)
     C                   ENDIF
     C                   ELSE
     C                   Z-ADD     PTO8          WKSCDT
     C                   ENDIF                                                  IF INVALID
      *
      * IF THE DATE IS VALID,
      *     CHECK THAT IT IS NOT IN A CLOSED PERIOD
      *     IF IT IS IN THE CURRENT PERIOD,
      *         CHECK THAT THE PERIOD CLOSE PROGRAMS ARE NOT RUNNING
      *
     C     *IN39         IFEQ      *OFF                                         IF GOOD DATE
      *
     C                   SELECT
     C     WKSCDT        WHENLT    DABPDT
     C                   SETON                                        39
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS09007'     MGID(E)
     C                   ENDIF
      *
     C     WKSCDT        WHENGE    DABPDT
     C     WKSCDT        ANDLE     DAEPDT
     C     DAPPFL        ANDNE     *BLANK
     C                   SETON                                        39
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS09016'     MGID(E)
     C                   ENDIF
     C                   ENDSL
      *
     C                   ENDIF                                                  IF GOOD DATE
     C                   ENDSL
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * Validate Trucker
      *---------------------------------------------------------------
      *
      * Validations:
      *   1) must exist in Trucker master file
      *   2) must be 'active'
      *
     C     $ticd         begsr
      *
     C                   MOVE      *BLANK        C1TINM
      *
     C     C1TICD        IFNE      *BLANK                                       IF TRUCKER
     C     C1TICD        CHAIN     HSP046                             92
     C     *IN92         IFEQ      *ON                                          IF BAD
     C                   SETON                                        33
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS00235'     MGID(E)
     C                   CLEAR                   MGW
     C                   MOVEA     C1TICD        MGW(1)
     C                   MOVEA     MGW           MGDT(E)
     C                   ENDIF
     C                   ELSE
      *
     C                   MOVEL     TITINM        C1TINM
      *
     C                   if        tiaist <> 'A'                                If not active
     C                   seton                                        33
     C                   if        error < maxmsg
     C                   add       1             error
     C                   exsr      $err1
     C                   eval      mgid(e) = 'HS00231'
     C                   clear                   mgw
     C                   movea     c1ticd        mgw(1)
     C                   movea     mgw           mgdt(e)
     C                   endif
     C                   endif                                                  If not active
     C                   ENDIF                                                  IF BAD
     C                   ENDIF                                                  IF TRUCKER
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * $CLR1 - SUBROUTINE TO CLEAR THE SUBFILE ON SCREEN 1
      *---------------------------------------------------------------
      *
     C     $CLR1         BEGSR
      *
      * CLEAR CONTROL RECORD ACCUMULATORS
      *
     C                   Z-ADD     0             C1SCHD
      *
      * CLEAR WORKFIELD ACCUMULATORS
      *
     C                   Z-ADD     0             WKSCHD
      *
     C                   Z-ADD     0             HDRRN1
     C                   Z-ADD     0             COUNT
     C                   Z-ADD     0             WKLNNO
      *
     C                   CLEAR                   HP129S1
     C                   SETON                                        83        SFLCLR
     C                   WRITE     HP129C1
     C                   SETOFF                                       818283
      *                                                    81=SFLDSP
      *                                                    82=SFLEND
      *                                                    83=SFLCLR
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * $FRSH1 - SUBROUTINE TO REFRESH/FILL THE SUBFILE ON SCREEN 1
      *---------------------------------------------------------------
      *
     C     $FRSH1        BEGSR
      *
      * SET INDICATORS TO CONTROL/PROTECT SCREEN FIELDS
      *
     C                   EXSR      $IND
      *
      * LOADING THE SUBFILE IN THIS PROGRAM IS DIFFERENT THAN IN THE
      * SHELL PROGRAMS AND DEPENDS ON THE 'MODE'.
      * FOR CREATE MODE, WE LOAD A PAGE OF BLANK LINES.
      * FOR REVISE MODE, WE LOAD A PAGE OF DATABASE RECORDS AND, IF WE
      *                  HIT EOF, WE FINISH THE PAGE WITH BLANK LINES.
      *
     C                   SELECT
     C     MODE          WHENEQ    CREATE
     C                   EXSR      $CLRLN
     C                   EXSR      $SETOF
     C                   DO        WKPLIN
     C                   EXSR      $BLANK
     C                   ENDDO
      *
     C     MODE          WHENEQ    REVISE
     C     C1MVSN        SETLL     HSP075
     C     *IN82         DOUEQ     *ON
     C                   EXSR      $LOAD1
     C                   ENDDO
      *
      * FILL UP THE SUBFILE PAGE WITH BLANK LINES IF NECESSARY
      *
     C     WKPLIN        IFNE      0
     C     WKPLIN        ANDNE     COUNT
     C     COUNT         DIV       WKPLIN        WKFLD
     C                   MVR                     WKRMDR
     C     WKPLIN        SUB       WKRMDR        WKDIFF
     C                   EXSR      $CLRLN
     C                   EXSR      $SETOF
     C                   DO        WKDIFF
     C                   EXSR      $BLANK
     C                   ENDDO
     C                   ENDIF
      *
     C                   ENDSL
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * $HDR - SETUP THE TRANSFER HEADER DATA FOR THE CONTROL RECORD
      *---------------------------------------------------------------
      *
     C     $HDR          BEGSR
      *
     C                   Z-ADD     MHORFS        c1orfscd
     C                   Z-ADD     MHDNFS        c1dnfscd
     C                   MOVEL     MHTICD        C1TICD
      *
      * RETRIEVE ORIGIN FARM SITE NAME
      *
     C     c1orfscd      CHAIN     HSP018                             92
     C     *IN92         IFEQ      *OFF
     C                   MOVEL     FSFSNM        c1orfsnm
     C                   ELSE
     C                   MOVEL(P)  'UNKNOWN'     c1orfsnm
     C                   ENDIF
      *
      * RETRIEVE DESTINATION FARM SITE NAME
      *
     C     c1dnfscd      CHAIN     HSP018                             92
     C     *IN92         IFEQ      *OFF
     C                   MOVEL     FSFSNM        c1dnfsnm
     C                   ELSE
     C                   MOVEL(P)  'UNKNOWN'     c1dnfsnm
     C                   ENDIF
      *
      * FLIP SCHEDULED SHIP DATE TO MDY FORMAT
      *
     C                   Z-ADD     MHSCDT        WKCYMD
     C                   EXSR      $TOMDY
     C                   Z-ADD     WKMDY         C1MDY
      *
      * RETRIEVE TRUCKER NAME
      *
     C     C1TICD        IFNE      *BLANK
     C     C1TICD        CHAIN     HSP046                             92
     C     *IN92         IFEQ      *OFF
     C                   MOVEL     TITINM        C1TINM
     C                   ELSE
     C                   MOVEL(P)  'UNKNOWN'     C1TINM
     C                   ENDIF
     C                   ENDIF
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * $LOAD1- SUBROUTINE TO LOAD THE SUBFILE ON SCREEN 1
      *---------------------------------------------------------------
      *
     C     $LOAD1        BEGSR
      *
      *  RETRIEVE TRANSFER DETAIL RECORDS FOR THE TRANSFER MOVEMENT.
      *
     C     C1MVSN        READE(N)  HSP075                                 82
     C     *IN82         IFEQ      *OFF                                         IF NOT EOF
     C                   SETON                                        85
      *
      * CLEAR FIELDS IN SUBFILE LINE
      *
     C                   EXSR      $CLRLN
      *
      * ACCUMULATE HEAD VALUES FOR CONTROL RECORD
      *
     C                   ADD       MDSCHD        C1SCHD
      *
      * SET UP LINE NUMBER FIELDS--A WORKFIELD IN CASE YOU HAVE TO
      * FILL THE REST OF THE PAGE WITH BLANK LINES--AND THE HIDDEN
      * FIELD ON THE SUBFILE RECORD.
      *
     C                   Z-ADD     MDLNNO        WKLNNO
     C                   Z-ADD     MDLNNO        HDLNNO
      *
     C                   Z-ADD     MDORSN        hdorhgsn
     C                   Z-ADD     MDDNSN        hddnhgsn
      *
     C                   Z-ADD     MDSCHD        dfschd
      *
      * RETRIEVE ALPHA HOG GROUP CODES AND GROUP STATUS
      *
     C                   EXSR      $rtvhgcd
      *
      * RESET INDICATORS
     C                   EXSR      $IND
      *
     C                   ADD       1             COUNT
     C                   ADD       1             HDRRN1
      *
     C                   WRITE     HP129S1
     C                   SETON                                        81
     C                   Z-ADD     HDRRN1        HDPAG1
     C                   ENDIF                                                  IF NOT EOF
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * $BLANK- SUBROUTINE TO LOAD BLANK SUBFILE RECORDS FOR SCREEN 1
      *---------------------------------------------------------------
      *
     C     $BLANK        BEGSR
      *
     C                   ADD       1             WKLNNO
     C                   Z-ADD     WKLNNO        HDLNNO
      *
     C                   ADD       1             COUNT
     C                   ADD       1             HDRRN1
      *
     C                   EXSR      $IND
     C                   WRITE     HP129S1
      *
     C                   SETON                                        81
     C                   Z-ADD     HDRRN1        HDPAG1
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * $CLRLN - CLEAR SUBFILE LINE
      *---------------------------------------------------------------
      *
      * THIS SUBROUTINE INITIALIZES THE FIELDS IN THE SUBFILE RECORD
      *
     C     $CLRLN        BEGSR
      *
      * ORIGIN
     C                   MOVE      *BLANK        dforhgcd
     C                   MOVE      *BLANK        dforptcd
     C                   MOVE      *BLANK        dforppcd
     C                   MOVE      *BLANK        dforgscd
     C                   MOVE      *BLANK        dforgtcd
     C                   MOVE      *BLANK        dforblcd
     C                   MOVE      *BLANK        dforrmcd
     C                   Z-ADD     0             dfschd
     C                   Z-ADD     0             hdorhgsn
      *
      * DESTINATION
     C                   MOVE      *BLANK        dfdnhgcd
     C                   MOVE      *BLANK        dfdngscd
     C                   MOVE      *BLANK        dfdngtcd
     C                   MOVE      *BLANK        dfdnptcd
     C                   MOVE      *BLANK        dfdnppcd
     C                   MOVE      *BLANK        dfdnblcd
     C                   MOVE      *BLANK        dfdnrmcd
     C                   Z-ADD     0             hddnhgsn
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * $OFFIN - REWRITE THE SUBFILE RECORDS WITH THE INDICATORS OFF
      *---------------------------------------------------------------
      *
      * THIS SUBROUTINE KEEPS THE CURSOR FROM BEING IN THE SUBFILE
      * WHILE THE DISPLAYED ERROR MESSAGE IS FOR THE CONTROL RECORD.
      *
     C     $OFFIN        BEGSR
      *
      * READ/PROCESS ALL THE CHANGED SUBFILE RECORDS
      *
     C                   SETOFF                                       80
     C     *IN80         DOUEQ     *ON                                          DO PROCESS
     C                   READC     HP129S1                                80
     C     *IN80         IFEQ      *OFF                                         IF REC READ
      *
      * SETON MDT
     C                   SETON                                        85
      *
      * SET OFF THE ERROR INDICATORS IN THE SUBFILE
      * AND THEN RESET INDICATORS
      *
     C                   EXSR      $SETOF
     C                   EXSR      $IND
      *
     C                   UPDATE    HP129S1
     C                   ENDIF                                                  IF REC READ
     C                   ENDDO                                                  DO PROCESS
      * SET MDT
     C                   SETOFF                                       85
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * $EDS1 - SUBROUTINE TO READ AND EDIT CHANGED SUBFILE RECORDS
      *          FOR SCREEN 1
      *---------------------------------------------------------------
      *
     C     $EDS1         BEGSR
      *
      * CLEAR WORKFIELD ACCUMULATORS
      *
     C                   Z-ADD     0             WKSCHD
      *
      * SET THE FLAG THAT WILL DETERMINE WHETHER THE USER KEYED
      * ANY SUBFILE ENTRIES.
      *
     C                   MOVEL     NO            ENTRFL
     C                   MOVE      *BLANK        SVORPTCD
     C                   MOVE      *BLANK        SVORPPCD
     C                   MOVE      *BLANK        SVDNPTCD
     C                   MOVE      *BLANK        SVDNPPCD
      *
      * READ/PROCESS ALL THE CHANGED SUBFILE RECORDS
      *
     C                   SETOFF                                       80
     C     *IN80         DOUEQ     *ON                                          DO PROCESS
     C                   READC     HP129S1                                80
     C     *IN80         IFEQ      *OFF                                         IF REC READ
      *
      * SETON MDT
     C                   SETON                                        85
      *
      * SET OFF THE ERROR INDICATORS IN THE SUBFILE
      *
     C                   EXSR      $SETOF
      *
      * IF THERE ARE NO USER ENTRIES FOR THE CHANGED SUBFILE RECORD,
      * CLEAR ALL FIELDS ON THE LINE
      *
     C     dforblcd      IFEQ      *BLANK                                       IF ENTRY
     C     dforrmcd      ANDEQ     *BLANK
     C     dforhgcd      ANDEQ     *BLANK
     C     dfschd        ANDEQ     0
     C     dfdnblcd      ANDEQ     *BLANK
     C     dfdnrmcd      ANDEQ     *BLANK
     C     dfdnhgcd      ANDEQ     *BLANK
     C                   EXSR      $CLRLN
     C                   ELSE
      *
      * Accumulate scheduled head for comparison with the total values keyed
      * by the user.
      *
     C                   ADD       dfschd        WKSCHD
      *
     C                   MOVEL     YES           ENTRFL
      *
      * If groups are blank, clear output only group status field.
      *
     C                   if        dforhgcd = *blank
     C                   move      *blank        dforgscd
     C                   move      *blank        dforgtcd
     C                   move      *blank        dforptcd
     C                   move      *blank        dforppcd
     C                   endif
      *
     C                   if        dfdnhgcd = *blank
     C                   move      *blank        dfdngscd
     C                   move      *blank        dfdngtcd
     C                   move      *blank        dfdnptcd
     C                   move      *blank        dfdnppcd
     C                   endif
      *
      * The user must at least enter an origin building or group. If neither are
      * entered, issue message; otherwise, validate and attempt to retrieve/
      * default in the missing values.
      *
     C                   if        dforblcd = *blank and                        If no origin
     C                             dforhgcd = *blank
     C                   seton                                        3435
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS09132'     mgid(e)
     C                   exsr      $err1
     C                   endif
     C                   else
      *
      * Perform 'origin' edits
      *
     C                   exsr      $edorigin
     C                   endif                                                  If no origin
      *
      *
      * The user must at least enter a destination building or group. If neither
      * are entered, issue message; otherwise, validate and attempt to retrieve/
      * default in the missing values.
      *
     C                   if        dfdnblcd = *blank and                        If no destination
     C                             dfdnhgcd = *blank
     C                   seton                                        3637
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS09132'     mgid(e)
     C                   exsr      $err1
     C                   endif
     C                   else
      *
      * Perform 'destination' edits
      *
     C                   exsr      $eddest
     C                   endif                                                  If no destination
      *
     C                   endif                                                  IF ENTRY
      *
      * RESET INDICATORS
     C                   EXSR      $IND
     C                   UPDATE    HP129S1
     C                   EXSR      $SETOF
     C                   ENDIF                                                  IF REC READ
     C                   ENDDO                                                  DO PROCESS
      * SET MDT
     C                   SETOFF                                       85
      *
      *---------------------------------------------------------------
      * AFTER ALL SUBFILE RECORDS HAVE BEEN EDITED,
      * ISSUE AN ERROR MESSAGE IF THERE ARE NO SUBFILE ENTRIES
      *
     C     ENTRFL        IFEQ      NO                                           IF NO ENTRY
     C     ERROR         ANDLT     MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS00593'     MGID(E)
     C                   ENDIF                                                  IF NO ENTRY
      *
      * IF THERE ARE NO ERRORS ON THE SUBFILE, CHECK THAT THE
      * ACCUMUALTED HEAD VALUES MATCH THE USER-KEYED VALUES.
      *
     C     ERROR         IFEQ      0                                            IF NO ERROR
      *
     C     C1SCHD        IFNE      WKSCHD                                       IF NE SCHED
     C                   SETON                                        30
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS00624'     MGID(E)
     C                   CLEAR                   MGW
     C                   MOVE      C1SCHD        XXHD
     C                   MOVEA     XXHD          MGW(1)
     C                   MOVE      WKSCHD        XXHD6
     C                   MOVEA     XXHD6         MGW(6)
     C                   MOVEA     MGW           MGDT(E)
     C                   ENDIF
     C                   ENDIF                                                  IF NE SCHED
      *
     C                   ENDIF                                                  IF NO ERROR
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $edorigin - Edits for origin entries
      *----------------------------------------------------------------
      *
     C     $edorigin     begsr
      *
      * Validate:
      *  1) Building
      *  2) Room
      *  3) Hog group
      *
     C                   exsr      $orblcd
      *
      * Edit room if a) it is not blank and b) it was not edited previously
      * in the logic that attempted to default a group for a building.
      * (The check on indicator 40 keeps you from issuing a second invalid
      * room message.)
      *
     C                   if        dforrmcd <> *blank and *in40 = *off
     C                   exsr      $orrmcd
     C                   endif
      *
     C                   exsr      $orhgcd
      *
      *
      * VALIDATE SCHEDULED HEAD
      *
     C                   SELECT
     C     dfschd        WHENEQ    0
     C                   SETON                                        38
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS09001'     MGID(E)
     C                   EXSR      $ERR1
     C                   ENDIF
      *
     C     dfschd        WHENLT    0
     C                   SETON                                        38
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS09000'     MGID(E)
     C                   EXSR      $ERR1
     C                   ENDIF
     C                   ENDSL
      *
     C                   endsr
      /EJECT
      *----------------------------------------------------------------
      * $orblcd - validate origin building
      *----------------------------------------------------------------
      *
     C     $orblcd       begsr
      *
      * If building is blank and hog group is entered, default building and
      * room from hog group.
      *
     C                   if        dforblcd = *blank and dforhgcd <> *blank
     C     dforhgcd      chain     hsp034                             92
     C                   if        *in92 = *off
     C                   move      hgblcd        dforblcd
     C                   move      hgrmcd        dforrmcd
     C                   endif
     C                   endif
      *
      * Validations:
      *  1) building is required
      *  2) building must exist on the specified farm site
      *
     C                   select
     C                   when      dforblcd = *blank
     C                   seton                                        35
     C                   if        error < maxmsg
     C                   add       1             error
     C                   exsr      $err1
     C                   eval      mgid(e) = 'HS09001'
     C                   endif
     C                   other
      *
     C     key01         chain     hsp019                             92
     C                   if        *in92 = *on                                  If not found
     C                   seton                                        35
     C                   if        error < maxmsg
     C                   add       1             error
     C                   exsr      $err1
     C                   eval      mgid(e) = 'HS00116'
     C                   clear                   mgw
     C                   movea     dforblcd      mgw(1)
     C                   move      c1orfscd      xxalphfscd
     C                   movea     xxalphfscd    mgw(6)
     C                   movea     mgw           mgdt(e)
     C                   endif
     C                   endif                                                  If not found
     C                   endsl
      *
      * Attempt to default in a group when:
      *   1) building is valid and group is blank and
      *   2) room is blank or valid
      *
     C                   if        *in35 = *off and dforhgcd = *blank           If valid building
      *
     C                   if        dforrmcd <> *blank
     C                   exsr      $orrmcd
     C                   endif
      *
     C                   if        *in40 = *off                                 If room OK/blank
     C                   exsr      $orgroup
     C                   endif                                                  If room OK/blank
     C                   endif                                                  If valid building
      *
     C                   endsr
      /EJECT
      *----------------------------------------------------------------
      * $orrmcd - validate origin room
      *----------------------------------------------------------------
      *
      * Validations:
      *  1) required
      *  2) must exist in the building on the farm site
      *  3) must have an active status
      *
     C     $orrmcd       begsr
      *
     C                   select
     C                   when      dforrmcd = *blank
     C                   seton                                        40
     C                   if        error < maxmsg
     C                   add       1             error
     C                   exsr      $err1
     C                   eval      mgid(e) = 'HS09001'
     C                   endif
     C                   other
      *
      * Check that the room exists in the building on the farm site
     *
     C     key02         chain     hsp020                             92
     C                   if        *in92 = *on                                  If no hit
     C                   seton                                        40
     C                   if        error < maxmsg
     C                   add       1             error
     C                   exsr      $err1
     C                   eval      mgid(e) = 'HS00117'
     C                   clear                   mgw
     C                   movea     dforrmcd      mgw(1)
     C                   movea     dforblcd      mgw(6)
     C                   move      c1orfscd      xxalphfscd
     C                   movea     xxalphfscd    mgw(11)
     C                   movea     mgw           mgdt(e)
     C                   endif
     C                   else
     *
      * Room must have an 'active' status
     *
     C                   if        brrmst <> 'A'                                If not active
     C                   seton                                        40
     C                   if        error < maxmsg
     C                   add       1             error
     C                   exsr      $err1
     C                   eval      mgid(e) = 'HS00726'
     C                   clear                   mgw
     C                   movea     dforrmcd      mgw(1)
     C                   movea     mgw           mgdt(e)
     C                   endif
     C                   endif                                                  If not active
     C                   endif                                                  If no hit
      *
     C                   endsl
      *
     C                   endsr
      /EJECT
      *--------------------------------------------------------------------
      * $orgroup - Attempt to find a default group in the origin building
      *--------------------------------------------------------------------
      *
      * If only one group is open in that building or building/room, default
      * it in. But, if multiple groups are open, issue a message to the
      * user to select a group.
      *
     C     $orgroup      begsr
      *
      * Key with either a) farm/building if room is blank or b) farm/
      * building/room if room is populated.
      *
     C                   if        dforrmcd = *blank
     C     key01         chain     hsl034p                            92
     C                   else
     C     key02         chain     hsl034p                            92
     C                   endif
      *
     C                   if        *in92 = *off                                 If open group
      *
      * Check to see if there is another open group.
      * Key with either a) farm/building if room is blank or b) farm/
      * building/room if room is populated.
      *
     C                   if        dforrmcd = *blank
     C     key01         reade     hsl034p                                92
     C                   else
     C     key02         reade     hsl034p                                92
     C                   endif
      *
     C                   if        *in92 = *off                                 If mult group
     C                   seton                                        34
     C                   if        error < maxmsg
     C                   add       1             error
     C                   clear                   mgw
     C                   movea     dforblcd      mgw(1)
      *
     C                   select
     C                   when      dforrmcd = *blank
     C                   movel     'HS00721'     mgid(e)
     C                   other
     C                   movel     'HS00765'     mgid(e)
     C                   movea     dforrmcd      mgw(6)
     C                   endsl
      *
     C                   movea     mgw           mgdt(e)
     C                   exsr      $err1
     C                   endif
     C                   else
      *
      * Only one group is open; default in its values.
      *
     C                   movel     p1hghgcd      dforhgcd
     C                   movel     p1hgrmcd      dforrmcd
     C                   endif                                                  If mult group
     C                   else
      *
      * No groups are open in this building/room
      *
     C                   seton                                        34
     C                   if        error < maxmsg
     C                   add       1             error
     C                   clear                   mgw
     C                   move      c1orfscd      xxalphfscd
     C                   movea     xxalphfscd    mgw(1)
     C                   movea     dforblcd      mgw(6)
      *
     C                   select
     C                   when      dforrmcd = *blank
     C                   movel     'HS00722'     mgid(e)
     C                   other
     C                   movel     'HS00766'     mgid(e)
     C                   movea     dforrmcd      mgw(11)
     C                   endsl
      *
     C                   movea     mgw           mgdt(e)
     C                   exsr      $err1
     C                   endif
     C                   endif                                                  If open group
      *
     C                   endsr
      /EJECT
      *----------------------------------------------------------------
      * $ORHGCD - EDIT ORIGIN HOG GROUP
      *----------------------------------------------------------------
      *
     C     $ORHGCD       BEGSR
      *
     C                   Z-ADD     0             hdorhgsn
     C                   move      *blank        dforgscd
     C                   move      *blank        dforgtcd
     C                   move      *blank        dforptcd
     C                   move      *blank        dforppcd
      *
     C                   SELECT
     C     dforhgcd      WHENEQ    *BLANK
     C                   SETON                                        34
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS09001'     MGID(E)
     C                   EXSR      $ERR1
     C                   ENDIF
     C                   OTHER
      *
     C     dforhgcd      CHAIN     HSP034                             91
     C     *IN91         IFEQ      *ON                                          IF BAD GROUP
     C                   SETON                                        34
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS00261'     MGID(E)
     C                   CLEAR                   MGW
     C                   MOVEA     dforhgcd      MGW(1)
     C                   MOVEA     MGW           MGDT(E)
     C                   EXSR      $ERR1
     C                   ENDIF
     C                   ELSE
      *
     C                   MOVEL     HGGSCD        dforgscd
     C                   MOVEL     HGGTCD        dforgtcd
     C                   MOVEL     HGPTCD        dforptcd
     C                   MOVEL     HGPPCD        dforppcd
      *
      * ORIGIN MUST ALWAYS HAVE A STATUS OF OPENED
      *
     C     HGGSCD        IFNE      'OP'                                         IF STATUS
     C                   SETON                                        34
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS00603'     MGID(E)
     C                   CLEAR                   MGW
     C                   MOVEA     dforhgcd      MGW(1)
     C                   MOVEA     HGGSCD        MGW(8)
     C                   MOVEA     MGW           MGDT(E)
     C                   EXSR      $ERR1
     C                   ENDIF
     C                   ENDIF                                                  IF STATUS
      *
      * GROUP MUST BE ON THE ORIGIN FARM
      *
     C     HGFSCD        IFNE      c1orfscd                                     IF DIFF FARM
     C                   SETON                                        34
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS00468'     MGID(E)
     C                   CLEAR                   MGW
     C                   MOVEA     dforhgcd      MGW(1)
     C                   MOVE      HGFSCD        xxalphfscd
     C                   MOVEA     xxalphfscd    MGW(8)
     C                   MOVE      c1orfscd      xxalphfscd
     C                   MOVEA     xxalphfscd    MGW(13)
     C                   MOVEA     MGW           MGDT(E)
     C                   EXSR      $ERR1
     C                   ENDIF
     C                   ENDIF                                                  IF DIFF FARM
      *
      *
      * Group building must match subfile building---if there is a building
      * on the subfile.
     C                   select
     C                   when      dforblcd = *blank
     C                   movel     hgblcd        dforblcd
     C                   other
     C                   if        dforblcd <> hgblcd                           If mismatch building
     C                   seton                                        34
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS00481'     mgid(e)
     C                   clear                   mgw
     C                   movea     dforhgcd      mgw(1)
     C                   movea     hgblcd        mgw(8)
     C                   movea     dforblcd      mgw(13)
     C                   movea     mgw           mgdt(e)
     C                   exsr      $err1
     C                   endif
     C                   endif                                                  If mismatch building
     C                   endsl
      *
      *
      * Group room must match subfile room---if there is a room
      * on the subfile.
     C                   select
     C                   when      dforrmcd = *blank
     C                   movel     hgrmcd        dforrmcd
     C                   other
     C                   if        dforrmcd <> hgrmcd                           If mismatch room
     C                   seton                                        34
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS00482'     mgid(e)
     C                   clear                   mgw
     C                   movea     dforhgcd      mgw(1)
     C                   movea     hgrmcd        mgw(8)
     C                   movea     dforrmcd      mgw(13)
     C                   movea     mgw           mgdt(e)
     C                   exsr      $err1
     C                   endif
     C                   endif                                                  If mismatch room
     C                   endsl
      *
      *
      * All origin groups must have the same production type
      *
     C                   if        svorptcd <> *blank and                       If type
     C                             svorptcd <> dforptcd
     C                   seton                                        34
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS01140'     mgid(e)
     C                   clear                   mgw
     C                   movea     dforptcd      mgw(1)
     C                   movea     svorptcd      mgw(6)
     C                   movea     mgw           mgdt(e)
     C                   exsr      $err1
     C                   endif
     C                   endif                                                  If type
      *
      *
      * All origin groups must have the same production phase
      *
     C                   if        svorppcd <> *blank and                       If phase
     C                             svorppcd <> dforppcd
     C                   seton                                        34
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS01143'     mgid(e)
     C                   clear                   mgw
     C                   movea     dforppcd      mgw(1)
     C                   movea     svorppcd      mgw(6)
     C                   movea     mgw           mgdt(e)
     C                   exsr      $err1
     C                   endif
     C                   endif                                                  If type
      *
     C                   ENDIF                                                  IF BAD GROUP
     C                   ENDSL
      *
     C     *IN34         IFEQ      *OFF
     C                   Z-ADD     HGHGSN        hdorhgsn
      *
     C                   if        svorptcd = *blank
     C                   move      dforptcd      svorptcd
     C                   endif
      *
     C                   if        svorppcd = *blank
     C                   move      dforppcd      svorppcd
     C                   endif
      *
     C                   ENDIF
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $eddest - Edits for destination entries
      *----------------------------------------------------------------
      *
     C     $eddest       begsr
      *
      * Validate destination:
      *  1) Building
      *  2) Room
      *  3) Hog group
      *
     C                   exsr      $dnblcd
      *
      * Edit room if a) it is not blank and b) it was not edited previously
      * in the logic that attempted to default a group for a building.
      * (The check on indicator 48 keeps you from issuing a second invalid
      * room message.)
      *
     C                   if        dfdnrmcd <> *blank and *in48 = *off
     C                   exsr      $dnrmcd
     C                   endif
      *
     C                   exsr      $dnhgcd
      *
     C                   endsr
      /EJECT
      *----------------------------------------------------------------
      * $dnbl - validate destination building
      *----------------------------------------------------------------
      *
     C     $dnblcd       begsr
      *
      * If building is blank and hog group is entered, default building and
      * room from hog group.
      *
     C                   if        dfdnblcd = *blank and dfdnhgcd <> *blank
     C     dfdnhgcd      chain     hsp034                             92
     C                   if        *in92 = *off
     C                   move      hgblcd        dfdnblcd
     C                   move      hgrmcd        dfdnrmcd
     C                   endif
     C                   endif
      *
      * Validations:
      *  1) building is required
      *  2) building must exist on the specified farm site
      *
     C                   select
     C                   when      dfdnblcd = *blank
     C                   seton                                        37
     C                   if        error < maxmsg
     C                   add       1             error
     C                   exsr      $err1
     C                   eval      mgid(e) = 'HS09001'
     C                   endif
     C                   other
      *
     C     key03         chain     hsp019                             92
     C                   if        *in92 = *on                                  If not found
     C                   seton                                        37
     C                   if        error < maxmsg
     C                   add       1             error
     C                   exsr      $err1
     C                   eval      mgid(e) = 'HS00116'
     C                   clear                   mgw
     C                   movea     dfdnblcd      mgw(1)
     C                   move      c1dnfscd      xxalphfscd
     C                   movea     xxalphfscd    mgw(6)
     C                   movea     mgw           mgdt(e)
     C                   endif
     C                   endif                                                  If not found
     C                   endsl
      *
      * Attempt to default in a group when:
      *   1) building is valid and group is blank and
      *   2) room is blank or valid
      *
     C                   if        *in37 = *off and dfdnhgcd = *blank           If valid building
      *
     C                   if        dfdnrmcd <> *blank
     C                   exsr      $dnrmcd
     C                   endif
      *
     C                   if        *in48 = *off                                 If room OK/blank
     C                   exsr      $dngroup
     C                   endif                                                  If room OK/blank
     C                   endif                                                  If valid building
      *
     C                   endsr
      /EJECT
      *----------------------------------------------------------------
      * $dnrmcd - validate destination room
      *----------------------------------------------------------------
      *
      * Validations:
      *  1) required
      *  2) must exist in the building on the farm site
      *  3) must have an active status
      *
     C     $dnrmcd       begsr
      *
     C                   select
     C                   when      dfdnrmcd = *blank
     C                   seton                                        48
     C                   if        error < maxmsg
     C                   add       1             error
     C                   exsr      $err1
     C                   eval      mgid(e) = 'HS09001'
     C                   endif
     C                   other
      *
      * Check that the room exists in the building on the farm site
     *
     C     key04         chain     hsp020                             92
     C                   if        *in92 = *on                                  If no hit
     C                   seton                                        48
     C                   if        error < maxmsg
     C                   add       1             error
     C                   exsr      $err1
     C                   eval      mgid(e) = 'HS00117'
     C                   clear                   mgw
     C                   movea     dfdnrmcd      mgw(1)
     C                   movea     dfdnblcd      mgw(6)
     C                   move      c1dnfscd      xxalphfscd
     C                   movea     xxalphfscd    mgw(11)
     C                   movea     mgw           mgdt(e)
     C                   endif
     C                   else
     *
      * Room must have an 'active' status
     *
     C                   if        brrmst <> 'A'                                If not active
     C                   seton                                        48
     C                   if        error < maxmsg
     C                   add       1             error
     C                   exsr      $err1
     C                   eval      mgid(e) = 'HS00726'
     C                   clear                   mgw
     C                   movea     dfdnrmcd      mgw(1)
     C                   movea     mgw           mgdt(e)
     C                   endif
     C                   endif                                                  If not active
     C                   endif                                                  If no hit
      *
     C                   endsl
      *
     C                   endsr
      /eject
      *-----------------------------------------------------------------------
      * $dngroup - Attempt to find a default group in the destination building
      *-----------------------------------------------------------------------
      *
     C     $dngroup      begsr
      *
      * If only one group is open/created in that building or building/room, default
      * it in. But, if multiple groups are open/created, issue a message to the
      * user to select a group.
      *
      * Key with either a) farm/building if room is blank or b) farm/
      * building/room if room is populated.
      *
     C                   if        dfdnrmcd = *blank
     C     key03         chain     hsl034o                            92
     C                   else
     C     key04         chain     hsl034o                            92
     C                   endif
     C                   if        *in92 = *off                                 If open/created
      *
      * Check to see if there is another open/created group.
      * Key with either a) farm/building if room is blank or b) farm/
      * building/room if room is populated.
      *
      *
     C                   if        dfdnrmcd = *blank
     C     key03         reade     hsl034o                                92
     C                   else
     C     key04         reade     hsl034o                                92
     C                   endif
      *
     C                   if        *in92 = *off                                 If mult group
     C                   seton                                        36
     C                   if        error < maxmsg
     C                   add       1             error
     C                   clear                   mgw
     C                   movea     dfdnblcd      mgw(1)
      *
     C                   select
     C                   when      dfdnrmcd = *blank
     C                   movel     'HS00767'     mgid(e)
     C                   other
     C                   movel     'HS00769'     mgid(e)
     C                   movea     dfdnrmcd      mgw(6)
     C                   endsl
      *
     C                   movea     mgw           mgdt(e)
     C                   exsr      $err1
     C                   endif
     C                   else
      *
      * Only one group is open/created; default in its values.
      *
     C                   movel     hghgcd        dfdnhgcd
     C                   movel     hgrmcd        dfdnrmcd
     C                   endif                                                  If mult group
     C                   else
      *
      * No groups are open/created in this building/room
      *
     C                   seton                                        36
     C                   if        error < maxmsg
     C                   add       1             error
     C                   clear                   mgw
     C                   move      c1dnfscd      xxalphfscd
     C                   movea     xxalphfscd    mgw(1)
     C                   movea     dfdnblcd      mgw(6)
      *
     C                   select
     C                   when      dfdnrmcd = *blank
     C                   movel     'HS00768'     mgid(e)
     C                   other
     C                   movel     'HS00770'     mgid(e)
     C                   movea     dfdnrmcd      mgw(11)
     C                   endsl
      *
     C                   movea     mgw           mgdt(e)
     C                   exsr      $err1
     C                   endif
     C                   endif                                                  If open/created
      *
     C                   endsr
      /EJECT
      *----------------------------------------------------------------
      * $dnhgcd - Edit destination hog group
      *----------------------------------------------------------------
      *
     C     $dnhgcd       BEGSR
      *
     C                   move      *BLANK        dfdngscd
     C                   move      *BLANK        dfdngtcd
     C                   move      *BLANK        dfdnptcd
     C                   move      *BLANK        dfdnppcd
      *
      * Validations:
      *  1) required
      *  2) must be valid in hog group file
      *  3) must have a status of created or open
      *  4) must be on the destination farm
      *  5) cannot be the same group as the origin group
      *  6) both groups must be wean-to-finish if either group is WTF
      *
     C                   SELECT
     C     dfdnhgcd      WHENEQ    *BLANK
     C                   SETON                                        36
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS09001'     MGID(E)
     C                   EXSR      $ERR1
     C                   ENDIF
     C                   OTHER
      *
     C     dfdnhgcd      CHAIN     HSP034                             91
     C     *IN91         IFEQ      *ON                                          IF BAD GROUP
     C                   SETON                                        36
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS00261'     MGID(E)
     C                   CLEAR                   MGW
     C                   MOVEA     dfdnhgcd      MGW(1)
     C                   MOVEA     MGW           MGDT(E)
     C                   EXSR      $ERR1
     C                   ENDIF
     C                   ELSE
      *
     C                   MOVEL     HGGSCD        dfdngscd
     C                   MOVEL     HGGTCD        dfdngtcd
     C                   MOVEL     HGPTCD        dfdnptcd
     C                   MOVEL     HGPPCD        dfdnppcd
      *
      * GROUP MUST HAVE A STATUS OF OPENED OR CREATED
      *
     C     HGGSCD        IFEQ      'DS'                                         IF STATUS
     C     HGGSCD        OREQ      'CL'
     C     HGGSCD        OREQ      'VD'
     C                   SETON                                        36
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS00602'     MGID(E)
     C                   CLEAR                   MGW
     C                   MOVEA     dfdnhgcd      MGW(1)
     C                   MOVEA     HGGSCD        MGW(8)
     C                   MOVEA     MGW           MGDT(E)
     C                   EXSR      $ERR1
     C                   ENDIF
     C                   ENDIF                                                  IF STATUS
      *
      * GROUP MUST BE ON THE DESTINATION FARM
      *
     C     HGFSCD        IFNE      c1dnfscd                                     IF DIFF FARM
     C                   SETON                                        36
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS00468'     MGID(E)
     C                   CLEAR                   MGW
     C                   MOVEA     dfdnhgcd      MGW(1)
     C                   MOVE      HGFSCD        xxalphfscd
     C                   MOVEA     xxalphfscd    MGW(8)
     C                   MOVE      c1dnfscd      xxalphfscd
     C                   MOVEA     xxalphfscd    MGW(13)
     C                   MOVEA     MGW           MGDT(E)
     C                   EXSR      $ERR1
     C                   ENDIF
     C                   ENDIF                                                  IF DIFF FARM
      *
      *
      * All destination groups must have the same production type
      *
     C                   if        svdnptcd <> *blank and                       If type
     C                             svdnptcd <> dfdnptcd
     C                   seton                                        36
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS01141'     mgid(e)
     C                   clear                   mgw
     C                   movea     dfdnptcd      mgw(1)
     C                   movea     svdnptcd      mgw(6)
     C                   movea     mgw           mgdt(e)
     C                   exsr      $err1
     C                   endif
     C                   endif                                                  If type
      *
      *
      * All destination groups must have the same production phase
      *
     C                   if        svdnppcd <> *blank and                       If phase
     C                             svdnppcd <> dfdnppcd
     C                   seton                                        36
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS01142'     mgid(e)
     C                   clear                   mgw
     C                   movea     dfdnppcd      mgw(1)
     C                   movea     svdnppcd      mgw(6)
     C                   movea     mgw           mgdt(e)
     C                   exsr      $err1
     C                   endif
     C                   endif                                                  If phase
      *
     C                   ENDIF                                                  IF BAD GROUP
      *
      *
      * Group building must match subfile building---if there is a building
      * on the subfile.
      *
     C                   select
     C                   when      dfdnblcd = *blank
     C                   movel     hgblcd        dfdnblcd
     C                   other
     C                   if        dfdnblcd <> hgblcd                           If mismatch building
     C                   seton                                        36
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS00481'     mgid(e)
     C                   clear                   mgw
     C                   movea     dfdnhgcd      mgw(1)
     C                   movea     hgblcd        mgw(8)
     C                   movea     dfdnblcd      mgw(13)
     C                   movea     mgw           mgdt(e)
     C                   exsr      $err1
     C                   endif
     C                   endif                                                  If mismatch building
     C                   endsl
      *
      *
      * Group room must match subfile room---if there is a room
      * on the subfile.
     C                   select
     C                   when      dfdnrmcd = *blank
     C                   movel     hgrmcd        dfdnrmcd
     C                   other
     C                   if        dfdnrmcd <> hgrmcd                           If mismatch room
     C                   seton                                        36
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS00482'     mgid(e)
     C                   clear                   mgw
     C                   movea     dfdnhgcd      mgw(1)
     C                   movea     hgrmcd        mgw(8)
     C                   movea     dfdnrmcd      mgw(13)
     C                   movea     mgw           mgdt(e)
     C                   exsr      $err1
     C                   endif
     C                   endif                                                  If mismatch room
     C                   endsl
     C                   ENDSL
      *
      *
      * Edits against origin/destination groups
      *
     C                   if        *in34 = *off and *in36 = *off                If edit more
      *
      * Origin/destination cannot be the same group
      *
     C     dforhgcd      ifeq      dfdnhgcd
     C                   SETON                                        36
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS00609'     MGID(E)
     C                   EXSR      $ERR1
     C                   ENDIF
     C                   endif
      *
      *
      * If either group is WTF, then both origin/destination groups must be WTF
      * (unless the orgin phase is BGF....)
      *
      *
     C                   if        dforppcd <> 'BGF  ' and                      If either WTF
     C                             (dforgtcd = 'W' or dfdngtcd = 'W') and
     C                             (dforgtcd <> 'W' or dfdngtcd <> 'W')
     C                   seton                                        3436
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS00883'     mgid(e)
     C                   exsr      $err1
     C                   endif
     C                   endif                                                  If either WTF
      *
     C                   endif                                                  If edit more
      *
      * Perform additional edits when the destination group is:
      *   1) a wean-to-finish group
      *
     C                   if        dfdngtcd = 'W'
     C                   exsr      $wtf
     C                   endif
      *
      *
     C     *IN36         IFEQ      *OFF
     C                   Z-ADD     HGHGSN        hddnhgsn
      *
     C                   if        svdnptcd = *blank
     C                   move      dfdnptcd      svdnptcd
     C                   endif
      *
     C                   if        svdnppcd = *blank
     C                   move      dfdnppcd      svdnppcd
     C                   endif
      *
     C                   ENDIF
      *
     C                   ENDSR
      /EJECT
      *-------------------------------------------------------------------------
      * Wean-to-finish edits for destination group
      *-------------------------------------------------------------------------
      *
      * You are only in this subroutine when the destination group has a group type
      * of wean-to-finish. Basically, we are just trying to prevent illegal WTF movements
      *
     C     $wtf          begsr
      *
     C                   select                                                 Big select
      * Out of BGF
     C                   when      dforppcd = 'BGF  ' and
     C                             (dfdnppcd = 'NUR  ' or dfdnppcd = 'GF   ')
      * Out of Nursery
      *
     C                   when      dforppcd = 'NUR  ' and
     C                             (dfdnppcd = 'GF   ' or
     C                             (dfdnppcd = 'NUR  ' and
     C                             (c1orfscd <> c1dnfscd or
     C                              dforblcd <> dfdnblcd or
     C                              dforrmcd <> dfdnrmcd)))
      * Out of Grow Finish
      *
     C                   when      dforppcd = 'GF   ' and
     C                             (dfdnppcd = 'GF   ' and
     C                             (c1orfscd <> c1dnfscd or
     C                              dforblcd <> dfdnblcd or
     C                              dforrmcd <> dfdnrmcd))
     C                   other
      *
      * All other transfer combinations into a wean-to-finish group are invalid
      *
     C                   seton                                        36
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS01146'     mgid(e)
     C                   exsr      $err1
     C                   endif
     C                   endsl                                                  Big select
      *
     C                   endsr
      /EJECT
      *----------------------------------------------------------------
      * $IND - RESET INDICATORS
      *----------------------------------------------------------------
      *
     C     $IND          BEGSR
      *
      * THE DSPATR LOGIC USED IN THE DSPF REQUIRES AT LEAST ONE
      * INDICATOR TO BE ON.
      *
     C                   SETON                                        42
      *
      * IF THE TRANSFER WAS DELETED OUT FROM UNDER THE USER, THEN SET
      * *IN90 BACK ON TO CONTROL AVAILABLE FUNCTION KEYS AND INPUT
      * FIELDS.
      *
     C     NOTFLG        IFEQ      YES                                          IF NOT FOUND
     C                   SETON                                        90
     C                   ENDIF                                                  IF NOT FOUND
      *
      *  SET UP FOR USER'S ACTION
      *
     C                   SELECT
     C     DFMODE        WHENEQ    REVISE                                       WH REVISE
     C                   SETON                                        60
     C     DFMODE        WHENEQ    CREATE                                       WH CREATE
     C                   SETON                                        61
     C                   ENDSL
      *
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $SETOF - SET OFF THE ERROR INDICATORS ON THE SUBFILE
      *----------------------------------------------------------------
      *
     C     $SETOF        BEGSR
      *
     C                   SETOFF                                       343536
     C                   SETOFF                                       3738
     C                   SETOFF                                       4048
     C                   SETOFF                                       505455
     C                   SETOFF                                       565758
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $DATE -  MANIPULATE DATES WITH DATE UTILITY
      *----------------------------------------------------------------
      *
     C     $DATE         BEGSR
      *
     C                   CALL      'UT80060R'
     C                   PARM                    PFRM8             8 0
     C                   PARM                    PTO8              8 0
     C                   PARM                    PFRFMT            4
     C                   PARM                    PTOFMT            4
     C                   PARM                    PDAYS             4 0
     C                   PARM                    PCODE             1
     C                   PARM                    PRTRN             1
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $DLT11 - DELETE A MOVEMENT WHEN THE USER TAKES F11=DELETE
      *----------------------------------------------------------------
      *
     C     $DLT11        BEGSR
      *
      * FIRST, CHECK USER'S AUTHORITY TO DELETE MOVEMENTS
      *
     C                   CALL      'HPSECD'
     C                   PARM                    MGID
     C                   PARM                    MGDT
     C                   PARM                    ERROR
     C                   PARM                    SDUSR
     C                   PARM                    MHMSCD
      *
     C     ERROR         IFNE      0                                            IF CAN'T DLT
     C                   MOVEL     SCRN1         ROUTNE
     C                   ELSE
      *
      * THE USER IS AUTHORIZED TO DELETE MOVEMENT, SO
      * CALL THE 'CONFIRM DELETE' PROGRAM WHERE THE USER
      * IS REQUIRED TO ENTER A DELETION COMMENT.
      *
     C                   CALL      'HP113'
     C                   PARM                    C1MVSN
     C                   PARM      *BLANK        XXDMCM
     C                   PARM      *BLANK        RTNCD
      *
      * IF THE RETURN CODE IS ZERO, THE THE USER IS DELETING.
      * WHEN DELETING THE HEADER, WRITE THE AUDIT INFO TO
      * THE 'DELETED MOVEMENT' FILE FIRST.
      *
     C     RTNCD         IFEQ      '00'                                         IF DELETE
     C                   EXSR      $DLTTH
     C                   EXSR      $DLTTD
     C                   EXSR      $DLTME
     C                   MOVEL     EXIT          ROUTNE
     C                   ELSE
     C                   MOVEL     SCRN1         ROUTNE
     C                   ENDIF                                                  IF DELETE
      *
     C                   ENDIF                                                  IF CAN'T DLT
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $DLTTH - DELETE THE TRANSFER MOVEMENT HEADER RECORD
      *----------------------------------------------------------------
      *
     C     $DLTTH        BEGSR
      *
     C     C1MVSN        CHAIN     HSP074                             92
     C     *IN92         IFEQ      *OFF                                         IF FOUND
      *
      * IF THE USER IS DELETING THE MOVEMENT (F11 HAS BEEN TAKEN OR
      * THE INCOMING MODE WAS "DELETE",
      * WRITE THE AUDIT INFO TO THE 'DELETED MOVEMENT' FILE PRIOR
      * TO DELETING THE HEADER RECORD.
      *
     C                   IF        *IN11 = *ON OR MODE = DELETE
     C                   EXSR      $CRTDM
     C                   ENDIF
      * DELETE HEADER
     C                   DELETE    MHREC
     C                   ENDIF                                                  IF FOUND
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $DLTTD - DELETE THE TRANSFER MOVEMENT DETAIL RECORDS
      *----------------------------------------------------------------
      *
     C     $DLTTD        BEGSR
      *
     C     C1MVSN        SETLL     HSP075
     C                   SETOFF                                       95
     C     *IN95         DOUEQ     *ON                                          DO DETAIL
     C     C1MVSN        READE     HSP075                                 95
     C     *IN95         IFEQ      *OFF                                         IF FOUND
     C                   DELETE    MDREC
     C                   ENDIF                                                  IF FOUND
     C                   ENDDO                                                  DO DETAIL
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $DLTME - DELETE ANY/ALL MOVEMENT EVENT RECORDS
      *----------------------------------------------------------------
      *
     C     $DLTME        BEGSR
      *
      * ALWAYS DELETE THE MOVEMENT EVENT RECORD FOR BOTH THE
      * ORIGIN AND DESTINATION HOG GROUPS.
      *
     C                   SETOFF                                       95
     C     C1MVSN        SETLL     HSP058
      *
     C     *IN95         DOUEQ     *ON                                          DO EVENTS
     C     C1MVSN        READE     HSP058                                 95
     C     *IN95         IFEQ      *OFF                                         IF EVENT
     C                   DELETE    MEREC
     C                   ENDIF                                                  IF EVENT
     C                   ENDDO                                                  DO EVENTS
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $CRTDM - CREATE AN AUDIT RECORD FOR THE DELETED MOVEMENT
      *----------------------------------------------------------------
      *
     C     $CRTDM        BEGSR
      *
     C                   Z-ADD     MHMVSN        DMMVSN
     C                   MOVEL     SDUSR         DMUSCD
     C                   Z-ADD     WKUDAT        DMDMDT
     C                   TIME                    HHMMSS
     C                   Z-ADD     HHMM          DMDMTM
     C                   MOVEL     XXDMCM        DMDMCM
      *
     C                   MOVEL     'T'           DMMTCD
     C                   MOVEL     MHMSCD        DMMSCD
     C                   Z-ADD     MHSCDT        DMSCDT
     C                   Z-ADD     0             dmsckldt
     C                   Z-ADD     MHSHDT        DMSHDT
     C                   Z-ADD     MHRCDT        DMRCDT
     C                   Z-ADD     0             DMCVNO
      *
     C                   MOVEL     MHORBO        DMORBO
     C                   MOVEL     MHORPT        DMORPT
     C                   MOVEL     MHORPP        DMORPP
     C                   Z-ADD     MHORFS        DMORFS
      *
     C                   MOVEL     MHDNBO        DMDNBO
     C                   MOVEL     MHDNPT        DMDNPT
     C                   MOVEL     MHDNPP        DMDNPP
     C                   Z-ADD     MHDNFS        DMDNFS
      *
     C                   WRITE     DMREC
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $CRTTH - CREATE A TRANSFER HEADER RECORD
      *----------------------------------------------------------------
      *
     C     $CRTTH        BEGSR
      *
      * IF YOU ARE CREATING A NEW TRANSFER, THEN ASSIGN A MOVEMENT
      * NUMBER.
     C     MODE          IFEQ      CREATE
     C     *LOCK         IN        DAMVSN
     C                   ADD       1             DAMVSN
     C                   OUT       DAMVSN
     C                   Z-ADD     DAMVSN        MHMVSN
     C                   ELSE
     C                   Z-ADD     C1MVSN        MHMVSN
     C                   ENDIF
      *
     C                   MOVEL     'SC'          MHMSCD
      *
     C                   MOVEL     SVDNPTCD      MHDNPT
     C                   MOVEL     SVDNPPCD      MHDNPP
     C                   MOVEL     SVORPTCD      MHORPT
     C                   MOVEL     SVORPPCD      MHORPP
      *
     C                   MOVEL     c1orfscd      MHORFS
     C                   MOVEL     WKORBO        MHORBO
      *
     C                   MOVEL     c1dnfscd      MHDNFS
     C                   MOVEL     WKDNBO        MHDNBO
      *
     C                   MOVEL     C1TICD        MHTICD
     C                   Z-ADD     WKSCDT        MHSCDT
      *
     C                   Z-ADD     0             MHSHDT
     C                   Z-ADD     0             MHSITM
     C                   Z-ADD     0             MHSOTM
     C                   Z-ADD     0             MHRCDT
     C                   Z-ADD     0             MHRITM
     C                   Z-ADD     0             MHROTM
     C                   Z-ADD     0             MHEPDT
     C                   Z-ADD     0             MHPODT
      *
     C                   WRITE     MHREC
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $CRTTD - CREATE TRANSFER DETAIL RECORDS
      *----------------------------------------------------------------
      *
     C     $CRTTD        BEGSR
      *
      *  SETUP THE VALUES FOR FIELDS THAT WON'T CHANGE PER LINE
      *
     C     MODE          IFEQ      CREATE
     C                   Z-ADD     DAMVSN        MDMVSN
     C                   ELSE
     C                   Z-ADD     C1MVSN        MDMVSN
     C                   ENDIF
      *
      *---------------------------------------------------------------
      * READ/PROCESS ALL THE CHANGED SUBFILE RECORDS
      *---------------------------------------------------------------
      *
     C                   SETOFF                                       80
     C     *IN80         DOUEQ     *ON                                          DO READ C
     C                   READC     HP129S1                                80
     C     *IN80         IFEQ      *OFF                                         IF REC READ
     C     dforhgcd      ANDNE     *BLANK
      *
     C                   Z-ADD     HDLNNO        MDLNNO
     C                   Z-ADD     hdorhgsn      MDORSN
     C                   Z-ADD     hddnhgsn      MDDNSN
      *
     C                   Z-ADD     dfschd        MDSCHD
     C                   Z-ADD     0             MDSHHD
     C                   Z-ADD     0             MDQLHD
     C                   Z-ADD     0             MDRJHD
     C                   Z-ADD     0             MDDOHD
     C                   Z-ADD     0             MDSHLB
     C                   Z-ADD     0             MDQLLB
     C                   Z-ADD     0             MDRJLB
     C                   Z-ADD     0             MDDOLB
      *
     C                   WRITE     MDREC
      *
      * CREATE MOVEMENT EVENT RECORDS FOR BOTH THE ORIGIN
      * THE DESTINATION GROUPS
      *
     C                   EXSR      $CRTME
      *
     C                   ENDIF                                                  IF REC READ
     C                   ENDDO                                                  DO READ C
      *
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $CRTME - CREATE MOVEMENT EVENT RECORDS
      *----------------------------------------------------------------
      *
     C     $CRTME        BEGSR
      *
      *
      * SET UP UPDATE TIME STAMP
      *
     C                   Z-ADD     WKUDAT        MEUPDT
     C                   TIME                    MEUPTM
     C                   MOVEL     SDUSR         MEUSCD
     C                   MOVEL     SDPGM         MEPGM
      *
      *
      * SETUP THE MOVEMENT NUMBER AND STATUS
      *
     C     MODE          IFEQ      CREATE
     C                   Z-ADD     DAMVSN        MEMVSN
     C                   ELSE
     C                   Z-ADD     C1MVSN        MEMVSN
     C                   ENDIF
      *
     C                   Z-ADD     0             MECVNO
     C                   Z-ADD     HDLNNO        MELNNO
      *
      * WRITE AN EVENT RECORD FOR THE ORIGIN GROUP
      *
     C                   Z-ADD     hdorhgsn      MEHGSN
     C                   MOVEL     'T'           MEMTCD
     C                   MOVEL     'O'           MEODFL
     C                   Z-ADD     hddnhgsn      MEHSN
      *
     C                   Z-ADD     WKSCDT        MEMEDT
      *
     C                   WRITE     MEREC
      *
      * WRITE AN EVENT RECORD FOR THE DESTINATION GROUP
      *
     C                   Z-ADD     hddnhgsn      MEHGSN
     C                   MOVEL     'T'           MEMTCD
     C                   MOVEL     'D'           MEODFL
     C                   Z-ADD     hdorhgsn      MEHSN
      *
     C                   Z-ADD     WKSCDT        MEMEDT
      *
     C                   WRITE     MEREC
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $F4S1 - F4 PROMPT FOR SCREEN 1
      *----------------------------------------------------------------
      *
     C     $F4S1         BEGSR
      *
     C                   SELECT
      *
      *----------------------------------------------------------------
      * PROMPTED FIELDS IN THE CONTROL RECORD
      *----------------------------------------------------------------
      *
      *
      * TRUCKER (active truckers only)
      *
     C     FLD           WHENEQ    'C1TICD'
     C                   CALL      'HP538'
     C                   PARM      'A'           XXAIST
     C                   PARM      *blank        XXTICD
     C                   PARM      *blank        XXTINM
     C                   PARM      *blank        XXmtechref
     C                   SETON                                        53
     C     XXTICD        IFNE      *BLANK                                       IF SELECTED
     C                   MOVEL(P)  XXTICD        C1TICD
     C                   MOVEL(P)  XXTINM        C1TINM
     C                   ENDIF                                                  IF SELECTED
      *
      * ORIGIN FARM SITE
      *
     C     FLD           WHENEQ    'C1ORFSCD'
     C                   CALL      'HP5040'
     C                   PARM      'A'           xxaist
     C                   PARM      0             xxfscd
     C                   PARM      *blank        xxfsnm
     C                   SETON                                        51
     C     xxfscd        IFNE      0                                            IF SELECTED
     C                   Z-ADD     xxfscd        c1orfscd
     C                   MOVEL     xxfsnm        c1orfsnm
     C                   ENDIF                                                  IF SELECTED
      *
      * DESTINATION FARM SITE
      *
     C     FLD           WHENEQ    'C1DNFSCD'
     C                   CALL      'HP5040'
     C                   PARM      'A'           xxaist
     C                   PARM      0             xxfscd
     C                   PARM      *blank        XXFSNM
     C                   SETON                                        52
     C     xxfscd        IFNE      0                                            IF SELECTED
     C                   Z-ADD     xxfscd        c1dnfscd
     C                   MOVEL     XXFSNM        c1dnfsnm
     C                   ENDIF                                                  IF SELECTED
      *
      *----------------------------------------------------------------
      * PROMPTED FIELDS IN THE SUBFILE
      *----------------------------------------------------------------
      *
      *-------------------
      *  ORIGIN BUILDING
      *-------------------
      *
     C     FLD           WHENEQ    'DFORBLCD'
     C                   EXSR      $RRN1
     C     *IN97         IFEQ      *OFF
     C                   CALL      'HP523'
     C                   PARM      c1orfscd      xxfscd
     C                   PARM      *blank        XXBLCD
     C                   PARM      *blank        XXBLDS
     C     XXBLCD        IFNE      *BLANK
     C                   MOVEL     XXBLCD        dforblcd
     C                   ENDIF
     C                   SETON                                        8555
     C                   UPDATE    HP129S1
     C                   SETOFF                                       8555
      *
      * RESET THE SUBFILE RELATIVE RECORD NUMBER TO WHAT IT WAS BEFORE
      * THE USER TOOK F4 (IT WILL BE OUT-OF-SYNCH IF THE USER HAS
      * SELECTED A RECORD THUS CAUSING THE SUBFILE RECORD TO BE
      * RETRIEVED AND UPDATED.)  BUT, SET THE PAGE OF THE SUBFILE TO
      * BE DISPLAYED TO THE PAGE ON WHICH THE USER TOOK F4.
      *
     C                   Z-ADD     COUNT         HDRRN1
     C                   Z-ADD     HDRR1         HDPAG1
     C                   ENDIF
      *
      *-------------------
      *  ORIGIN ROOM
      *-------------------
      *
     C     FLD           WHENEQ    'DFORRMCD'
     C                   EXSR      $RRN1
     C     *IN97         IFEQ      *OFF
     C                   call      'HP524'
     C                   parm                    c1orfscd
     C                   parm                    dforblcd
     C                   parm      *blank        xxrmcd
     C                   if        xxrmcd <> *blank
     C                   movel     xxrmcd        dforrmcd
     C                   endif
      *
     C                   SETON                                        8557
     C                   UPDATE    HP129S1
     C                   SETOFF                                       8557
      *
      * RESET THE SUBFILE RELATIVE RECORD NUMBER TO WHAT IT WAS BEFORE
      * THE USER TOOK F4 (IT WILL BE OUT-OF-SYNCH IF THE USER HAS
      * SELECTED A RECORD THUS CAUSING THE SUBFILE RECORD TO BE
      * RETRIEVED AND UPDATED.)  BUT, SET THE PAGE OF THE SUBFILE TO
      * BE DISPLAYED TO THE PAGE ON WHICH THE USER TOOK F4.
      *
     C                   Z-ADD     COUNT         HDRRN1
     C                   Z-ADD     HDRR1         HDPAG1
     C                   ENDIF
      *
      *-----------------
      * ORIGIN HOG GROUP
      *-----------------
      *
     C     FLD           WHENEQ    'DFORHGCD'
     C                   EXSR      $RRN1
     C     *IN97         IFEQ      *OFF                                         If hgcd hit
     C                   CALL      'HP5950'
     C                   PARM                    c1orfscd
     C                   PARM                    c1orfsnm
     C                   PARM      dforblcd      XXBLCD
     C                   PARM      dforrmcd      XXRMCD
     C                   PARM      *blank        XXHGCD
     C                   PARM      *blank        XXGSCD
     C                   PARM      *blank        XXPTCD
     C                   PARM      *blank        XXPPCD
     C     XXHGCD        IFNE      *BLANK
     C                   MOVEL     XXHGCD        dforhgcd
     C                   MOVEL     XXBLCD        DFORBLCD
     C                   MOVEL     XXRMCD        DFORRMCD
     C                   MOVEL     XXGSCD        DFORGSCD
     C                   MOVEL     XXPTCD        DFORPTCD
     C                   MOVEL     XXPPCD        DFORPPCD
     C                   ENDIF
     C                   SETON                                        8554
     C                   UPDATE    HP129S1
     C                   SETOFF                                       8554
      *
      * RESET THE SUBFILE RELATIVE RECORD NUMBER TO WHAT IT WAS BEFORE
      * THE USER TOOK F4 (IT WILL BE OUT-OF-SYNCH IF THE USER HAS
      * SELECTED A RECORD THUS CAUSING THE SUBFILE RECORD TO BE
      * RETRIEVED AND UPDATED.)  BUT, SET THE PAGE OF THE SUBFILE TO
      * BE DISPLAYED TO THE PAGE ON WHICH THE USER TOOK F4.
      *
     C                   Z-ADD     COUNT         HDRRN1
     C                   Z-ADD     HDRR1         HDPAG1
     C                   ENDIF
      *
      *
      *----------------------
      * DESTINATION BUILDING
      *----------------------
      *
     C     FLD           WHENEQ    'DFDNBLCD'
     C                   EXSR      $RRN1
     C     *IN97         IFEQ      *OFF
     C                   CALL      'HP523'
     C                   PARM      c1dnfscd      xxfscd
     C                   PARM      *blank        XXBLCD
     C                   PARM      *blank        XXBLDS
     C     XXBLCD        IFNE      *BLANK                                       IF SELECTED
     C                   MOVEL     XXBLCD        dfdnblcd
     C                   ENDIF                                                  IF SELECTED
     C                   SETON                                        8557
     C                   UPDATE    HP129S1
     C                   SETOFF                                       8557
      *
      * RESET THE SUBFILE RELATIVE RECORD NUMBER TO WHAT IT WAS BEFORE
      * THE USER TOOK F4 (IT WILL BE OUT-OF-SYNCH IF THE USER HAS
      * SELECTED A RECORD THUS CAUSING THE SUBFILE RECORD TO BE
      * RETRIEVED AND UPDATED.)  BUT, SET THE PAGE OF THE SUBFILE TO
      * BE DISPLAYED TO THE PAGE ON WHICH THE USER TOOK F4.
      *
     C                   Z-ADD     COUNT         HDRRN1
     C                   Z-ADD     HDRR1         HDPAG1
     C                   ENDIF
      *
      *-----------------------
      * DESTINATION ROOM
      *-----------------------
      *
     C     FLD           WHENEQ    'DFDNRMCD'
     C                   EXSR      $RRN1
     C     *IN97         IFEQ      *OFF
     C                   call      'HP524'
     C                   parm                    c1dnfscd
     C                   parm                    dfdnblcd
     C                   parm      *blank        xxrmcd
     C                   if        xxrmcd <> *blank
     C                   movel     xxrmcd        dfdnrmcd
     C                   endif
      *
     C                   SETON                                        8557
     C                   UPDATE    HP129S1
     C                   SETOFF                                       8557
      *
      * RESET THE SUBFILE RELATIVE RECORD NUMBER TO WHAT IT WAS BEFORE
      * THE USER TOOK F4 (IT WILL BE OUT-OF-SYNCH IF THE USER HAS
      * SELECTED A RECORD THUS CAUSING THE SUBFILE RECORD TO BE
      * RETRIEVED AND UPDATED.)  BUT, SET THE PAGE OF THE SUBFILE TO
      * BE DISPLAYED TO THE PAGE ON WHICH THE USER TOOK F4.
      *
     C                   Z-ADD     COUNT         HDRRN1
     C                   Z-ADD     HDRR1         HDPAG1
     C                   ENDIF
      *
      *
      *----------------------
      * DESTINATION HOG GROUP
      *----------------------
      *
     C     FLD           WHENEQ    'DFDNHGCD'
     C                   EXSR      $RRN1
     C                   if        *in97 = *off
     C                   CALL      'HP598'
     C                   PARM                    c1dnfscd
     C                   PARM                    c1dnfsnm
     C                   PARM      dfdnblcd      XXBLCD
     C                   PARM      dfdnrmcd      XXRMCD
     C                   PARM      *blank        XXHGCD
     C                   PARM      *blank        XXGSCD
     C                   PARM      *blank        XXPTCD
     C                   PARM      *blank        XXPPCD
     C     XXHGCD        IFNE      *BLANK
     C                   MOVEL     XXHGCD        dfdnhgcd
     C                   MOVEL     XXBLCD        dfdnblcd
     C                   MOVEL     XXRMCD        dfdnrmcd
     C                   MOVEL     XXGSCD        dfdngscd
     C                   MOVEL     XXPTCD        dfdnptcd
     C                   MOVEL     XXPPCD        dfdnppcd
     C                   ENDIF
     C                   SETON                                        8556
     C                   UPDATE    HP129S1
     C                   SETOFF                                       8556
      *
      * RESET THE SUBFILE RELATIVE RECORD NUMBER TO WHAT IT WAS BEFORE
      * THE USER TOOK F4 (IT WILL BE OUT-OF-SYNCH IF THE USER HAS
      * SELECTED A RECORD THUS CAUSING THE SUBFILE RECORD TO BE
      * RETRIEVED AND UPDATED.)  BUT, SET THE PAGE OF THE SUBFILE TO
      * BE DISPLAYED TO THE PAGE ON WHICH THE USER TOOK F4.
      *
     C                   Z-ADD     COUNT         HDRRN1
     C                   Z-ADD     HDRR1         HDPAG1
     C                   ENDIF
     C                   OTHER
      *
      * F4 NOT VALID FOR THIS FIELD SO ISSUE MESSAGE
      *
     C     ERROR         IFLT      MAXMSG                                       IF FIRST ERR
     C                   ADD       1             ERROR
     C                   MOVEL     'HS09011'     MGID(E)
     C                   ENDIF                                                  IF FIRST ERR
      *
     C                   ENDSL
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * $ERR1  - SUBROUTINE TO SET THE SUBFILE TO REDISPLAY AT
      *          THE FIRST SCREEN THAT HAS ERRORS
      *---------------------------------------------------------------
      *
     C     $ERR1         BEGSR
      *
     C     ERROR         IFEQ      1
     C                   Z-ADD     HDRRN1        HDPAG1
     C                   ENDIF
      *
     C                   ENDSR
      *
      *---------------------------------------------------------------
      * $EXIT - SUBROUTINE TO SET ON LR AND END JOB
      *---------------------------------------------------------------
      *
     C     $EXIT         BEGSR
      *
     C                   SETON                                        LR
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * $TOMDY - FLIP DATE FROM CCYYMMDD TO MMDDYY
      *---------------------------------------------------------------
      *
     C     $TOMDY        BEGSR
      *
     C                   Z-ADD     WKMON         WKMM
     C                   Z-ADD     WKDAY         WKDD
     C                   Z-ADD     WKYER         WKYY
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * $WRMSG - SUBROUTINE TO WRITE ERROR MESSAGES
      *---------------------------------------------------------------
      *
     C     $WRMSG        BEGSR
      *
     C                   CALL      'UT80045J'                           43
     C                   PARM                    MGID
     C                   PARM                    MGDT
     C                   PARM                    ERROR
     C                   PARM      SDPGM         PGM
     C                   PARM      MSGFIL        MSGFL
      *
      * IF CALL TO UT80045J FAILED, A MESSAGE HARDCODED IN THE DSPF
      * COMMAND KEY RECORD WILL BE SET ON. SO, REDISPLAY SCREEN TO USER
      *
     C     *IN43         IFEQ      *ON
     C                   MOVEL     SET1          ROUTNE
     C                   ENDIF
      *
     C                   ENDSR
      *
      *---------------------------------------------------------------
      * $CLMSG - SUBROUTINE TO CLEAR MESSAGES
      *---------------------------------------------------------------
      *
     C     $CLMSG        BEGSR
      *
     C                   CALL      'UT80045J'                           43
     C                   PARM      *BLANKS       MGI
     C                   PARM      *BLANKS       MGD
     C                   PARM      *ZEROS        ERROR
     C                   PARM      SDPGM         PGM
     C                   PARM      MSGFIL        MSGFL
      *
      * IF CALL TO UT80045J FAILED, A MESSAGE HARDCODED IN THE DSPF
      * COMMAND KEY RECORD WILL BE SET ON. SO, REDISPLAY SCREEN TO USER
      *
     C     *IN43         IFEQ      *ON
     C                   MOVEL     SET1          ROUTNE
     C                   ENDIF
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $RRN1  - ROUTINE TO FIND/RETRIEVE THE SUBFILE RECORD TO UPDATE
      *          WHEN RETURNING FROM A SELECT PROGRAM ON SCREEN 1
      *----------------------------------------------------------------
      *
      * HDRR1 IS THE HIDDEN FIELD ASSOCIATED WITH THE KEYWORD SFLCSRRRN
      * ON THE CONTROL RECORD IN THE DSPF.
      * HDRRN1 IS THE VALUE ASSOCIATED WITH THE KEYWORD 'KSFILE' IN
      * THE F-SPECS FOR THE WORKSTATION.
      *
     C     $RRN1         BEGSR
      *
     C                   SETOFF                                       97
      *
     C     HDRR1         IFNE      0
     C                   Z-ADD     HDRR1         HDRRN1
     C     HDRRN1        CHAIN     HP129S1                            97
     C                   ENDIF
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $rtvhgcd - RETRIEVE THE USER-ASSIGNED ALPHA HOG GROUP CODE
      *----------------------------------------------------------------
      *
     C     $rtvhgcd      BEGSR
      *
      * ORIGIN GROUP
      *
     C     MDORSN        CHAIN     HSL034D                            92
     C     *IN92         IFEQ      *ON
     C                   MOVEL     'UNKNOWN'     dforhgcd
     C                   ELSE
     C                   MOVEL     HGHGCD        dforhgcd
     C                   MOVEL     HGGSCD        dforgscd
     C                   MOVEL     HGGTCD        dforgtcd
     C                   MOVEL     HGPTCD        dforptcd
     C                   MOVEL     HGPPCD        dforppcd
     C                   MOVEL     HGBLCD        dforblcd
     C                   MOVEL     HGRMCD        dforrmcd
     C                   ENDIF
      *
      * DESTINATION GROUP
      *
     C     MDDNSN        CHAIN     HSL034D                            92
     C     *IN92         IFEQ      *ON
     C                   MOVEL     'UNKNOWN'     dfdnhgcd
     C                   ELSE
     C                   MOVEL     HGHGCD        dfdnhgcd
     C                   MOVEL     HGGSCD        dfdngscd
     C                   MOVEL     HGGTCD        dfdngtcd
     C                   MOVEL     HGPTCD        dfdnptcd
     C                   MOVEL     HGPPCD        dfdnppcd
     C                   MOVEL     HGBLCD        dfdnblcd
     C                   MOVEL     HGRMCD        dfdnrmcd
     C                   ENDIF
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * *INZSR - INITIALIZATION SUBROUTINE
      *---------------------------------------------------------------
      *
     C     *INZSR        BEGSR
      *
      *
      * PARM LISTS
      *
     C     *ENTRY        PLIST
     C                   PARM                    MODE
     C                   PARM                    C1MVSN
     C                   PARM                    RTNCD
      *
      *
      * Key lists
      *
     C     key01         klist
     C                   kfld                    c1orfscd
     C                   kfld                    dforblcd
      *
     C     key02         klist
     C                   kfld                    c1orfscd
     C                   kfld                    dforblcd
     C                   kfld                    dforrmcd
      *
      *
     C     key03         klist
     C                   kfld                    c1dnfscd
     C                   kfld                    dfdnblcd
      *
      *
     C     key04         klist
     C                   kfld                    c1dnfscd
     C                   kfld                    dfdnblcd
     C                   kfld                    dfdnrmcd
      *
      *
      *
      * GLOBAL DEFINES
      *
     C                   MOVE      *BLANK        PGM              10            USED IN MSG
     C                   MOVE      *BLANK        MSGFL            10            USED IN MSG
     C                   MOVE      *BLANK        ROUTNE            6            ROUTINE
     C                   Z-ADD     0             MAXRRN            4 0          REPOSITION
     C                   MOVEL     NO            RESET             1            REPOSITION
     C                   MOVEL     NO            NOTFLG            1
     C                   MOVEL     '00'          RTNCD             2            RETURN CODE
     C     *LIKE         DEFINE    DFMODE        MODE                           SFL POSITION
     C     *LIKE         DEFINE    IDFLOW        WKFLOW                         SFL POSITION
     C                   MOVEL     NO            EDITFL            1
     C                   MOVEL     NO            ERRFL             1
      *
      *
      * ALPHA AND NUMERIC FIELDS FOR USE AS PARMS
      *
     C     *LIKE         DEFINE    DMDMCM        XXDMCM
     C                   MOVE      *BLANK        XXMVSN            7
     C     *LIKE         DEFINE    HGHGCD        XXHGCD
     C     *LIKE         DEFINE    FSFSCD        xxfscd
     C                   MOVE      *BLANK        xxalphfscd        5
     C     *LIKE         DEFINE    FSFSNM        XXFSNM
     C     *LIKE         DEFINE    HGBLCD        XXBLCD
     C     *like         define    fbblds        XXBLDS
     C     *LIKE         DEFINE    HGRMCD        XXRMCD
     C     *LIKE         DEFINE    HGGSCD        XXGSCD
     C     *LIKE         DEFINE    HGPTCD        XXPTCD
     C     *LIKE         DEFINE    HGPTCD        XXPPCD
     C     *LIKE         DEFINE    TITICD        XXTICD
     C     *LIKE         DEFINE    TITINM        XXTINM
     C                   MOVE      *BLANK        XXAIST            1
     C                   MOVE      *BLANK        XXHD              5
     C                   MOVE      *BLANK        XXHD6             6
     C     *LIKE         DEFINE    fsmtechref    xxmtechref
      *
      * CONTROL FIELDS, FLAGS, COUNTERS, WORK FIELDS
      *
     C     *LIKE         DEFINE    FSPTCD        SVDNPTCD
     C     *LIKE         DEFINE    HGPPCD        SVDNPPCD
      *
     C     *LIKE         DEFINE    FSPTCD        SVORPTCD
     C     *LIKE         DEFINE    HGPPCD        SVORPPCD
      *
     C     *LIKE         DEFINE    DMDMDT        WKUDAT
     C     *LIKE         DEFINE    MHSCDT        WKSCDT
      *
     C     *LIKE         DEFINE    FSFSBO        WKORBO
      *
     C     *LIKE         DEFINE    FSFSBO        WKDNBO
      *
      * ACCUMULATORS FOR SUBFILE HEAD
      *
     C     *LIKE         DEFINE    MDSCHD        WKSCHD          + 1
      *
     C                   MOVEL     NO            ENTRFL            1            SFL ENTRIES
      *
      * TO SUPPORT F5-REFRESH, LOGIC REQUIRES THE NUMBER OF LINES ON
      * THE SUBFILE PAGE AND ALSO THE NUMBER OF LINES LESS 1.  BY
      * SETTING THESE VALUES UP AS CONSTANT WORK FIELDS, THE PROCESSING
      * LOGIC CAN BE STANDARDIZED BETWEEN PROGRAMS. SINCE THIS DISPLAY
      * HAS FOLD/DROP LOGIC, DOUBLE THE SUBFILE PAGE VALUE.
      *
     C                   Z-ADD     04            WKPLIN            2 0          SFLPAG(0002)
     C     WKPLIN        SUB       1             WKRLIN            2 0          SFLPAG - 1
     C                   Z-ADD     0             COUNT             2 0
     C     *LIKE         DEFINE    COUNT         WKDIFF
     C     *LIKE         DEFINE    COUNT         WKRMDR
     C     *LIKE         DEFINE    COUNT         WKFLD
     C     *LIKE         DEFINE    MDLNNO        WKLNNO
      *
      * THIS PROGRAM DIFFERS FROM THE SHELL PROGRAMS IN THAT F5=REFRESH
      * ALWAYS PLACES THE USER AT THE FIRST PAGE OF THE SUBFILE
      *
     C                   Z-ADD     WKPLIN        MAXRRN
      *
      *
      * FLIP JOB DATE TO CCYYMMDD FOR UPDATING DELETED RECORD INFO
      *
     C                   Z-ADD     UDATE         PFRM8
     C                   Z-ADD     0             PTO8
     C                   MOVEL     'MDY '        PFRFMT
     C                   MOVEL     'CYMD'        PTOFMT
     C                   Z-ADD     0             PDAYS
     C                   MOVE      *BLANK        PCODE
     C                   MOVE      *BLANK        PRTRN
      *
     C                   EXSR      $DATE
      *
     C     PRTRN         IFEQ      *BLANK                                       IF VALID
     C                   Z-ADD     PTO8          WKUDAT
     C                   ELSE
     C                   Z-ADD     0             WKUDAT
     C                   ENDIF                                                  IF VALID
      *
      *
      * We typically set the initial routine at the beginning of the
      * mainline section.  But, having it there was overriding my exit logic
      * when the incoming mode was DELETE.  So, I moved it to here.
      *
     C                   MOVEL     SET1          ROUTNE
      *
      * THE USER CAN COME INTO THIS PROGRAM SEVERAL DIFFERENT WAYS.
      * CHECK THE ENTRY MODE AND SET THE APPROPRIATE INDICATORS TO
      * CONTROL THE SCREEN DISPLAY.
      *
     C                   SELECT
     C     MODE          WHENEQ    CREATE
     C                   MOVEL     CREATE        DFMODE
      *
     C     MODE          WHENEQ    REVISE
     C                   MOVEL     REVISE        DFMODE
      *
      * When incoming mode is DELETE, all authority checks will have already
      * been done in the calling program.  So, just perform the deletes and
      * get out without displaying any screens. Set the required 'comment'
      * value to CANCELLED.
      *
     C     MODE          WHENEQ    DELETE
     C                   EVAL      XXDMCM = 'CANCELLED'
     C                   EXSR      $DLTTH
     C                   EXSR      $DLTTD
     C                   EXSR      $DLTME
     C                   MOVEL     EXIT          ROUTNE
     C                   ENDSL
      *
      *
      * BRING IN THE DATA AREA THAT CONTAINS THE CURRENT ACCOUNTING
      * PERIOD
      *
     C     *DTAARA       DEFINE                  DAAPER
     C                   IN        *DTAARA
      *
      * DEFINE THE DATA AREA THAT CONTAINS THE MOVEMENT NUMBER
      *
     C     *DTAARA       DEFINE                  DAMVSN
      *
      * THE FOLLOWING STANDARD CODE MUST BE INCLUDED TO MAKE THE
      * STANDARD ERROR MESSAGE HANDLING PROGRAM FUNCTION PROPERLY.
      * THIS CODE SETS AND CLEARS THE PROGRAM MESSAGE QUEUE.
      *
     C                   Z-ADD     20            MAXMSG            2 0
     C                   MOVEL     '*'           MSGPGM                         SET QUEUE
     C                   EXSR      $CLMSG                                       CLEAR MSG
      *
     C                   ENDSR
      /EJECT
