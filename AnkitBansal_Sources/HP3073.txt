      *****************  RPG PROGRAM HEADING  ************************
     h option(*SRCSTMT:*NODEBUGIO)
      ****************************************************************
      *
      * SYSTEM:      Hog Production System
      * PROGRAM:     Hp3073
      * TITLE:       Workfile: Government Census Listing: Closed Groups
      * PROGRAMMER:  LeAnne Ramsey
      * CREATED:     05/27/08
      *
      *  FUNCTION:   This program builds the workfile for the Closed Groups Listing.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 07/06/09  LeAnne Ramsey
      *           Recompile only. Added new field 'Continuous Flow Flag' to Hog Group file.
      *
      * 10/15/13  LeAnne Ramsey (E2831)
      *           Recompile only. Added field 'MTech Reference'.
      /EJECT
      ****************************************************************
      * File specifications
      ****************************************************************
      *
     Fhsp018    if   e           k disk
      *  Farm site
      *
      *
     Fhsl034i   if   e           k disk
      *  Hog group  (LF selects closed groups only)
      *
      *
     Fhsp055    if   e           k disk
      *  Period costs for hogs moved out
      *
      *
     Fhsp3073   uf a e           k disk
      *  Workfile: Closed groups
      *
      /EJECT
      ****************************************************************
      * Definition specifications
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Named Constants
      *---------------------------------------------------------------
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      *---------------------------------------------------------------------
      * Standalone fields
      *---------------------------------------------------------------------
      *
      * Workfields
      *
     d wkslvhd         s                   like(w1slvhd)
     d wksam           s                   like(w1sam)
     D wksacd          s                   like(fssacd)
     D wkcnty          s                   like(fscnty)
      *
      *
      ****************************************************************
      * Data structures
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *---------------------------------------------------------------
      * Local data area.
      *---------------------------------------------------------------
      *
     Dlda             uds                  dtaara(*lda)
     D  ldyr                   1      4  0
      *
     D  ldfdt                  9     16  0
     D  ldtdt                 17     24  0
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      *-------------------------------------------------------------------------------------
      * MAINLINE
      *-------------------------------------------------------------------------------------
      *
      * Process all groups closed in the selected year. (LF selects only Closed Groups.)
      *
     C     *loval        setll     hsl034i
      *
     C                   dou       *in90 = *on                                  Main do
     C                   read      hsl034i                                90
     C                   if        *in90 = *off and                             If not EOF
     C                             hgcldt >= ldfdt and
     C                             hgcldt <= ldtdt
      *
      * Retrieve the County and State of the Farm from the Farm Site Master.
      *
     C     hgfscd        chain     hsp018                             92
     C                   if        *in92 = *off
     C                   move      fscnty        wkcnty
     C                   move      fssacd        wksacd
     C                   else
     C                   move      *blank        wkcnty
     C                   move      *blank        wksacd
     C                   endif
      *
      *
      * Retrieve values from the Period Costs for Hogs Moved Out file.
      *
     C                   exsr      $rtv055
      *
      * Write/update a Workfile record for this State/County/Phase/Farm.
      *
     C                   exsr      $wrt3073
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Main do
      *
      * EOF processing
      *
     C                   seton                                        lr
      /eject
      *------------------------------------------------------------------------------------
      * Retrieve/accumulate Head and Cost of Sales for the Group from the Period Costs file
      *------------------------------------------------------------------------------------
      *
     C     $rtv055       begsr
      *
     C     hghgsn        setll     hsp055
      *
     C                   dou       *in91 = *on                                  Do loop
     C     hghgsn        reade     hsp055                                 91
     C                   if        *in91 = *off                                 If not EOF
     C                   add       pcsam         wksam
     C                   add       pcslvhd       wkslvhd
     C                   endif                                                  If not EOF
     C                   enddo                                                  Do loop
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Write/Update a workfile record
      *---------------------------------------------------------------
      *
     C     $wrt3073      begsr
      *
      *
      * Retrieve the Workfile record for this County/State/Phase/Farm.
      *
     C     key01         chain     hsp3073                            92
     C                   if        *in92 = *off
     C                   add       wkslvhd       w1slvhd
     C                   add       wksam         w1sam
     C                   update    w1rec
     C                   else
      * Write a recod
     C                   z-add     wkslvhd       w1slvhd
     C                   z-add     wksam         w1sam
     C                   z-add     hgfscd        w1fscd
     C                   move      hgppcd        w1ppcd
     C                   move      wksacd        w1sacd
     C                   move      wkcnty        w1cnty
     C                   write     w1rec
     C                   endif
      *
     C                   z-add     0             wkslvhd
     C                   z-add     0             wksam
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Key lists
      *
     C     key01         klist
     C                   kfld                    wkcnty
     C                   kfld                    wksacd
     C                   kfld                    hgppcd
     C                   kfld                    hgfscd
      *
     C                   endsr
      /eject
