      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Hog Production System
      * PROGRAM:     HP6051
      * TITLE:       Weekly BGF Datamart Variance Report
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     02/20/01
      *
      * FUNCTION:  Batch program to list the records in the file.
      *            This listing is submitted from HP4051-Specify Options for
      *            BGF Datamart Edit Listings.
      *
/*    * This report is a replica of a COGNOS report.  The users are printing it
/* TH * for an edit listing prior to loading data from HPS into COGNOS.  So,
/* US * in the submission CLP, we replicate the COGNOS file generation so that
/* SO * we will have an exact match for the COGNOS report.  However, we create
/* WE * the Datamart files in QTEMP and we do not send the files to NIBBLE!
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 06/05/01  LeAnne Fedor
      *           Recompile only. New field 'multisite' added to Farm Site file.
      *
      * 07/19/01  LeAnne Fedor
      *           Recompile only. Three new 'ration' fields added to Datamart BGF:
      *           Weekly Production file.
      *
      * 11/06/01  LeAnne Fedor
      *           Recompile only. Sire line added to Datamart: Weekly Production file.
      *
      * 12/06/01  LeAnne Fedor
      *           Recompile only. Added: number of doses, report PCP sire line, Report
      *           farr sire line to file Datmart BGF: Weekly Production.
      *
      * 01/03/02  LeAnne Fedor
      *           Recompile only. Added: Report Farrowed Born Alive to Datamart BGF file:
      *           Weekly Production.
      *
      * 10/23/03  LeAnne Fedor
      *           Added 'euthanized' gilt/sow logic in everywhere we already had 'dead'
      *           gilt/sow logic.
      *
      * 12/28/04  LeAnne Fedor
      *           Recompile only. New field 'Farm Size' added to Datamart Farm Site
      *           file.
      *
      * 02/01/05  LeAnne Fedor
      *           Recompile only. New fields (Sire Line Company and Prime Line) added
      *           to Datamart BGF: Weekly Production file.
      *
      * 02/07/05  LeAnne Fedor
      *           File changes in Datamart BGF Weekly Production file:
      *                    Added: Transferred In Open Gilts
      *                  Removed: Total Lactation Pounds
      *           SO, added logic for new Gilt field.
      *
      * 02/15/05  LeAnne Fedor
      *           The users realized that the Week Change in Inventory calculated in
      *           this program was different than the calc in Cognos. So, we changed
      *           the calc here to match Cognos. We added logic for 3 fields:
      *                   Sows arrived
      *                   Sows transferred out open
      *                   Gilts transferred out open
      *
      * 07/05/05  LeAnne Fedor
      *           Recompile only due to file changes in Datamart BGF Weekly Production file:
      *           Added 1 new field: Average Daily Beginning Piglet Inventory
      *
      * 08/23/05  LeAnne Fedor
      *           Recompile only. New field added to Datamart BGF Weekly Production file:
      *                Field: Man hours
      *
      * 03/23/06  LeAnne Fedor
      *           Recompile only. New fields added to Datamart BGF Weekly Production file.
      *
      * 04/24/06  LeAnne Fedor
      *           Recompile only. New fields added to Datamart BGF Weekly Production file.
      *                 Cognos Report Grouping
      *                 Weaning Cognos Report Grouping
      *                 Report PCP Cognos Report Grouping
      *                 Report Farrowed Cognos Report Grouping
      *
      * 06/15/06  LeAnne Ramsey
      *           Recompile only. New field added to Datamart BGF Weekly Production file:
      *                Field: Total Gilt Weight @ 28 Weeks
      *
      * 08/04/08  LeAnne Ramsey
      *           Recompile only. Fields deleted from Weekly Production file.
      *                           BPPSGLSV - Nbr Gilts Bred w/Passed Date
      *                           BPWSLB   - Weekly Ending Lbs Supplement Used
      *                           BPP2LT   - Total P2 Litters Farrowed
      *                           BPTDGB   - Total Days Gilt Breeding Age
      *                           BPIOGLHD - Transferred in Open Gilts
      *                           BPOOGLHD - Gilts Transferred Out Open
      *                           BP17WNLT - 17+ Day Weaned Sows
      *                           BPTGLLB  - Total Gilt Weight @ 28 Weeks
      *
      * 11/07/08  LeAnne Ramsey
      *           Recompile only:
      *           Added 3 new fields to HPPB092-Datamart BGF: Weekly Production
      *            1) Target open gilt pool eligible
      *            2) Target litters farrowed/mated female/year
      *            3) Target pigs weaned/mated female/year
      *
      * 05/28/09  LeAnne Ramsey
      *           Recompile only:
      *           Added new field "Farrowing Crates" to:
      *                HPPB092-Datamart BGF: Weekly Production
      *
      * 09/01/09  LeAnne Ramsey
      *           Recompile only. Added new field "Report Total Litters Farrowed" to:
      *                 HPPB092-Datamart BGF: Weekly Production
      *
      * 01/14/10  LeAnne Ramsey
      *           Recompile only. Changed text on BPLVHD and BPLVLB in:
      *                HPPB092-Datamart BGF: Weekly Production
      *
      * 10/18/10  LeAnne Ramsey  (E1079)
      *           Recompile only. Sire Line Company is now 2-characters instead
      *           of one.
      *
      * 11/04/13  LeAnne Ramsey  (E2865)
      *           Added a new field (Wean Variance %) to the report for Kim
      *           Humbard.
      *
      * 01/21/21  ISE             TSN593
      *           Replaced *loval with *start
      *
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fhppb018   if   e           k disk
      *   Datamart BGF: Farm Site
      *   (file keys overridden in submission CL)
      *
      *
     Fhplb092b  if   e           k disk
      *   Datamart BGF: Weekly Production
      *   (records selected by query in submission CL)
      *
      *
     Fqprint    o    f  198        printer oflind(*inof)
      *
      /EJECT
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      *---------------------------------------------------------------
      * CONSTANTS
      *---------------------------------------------------------------
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Standard fields
      *
     D prtfl           s              1    inz('N')
     D dash            s            198    inz(*all'-')
     d rtime           s              6  0
      *
      *
      * Control fields
      *
     D svcellds        s                   like(fscellds)
      *
      * Print fields
      *
     D d1calendhd      s                   like(bpdohd)
     D d1pigvarhd      s                   like(bpdohd)
     D d1rchd          s                   like(bpdohd)
     D d1wnvarhd       s                   like(bpdohd)
     D d1wnvarpc       s              5  2
     D d1changehd      s                   like(bpdohd)
     D d1calchd        s                   like(bpdohd)
     D d1invarhd       s                   like(bpdohd)
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *---------------------------------------------------------------
      * Local data area.
      *---------------------------------------------------------------
      *
     Dlda             uds                  dtaara(*lda)
     D  ldcdyr                 1      4  0
     D  ldcdwk                 5      6  0
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * MAINLINE
      ****************************************************************
      *
      * Prepare to read the file and print headings the first
      * time through.
      *
593  C*    *loval        setll     hppb018
593  C     *start        setll     hppb018
     C                   except    h1hdr
      *
     C                   dou       *in90 = *on                                  Main do
     C                   read      hppb018                                90
     C                   if        *in90 = *off                                 If not EOF
      *
     C     fsfscd        chain     hplb092b                           92
     C                   if        *in92 = *off
      *
     C                   if        svcellds <> fscellds
     C                   seton                                        87
     C                   move      fscellds      svcellds
     C                   endif
      *
      * Calculate 'calculated ending piglet inventory'
      *
     C                   z-add     bpbgplhd      d1calendhd
     C                   add       bpbahd        d1calendhd
     C                   sub       bptolshd      d1calendhd
     C                   sub       bptownhd      d1calendhd
      *
      * Calculate 'piglet inventory variance'
      *
     C                   z-add     d1calendhd    d1pigvarhd
     C                   sub       bpenplhd      d1pigvarhd
      *
      * Calculate 'total pigs received at nursery'
      *
     C                   z-add     bpqlhd        d1rchd
     C                   add       bprjhd        d1rchd
     C                   add       bplvhd        d1rchd
      *
      * Calculate 'wean variance'
      *
     C                   z-add     d1rchd        d1wnvarhd
     C                   add       bpdohd        d1wnvarhd
     C                   sub       bptownhd      d1wnvarhd
      *
      * Calculate 'wean variance %'
      *
     C                   if        bptownhd <> 0
     C                   eval(h)   d1wnvarpc = (d1wnvarhd / bptownhd) * 100
     C                   endif
      *
      * Calculate 'week change in inventory'
      *
     C                   z-add     bparglhd      d1changehd
     C                   add       bptiglhd      d1changehd
     C                   add       bptisohd      d1changehd
     C                   add       bparsohd      d1changehd
     C                   sub       bpslglhd      d1changehd
     C                   sub       bpslsohd      d1changehd
     C                   sub       bpddglhd      d1changehd
     C                   sub       bpeuglhd      d1changehd
     C                   sub       bpddsohd      d1changehd
     C                   sub       bpeusohd      d1changehd
     C                   sub       bptoglhd      d1changehd
     C                   sub       bptosohd      d1changehd
     C                   sub       bpoosohd      d1changehd
      *
      *
      * Calculate 'calculated ending inventory' as:
      *      beginning gilt and sow inventory
      * plus (gilts arrived + tran in bred gilts +
      *       + tran in open gilts + sows arrived + tran in bred sows)
      * less (gilts sold + gilts dead naturally + gilts euthanized
      *                                         + tran out bred gilts + tran out open gilts)
      * less (sows sold + sows dead naturally + sows euthanized
      *                                       + tran out bred sows + tran out open sows)
      *
     C                   z-add     bpbggshd      d1calchd
     C                   add       bparglhd      d1calchd
     C                   add       bptiglhd      d1calchd
     C                   add       bparsohd      d1calchd
     C                   add       bptisohd      d1calchd
      *
     C                   sub       bpslglhd      d1calchd
     C                   sub       bpddglhd      d1calchd
     C                   sub       bpeuglhd      d1calchd
     C                   sub       bptoglhd      d1calchd
      *
     C                   sub       bpslsohd      d1calchd
     C                   sub       bpddsohd      d1calchd
     C                   sub       bpeusohd      d1calchd
     C                   sub       bptosohd      d1calchd
     C                   sub       bpoosohd      d1calchd
      *
      * Calculate 'inventory variance' as:
      *      calculated ending inventory - reported ending inventory
      *
     C     d1calchd      sub       bpengshd      d1invarhd
      *
     C                   if        *inof = *on
     C                   except    h1hdr
     C                   seton                                        87
     C                   endif
      *
     C                   except    l1dtl
     C                   setoff                                       87
     C                   movel     yes           prtfl
     C                   endif
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Main do
      *
      *---------------------------------------------------------------
      * EOF processing
      *---------------------------------------------------------------
      * If the file was empty, print the standard 'no data...'
      * error message
      *
     C                   if        prtfl = no
     C                   except    nodata
     C                   endif
      *
     C                   seton                                        lr
      /EJECT
      *---------------------------------------------------------------
      * *inzsr - initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Rretrieve time for report heading.
      *
     C                   time                    rtime
      *
     C                   endsr
      /EJECT
      *-------------------------------------------------------------
      * Report heading lines
      *-------------------------------------------------------------
      *
     Oqprint    e            h1hdr          1 01
     O                       sdpgm               10
     O                                          114 'HOG PRODUCTION SYSTEM'
     O                                          188 'DATE'
     O                       udate         y    198
      *
     O          e            h1hdr          1
     O                       sdusr               10
     O                                          105 'WEEKLY BGF DATAMART'
     O                                          121 ' VARIANCE REPORT'
     O                                          188 'TIME'
     O                       rtime              198 '  :  :  '
      *
     O          e            h1hdr          1
     O                                          188 'PAGE'
     O                       page          z    198
      *
     O          e            h1hdr          1
     O                                           17 'Calendar Year ..:'
     O                       ldcdyr        z     24
      *
      *
     O          e            h1hdr          1
     O                                           17 'Calendar Week ..:'
     O                       ldcdwk        z     24
      *
     O          e            h1hdr          1
     O                                           53 'Calculated'
     O                                           78 'Pigs'
      *
     O          e            h1hdr          1
     O                                           31 'Beginning'
     O                                           40 'Ending'
     O                                           51 'Ending'
     O                                           62 'Piglet'
     O                                           79 'Weaned'
     O                                           89 'DOA/KOA'
     O                                          101 'Total Pigs'
     O                                          121 'Wean'
      *
     O                                          143 'Beginning'
     O                                          152 'Week'
     O                                          166 'Calculated'
     O                                          177 'Reported'
      *
     O          e            h1hdr          1
     O                                           29 'Piglet'
     O                                           40 'Piglet'
     O                                           51 'Piglet'
     O                                           64 'Inventory'
     O                                           78 'From'
     O                                           86 'At'
     O                                          100 'Received'
     O                                          110 'Wean'
     O                                          123 'Variance'
      *
     O                                          143 'Gilt & Sow'
     O                                          154 'Change in'
     O                                          164 'Ending'
     O                                          176 'Ending'
     O                                          188 'Inventory'
     O          e            h1hdr          0
     O                                           31 'Inventory'
     O                                           42 'Inventory'
     O                                           53 'Inventory'
     O                                           64 'Variance'
     O                                           80 'Sow Farm'
     O                                           89 'Nursery'
     O                                          101 'At Nursery'
     O                                          112 'Variance'
     O                                          123 'Percent'
      *
     O                                          143 'Inventory'
     O                                          154 'Inventory'
     O                                          166 'Inventory'
     O                                          177 'Inventory'
     O                                          188 'Variance'
      *
      *
      *-------------------------------------------------------------
      * Detail line
      *-------------------------------------------------------------
      *
     O          e    87      l1dtl       1  1
     O                       dash               198
      *
     O          e            l1dtl       1  1
     O               87      fscellds             7
     O                       fsfsds              19
     O                       bpbgplhd      k     30
     O                       bpenplhd      k     41
     O                       d1calendhd    kb    52
     O                       d1pigvarhd    kb    63
     O                       bptownhd      k     80
     O                       bpdohd        k     89
     O                       d1rchd        kb   100
     O                       d1wnvarhd     kb   111
     O                       d1wnvarpc     kb   122
      *
     O                       bpbggshd      k    143
     O                       d1changehd    k    153
     O                       d1calchd      k    166
     O                       bpengshd      k    177
     O                       d1invarhd     k    188
      *-------------------------------------------------------------
      * No data message line
      *-------------------------------------------------------------
      *
     O          e            nodata      1
     O                                           19 'NO DATA MEETS YOUR'
     O                                           38 'SELECTION CRITERIA'
