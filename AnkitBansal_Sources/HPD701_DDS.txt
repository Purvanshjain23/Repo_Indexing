     A*%%TS  SD  20000907  081230  ISLRAMS     REL-V4R4M0  5769-PW1
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD701
     A*  TITLE:         VIEW MOVEMENTS FOR A GROUP
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  CREATED:       06/22/95
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A                                      CF04(04 'PROMPT')
     A                                      CF05(05 'REFRESH')
     A                                      CA12(12 'PREVIOUS')
     A*
     A*---------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP701S1                   SFL
     A*%%TS  SD  20000907  071452  ISLRAMS     REL-V4R4M0  5769-PW1
     A  85                                  SFLNXTCHG
     A            HDRRN1         4Y 0H
     A            DFOPT     R        B 11  2REFFLD(HSRREF/@@OPT *LIBL/HSPREF)
     A  30                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A N30                                  DSPATR(UL)
     A N30                                  DSPATR(HI)
     A            DFMDY     R        O 11  7REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A            DFMVSN    R     Y  O 11 19REFFLD(IAMVSN)
     A                                      EDTCDE(M)
     A            DFMSCD    R        O 11 31REFFLD(IAMSCD)
     A            DFMTCD    R        O 11 38REFFLD(MEMTCD)
     A            DFHD      R        O 11 42REFFLD(IAIAHD)
     A                                      EDTCDE(K)
     A            DFODFL    R        O 11 55REFFLD(MEODFL)
     A            DFHG      R        O 11 63REFFLD(HGHGCD)
     A            DFCVNO    R        O 11 72REFFLD(SHCVNO)
     A                                      EDTCDE(Z)
     A*
     A*---------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP701C1                   SFLCTL(HP701S1)
     A*%%TS  SD  20000907  081230  ISLRAMS     REL-V4R4M0  5769-PW1
     A                                      SFLSIZ(0100)
     A                                      SFLPAG(0011)
     A                                      SETOF(30)
     A                                      SETOF(54)
     A                                      SETOF(55)
     A                                      SETOF(56)
     A                                      SETOF(57)
     A N82                                  ROLLUP(25 'PAGE FORWARD')
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A                                      OVERLAY
     A                                      RTNCSRLOC(&RCD &FLD)
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 28'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  2 26'VIEW MOVEMENTS FOR A GROUP'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  4  2'Farm site ..:'
     A            C1FSCD    R        O  4 17REFFLD(FSFSCD)
     A                                      EDTCDE(M)
     A            C1FSNM    R        O  4 25REFFLD(FSFSNM)
     A                                  5  2'Hog group ..:'
     A            C1HGCD    R        O  5 17REFFLD(HGHGCD)
     A                                  7  2'5=View Sale or Transfer Movement'
     A                                  8 32'?'
     A                                      DSPATR(HI)
     A                                  8 38'?'
     A                                      DSPATR(HI)
     A                                  8 51'Origin/'
     A                                      DSPATR(HI)
     A                                  8 66'?'
     A                                      DSPATR(HI)
     A                                  8 75'?'
     A                                      DSPATR(HI)
     A                                  9  2'Opt'
     A                                      DSPATR(HI)
     A                                  9  9'Date'
     A                                      DSPATR(HI)
     A                                  9 18'Movement'
     A                                      DSPATR(HI)
     A                                  9 29'Status'
     A                                      DSPATR(HI)
     A                                  9 37'Type'
     A                                      DSPATR(HI)
     A                                  9 44'Head'
     A                                      DSPATR(HI)
     A                                  9 50'Destination'
     A                                      DSPATR(HI)
     A                                  9 64'Group'
     A                                      DSPATR(HI)
     A                                  9 72'Customer'
     A                                      DSPATR(HI)
     A            C1MDY     R        B 10  7REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A            C1MVSN    R        B 10 19REFFLD(IAMVSN)
     A                                      DSPATR(HI)
     A            C1MSCD    R        B 10 31REFFLD(HSRREF/@@MSCD *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A  54                                  DSPATR(PC)
     A            C1MTCD    R        B 10 38REFFLD(MEMTCD)
     A                                      DSPATR(HI)
     A  55                                  DSPATR(PC)
     A            C1ODFL    R        B 10 55REFFLD(MEODFL)
     A                                      DSPATR(HI)
     A            C1HG      R        B 10 63REFFLD(HGHGCD)
     A                                      DSPATR(HI)
     A  56                                  DSPATR(PC)
     A            C1CVNO    R     Y  B 10 72REFFLD(SHCVNO)
     A                                      DSPATR(HI)
     A                                      EDTCDE(Z)
     A  57                                  DSPATR(PC)
     A*
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R HP701K1
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A                                      DSPATR(HI)
     A                                 23  2'F3=Exit'
     A                                 23 11'F4=Prompt'
     A                                 23 22'F5=Refresh'
     A                                 23 34'F12=Previous'
     A*
     A*---------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE FORMAT
     A*---------------------------------------------------------------
     A          R HP701ES                   SFL SFLMSGRCD(24)
     A*
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
     A*
     A*---------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*---------------------------------------------------------------
     A          R HP701EC                   SFLCTL(HP701ES)
     A*
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
