/***************************************************************** */
/*                                                                 */
/*  SYSTEM:          TRIUMPH FOODS                                 */
/*  PROGRAM NUMBER:  TF270CL                                       */
/*  PROGRAM NAME:    YIELD BUILD (WEEKLY)                          */
/*  PROGRAMMER:      LEANNE RAMSEY                                 */
/*  CREATE DATE:     12/02/08                                      */
/*                                                                 */
/*  FUNCTION: BUILD DATA FOR WEEKLY YIELD REPORTS.                 */
/*                                                                 */
/*            WE INTEND FOR THIS TO BE CALLED FROM ROSE'S PROGRAM  */
/*            PPPHXFR-POST INVENTORY VALUES.                       */
/*                                                                 */
/*            SO, WHEN NATHAN MALONE DOES HIS INVENTORY POST ON    */
/*            TUESDAY/WEDNESDAY, WE WILL AUTOMATICALLY FOLLOW      */
/*            HIS POST WITH THE WEEKLY YIELD BUILD.                */
/*                                                                 */
/*******************************************************************/
/* MODIFICATIONS:                                                  */
/*                                                                 */
/* 05/07/09  LeAnne Ramsey                                         */
/*           Removed hardcoding of LDA print positions. These are  */
/*           now populated in TF270---since they are now the same  */
/*           for both TFS and Meat Costing.                        */
/*******************************************************************/

             PGM

             DCL        VAR(&QDATA) TYPE(*CHAR) LEN(110)

             DCL        VAR(&LDYR)    TYPE(*DEC) LEN(4)
             DCL        VAR(&LDWK)    TYPE(*DEC) LEN(2)
             DCL        VAR(&LDWBDT)  TYPE(*DEC) LEN(8)
             DCL        VAR(&LDWEDT)  TYPE(*DEC) LEN(8)
             DCL        VAR(&LDWESYN) TYPE(*DEC) LEN(7)
             DCL        VAR(&LDWBSYN) TYPE(*DEC) LEN(7)

             DCL        VAR(&ERROR) TYPE(*CHAR) LEN(1)
             DCL        VAR(&CONO)  TYPE(*DEC)  LEN(3)

/*-------------------------------------------------------------------*/
/* SINCE THIS CL WILL CALL BOTH TFS MARGIN ADJUSTMENT CLOSE          */
/* FUNCTIONS AND MEAT COST CALCULATION FUNCTIONS, WE MUST POPULATE   */
/* THE LDA TO MATCH WHAT IS EXPECTED BY PROGRAMS IN THOSE FUNCTIONS. */
/*-------------------------------------------------------------------*/

             CALL       PGM(TF270)       /* Populate LDA */

             CHGVAR     VAR(&LDYR)     VALUE(%SST(*LDA 2 4))
             CHGVAR     VAR(&LDWK)     VALUE(%SST(*LDA 6 2))
             CHGVAR     VAR(&LDWBDT)   VALUE(%SST(*LDA  8 8))
             CHGVAR     VAR(&LDWBSYN)  VALUE(%SST(*LDA 16 7))
             CHGVAR     VAR(&LDWEDT)   VALUE(%SST(*LDA 29 8))
             CHGVAR     VAR(&LDWESYN)  VALUE(%SST(*LDA 37 7))

/************************************************************************/
/* CHECK FOR ERRORS                                                     */
/************************************************************************/

             OVRPRTF    FILE(PRINT80) OUTQ(*JOB) COPIES(1) +
                          HOLD(*YES) SAVE(*YES)

             CHGVAR     VAR(&ERROR) VALUE('N')
             CALL       PGM(TF272) PARM(&ERROR)
             DLTOVR     FILE(PRINT80)

/****************************************************************************/
/* CONTINUE IF THERE ARE NO ERRORS                                          */
/****************************************************************************/

             IF         COND(&ERROR *EQ 'N') THEN(DO)        /* No Error Do */

/*-----------------------------------*/
/* Set "Processes are Running" Flags */
/*-----------------------------------*/

             CHGDTAARA  DTAARA(DAROCALC (1 1)) VALUE('Y')   /* Meat Costing */
             CALL       PGM(TF273) PARM('Y')                /* Margin Close */

/*-----------------*/
/* Meat Costing    */
/*-----------------*/

             CALL       PGM(RO201)                        /* create RO header */
             CALL       PGM(RO410CL) PARM('N')            /* meat costing     */

/*--------------------------*/
/* Margin Adjustment Close  */
/*--------------------------*/

             CALL       PGM(TF415CL) PARM('N')
             ENDDO                                           /* No Error Do */

             ENDPGM

