/*                                                                 */
/*  PROGRAM NUMBER:  HP4099CL                                      */
/*  PROGRAM NAME:    UPDATE HPS SALES POUNDS WHEN SCALE WAS BROKEN */
/*  PROGRAMMER:      ALICE BROWNFIELD                              */
/*  CREATE DATE:     01/23/03                                      */
/*                                                                 */
/*  NOTE: THIS FUNCTION IS FOR I.S. PERSONNEL ONLY!!!              */
/*                                                                 */
/*  FUNCTION:        THIS CL IS SUBMITTED WITH QCMDEXC FROM        */
/*                   HP4099-SPECIFY OPTIONS: UPDATE HPS SALES LBS  */
/*                   WITH HPE TATTOO LBS                           */
/*                                                                 */
/*                   OPTIONS ARE PASSED THROUGH THE LDA.           */
/*                                                                 */
/***************************************************************** */
/* MODIFICATIONS                                                   */
/*                                                                 */
/* 02/25/03  LEANNE FEDOR                                          */
/*           ALICE INITIALLY WROTE/RAN A FIX PROGRAM IN JANUARY.   */
/*           THIS IS SIMPLY A CLEANED-UP VERSION OF THE ORIGINAL   */
/*           CLP.                                                  */
/*******************************************************************/

             PGM

             DCL        VAR(&QDATA) TYPE(*CHAR) LEN(89)

             DCL        VAR(&LDFCYMD) TYPE(*DEC) LEN(8)
             DCL        VAR(&LDTCYMD) TYPE(*DEC) LEN(8)

             CHGVAR     VAR(&LDFCYMD) VALUE(%SST(*LDA 15 8))
             CHGVAR     VAR(&LDTCYMD) VALUE(%SST(*LDA 24 8))


/*********************************************************************/
/*  SETUP OPEN QUERY FOR SALES MOVEMENT HEADER                       */
/*********************************************************************/

/*  ALWAYS SELECT RECORDS FOR DATE RANGE                             */

             CHGVAR     VAR(%SST(&QDATA 1 10)) VALUE('SHRCDT *GE')
             CHGVAR     VAR(%SST(&QDATA 12 8)) VALUE(&LDFCYMD)
             CHGVAR     VAR(%SST(&QDATA 21 15)) VALUE('*AND SHRCDT +
                          *LE')
             CHGVAR     VAR(%SST(&QDATA 37 8)) VALUE(&LDTCYMD)

/*  ALWAYS SELECT ONLY MARKET SALES                                      */

             CHGVAR     VAR(%SST(&QDATA 46 23)) VALUE('*AND SHSTCD +
                          *EQ "MRKTS"')

/*  ALWAYS OMIT POSTED MOVEMENTS                                         */

             CHGVAR     VAR(%SST(&QDATA 70 20)) VALUE('*AND SHMSCD +
                          *NE "PP"')

/*********************************************************************/
/* PERFORM QUERY OVER HPS SALES MOVEMENT HEADER                      */
/*********************************************************************/

             OVRDBF     FILE(HSL084B) SHARE(*YES)
             OPNQRYF    FILE((HSL084B)) QRYSLT(&QDATA) +
                          KEYFLD(*FILE) SEQONLY(*YES)


/*********************************************************************/
/*  CALL PROGRAM TO RECALC/UPDATE HPS POUNDS                         */
/*********************************************************************/

             CALL       PGM(HP2099)

/*******************************************************************/
/*  DELETE FILE OVERRIDES                                          */
/*******************************************************************/

             CLOF       OPNID(HSL084B)
             DLTOVR     FILE(HSL084B)

             ENDPGM

