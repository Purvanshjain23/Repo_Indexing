/***************************************************************** */
/*                                                                 */
/*  PROGRAM NUMBER:  HP4053CL                                      */
/*  PROGRAM NAME:    WEAN PIG PROJECTION LISTING                   */
/*  PROGRAMMER:      LEANNE FEDOR                                  */
/*  CREATE DATE:     06/09/02                                      */
/*                                                                 */
/*  FUNCTION:        THIS CL IS SUBMITTED WITH QCMDEXC FROM        */
/*                   HP4053-SPECIFY OPTIONS FOR WEAN PIG           */
/*                   PROJECTION LISTING.                           */
/*                                                                 */
/*                   OPTIONS ARE PASSED THROUGH THE LDA.           */
/*                                                                 */
/*******************************************************************/
/*  MODIFIED:                                                      */
/*******************************************************************/

/*******************************************************************/

             PGM

/* DEFINE THE VARIABLE REQUIRED IN THE COMMAND TO OVERRIDE THE     */
/* PRINT FILE TO A LASER PRINTER                                   */

             DCL        VAR(&CMD) TYPE(*CHAR) LEN(512)
             DCL        VAR(&QDATA) TYPE(*CHAR) LEN(101)

             DCL        VAR(&HOLD) TYPE(*CHAR) LEN(10)
             DCL        VAR(&COPYS) TYPE(*DEC) LEN(2)
             DCL        VAR(&OUTQ) TYPE(*CHAR) LEN(10)

             DCL        VAR(&LDBWFL) TYPE(*CHAR) LEN(1)
             DCL        VAR(&LDCELL) TYPE(*DEC) LEN(2)
             DCL        VAR(&LDFSCD) TYPE(*DEC) LEN(5)
             DCL        VAR(&LDFRYR) TYPE(*DEC) LEN(4)
             DCL        VAR(&LDFRWK) TYPE(*DEC) LEN(2)

             CHGVAR     VAR(&LDBWFL) VALUE(%SST(*LDA 13 1))
             CHGVAR     VAR(&LDCELL) VALUE(%SST(*LDA 39 2))
             CHGVAR     VAR(&LDFSCD) VALUE(%SST(*LDA 41 5))
             CHGVAR     VAR(&LDFRYR) VALUE(%SST(*LDA 16 4))
             CHGVAR     VAR(&LDFRWK) VALUE(%SST(*LDA 20 2))

/*-------------------------------------------------------------------*/
/* SET UP PRINT OVERRIDES                                            */
/*-------------------------------------------------------------------*/

 HOLD:       IF         COND(%SST(*LDA 411 1) = 'Y') THEN(CHGVAR +
                          VAR(&HOLD) VALUE(*YES))
             ELSE       CMD(CHGVAR VAR(&HOLD) VALUE(*NO))

 COPYS:      CHGVAR     VAR(&COPYS) VALUE(%SST(*LDA 412 1))
             IF         COND(&COPYS = 0) THEN(CHGVAR VAR(&COPYS) +
                          VALUE(1))

OUTQ:        CHGVAR     VAR(&OUTQ) VALUE(%SST(*LDA 413 10))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(%SST(*LDA 401 10)))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(*JOB))

/*------------------------------------------------------------------*/
/* OVERRIDE PRINT FILE                                              */
/*------------------------------------------------------------------*/

/* USE THE CUSTOMIZED SEABOARD COMMAND TO OVERRIDE THE PRINT FILE.  */
/* THIS COMMAND ALLOWS THE REPORT TO BE GENERATED ON A STANDARD LINE*/
/* PRINTER OR A LASER PRINTER. TO USE THIS OVERRIDE YOU MUST DECLARE*/
/* A COMMAND VARIABLE AND FOLLOW THE OVERRIDE WITH A CALL TO        */
/* QCMDEXC.                                                         */

             SBDPRTOVR  CMDVAR(&CMD) FILE(QPRINT2) OUTQ(&OUTQ) +
                          WIDTH(198) LANDSCAPE(*YES) DRAWER(*PRTF) +
                          COPIES(&COPYS) HOLD(&HOLD)

             CALL       PGM(QCMDEXC) PARM(&CMD 512)

/*------------------------------------------------------------------*/
/* OVERRIDE PRINT FILE                                              */
/*------------------------------------------------------------------*/

/* USE THE CUSTOMIZED SEABOARD COMMAND TO OVERRIDE THE PRINT FILE.  */
/* THIS COMMAND ALLOWS THE REPORT TO BE GENERATED ON A STANDARD LINE*/
/* PRINTER OR A LASER PRINTER. TO USE THIS OVERRIDE YOU MUST DECLARE*/
/* A COMMAND VARIABLE AND FOLLOW THE OVERRIDE WITH A CALL TO        */
/* QCMDEXC.                                                         */

             SBDPRTOVR  CMDVAR(&CMD) FILE(QPRINT) OUTQ(&OUTQ) +
                          WIDTH(198) LANDSCAPE(*YES) DRAWER(*PRTF) +
                          COPIES(&COPYS) HOLD(&HOLD)

             CALL       PGM(QCMDEXC) PARM(&CMD 512)

/*-------------------------------------------------------------------*/
/* SET UP OPEN QUERY                                                 */
/*-------------------------------------------------------------------*/

/* WHEN SELECTION IS OVER 'WEAN' YEAR/WEEK                           */
/* NOTE: WE DO THE 'TO' YEAR/WEEK SELECTION IN THE RPG PROGRAM       */

             IF         COND(&LDBWFL *EQ 'W') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 1 12)) VALUE('((WPWNYR *EQ')
             CHGVAR     VAR(%SST(&QDATA 14 4)) VALUE(&LDFRYR)
             CHGVAR     VAR(%SST(&QDATA 19 15)) VALUE('*AND WPWNWK *GE')
             CHGVAR     VAR(%SST(&QDATA 35 2)) VALUE(&LDFRWK)
             CHGVAR     VAR(%SST(&QDATA 37 5)) VALUE(') *OR')
             CHGVAR     VAR(%SST(&QDATA 43 11)) VALUE('(WPWNYR *GT')
             CHGVAR     VAR(%SST(&QDATA 55 4)) VALUE(&LDFRYR)
             CHGVAR     VAR(%SST(&QDATA 59 2)) VALUE('))')
             ENDDO


/* WHEN SELECTION IS OVER 'BREED' YEAR/WEEK                           */
/* NOTE: WE DO THE 'TO' YEAR/WEEK SELECTION IN THE RPG PROGRAM       */

             IF         COND(&LDBWFL *EQ 'B') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 1 12)) VALUE('((WPSVYR *EQ')
             CHGVAR     VAR(%SST(&QDATA 14 4)) VALUE(&LDFRYR)
             CHGVAR     VAR(%SST(&QDATA 19 15)) VALUE('*AND WPSVWK *GE')
             CHGVAR     VAR(%SST(&QDATA 35 2)) VALUE(&LDFRWK)
             CHGVAR     VAR(%SST(&QDATA 37 5)) VALUE(') *OR')
             CHGVAR     VAR(%SST(&QDATA 43 11)) VALUE('(WPSVYR *GT')
             CHGVAR     VAR(%SST(&QDATA 55 4)) VALUE(&LDFRYR)
             CHGVAR     VAR(%SST(&QDATA 59 2)) VALUE('))')
             ENDDO


/* IF THE USER SELECTED A SINGLE CELL                                 */

             IF         COND(&LDCELL *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 62 15)) VALUE('*AND WPCELL +
                          *EQ')
             CHGVAR     VAR(%SST(&QDATA 78 2)) VALUE(&LDCELL)
             ENDDO

/* IF THE USER SELECTED A SINGLE FARM SITE                            */

             IF         COND(&LDFSCD *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 81 15)) VALUE('*AND WPFSCD +
                          *EQ')
             CHGVAR     VAR(%SST(&QDATA 97 5)) VALUE(&LDFSCD)
             ENDDO


/* OVERRIDE THE DATAMART BGF WEAN PIG PREDICTIONS FILE                */

             OVRDBF     FILE(HPLB192B) SHARE(*YES)
             OPNQRYF    FILE((HPLB192B)) QRYSLT(&QDATA) KEYFLD(*FILE)

/*-------------------------------------------------------------------*/
/* PRINT THE LISTING                                                 */
/*-------------------------------------------------------------------*/

             CALL       PGM(HP6053)

             CLOF       OPNID(HPLB192B)
             DLTOVR     FILE(*ALL)

             ENDPGM

