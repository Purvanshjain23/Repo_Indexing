/***************************************************************** */
/***************************************************************** */
/*                                                                 */
/*  PROGRAM NUMBER:  EDIPCC                                        */
/*  PROGRAM NAME:    EDI CARCASS SUMMARY & DETAIL                  */
/*  PROGRAMMER:      JOHN ERICSON                                  */
/*  CREATE DATE:     ??/??/??                                      */
/*                                                                 */
/*******************************************************************/
/*  Modification History                                           */
/* 02/13/19 E14473 JJH - Add Parameter &PCFMT; Set &PCFMT to       */
/*          '.TXT' or '.XLS'.                                      */
/* 11/15/18 E13790 JJH - If Company = 360 use .TXT extension       */
/* 09/12/17 E11653 JJH - Copied from HPEEDIPCC.  This version is   */
/*          only called by the EDISUMDTTF program.                 */
/*          Change naming convention of the FTP and SFTP files     */
/*          to use format: SUM_YYYYMMDD_COM_S, where               */
/*          COM=Company and S=Suffix.                              */
/*          Change &CO to &TP and &CONUM to *TPNUM.                */
/*          Add new DCL *COM for the Company Number.               */
/*          Remove &FUN which was not being used.                  */
/*                                                                 */
/* 11/10/14 E3503 JJH - Add logic to support SFTP with new parm    */
/*          FTPMETHOD.                                             */
/*                                                                 */
/* 10/15/10 CHANGE THE .TXT TO .XLS                                */
/*                                                                 */
/* 11/14/01 COMMENTED OUT DOWNLOAD TO EDIPC.                       */
/*                                                                 */
/* 10/18/01 CHANGED TO SEND THE FILES TO A FTP SITE INSTEAD OF     */
/*          THE EDIPC IF THE TRADING PARTNER IS GE 10000.          */
/* 11/23/24 Santosh Task: SR3022491 CM: S322491                    */
/*                  Logic is add MONMSG for SFTP_IFS and RMVLNK    */
/*******************************************************************/
             PGM        PARM(&TP &DATEIN &COM &FTPMETHOD &PCFMT)

/* INPUT PARAMETERS*/
             DCL        VAR(&TP) TYPE(*CHAR) LEN(6)      /* TRADING PARTNER  */
             DCL        VAR(&DATEIN) TYPE(*CHAR) LEN(6)  /* YMD-KILL +
                          DATE */
             DCL        VAR(&COM) TYPE(*CHAR) LEN(3)     /* COMPANY       */
             DCL        VAR(&FTPMETHOD) TYPE(*CHAR) LEN(1)  /* FTP METHOD    */
/*           DCL        VAR(&FUN) TYPE(*CHAR) LEN(1)           FUNCTION      */

/* FTP VARIABLES       */
             DCL        VAR(&FTPDIR) TYPE(*CHAR) LEN(64) /* FTP DIRECTORY     */

             DCL        VAR(&CS) TYPE(*CHAR) LEN(64)     /* SUMMARY PCFILE    */
             DCL        VAR(&CD) TYPE(*CHAR) LEN(64)     /* DETAIL PCFILE     */

             DCL        VAR(&PCCS) TYPE(*CHAR) LEN(12)   /* SUMMARY FILE NAME */
             DCL        VAR(&PCCD) TYPE(*CHAR) LEN(12)   /* DETAIL FILE NAME  */

             DCL        VAR(&FTPCS) TYPE(*CHAR) LEN(64)  /* SUMMARY FILE NAME */
             DCL        VAR(&FTPCD) TYPE(*CHAR) LEN(64)  /* DETAIL FILE NAME  */

             DCL        VAR(&DATE) TYPE(*CHAR) LEN(6)    /* JOB DATE          */
             DCL        VAR(&SEQ) TYPE(*CHAR) LEN(1)     /* SEQUENCE NUMBER   */
             DCL        VAR(&TPNUM) TYPE(*DEC) LEN(6 0)  /* COMPANY NUMERIC   */
             DCL        VAR(&DATE8) TYPE(*CHAR) LEN(8)   /* YYMD FORMAT DATE  */

/* SFTP VARIABLES       */
             DCL        VAR(&RMTDIR) TYPE(*CHAR) LEN(50)
             DCL        VAR(&LOCALDIR) TYPE(*CHAR) LEN(7)
             DCL        VAR(&URL) TYPE(*CHAR) LEN(20)

             DCL        VAR(&TDOCSUM) TYPE(*CHAR) LEN(7)
             DCL        VAR(&TDOCDTL) TYPE(*CHAR) LEN(7)
             DCL        VAR(&TOSTMF) TYPE(*CHAR) LEN(30)
             DCL        VAR(&PCFMT) TYPE(*CHAR) LEN(10)
/* S322491 */DCL        VAR(&ERRMSG) TYPE(*CHAR) LEN(70) VALUE(' ')
/*-----------------------------------------------------------------*/
/* INITIALIZE                                                      */
/*-----------------------------------------------------------------*/
/* CONVERT TRADING PARTNER TO NUMERIC */
             CHGVAR     VAR(&TPNUM) VALUE(&TP)

/* RETRIEVE & CONVERT JOB DATE        */
/*           RTVJOBA    DATE(&DATE) */
             /* CONVERT DATE TO YMD FORMAT */
/*           CHGVAR     VAR(&DATE) VALUE(%SST(&DATE 5 2) *CAT +
                          %SST(&DATE 1 4)) */
             /* CONVERT DATE TO YYMD FORMAT */
             CVTDAT     DATE(&DATEIN) TOVAR(&DATE8) FROMFMT(*YMD) +
                          TOFMT(*YYMD) TOSEP(*NONE)

/* FORMAT PC FILE NAME ON THE AS400 */
             /* SOURCE FILE NAME FOR FTP */
             CHGVAR     VAR(&CS) VALUE('GUY.IS/S' *CAT &TP)
             CHGVAR     VAR(&CD) VALUE('GUY.IS/D' *CAT &TP)

             /* SOURCE FILE NAME FOR SFTP */
             CHGVAR     VAR(&TDOCSUM)  VALUE('S' *CAT &TP)
             CHGVAR     VAR(&TDOCDTL)  VALUE('D' *CAT &TP)

             /* FORMAT TARGET FILE NAME FTP & SFTP */
             CALL       PGM(HPEEDIPCCR) PARM(&TP &DATEIN &SEQ)

/* PREVIOUS CODE                                                            */
/*           IF         COND(&SEQ *EQ ' ') THEN(CHGVAR VAR(&SEQ) +          */
/*                        VALUE(0))                                         */
/*           CHGVAR     VAR(&FTPCS) VALUE('SUM_' *CAT &DATE8 *CAT +         */
/*                        '_' *CAT &COM *CAT '_' *CAT &SEQ *TCAT +          */
/*                        '.XLS')                                           */
/*           CHGVAR     VAR(&FTPCD) VALUE('DTL_' *CAT &DATE8 *CAT +         */
/*                        '_' *CAT &COM *CAT '_' *CAT &SEQ *TCAT +          */
/*                        '.XLS')                                           */
/*           CHGVAR     VAR(&PCFMT) VALUE('.XLS')                           */
/*           IF         COND(&COM *EQ '360') THEN(CHGVAR VAR(&PCFMT) +      */
/*                        VALUE('.TXT'))                                    */

             IF         COND(&PCFMT *EQ '*XLS') THEN(CHGVAR +
                          VAR(&PCFMT) VALUE('.XLS'))
             IF         COND(&PCFMT *EQ '*DELIMITED') THEN(CHGVAR +
                          VAR(&PCFMT) VALUE('.TXT'))

             IF         COND(&SEQ *EQ ' ') THEN(CHGVAR VAR(&SEQ) +
                          VALUE(0))
             CHGVAR     VAR(&FTPCS) VALUE('SUM_' *CAT &DATE8 *CAT +
                          '_' *CAT &COM *CAT '_' *CAT &SEQ *TCAT +
                          &PCFMT)
             CHGVAR     VAR(&FTPCD) VALUE('DTL_' *CAT &DATE8 *CAT +
                          '_' *CAT &COM *CAT '_' *CAT &SEQ *TCAT +
                          &PCFMT)

/*-----------------------------------------------------------------*/
/* MAIN LINE                                                       */
/*-----------------------------------------------------------------*/

/**********  SEND FILES TO FTP SITE ON SERVER USING FTP      ************/

             IF         COND(&FTPMETHOD *EQ 'F') THEN(DO)

/* ADD FTP LIBRARY TO THE THE LIBRARY LIST */
             ADDLIBLE   LIB(FTP_REMOTE) POSITION(*LAST)
             MONMSG     MSGID(CPF0000)

/* DOWNLOAD TO FTP SITE ON THE SERVER */
             IF         COND(&TPNUM *GE 10000) THEN(DO)
             CHGVAR     VAR(&FTPDIR) VALUE('/PRODCR_ACCTS/' *CAT &TP)
             FTP_DOC2   FUNCTION(*SEND) IDIR(&FTPDIR) IFILE(&FTPCS) +
                          HDOC(&CS) URL(ftp.seaboardpork.com) +
                          USER('PDINETPRRP') PASS('prodcr36') +
                          ASCII(*NO)
             FTP_DOC2   FUNCTION(*SEND) IDIR(&FTPDIR) IFILE(&FTPCD) +
                          HDOC(&CD) URL(ftp.seaboardpork.com) +
                          USER('PDINETPRRP') PASS('prodcr36') +
                          ASCII(*NO)
             RMVLIBLE   LIB(FTP_REMOTE)
             MONMSG     MSGID(CPF0000)

             ENDDO
             ENDDO

/**********  SEND FILES TO BRICKFTP USING SFTP               ************/
             IF         COND(&FTPMETHOD *EQ 'S') THEN(DO)

             ADDLIBLE   LIB(SFTPREMOTE) POSITION(*LAST)
             MONMSG     MSGID(CPF0000)

             CHGVAR     VAR(&RMTDIR)  VALUE('/ProducerServices/' *TCAT +
                          &TP *TCAT '/')
             CHGVAR     VAR(&LOCALDIR) VALUE('/' *TCAT &TP)

             SFTP_IFS   FUNCTION(*SEND) +
                          RMTDIR(&RMTDIR) +
                          RMTFILE(&FTPCS) +
                          LOCALDIR(&LOCALDIR) +
                          LOCALFILE(&TDOCSUM) +
                          URL('Seaboard.BrickFTP.com') +
                          USER('TFSFTP@Seaboard.BrickFTP.com')
             MONMSG     MSGID(CPF0000) EXEC(SNDPGMMSG MSG('Summary +
                          SFTP for trading partner' *BCAT &TP *BCAT +
                          'ended in error.')) /* S322491 */

             SFTP_IFS   FUNCTION(*SEND) +
                           RMTDIR(&RMTDIR) +
                           RMTFILE(&FTPCD) +
                           LOCALDIR(&LOCALDIR) +
                           LOCALFILE(&TDOCDTL) +
                           URL('Seaboard.BrickFTP.com') +
                           USER('TFSFTP@Seaboard.BrickFTP.com')
             MONMSG     MSGID(CPF0000) EXEC(SNDPGMMSG MSG('Detail +
                          SFTP for trading partner' *BCAT &TP *BCAT +
                          'ended in error.')) /* S322491 */

             CHGVAR     VAR(&TOSTMF) VALUE('/PORK/SFTP/OUT/' *CAT +
                          &TP *CAT '/' *CAT &TDOCSUM)
             RMVLNK     OBJLNK(&TOSTMF)
             MONMSG     MSGID(CPF0000) EXEC(SNDPGMMSG MSG('Remove +
                          link operation on Summary for trading +
                          partner' *BCAT &TP *BCAT 'failed.')) /* +
                          S322491 */
             CHGVAR     VAR(&TOSTMF) VALUE('/PORK/SFTP/OUT/' *CAT +
                          &TP *CAT '/' *CAT &TDOCDTL)
             RMVLNK     OBJLNK(&TOSTMF)
             MONMSG     MSGID(CPF0000) EXEC(SNDPGMMSG MSG('Remove +
                          link operation on Detail for trading +
                          partner' *BCAT &TP *BCAT 'failed.')) /* +
                          S322491 */

             RMVLIBLE   LIB(SFTPREMOTE)
             MONMSG     MSGID(CPF0000)

             ENDDO

/*-----------------------------------------------------------------*/
/* END PROGRAM                                                     */
/*-----------------------------------------------------------------*/
             ENDPGM
