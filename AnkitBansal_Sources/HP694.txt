      *****************  RPG PROGRAM HEADING  ************************
      * SYSTEM:      HOG PRODUCTION
      * PROGRAM:     HP694
      * TITLE:       PRODUCTION ANALYSIS REPORT
      * PROGRAMMER:  LEANNE FEDOR
      * CREATED:     01/30/95
      *
      *  FUNCTION:   THIS PROGRAM PRINTS THE DATA IN WORKFILE HSP394
      *              WHICH IS CREATED IN PROGRAM HP394.
      *
      *              THIS PROGRAM IS SUBMITTED BY HP494CL WHICH IS
      *              CALLED FROM HP494 THROUGH QCMDEXC.
      *
      *              THIS PROGRAM PRINTS ONE COLUMN OF DATA FOR
      *              EACH INCIDENCE OF THE ANALYSIS FACTOR SELECTED
      *              BY THE USER. THE MAXIMUM COLUMNS ON A PAGE IS 12.
      *              THUS, ALL 12 COLUMNS ARE BUILT BEFORE A PAGE IS
      *              PRINTED. AND, IF THERE ARE MORE THAN 12
      *              OCCURENCES OF THE FACTOR, ADDITIONAL PAGES WILL
      *              BE PRINTED.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 03/14/95  LEANNE FEDOR
      *           RECOMPILE ONLY. ADDED TWO NEW PRODUCTION ANALYSIS
      *           FACTORS: ORIGIN BGF FARM SITE AND ORIGIN NURSERY
      *           FARM SITE.
      *
      * 04/26/95  LEANNE FEDOR
      *           CHANGED FARM SITE CODE FROM ALPHA TO NUMERIC.
      *
      * 08/17/00  LeAnne Fedor
      *           Added logic to print a line for 'adjusted transferred in head.'
      *
      * 09/27/01  LeAnne Fedor
      *           'Group type' added as a new report option.
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     FHSP009    IF   E           K DISK
      *    BUSINESS OFFICE            (KEY: BOBOBO)
      *
     FHSP010    IF   E           K DISK
      *    PRODUCTION TYPE            (KEY: PTPTCD)
      *
     FHSP394    IF   E           K DISK
      *    WORKFILE FOR ANALYSIS FACTOR
      *
     FQPRINT    O    F  198        PRINTER OFLIND(*INOF)
      /EJECT
      ****************************************************************
      * TABLE AND ARRAY SPECIFICATIONS
      ****************************************************************
      *
      * COLUMN HEADINGS
      *
     D COL             S              5    DIM(12)                              COL HEADING
      *
      *  PERFORMANCE VALUES
      *
     D PHA             S              9  0 DIM(12)                              ADJUSTED IN
     D PHI             S              9  0 DIM(12)                              HEAD TRANI
     D PHS             S              9  0 DIM(12)                              HEAD SOLD
     D PHC             S              9  0 DIM(12)                              CULL SALES
     D PHO             S              9  0 DIM(12)                              HEAD TRANO
     D PDL             S              5  2 DIM(12)                              DEATH LOSS %
     D PPP             S              5  2 DIM(12)                              AVG LBS PLACED
     D PAP             S              5  2 DIM(12)                              AVG POUNDS
     D PFP             S              5  2 DIM(12)                              AVG FEED LBS
     D PFC             S              5  2 DIM(12)                              FEED CONVERS
     D PDG             S              5  2 DIM(12)                              AVG DAILY GAIN
     D PLD             S              9  0 DIM(12)                              LYSINE PER DAY
     D PKD             S              9  0 DIM(12)                              KCAL PER DAY
     D PLP             S              5  2 DIM(12)                              LEAN PERCENT
     D PBI             S              5  2 DIM(12)                              BACKFAT
     D PCY             S              5  2 DIM(12)                              CARCASS YIELD
      *
      *  COST VALUES
     D CPG             S              5  2 DIM(12)                              PHASE COST
     D CFG             S              5  2 DIM(12)                              FEED COST
     D CAF             S              5  2 DIM(12)                              AVG FEED/TON
     D CIM             S              5  2 DIM(12)                              TRANI COSTS
     D CFM             S              5  2 DIM(12)                              FEED COSTS
     D COM             S              5  2 DIM(12)                              OTHER COSTS
     D CTM             S              5  2 DIM(12)                              TOTAL COSTS
     D CNS             S              5  2 DIM(12)                              NET SALES/CWT
     D CMR             S              5  2 DIM(12)                              MARGIN PER CWT
      *
      *
      /EJECT
      ****************************************************************
      * INPUT SPECS
      ****************************************************************
      *
      *---------------------------------------------------------------
      * STANDARD PROGRAM STATUS DATA STRUCTURE
      *---------------------------------------------------------------
      *    EXTERNALLY DEFINED AS UTPGFR (RECORD FORMAT: PGMDSR)
     D PGMDS         ESDS                  EXTNAME(UTPGFR)
      *
      *---------------------------------------------------------------
      *  LDA -  WITH THE REQUESTED PRINT SELECTIONS
      *---------------------------------------------------------------
     D                UDS
     D  LDFSBO                 1      5
     D  LDFYMD                 6     13  0
     D  LDTYMD                14     21  0
     D  LDPTCD                22     26
     D  LDPPCD                27     31
     D  LDPACD                32     36
     D  LDFMDY                37     42  0
     D  LDTMDY                43     48  0
     D  LDICFL                49     49
     D  LDETFL                50     50
      *
     D  LDPPDS                51     80
     D  LDPADS                81    100
      *
     D  LDGTCD               101    101
     D  LDGTDS               102    116
      *---------------------------------------------------------------
      *  NAMED CONSTANTS
      *---------------------------------------------------------------
      *
     D YES             C                   CONST('Y')
     D NO              C                   CONST('N')
     D ALL             C                   CONST('ALL')
      /EJECT
      ****************************************************************
      * MAIN LINE
      ****************************************************************
      *
      * READ THE ANALYSIS FACTOR WORKFILE IN ORDER OF THE SORT FIELD
      *
     C     *LOVAL        SETLL     HSP394
     C                   SETOFF                                       99
      *
     C     *IN99         DOUEQ     *ON                                          MAIN DO
     C                   READ      HSP394                                 99
     C     *IN99         IFEQ      *OFF                                         IF NOT EOF
      *
     C                   MOVEL     NO            FIRST
      *
      * IF YOU HAVE ALL 12 COLUMNS FULL, THEN PRINT THE PAGE
      *
     C     X             IFEQ      12
     C                   EXSR      $PRINT
     C                   Z-ADD     0             X
     C                   ENDIF
      *
     C                   ADD       1             X
     C                   MOVEL     WFSORT        COL(X)
     C                   Z-ADD     WFAJTIHD      PHA(X)
     C                   Z-ADD     WFTIHD        PHI(X)
     C                   Z-ADD     WFSLHD        PHS(X)
     C                   Z-ADD     WFCUHD        PHC(X)
     C                   Z-ADD     WFTOHD        PHO(X)
     C                   Z-ADD     WFDLPR        PDL(X)
     C                   Z-ADD     WFAPLB        PPP(X)
     C                   Z-ADD     WFAMLB        PAP(X)
     C                   Z-ADD     WFAFLB        PFP(X)
     C                   Z-ADD     WFFDCV        PFC(X)
     C                   Z-ADD     WFAGLB        PDG(X)
     C                   Z-ADD     WFLYPD        PLD(X)
     C                   Z-ADD     WFKCPD        PKD(X)
      *
      * IF THE USER SELECTED THE GROW FINISH PHASE
      *
     C     LDPPCD        IFEQ      'GF   '
     C                   Z-ADD     WFLEAN        PLP(X)
     C                   Z-ADD     WFBFIN        PBI(X)
     C                   Z-ADD     WFCRYD        PCY(X)
     C                   ENDIF
      *
      *  IF THE USER WANTS TO SEE COSTS
      *
     C     LDICFL        IFEQ      YES                                          IF COSTS
     C                   Z-ADD     WFP$GN        CPG(X)
     C                   Z-ADD     WFF$GN        CFG(X)
     C                   Z-ADD     WFFC$T        CAF(X)
     C                   Z-ADD     WFTI$M        CIM(X)
     C                   Z-ADD     WFFC$M        CFM(X)
     C                   Z-ADD     WFOC$M        COM(X)
     C                   Z-ADD     WFTC$M        CTM(X)
     C                   Z-ADD     WFN$CW        CNS(X)
     C                   Z-ADD     WFM$CW        CMR(X)
     C                   ENDIF                                                  IF COSTS
      *
     C                   ENDIF                                                  IF NOT EOF
     C                   ENDDO                                                  MAIN DO
      *
      *---------------------------------------------------------------
      * EOF PROCESSING
      *---------------------------------------------------------------
      *  IF NO DETAIL RECORDS MATCHED THE SELECTION CRITERIA, THEN
      *  PRINT THE STANDARD 'NO DATA...' ERROR MESSAGE
      *
     C     FIRST         IFEQ      YES
     C                   EXCEPT    HRHDR
     C                   EXCEPT    NODATA
     C                   ELSE
      *
      * PRINT TOTALS FOR LAST PAGE
      *
     C                   EXSR      $PRINT
     C                   ENDIF
      *
     C                   SETON                                        LR
      /EJECT
      *---------------------------------------------------------------
      * PRINT A PAGE
      *---------------------------------------------------------------
      *
     C     $PRINT        BEGSR
      *
     C                   EXCEPT    HRHDR
     C                   EXCEPT    HAHDR
     C                   EXCEPT    DTPHI
     C                   EXCEPT    DTPHA
     C                   EXCEPT    DTPHS
     C                   EXCEPT    DTPHC
     C                   EXCEPT    DTPHO
     C                   EXCEPT    DTPDL
     C                   EXCEPT    DTPPP
     C                   EXCEPT    DTPAP
     C                   EXCEPT    DTPFP
     C                   EXCEPT    DTPFC
     C                   EXCEPT    DTPDG
     C                   EXCEPT    DTPLD
     C                   EXCEPT    DTPKD
      *
      * IF THE USER SELECTED THE GROW FINISH PHASE
      *
     C     LDPPCD        IFEQ      'GF   '
     C                   EXCEPT    DTPLP
     C                   EXCEPT    DTPBI
     C                   EXCEPT    DTPCY
     C                   ENDIF
      *
      * IF THE USER ELECTED TO SEE COST DATA
      *
     C     LDICFL        IFEQ      YES
     C                   EXCEPT    DTCPG
     C                   EXCEPT    DTCFG
     C                   EXCEPT    DTCAF
     C                   EXCEPT    DTCIM
     C                   EXCEPT    DTCFM
     C                   EXCEPT    DTCOM
     C                   EXCEPT    DTCTM
     C                   EXCEPT    DTCNS
     C                   EXCEPT    DTCMR
     C                   ENDIF
      *
     C                   ENDSR
      /EJECT
      ****************************************************************
      * *INZSR - INITIALIZATION SUBROUTINE
      ****************************************************************
      *
     C     *INZSR        BEGSR
      *
      * DEFINE PRINT FIELDS FOR THE REPORT HEADING
      *
     C     *LIKE         DEFINE    BOBOBO        HRFSBO
     C     *LIKE         DEFINE    BOBODS        HRBODS
     C                   MOVE      *BLANK        HRPTCD            5
     C                   MOVE      *BLANK        HRPTDS           10
      *
      * ARRAY INDEX
     C                   Z-ADD     0             X                 3 0
      *
      * GET BUSINESS OFFICE DESCRIPTION
      *
     C     LDFSBO        IFEQ      *BLANK                                       IF ALL BO
     C                   MOVEL     ALL           HRFSBO
     C                   ELSE
     C                   MOVEL     LDFSBO        HRFSBO
     C     LDFSBO        CHAIN     HSP009                             92
     C     *IN92         IFEQ      *OFF                                         IF FOUND
     C                   MOVEL     BOBODS        HRBODS
     C                   ELSE
     C                   MOVEL     'UNKNOWN'     HRBODS
     C                   ENDIF                                                  IF FOUND
     C                   ENDIF                                                  IF ALL BO
      *
      * GET PRODUCTION TYPE DESCRIPTION
      *
     C     LDPTCD        IFEQ      *BLANK                                       IF ALL TYPE
     C                   MOVEL     ALL           HRPTCD
     C                   ELSE
     C     LDPTCD        CHAIN     HSP010                             93
     C                   MOVEL     LDPTCD        HRPTCD
     C     *IN93         IFEQ      *OFF                                         IF FOUND
     C                   MOVEL     PTPTDS        HRPTDS
     C                   ELSE
     C                   MOVEL     'UNKNOWN'     HRPTDS
     C                   ENDIF                                                  IF FOUND
     C                   ENDIF                                                  IF ALL TYPE
      *
      * Group type
      *
     C                   if        ldgtcd = *blank
     C                   seton                                        82
     C                   endif
      *
      * RETRIEVE TIME FOR REPORT HEADING
      * SET UP A FIRST TIME FLAG
      * SET UP A DASHED LINE FOR UNDERLINING THE HEADING
      *
     C                   TIME                    RTIME             6 0
     C                   MOVEL     YES           FIRST             1
     C                   MOVEL     *ALL'-'       DASH            198
      *
     C                   ENDSR
      /EJECT
      *-------------------------------------------------------------
      * REPORT HEADING LINES
      *-------------------------------------------------------------
      *
     OQPRINT    E            HRHDR          1 01
     O                       SDPGM               10
     O                                          113 'HOG PRODUCTION SYSTEM'
     O                                          189 'DATE'
     O                       UDATE         Y    198
      *
     O          E            HRHDR          1
     O                       SDUSR               10
     O                                          108 'PRODUCTION ANALYSIS'
     O                                          115 'REPORT'
     O                                          189 'TIME'
     O                       RTIME              198 '  :  :  '
      *
     O          E            HRHDR          1
     O                                          189 'PAGE'
     O                       PAGE          Z    198
      *
     O          E            HRHDR          1
     O                                           16 'BUSINESS OFFICE '
     O                                           22 '.....'
     O                       HRFSBO              29
     O                       HRBODS              63
      *
     O          E            HRHDR          1
     O                                           13 'FROM CLOSED D'
     O                                           22 'ATE......'
     O                       LDFMDY        Y     32
      *
     O          E            HRHDR          1
     O                                           13 'TO CLOSED DAT'
     O                                           22 'E........'
     O                       LDTMDY        Y     32
      *
      *
     O          E            HRHDR          1
     O                                           22 'PRODUCTION TYPE ......'
     O                       HRPTCD              29
     O                       HRPTDS              43
      *
     O          E            HRHDR          1
     O                                           22 'PRODUCTION PHASE .....'
     O                       LDPPCD              29
     O                       LDPPDS              63
      *
      *
     O          e            hrhdr          1
     O                                           22 'GROUP TYPE ...........'
     O              n82      ldgtcd              25
     O               82                          27 'ALL'
     O                       ldgtds              48
      *
      *
     O          E            HRHDR          2
     O                                           22 'ANALYSIS FACTOR ......'
     O                       LDPACD              29
     O                       LDPADS              53
      *
     O          E            HRHDR          1
     O                       DASH               198
      *
      *-------------------------------------------------------------
      * ANALYSIS FACTOR HEADING
      *-------------------------------------------------------------
      *
     O          E            HAHDR          1
     O                       COL(1)         B    41
     O                       COL(2)         B    55
     O                       COL(3)         B    69
     O                       COL(4)         B    83
     O                       COL(5)         B    97
     O                       COL(6)         B   111
     O                       COL(7)         B   125
     O                       COL(8)         B   139
     O                       COL(9)         B   153
     O                       COL(10)        B   167
     O                       COL(11)        B   181
     O                       COL(12)        B   195
      *
     O          E            HAHDR          2
     O                       DASH               198
      *
      *-------------------------------------------------------------
      * HEAD TRANSFERRED IN
      *-------------------------------------------------------------
      *
     O          E            DTPHI          2
     O                                           19 '*** PERFORMANCE ***'
      *
     O          E            DTPHI          1
     O                                           19 'HEAD TRANSFERRED IN'
     O                       PHI(1)        KB    42
     O                       PHI(2)        KB    56
     O                       PHI(3)        KB    70
     O                       PHI(4)        KB    84
     O                       PHI(5)        KB    98
     O                       PHI(6)        KB   112
     O                       PHI(7)        KB   126
     O                       PHI(8)        KB   140
     O                       PHI(9)        KB   154
     O                       PHI(10)       KB   168
     O                       PHI(11)       KB   182
     O                       PHI(12)       KB   196
      *
      *-------------------------------------------------------------
      * ADJUSTED HEAD TRANSFERRED IN
      *-------------------------------------------------------------
      *
     O          E            DTPHA          1
     O                                           25 'TRANSFER IN ADJUSTED HEAD'
     O                       PHA(1)        KB    42
     O                       PHA(2)        KB    56
     O                       PHA(3)        KB    70
     O                       PHA(4)        KB    84
     O                       PHA(5)        KB    98
     O                       PHA(6)        KB   112
     O                       PHA(7)        KB   126
     O                       PHA(8)        KB   140
     O                       PHA(9)        KB   154
     O                       PHA(10)       KB   168
     O                       PHA(11)       KB   182
     O                       PHA(12)       KB   196
      *
      *-------------------------------------------------------------
      * HEAD SOLD
      *-------------------------------------------------------------
      *
     O          E            DTPHS          1
     O                                            9 'HEAD SOLD'
     O                       PHS(1)        KB    42
     O                       PHS(2)        KB    56
     O                       PHS(3)        KB    70
     O                       PHS(4)        KB    84
     O                       PHS(5)        KB    98
     O                       PHS(6)        KB   112
     O                       PHS(7)        KB   126
     O                       PHS(8)        KB   140
     O                       PHS(9)        KB   154
     O                       PHS(10)       KB   168
     O                       PHS(11)       KB   182
     O                       PHS(12)       KB   196
      *
      *-------------------------------------------------------------
      * CULLS SOLD
      *-------------------------------------------------------------
      *
     O          E            DTPHC          1
     O                                           10 'CULLS SOLD'
     O                       PHC(1)        KB    42
     O                       PHC(2)        KB    56
     O                       PHC(3)        KB    70
     O                       PHC(4)        KB    84
     O                       PHC(5)        KB    98
     O                       PHC(6)        KB   112
     O                       PHC(7)        KB   126
     O                       PHC(8)        KB   140
     O                       PHC(9)        KB   154
     O                       PHC(10)       KB   168
     O                       PHC(11)       KB   182
     O                       PHC(12)       KB   196
      *
      *-------------------------------------------------------------
      * HEAD TRANSFERRED OUT
      *-------------------------------------------------------------
      *
     O          E            DTPHO          1
     O                                           20 'HEAD TRANSFERRED OUT'
     O                       PHO(1)        KB    42
     O                       PHO(2)        KB    56
     O                       PHO(3)        KB    70
     O                       PHO(4)        KB    84
     O                       PHO(5)        KB    98
     O                       PHO(6)        KB   112
     O                       PHO(7)        KB   126
     O                       PHO(8)        KB   140
     O                       PHO(9)        KB   154
     O                       PHO(10)       KB   168
     O                       PHO(11)       KB   182
     O                       PHO(12)       KB   196
      *
      *-------------------------------------------------------------
      * DEATH LOSS PERCENT
      *-------------------------------------------------------------
      *
     O          E            DTPDL       1  1
     O                                           18 'DEATH LOSS PERCENT'
     O                       PDL(1)        KB    42
     O                       PDL(2)        KB    56
     O                       PDL(3)        KB    70
     O                       PDL(4)        KB    84
     O                       PDL(5)        KB    98
     O                       PDL(6)        KB   112
     O                       PDL(7)        KB   126
     O                       PDL(8)        KB   140
     O                       PDL(9)        KB   154
     O                       PDL(10)       KB   168
     O                       PDL(11)       KB   182
     O                       PDL(12)       KB   196
      *
      *-------------------------------------------------------------
      * AVERAGE POUNDS PLACED
      *-------------------------------------------------------------
      *
     O          E            DTPPP          1
     O                                           21 'AVERAGE POUNDS PLACED'
     O                       PPP(1)        KB    42
     O                       PPP(2)        KB    56
     O                       PPP(3)        KB    70
     O                       PPP(4)        KB    84
     O                       PPP(5)        KB    98
     O                       PPP(6)        KB   112
     O                       PPP(7)        KB   126
     O                       PPP(8)        KB   140
     O                       PPP(9)        KB   154
     O                       PPP(10)       KB   168
     O                       PPP(11)       KB   182
     O                       PPP(12)       KB   196
      *
      *-------------------------------------------------------------
      * AVERAGE POUNDS PER HEAD MOVED
      *-------------------------------------------------------------
      *
     O          E            DTPAP       1  1
     O                                           14 'PER HEAD MOVED'
     O          E            DTPAP          1
     O                                           19 'AVERAGE POUNDS'
     O                       PAP(1)        KB    42
     O                       PAP(2)        KB    56
     O                       PAP(3)        KB    70
     O                       PAP(4)        KB    84
     O                       PAP(5)        KB    98
     O                       PAP(6)        KB   112
     O                       PAP(7)        KB   126
     O                       PAP(8)        KB   140
     O                       PAP(9)        KB   154
     O                       PAP(10)       KB   168
     O                       PAP(11)       KB   182
     O                       PAP(12)       KB   196
      *
      *-------------------------------------------------------------
      * AVERAGE FEED POUNDS PER HEAD MOVED
      *-------------------------------------------------------------
      *
     O          E            DTPFP          1
     O                                           24 'AVERAGE FEED POUNDS'
     O                       PFP(1)        KB    42
     O                       PFP(2)        KB    56
     O                       PFP(3)        KB    70
     O                       PFP(4)        KB    84
     O                       PFP(5)        KB    98
     O                       PFP(6)        KB   112
     O                       PFP(7)        KB   126
     O                       PFP(8)        KB   140
     O                       PFP(9)        KB   154
     O                       PFP(10)       KB   168
     O                       PFP(11)       KB   182
     O                       PFP(12)       KB   196
      *
      *-------------------------------------------------------------
      * FEED CONVERSION
      *-------------------------------------------------------------
      *
     O          E            DTPFC       1  1
     O                                           15 'FEED CONVERSION'
     O                       PFC(1)        KB    42
     O                       PFC(2)        KB    56
     O                       PFC(3)        KB    70
     O                       PFC(4)        KB    84
     O                       PFC(5)        KB    98
     O                       PFC(6)        KB   112
     O                       PFC(7)        KB   126
     O                       PFC(8)        KB   140
     O                       PFC(9)        KB   154
     O                       PFC(10)       KB   168
     O                       PFC(11)       KB   182
     O                       PFC(12)       KB   196
      *
      *-------------------------------------------------------------
      * AVERAGE DAILY GAIN
      *-------------------------------------------------------------
      *
     O          E            DTPDG       1  1
     O                                           18 'AVERAGE DAILY GAIN'
     O                       PDG(1)        KB    42
     O                       PDG(2)        KB    56
     O                       PDG(3)        KB    70
     O                       PDG(4)        KB    84
     O                       PDG(5)        KB    98
     O                       PDG(6)        KB   112
     O                       PDG(7)        KB   126
     O                       PDG(8)        KB   140
     O                       PDG(9)        KB   154
     O                       PDG(10)       KB   168
     O                       PDG(11)       KB   182
     O                       PDG(12)       KB   196
      *
      *-------------------------------------------------------------
      * GRAMS OF LYSINE PER PIG DAY
      *-------------------------------------------------------------
      *
     O          E            DTPLD       1  1
     O                                           15 'GRAMS OF LYSINE'
     O                                           27 'PER PIG DAY'
     O                       PLD(1)        KB    42
     O                       PLD(2)        KB    56
     O                       PLD(3)        KB    70
     O                       PLD(4)        KB    84
     O                       PLD(5)        KB    98
     O                       PLD(6)        KB   112
     O                       PLD(7)        KB   126
     O                       PLD(8)        KB   140
     O                       PLD(9)        KB   154
     O                       PLD(10)       KB   168
     O                       PLD(11)       KB   182
     O                       PLD(12)       KB   196
      *
      *-------------------------------------------------------------
      * THOUSANDS OF CALORIES PER PIG DAY
      *-------------------------------------------------------------
      *
     O          E            DTPKD          1
     O                                           22 'K-CALORIES PER PIG DAY'
     O                       PKD(1)        KB    42
     O                       PKD(2)        KB    56
     O                       PKD(3)        KB    70
     O                       PKD(4)        KB    84
     O                       PKD(5)        KB    98
     O                       PKD(6)        KB   112
     O                       PKD(7)        KB   126
     O                       PKD(8)        KB   140
     O                       PKD(9)        KB   154
     O                       PKD(10)       KB   168
     O                       PKD(11)       KB   182
     O                       PKD(12)       KB   196
      *
      *-------------------------------------------------------------
      * LEAN PERCENT
      *-------------------------------------------------------------
      *
     O          E            DTPLP       1  1
     O                                           12 'LEAN PERCENT'
     O                       PLP(1)        KB    42
     O                       PLP(2)        KB    56
     O                       PLP(3)        KB    70
     O                       PLP(4)        KB    84
     O                       PLP(5)        KB    98
     O                       PLP(6)        KB   112
     O                       PLP(7)        KB   126
     O                       PLP(8)        KB   140
     O                       PLP(9)        KB   154
     O                       PLP(10)       KB   168
     O                       PLP(11)       KB   182
     O                       PLP(12)       KB   196
      *
      *-------------------------------------------------------------
      * BACKFAT INCHES
      *-------------------------------------------------------------
      *
     O          E            DTPBI          1
     O                                           14 'BACKFAT INCHES'
     O                       PBI(1)        KB    42
     O                       PBI(2)        KB    56
     O                       PBI(3)        KB    70
     O                       PBI(4)        KB    84
     O                       PBI(5)        KB    98
     O                       PBI(6)        KB   112
     O                       PBI(7)        KB   126
     O                       PBI(8)        KB   140
     O                       PBI(9)        KB   154
     O                       PBI(10)       KB   168
     O                       PBI(11)       KB   182
     O                       PBI(12)       KB   196
      *
      *-------------------------------------------------------------
      * CARCASS YIELD
      *-------------------------------------------------------------
      *
     O          E            DTPCY          1
     O                                           13 'CARCASS YIELD'
     O                       PCY(1)        KB    42
     O                       PCY(2)        KB    56
     O                       PCY(3)        KB    70
     O                       PCY(4)        KB    84
     O                       PCY(5)        KB    98
     O                       PCY(6)        KB   112
     O                       PCY(7)        KB   126
     O                       PCY(8)        KB   140
     O                       PCY(9)        KB   154
     O                       PCY(10)       KB   168
     O                       PCY(11)       KB   182
     O                       PCY(12)       KB   196
      *
      *-------------------------------------------------------------
      * PHASE COSTS PER POUND OF GAIN
      *-------------------------------------------------------------
      *
     O          E            DTCPG       1  1
     O                                           13 '*** COSTS ***'
      *
     O          E            DTCPG       1  1
     O                                           22 'COST PER POUND OF GAIN'
     O          E            DTCPG          1
     O                                           15 'PHASE COST'
     O                       CPG(1)        KB    42
     O                       CPG(2)        KB    56
     O                       CPG(3)        KB    70
     O                       CPG(4)        KB    84
     O                       CPG(5)        KB    98
     O                       CPG(6)        KB   112
     O                       CPG(7)        KB   126
     O                       CPG(8)        KB   140
     O                       CPG(9)        KB   154
     O                       CPG(10)       KB   168
     O                       CPG(11)       KB   182
     O                       CPG(12)       KB   196
      *
      *-------------------------------------------------------------
      * FEED COST PER POUND OF GAIN
      *-------------------------------------------------------------
      *
     O          E            DTCFG          1
     O                                           14 'FEED COST'
     O                       CFG(1)        KB    42
     O                       CFG(2)        KB    56
     O                       CFG(3)        KB    70
     O                       CFG(4)        KB    84
     O                       CFG(5)        KB    98
     O                       CFG(6)        KB   112
     O                       CFG(7)        KB   126
     O                       CFG(8)        KB   140
     O                       CFG(9)        KB   154
     O                       CFG(10)       KB   168
     O                       CFG(11)       KB   182
     O                       CFG(12)       KB   196
      *
      *-------------------------------------------------------------
      * AVERAGE FEED COST PER TON
      *-------------------------------------------------------------
      *
     O          E            DTCAF       1  1
     O                                           21 'AVERAGE FEED COST PER'
     O                                           25 'TON'
     O                       CAF(1)        KB    42
     O                       CAF(2)        KB    56
     O                       CAF(3)        KB    70
     O                       CAF(4)        KB    84
     O                       CAF(5)        KB    98
     O                       CAF(6)        KB   112
     O                       CAF(7)        KB   126
     O                       CAF(8)        KB   140
     O                       CAF(9)        KB   154
     O                       CAF(10)       KB   168
     O                       CAF(11)       KB   182
     O                       CAF(12)       KB   196
      *
      *-------------------------------------------------------------
      * TRANSFER IN COSTS PER CWT LIVE MOVED OUT
      *-------------------------------------------------------------
      *
     O          E            DTCIM       1  1
     O                                           17 'COST PER CWT LIVE'
     O                                           27 'MOVED OUT'
     O          E            DTCIM          1
     O                                           24 'TRANSFERRED IN COST'
     O                       CIM(1)        KB    42
     O                       CIM(2)        KB    56
     O                       CIM(3)        KB    70
     O                       CIM(4)        KB    84
     O                       CIM(5)        KB    98
     O                       CIM(6)        KB   112
     O                       CIM(7)        KB   126
     O                       CIM(8)        KB   140
     O                       CIM(9)        KB   154
     O                       CIM(10)       KB   168
     O                       CIM(11)       KB   182
     O                       CIM(12)       KB   196
      *
      *-------------------------------------------------------------
      * FEED COSTS PER CWT LIVE MOVED OUT
      *-------------------------------------------------------------
      *
     O          E            DTCFM          1
     O                                           14 'FEED COST'
     O                       CFM(1)        KB    42
     O                       CFM(2)        KB    56
     O                       CFM(3)        KB    70
     O                       CFM(4)        KB    84
     O                       CFM(5)        KB    98
     O                       CFM(6)        KB   112
     O                       CFM(7)        KB   126
     O                       CFM(8)        KB   140
     O                       CFM(9)        KB   154
     O                       CFM(10)       KB   168
     O                       CFM(11)       KB   182
     O                       CFM(12)       KB   196
      *
      *-------------------------------------------------------------
      * OTHER COSTS PER CWT LIVE MOVED OUT
      *-------------------------------------------------------------
      *
     O          E            DTCOM          1
     O                                           15 'OTHER COST'
     O                       COM(1)        KB    42
     O                       COM(2)        KB    56
     O                       COM(3)        KB    70
     O                       COM(4)        KB    84
     O                       COM(5)        KB    98
     O                       COM(6)        KB   112
     O                       COM(7)        KB   126
     O                       COM(8)        KB   140
     O                       COM(9)        KB   154
     O                       COM(10)       KB   168
     O                       COM(11)       KB   182
     O                       COM(12)       KB   196
      *
      *-------------------------------------------------------------
      * TOTAL COSTS PER CWT LIVE MOVED OUT
      *-------------------------------------------------------------
      *
     O          E            DTCTM          1
     O                                           13 'ALL COST'
     O                       CTM(1)        KB    42
     O                       CTM(2)        KB    56
     O                       CTM(3)        KB    70
     O                       CTM(4)        KB    84
     O                       CTM(5)        KB    98
     O                       CTM(6)        KB   112
     O                       CTM(7)        KB   126
     O                       CTM(8)        KB   140
     O                       CTM(9)        KB   154
     O                       CTM(10)       KB   168
     O                       CTM(11)       KB   182
     O                       CTM(12)       KB   196
      *
      *-------------------------------------------------------------
      * NET SALES PER CWT SOLD
      *-------------------------------------------------------------
      *
     O          E            DTCNS       1  1
     O                                           22 'NET SALES PER CWT SOLD'
     O                       CNS(1)        KB    42
     O                       CNS(2)        KB    56
     O                       CNS(3)        KB    70
     O                       CNS(4)        KB    84
     O                       CNS(5)        KB    98
     O                       CNS(6)        KB   112
     O                       CNS(7)        KB   126
     O                       CNS(8)        KB   140
     O                       CNS(9)        KB   154
     O                       CNS(10)       KB   168
     O                       CNS(11)       KB   182
     O                       CNS(12)       KB   196
      *
      *-------------------------------------------------------------
      * MARGIN PER CWT
      *-------------------------------------------------------------
      *
     O          E            DTCMR          1
     O                                           14 'MARGIN PER CWT'
     O                       CMR(1)        KB    42
     O                       CMR(2)        KB    56
     O                       CMR(3)        KB    70
     O                       CMR(4)        KB    84
     O                       CMR(5)        KB    98
     O                       CMR(6)        KB   112
     O                       CMR(7)        KB   126
     O                       CMR(8)        KB   140
     O                       CMR(9)        KB   154
     O                       CMR(10)       KB   168
     O                       CMR(11)       KB   182
     O                       CMR(12)       KB   196
      *
      *-------------------------------------------------------------
      * NO DATA MESSAGE LINE
      *-------------------------------------------------------------
      *
     O          E            NODATA         1
     O                                           19 'NO DATA MEETS YOUR'
     O                                           38 'SELECTION CRITERIA'
