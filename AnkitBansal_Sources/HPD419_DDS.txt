     A*%%TS  SD  20171113  155337  ISJBADE     REL-V7R1M0  5770-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION LISTING
     A*  DISPLAY FILE:  HPD419
     A*  TITLE:         SPECIFY OPTIONS FOR HPS/HPE COMPARISON LISTING
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  CREATED:       09/05/00
     A*
     A*****************************************************************
     A* MODIFICATIONS
     A* 11/08/2017 Brad Baden   E010812
     A*            Added Business Office to screen.
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A N99                                  CF04(04 'PROMPT')
     A N99 44                               CF08(08 'SUBMIT')
     A N99                                  CF20(20 'WRKSPLF')
     A N99                                  CF17(17 'PRINT OPTIONS')
     A*
     A*---------------------------------------------------------------
     A* RECORD FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP419R1
     A*%%TS  SD  20000905  105739  ISLRAMS     REL-V4R4M0  5769-PW1
     A                                      SETOF(35)
     A                                      SETOF(36)
     A                                      SETOF(37)
     A                                      RTNCSRLOC(&RCD &FLD)
     A                                      OVERLAY
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 25'ACCOUNTS RECEIVABLE SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USR)
     A                                      DSPATR(HI)
     A                                  2 15'SPECIFY OPTIONS FOR HPE/HPS COMPAR-
     A                                      ISON LISTING'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@JOB)
     A                                      DSPATR(HI)
     A                                  4  2'Business Office ?'
     A N99        D1FSBO    R        B  4 21REFFLD(@@BOCD)
     A N37                                  DSPATR(HI)
     A  37                                  DSPATR(RI)
     A  37                                  DSPATR(PC)
     A N99        D1BODS    R        O  4 32REFFLD(@@DS30)
     A                                  5  2'From kill date ..'
     A N99        D1FMDY    R        B  5 21REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A N35                                  DSPATR(HI)
     A  35                                  DSPATR(RI)
     A  35                                  DSPATR(PC)
     A                                  6  2'To kill date ....'
     A N99        D1TMDY    R        B  6 21REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A N36                                  DSPATR(HI)
     A  36                                  DSPATR(RI)
     A  36                                  DSPATR(PC)
     A*
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R HP419K1
     A*%%TS  SD  20000905  105739  ISLRAMS     REL-V4R4M0  5769-PW1
     A  43                             21  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A                                 23 11'F4=Prompt'
     A  44N99                          23 22'F8=Submit'
     A    N99                          23 33'F17-Print Options'
     A    N99                          23 52'F20=Spool Files'
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *---------------------------------------------------------------
     A          R HP419ES                   SFL SFLMSGRCD(24)
      *
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
     A*---------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*---------------------------------------------------------------
     A*
     A          R HP419EC                   SFLCTL(HP419ES)
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
