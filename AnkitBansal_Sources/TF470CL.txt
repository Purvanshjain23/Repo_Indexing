/***************************************************************** */
/*                                                                 */
/*  SYSTEM:          TRIUMPH FOODS                                 */
/*  PROGRAM NUMBER:  TF470CL                                       */
/*  PROGRAM NAME:    SPECIFY LISTING OF OMS CREDIT/DEBIT MEMOS     */
/*  PROGRAMMER:      LEANNE RAMSEY                                 */
/*  CREATE DATE:     08/21/09                                      */
/*                                                                 */
/*******************************************************************/
/* MODIFIED:                                                       */
/* 09/10/09  LEANNE RAMSEY                                         */
/*           SWITCHED FROM SHIP TO CUSTOMER TO A/R CUSTOMER.       */
/*******************************************************************/

             PGM

             DCL        VAR(&QDATA) TYPE(*CHAR) LEN(325)

             DCL        VAR(&OUTQ) TYPE(*CHAR) LEN(10)
             DCL        VAR(&HOLD) TYPE(*CHAR) LEN(4)
             DCL        VAR(&SAVE) TYPE(*CHAR) LEN(4)
             DCL        VAR(&COPY) TYPE(*DEC)  LEN(1)

             DCL        VAR(&LDRPT)    TYPE(*CHAR) LEN(1)
             DCL        VAR(&LDFSYNDT) TYPE(*DEC)  LEN(7)
             DCL        VAR(&LDTSYNDT) TYPE(*DEC)  LEN(7)
             DCL        VAR(&LDDSCD)   TYPE(*CHAR) LEN(1)
             DCL        VAR(&LDCONO)   TYPE(*DEC)  LEN(3)
             DCL        VAR(&LDCLNO)   TYPE(*DEC)  LEN(7)
             DCL        VAR(&LDMMTY)   TYPE(*CHAR) LEN(2)
             DCL        VAR(&LDMMNO)   TYPE(*DEC)  LEN(7)
             DCL        VAR(&LDCLORNO) TYPE(*DEC)  LEN(7)
             DCL        VAR(&LDPRCD)   TYPE(*DEC)  LEN(7)
             DCL        VAR(&LDCUNO)   TYPE(*DEC)  LEN(7)

             CHGVAR     VAR(&LDFSYNDT) VALUE(%SST(*LDA 25 7))
             CHGVAR     VAR(&LDTSYNDT) VALUE(%SST(*LDA 32 7))
             CHGVAR     VAR(&LDCONO)   VALUE(%SST(*LDA 68 3))
             CHGVAR     VAR(&LDCLNO)   VALUE(%SST(*LDA 71 7))
             CHGVAR     VAR(&LDMMNO)   VALUE(%SST(*LDA 78 7))
             CHGVAR     VAR(&LDPRCD)   VALUE(%SST(*LDA 85 7))
             CHGVAR     VAR(&LDMMTY)   VALUE(%SST(*LDA 92 2))
             CHGVAR     VAR(&LDCLORNO) VALUE(%SST(*LDA 94 7))
             CHGVAR     VAR(&LDDSCD)   VALUE(%SST(*LDA 108 1))
             CHGVAR     VAR(&LDCUNO)   VALUE(%SST(*LDA 109 7))
             CHGVAR     VAR(&LDRPT)    VALUE(%SST(*LDA 299 1))

/*---------------------------------------------------------------------*/
/* SET UP THE PRINT FILE                                               */
/*---------------------------------------------------------------------*/

 OUTQ:       CHGVAR     VAR(&OUTQ) VALUE(%SST(*LDA 401 10))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(*JOB))

 HOLD:       CHGVAR     VAR(&HOLD) VALUE(%SST(*LDA 411 4))
             IF         COND(&HOLD *NE '*YES' *AND &HOLD *NE '*NO ') +
                          THEN(CHGVAR VAR(&HOLD) VALUE(*NO))

 COPY:       CHGVAR     VAR(&COPY) VALUE(%SST(*LDA 419 1))
             IF         COND(&COPY = 0) THEN(CHGVAR VAR(&COPY) +
                          VALUE(1))

 SAVE:       CHGVAR     VAR(&SAVE) VALUE(%SST(*LDA 415 4))
             IF         COND(&SAVE *NE '*YES' *AND &SAVE *NE '*NO ') +
                          THEN(CHGVAR VAR(&SAVE) VALUE(*NO))

             OVRPRTF    FILE(PRINT198) OUTQ(&OUTQ) COPIES(&COPY) +
                          HOLD(&HOLD) SAVE(&SAVE)

/***********************************************************************/
/* EXTRACT THE DATA FROM THE SALES HISTORY FILE                        */
/***********************************************************************/

             CHGVAR     VAR(&QDATA) VALUE(' ')
             CHGVAR     VAR(%SST(&QDATA 1 36)) VALUE('F4JNCD *NE +
                          "OR" *AND F4JNCD *NE "TR"')

             CHGVAR     VAR(%SST(&QDATA 38 40)) VALUE('*AND (F4AIC3 +
                          *EQ 360 *OR F4AIC3 *EQ 960)')


/* IF FROM DATE                                                     */
             IF         COND(&LDFSYNDT *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 79 15)) VALUE('*AND F4GNDT *GE')
             CHGVAR     VAR(%SST(&QDATA 95 7)) VALUE(&LDFSYNDT)
             ENDDO

/* IF TO DATE                                                       */
             IF         COND(&LDTSYNDT *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 104 15)) VALUE('*AND F4GNDT *LE')
             CHGVAR     VAR(%SST(&QDATA 120 7)) VALUE(&LDTSYNDT)
             ENDDO

/* IF DATA SOURCE CODE IS S=SALES                                      */

             IF         COND(&LDDSCD *EQ 'S') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 129 17)) VALUE('*AND F4C0C7 +
                          *EQ 0')
             ENDDO

/* IF DATA SOURCE CODE IS C=CLAIMS                                     */

             IF         COND(&LDDSCD *EQ 'C') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 147 17)) VALUE('*AND F4C0C7 +
                          *NE 0')
             ENDDO

/* IF COMPANY                                                       */
             IF         COND(&LDCONO *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 165 15)) VALUE('*AND F4AIC3 *EQ')
             CHGVAR     VAR(%SST(&QDATA 181 3)) VALUE(&LDCONO)
             ENDDO

/* IF CLAIM NUMBER                                                  */

             IF         COND(&LDCLNO *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 185 15)) VALUE('*AND F4C0C7 *EQ')
             CHGVAR     VAR(%SST(&QDATA 201 7)) VALUE(&LDCLNO)
             ENDDO

/* IF MEMO TYPE                                                        */

             IF         COND(&LDMMTY *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 209 17)) VALUE('*AND F4JNCD *EQ "')
             CHGVAR     VAR(%SST(&QDATA 226 2)) VALUE(&LDMMTY)
             CHGVAR     VAR(%SST(&QDATA 228 1)) VALUE('"')
             ENDDO

/* IF CREDIT/DEBIT MEMO NUMBER                                      */

             IF         COND(&LDMMNO *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 230 15)) VALUE('*AND F4C4NB *EQ')
             CHGVAR     VAR(%SST(&QDATA 246 7)) VALUE(&LDMMNO)
             ENDDO

/* IF CLAIM ORDER NUMBER                                               */

             IF         COND(&LDCLORNO *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 254 15)) VALUE('*AND F4T3NB *EQ')
             CHGVAR     VAR(%SST(&QDATA 270 7)) VALUE(&LDCLORNO)
             ENDDO

/* IF PRODUCT CODE                                                  */

             IF         COND(&LDPRCD *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 278 15)) VALUE('*AND F4S5NB *EQ')
             CHGVAR     VAR(%SST(&QDATA 294 7)) VALUE(&LDPRCD)
             ENDDO

/* IF A/R CUSTOMER                                                  */

             IF         COND(&LDCUNO *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 302 15)) VALUE('*AND F4ANC7 *EQ')
             CHGVAR     VAR(%SST(&QDATA 318 7)) VALUE(&LDCUNO)
             ENDDO

/***********************************************************************/
/* SORTED BY "CLAIM"                                                   */
/***********************************************************************/

             IF         COND(&LDRPT *EQ 'C') THEN(DO)

             OVRDBF     FILE(OMHSTPLB) SHARE(*YES)
             OPNQRYF    FILE((OMHSTPLB)) QRYSLT(&QDATA) +
                          KEYFLD((F4AIC3) (F4C0C7) (F4C4NB) +
                          (F4DPNB)) SEQONLY(*YES)

             CALL       PGM(TF670)

             ENDDO

/***********************************************************************/
/* SORTED BY "PRODUCT"                                                 */
/***********************************************************************/

             IF         COND(&LDRPT *EQ 'P') THEN(DO)

             OVRDBF     FILE(OMHSTPLB) SHARE(*YES)
             OPNQRYF    FILE((OMHSTPLB)) QRYSLT(&QDATA) +
                          KEYFLD((F4AIC3) (F4S5NB) (F4C4NB) +
                          (F4DPNB) (F4C0C7)) SEQONLY(*YES)

             CALL       PGM(TF671)

             ENDDO

/*---------------------------------------------------------------------*/
/* END OF PROCESSING                                                   */
/*---------------------------------------------------------------------*/

             CLOF       OPNID(OMHSTPLB)
             DLTOVR     FILE(*ALL)

             ENDPGM

