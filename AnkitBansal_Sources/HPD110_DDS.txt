     A*%%TS  SD  20030623  161127  ISLRAMS     REL-V5R2M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION
     A*  DISPLAY FILE:  HPD110
     A*  TITLE:         REVISE EXPENSE CLASS
     A*  PROGRAMMER:    MARY PALMER
     A*  DATE:          04/11/94
     A*
     A*  INDICATOR 60 IS ON WHEN IN REVISE MODE
     A*  INDICATOR 61 IS ON WHEN IN CREATE MODE
     A*  INDICATOR 62 IS ON WHEN IN DELETE MODE
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A N62                                  CF04(04 'PROMPT')
     A  60N62                               CF05(05 'REFRESH')
     A  60N62                               CF11(11 'DELETE')
     A                                      CA12(12 'PREVIOUS')
     A*
     A*---------------------------------------------------------------
     A* RECORD FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP110R1
     A*%%TS  SD  20030623  161127  ISLRAMS     REL-V5R2M0  5722-WDS
     A                                      SETOF(31)
     A                                      SETOF(32)
     A                                      SETOF(33)
     A                                      SETOF(34)
     A                                      SETOF(53)
     A                                      RTNCSRLOC(&RCD &FLD)
     A                                      OVERLAY
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 29'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A            DFMODE         6A  O  2  2DSPATR(HI)
     A                                  2 29'REVISE EXPENSE CLASS'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  4  2'Expense class code .........'
     A            DFECCD    R        B  4 33REFFLD(ECECCD)
     A  61N31                               DSPATR(HI)
     A  31                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A  60
     AO 62                                  DSPATR(PR)
     A  61                                  DSPATR(UL)
     A                                  5  2'Expense class description ..'
     A            DFECDS    R        B  5 33REFFLD(ECECDS)
     A                                      CHECK(LC)
     A N32N62                               DSPATR(HI)
     A  32                                  DSPATR(PC)
     A  32                                  DSPATR(RI)
     A  62                                  DSPATR(PR)
     A N62                                  DSPATR(UL)
     A                                  6  2'Expense class sequence .....'
     A            DFECSEQ   R        B  6 33REFFLD(HSRREF/@@SEQ *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A N34N62                               DSPATR(HI)
     A  34                                  DSPATR(RI)
     A  34                                  DSPATR(PC)
     A  62                                  DSPATR(PR)
     A N62                                  DSPATR(UL)
     A                                  7  2'Allocation method code .....'
     A                                  7 31'?'
     A            DFAMCD    R        B  7 33REFFLD(ECAMCD)
     A N33N62                               DSPATR(HI)
     A  33                                  DSPATR(RI)
     A  33
     AO 53                                  DSPATR(PC)
     A  62                                  DSPATR(PR)
     A N62                                  DSPATR(UL)
     A            DFAMDS    R        O  7 40REFFLD(HSRREF/AMAMDS *LIBL/HSPREF)
     A*
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R HP110K1
     A*%%TS  SD  19980102  103805  ISLRAMS     REL-V3R7M0  5716-PW1
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A N62N61                          23 11'F4=Prompt'
     A N62N61                          23 22'F5=Refresh'
     A N62N61                          23 34'F11=Delete'
     A                                 23 46'F12=Previous'
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *---------------------------------------------------------------
     A          R HP110ES                   SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE CONTROL FORMAT
      *---------------------------------------------------------------
     A          R HP110EC                   SFLCTL(HP110ES)
      *
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
