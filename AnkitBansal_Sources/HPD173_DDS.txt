     A*%%TS  SD  20050321  132127  ISLRAMS     REL-V5R2M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD173
     A*  TITLE:         MAINTAIN FARM COSTS
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  CREATED:       06/28/94
     A*
     A*  INDICATOR 60 IS ON WHEN IN REVISE MODE
     A*  INDICATOR 61 IS ON WHEN IN CREATE MODE
     A*  INDICATOR 62 IS ON WHEN USER HAS DELETED OR CREATED A RECORD
     A*               IN THE SUBFILE
     A*  INDICATOR 90 IS ON WHEN THE BUSINESS OFFICE THE USER HAS
     A*               SELECTED FROM THE SPECIFY HAS BEEN DELETED
     A*               BY ANOTHER USER
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A N60                                  CF04(04 'PROMPT')
     A N90                                  CF05(05 'REFRESH')
     A  60N90                               CF06(06 'CREATE')
     A                                      CA12(12 'PREVIOUS')
     A N82N90                               ROLLUP(25 'PAGE FORWARD')
     A*
     A*---------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP173S1                   SFL
     A*%%TS  SD  20050321  132127  ISLRAMS     REL-V5R2M0  5722-WDS
     A                                      SETOF(30)
     A                                      SETOF(31)
     A                                      SETOF(32)
     A                                      SETOF(51)
     A                                      SETOF(62)
     A  85                                  SFLNXTCHG
     A            HDRRN1         4Y 0H
     A            DFOPT          2A  B 11  2CHGINPDFT(UL)
     A  61
     AO 62
     AO 90                                  DSPATR(PR)
     A  61
     AO 90                                  DSPATR(ND)
     A  30                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A N30N61N90                            DSPATR(HI)
     A  60N30N90                            DSPATR(UL)
     A            DFFSCD    R        B 11  6REFFLD(FCFSCD)
     A  60
     AO 62
     AO 90                                  DSPATR(PR)
     A  61 90                               DSPATR(ND)
     A  31                                  DSPATR(RI)
     A  31
     AO 51                                  DSPATR(PC)
     A N31N60N62
     A N90                                  DSPATR(HI)
     A N60N62N90                            DSPATR(UL)
     A            DFHDRT    R        B 11 14REFFLD(FCHDRT)
     A                                      EDTCDE(K)
     A  62
     AO 90                                  DSPATR(PR)
     A  61 90                               DSPATR(ND)
     A  32                                  DSPATR(RI)
     A  32                                  DSPATR(PC)
     A N32N62N90                            DSPATR(HI)
     A N62N90                               DSPATR(UL)
     A            DFFSNM    R        O 11 26REFFLD(FSFSNM)
     A            DFACT          8A  O 11 55
     A*
     A*---------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP173C1                   SFLCTL(HP173S1)
     A*%%TS  SD  20050321  132127  ISLRAMS     REL-V5R2M0  5722-WDS
     A                                      SFLSIZ(0100)
     A                                      SFLPAG(0011)
     A                                      SFLCSRRRN(&HDRR1)
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A                                      OVERLAY
     A                                      RTNCSRLOC(&RCD &FLD)
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            HDRR1          5S 0H
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 25'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A            DFMODE    R        O  2  2REFFLD(@@MODE)
     A                                      DSPATR(HI)
     A                                  2 26'MAINTAIN FARM COSTS'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  4  2'Business office ..:'
     A            DFFSBO    R        O  4 23REFFLD(FCFSBO)
     A            DFBODS    R        O  4 33REFFLD(BOBODS)
     A                                  5  2'Effective date ...:'
     A            DFMDY     R        O  5 23REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A N61                              7  2'4=Delete'
     A N60                              8  9'?'
     A                                      DSPATR(HI)
     A                                  9  7'Farm'
     A                                      DSPATR(HI)
     A                                  9 17'Cost'
     A                                      DSPATR(HI)
     A N61                             10  2'Opt'
     A                                      DSPATR(HI)
     A                                 10  7'Site'
     A                                      DSPATR(HI)
     A                                 10 15'Per Head'
     A                                      DSPATR(HI)
     A                                 10 26'Farm Name'
     A                                      DSPATR(HI)
     A*
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R HP173K1
     A*%%TS  SD  19940321  103222  ISLRAMS     REL-V2R2M0  5738-PW1
     A                                      SETOF(60)
     A                                      SETOF(61)
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A                                      DSPATR(HI)
     A                                 23  2'F3=Exit'
     A N60                             23 11'F4=Prompt'
     A N90                             23 22'F5=Refresh'
     A  60N90                          23 34'F6=Create'
     A                                 23 45'F12=Previous'
     A*
     A*---------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE FORMAT
     A*---------------------------------------------------------------
     A          R HP173ES                   SFL SFLMSGRCD(24)
     A*
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
     A*                                                                *********
     A*---------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*----------------------------------------------------------------
     A          R HP173EC                   SFLCTL(HP173ES)
     A*
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
