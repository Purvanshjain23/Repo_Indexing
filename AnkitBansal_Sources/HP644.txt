      *****************  RPG PROGRAM HEADING  ************************
      * SYSTEM:      HOG PRODUCTION
      * PROGRAM:     HP644
      * TITLE:       DEATHS/CULL SALES BY WEEKS IN PHASE REPORT
      * PROGRAMMER:  LEANNE FEDOR
      * CREATED:     10/11/96
      *
      *  FUNCTION:   THIS PROGRAM PRINTS A REPORT WHICH COMBINES DEATHS
      *              AND CULL SALES IN 28 COLUMNS--EACH COLUMN
      *              REPRESENTING A WEEK IN THE LIFE OF THE GROUP.
      *
      *              THE USER CAN INCLUDE ANY OF THE FOLLOWING:
      *                   1) DOAS FROM TRANSFERS
      *                   2) DOAS FROM SALES
      *                   3) DAILY DEADS
      *                   4) CULL SALES
      *
      *              THE SELECTIONS ARE ENTERED THROUGH HP444 AND AN
      *              OPNQRY IS USED IN HP444CL TO SELECT RECORDS.
      *              THREE WORKFILES ARE BUILT IN HP344.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 08/26/99  LeAnne Fedor
      *           Added logic for 8 additional weeks.  Report will now have
      *           28 weeks instead of 20.
      *
      * 11/03/99  LeAnne Fedor
      *           Added building/room to report.
      *
      * 08/21/00  LeAnne Fedor
      *           Since death and cull percents are now calculated with the
      *           'adjusted transfers in', I changed the report to print
      *           adjusted transfer in head instead of 'transfer in' head.
      *
      * 09/17/01  LeAnne Fedor
      *           'Group type' added as a new report option.
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     FHSP011    IF   E           K DISK
      *   PRODUCTION PHASE           (KEY: PPPPCD)
      *
     F*SP018  IF  E           K        DISK
      *   FARM SITE                  (KEY: FSFSCD)
      *
     FHSL345B   IF   E           K DISK
      *  WORKFILE OF GROUP TOTALS    (KEY: WGPPCD WGOPDT WGHGCD)
      *
     FHSP346    IF   E           K DISK
      *  WORKFILE OF PHASE TOTALS    (KEY: WPPPCD)
      *
     FQPRINT    O    F  198        PRINTER OFLIND(*INOF)
      *
      /EJECT
      ****************************************************************
      * TABLE AND ARRAY SPECIFICATIONS
      ****************************************************************
      * INPUT SPECS
      ****************************************************************
      *
      *---------------------------------------------------------------
      * ARRAYS USED TO PRINT THE 'CATEGORIES OF DATA' IN THE HEADING
      *---------------------------------------------------------------
     D CAT             S             19    DIM(4)
      *
      *----------------------------------------------------------------
      * LDA - REPORT SELECTIONS
      *----------------------------------------------------------------
      *
     D                UDS
     D  LDFSBO                 1      5
     D  LDFYMD                 6     13  0
     D  LDTYMD                14     21  0
     D  LDPPCD                22     26
     D  LDFSCD                27     31  0
     D  LDBSCD                32     36
     D  LDGTCD                47     47
     D  LDPTCD                48     52
     D  LDHGSN                53     59  0
     D  LDFMDY                60     65  0
     D  LDTMDY                66     71  0
     D  LDCELL                77     79  0
      *
     D  LDPPFL                81     81
     D  LDTDFL                82     82
     D  LDSDFL                83     83
     D  LDDDFL                84     84
     D  LDCSFL                85     85
      *
     D  LDBODS                86    117
     D  LDPTDS               118    127
     D  LDPPDS               128    157
     D  LDFSNM               158    182
     D  LDHGCD               183    189
     D  LDBSDS               190    209
     D  LDGTDS               210    224
      *
     D  LDOUTU               401    410
     D  LDHOLD               411    411
     D  LDCOPY               412    412
     D  LDOUTQ               413    422
      *
      *
      *
      *---------------------------------------------------------------
      * STANDARD PROGRAM STATUS DATA STRUCTURE
      *---------------------------------------------------------------
      *    EXTERNALLY DEFINED AS UTPGFR (RECORD FORMAT: PGMDSR)
     D PGMDS         ESDS                  EXTNAME(UTPGFR)
      *
     D  DUMMYD                 1      1
      *
      *---------------------------------------------------------------
      * NAMED CONSTANTS
      *---------------------------------------------------------------
     D YES             C                   CONST('Y')
     D NO              C                   CONST('N')
      *
     D CSALE           C                   CONST('CULL SALES         ')
     D TDOAS           C                   CONST('DOAS FROM TRANSFERS')
     D SDOAS           C                   CONST('DOAS FROM SALES    ')
     D DDEAD           C                   CONST('DAILY DEADS        ')
      *  THE PRECEDING DUMMY DATA STRUCTURE IS ONLY TO KEEP THE I-SPEC
      *  INFO FROM RUNNING OVER INTO THE 'CALC SPECS' ON THE COMPILE
      *  LISTING.  IT SERVES NO OTHER PURPOSE AND CAN BE DELETED
      *  AT ANY TIME!!!
      /EJECT
      ****************************************************************
      * MAINLINE
      ****************************************************************
      *
      *
      * PROCESS EACH GROUP IN THE GROUP TOTAL WORKFILE
      *
     C     *IN90         DOUEQ     *ON                                          DO DETAIL
     C                   READ      HSL345B                                90
     C     *IN90         IFEQ      *OFF                                         IF NOT EOF
      *
     C     FIRST         IFEQ      YES                                          IF FIRST
     C                   MOVEL     WGPPCD        SVPPCD
     C                   EXSR      $PPDS
     C                   EXSR      $HDR
     C                   MOVEL     NO            FIRST
     C                   ENDIF                                                  IF FIRST
      *
      *--------------------------------------------------------------
      * PERFORM CONTROL BREAK LOGIC WHEN THE USER DOES NOT LIMIT
      * THE REPORT TO A SINGLE GROUP
      *--------------------------------------------------------------
      *
      * IF BREAK IN PRODUCTION PHASE,
      *    PRINT PRODUCTION PHASE TOTAL LINE
      *    RETRIEVE DESCRIPTION OF NEXT PHASE
      *    PAGE BREAK AND PRINT REPORT HEADINGS
      *
     C     LDHGSN        IFEQ      0                                            IF ALL GROUP
     C     WGPPCD        ANDNE     SVPPCD
      *
     C     *INOF         IFEQ      *ON
     C                   EXSR      $HDR
     C                   ENDIF
     C                   EXSR      $TPTOT
      *
     C                   MOVEL     WGPPCD        SVPPCD
     C                   EXSR      $PPDS
     C                   EXSR      $HDR
      *
     C                   ENDIF                                                  IF ALL GROUP
      *
      *----------------------------------------------------------------
      * DETAIL PROCESSING
      *----------------------------------------------------------------
      * PRINT THE GROUP TOTAL RECORD FROM THE WORKFILE.
      *
     C                   MOVEL(P)  WGFSNM        DTFSNM
      *
     C     *INOF         IFEQ      *ON
     C                   EXSR      $HDR
     C                   ENDIF
     C                   EXCEPT    DGDTL
      *
     C                   ENDIF                                                  IF NOT EOF
     C                   ENDDO                                                  DO DETAIL
      *
      *
      *----------------------------------------------------------------
      * EOF PROCESSING
      *----------------------------------------------------------------
      *
      *  IF NO DETAIL RECORDS MATCHED THE SELECTION CRITERIA,
      *         PRINT THE STANDARD 'NO DATA...' ERROR MESSAGE
      *
     C                   SELECT
     C     FIRST         WHENEQ    YES
     C                   EXCEPT    HRHDR
     C                   EXCEPT    NODATA
     C                   OTHER
      *
      * ONLY PRINT TOTAL LINES WHEN THE USER DID NOT LIMIT
      * THE REPORT TO A SINGLE HOG GROUP.
      *
     C     LDHGSN        IFEQ      0                                            IF ALL GROUP
      *
     C     *INOF         IFEQ      *ON
     C                   EXSR      $HDR
     C                   ENDIF
     C                   EXSR      $TPTOT
     C                   ENDIF                                                  IF ALL GROUP
      *
     C                   ENDSL
      *
     C                   SETON                                        LR
      /EJECT
      *----------------------------------------------------------------
      * $PPDS - RETRIEVE PRODUCTION PHASE DESCRIPTION
      *----------------------------------------------------------------
      *
     C     $PPDS         BEGSR
      *
     C     SVPPCD        CHAIN     HSP011                             92
     C     *IN92         IFEQ      *OFF
     C                   MOVEL     PPPPDS        HPPPDS
     C                   ELSE
     C                   MOVEL(P)  'UNKNOWN'     HPPPDS
     C                   ENDIF
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $TPTOT - RETRIEVE PHASE TOTAL WORKFILE RECORD AND PRINT
      *----------------------------------------------------------------
      *
     C     $TPTOT        BEGSR
      *
     C     SVPPCD        CHAIN     HSP346                             92
     C     *IN92         IFEQ      *OFF
     C                   EXCEPT    TPTOT
     C                   ENDIF
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $HDR  - PRINT REPORT HEADINGS
      *----------------------------------------------------------------
      *
     C     $HDR          BEGSR
      *
     C                   EXCEPT    HRHDR
     C                   EXCEPT    HPHDR
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * *INZSR - INITIALIZATION SUBROUTINE
      *---------------------------------------------------------------
      *
     C     *INZSR        BEGSR
      *
      * SAVE FIELDS FOR LEVEL BREAK COMPARISONS
      *
     C     *LIKE         DEFINE    WGPPCD        SVPPCD
      *
      * PRINT FIELD FOR HEADINGS
      *
     C     *LIKE         DEFINE    PPPPDS        HPPPDS
      *
      *
      * PRINT FIELD FOR DETAIL LINES
      *
     C                   MOVE      *BLANK        DTFSNM           11
      *
      *
      *  SETUP THE USER SELECTIONS IN THE REPORT HEADINGS.
      *
     C     LDFSBO        IFEQ      *BLANK
     C                   MOVEL     'ALL'         LDFSBO
     C                   ENDIF
      *
     C     LDPPCD        IFEQ      *BLANK
     C                   MOVEL     'ALL'         LDPPCD
     C                   ENDIF
      *
     C     LDPTCD        IFEQ      *BLANK
     C                   MOVEL     'ALL'         LDPTCD
     C                   ENDIF
      *
     C     LDBSCD        IFEQ      *BLANK
     C                   MOVEL     'ALL'         LDBSCD
     C                   ENDIF
      *
      *
     C     LDGTCD        IFEQ      *BLANK
     C                   SETON                                        87
     C                   ENDIF
      *
     C     LDCELL        IFEQ      0
     C                   SETON                                        88
     C                   ENDIF
      *
     C     LDFSCD        IFEQ      0
     C                   SETON                                        89
     C                   ENDIF
      *
     C     LDHGCD        IFEQ      *BLANK
     C                   MOVEL     'ALL'         LDHGCD
     C                   ENDIF
      *
      * SET UP A FIRST TIME FLAG
      * SET UP A DASHED LINE FOR UNDERLINING THE HEADING
      * RETRIEVE TIME FOR REPORT HEADING
      *
     C                   MOVEL     YES           FIRST             1
     C                   MOVEL     *ALL'-'       DASH            198
     C                   TIME                    RTIME             6 0
      *
      * LOAD ARRAY THAT HOLDS THE 'CATEGORIES OF DATA' THE
      * USER HAS SELECTED. THE ARRAY ELEMENTS WILL BE PRINTED
      * IN THE REPORT HEADING.
      *
     C                   Z-ADD     1             X                 1 0
     C     LDTDFL        IFEQ      'X'                                          IF TRAN DOA
     C     *BLANK        LOOKUP    CAT(X)                                 92
     C     *IN92         IFEQ      *ON                                          IF ADD
     C                   MOVEL     TDOAS         CAT(X)
     C                   ENDIF                                                  IF ADD
     C                   ENDIF                                                  IF TRAN DOA
      *
     C                   Z-ADD     1             X
     C     LDSDFL        IFEQ      'X'                                          IF SALES DOA
     C     *BLANK        LOOKUP    CAT(X)                                 92
     C     *IN92         IFEQ      *ON                                          IF ADD
     C                   MOVEL     SDOAS         CAT(X)
     C                   ENDIF                                                  IF ADD
     C                   ENDIF                                                  IF SALES DOA
      *
     C                   Z-ADD     1             X
     C     LDDDFL        IFEQ      'X'                                          IF DEAD
     C     *BLANK        LOOKUP    CAT(X)                                 92
     C     *IN92         IFEQ      *ON                                          IF ADD
     C                   MOVEL     DDEAD         CAT(X)
     C                   ENDIF                                                  IF ADD
     C                   ENDIF                                                  IF DEAD
      *
     C                   Z-ADD     1             X
     C     LDCSFL        IFEQ      'X'                                          IF SALES
     C     *BLANK        LOOKUP    CAT(X)                                 92
     C     *IN92         IFEQ      *ON                                          IF ADD
     C                   MOVEL     CSALE         CAT(X)
     C                   ENDIF                                                  IF ADD
     C                   ENDIF                                                  IF SALES
      *
      * SET THE INDICATOR THAT WILL CONTROL THE PRINTING OF
      * THE PERCENT VALUES ACCORDING TO THE USER'S SELECTION
      *
     C     LDPPFL        IFEQ      YES
     C                   SETON                                        86
     C                   MOVEL     'YES'         HRPPFL            3
     C                   ELSE
     C                   MOVEL     'NO '         HRPPFL
     C                   ENDIF
      *
     C                   ENDSR
      /EJECT
      *-------------------------------------------------------------
      * REPORT HEADINGS
      *-------------------------------------------------------------
     OQPRINT    E            HRHDR          1 01
     O                       SDPGM               10
     O                                          106 'HOG PRODUCTION SYSTEM'
     O                                          188 'DATE'
     O                       UDATE         Y    198
      *
     O          E            HRHDR          1
     O                       SDUSR               10
     O                                          103 'DEATHS AND CULL SALES'
     O                                          110 'REPORT'
     O                                          188 'TIME'
     O                       RTIME              198 '  :  :  '
      *
      *
     O          E            HRHDR          1
     O                                          104 'BY WEEKS IN PHASE'
     O                                          188 'PAGE'
     O                       PAGE          Z    198
      *
      *
     O          E            HRHDR          1
     O                                           18 'BUSINESS OFFICE:'
     O                       LDFSBO              25
     O                       LDBODS            +  5
     O                                          140 'CATEGORIES OF DATA'
     O                                         +  1 'INCLUDED:'
      *
      *
     O          E            HRHDR          1
     O                                           18 'FROM DATE:'
     O                       LDFMDY            +  2 '  /  /  '
     O                       CAT(1)             145
      *
      *
     O          E            HRHDR          1
     O                                           18 'TO DATE:'
     O                       LDTMDY            +  2 '  /  /  '
     O                       CAT(2)             145
      *
      *
     O          E            HRHDR          1
     O                                           18 'PRODUCTION TYPE:'
     O                       LDPTCD              25
     O                       LDPTDS            +  5
     O                       CAT(3)             145
      *
      *
     O          E            HRHDR          1
     O                                           18 'PRODUCTION PHASE:'
     O                       LDPPCD              25
     O                       LDPPDS            +  5
     O                       CAT(4)             145
      *
      *
     O          E            HRHDR          1
     O                                           18 'BREED STOCK:'
     O                       LDBSCD              25
     O                       LDBSDS            +  5
      *
      *
     O          E            HRHDR          1
     O                                           18 'GROUP TYPE:'
     O              N87      LDGTCD              21
     O              N87      LDGTDS            +  9
     O               87                          23 'ALL'
      *
      *
     O          E            HRHDR          1
     O                                           18 'CELL:'
     O              N88      LDCELL        M     25
     O               88                          23 'ALL'
      *
     O          E            HRHDR          1
     O                                           18 'FARM SITE:'
     O              N89      LDFSCD        Z     26
     O              N89      LDFSNM              55
     O               89                          23 'ALL'
      *
      *
     O          E            HRHDR          1
     O                                           18 'HOG GROUP:'
     O                       LDHGCD              27
      *
      *
     O          E            HRHDR          2
     O                                           18 'PRINT PERCENT:'
     O                       HRPPFL              23
      *
      *
      *-------------------------------------------------------------
      * COLUMN HEADINGS
      *-------------------------------------------------------------
      *
     O          E            HRHDR          1
     O                                           40 'WEEK 1'
     O                                           48 'WEEK 2'
     O                                           56 'WEEK 3'
     O                                           64 'WEEK 4'
     O                                           72 'WEEK 5'
     O                                           80 'WEEK 6'
     O                                           88 'WEEK 7'
     O                                           96 'WEEK 8'
     O                                          104 'WEEK 9'
     O                                          112 'WEEK 10'
     O                                          121 'WEEK 11'
     O                                          129 'WEEK 12'
     O                                          137 'WEEK 13'
     O                                          145 'WEEK 14'
     O                                          153 'WEEK 15'
     O                                          161 'WEEK 16'
     O                                          169 'WEEK 17'
     O                                          177 'WEEK 18'
     O                                          185 'WEEK 19'
     O                                          193 'WEEK 20'
      *
     O          E            HRHDR          1
     O                                           13 'GROUP'
     O                                           29 'TOTAL HEAD'
     O                                           40 'WEEK 21'
     O                                           48 'WEEK 22'
     O                                           56 'WEEK 23'
     O                                           64 'WEEK 24'
     O                                           72 'WEEK 25'
     O                                           80 'WEEK 26'
     O                                           88 'WEEK 27'
     O                                           96 'WEEK 28'
      *
      *
     O          E            HRHDR          1
     O                                            5 'FARM'
     O                                           17 'FARM NAME'
     O                                           29 'DEAD/CULL'
     O                                           38 'HEAD'
     O                                           46 'HEAD'
     O                                           54 'HEAD'
     O                                           62 'HEAD'
     O                                           70 'HEAD'
     O                                           78 'HEAD'
     O                                           86 'HEAD'
     O                                           94 'HEAD'
     O                                          102 'HEAD'
     O                                          109 'HEAD'
     O                                          118 'HEAD'
     O                                          126 'HEAD'
     O                                          134 'HEAD'
     O                                          142 'HEAD'
     O                                          150 'HEAD'
     O                                          158 'HEAD'
     O                                          166 'HEAD'
     O                                          174 'HEAD'
     O                                          182 'HEAD'
     O                                          190 'HEAD'
      *
      *
     O          E            HRHDR          1
     O                                            5 'SITE'
     O                                           12 'BLDG'
     O                                           18 'ROOM'
     O                                           29 'ADJ TRN IN'
     O                                           41 '%'
     O                                           49 '%'
     O                                           57 '%'
     O                                           65 '%'
     O                                           73 '%'
     O                                           81 '%'
     O                                           89 '%'
     O                                           97 '%'
     O                                          105 '%'
     O                                          112 '%'
     O                                          121 '%'
     O                                          129 '%'
     O                                          137 '%'
     O                                          145 '%'
     O                                          153 '%'
     O                                          161 '%'
     O                                          169 '%'
     O                                          177 '%'
     O                                          185 '%'
     O                                          193 '%'
      *
     O          E            HRHDR          1
     O                       DASH               198
      *
      *-------------------------------------------------------------
      * PRODUCTION PHASE HEADING
      *-------------------------------------------------------------
      *
     O          E            HPHDR          2
     O                                           84 '*****'
     O                                          101 'PRODUCTION PHASE'
     O                                          107 '*****'
     O                       HPPPDS            +  3
      *
      *-------------------------------------------------------------
      * GROUP DETAIL
      *-------------------------------------------------------------
      *
     O          E            DGDTL          1
     O                       WGFSCD        Z      5
     O                       WGHGCD              15
     O                       WGCDHD        K     30
     O                       WG01HD        K     39
     O                       WG02HD        K     47
     O                       WG03HD        K     55
     O                       WG04HD        K     63
     O                       WG05HD        K     71
     O                       WG06HD        K     79
     O                       WG07HD        K     87
     O                       WG08HD        K     95
     O                       WG09HD        K    103
     O                       WG10HD        K    110
     O                       WG11HD        K    119
     O                       WG12HD        K    127
     O                       WG13HD        K    135
     O                       WG14HD        K    143
     O                       WG15HD        K    151
     O                       WG16HD        K    159
     O                       WG17HD        K    167
     O                       WG18HD        K    175
     O                       WG19HD        K    183
     O                       WG20HD        K    191
      *
      *
     O          E            DGDTL          1
     O                       DTFSNM              19
     O                       WGTIHD        K     30
     O               86      WG01PR        K     44
     O               86      WG02PR        K     52
     O               86      WG03PR        K     60
     O               86      WG04PR        K     68
     O               86      WG05PR        K     76
     O               86      WG06PR        K     84
     O               86      WG07PR        K     92
     O               86      WG08PR        K    100
     O               86      WG09PR        K    108
     O               86      WG10PR        K    115
     O               86      WG11PR        K    124
     O               86      WG12PR        K    132
     O               86      WG13PR        K    140
     O               86      WG14PR        K    148
     O               86      WG15PR        K    156
     O               86      WG16PR        K    164
     O               86      WG17PR        K    172
     O               86      WG18PR        K    180
     O               86      WG19PR        K    188
     O               86      WG20PR        K    196
      *
      *
     O          E            DGDTL          1
     O                       WGBLCD              13
     O                       WGRMCD              19
     O                       WG21HD        K     39
     O                       WG22HD        K     47
     O                       WG23HD        K     55
     O                       WG24HD        K     63
     O                       WG25HD        K     71
     O                       WG26HD        K     79
     O                       WG27HD        K     87
     O                       WG28HD        K     95
      *
     O          E            DGDTL          1
     O               86      WG21PR        K     44
     O               86      WG22PR        K     52
     O               86      WG23PR        K     60
     O               86      WG24PR        K     68
     O               86      WG25PR        K     76
     O               86      WG26PR        K     84
     O               86      WG27PR        K     92
     O               86      WG28PR        K    100
      *
      *----------------------------------------------------------------
      * TOTAL FOR PRODUCTION PHASE
      *----------------------------------------------------------------
      *
     O          E            TPTOT       1  0
     O                                           13 'TOTAL PHASE'
     O                       SVPPCD              20
     O                       WPCDHD        KB    30
     O                       WP01HD        KB    39
     O                       WP02HD        K     47
     O                       WP03HD        K     55
     O                       WP04HD        K     63
     O                       WP05HD        K     71
     O                       WP06HD        K     79
     O                       WP07HD        K     87
     O                       WP08HD        K     95
     O                       WP09HD        K    103
     O                       WP10HD        K    110
     O                       WP11HD        K    119
     O                       WP12HD        K    127
     O                       WP13HD        K    135
     O                       WP14HD        K    143
     O                       WP15HD        K    151
     O                       WP16HD        K    159
     O                       WP17HD        K    167
     O                       WP18HD        K    175
     O                       WP19HD        K    183
     O                       WP20HD        K    191
      *
     O          E            TPTOT       1  0
     O                       WPTIHD        KB    30
     O                       WP01PR        KB    44
     O                       WP02PR        K     52
     O                       WP03PR        K     60
     O                       WP04PR        K     68
     O                       WP05PR        K     76
     O                       WP06PR        K     84
     O                       WP07PR        K     92
     O                       WP08PR        K    100
     O                       WP09PR        K    108
     O                       WP10PR        K    115
     O                       WP11PR        K    124
     O                       WP12PR        K    132
     O                       WP13PR        K    140
     O                       WP14PR        K    148
     O                       WP15PR        K    156
     O                       WP16PR        K    164
     O                       WP17PR        K    172
     O                       WP18PR        K    180
     O                       WP19PR        K    188
     O                       WP20PR        K    196
      *
     O          E            TPTOT       1  0
     O                       WP21HD        K     39
     O                       WP22HD        K     47
     O                       WP23HD        K     55
     O                       WP24HD        K     63
     O                       WP25HD        K     71
     O                       WP26HD        K     79
     O                       WP27HD        K     87
     O                       WP28HD        K     95
      *
     O          E            TPTOT       1  0
     O                       WP21PR        K     44
     O                       WP22PR        K     52
     O                       WP23PR        K     60
     O                       WP24PR        K     68
     O                       WP25PR        K     76
     O                       WP26PR        K     84
     O                       WP27PR        K     92
     O                       WP28PR        K    100
      *
      *-------------------------------------------------------------
      * NO DATA MESSAGE LINE
      *-------------------------------------------------------------
      *
     O          E            NODATA      2
     O                                           19 'NO DATA MEETS YOUR'
     O                                           38 'SELECTION CRITERIA'
