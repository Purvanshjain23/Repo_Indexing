     A*%%TS  SD  20090807  154348  ISLRAMS     REL-V5R4M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD756
     A*  TITLE:         VIEW COST CENTERS ASSIGNED TO MULTIPLE FARMS
     A*  PROGRAMMER:    LEANNE RAMSEY
     A*  DATE:          08/07/09
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A                                      CF04(04 'PROMPT')
     A                                      CF05(05 'REFRESH')
     A*
     A*
     A*---------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP756S1                   SFL
     A*%%TS  SD  20090807  154348  ISLRAMS     REL-V5R4M0  5722-WDS
     A  85                                  SFLNXTCHG
     A            HDRRN1         4Y 0H
     A            D1CJD     R        O  8  2REFFLD(HSRREF/@@CJD *LIBL/HSPREF)
     A            D1FSCD    R        O  8 16REFFLD(@@FSCD)
     A                                      EDTCDE(M)
     A            D1AIST    R        O  8 24REFFLD(HSRREF/@@AIST *LIBL/HSPREF)
     A            D1FSNM    R        O  8 27REFFLD(FSREC/FSFSNM *LIBL/HSP018)
     A            D1TFCD    R        O  8 54REFFLD(HSRREF/@@TFCD *LIBL/HSPREF)
     A            D1CELL    R        O  8 62REFFLD(FSREC/FSCELL *LIBL/HSP018)
     A                                      EDTCDE(M)
     A            D1PPCD    R        O  8 67REFFLD(HSRREF/@@BOCD *LIBL/HSPREF)
     A            D1PTCD    R        O  8 74REFFLD(HSRREF/@@PTCD *LIBL/HSPREF)
     A*
     A*
     A*---------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP756C1                   SFLCTL(HP756S1)
     A*%%TS  SD  20090807  154348  ISLRAMS     REL-V5R4M0  5722-WDS
     A                                      SFLSIZ(0050)
     A                                      SFLPAG(0014)
     A N82                                  ROLLUP(25 'PAGE FORWARD')
     A                                      SETOF(51)
     A                                      SETOF(52)
     A                                      SETOF(53)
     A                                      SETOF(54)
     A                                      SETOF(55)
     A                                      RTNCSRLOC(&RCD &FLD)
     A                                      OVERLAY
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 26'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  2 15'VIEW COST CENTERS ASSIGNED TO MULT-
     A                                      IPLE FARMS'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  4 19'?'
     A                                      DSPATR(HI)
     A                                  4 24'?'
     A                                      DSPATR(HI)
     A                                  4 56'?'
     A                                      DSPATR(HI)
     A                                  4 69'?'
     A                                      DSPATR(HI)
     A                                  4 76'?'
     A                                      DSPATR(HI)
     A                                  5 23'A/I'
     A                                      DSPATR(HI)
     A                                  5 54'Farm'
     A                                      DSPATR(HI)
     A                                  5 67'Prod'
     A                                      DSPATR(HI)
     A                                  5 74'Prod'
     A                                      DSPATR(HI)
     A                                  6  3'Cost Center'
     A                                      DSPATR(HI)
     A                                  6 17'Farm'
     A                                      DSPATR(HI)
     A                                  6 24'St'
     A                                      DSPATR(HI)
     A                                  6 54'Type'
     A                                      DSPATR(HI)
     A                                  6 61'Cell'
     A                                      DSPATR(HI)
     A                                  6 67'Phase'
     A                                      DSPATR(HI)
     A                                  6 74'Type'
     A                                      DSPATR(HI)
     A            C1CJD     R        B  7  2REFFLD(HSRREF/@@CJD *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A                                      CHECK(RB)
     A            C1FSCD    R        B  7 16REFFLD(@@FSCD)
     A                                      EDTCDE(M)
     A                                      DSPATR(HI)
     A  53                                  DSPATR(PC)
     A            C1AIST    R        B  7 24REFFLD(HSRREF/@@AIST *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A  51                                  DSPATR(PC)
     A                                  7 27'Name'
     A                                      DSPATR(HI)
     A            C1TFCD    R        B  7 54REFFLD(HSRREF/@@TTBCD *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A  52                                  DSPATR(PC)
     A            C1CELL    R        B  7 62REFFLD(HSRREF/@@CELL *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A            C1PPCD    R        B  7 67REFFLD(HSRREF/@@BOCD *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A  54                                  DSPATR(PC)
     A            C1PTCD    R        B  7 74REFFLD(HSRREF/@@PTCD *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A  55                                  DSPATR(PC)
     A*
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R HP756K1
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A                                 23 11'F4=Prompt'
     A                                 23 22'F5=Refresh'
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *---------------------------------------------------------------
     A          R HP756ES                   SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE CONTROL FORMAT
      *---------------------------------------------------------------
     A          R HP756EC                   SFLCTL(HP756ES)
      *
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
