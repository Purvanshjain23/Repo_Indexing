/***************************************************************** */
/*                                                                 */
/*  PROGRAM NUMBER --> HP442CL                                     */
/*  PROGRAM NAME ----> FACILITY UTILIZATION REPORT                 */
/*  PROGRAMMER ------> LEANNE FEDOR                                */
/*  DATE ------------> 07/12/96                                    */
/*                                                                 */
/*  PROGRAM PURPOSE -> THIS CL CALLS THE PROGRAMS TO BUILD         */
/*                     WORKFILES AND PRINT REPORT                  */
/*                                                                 */
/***************************************************************** */
/*  MODIFIED:                                                      */
/***************************************************************** */
/*                                                                 */
/* 06/03/97 LEANNE FEDOR                                           */
/*          RECOMPILE ONLY.  NEW FIELDS 'PURGE FLAG' AND 'DATE OF  */
/*          FIRST PURGE' ADDED TO HOG GROUP FILE.                  */
/***************************************************************** */

             PGM

/* DEFINE THE VARIABLE REQUIRED IN THE COMMAND TO OVERRIDE THE     */
/* PRINT FILE TO A LASER PRINTER                                   */

             DCL        VAR(&CMD) TYPE(*CHAR) LEN(512)

             DCL        VAR(&QDATA) TYPE(*CHAR) LEN(350)
             DCL        VAR(&LDFSBO) TYPE(*CHAR) LEN(5)
             DCL        VAR(&LDFYMD) TYPE(*DEC) LEN(8)
             DCL        VAR(&LDTYMD) TYPE(*DEC) LEN(8)
             DCL        VAR(&LDPTCD) TYPE(*CHAR) LEN(5)
             DCL        VAR(&LDPPCD) TYPE(*CHAR) LEN(5)
             DCL        VAR(&LDFSCD) TYPE(*DEC) LEN(5 0)
             DCL        VAR(&LDGTCD) TYPE(*CHAR) LEN(1)

             DCL        VAR(&HOLD) TYPE(*CHAR) LEN(10)
             DCL        VAR(&COPYS) TYPE(*DEC) LEN(2)
             DCL        VAR(&OUTQ) TYPE(*CHAR) LEN(10)

             CHGVAR     VAR(&LDFSBO) VALUE(%SST(*LDA 1 5))
             CHGVAR     VAR(&LDTYMD) VALUE(%SST(*LDA 6 8))
             CHGVAR     VAR(&LDFYMD) VALUE(%SST(*LDA 14 8))
             CHGVAR     VAR(&LDPTCD) VALUE(%SST(*LDA 22 5))
             CHGVAR     VAR(&LDPPCD) VALUE(%SST(*LDA 27 5))
             CHGVAR     VAR(&LDFSCD) VALUE(%SST(*LDA 32 5))
             CHGVAR     VAR(&LDGTCD) VALUE(%SST(*LDA 53 1))

/*  CREATE WORKFILES USING CUSTOMIZED SEABOARD COMMAND            */

             CRTDUPOBJL OBJ(HSP341) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)

             CRTDUPOBJL OBJ(HSP342) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)

             CRTDUPOBJL OBJ(HSP343) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)


/*---------------------------------------------------------------*/
/*  SETUP QUERY SELECTION                                        */
/*---------------------------------------------------------------*/

/*  ALWAYS OMIT "BGF" PRODUCTION PHASE GROUPS                    */

             CHGVAR     VAR(%SST(&QDATA 1 18)) VALUE('HGPPCD +
                          *NE "BGF  "')

/*  ALWAYS OMIT HOG GROUPS WITH A STATUS OF VOIDED               */

             CHGVAR     VAR(%SST(&QDATA 20 20)) VALUE('*AND HGGSCD +
                          *NE "VD"')

/*  IF PRODUCTION PHASE IS NOT BLANK                             */

             IF         COND(&LDPPCD *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 44 17)) VALUE('*AND HGPPCD +
                          *EQ "')
             CHGVAR     VAR(%SST(&QDATA 61 5)) VALUE(&LDPPCD)
             CHGVAR     VAR(%SST(&QDATA 66 1)) VALUE('"')
             ENDDO

/*  IF FARM SITE                                                 */

             IF         COND(&LDFSCD *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 68 15)) VALUE('*AND HGFSCD +
                          *EQ')
             CHGVAR     VAR(%SST(&QDATA 84 5)) VALUE(&LDFSCD)
             ENDDO


/*  HOG GROUP OPENED DATE CANNOT BE ZERO AND THE OPEN DATE MUST     */
/*  FALL ON OR BEFORE THE CUT-OFF DATE                              */

             CHGVAR     VAR(%SST(&QDATA 116 15)) VALUE('*AND HGOPDT +
                          *GT')
             CHGVAR     VAR(%SST(&QDATA 132 8)) VALUE(00000000)
             CHGVAR     VAR(%SST(&QDATA 141 15)) VALUE('*AND HGOPDT +
                          *LE')
             CHGVAR     VAR(%SST(&QDATA 157 8)) VALUE(&LDTYMD)



/*  DISPOSE DATE MUST BE ZERO OR FALL ON OR AFTER THE FROM DATE       */

             CHGVAR     VAR(%SST(&QDATA 166 16)) VALUE('*AND (HGDSDT +
                          *GE')
             CHGVAR     VAR(%SST(&QDATA 183 8)) VALUE(&LDFYMD)
             CHGVAR     VAR(%SST(&QDATA 192 14)) VALUE('*OR HGDSDT +
                          *EQ')
             CHGVAR     VAR(%SST(&QDATA 207 1)) VALUE(0)
             CHGVAR     VAR(%SST(&QDATA 208 1)) VALUE(')')



/*  IF BUSINESS OFFICE                                           */

             IF         COND(&LDFSBO *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 254 17)) VALUE('*AND HGFSBO +
                          *EQ "')
             CHGVAR     VAR(%SST(&QDATA 271 5)) VALUE(&LDFSBO)
             CHGVAR     VAR(%SST(&QDATA 276 1)) VALUE('"')
             ENDDO

/*  IF PRODUCTION TYPE                                           */

             IF         COND(&LDPTCD *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 278 17)) VALUE('*AND HGPTCD +
                          *EQ "')
             CHGVAR     VAR(%SST(&QDATA 295 5)) VALUE(&LDPTCD)
             CHGVAR     VAR(%SST(&QDATA 300 1)) VALUE('"')
             ENDDO


/*  IF GROUP TYPE                                               */

             IF         COND(&LDGTCD *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 302 17)) VALUE('*AND HGGTCD +
                          *EQ "')
             CHGVAR     VAR(%SST(&QDATA 319 1)) VALUE(&LDGTCD)
             CHGVAR     VAR(%SST(&QDATA 320 1)) VALUE('"')
             ENDDO

/*---------------------------------------------------------------*/
/*  RUN OPEN QUERY AND BUILD WORKFILES                           */
/*---------------------------------------------------------------*/

             OVRDBF     FILE(HSP034) SHARE(*YES)
             OPNQRYF    FILE((HSP034)) QRYSLT(&QDATA) KEYFLD(*FILE) +
                          SEQONLY(*YES)

/* CALL THE PROGRAM TO BUILD THE WORKFILES                      */

             CALL       PGM(HP342)

/*---------------------------------------------------------------*/
/* SET PRINT PARAMETERS FROM LDA                                 */
/*---------------------------------------------------------------*/

 HOLD:       IF         COND(%SST(*LDA 411 1) = 'Y') THEN(CHGVAR +
                          VAR(&HOLD) VALUE(*YES))
             ELSE       CMD(CHGVAR VAR(&HOLD) VALUE(*NO))

COPYS:       CHGVAR     VAR(&COPYS) VALUE(%SST(*LDA 412 1))
             IF         COND(&COPYS = 0) THEN(CHGVAR VAR(&COPYS) +
                          VALUE(1))

 OUTQ:       CHGVAR     VAR(&OUTQ) VALUE(%SST(*LDA 413 10))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(%SST(*LDA 401 10)))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(*JOB))

/*------------------------------------------------------------------*/
/* OVERRIDE PRINT FILE                                              */
/*------------------------------------------------------------------*/

/* USE THE CUSTOMIZED SEABOARD COMMAND TO OVERRIDE THE PRINT FILE.  */
/* THIS COMMAND ALLOWS THE REPORT TO BE GENERATED ON A STANDARD LINE*/
/* PRINTER OR A LASER PRINTER. TO USE THIS OVERRIDE YOU MUST DECLARE*/
/* A COMMAND VARIABLE AND FOLLOW THE OVERRIDE WITH A CALL TO        */
/* QCMDEXC.                                                         */

/* THIS REPORT PRINTS AT 15 CPI IN 198 CHARACTERS                   */

             SBDPRTOVR  CMDVAR(&CMD) FILE(QPRINT) OUTQ(&OUTQ) +
                          WIDTH(198) LANDSCAPE(*YES) DRAWER(2) +
                          COPIES(&COPYS) HOLD(&HOLD)

             CALL       PGM(QCMDEXC) PARM(&CMD 512)

/*-------------------------------------------------------------------*/
/* PRINT REPORT                                                     */
/*-------------------------------------------------------------------*/

             CALL       PGM(HP642)

             CLOF       OPNID(HSP034)
             DLTOVR     FILE(*ALL)

             ENDPGM

