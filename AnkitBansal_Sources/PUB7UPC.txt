/*                                                                 */
/* PUB7UPC - CREATE 961 CALENDAR                                   */
/*                                                                 */
/* The CLP works with CAAFREP and CAATREP only                     */
/*                                                                 */
/* This CL should only be ran after the OMS calendar files have    */
/* been updated for Company 440 in PRKFLIB.                        */
/*                                                                 */
/* If you get the files created for 1 Library you can use this     */
/*  CLP to get them copied into the other libraries.               */
/*                                                                 */
/* This CLP assumes you have created the new year in PRKFLIB for   */
/* company 440 only and you have verified all dates/periods/weeks  */
/* for accounting.                                                 */
/*                                                                 */
/* This CLP will copy from PRKFLIB to ISJHIGHLIB, for the year     */
/* specified.  After updating the 961 file in ISJHIGHLIB, it will  */
/* add the 961 to the specified library.                           */
/*                                                                 */
/* --------------------------------------------------------------- */
/* Created  by:  Julius High     4/20/17   TFS 10148               */
/*******************************************************************/

             PGM        PARM(&YEAR &UPDLIB)

             DCL        VAR(&YEAR) TYPE(*CHAR) LEN(4)
             DCL        VAR(&UPDLIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&ERR)    TYPE(*CHAR) LEN(1) VALUE('N')

/*  Delete files in ISJHIGHLIB                                     */

             DLTF       FILE(ISJHIGHLIB/CAAFREP)
             MONMSG     MSGID(CPF0000)
             DLTF       FILE(ISJHIGHLIB/CAATREP)
             MONMSG     MSGID(CPF0000)

/*  Copy company 440 records that were created in PRKFLIB for the  */
/*  &YEAR into ISJHIGHLIB.                                         */

             CPYF       FROMFILE(PRKFLIB/CAAFREP) +
                          TOFILE(ISJHIGHLIB/CAAFREP) +
                          MBROPT(*REPLACE) CRTFILE(*YES) +
                          INCREL((*IF AFAIC3 *EQ 440) (*AND AFESNB +
                          *EQ &YEAR))

             MONMSG     MSGID(CPF0000) EXEC(CHGVAR VAR(&ERR) +
                          VALUE('Y'))

             CPYF       FROMFILE(PRKFLIB/CAATREP) +
                          TOFILE(ISJHIGHLIB/CAATREP) +
                          MBROPT(*REPLACE) CRTFILE(*YES) +
                          INCREL((*IF ATAIC3 *EQ 440) (*AND ATESNB +
                          *EQ &YEAR))

             MONMSG     MSGID(CPF0000) EXEC(CHGVAR VAR(&ERR) +
                          VALUE('Y'))

             IF         COND(&ERR = 'N') THEN(DO)
/*-----------------------------------------------------------------*/

/*  The update sequel statement is going to change the Company     */
/*  in ISJHIGHLIB to company 961 for CAAFREP and CAATREO.          */
/*  The "Production Day Status" in CAAFREP is set to "N" for all   */
/*  days.                                                          */

             UPDATE     SET((AFAIC3 961)) SQL('FROM +
                          ISJHIGHLIB/CAAFREP')
             UPDATE     SET((AFWAST '"N"')) SQL('FROM +
                          ISJHIGHLIB/CAAFREP')

             UPDATE     SET((ATAIC3 961)) SQL('FROM +
                          ISJHIGHLIB/CAATREP')

/*  Update the &UPDLIB with the 961 &YEAR                          */

             CPYF       FROMFILE(ISJHIGHLIB/CAAFREP) +
                          TOFILE(&UPDLIB/CAAFREP) MBROPT(*ADD) +
                          CRTFILE(*NO)

             CPYF       FROMFILE(ISJHIGHLIB/CAATREP) +
                          TOFILE(&UPDLIB/CAATREP) MBROPT(*ADD) +
                          CRTFILE(*NO)
             ENDDO

             IF         COND(&ERR = 'Y') THEN(SNDPGRMSG TOPGR(ISJHIGH) +
                          MSG('NEW YEAR DID NOT GET CREATED.') +
                          CONFMSG(*NO))

             IF         COND(&ERR = 'N') THEN(SNDPGRMSG TOPGR(ISJHIGH) +
                          MSG('NEW YEAR CREATED IN ALL LIBRARIES.') +
                          CONFMSG(*NO))

             ENDPGM

