      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Hog Production System
      * PROGRAM:     HP8002 - Roll-up BGF Daily Production and Update Weekly
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     12/20/01
      *
      * Function:    As of December 2001, we have two BGF Production functions.
      *
      *              The user enters 'daily' production values thru the Daily function.
      *              These values are stored in the Daily file.
      *
      *              The user enters 'weekly' production values thru the Weekly function.
      *              The 'weekly' record holds these values as well as the sum of the values
      *              from the daily file.
      *
      *              This program sums/rolls up the Daily records and updates the Weekly record
      *
      ****************************************************************************************
      * MODIFICATIONS:
      ****************************************************************************************
      * DATE      PROGRAMMER
      *
      * 09/17/02  LeAnne Fedor
      *           'Ending Piglet Inventory' is now keyed on the weekly record. "Beginning piglet
      *            Inventory" is no longer keyed in anywhere.
      *
      * 10/22/03  LeAnne Fedor
      *           Added fields:
      *               gilt total stillborns
      *               gilt total mummies
      *               17+ day weaned sows
      *               euthanized gilts
      *               euthanized sows
      *
      * 02/03/05  LeAnne Fedor
      *           New field "Transferred In Open Gilts" added
      *           to BGF Daily and Weekly Production files.
      *
      * 08/23/05  LeAnne Fedor
      *           New field "Man Hours" added to BGF Daily and Weekly Production files
      *
      * 06/15/06  LeAnne Ramsey
      *           Recompile only. New field "Avg Gilt Weight @ 28 Weeks" added
      *           to BGF Weekly Production file.
      *
      * 10/19/07  LeAnne Ramsey
      *           The users have changed "manhours" from total hours to "average" hours.
      *           So, the rollup from daily to weekly now needs to calc a weekly "average".
      *
      * 08/04/08  LeAnne Ramsey
      *           Recompile only. Fields were deleted from both the Daily and
      *           Weekly Production files.
      *
      * 09/19/07  LeAnne Ramsey
      *           The users have changed "manhours" back to total hours from "average" hours.
      *           So, the rollup from daily to weekly no longer needs to calc a weekly
      *           "average".
      * 03-04-25  Task: SR3026630 || Tag: S326630 || Developer: Santosh Patil
      *           Update fields bpupdt and bpupusr with "System Date" and "Current User"
      *           respectively in HSP092.
      /eject
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
      *
     Fhsl082b   if   e           k disk
      *    BGF daily production
      *
      *
     Fhsl092b   uf a e           k disk
      *    BGF weekly production
      *
      /eject
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Control fields
      *
     D count           s              1  0
     D countavg        s              1  0
     D wkdays          s              5  1
      *S326630 Changes Start
     D l_curdate       S               D
      *S326630 Changes End
      *
      * Parm fields
      *
     D xxfscd          s                   like(bdfscd)
     D xxwedt          s                   like(bdwedt)
      *
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * Mainline
      ****************************************************************
      *
      * Retrieve the Weekly Production record.
      *
     C     key01         chain     hsl092b                            92
     C                   if        *in92 = *off                                 If found weekly
     C                   exsr      $clear
      *
      * Process all 7 Daily Production records summing the values for the
      * Weekly Production record.
      *
      *
     C     key01         setll     hsl082b
      *
     C                   dou       *in91 = *on                                  Do daily
     C     key01         reade     hsl082b                                91
     C                   if        *in91 = *off                                 If not EOF
      *
     C                   add       1             count
      *
      * If this daily record had a value for 'Average Age of Piglet Weaned'
      * then increment your counter.
      *
     C                   if        bdadwn <> 0
     C                   add       1             countavg
     C                   endif
      *
      *
      * Do not 'roll-up' the following fields:
      * 1) open gilt pool eligible  (use record 7's value)
      * 2) open sow pool            (use record 7's value)
      *
     C                   select
     C                   when      count = 7
     C                   z-add     bdopglhd      bpopglhd
     C                   z-add     bdopsohd      bpopsohd
     C                   endsl
      * Services
     C                   add       bdwnsv        bpwnsv
     C                   add       bdlwnsv       bplwnsv
     C                   add       bdrtsv        bprtsv
     C                   add       bdglsv        bpglsv
     C                   add       bdabhd        bpabhd
     C                   add       bdglabhd      bpglabhd
     C                   add       bdnipqt       bpnipqt
      *
      * Farrowing
     C                   add       bdgllt        bpgllt
     C                   add       bdglbahd      bpglbahd
     C                   add       bdglsbhd      bpglsbhd
     C                   add       bdglmmhd      bpglmmhd
     C                   add       bdtolt        bptolt
     C                   add       bdbahd        bpbahd
     C                   add       bdsbhd        bpsbhd
     C                   add       bdmmhd        bpmmhd
      *
      * Piglet losses
     C                   add       bdtolshd      bptolshd
      *
      * Weaning
     C                   add       bdewnlt       bpewnlt
     C                   add       bdwnlt        bpwnlt
     C                   add       bdtownhd      bptownhd
     C                   add       bdadwn        wkdays
      *
      * Inventory
     C                   add       bdarglhd      bparglhd
     C                   add       bdibglhd      bpibglhd
     C                   add       bdslglhd      bpslglhd
     C                   add       bdddglhd      bpddglhd
     C                   add       bdeuglhd      bpeuglhd
     C                   add       bdobglhd      bpobglhd
      *
     C                   add       bdarsohd      bparsohd
     C                   add       bdibsohd      bpibsohd
     C                   add       bdslsohd      bpslsohd
     C                   add       bdddsohd      bpddsohd
     C                   add       bdeusohd      bpeusohd
     C                   add       bdobsohd      bpobsohd
     C                   add       bdoosohd      bpoosohd
      *
     C                   add       bdarbohd      bparbohd
     C                   add       bdslbohd      bpslbohd
     C                   add       bdddbohd      bpddbohd
      *
     C                   add       bdmanhr       bpmanhr
     C                   endif                                                  If not EOF
     C                   enddo                                                  Do daily
      *
      * Calculate 'Average Age of Piglet Weaned' as:
      *  Accumulated daily averages / the number of days with age entries
      *

      *S326630 Changes Starts
      /free
            l_curdate = %date();
            bpupdt  = %dec(l_curdate);
            bpupusr = sdusr   ;
      /end-free
      *S326630 Changes End

     C                   if        countavg <> 0
     C     wkdays        div(h)    countavg      bpadwn
     C                   endif
      *
     C                   update    bprec
     C                   endif                                                  If found weekly
      *
     C                   seton                                        lr
      /eject
      *---------------------------------------------------------------
      * Clear accumulated daily fields
      *---------------------------------------------------------------
      *
     C     $clear        begsr
      *
     C                   z-add     0             countavg
     C                   z-add     0             wkdays
      * Services
     C                   z-add     0             bpwnsv
     C                   z-add     0             bplwnsv
     C                   z-add     0             bprtsv
     C                   z-add     0             bpglsv
     C                   z-add     0             bpopglhd
     C                   z-add     0             bpopsohd
     C                   z-add     0             bpabhd
     C                   z-add     0             bpglabhd
     C                   z-add     0             bpnipqt
      *
      * Farrowing
     C                   z-add     0             bpgllt
     C                   z-add     0             bpglbahd
     C                   z-add     0             bpglsbhd
     C                   z-add     0             bpglmmhd
     C                   z-add     0             bptolt
     C                   z-add     0             bpbahd
     C                   z-add     0             bpsbhd
     C                   z-add     0             bpmmhd
      *
      * Piglet losses
     C                   z-add     0             bptolshd
      *
      * Weaning
     C                   z-add     0             bpewnlt
     C                   z-add     0             bpwnlt
     C                   z-add     0             bptownhd
     C                   z-add     0             bpadwn
      *
      * Inventory
     C                   z-add     0             bparglhd
     C                   z-add     0             bpibglhd
     C                   z-add     0             bpslglhd
     C                   z-add     0             bpddglhd
     C                   z-add     0             bpeuglhd
     C                   z-add     0             bpobglhd
      *
     C                   z-add     0             bparsohd
     C                   z-add     0             bpibsohd
     C                   z-add     0             bpslsohd
     C                   z-add     0             bpddsohd
     C                   z-add     0             bpeusohd
     C                   z-add     0             bpobsohd
     C                   z-add     0             bpoosohd
      *
     C                   z-add     0             bparbohd
     C                   z-add     0             bpslbohd
     C                   z-add     0             bpddbohd
      * Manhours
     C                   z-add     0             bpmanhr
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
     C     *entry        plist
     C                   parm                    xxfscd
     C                   parm                    xxwedt
      *
      *
     C     key01         klist
     C                   kfld                    xxfscd
     C                   kfld                    xxwedt
      *
     C                   endsr
      /eject
