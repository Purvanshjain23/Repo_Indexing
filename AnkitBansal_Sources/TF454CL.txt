/********************************************************************* */
/*                                                                     */
/*  SYSTEM:          TRIUMPH FOODS                                     */
/*  PROGRAM NUMBER:  TF454CL                                           */
/*  PROGRAM NAME:    SPECIFY SALES TIMING ALLOCATION DETAIL REPORT/DWL */
/*  PROGRAMMER:      LEANNE RAMSEY                                     */
/*  CREATE DATE:     11/12/10                                          */
/*                                                                     */
/*  FUNCTION:        DAMON GINTHER REQUESTED THIS FUNCTION SO THAT HE  */
/*                   CAN 'PROVE' THE DOLLARS FOR THE WEEK THAT THE     */
/*                   USERS SEE ON TF651-SALES TIMING ALLOCATION LISTING*/
/*                                                                     */
/*                   NOTE: THE 'DETAIL' IS AT THE ITEM LEVEL.          */
/*                                                                     */
/***********************************************************************/
/* MODIFICATIONS:                                                      */
/*                                                                     */
/* 11/22/10  LeAnne Ramsey (#1173)                                     */
/*           Tom Dye now wants the Report and the DWL generated during */
/*           the Preliminary run of the Weekly Inventory Close.        */
/***********************************************************************/

             PGM

             DCL        VAR(&OUTQ) TYPE(*CHAR) LEN(10)
             DCL        VAR(&HOLD) TYPE(*CHAR) LEN(4)
             DCL        VAR(&SAVE) TYPE(*CHAR) LEN(4)
             DCL        VAR(&COPY) TYPE(*DEC)  LEN(1)

             DCL        VAR(&LDPFCD)  TYPE(*CHAR) LEN(1)
             DCL        VAR(&LDWEDT)  TYPE(*DEC)  LEN(8)
             DCL        VAR(&LDRDFL)  TYPE(*CHAR) LEN(1)
             DCL        VAR(&LDEMAIL) TYPE(*CHAR) LEN(50)
             DCL        VAR(&EMAIL)   TYPE(*CHAR) LEN(50)
             DCL        VAR(&MYMSG)   TYPE(*CHAR) LEN(50)
             DCL        VAR(&TSTPRDFL) TYPE(*CHAR) LEN(1)

             CHGVAR     VAR(&LDPFCD)    VALUE(%SST(*LDA 1 1))
             CHGVAR     VAR(&LDWEDT)    VALUE(%SST(*LDA 29 8))
             CHGVAR     VAR(&LDRDFL)    VALUE(%SST(*LDA 300 1))
             CHGVAR     VAR(&LDEMAIL)   VALUE(%SST(*LDA 350 50))


/*----------------------------------------------------------------*/
/* SET UP EMAIL ADDRESS FOR THE ESEND OF THE DWL FILE             */
/*----------------------------------------------------------------*/

 /* On-Demand */

             IF         COND(&LDPFCD *EQ 'D')
             CHGVAR     VAR(&EMAIL) VALUE(&LDEMAIL)

 /* From Inventory Close Preliminary Run */

             IF         COND(&LDPFCD *EQ 'P') THEN(DO)

             CHGVAR     VAR(&MYMSG) VALUE('Distribution List: TF +
                          Inventory Close-Preliminary')

       /* Default EMail recipient to "TF TEST". Then, if you are in the */
       /* Production Environment, reset to the Distribution List.       */

             CHGVAR     VAR(&EMAIL) VALUE('TF Test')
             RTVDTAARA  DTAARA(DATSTPRD (1 1)) RTNVAR(&TSTPRDFL)
             IF         COND(&TSTPRDFL *EQ 'P')
             CHGVAR     VAR(&EMAIL) VALUE('TF Inventory +
                          Close-Preliminary')
             ENDDO

/*----------------------------------------------------------------*/
/* CREATE A WORKFILE OF DATA                                      */
/*----------------------------------------------------------------*/

             CRTDUPOBJ  OBJ(TFP354) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)
             CALL       PGM(TF354)


/*----------------------------------------------------------------*/
/* WHEN YOU ARE DOWNLOADING THE DATA, EMAIL A SPREADSHEET         */
/*----------------------------------------------------------------*/

             IF         COND(&LDRDFL *EQ 'D' *OR &LDRDFL *EQ 'B') +
                          THEN(DO)

             EXECUTE    SQL('SELECT * FROM TFP354') NBRRCDS(*ALL) +
                          PCFMT(*XLS) RECIPIENT(&EMAIL) +
                          EMLMSG(&MYMSG) TEXT('Sales Timing +
                          Allocation Detail Download')
             MONMSG     MSGID(CPF0000)

             ENDDO

/*---------------------------------------------------------------------*/
/* WHEN THE USER SELECTED THE "REPORT", PRINT THE REPORT               */
/*---------------------------------------------------------------------*/

             IF         COND(&LDRDFL *EQ 'R' *OR &LDRDFL *EQ 'B') +
                          THEN(DO)

/* SET PARAMETERS FOR LISTINGS                                    */

 OUTQ:       CHGVAR     VAR(&OUTQ) VALUE(%SST(*LDA 401 10))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(*JOB))

 HOLD:       CHGVAR     VAR(&HOLD) VALUE(%SST(*LDA 411 4))
             IF         COND(&HOLD *NE '*YES' *AND &HOLD *NE '*NO ') +
                          THEN(CHGVAR VAR(&HOLD) VALUE(*NO))

 COPY:       CHGVAR     VAR(&COPY) VALUE(%SST(*LDA 419 1))
             IF         COND(&COPY = 0) THEN(CHGVAR VAR(&COPY) +
                          VALUE(1))

 SAVE:       CHGVAR     VAR(&SAVE) VALUE(%SST(*LDA 415 4))
             IF         COND(&SAVE *NE '*YES' *AND &SAVE *NE '*NO ') +
                          THEN(CHGVAR VAR(&SAVE) VALUE(*NO))

             OVRPRTF    FILE(PRINT198) OUTQ(&OUTQ) COPIES(&COPY) +
                          HOLD(&HOLD) SAVE(&SAVE)

             CALL       PGM(TF654)
             ENDDO


             ENDPGM

