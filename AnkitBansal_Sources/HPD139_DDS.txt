     A*%%TS  SD  20111219  090305  ISLRAMS     REL-V6R1M0  5761-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION
     A*  DISPLAY FILE:  HPD139
     A*  TITLE:         MAINTAIN TRANSFER
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  DATE:          04/10/95
     A*
     A*----------------------------------------------------------------
     A*  INDICATORS:
     A*
     A*  60  REVISE MODE
     A*  61  CREATE MODE
     A*  63  PROTECT ORIGIN HEADER FIELDS--AN ORIGIN GROUP IS CLOSED.
     A*  64  PROTECT ORIGIN GROUP SUBFILE RECORD FIELDS--THE GROUP IS
     A*      CLOSED.
     A*  65  PROTECT DESTINATION HEADER FIELDS--A DESTINATION GROUP IS
     A*      CLOSED
     A*  66  PROTECT DESTINATION GROUP SUBFILE RECORD FIELDS--THE
     A*      GROUP IS CLOSED.
     A*  90  THE TRANSFER THE USER HAS SELECTED FROM THE WORK WITH HAS
     A*      BEEN DELETED BY ANOTHER USER SINCE SELECTED
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A N90                                  CF04(04 'PROMPT')
     A N90                                  CF05(05 'REFRESH')
     A N90                                  CF09(09 'ACCEPT')
     A N90N63N65
     A  60                                  CF11(11 'DELETE')
     A                                      CA12(12 'PREVIOUS')
     A                                      CF13(13 'FOLD/DROP')
     A*---------------------------------------------------------------
     A* SUBFILE FORMAT 1
     A*---------------------------------------------------------------
     A*
     A          R HP139S1                   SFL
     A*%%TS  SD  20111215  091510  ISLRAMS     REL-V6R1M0  5761-WDS
     A  85                                  SFLNXTCHG
     A            HDRRN1         4Y 0H
     A            HDLNNO    R        H      REFFLD(MDLNNO)
     A            HDORHGSN  R        H      REFFLD(HGHGSN)
     A            HDSHLB    R        H      REFFLD(MDSHLB)
     A            HDDNHGSN  R        H      REFFLD(HGHGSN)
     A            HDDNOLDHG R        H      REFFLD(HGHGCD)
     A            HDQLLB    R        H      REFFLD(MDQLLB)
     A            HDDOLB    R        H      REFFLD(MDDOLB)
     A            DFORBLCD  R        B 18  2REFFLD(HSRREF/@@BLCD *LIBL/HSPREF)
     A  45
     AO 55                                  DSPATR(PC)
     A  45                                  DSPATR(RI)
     A  90
     AO 64                                  DSPATR(PR)
     A N45N64N90
     A  42                                  DSPATR(HI)
     A N45N64N90
     A  42                                  DSPATR(UL)
     A            DFORRMCD  R        B 18  8REFFLD(HSRREF/@@RMCD *LIBL/HSPREF)
     A  47
     AO 57                                  DSPATR(PC)
     A  47                                  DSPATR(RI)
     A  90
     AO 64                                  DSPATR(PR)
     A N47N64N90
     A  42                                  DSPATR(HI)
     A N47N64N90
     A  42                                  DSPATR(UL)
     A            DFORHGCD  R        B 18 14REFFLD(HGHGCD)
     A  44
     AO 54                                  DSPATR(PC)
     A  44                                  DSPATR(RI)
     A  90
     AO 64                                  DSPATR(PR)
     A N44N64N90
     A  42                                  DSPATR(HI)
     A N44N64N90
     A  42                                  DSPATR(UL)
     A            DFSCHD         4Y 0B 18 22EDTCDE(M)
     A  74                                  DSPATR(PC)
     A  74                                  DSPATR(RI)
     A  90
     AO 64                                  DSPATR(PR)
     A N74N64N90
     A  42                                  DSPATR(HI)
     A N74N64N90
     A  42                                  DSPATR(UL)
     A            DFSHHD         4Y 0B 18 28EDTCDE(M)
     A  71                                  DSPATR(PC)
     A  71                                  DSPATR(RI)
     A  90
     AO 64                                  DSPATR(PR)
     A N71N64N90
     A  42                                  DSPATR(HI)
     A N71N64N90
     A  42                                  DSPATR(UL)
     A            DFDNBLCD  R        B 18 34REFFLD(HSRREF/@@BLCD *LIBL/HSPREF)
     A  75
     AO 50                                  DSPATR(PC)
     A  75                                  DSPATR(RI)
     A  90
     AO 66                                  DSPATR(PR)
     A N75N66N90
     A  42                                  DSPATR(HI)
     A N75N66N90
     A  42                                  DSPATR(UL)
     A            DFDNRMCD  R        B 18 40REFFLD(HSRREF/@@RMCD *LIBL/HSPREF)
     A  77
     AO 59                                  DSPATR(PC)
     A  77                                  DSPATR(RI)
     A  90
     AO 66                                  DSPATR(PR)
     A N77N66N90
     A  42                                  DSPATR(HI)
     A N77N66N90
     A  42                                  DSPATR(UL)
     A            DFDNHGCD  R     A  B 18 46REFFLD(HGHGCD)
     A  46
     AO 56                                  DSPATR(PC)
     A  46                                  DSPATR(RI)
     A  90
     AO 66                                  DSPATR(PR)
     A N46N66N90
     A  42                                  DSPATR(HI)
     A N46N66N90
     A  42                                  DSPATR(UL)
     A            DFQLHD         4Y 0B 18 54EDTCDE(M)
     A  48                                  DSPATR(PC)
     A  48                                  DSPATR(RI)
     A  90
     AO 66                                  DSPATR(PR)
     A N48N66N90
     A  42                                  DSPATR(HI)
     A N48N66N90
     A  42                                  DSPATR(UL)
     A            DFDOAHD        4Y 0B 18 60EDTCDE(M)
     A  70                                  DSPATR(PC)
     A  70                                  DSPATR(RI)
     A  90
     AO 66                                  DSPATR(PR)
     A N70N66N90
     A  42                                  DSPATR(HI)
     A N70N66N90
     A  42                                  DSPATR(UL)
     A            DFKOAHD        4Y 0B 18 66EDTCDE(M)
     A  69                                  DSPATR(PC)
     A  69                                  DSPATR(RI)
     A  90
     AO 66                                  DSPATR(PR)
     A N69N66N90
     A  42                                  DSPATR(HI)
     A N69N66N90
     A  42                                  DSPATR(UL)
     A            DFSEXDS        7   O 18 73
     A            DFORPTCD  R        O 19  2REFFLD(HSRREF/@@PTCD *LIBL/HSPREF)
     A            DFORPPCD  R        O 19  8REFFLD(HSRREF/@@PPCD *LIBL/HSPREF)
     A            DFORGTCD  R        O 19 14REFFLD(HSRREF/@@GTCD *LIBL/HSPREF)
     A            DFORGSCD  R        O 19 16REFFLD(HGGSCD)
     A            DFORSDCD  R        O 19 19REFFLD(HSRREF/@@SDCD *LIBL/HSPREF)
     A            DFORSMCD  R        O 19 21REFFLD(HSRREF/@@SMCD *LIBL/HSPREF)
     A                                 19 23'PTyp/Phs/GTyp/Stat/StkDen/StayMove'
     A                                      COLOR(BLU)
     A            DFDNPTCD  R        O 19 58REFFLD(HSRREF/@@PTCD *LIBL/HSPREF)
     A            DFDNPPCD  R        O 19 64REFFLD(HSRREF/@@PPCD *LIBL/HSPREF)
     A            DFDNGTCD  R        O 19 70REFFLD(HSRREF/@@GTCD *LIBL/HSPREF)
     A            DFDNGSCD  R        O 19 73REFFLD(HGGSCD)
     A            DFDNSDCD  R        O 19 76REFFLD(HSRREF/@@SDCD *LIBL/HSPREF)
     A            DFDNSMCD  R        O 19 78REFFLD(HSRREF/@@SMCD *LIBL/HSPREF)
     A*----------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT  1
     A*----------------------------------------------------------------
     A*
     A          R HP139C1                   SFLCTL(HP139S1)
     A*%%TS  SD  20111219  090305  ISLRAMS     REL-V6R1M0  5761-WDS
     A                                      SFLSIZ(0100)
     A                                      SFLPAG(0002)
     A N90                                  ROLLUP(25 'PAGE FORWARD')
     A                                      SFLCSRRRN(&HDRR1)
     A                                      OVERLAY
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A  88                                  SFLFOLD(CF13)
     A  89                                  SFLDROP(CF13)
     A                                      SFLMODE(&FOLD)
     A                                      SETOF(31)
     A                                      SETOF(32)
     A                                      SETOF(33)
     A                                      SETOF(34)
     A                                      SETOF(35)
     A                                      SETOF(36)
     A                                      SETOF(37)
     A                                      SETOF(38)
     A                                      SETOF(39)
     A                                      SETOF(40)
     A                                      SETOF(41)
     A                                      SETOF(44)
     A                                      SETOF(45)
     A                                      SETOF(46)
     A                                      SETOF(47)
     A                                      SETOF(48)
     A                                      SETOF(50)
     A                                      SETOF(51)
     A                                      SETOF(52)
     A                                      SETOF(53)
     A                                      SETOF(54)
     A                                      SETOF(55)
     A                                      SETOF(56)
     A                                      SETOF(57)
     A                                      SETOF(58)
     A                                      SETOF(59)
     A                                      SETOF(68)
     A                                      SETOF(69)
     A                                      SETOF(70)
     A                                      SETOF(71)
     A                                      SETOF(72)
     A                                      SETOF(73)
     A                                      SETOF(74)
     A                                      SETOF(75)
     A                                      SETOF(76)
     A                                      SETOF(77)
     A                                      SETOF(78)
     A                                      SETOF(79)
     A                                      RTNCSRLOC(&RCD &FLD)
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            HDRR1          5S 0H
     A            FOLD           1A  H
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 26'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A            DFMODE    R        O  2  2REFFLD(@@MODE)
     A                                      DSPATR(HI)
     A                                  2 27'MAINTAIN TRANSFER'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  4  2'Movement number .....:'
     A            C1MVSN    R        O  4 27REFFLD(@@MVSN)
     A                                      EDTCDE(Z)
     A                                  4 41'Movement status .......'
     A            C1MSCD    R     A  B  4 67REFFLD(MHMSCD)
     A  78
     AO 58                                  DSPATR(PC)
     A  78                                  DSPATR(RI)
     A  63
     AO 65
     AO 90                                  DSPATR(PR)
     A N63N65N78
     A  42N90                               DSPATR(HI)
     A N63N65N78
     A  42N90                               DSPATR(UL)
     A                                  5  2'Scheduled ship date ..'
     A            C1MDY     R     S  B  5 27REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  ')
     A  76                                  DSPATR(PC)
     A  76                                  DSPATR(RI)
     A  63
     AO 90                                  DSPATR(PR)
     A N63N76N90
     A  42                                  DSPATR(HI)
     A N63N76N90
     A  42                                  DSPATR(UL)
     A                                  5 41'Trucker ...............'
     A N90                              5 65'?'
     A            C1TICD    R        B  5 67REFFLD(MHTICD)
     A  33
     AO 53                                  DSPATR(PC)
     A  33                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A  42N33N90                            DSPATR(HI)
     A  42N33N90                            DSPATR(UL)
     A                                  6  2'Shipped date .........'
     A            C1SMDY    R     S  B  6 27REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  ')
     A  38                                  DSPATR(PC)
     A  38                                  DSPATR(RI)
     A  63
     AO 90                                  DSPATR(PR)
     A N63N38N90
     A  42                                  DSPATR(HI)
     A N63N38N90
     A  42                                  DSPATR(UL)
     A                                      TEXT('received date')
     A                                  6 41'Shipped time in/out ...'
     A            C1SITM    R        B  6 67REFFLD(MHSITM)
     A                                      EDTWRD(' 0:  -')
     A  35                                  DSPATR(PC)
     A  35                                  DSPATR(RI)
     A  63
     AO 90                                  DSPATR(PR)
     A N63N35N90
     A  42                                  DSPATR(HI)
     A N63N35N90
     A  42                                  DSPATR(UL)
     A            C1SOTM    R        B  6 75REFFLD(MHSOTM)
     A                                      EDTWRD(' 0:  -')
     A  34                                  DSPATR(PC)
     A  34                                  DSPATR(RI)
     A  63
     AO 90                                  DSPATR(PR)
     A N63N34N90
     A  42                                  DSPATR(HI)
     A N63N34N90
     A  42                                  DSPATR(UL)
     A                                  7  2'Received date ........'
     A            C1RMDY    R     S  B  7 27REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  ')
     A  39                                  DSPATR(PC)
     A  39                                  DSPATR(RI)
     A  65
     AO 90                                  DSPATR(PR)
     A N65N39N90
     A  42                                  DSPATR(HI)
     A N65N39N90
     A  42                                  DSPATR(UL)
     A                                      TEXT('received date')
     A                                  7 41'Received time in/out ..'
     A            C1RITM    R        B  7 67REFFLD(MHRITM)
     A                                      EDTWRD(' 0:  -')
     A  40                                  DSPATR(PC)
     A  40                                  DSPATR(RI)
     A  65
     AO 90                                  DSPATR(PR)
     A N65N40N90
     A  42                                  DSPATR(HI)
     A N65N40N90
     A  42                                  DSPATR(UL)
     A            C1ROTM    R        B  7 75REFFLD(MHROTM)
     A                                      EDTWRD(' 0:  -')
     A  41                                  DSPATR(PC)
     A  41                                  DSPATR(RI)
     A  65
     AO 90                                  DSPATR(PR)
     A N65N41N90
     A  42                                  DSPATR(HI)
     A N65N41N90
     A  42                                  DSPATR(UL)
     A                                  8  2'Origin farm ..........'
     A N90N63                           8 25'?'
     A            C1ORFSCD  R        B  8 27REFFLD(FSFSCD)
     A  31
     AO 51                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A  63
     AO 90                                  DSPATR(PR)
     A N63N31N90
     A  42                                  DSPATR(HI)
     A N63N31N90
     A  42                                  DSPATR(UL)
     A                                  8 41'Destination farm ......'
     A N90N65                           8 65'?'
     A            C1DNFSCD  R        B  8 67REFFLD(FSFSCD)
     A  32
     AO 52                                  DSPATR(PC)
     A  32                                  DSPATR(RI)
     A  65
     AO 90                                  DSPATR(PR)
     A N65N32N90
     A  42                                  DSPATR(HI)
     A N65N32N90
     A  42                                  DSPATR(UL)
     A            C1ORFSNM  R        O  9  3REFFLD(FSFSNM)
     A            C1DNFSNM  R        O  9 55REFFLD(FSFSNM)
     A                                 10  3'Sched'
     A                                      COLOR(BLU)
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 10'      Shipped      '
     A                                      COLOR(BLU)
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 31'      Arrived      '
     A                                      COLOR(BLU)
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 52'   KOA/DOA   '
     A                                      COLOR(BLU)
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 66'   Received   '
     A                                      COLOR(BLU)
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 11  4'Head'
     A                                 11 11'Head'
     A                                 11 17'Pounds'
     A                                 11 26'Avg'
     A                                 11 32'Head'
     A                                 11 38'Pounds'
     A                                 11 47'Avg'
     A                                 11 53'Head'
     A                                 11 59'Pounds'
     A                                 11 68'Head'
     A                                 11 74'Pounds'
     A            C1SCHD    R        B 12  3REFFLD(MDSCHD)
     A                                      EDTCDE(M)
     A  79                                  DSPATR(PC)
     A  79                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A  42N79N90                            DSPATR(HI)
     A  42N79N90                            DSPATR(UL)
     A            C1SHHD    R        B 12 10REFFLD(MDSHHD)
     A                                      EDTCDE(M)
     A  36                                  DSPATR(PC)
     A  36                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A  42N36N90                            DSPATR(HI)
     A  42N36N90                            DSPATR(UL)
     A            C1SHLB    R        B 12 17REFFLD(MDSHLB)
     A                                      EDTCDE(K)
     A  37                                  DSPATR(PC)
     A  37                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A  42N37N90                            DSPATR(HI)
     A  42N37N90                            DSPATR(UL)
     A            C1AVSHLB  R        O 12 25REFFLD(HSRREF/@@NU40 *LIBL/HSPREF)
     A                                      EDTCDE(M)
     A            C1ARHD    R        B 12 31REFFLD(MDSHHD)
     A                                      EDTCDE(M)
     A  72                                  DSPATR(PC)
     A  72                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A  42N72N90                            DSPATR(HI)
     A  42N72N90                            DSPATR(UL)
     A            C1ARLB    R        B 12 38REFFLD(MDSHLB)
     A                                      EDTCDE(K)
     A  73                                  DSPATR(PC)
     A  73                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A  42N73N90                            DSPATR(HI)
     A  42N73N90                            DSPATR(UL)
     A            C1AVARLB  R        O 12 46REFFLD(HSRREF/@@NU40 *LIBL/HSPREF)
     A                                      EDTCDE(M)
     A  68                                  DSPATR(RI)
     A            C1DOHD    R        O 12 52REFFLD(MDDOHD)
     A                                      EDTCDE(M)
     A            C1DOLB    R        O 12 59REFFLD(MDDOLB)
     A                                      EDTCDE(K)
     A            C1RCHD    R        O 12 67REFFLD(MDSHHD)
     A                                      EDTCDE(M)
     A            C1RCLB    R        O 12 74REFFLD(MDSHLB)
     A                                      EDTCDE(K)
     A                                 14  2'*********** ORIGIN ***********'
     A                                      DSPATR(HI)
     A                                      COLOR(BLU)
     A                                 14 34'****************** DESTINATION ***-
     A                                      ************'
     A                                      DSPATR(HI)
     A                                      COLOR(BLU)
     A                                 15  4'?'
     A                                 15 10'?'
     A N90                             15 16'?'
     A                                 15 21'Sched'
     A                                 15 28'Ship'
     A                                 15 36'?'
     A                                 15 42'?'
     A N90                             15 48'?'
     A                                 15 54'Qual'
     A                                 15 61'DOA'
     A                                 15 67'KOA'
     A                                 16  2'Bldg'
     A                                 16  8'Room'
     A                                 16 14'Group'
     A                                 16 22'Head'
     A                                 16 28'Head'
     A                                 16 34'Bldg'
     A                                 16 40'Room'
     A                                 16 46'Group'
     A                                 16 54'Head'
     A                                 16 60'Head'
     A                                 16 66'Head'
     A                                 16 73'Sex'
     A*----------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*----------------------------------------------------------------
     A          R HP139K1
     A*%%TS  SD  20010206  110106  ISLRAMS     REL-V4R4M0  5769-PW1
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A N90                             23 10'F4=Prompt'
     A N90                             23 20'F5=Refresh'
     A N90                             23 31'F9=Accept'
     A N90N63N65
     A  60                             23 41'F11=Delete'
     A                                 23 52'F12=Previous'
     A                                 23 65'F13=Fold/Unfold'
      *----------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *----------------------------------------------------------------
     A          R HP139ES                   SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
     A*----------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*----------------------------------------------------------------
     A*
     A          R HP139EC                   SFLCTL(HP139ES)
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
