      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      HOG PRODUCTION
      * PROGRAM:     HP606
      * TITLE:       LISTING OF DEATH/DISEASE REASONS
      * PROGRAMMER:  MARY PALMER
      * CREATED:     4/05/94
      *
      * FUNCTION: BATCH PROGRAM TO LIST THE RECORDS IN THE
      *           DEATH/DISEASE REASONS FILE
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      * 99/99/99  NAME
      *
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     FHSP041    IF   E           K DISK
      *    DEATH/DISEASE REASONS MASTER FILE  (KEY: DRDRMGMGCD)
      *
     FQPRINT    O    F  132        PRINTER OFLIND(*INOF)
      *
      /EJECT
      ****************************************************************
      * INPUT SPECS
      ****************************************************************
      *
      *---------------------------------------------------------------
      * STANDARD PROGRAM STATUS DATA STRUCTURE
      *---------------------------------------------------------------
      *    EXTERNALLY DEFINED AS UTPGFR (RECORD FORMAT: PGMDSR)
     D PGMDS         ESDS                  EXTNAME(UTPGFR)
      *
      *---------------------------------------------------------------
      *  NAMED CONSTANTS
      *---------------------------------------------------------------
      *
     D YES             C                   CONST('Y')
     D NO              C                   CONST('N')
      *
      /EJECT
      ****************************************************************
      * MAIN LINE
      ****************************************************************
      *
      * PREPARE TO READ THE DEATH/DISEASE REASONS MASTER FILE AND
      * PRINT HEADINGS FOR FIRST TIME THROUGH
      *
     C     *LOVAL        SETLL     HSP041
     C                   EXCEPT    HDRPT
      *
      * READ AND PRINT ALL RECORDS IN THE DEATH/DISEASE REASONS MASTER.
      *
     C     *IN92         DOUEQ     *ON                                          MAIN DO
     C                   READ      HSP041                                 92
      *
     C     *IN92         IFEQ      *OFF                                         IF NOT EOF
      *
     C     *INOF         IFEQ      *ON
     C                   EXCEPT    HDRPT
     C                   ENDIF
      *
      * PRINT DEATH/DISEASE REASONS LINE
      *
     C                   EXCEPT    DTTYPE
     C                   MOVEL     YES           PRTFLG
      *
     C                   ENDIF                                                   IF NOT EOF
      *
      *  IF THE MASTER FILE WAS EMPTY AND NO RECORDS WERE PRINTED,
      *  PRINT THE STANDARD 'NO DATA...' ERROR MESSAGE
      *
     C     PRTFLG        IFEQ      NO
     C                   EXCEPT    NODATA
     C                   ENDIF
      *
     C                   ENDDO                                                  MAIN DO
      *
     C                   SETON                                        LR
      /EJECT
      *---------------------------------------------------------------
      * *INZSR - INITIALIZATION SUBROUTINE
      *---------------------------------------------------------------
      *
     C     *INZSR        BEGSR
      *
      * SET PRINT FLAG TO DETERMINE IF ANY RECORDS WERE PRINTED
      *
     C                   MOVEL     NO            PRTFLG            1
      *
     C                   MOVEL     *ALL'-'       DASH             34
      *
      * RETRIEVE TIME FOR REPORT HEADING
      *
     C                   TIME                    RTIME             6 0
      *
     C                   ENDSR
      *
      /EJECT
      *-------------------------------------------------------------
      * REPORT LAYOUT
      *-------------------------------------------------------------
      *
      *-------------------------------------------------------------
      * REPORT HEADING LINES
      *-------------------------------------------------------------
      *
     OQPRINT    E            HDRPT          1 01
     O                       SDPGM               10
     O                                           48 'HOG PRODUCTION SYSTEM'
     O                                           66 'DATE'
     O                       UDATE         Y     76
      *
     O          E            HDRPT          1
     O                       SDUSR               10
     O                                           41 '  LISTING OF DEATH/DI'
     O                                           55 'SEASE REASONS '
     O                                           66 'TIME'
     O                       RTIME               76 '  :  :  '
      *
     O          E            HDRPT          2
     O                                           66 'PAGE'
     O                       PAGE          Z     71
      *
     O          E            HDRPT          1
     O                                            7 'REASON '
     O                                           34 'REASON DESCRIPTION'
      *
     O          E            HDRPT          1
     O                       DASH                34
      *
      *-------------------------------------------------------------
      * DEATH/DISEASE REASON DETAIL LINE
      *-------------------------------------------------------------
      *
     O          E            DTTYPE      1
     O                       DRDRCD               6
     O                       DRDRDS              36
      *
      *-------------------------------------------------------------
      * NO DATA MESSAGE LINE
      *-------------------------------------------------------------
      *
     O          E            NODATA      1
     O                                           19 'NO DATA MEETS YOUR'
     O                                           38 'SELECTION CRITERIA'
