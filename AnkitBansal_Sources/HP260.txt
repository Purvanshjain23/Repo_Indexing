      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Hog Production System
      * PROGRAM:     Safedata: Build Download Files for Farm Sites and Groups
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     12/19/00
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 02/05/02  LeAnne Fedor
      *           Added 'production phase' to the download file.
      *
      * 04/16/02  LeAnne Fedor
      *           Per Anne Marie Dushay, we will no longer download:
      *           1) any 'Boar' groups to Safedata
      *           2) any 'Boar' farms to Safedata
      *
      * 06/07/03  Alice Brownfield
      *            changed to NOT exclude BOAR group types per
      *            Anne Marie  -  email 06/06/2003
      *
      * 10/15/13  LeAnne Ramsey (E2831)
      *           Recompile only. Added field 'MTech Reference'.
      *
      * 01/22/20  Danny Nguyen  (S16086)
      *           Commented out 'BGF' Production Phase Code when selecting records from
      *           hsl034b file. Also commented out 'B' (Boar Farms) Group Type Code when
      *           selecting from hsp018 file. This change is in reference to helpdesk
      *           ticket WHD59001. The Business would like these farms to start utilizing
      *           Safe Data in regards to Feed Ordering.
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fhsp018    if   e           k disk
      *  Farm site
      *
      *
     Fhsl034b   if   e           k disk
      *  Hog group
      *
      *
     Fhsp259    o    e           k disk
      *  Safedata: Download Farm Sites
      *
      *
     Fhsp260    o    e           k disk
      *  Safedata: Download Open Groups
      *
      /eject
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      ****************************************************************
      * ARRAYS AND TABLES
      ****************************************************************
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Workfields
      *
     D wkname          s             25a
     D position        s              5I 0
      *
      * Work fields for date manipulation
      *
     D wkisodate       s               D   datfmt(*iso)
     D wkopdt          s              8  0
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * MAINLINE
      ****************************************************************
      *
     C                   exsr      $groups
      *
     C                   exsr      $farms
      *
     C                   seton                                        lr
      /eject
      *---------------------------------------------------------------
      * Build file for downloading open groups
      *---------------------------------------------------------------
      *
     C     $groups       begsr
      *
      * Read groups. Process each group that:
      *  1) is not a BGF group        --  changed to include BGF Group; S16086
      *  2) is not a BOAR group ////  --  changed to include 06/06/2003  amb
      *              ////  --  changed to include Boar Group Types; Jan 2020. S16086
      *  3) has a status of OPEN or CREATED
      *
     C                   dou       *in90 = *on                                  Do groups
     C                   read      hsl034b                                90
     C                   if        *in90 = *off and                             If not EOF
16086C***                          hgppcd <> 'BGF  ' and
     C********//////               hggtcd <> 'B' and
     C                             (hggscd = 'CR' or hggscd = 'OP')
      *
     C                   clear                   d1rec
      *
     C                   z-add     hgfscd        d1fscd
     C                   move      hgblcd        d1blcd
     C                   move      hgrmcd        d1rmcd
     C                   move      hghgcd        d1hgcd
      *
      * If open date is zero, plug system date into open date.
      *
     C                   if        hgopdt = 0
     C                   z-add     wkopdt        d1opdt
     C                   else
     C                   z-add     hgopdt        d1opdt
     C                   endif
      *
     C                   write     d1rec
     C
     C                   endif                                                  If not EOF
     C                   enddo                                                  Do groups
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Build file for downloading farm sites
      *---------------------------------------------------------------
      *
     C     $farms        begsr
      *
      * Read each farm. Process each farm that:
      *  1) is not a BOAR group       --  changed to include BOAR Group; S16086
      *
     C     *loval        setll     hsp018
      *
     C                   dou       *in91 = *on                                  Do farms
     C                   read      hsp018                                 91
16086C***                if        *in91 = *off and                             If not EOF
  |  C                   if        *in91 = *off                                 If not EOF
16086C***                          fsgtcd <> 'B'
      *
     C                   clear                   d2rec
     C                   z-add     fsfscd        d2fscd
     C                   move      fsppcd        d2ppcd
      *
      * Get rid of any/all commas that may be in the farm name.
      *
     C                   move      fsfsnm        wkname
      *
     C                   dou       position = 0
     C                   eval      position =  %scan(',':wkname:1)
     C                   if        position <> 0
     C                   eval      wkname = %replace(' ':wkname:position)
     C                   endif
     C                   enddo
      *
     C                   move      wkname        d2fsnm
      *
     C                   write     d2rec
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Do farms
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Save system date to use as a default opened date.
      *
     C     *mdy          move      udate         wkisodate
     C                   move      wkisodate     wkopdt
      *
     C                   endsr
      /eject
