/********************************************************************/
/*                                                                  */
/*  SYNOPSIS -  FAX THE LOAD CONFIRMATION WHEN A CARRIER CODE IS    */
/*              ENTERED FROM 'EDIT LOAD HEADER' AND 'EDIT LOAD PLAN'*/
/*                                                                  */
/*  SYSTEM    - ORDER MANAGEMENT SYSTEM                             */
/*  PROGRAM   - FAX BOOKING CONFIRMATION                            */
/*  DATE      - 01/10/97                                            */
/*                                                                  */
/*      PARAMETERS:                                                 */
/*                                                                  */
/*     O    &RETURN     - RETURN CODE                               */
/*     B    &COMPNYN    - COMPANY NUMBER  (PASSED)                  */
/*     B    &LOADN      - LOAD NUMBER (PASSED)                      */
/*     B    &CARRIER    - CARRIER CODE (PASSED)                     */
/*                                                                  */

 PDVFUPC:    PGM        PARM(&RETURN &COMPNYN &LOADN &CARRIER)
/* PARMS                                                                      */
        DCL    &RETURN     *CHAR   (7)           /* Return Code               */
        DCL    &CARRIER    *CHAR   (03)          /* Application Code          */
        DCL    &COMPNYN    *DEC                  /* Company Number  (Passed)  */
        DCL    &LOADN      *DEC                  /* LOAD NUMBER     (Passed)  */
        DCL    &SENDYN     *CHAR   (01)          /* SEND FAX YES/NO           */

/* INTERNAL FIELDS                                                            */
        DCL    &COMPANY    *DEC    (3 0)         /* Company Number            */
        DCL    &LOADID     *DEC    (7 0)         /* LOAD ID                   */

/* ERROR MESSAGE FIELDS                                                       */
        DCL    &MSG        *CHAR   (512)         /* message                   */
        DCL    &MSGID      *CHAR   (7)           /* message id                */
        DCL    &MSGL       *DEC    (5 0)

/********************************************************************/
/*   INITIALIZE VARIABLES                                           */
/********************************************************************/

             CHGVAR     VAR(&RETURN) VALUE('       ')
             CHGVAR     VAR(&SENDYN) VALUE('N')
             CHGVAR     VAR(&COMPANY) VALUE(&COMPNYN)
             CHGVAR     VAR(&LOADID) VALUE(&LOADN)

/********************************************************************/
/*   MONITOR FOR ALL MESSAGES AT PROGRAM LEVEL                      */
/********************************************************************/

             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(MSGTRAP))

/********************************************************************/
/*   CALL PROMPT PROGRAM                                            */
/********************************************************************/

             CALL       PGM(PDVKPVR) PARM(&RETURN &SENDYN)
             IF         COND(&RETURN *NE ' ') THEN(GOTO +
                          CMDLBL(USERQUIT))

/********************************************************************/
/*   SEND FAX IF USER SELECTED YES ON THE PROMPT                    */
/********************************************************************/
             IF         COND(&SENDYN *EQ 'N') THEN(GOTO +
                          CMDLBL(USERQUIT))

             OVRPRTF    FILE(PDAUPFR$) OUTQ(FASTFAX/FFXOTQIAP)

/*   CALL PRINT PROGRAM                                            */
             CALL PGM(PDAUPFR) PARM(' ' &COMPANY &LOADID)

/*   SEND FAX                                                      */
             CALL PGM(PDVDXFR) PARM(' ' &CARRIER)

             GOTO       CMDLBL(END)

/*-------------------------USER QUIT--------------------------------*/
 USERQUIT:   CHGVAR     VAR(&RETURN) VALUE('Y2U0005')
             GOTO       CMDLBL(END)

/*----------------------RECEIVE MESSAGES----------------------------*/

 MSGTRAP:    RCVMSG     MSGTYPE(*EXCP) MSGDTA(&MSG) MSGDTALEN(&MSGL) +
                          MSGID(&MSGID)
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(END))

             SNDPGMMSG  MSGID(&MSGID) MSGF(QCPFMSG) MSGDTA(%SST(&MSG +
                          1 &MSGL)) MSGTYPE(*ESCAPE)
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(END))

 END:        ENDPGM

