      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Hog Production System
      * PROGRAM:     HP6000
      * TITLE:       Listing of BGF Weekly Production Data for a Farm
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     06/05/00
      *
      * Function:    This batch listing prints one week of data for a farm.
      *
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 07/20/00  LeAnne Fedor
      *           Removed all 'calculated' fields per Anne Dushay. The
      *           users only want to see the values that they keyed in.
      *
      * 09/14/00  LeAnne Fedor
      *           The fields in HSP093 were renamed.  Program logic was
      *           changed accordingly.
      *
      * 12/11/00  LeAnne Fedor
      *           Added new field 'de-pop flag' to listing.
      *
      * 02/27/01  LeAnne Fedor
      *           Added 5 new fields to Weekly Production File:
      *              gilts transferred in
      *              sows transferred in
      *              gilts transferred out
      *              sows transferred out
      *              number of gilt abortions
      *
      * 10/10/01  LeAnne Fedor
      *           Changed text on 2 fields: lwnsv and opsohd
      *
      * 12/26/01  LeAnne Fedor
      *           Recompile only. Many new fields added to Weekly Production file.
      *
      * 01/09/02  LeAnne Fedor
      *           The report can no longer be generated for 13 weeks of data. It now
      *           generates for a single week...but you can kick it off for all BGF
      *           farms or just a single farm.  The layout has been changed to letter/
      *           portrait. The data entered 'daily' appears on the left side and the
      *           data entered 'weekly' appears on the right side.
      *
      * 02/19/02  LeAnne Fedor
      *           Recompile only. 'Weeking ending pounds of supplement used' changed to
      *           7,0 from 9,2.
      *
      * 04/11/02  LeAnne Fedor
      *           Added 'average lactation pounds'
      *
      * 11/18/02  LeAnne Fedor
      *           Replaced  'de-pop flag' with 'population code'.
      *
      * 10/22/03  LeAnne Fedor
      *           Added fields:
      *               gilt total stillborns
      *               gilt total mummies
      *               17+ day weaned sows
      *               euthanized gilts
      *               euthanized sows
      *               total p2 litters farrowed
      *           Changed text on fields:
      *               from: gilts dead     to:  gilts dead naturally
      *               from: sows dead      to:  sows dead naturally
      *
      *
      * 06/30/04  LeAnne Fedor
      *           Added 1 new field: Number of Gilts Bred with Passed Dates
      *           Removed 10 fields: number of good piglets farrowed
      *                              number of dome heads
      *                              number of splays
      *                              number of weak and lethargic
      *                              number of scours
      *                              number of good piglets weaning next
      *                              number of lame
      *                              number of under 7 pounds
      *                              number of rough hair coat
      *                              number of swollen joints
      *
      * 02/03/05  LeAnne Fedor
      *           Added new field  "Transferred In Open Gilts" to listing.
      *
      * 02/07/05  LeAnne Fedor
      *           Removed field  "Average Lactation Pounds" from listing.
      *
      * 08/23/05  LeAnne Fedor
      *           Added new field  "Man Hours" to listing.
      *
      * 06/15/06  LeAnne Ramsey
      *           Recompile only. New field "Avg Gilt Weight @ 28 Weeks" added
      *           to BGF Weekly Production file.
      *
      * 10/19/07  LeAnne Ramsey
      *           The users are going to start entering the "average" manhours
      *           instead of the "total" manhours. So, changed column heading
      *           to say "average" manhours.
      *
      * 08/04/08  LeAnne Ramsey
      *           Fields were deleted and Text changed in BGF Weekly Production file:
      *           Deleted fields: BPIOGLHD - Transferred in Open Gilts
      *                           BPOOGLHD - Gilts Transferred Out Open
      *                           BPPSGLSV - Nbr Gilts Bred w/Passed Date
      *                           BPP2LT   - Total P2 Litters Farrowed
      *                           BPADGB   - Avg Gilt Age @ Breeding-Days
      *                           BPGLAVLB - Avg Gilt Weight @ 28 Weeks
      *                           BPWSLB   - Weekly Ending LBS Sup Used
      *                           BP17WNLT - 17+ Day Weaned Sows
      *           Text changed:   BPEWNLT is now '0-15 day weaned sows'
      *                           BPWNLT  is now '16+ day weaned sows'
      *           New calculated: D1TOTWNLT = BPEWNLT + BPWNLT
      *
      *
      * 09/19/08  LeAnne Ramsey
      *           Recompile only.
      *           The users are switching "average manhours" back to "total manhours".
      *           So, removed the word "average" from the screen text.
      *
      * 10/16/13  LeAnne Ramsey (E2831)
      *           Recompile only. Added field 'MTech Reference'.
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
      *
     Fhsp018    if   e           k disk
      *    Farm site (open query limits records to BGF farms)
      *
      *
     Fhsl092a   if   e           k disk
      *  BGF weekly production  (records selected in open query)
      *
      *
     Fhsp093    if   e           k disk
      *    BGF farrowing and pregnancy check events
      *
      *
     Fhsp104    if   e           k disk
      *    Population codes
      *
      *
     Fqprint    o    f   80        printer oflind(*inof)
      *
      /EJECT
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Print fields
      *
     D rtime           s              6  0
      *
     D h1fscd          s                   like(fsfscd)
     D h1fsnm          s                   like(fsfsnm)
      *
     D d1pods          s                   like(popods)
      *
     D d1totwnlt       s                   like(bpewnlt)
      *
     D d1glfahd        s                   like(beevhd)
     D d1fahd          s                   like(beevhd)
     D d1glprhd        s                   like(beevhd)
     D d1prhd          s                   like(beevhd)
     D d1bredpr        s              4  1
      *
     D d1pryr          s                   like(besvyr)
     D d1prwk          s                   like(besvwk)
     D d1fayr          s                   like(besvyr)
     D d1fawk          s                   like(besvwk)
      *
      *
      * Work fields
      *
     D wkcdyr          s                   like(bpcdyr)
     D wkcdwk          s                   like(bpcdwk)
     D wkdtfl          s              1
      *
      * Control values
      *
     d prtfl           s              1    inz('N')
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *---------------------------------------------------------------
      * Standard printer file data structure
      *---------------------------------------------------------------
      *    externally defined as UTPPFR (record format:FDBCKP)
     D prtds         e ds                  extname(utppfr)
      *
      *
      *---------------------------------------------------------------
      * Local data area.
      *---------------------------------------------------------------
      *
     Dlda             uds                  dtaara(*lda)
     D  ldfscd                 1      5  0
     D  ldcdyr                 6      9  0
     D  ldcdwk                10     11  0
     D  ldfsnm                13     37
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * MAINLINE
      ****************************************************************
      *
     C                   select
      * One BGF farm
     C                   when      ldfscd <> 0
     C     ldfscd        chain     hsp018                             92
     C                   if        *in92 = *off
     C                   exsr      $farm
     C                   endif
     C                   other
      *
      * All BGF farms
      *
      * Before starting for 'all' farms, determine if there is any data for
      * the selected year/week.
      *
     C     *loval        setll     hsl092a
     C                   read      hsl092a                                92
     C                   if        *in92 = *off                                 If anything
      *
     C     *loval        setll     hsp018
     C                   dou       *in90 = *on                                  Do mult farm
     C                   read      hsp018                                 90
     C                   if        *in90 = *off                                 If read
     C                   exsr      $farm
     C                   endif                                                  If read
     C                   enddo                                                  Do mult farm
     C                   endif                                                  If anything
      *
     C                   endsl
      *
      *---------------------------------------------------------------
      * End of file processing
      *---------------------------------------------------------------
      *
     C                   if        prtfl = no
      *
     C                   if        ldfscd <> 0
     C                   z-add     ldfscd        h1fscd
     C                   move      ldfsnm        h1fsnm
     C                   endif
      *
     C                   except    h1hdr
     C                   except    nodata
     C                   endif
      *
     C                   seton                                        lr
      /eject
      *---------------------------------------------------------------
      * Retrieve and print page of data for a farm
      *---------------------------------------------------------------
      *
     C     $farm         begsr
      *
     C                   z-add     fsfscd        h1fscd
     C                   move      fsfsnm        h1fsnm
      *
     C     key01         chain     hsl092a                            92
     C                   if        *in92 = *off                                 If hit
      *
      * Retrieve description for Population Code
      *
     C     bppocd        chain     hsp104                             92        If pop code
     C                   if        *in92 = *off
     C                   move      popods        d1pods
     C                   else
     C                   move      *blank        d1pods
     C                   endif
      *
      * Retrieve pregnancy checked positve values
      *
     C                   move      'P'           wkdtfl
     C     key02         chain     hsp093                             92        If farrow data
     C                   if        *in92 = *off
     C                   z-add     beglevhd      d1glprhd
     C                   z-add     beevhd        d1prhd
     C                   z-add     besvyr        d1pryr
     C                   z-add     besvwk        d1prwk
     C                   endif                                                  If farrow data
      *
      *
      * Retrieve 'total group farrowed' values
      *
     C                   move      'F'           wkdtfl
     C     key02         chain     hsp093                             92        If farrow data
     C                   if        *in92 = *off
     C                   z-add     beglevhd      d1glfahd
     C                   z-add     beevhd        d1fahd
     C                   z-add     besvyr        d1fayr
     C                   z-add     besvwk        d1fawk
     C                   endif                                                  If farrow data
      *
      * Move 'breed percent' to a field with only 1 decimal positions for
      * printing.
      *
     C                   z-add     bpbredpr      d1bredpr
      *
      * Total "weaned sows"
      *
     C     bpewnlt       add       bpwnlt        d1totwnlt
      *
      * Print headings
     C                   except    h1hdr
     C                   except    d1dtl
     C                   move      yes           prtfl
     C                   endif                                                  If hit
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      *
     C     key01         klist
     C                   kfld                    fsfscd
     C                   kfld                    ldcdyr
     C                   kfld                    ldcdwk
      *
      *
     C     key02         klist
     C                   kfld                    fsfscd
     C                   kfld                    bpcdyr
     C                   kfld                    bpcdwk
     C                   kfld                    wkdtfl
      *
      *
      * Retrieve time for report heading.
      *
     C                   time                    rtime
      *
     C                   endsr
      /eject
      *-------------------------------------------------------------
      * Report Headings
      *-------------------------------------------------------------
      *
     Oqprint    e            h1hdr          1 01
     O                       sdpgm               10
     O                                           50 'HOG PRODUCTION SYSTEM'
     O                                           70 'DATE'
     O                       udate         y     80
      *
     O          e            h1hdr          1
     O                       sdusr               10
     O                                           45 'LISTING OF BGF WEEKLY'
     O                                           56 ' PRODUCTION'
     O                                           70 'TIME'
     O                       rtime               80 '  :  :  '
      *
     O          e            h1hdr          1
     O                                           70 'PAGE'
     O                       page          z     80
      *
     O          e            h1hdr          1
     O                                           19 'Farm site:'
     O                       h1fscd        z     25
     O                       h1fsnm              52
      *
      *
     O          e            h1hdr          1
     O                                           19 'Calendar year/week:'
     O                       ldcdyr        z     25
     O                       ldcdwk        z     29
      *
      *
     O          e            h1hdr          2
     O                                           19 'Population code:'
     O                       bppocd              22
     O                       d1pods              37
      *
      *-------------------------------------------------------------
      * Detail line
      *-------------------------------------------------------------
      *
     O          e            d1dtl          1
     O                                           30 '** DATA ENTERED DAILY **'
     O                                           72 '** DATA ENTERED WEEKLY **'
      *
     O          e            d1dtl       1
     O                                           30 'Number of weaners'
     O                       bpwnsv        k     38
      *
      *
     O          e            d1dtl       1
     O                                           24 'Number of open sow pool '
     O                                           30 'served'
     O                       bplwnsv       k     38
      *
      *
      *
     O          e            d1dtl       1
     O                                           30 'Number of returns'
     O                       bprtsv        k     38
      *
      *
     O          e            d1dtl       1
     O                                           30 'Number of gilts'
     O                       bpglsv        k     38
      *
      *
     O          e            d1dtl       1
     O                                           30 'Open gilt pool-eligible'
     O                       bpopglhd      k     38
      *
      *
     O          e            d1dtl       1
     O                                           30 'Open sow pool'
     O                       bpopsohd      k     38
      *
      *
     O          e            d1dtl       1
     O                                           30 'Number of gilt abortions'
     O                       bpglabhd      k     38
      *
      *
     O          e            d1dtl       1
     O                                           30 'Number of abortions'
     O                       bpabhd        k     38
      *
      *
     O          e            d1dtl       1
     O                                           30 'Number of NIPs'
     O                       bpnipqt       k     38
      *
      *
     O          e            d1dtl       1
     O                                           30 'Gilt litters farrowed'
     O                       bpgllt        k     38
      *
      *
     O          e            d1dtl       1
     O                                           30 'Gilt total born alive'
     O                       bpglbahd      k     38
      *
      *
     O          e            d1dtl       1
     O                                           30 'Gilt total stillborns'
     O                       bpglsbhd      k     38
      *
     O                                           72 'Percent breed back'
     O                       d1bredpr      k     80
      *
      *
     O          e            d1dtl       1
     O                                           30 'Gilt total mummies'
     O                       bpglmmhd      k     38
      *
      *
     O          e            d1dtl       1
     O                                           30 'Total litters farrowed'
     O                       bptolt        k     38
      *
      *
     O          e            d1dtl       1
     O                                           30 'Total born alive'
     O                       bpbahd        k     38
      *
     O                                           67 'Preg checked positive year'
     O                                           72 '/week'
     O                       d1pryr        z     77
     O                       d1prwk        z     80
      *
      *
     O          e            d1dtl       1
     O                                           30 'Total stillborns'
     O                       bpsbhd        k     38
      *
     O                                           62 'Preg checked positive'
     O                                           72 ' gilt head'
     O                       d1glprhd      kb    80
      *
      *
      *
     O          e            d1dtl       1
     O                                           30 'Total mummies'
     O                       bpmmhd        k     38
      *
     O                                           61 'Preg checked positive'
     O                                           72 ' group head'
     O                       d1prhd        kb    80
      *
      *
     O          e            d1dtl       1
     O                                           30 'Total pre-wean losses'
     O                       bptolshd      k     38
      *
     O                                           67 'Total group farrowed year'
     O                                           72 '/week'
     O                       d1fayr        zb    77
     O                       d1fawk        zb    80
      *
      *
     O          e            d1dtl       1
     O                                           30 '0-15 day weaned sows'
     O                       bpewnlt       k     38
      *
     O                                           67 'Total group farrowed gilt'
     O                                           72 ' head'
     O                       d1glfahd      kb    80
      *
      *
     O          e            d1dtl       1
     O                                           30 '16+ day weaned sows'
     O                       bpwnlt        k     38
      *
     O                                           67 'Total group farrowed group'
     O                                           72 ' head'
     O                       d1fahd        k     80
      *
      *
     O          e            d1dtl       1
     O                                           30 'Total weaned sows'
     O                       d1totwnlt     kb    38
      *
      *
     O          e            d1dtl       1
     O                                           30 'Total pigs weaned'
     O                       bptownhd      k     38
      *
      *
     O          e            d1dtl       1
     O                                           23 'Average age of piglet'
     O                                           30 ' weaned'
     O                       bpadwn        k     38
      *
      *
     O          e            d1dtl       2
     O                                           72 'Ending piglet inventory'
     O                       bpenplhd      k     80
      *
      *
      *
     O          e            d1dtl       1
     O                                           30 'Gilts arrived'
     O                       bparglhd      k     38
      *
     O                                           63 'Ending gilt and sow '
     O                                           72 'inventory'
     O                       bpglsohd      k     80
      *
      *
     O          e            d1dtl       1
     O                                           30 'Transferred in bred gilts'
     O                       bpibglhd      k     38
      *
     O                                           72 'Ending boar inventory'
     O                       bpbohd        k     80
      *
      *
     O          e            d1dtl       1
     O                                           30 'Sows arrived'
     O                       bparsohd      k     38
      *
      *
     O          e            d1dtl       1
     O                                           30 'Transferred in bred sows'
     O                       bpibsohd      k     38
      *
      *
     O          e            d1dtl       1
     O                                           30 'Gilts sold'
     O                       bpslglhd      k     38
      *
      *
     O          e            d1dtl       1
     O                                           30 'Sows sold'
     O                       bpslsohd      k     38
      *
      *
     O          e            d1dtl       1
     O                                           30 'Gilts dead-naturally only'
     O                       bpddglhd      k     38
      *
     O          e            d1dtl       1
     O                                           30 'Sows dead-naturally only'
     O                       bpddsohd      k     38
      *
      *
     O          e            d1dtl       1
     O                                           30 'Gilts euthanized'
     O                       bpeuglhd      k     38
      *
     O          e            d1dtl       1
     O                                           30 'Sows euthanized'
     O                       bpeusohd      k     38
      *
      *
     O          e            d1dtl       1
     O                                           30 'Transferred out bred gilts'
     O                       bpobglhd      k     38
      *
      *
     O          e            d1dtl       1
     O                                           30 'Transferred out open sows'
     O                       bpoosohd      k     38
      *
      *
     O          e            d1dtl       1
     O                                           30 'Transferred out bred sows'
     O                       bpobsohd      k     38
      *
      *
     O          e            d1dtl       1
     O                                           30 'Boars in'
     O                       bparbohd      k     38
      *
     O          e            d1dtl       1
     O                                           30 'Boars sold'
     O                       bpslbohd      k     38
      *
     O          e            d1dtl       1
     O                                           30 'Boars dead'
     O                       bpddbohd      k     38
      *
      *
     O          e            d1dtl       1
     O                                           30 'Manhours'
     O                       bpmanhr       k     41
      *
      *-------------------------------------------------------------
      * NO DATA MESSAGE LINE
      *-------------------------------------------------------------
      *
     O          e            nodata      2
     O          e            nodata      2
     O                                           19 'No data meets your'
     O                                           39 'selection criteria.'
      /eject
