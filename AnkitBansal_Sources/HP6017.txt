      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Hog Production System
      * PROGRAM:     HP6017
      * TITLE:       Listing of Group Overrides
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     09/01/04
      *
      *            This listing is submitted from HP4016-Work With Group Overrides.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 07/06/09  LeAnne Ramsey
      *           Recompile only. Added new field 'Continuous Flow Flag' to Hog Group file.
      *
      * 02/29/12  LeAnne Ramsey (E1978)
      *           Added Farm Site and Group Status as selectors and as columns on
      *           the report.
      *
      * 10/16/13  LeAnne Ramsey (E2831)
      *           Recompile only. Added field 'MTech Reference'.
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fhsp011    if   e           k disk
      *    Production phase
      *
      *
     Fhsp014    if   e           k disk
      *    Group status
      *
      *
     Fhsp018    if   e           k disk
      *    Farm site
      *
      *
     Fhsj034s   if   e           k disk
      *    Hog group + Group overrides
      *    (records selected in open query)
      *
      *
     Fqprint    o    f   80        printer oflind(*inof)
      *
      /EJECT
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Standard fields
      *
     D prtfl           s              1    inz('N')
     D dash            s             80    inz(*all'-')
     d rtime           s              6  0
      *
      *
      * Print fields
      *
     D h1ppds          s                   like(ppppds)
     D h1gsds          s                   like(gsgsds)
     D h1fsnm          s                   like(fsfsnm)
     D h1obfsnm        s                   like(fsfsnm)
     D h1onfsnm        s                   like(fsfsnm)
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *---------------------------------------------------------------
      * Local data area.
      *---------------------------------------------------------------
      *
     Dlda             uds                  dtaara(*lda)
      *
     D  ldreport               1      1
     D  ldppcd                 9     13
     D  ldsicd                14     18
     D  ldobfs                19     23  0
     D  ldonfs                24     28  0
     D  ldfscd                29     33  0
     D  ldgscd                34     35
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      *******************************************************************************
      * MAINLINE
      *******************************************************************************
      *
      * Print headings the first time through.
      *
     C                   except    h1hdr
      *
      *
      * Read/process all records selected by the open query.
      *
     C     *loval        setll     hsj034s
      *
     C                   dou       *in90 = *on                                  Main do
     C                   read      hsj034s                                90
     C                   if        *in90 = *off                                 If not EOF
      * Print
     C                   if        *inof = *on
     C                   except    h1hdr
     C                   endif
     C                   except    l1dtl
     C                   movel     yes           prtfl
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Main do
      *
      *-----------------------------------------------------------------------------
      * EOF processing
      *-----------------------------------------------------------------------------
      *
      * If no records met the user's selections, print the standard 'no data...'
      * error message
      *
     C                   if        prtfl = no
     C                   except    nodata
     C                   endif
      *
     C                   seton                                        lr
      /eject
      *-------------------------------------------------------------------------------------
      * Initialization subroutine
      *-------------------------------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Key lists
      *  None
      *
      * Set up heading info.
      *
     C                   time                    rtime
      *
      * Farm Site
     C                   if        ldfscd = 0
     C                   seton                                        83
     C                   else
     C     ldfscd        chain     hsp018                             92
     C                   if        *in92 = *off
     C                   move      fsfsnm        h1fsnm
     C                   endif
     C                   endif
      *
      * Group Status
     C                   if        ldgscd = *blank
     C                   seton                                        84
     C                   else
     C     ldgscd        chain     hsp014                             92
     C                   if        *in92 = *off
     C                   move      gsgsds        h1gsds
     C                   endif
     C                   endif
      *
      * Production phase
      *
     C                   if        ldppcd = *blank
     C                   seton                                        85
     C                   else
     C     ldppcd        chain     hsp011                             92
     C                   if        *in92 = *off
     C                   move      ppppds        h1ppds
     C                   endif
     C                   endif
      *
      * Sire line
     C                   if        ldsicd = *blank
     C                   seton                                        86
     C                   endif
      *
      * Origin BGF farm
      *
     C                   if        ldobfs = 0
     C                   seton                                        87
     C                   else
     C     ldobfs        chain     hsp018                             92
     C                   if        *in92 = *off
     C                   move      fsfsnm        h1obfsnm
     C                   endif
     C                   endif
      *
      * Origin Nursery farm
      *
     C                   if        ldonfs = 0
     C                   seton                                        88
     C                   else
     C     ldonfs        chain     hsp018                             92
     C                   if        *in92 = *off
     C                   move      fsfsnm        h1onfsnm
     C                   endif
     C                   endif
      *
     C                   endsr
      /EJECT
      *-------------------------------------------------------------
      * Report heading lines
      *-------------------------------------------------------------
      *
     Oqprint    e            h1hdr          1 01
     O                       sdpgm               10
     O                                           48 'HOG PRODUCTION SYSTEM'
     O                                           70 'DATE'
     O                       udate         y     80
      *
      *
     O          e            h1hdr          1
     O                       sdusr               10
     O                                           50 'LISTING OF GROUP OVERRIDES'
     O                                           70 'TIME'
     O                       rtime               80 '  :  :  '
      *
     O          e            h1hdr          2
     O                                           70 'PAGE'
     O                       page          z     80
      *
      *
     O          e            h1hdr          1
     O                                           22 'Farm site ............'
     O               83                          27 'All'
     O              n83      ldfscd        z     29
     O              n83      h1fsnm              56
      *
      *
     O          e            h1hdr          1
     O                                           22 'Group status .........'
     O               84                          27 'All'
     O              n84      ldgscd              26
     O              n84      h1gsds              51
      *
     O          e            h1hdr          1
     O                                           22 'Production phase .....'
     O               85                          27 'All'
     O              n85      ldppcd              29
     O              n85      h1ppds              61
      *
      *
     O          e            h1hdr          1
     O                                           22 'Sire line ............'
     O               86                          27 'All'
     O              n86      ldsicd              29
      *
      *
     O          e            h1hdr          1
     O                                           22 'Origin BGF farm ......'
     O               87                          27 'All'
     O              n87      ldobfs        z     29
     O              n87      h1obfsnm            56
      *
      *
     O          e            h1hdr          2
     O                                           22 'Origin nursery farm ..'
     O               88                          27 'All'
     O              n88      ldonfs        z     29
     O              n88      h1onfsnm            56
      *
      *
      *-------------------------------------------------------------
      * Column headings
      *-------------------------------------------------------------
      *
     O          e            h1hdr       0  1
     O                                           52 'Origin'
     O                                           62 'Origin'
      *
     O          e            h1hdr       0  1
     O                                            3 'Hog'
     O                                           14 'Farm'
     O                                           22 'Group'
     O                                           35 'Production'
     O                                           42 'Sire'
     O                                           51 'BGF'
     O                                           62 'Nursery'
      *
      *
     O          e            h1hdr       0  1
     O                                            5 'Group'
     O                                           14 'Site'
     O                                           23 'Status'
     O                                           33 'Phase'
     O                                           42 'Line'
     O                                           51 'Farm'
     O                                           61 'Farm'
      *
     O          e            h1hdr       0  2
     O                       dash                80
      *
      *-------------------------------------------------------------
      * Detail line
      *-------------------------------------------------------------
      *
     O          e            l1dtl          1
     O                       hghgcd               7
     O                       hgfscd        z     14
     O                       hggscd              21
     O                       hgppcd              33
     O                       gosicd              43
     O                       goobfs        z     51
     O                       goonfs        z     61
      *
      *-------------------------------------------------------------
      * No data message line
      *-------------------------------------------------------------
      *
     O          e            nodata      1
     O                                           19 'NO DATA MEETS YOUR'
     O                                           38 'SELECTION CRITERIA'
