      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Hog Production System
      * PROGRAM:     HP8004 - Determine If a Feed Bin is Shared
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     01/28/02
      *
      * Function:  This function determines if a feed bin is shared.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 01/17/06  LeAnne Fedor
      *           The second check for a shared bin now uses the join file
      *           of Building/Rooms + Rooms/Bin Sets (since we have removed
      *           Bin Set from the Building Rooms file and put it in our new
      *           Rooms/Bin Sets file.)
      *
      * 03/26/20  Danny Nguyen - P405 - Farm Number Increase
      *           Recompile only. Increased Bin Code (@@BNCD) in HSPREF file from 5A to 6A.
      /eject
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
      *
     Fhsj113b   if   e           k disk
      *  Rooms/Bin Sets + Building Rooms
      *
      *
     Fhsl036b   if   e           k disk
      *  Bin set bins
      *
      /eject
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      *
      *------------------------------------------------------------------
      * CONSTANTS
      *------------------------------------------------------------------
      *
     D yes             c                   'Y'
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Control fields
      *
     D count           s              3  0
      *
      *
      * Workfields
      *
     D wkbicd          s                   like(bnbicd)
      *
      *
      * Parm fields
      *
     D xxfscd          s                   like(brfscd)
     D xxbncd          s                   like(bnbncd)
     D xxshared        s              1
      *
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * Mainline
      ****************************************************************
      *
      * Determining if a bin is 'shared' is a 2-step process.
      *
      * 1) If more than 1 record exists for the farm site/bin in the 'bin set bins' file,
      *    the bin is shared.
      *    (Note: Logic no longer allows users to put a "Bin" in more than 1 Bin Set.
      *     But, I found 1 farm in the file that had a "Bin" in more than 1 Bin Set.
      *     So, we will continue to make this first check although we are extremely
      *     unlikely to get "shared" from this logic.)
      *
      * 2) If more than 1 'active' room record exists in the join file of
      *    Rooms/Bin Sets + Building/Rooms a bin's corresonding bin set,
      *    the bin is shared.
      *
      *
      *  Position in the 'bin set bins' file with farm site/bin.
      *
     C     key01         setll     hsl036b
      *
     C                   dou       *in90 = *on or count > 1                     Do step 1
     C     key01         reade     hsl036b                                90
     C                   if        *in90 = *off
     C                   move      bnbicd        wkbicd
     C                   add       1             count
     C                   endif
     C                   enddo                                                  Do step 1
      *
      *
      * If more than 1 record existed for the farm site/bin in the 'bin set bins'
      * file, the bin is shared. So, set the outgoing parm flag field; otherwise,
      * continue with your second check.
      *
     C                   if        count > 1                                    If shared
     C                   move      yes           xxshared
     C                   else
      *
     C                   z-add     0             count
     C     key02         setll     hsj113b
      *
     C                   dou       *in91 = *on or count > 1                     Do step 2
     C     key02         reade     hsj113b                                91
     C                   if        *in91 = *off and brrmst = 'A'
     C                   add       1             count
     C                   endif
     C                   enddo                                                  Do step 2
      *
      * If more than 1 'active' room record existed in the 'rooms' file for
      * the bin's corresponding bin set, the bin is shared.
      *
     C                   if        count > 1
     C                   move      yes           xxshared
     C                   endif
      *
     C                   endif                                                  If shared
      *
     C                   seton                                        lr
      /eject
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
     C     *entry        plist
     C                   parm                    xxfscd
     C                   parm                    xxbncd
     C                   parm                    xxshared
      *
      *
     C     key01         klist
     C                   kfld                    xxfscd
     C                   kfld                    xxbncd
      *
      *
     C     key02         klist
     C                   kfld                    xxfscd
     C                   kfld                    wkbicd
      *
     C                   endsr
      /eject
