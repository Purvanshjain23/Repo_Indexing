/*****************************************************************   */
/*  PROGRAM NUMBER --> HP374CL                                       */
/*  PROGRAM NAME ----> REOPEN CLOSED GROUPS                          */
/*  PROGRAMMER ------> LEANNE FEDOR                                  */
/*  DATE ------------> 04/24/03                                      */
/*                                                                   */
/*  PROGRAM PURPOSE -> THIS CL CALLS THE PROGRAMS TO UNCLOSE GROUPS. */
/*                                                                   */
/*                     WHEN THIS IS CALLED FROM THE ACTUAL 'REOPEN'  */
/*                     FUNCTION, THE INCOMING VALUE FOR &REOPEN WILL */
/*                     BE Y=YES AND A LISTING OF THE GROUPS UNCLOSED */
/*                     WILL BE PRINTED.                              */
/*                                                                   */
/*                     WHEN THIS IS CALLED FROM THE PERIOD CLOSE     */
/*                     FUNCTION, THE INCOMING VALUE FOR &REOPEN WILL */
/*                     BE N=NO AND NO LISTING IS PRINTED.            */
/*                                                                   */
/*********************************************************************/
/* MODIFICATIONS                                                     */
/*********************************************************************/
/* DATE      PROGRAMMER                                              */
/*                                                                 */
/* 02/26/16    Rose Centonze    E5290                              */
/*             Added company code parm and add to opnqry select    */
/*                                                                 */
/*********************************************************************/

             PGM        PARM(&REOPEN &PPCOCD)

             DCL        VAR(&PPCOCD) TYPE(*CHAR) LEN(3)            /* E5290 */

/* DEFINE THE VARIABLE REQUIRED IN THE COMMAND TO OVERRIDE THE     */
/* PRINT FILE TO A LASER PRINTER                                   */

             DCL        VAR(&CMD) TYPE(*CHAR) LEN(512)
             DCL        VAR(&REOPEN) TYPE(*CHAR) LEN(1)

             DCL        VAR(&QDATA)  TYPE(*CHAR) LEN(85) /* ADD 20 FOR COCD  */
             DCL        VAR(&LDBPDT) TYPE(*DEC)  LEN(8)
             DCL        VAR(&LDEPDT) TYPE(*DEC)  LEN(8)

             DCL        VAR(&HOLD) TYPE(*CHAR) LEN(10)
             DCL        VAR(&COPYS) TYPE(*DEC) LEN(2)
             DCL        VAR(&OUTQ) TYPE(*CHAR) LEN(10)

             CHGVAR     VAR(&LDBPDT) VALUE(%SST(*LDA 1 8))
             CHGVAR     VAR(&LDEPDT) VALUE(%SST(*LDA 9 8))


/*-------------------------------------------------------------------*/
/*  SETUP QUERY SELECTION                                            */
/*-------------------------------------------------------------------*/
/*  ALWAYS SELECT ONLY GROUPS IN CURRENT PERIOD                      */

             CHGVAR     VAR(%SST(&QDATA 1 10)) VALUE('HGCLDT +
                          *GE')
             CHGVAR     VAR(%SST(&QDATA 12 8)) VALUE(&LDBPDT)
             CHGVAR     VAR(%SST(&QDATA 21 15)) VALUE('*AND HGCLDT +
                          *LE')
             CHGVAR     VAR(%SST(&QDATA 37 8)) VALUE(&LDEPDT)


/*  ALWAYS SELECT ONLY GROUPS THAT HAVE NOT HAD ANY DATA PURGED      */

             CHGVAR     VAR(%SST(&QDATA 46 19)) VALUE('*AND HGPUFL +
                          *EQ "N"')

/*   SELECT COMPANY IN PARM          E5290                     */

             CHGVAR     VAR(%SST(&QDATA 66 15)) VALUE('*AND HGCOCD +
                          *EQ')
             CHGVAR     VAR(%SST(&QDATA 82 3)) VALUE(&PPCOCD)
/*-------------------------------------------------------------------*/
/*  RUN QUERY OVER CLOSED GROUPS                                     */
/*-------------------------------------------------------------------*/

             OVRDBF     FILE(HSL034I) SHARE(*YES)

             OPNQRYF    FILE((HSL034I)) QRYSLT(&QDATA) +
                          KEYFLD((HGFSBO) (HGPPCD) (HGFSCD) +
                          (HGCLDT)) SEQONLY(*YES)

/*-------------------------------------------------------------------*/
/* SET PRINT PARAMETERS FROM LDA                                     */
/*-------------------------------------------------------------------*/

 HOLD:       IF         COND(%SST(*LDA 411 1) = 'Y') THEN(CHGVAR +
                          VAR(&HOLD) VALUE(*YES))
             ELSE       CMD(CHGVAR VAR(&HOLD) VALUE(*NO))

 COPYS:      CHGVAR     VAR(&COPYS) VALUE(%SST(*LDA 412 1))
             IF         COND(&COPYS = 0) THEN(CHGVAR VAR(&COPYS) +
                          VALUE(1))

 OUTQ:       CHGVAR     VAR(&OUTQ) VALUE(%SST(*LDA 413 10))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(%SST(*LDA 401 10)))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(*JOB))

/*------------------------------------------------------------------*/
/* OVERRIDE PRINT FILE                                              */
/*------------------------------------------------------------------*/

/* USE THE CUSTOMIZED SEABOARD COMMAND TO OVERRIDE THE PRINT FILE.  */
/* THIS COMMAND ALLOWS THE REPORT TO BE GENERATED ON A STANDARD LINE*/
/* PRINTER OR A LASER PRINTER. TO USE THIS OVERRIDE YOU MUST DECLARE*/
/* A COMMAND VARIABLE AND FOLLOW THE OVERRIDE WITH A CALL TO        */
/* QCMDEXC.                                                         */

/* THIS REPORT CAN PRINT AT 10 CPI IN 132 CHARACTERS                */

             SBDPRTOVR  CMDVAR(&CMD) FILE(QPRINT) OUTQ(&OUTQ) +
                          WIDTH(132) COPIES(&COPYS) HOLD(&HOLD)

             CALL       PGM(QCMDEXC) PARM(&CMD 512)

/*-------------------------------------------------------------------*/
/*  CALL PROGRAM TO UNCLOSE THE SPECIFIED GROUPS AND PRINT REPORT    */
/*-------------------------------------------------------------------*/

             CALL       PGM(HP393) PARM(&REOPEN)

/*-------------------------------------------------------------------*/
/* DELETE FILE OVERRIDES                                             */
/*-------------------------------------------------------------------*/

             CLOF       OPNID(HSL034I)
             DLTOVR     FILE(HSL034I QPRINT)


 EXIT:       ENDPGM

