     A*%%TS  SD  20040412  083057  ISLRAMS     REL-V5R2M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION
     A*  DISPLAY FILE:  HPD5951- SELECT OPEN BGF OR GROW FINISH GROUP FOR
     A*                          A MARKET SALE
     A*  CREATED BY:    LEANNE FEDOR
     A*  DATE:          04/12/04
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA12(12 'PREVIOUS')
     A N82                                  ROLLUP(25 'PAGE FORWARD')
     A*--------------------------------------------------------------*
     A* REFERENCE WINDOW RECORD FORMAT 1
     A*--------------------------------------------------------------*
     A          R HP5951R1
     A*%%TS  SD  19991110  083626  ISLRAMS     REL-V4R2M0  5769-PW1
     A                                      TEXT('Reference window')
     A                                      WINDOW(*DFT 21 70)
     A                                      WDWBORDER((*DSPATR RI) (*CHAR '    -
     A                                          '))
     A*--------------------------------------------------------------*
     A* RECORD FORMAT ONE
     A*--------------------------------------------------------------*
     A          R HP5951R2
     A                                      TEXT('Written in RPG pgm to prevent-
     A                                       screen from clearing')
     A                                      CLRL(*NO)
     A                                      OVERLAY
     A*--------------------------------------------------------------*
     A* RECORD FORMAT TWO
     A*--------------------------------------------------------------*
     A* THIS FORMAT IS NOT WRITTEN IN THE RPG PGM.  IT PREVENTS THE
     A* CALLING_PROGRAM_SCREEN FROM BEING CLEARED.
     A*--------------------------------------------------------------*
     A          R HP5951R3
     A                                      TEXT('Dummy format not written in R-
     A                                      PG pgm.')
     A                                      ASSUME
     A                                 11  1' '
     A*--------------------------------------------------------------*
     A* SUBFILE FORMAT ONE
     A*--------------------------------------------------------------
     A          R HP5951S1                  SFL
     A*%%TS  SD  20021202  154029  ISLRAMS     REL-V5R1M0  5722-WDS
     A                                      TEXT('subfile record format one')
     A            HDRRN1         4S 0H
     A            DFBLCD    R        O 10  1REFFLD(@@BLCD)
     A            DFRMCD    R        O 10 11REFFLD(HSRREF/HGRMCD *LIBL/HSPREF)
     A            DFHGCD    R        O 10 18REFFLD(HGHGCD)
     A            DFPTCD    R        O 10 27REFFLD(HSRREF/@@PTCD *LIBL/HSPREF)
     A            DFPPCD    R        O 10 34REFFLD(HSRREF/@@PPCD *LIBL/HSPREF)
     A            DFGTCD    R        O 10 43REFFLD(HSRREF/@@GTCD *LIBL/HSPREF)
     A            DFOPDTMDY R        O 10 48REFFLD(HSRREF/@@DT06 *LIBL/HSPREF)
     A                                      EDTWRD('  /  /  ')
     A            DFGSCD    R        O 10 58REFFLD(HSRREF/@@GSCD *LIBL/HSPREF)
     A*--------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*--------------------------------------------------------------
     A          R HP5951C1                  SFLCTL(HP5951S1)
     A*%%TS  SD  20040412  083057  ISLRAMS     REL-V5R2M0  5722-WDS
     A                                      SFLSIZ(0050)
     A                                      SFLPAG(0008)
     A                                      WINDOW(HP5951R1)
     A                                      TEXT('control format one')
     A                                      OVERLAY
     A                                      SFLCSRRRN(&HDRRN)
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND(*MORE)
     A                                      RMVWDW
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            HDRRN          5S 0H
     A            SDPGM     R        O  1  1REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 14'SELECT OPEN BGF OR GROW FINISH GRO-
     A                                      UP FOR A MARKET SALE'
     A                                      DSPATR(HI)
     A                                  3  1'Position Cursor, Press Enter'
     A                                  5  1'Farm site:'
     A            C1FSCD    R        O  5 12REFFLD(HSRREF/@@FSCD *LIBL/HSPREF)
     A                                      EDTCDE(Z)
     A            C1FSNM    R        O  5 19REFFLD(FSFSNM)
     A                                  7 27'-Production-'
     A                                      DSPATR(HI)
     A                                  7 41'Group'
     A                                      DSPATR(HI)
     A                                  7 50'Date'
     A                                      DSPATR(HI)
     A                                  8  1'Building'
     A                                      DSPATR(HI)
     A                                  8 11'Room'
     A                                      DSPATR(HI)
     A                                  8 18'Group'
     A                                      DSPATR(HI)
     A                                  8 27'Type'
     A                                      DSPATR(HI)
     A                                  8 34'Phase'
     A                                      DSPATR(HI)
     A                                  8 42'Type'
     A                                      DSPATR(HI)
     A                                  8 49'Opened'
     A                                      DSPATR(HI)
     A                                  8 58'St'
     A                                      DSPATR(HI)
     A            C1BLCD    R        B  9  1REFFLD(HSRREF/@@BLCD *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A            C1RMCD    R        B  9 11REFFLD(HSRREF/@@RMCD *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A*
     A*-------------------------------------------------------------------------
     A* COMMAND LINE
     A*-------------------------------------------------------------------------
     A          R HP5951K1
     A                                      OVERLAY
     A                                      WINDOW(HP5951R1)
     A                                 19  1'F12=Previous'
