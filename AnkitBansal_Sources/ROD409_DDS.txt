     A*%%TS  SD  20080609  091704  ISLRAMS     REL-V5R4M0  5722-WDS
     A*****************************************************************
     A*
     A*  ENVIRONMENT:   PORK DIVISION
     A*  SYSTEM:        RESOURCE OPTIMIZATION
     A*  DISPLAY FILE:  ROD409
     A*  TITLE:         WORK WITH BYPRODUCT VALUES
     A*  PROGRAMMER:    LEANNE RAMSEY
     A*  CREATED:       02/12/07
     A*
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/ROPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A                                      CF04(04 'PROMPT')
     A                                      CF05(05 'REFRESH')
     A                                      CA07(07 'REPORT')
     A                                      CF17(17 'PRINT OPTIONS')
     A                                      CF20(20 'WRKSPLF')
     A*
     A*
     A*---------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*---------------------------------------------------------------
     A          R RO409S1                   SFL
     A*%%TS  SD  20080609  091704  ISLRAMS     REL-V5R4M0  5722-WDS
     A  85                                  SFLNXTCHG
     A            HDRRN1         4Y 0H
     A            HDWEDT    R        H      REFFLD(@@WEDT)
     A            D1OPT          2A  B 10  2CHGINPDFT(UL)
     A  30                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A N30                                  DSPATR(HI)
     A            D1CGCD    R        O 10  6REFFLD(ROREF/@@CGCD *LIBL/ROPREF)
     A            D1SGCD    R        O 10 12REFFLD(ROREF/@@SGCD *LIBL/ROPREF)
     A            D1CONO    R        O 10 18REFFLD(ROREF/@@CONO *LIBL/ROPREF)
     A                                      EDTCDE(K)
     A            D1WEMDY   R        O 10 23REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  ')
     A            D1PUPR    R        O 10 32REFFLD(ROREF/@@PUPR *LIBL/ROPREF)
     A                                      EDTCDE(K)
     A            D1NPUPR   R        O 10 44REFFLD(ROREF/@@NPUPR *LIBL/ROPREF)
     A                                      EDTCDE(K)
     A            D1TOTCO   R        O 10 56REFFLD(ROREF/@@PCO *LIBL/ROPREF)
     A                                      EDTCDE(K)
     A*
     A*---------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*---------------------------------------------------------------
     A          R RO409C1                   SFLCTL(RO409S1)
     A*%%TS  SD  20080609  091704  ISLRAMS     REL-V5R4M0  5722-WDS
     A                                      SFLSIZ(0050)
     A                                      SFLPAG(0012)
     A N82                                  ROLLUP(25 'PAGE FORWARD')
     A                                      SETOF(52)
     A                                      RTNCSRLOC(&RCD &FLD)
     A                                      OVERLAY
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 25'RESOURCE OPTIMIZATION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USR)
     A                                      DSPATR(HI)
     A                                  2 26'WORK WITH BYPRODUCT VALUES'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@JOB)
     A                                      DSPATR(HI)
     A                                  3 30'                  '
     A                                  4  2'5=View ByProduct Items'
     A                                  4 26'6=Print ByProduct Values Report'
     A                                  5  8'?'
     A                                      DSPATR(HI)
     A                                  6  6'-Meat Cost-'
     A                                      DSPATR(HI)
     A                                  6 25'Week'
     A                                      DSPATR(HI)
     A                                  7 12'Sub'
     A                                      DSPATR(HI)
     A                                  7 18'Comp'
     A                                      DSPATR(HI)
     A                                  7 24'Ending'
     A                                      DSPATR(HI)
     A                                  7 34'Produced'
     A                                      DSPATR(HI)
     A                                  7 46'Net Prod'
     A                                      DSPATR(HI)
     A                                  7 61'Total'
     A                                      DSPATR(HI)
     A                                  8  2'Opt'
     A                                      DSPATR(HI)
     A                                  8  6'Group'
     A                                      DSPATR(HI)
     A                                  8 12'Group'
     A                                      DSPATR(HI)
     A                                  8 18'Nbr'
     A                                      DSPATR(HI)
     A                                  8 25'Date'
     A                                      DSPATR(HI)
     A                                  8 34'Price/Lb'
     A                                      DSPATR(HI)
     A                                  8 46'Price/Lb'
     A                                      DSPATR(HI)
     A                                  8 59'Cost/Lb'
     A                                      DSPATR(HI)
     A            C1CGCD    R        B  9  6REFFLD(ROREF/@@CGCD *LIBL/ROPREF)
     A                                      DSPATR(HI)
     A  52                                  DSPATR(PC)
     A            C1SGCD    R        B  9 12REFFLD(ROREF/@@SGCD *LIBL/ROPREF)
     A                                      DSPATR(HI)
     A            C1CONO    R        B  9 18REFFLD(ROREF/@@CONO *LIBL/ROPREF)
     A                                      DSPATR(HI)
     A                                      EDTCDE(K)
     A            C1WEMDY   R        B  9 23REFFLD(@@DT06)
     A                                      DSPATR(HI)
     A                                      EDTWRD('  /  /  -')
     A*
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R RO409K1
     A*%%TS  SD  20071112  104258  ISLRAMS     REL-V5R4M0  5722-WDS
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A                                 23 11'F4=Prompt'
     A                                 23 22'F5=Refresh'
     A                                 23 34'F7=Report'
     A                                 23 45'F17=Print Options'
     A                                 23 64'F20=Spool Files'
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *---------------------------------------------------------------
     A          R RO409ES                   SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE CONTROL FORMAT
      *---------------------------------------------------------------
     A          R RO409EC                   SFLCTL(RO409ES)
      *
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
