/*****************************************************************  */
/*                                                                  */
/*  PROGRAM NUMBER --> HP480CL                                      */
/*  PROGRAM NAME ----> HOG SALES ACTIVITY REPORTS                   */
/*  PROGRAMMER ------> LEANNE FEDOR                                 */
/*  DATE ------------> 05/04/95                                     */
/*                                                                  */
/*  PROGRAM PURPOSE -> THIS CL GENERATES A WORKFILE AND THEN        */
/*                     EXECUTES THE PRINT PROGRAM.                  */
/*                     VALUES ARE PASSED THRU THE LDA FROM HP480.   */
/*                                                                  */
/*******************************************************************/
/*  MODIFIED:                                                      */
/*  08/21/95  LEANNE FEDOR                                         */
/*            ADDED OPEN QUERY ON SALES HEADER FOR THE MOVEMENT    */
/*            REWRITE.                                             */
/*                                                                  */
/*  08/30/95  LEANNE FEDOR                                         */
/*            BUSINESS OFFICE IS NOW AN OPTIONAL SELECTION.        */
/*                                                                 */
/*  02/06/96  LEANNE FEDOR                                         */
/*            RECOMPILE ONLY. NEW FIELD 'BOL PRINTED COUNT' ADDED  */
/*            TO SALES MOVEMENT HEADER FILE.                       */
/*                                                                 */
/*  04/08/96  LEANNE FEDOR                                         */
/*            ADDED PRINTER LOGIC TO ALLOW REPORT TO BE            */
/*            GENERATED ON LASER PRINTER.                          */
/*                                                                 */
/* 05/21/96  LEANNE FEDOR                                          */
/*           IN PREPARATION FOR HAVING TWO BOXES (CISC AND RISC),  */
/*           REPLACED STANDARD CREATE DUPLICATE OBJECT COMMAND     */
/*           WITH A COMMAND CUSTOMIZED BY JOHN ERICSON. THIS       */
/*           COMMAND WILL RETRIEVE/USE THE FILE FROM THE APPRO-    */
/*           PRIATE LIBRARY INSTEAD OF USING HARDCODED 'PRKFLIB'.  */
/*           THE CUSTOMIZED COMMAND IS CRTDUPOBJL FOR 'CREATE      */
/*           DUPLICATE OBJECT FROM LIBRARY'.                       */
/*                                                                 */
/* 06/19/97  LEANNE FEDOR                                          */
/*           CHANGED OUT QUEUE LOGIC.                              */
/*                                                                 */
/* 12/18/02  LEANNE FEDOR                                          */
/*           'PRODUCTION TYPE' AND 'PRODUCTION PHASE' WERE REMOVED */
/*           FROM THE SALES MOVEMENT HEADER FILE. SO, WE REMOVED   */
/*           THE OPEN QUERY SELECTIONS ON THESE FIELDS. THE        */
/*           CHECKS WILL NOW BE MADE IN THE WORKFILE BUILD PROGRAM.*/
/*******************************************************************/

             PGM

/* DEFINE THE VARIABLE REQUIRED IN THE COMMAND TO OVERRIDE THE     */
/* PRINT FILE TO A LASER PRINTER                                   */

             DCL        VAR(&CMD) TYPE(*CHAR) LEN(512)

             DCL        VAR(&QDATA) TYPE(*CHAR) LEN(200)

             DCL        VAR(&LDFSBO) TYPE(*CHAR) LEN(5)
             DCL        VAR(&LDFYMD) TYPE(*DEC) LEN(8)
             DCL        VAR(&LDTYMD) TYPE(*DEC) LEN(8)
             DCL        VAR(&LDFSCD) TYPE(*DEC) LEN(5)
             DCL        VAR(&LDSTCD) TYPE(*CHAR) LEN(5)
             DCL        VAR(&LDTPCD) TYPE(*CHAR) LEN(1)
             DCL        VAR(&LDHGSN) TYPE(*DEC) LEN(7)
             DCL        VAR(&LDCVNO) TYPE(*DEC) LEN(8)
             DCL        VAR(&LDRPFL) TYPE(*CHAR) LEN(1)

             DCL        VAR(&HOLD) TYPE(*CHAR) LEN(10)
             DCL        VAR(&COPYS) TYPE(*DEC) LEN(2)
             DCL        VAR(&OUTQ) TYPE(*CHAR) LEN(10)

             CHGVAR     VAR(&LDFSBO) VALUE(%SST(*LDA 1 5))
             CHGVAR     VAR(&LDFYMD) VALUE(%SST(*LDA 6 8))
             CHGVAR     VAR(&LDTYMD) VALUE(%SST(*LDA 14 8))
             CHGVAR     VAR(&LDSTCD) VALUE(%SST(*LDA 32 5))
             CHGVAR     VAR(&LDFSCD) VALUE(%SST(*LDA 37 5))
             CHGVAR     VAR(&LDCVNO) VALUE(%SST(*LDA 42 8))
             CHGVAR     VAR(&LDHGSN) VALUE(%SST(*LDA 50 7))
             CHGVAR     VAR(&LDRPFL) VALUE(%SST(*LDA 57 1))
             CHGVAR     VAR(&LDTPCD) VALUE(%SST(*LDA 58 1))


/*-------------------------------------------------------------------*/
/* SET UP QUERY OVER THE SALES HEADER                                */
/*-------------------------------------------------------------------*/

             CHGVAR     VAR(&QDATA) VALUE(' ')

/*  SELECT ALL SALES RECORDS IN THE DATE RANGE USING RECEIVED DATE   */

             CHGVAR     VAR(%SST(&QDATA 1 10)) VALUE('SHRCDT *GE')
             CHGVAR     VAR(%SST(&QDATA 12 8)) VALUE(&LDFYMD)
             CHGVAR     VAR(%SST(&QDATA 21 15)) VALUE('*AND SHRCDT +
                          *LE')
             CHGVAR     VAR(%SST(&QDATA 37 8)) VALUE(&LDTYMD)

/*  IF BUSINESS OFFICE                                               */

             IF         COND(&LDFSBO *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 46 17)) VALUE('*AND SHFSBO +
                          *EQ "')
             CHGVAR     VAR(%SST(&QDATA 63 5)) VALUE(&LDFSBO)
             CHGVAR     VAR(%SST(&QDATA 68 1)) VALUE('"')
             ENDDO

/*  IF FARM SITE                                                 */

             IF         COND(&LDFSCD *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 118 15)) VALUE('*AND SHFSCD +
                          *EQ')
             CHGVAR     VAR(%SST(&QDATA 134 5)) VALUE(&LDFSCD)
             ENDDO

/*  IF CUSTOMER                                                  */

             IF         COND(&LDCVNO *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 140 15)) VALUE('*AND SHCVNO +
                          *EQ')
             CHGVAR     VAR(%SST(&QDATA 156 8)) VALUE(&LDCVNO)
             ENDDO

/*  IF SALES TYPE                                                */

             IF         COND(&LDSTCD *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 165 17)) VALUE('*AND SHSTCD +
                          *EQ "')
             CHGVAR     VAR(%SST(&QDATA 182 5)) VALUE(&LDSTCD)
             CHGVAR     VAR(%SST(&QDATA 187 1)) VALUE('"')
             ENDDO


/* RUN QUERY OVER THE SALES HEADER                              */

             OVRDBF     FILE(HSP084) SHARE(*YES)
             OPNQRYF    FILE((HSP084)) QRYSLT(&QDATA) KEYFLD(*FILE) +
                          SEQONLY(*YES)

/*-------------------------------------------------------------------*/
/* SET UP QUERY OVER THE SALES DETAIL                                */
/*-------------------------------------------------------------------*/

             CHGVAR     VAR(&QDATA) VALUE(' ')

/*  IF HOG GROUP                                                     */

             IF         COND(&LDHGSN *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 1 12)) VALUE('SGHGSN +
                          *EQ ')
             CHGVAR     VAR(%SST(&QDATA 13 7)) VALUE(&LDHGSN)

             OVRDBF     FILE(HSP085) SHARE(*YES)
             OPNQRYF    FILE((HSP085)) QRYSLT(&QDATA) KEYFLD(*FILE) +
                          SEQONLY(*YES)
             ENDDO


/*-------------------------------------------------------------------*/
/* SET UP QUERY OVER THE CHECK DETAIL FILE                           */
/*-------------------------------------------------------------------*/

             CHGVAR     VAR(&QDATA) VALUE(' ')

/*  IF TYPE OF PAYMENT                                               */

             IF         COND(&LDTPCD *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 1 12)) VALUE('CDTPCD *EQ "')
             CHGVAR     VAR(%SST(&QDATA 13 1)) VALUE(&LDTPCD)
             CHGVAR     VAR(%SST(&QDATA 14 1)) VALUE('"')

             OVRDBF     FILE(HSL064B) SHARE(*YES)
             OPNQRYF    FILE((HSL064B)) QRYSLT(&QDATA) KEYFLD(*FILE) +
                          SEQONLY(*YES)
             ENDDO

/*********************************************************************/
/* CREATE WORKFILE                                                   */
/*********************************************************************/

             CRTDUPOBJL OBJ(HSP380) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)

/* GENERATE WORKFILE AND DELETE QUERY OVERRIDES                  */

             CALL HP380

             CLOF       OPNID(HSP084)
             DLTOVR     FILE(HSP084)

             IF         COND(&LDTPCD *NE ' ') THEN(DO)
             CLOF       OPNID(HSL064B)
             DLTOVR     FILE(HSL064B)
             ENDDO

             IF         COND(&LDHGSN *NE 0) THEN(DO)
             CLOF       OPNID(HSP085)
             DLTOVR     FILE(HSP085)
             ENDDO


/*******************************************************************/
/* SET UP PRINT PARAMETERS FROM LDA                                */
/*******************************************************************/

 HOLD:       IF         COND(%SST(*LDA 411 1) = 'Y') THEN(CHGVAR +
                          VAR(&HOLD) VALUE(*YES))
             ELSE       CMD(CHGVAR VAR(&HOLD) VALUE(*NO))

 COPYS:      CHGVAR     VAR(&COPYS) VALUE(%SST(*LDA 412 1))
             IF         COND(&COPYS = 0) THEN(CHGVAR VAR(&COPYS) +
                          VALUE(1))

 OUTQ:       CHGVAR     VAR(&OUTQ) VALUE(%SST(*LDA 413 10))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(%SST(*LDA 401 10)))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(*JOB))

/*------------------------------------------------------------------*/
/* OVERRIDE PRINT FILE                                              */
/*------------------------------------------------------------------*/

/* USE THE CUSTOMIZED SEABOARD COMMAND TO OVERRIDE THE PRINT FILE.  */
/* THIS COMMAND ALLOWS THE REPORT TO BE GENERATED ON A STANDARD LINE*/
/* PRINTER OR A LASER PRINTER. TO USE THIS OVERRIDE YOU MUST DECLARE*/
/* A COMMAND VARIABLE AND FOLLOW THE OVERRIDE WITH A CALL TO        */
/* QCMDEXC.                                                         */

/* THIS REPORT IS CONDENSED PRINT AT 15 CPI; SO, ON A LASER PRINTER */
/* FORCE IT TO DRAWER '2' FOR LEGAL PAPER AND FORCE 'LANDSCAPE'     */

             SBDPRTOVR  CMDVAR(&CMD) FILE(QPRINT) OUTQ(&OUTQ) +
                          WIDTH(198) LANDSCAPE(*YES) DRAWER(2) +
                          COPIES(&COPYS) HOLD(&HOLD)

             CALL       PGM(QCMDEXC) PARM(&CMD 512)

/*------------------------------------------------------------------*/
/*  RUN REPORT                                                      */
/*------------------------------------------------------------------*/

             CALL       PGM(HP680)

             DLTOVR     FILE(QPRINT)

             ENDPGM

