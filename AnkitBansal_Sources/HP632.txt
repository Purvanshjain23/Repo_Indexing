      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      HOG PRODUCTION
      * PROGRAM:     HP632
      * TITLE:       LISTING OF REPORT CATEGORIES/REPORT CODES
      * PROGRAMMER:  MARY PALMER
      * CREATED:     06/10/94
      *
      * FUNCTION: BATCH PROGRAM TO LIST THE RECORDS IN THE
      *           REPORT CATEGORY MASTER FILE  ALONG WITH THE
      *           REPORT CODES ASSIGNED TO EACH REPORT CATEGORY
      *           IN THE REPORT CODE FILE.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 04/11/96  LEANNE FEDOR
      *           THE FIELD RPOIFL-OPERATIONAL FLAG WAS NEVER USED.
      *           TO AVOID RECOMPILING, WE HAVE LEFT THE FIELD IN
      *           THIS PHYSICAL FILE AND IN THE FIELD REFERENCE FILE.
      *           THE FIELD HAS BEEN REMOVED FROM THIS LISTING.
      *           THE FIELD CAN BE REMOVED FROM THE FIELD
      *           REFERENCE FILE AND HSP006 ANY TIME THAT YOU WANT TO
      *           RECOMPILE ALL THE PROGRAMS.
      *
      * 09/03/03  LeAnne Fedor
      *           Added new field 'sequence' to report. And, now sort listing
      *           by sequence.
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fhsl005a   IF   E           K DISK
      *    REPORT CATEGORY
      *
      *
     FHSL006B   IF   E           K DISK
      *    REPORT CODE
      *
      *
     FQPRINT    O    F  132        PRINTER OFLIND(*INOF)
      *
      /EJECT
      ****************************************************************
      * INPUT SPECS
      ****************************************************************
      *
      *---------------------------------------------------------------
      * STANDARD PROGRAM STATUS DATA STRUCTURE
      *---------------------------------------------------------------
      *    EXTERNALLY DEFINED AS UTPGFR (RECORD FORMAT: PGMDSR)
     D PGMDS         ESDS                  EXTNAME(UTPGFR)
      *
      *
      *---------------------------------------------------------------
      *  NAMED CONSTANTS
      *---------------------------------------------------------------
      *
     D YES             C                   CONST('Y')
     D NO              C                   CONST('N')
      *
      /EJECT
      ****************************************************************
      * MAIN LINE
      ****************************************************************
      *
      * PREPARE TO READ THE BREED STOCK MASTER FILE AND
      * PRINT HEADINGS FOR FIRST TIME THROUGH
      *
     C     *LOVAL        SETLL     hsl005a
     C                   EXCEPT    HDRPT
      *
      * READ AND PRINT ALL RECORDS IN THE REPORT CATEGORY MASTER.
      * FOR EACH RECORD READ, RETRIEVE THE REPORT CODES ASSIGNED TO
      * THAT REPORT CATEGORY FROM THE REPORT CODE MASTER.
      *
     C     *IN92         DOUEQ     *ON                                          MAIN DO
     C                   READ      hsl005a                                92
      *
     C     *IN92         IFEQ      *OFF                                         IF NOT EOF
      *
     C     *INOF         IFEQ      *ON
     C                   EXCEPT    HDRPT
     C                   ENDIF
      *
      * PRINT REPORT CATEGORY LINE
      *
     C                   EXCEPT    DTTYPE
     C                   MOVEL     YES           PRTFLG
      *
      * RETRIEVE/PRINT ALL REPORT CODES FOR THIS REPORT CATEGORY
      *
     C                   EXSR      $CODES
     C                   ENDIF                                                  IF NOT EOF
      *
      *  IF THE MASTER FILE WAS EMPTY AND NO RECORDS WERE PRINTED,
      *  PRINT THE STANDARD 'NO DATA...' ERROR MESSAGE
      *
     C     PRTFLG        IFEQ      NO
     C                   EXCEPT    NODATA
     C                   ENDIF
      *
     C                   ENDDO                                                  MAIN DO
      *
     C                   SETON                                        LR
      /EJECT
      ****************************************************************
      * SUBROUTINES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * $CODES - RETRIEVE/PRINT REPORT CODES
      *---------------------------------------------------------------
      *
     C     $CODES        BEGSR
      *
     C     CACACD        SETLL     RPREC
      *
     C     *IN94         DOUEQ     *ON
     C     CACACD        READE     HSL006B                                94
     C     *IN94         IFEQ      *OFF
      *
     C     *INOF         IFEQ      *ON
     C                   EXCEPT    HDRPT
     C                   EXCEPT    DTTYPE
     C                   ENDIF
      *
     C                   EXCEPT    DTRC
     C                   ENDIF
     C                   ENDDO
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * *INZSR - INITIALIZATION SUBROUTINE
      *---------------------------------------------------------------
      *
     C     *INZSR        BEGSR
      *
      * SET PRINT FLAG TO DETERMINE IF ANY RECORDS WERE PRINTED
      *
     C                   MOVEL     NO            PRTFLG            1
      *
     C                   MOVE      *ALL'-'       DASH             90
      *
      * RETRIEVE TIME FOR REPORT HEADING
      *
     C                   TIME                    RTIME             6 0
      *
     C                   ENDSR
      *
      /EJECT
      *-------------------------------------------------------------
      * REPORT LAYOUT
      *-------------------------------------------------------------
      *
      *-------------------------------------------------------------
      * REPORT HEADING LINES
      *-------------------------------------------------------------
      *
     OQPRINT    E            HDRPT          1 01
     O                       SDPGM               10
     O                                           48 'HOG PRODUCTION SYSTEM'
     O                                           80 'DATE'
     O                       UDATE         Y     90
      *
     O          E            HDRPT          1
     O                       SDUSR               10
     O                                           38 'LISTING OF REPORT CAT'
     O                                           58 'EGORIES/REPORT CODES'
     O                                           80 'TIME'
     O                       RTIME               90 '  :  :  '
      *
     O          E            HDRPT          2
     O                                           80 'PAGE'
     O                       PAGE          Z     90
      *
     O          E            HDRPT          1
     O                                           26 '---------- CATEGORY ------'
     O                                           30 '----'
     O                                           61 '--------- REPORT -------'
     O                                           63 '--'
     O                                           80 'USER'
      *
     O          E            HDRPT          1
     O                                            6 'SEQ'
     O                                           27 'CODE  DESCRIPTION'
     O                                           53 'CODE  SHORT NAME'
     O                                           63 'LONG NAME'
     O                                           83 'CONTROLLED'
     O                                           89 'SEQ'
      *
      *
     O          E            HDRPT          1
     O                       DASH                90
      *
      *-------------------------------------------------------------
      * REPORT CATEGORY DETAIL LINE
      *-------------------------------------------------------------
      *
     O          E            DTTYPE      1
     O                       CASEQ         l      7
     O                       CACACD              15
     O                       CACADS              36
      *
      *-------------------------------------------------------------
      * REPORT CODE LINE
      *-------------------------------------------------------------
      *
     O          E            DTRC        1
     O                       RPRPCD              42
     O                       RPRSDS              53
     O                       RPRLDS              74
     O                       RPUCFL              79
     O                       RPSEQ         M     90
      *
      *-------------------------------------------------------------
      * NO DATA MESSAGE LINE
      *-------------------------------------------------------------
      *
     O          E            NODATA      1
     O                                           19 'NO DATA MEETS YOUR'
     O                                           38 'SELECTION CRITERIA'
