/********************************************************************/
/*                                                                  */
/*  SYNOPSIS -  JOB CONTROL PROMPT FOR PRINTER OVERRIDE INFORMATION */
/*              GENERIC PROMPTING FUNCTION USED TO PRINT AN INVOICE */
/*              OR THE PRE-INVOICE REGISTER OR REPRINT AN INVOICE.  */
/*                                                                  */
/*  SYSTEM    - ORDER MANAGEMENT SYSTEM                             */
/*  PROGRAM   - PRT INVOICE/PRE-INVOICE REGISTER                    */
/*  DATE      - 07/12/95                                            */
/*                                                                  */
/*      PARAMETERS:                                                 */
/*                                                                  */
/*     O    &RETURN     - RETURN CODE                               */
/*     I    &PGM        - CL PROGRAM TO SUBMIT                      */
/*     I    &APPLIC     - APPLICATION CODE                          */
/*     I    &BILTYP     - BILLING ACTIVITY TYPE (INVOICE, MEMO)     */
/*     I    &DESC       - PRINT PROGRAM DESCRIPTION (FOR PROMPT     */
/*                         SCREEN)                                  */
/*     B    &OUTQ       -  OUTPUT QUEUE                             */
/*     B    &HOLD       -  HOLD STATUS                              */
/*     B    &SAVE       -  SAVE STATUS                              */
/*     B    &NQSTAT     -  INDICATOR WHETHER MUST RUN IN NIGHT Q    */
/*     B    &NIGHTQ     -  NIGHT JOB QUEUE                          */
/*     I    &COMPNYN    -  COMPANY NUMBER (PASSED)                  */
/*     B    &THRDATN    -  THRU SHIP DATE (PASSED)                  */
/*     I    &LOADIDN    -  LOAD ID   (PASSED)                       */
/*     I    &ORDRN      -  ORDER NO. (PASSED)                       */
/*     I    &RFLAG      -  REPRNT FLAG (PASSED)                     */
/*     B    &COPYN      -  NO. OF COPIES  (PASSED)                  */
/* THE FOLLOWING 2 PARAMETERS DON'T APPLY FOR INVOICE PRINT/REPRINT */
/*     I    &PRTREG     -  PRINT PRE-INVOICE REGISTER (PASSED)      */
/*     I    &PRTEXC     -  PRINT PRE-INVOICE EXCEPTION (PASSED)     */
/********************************************************************/
/* RMC 5/30/03 &JOBNME IS A NEW PARM AND MAY BE DIFFERENT THAN &PGM NOW */
/* RMC 4/25/05 DONT LOOP THRU PRINT OPTIONS PRMPT TO ALLOW MULT PRINTS.*/
/*             PRINT THE INV EXCEPTION ON PRKPRT05 AUTOMATICALLY IN  */
/*             PDULPCLP FOR DAMON GINTHER... PER JEFF SHERBONDY    */
/* SLM 10/26/05 PASS THE PROCESS CUSTOMER TYPE                      */
/* SLM 11/04/05 PASS THE PROCESS CUSTOMER TYPE FOR THE INVOICE SIDE */
/*              PDR4PCLP                                            */
/********************************************************************/

 PDTLUPR:    PGM PARM(&RETURN &PGM &PGM2 &APPLIC &JOBNME &JOBNUM +
                &BILTYP   &DESC &OUTQ &HOLD &SAVE &NQSTAT &NIGHTQ +
                          &COMPNYN &THRDATN &LOADIDN &ORDRN &COPYN +
                          &RFLAG &PRTREG &PRTEXC &PRTINV &ARCUSTP +
                          &PRCUSTT)
/* PARMS                                                                      */
        DCL    &RETURN     *CHAR   (7)           /* Return Code               */
        DCL    &APPLIC     *CHAR   (06)          /* Application Code          */
        DCL    &BILTYP     *CHAR   (01)          /* Bill Type (Invoice,Memo)  */
        DCL    &RFLAG      *CHAR   (01)          /* REPRINT FLAG              */
        DCL    &PGM        *CHAR   (10)          /* CL to Submit              */
        DCL    &PGM2       *CHAR   (10)          /* CL to Submit              */
        DCL    &JOBNME     *CHAR   (10)          /* JOB NAME-PGM NAME         */
        DCL    &JOBNUM     *CHAR   (6)           /* JOB NUMBER                */
        DCL    &DESC       *CHAR   (25)          /* PRT Pgm Description       */
        DCL    &COPYN      *DEC                  /* No. of Copies             */
        DCL    &OUTQ       *CHAR   (10)          /* Output Queue              */
        DCL    &HOLD       *CHAR   (4)           /* Hold Status               */
        DCL    &SAVE       *CHAR   (4)           /* Save Status               */
        DCL    &NQSTAT     *CHAR   (01)          /* If MUST run in Night Q    */
        DCL    &NIGHTQ     *CHAR   (10)          /* Night Job Queue           */
        DCL    &COMPNYN    *DEC                  /* Company Number (Passed)   */
        DCL    &THRDATN    *DEC                  /* Thru Ship Date (Passed)   */
        DCL    &LOADIDN    *DEC                  /* Load ID        (Passed)   */
        DCL    &ORDRN      *DEC                  /* Order Number   (Passed)   */
        DCL    &COPYN      *DEC                  /* No. of Copies  (Passed)   */
        DCL    &PRTREG     *CHAR   (01)          /* Print Pre-Invc Reg (Passd)*/
        DCL    &PRTEXC     *CHAR   (01)          /* Print Pre-Invc Exc (Passd)*/
        DCL    &PRTINV     *CHAR   (01)          /* Print INV OR PREINV(PASSD)*/
        DCL    &CT         *DEC    (1 0) VALUE(0)/* PRINTER COUNT */
        DCL    &XCT        *CHAR   (01)          /* Print INV OR PREINV(PASSD)*/
        DCL    &ARCUSTP    *DEC                  /* BILLTO CUSTOMER (PASSED)  */
        DCL    &PRCUSTT    *CHAR   (02)          /* PROCESS CUST TYPE (PASSED)*/

/* INTERNAL FIELDS                                                            */
        DCL    &COPY       *DEC    (2 0)         /* Number of copies          */

/* ERROR MESSAGE FIELDS                                                       */
        DCL    &MSG        *CHAR   (512)         /* message                   */
        DCL    &MSGID      *CHAR   (7)           /* message id                */
        DCL    &MSGL       *DEC    (5 0)

/********************************************************************/
/*   INITIALIZE VARIABLES                                           */
/********************************************************************/

             CHGVAR     VAR(&RETURN) VALUE('       ')
             CHGVAR     VAR(&COPY) VALUE(&COPYN)
/********************************************************************/
/*   MONITOR FOR ALL MESSAGES AT PROGRAM LEVEL                      */
/********************************************************************/

             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(MSGTRAP))

/********************************************************************/
/*   CALL PROMPT PROGRAM-- ONLY FIRST TIME THROUGH                  */
/********************************************************************/

             IF (&OUTQ *NE ' ') THEN(GOTO CMDLBL(SUBMIT))

PRINTER:
             CHGVAR     VAR(&CT) VALUE(&CT + 1)
             CHGVAR     VAR(&XCT) VALUE(&CT)
             IF         COND(&PRTINV *EQ 'P') THEN(CHGVAR VAR(&DESC) +
                          VALUE('Printer ' *BCAT &XCT *BCAT 'for Pre +
                          Invoice'))
             CALL       PGM(PDG0PVR) PARM(&RETURN &DESC &OUTQ &HOLD +
                          &SAVE &COPY &NQSTAT &NIGHTQ &APPLIC)
             CHGVAR     VAR(&COPYN) VALUE(&COPY)
             IF         COND(&RETURN *NE ' ') THEN(GOTO +
                          CMDLBL(USERQUIT))

/********************************************************************/
/*   SUBMIT PRINT CL PROGRAM                                        */
/********************************************************************/
/*******************************************************************/
/* 102605 SLM.                                                     */
/* CHANGE PARMS TO                                                 */
/* MATCH UP TO PGM, AN ARRAY WAS USED BECAUSE OF RUNNING           */
/* OUT OF PARMS AGAIN-THIS IS ONLY FOR PDULPCLP.                   */
/*******************************************************************/
 SUBMIT:

             IF         COND(&PGM *EQ 'PDULPCLP') THEN(DO)

             SBMJOB     CMD(CALL PGM(&PGM) PARM(&COMPNYN &ORDRN +
                          &THRDATN &LOADIDN &BILTYP &OUTQ &HOLD +
                          &SAVE &COPYN &NQSTAT &NIGHTQ &RFLAG +
                          &PRTREG &PRTEXC &JOBNME &JOBNUM &ARCUSTP +
                          &PRCUSTT)) JOB(&PGM) JOBQ(&NIGHTQ)
             ENDDO

             IF         COND(&PGM *NE 'PDULPCLP') THEN(DO)
             SBMJOB     CMD(CALL PGM(&PGM) PARM(&COMPNYN &LOADIDN +
                          &THRDATN &ORDRN &BILTYP &OUTQ &HOLD &SAVE +
                          &COPYN &NQSTAT &NIGHTQ &RFLAG &PRTREG +
                          &PRTEXC &JOBNME &JOBNUM &ARCUSTP +
                          &PRCUSTT)) JOB(&PGM) JOBQ(&NIGHTQ)

             ENDDO
             GOTO       CMDLBL(END)

/*-------------------------USER QUIT--------------------------------*/
 USERQUIT:   CHGVAR     VAR(&RETURN) VALUE('Y2U0005')
             GOTO       CMDLBL(END)

/*----------------------RECEIVE MESSAGES----------------------------*/

 MSGTRAP:    RCVMSG     MSGTYPE(*EXCP) MSGDTA(&MSG) MSGDTALEN(&MSGL) +
                          MSGID(&MSGID)
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(END))

             SNDPGMMSG  MSGID(&MSGID) MSGF(QCPFMSG) MSGDTA(%SST(&MSG +
                          1 &MSGL)) MSGTYPE(*ESCAPE)
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(END))

 END:        ENDPGM


/********    SBMJOB     CMD(CALL PGM(&PGM) PARM(&COMPNYN &LOADIDN +
                          &THRDATN &ORDRN &BILTYP &OUTQ &HOLD &SAVE +
                          &COPYN &NQSTAT &NIGHTQ &RFLAG &PRTREG +
                          &PRTEXC &JOBNME &ARCUSTP)) JOB(&PGM) +
                          JOBQ(&NIGHTQ) INQMSGRPY(*SYSRPYL) *****/

   /* RMC 4/25/05 DONT LOOP FOR MULTIPLE PRINTS  */
IGNORE:      IF         COND(&PRTINV *EQ 'P') THEN(DO)
/*           SNDPGMMSG  MSG('Print has been submitted for Printer' +
                          *BCAT &XCT)              +
             GOTO CMDLBL(PRINTER)                */
             ENDDO
