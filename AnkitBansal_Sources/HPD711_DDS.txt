     A*%%TS  SD  20030624  081311  ISLRAMS     REL-V5R2M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD711
     A*  TITLE:         VIEW EXPENSE SUBCLASSES
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  DATE:          06/23/03
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A                                      CF05(05 'REFRESH')
     A                                      CA12(12 'PREVIOUS')
     A                                      CF20(20 'WRKSPLF')
     A*
     A*
     A*---------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP711S1                   SFL
     A*%%TS  SD  20030619  152637  ISLRAMS     REL-V5R2M0  5722-WDS
     A  85                                  SFLNXTCHG
     A            HDRRN1         4Y 0H
     A            D1OPT          2A  B 11  2CHGINPDFT(UL)
     A  30                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A N30                                  DSPATR(HI)
     A            D1ESSEQ   R        O 11  7REFFLD(HSRREF/@@SEQ *LIBL/HSPREF)
     A                                      EDTCDE(L)
     A            D1ESCD    R        O 11 19REFFLD(HSRREF/@@ESCD *LIBL/HSPREF)
     A            D1ESDS    R        O 11 31REFFLD(HSRREF/@@DS20 *LIBL/HSPREF)
     A*
     A*---------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP711C1                   SFLCTL(HP711S1)
     A*%%TS  SD  20030624  081311  ISLRAMS     REL-V5R2M0  5722-WDS
     A                                      SFLSIZ(0050)
     A                                      SFLPAG(0011)
     A N82                                  ROLLUP(25 'PAGE FORWARD')
     A                                      SETOF(30)
     A                                      OVERLAY
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 26'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  2 25'VIEW EXPENSE SUBCLASSES'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  4  2'Expense class:'
     A                                      DSPATR(HI)
     A            C1ECCD    R        O  4 18REFFLD(HSRREF/@@ECCD *LIBL/HSPREF)
     A            C1ECDS    R        O  4 25REFFLD(HSRREF/ECECDS *LIBL/HSPREF)
     A                                  6  2'8=View Expense Codes'
     A                                  8  7'Subclass'
     A                                      DSPATR(HI)
     A                                  8 19'Subclass'
     A                                      DSPATR(HI)
     A                                  8 31'Subclass'
     A                                      DSPATR(HI)
     A                                  9  2'Opt'
     A                                      DSPATR(HI)
     A                                  9  7'Sequence'
     A                                      DSPATR(HI)
     A                                  9 19'Code'
     A                                      DSPATR(HI)
     A                                  9 31'Description'
     A                                      DSPATR(HI)
     A            C1ESSEQ   R        B 10  7REFFLD(HSRREF/@@SEQ *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A                                      EDTCDE(K)
     A            C1ESCD    R        B 10 19REFFLD(HSRREF/@@ESCD *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A            C1ESDS    R        B 10 31REFFLD(HSRREF/@@DS20 *LIBL/HSPREF)
     A                                      CHECK(LC)
     A                                      DSPATR(HI)
     A*
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R HP711K1
     A*%%TS  SD  20030623  152550  ISLRAMS     REL-V5R2M0  5722-WDS
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A                                 23 12'F5=Refresh'
     A                                 23 25'F12=Previous'
     A                                 23 40'F20=Spool Files'
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *---------------------------------------------------------------
     A          R HP711ES                   SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE CONTROL FORMAT
      *---------------------------------------------------------------
     A          R HP711EC                   SFLCTL(HP711ES)
      *
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
