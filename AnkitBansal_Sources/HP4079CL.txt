/***************************************************************** */
/*                                                                 */
/*  PROGRAM NUMBER:  HP4079CL                                      */
/*  PROGRAM NAME:    ROOM INVENTORY LISTING                        */
/*  PROGRAMMER:      LEANNE FEDOR                                  */
/*  CREATE DATE:     04/16/02                                      */
/*                                                                 */
/*  FUNCTION:        THIS CL IS SUBMITTED WITH QCMDEXC FROM        */
/*                   HP4079-SPECIFY OPTIONS FOR ROOM INVENTORY     */
/*                   LISTING.                                      */
/*                                                                 */
/*                   OPTIONS ARE PASSED THROUGH THE LDA.           */
/*                                                                 */
/*******************************************************************/
/*  MODIFIED:                                                      */
/*******************************************************************/

/*******************************************************************/

             PGM

/* DEFINE THE VARIABLE REQUIRED IN THE COMMAND TO OVERRIDE THE     */
/* PRINT FILE TO A LASER PRINTER                                   */

             DCL        VAR(&CMD) TYPE(*CHAR) LEN(512)
             DCL        VAR(&QDATA) TYPE(*CHAR) LEN(83)

             DCL        VAR(&LDPPCD) TYPE(*CHAR) LEN(5)
             DCL        VAR(&LDCELL) TYPE(*DEC) LEN(2)
             DCL        VAR(&LDFSCD) TYPE(*DEC) LEN(5)

             DCL        VAR(&HOLD) TYPE(*CHAR) LEN(10)
             DCL        VAR(&COPYS) TYPE(*DEC) LEN(2)
             DCL        VAR(&OUTQ) TYPE(*CHAR) LEN(10)

             CHGVAR     VAR(&LDPPCD) VALUE(%SST(*LDA 1 5))
             CHGVAR     VAR(&LDCELL) VALUE(%SST(*LDA 6 2))
             CHGVAR     VAR(&LDFSCD) VALUE(%SST(*LDA 8 5))


/*-------------------------------------------------------------------*/
/*  SETUP QUERY OVER THE BUILDINB/ROOMS FILE                         */
/*-------------------------------------------------------------------*/

/*  ALWAYS SELECT ONLY 'ACTIVE' ROOMS                                */

             CHGVAR     VAR(&QDATA) VALUE(' ')
             CHGVAR     VAR(%SST(&QDATA 1 14)) VALUE('BRRMST *EQ "A"')

             OVRDBF     FILE(HSP020) SHARE(*YES)
             OPNQRYF    FILE((HSP020)) QRYSLT(&QDATA) KEYFLD(*FILE) +
                          SEQONLY(*YES)


/*-------------------------------------------------------------------*/
/*  SETUP QUERY OVER THE FARM SITE MASTER                            */
/*-------------------------------------------------------------------*/


/*  ALWAYS OMIT BGF FARMS                                            */

             CHGVAR     VAR(&QDATA) VALUE(' ')
             CHGVAR     VAR(%SST(&QDATA 1 18)) VALUE('FSPPCD *NE "BGF  "')


/* PRODUCTION PHASE                                                      */

             IF         COND(&LDPPCD *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 20 17)) VALUE('*AND FSPPCD +
                          *EQ "')
             CHGVAR     VAR(%SST(&QDATA 37 5)) VALUE(&LDPPCD)
             CHGVAR     VAR(%SST(&QDATA 42 1)) VALUE('"')
             ENDDO

/* CELL                                                       */
             IF         COND(&LDCELL *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 44 16)) VALUE('*AND FSCELL +
                           *EQ ')
             CHGVAR     VAR(%SST(&QDATA 60 2)) VALUE(&LDCELL)
             ENDDO


/* FARM SITE                                                  */

             IF         COND(&LDFSCD *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 63 16)) VALUE('*AND FSFSCD +
                           *EQ ')
             CHGVAR     VAR(%SST(&QDATA 79 5)) VALUE(&LDFSCD)
             ENDDO


             OVRDBF     FILE(HSP018) SHARE(*YES)
             OPNQRYF    FILE((HSP018)) QRYSLT(&QDATA) +
                          KEYFLD((FSPPCD) (FSCELL) (FSFSCD)) +
                          SEQONLY(*YES)


/*-------------------------------------------------------------------*/
/* SET UP PRINT OVERRIDES                                            */
/*-------------------------------------------------------------------*/

 HOLD:       IF         COND(%SST(*LDA 411 1) = 'Y') THEN(CHGVAR +
                          VAR(&HOLD) VALUE(*YES))
             ELSE       CMD(CHGVAR VAR(&HOLD) VALUE(*NO))

 COPYS:      CHGVAR     VAR(&COPYS) VALUE(%SST(*LDA 412 1))
             IF         COND(&COPYS = 0) THEN(CHGVAR VAR(&COPYS) +
                          VALUE(1))

OUTQ:        CHGVAR     VAR(&OUTQ) VALUE(%SST(*LDA 413 10))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(%SST(*LDA 401 10)))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(*JOB))

/*------------------------------------------------------------------*/
/* OVERRIDE PRINT FILE                                              */
/*------------------------------------------------------------------*/

/* USE THE CUSTOMIZED SEABOARD COMMAND TO OVERRIDE THE PRINT FILE.  */
/* THIS COMMAND ALLOWS THE REPORT TO BE GENERATED ON A STANDARD LINE*/
/* PRINTER OR A LASER PRINTER. TO USE THIS OVERRIDE YOU MUST DECLARE*/
/* A COMMAND VARIABLE AND FOLLOW THE OVERRIDE WITH A CALL TO        */
/* QCMDEXC.                                                         */

/* THIS REPORT IS STANDARD PRINT AT 10 CPI:  SO, ON A LASER PRINTER */
/* IT CAN PRINT 'PORTRAIT' ON LETTER PAPER                          */

             SBDPRTOVR  CMDVAR(&CMD) FILE(QPRINT) OUTQ(&OUTQ) +
                          WIDTH(85) LANDSCAPE(*NO) DRAWER(*PRTF) +
                          COPIES(&COPYS) HOLD(&HOLD)

             CALL       PGM(QCMDEXC) PARM(&CMD 512)


/*-------------------------------------------------------------------*/
/* PRINT THE LISTING                                                 */
/*-------------------------------------------------------------------*/

             CALL       PGM(HP6079)

             CLOF       OPNID(HSP018)
             CLOF       OPNID(HSP020)

             DLTOVR     FILE(*ALL)

             ENDPGM

