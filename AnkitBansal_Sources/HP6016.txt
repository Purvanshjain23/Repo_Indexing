      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Hog Production System
      * PROGRAM:     HP6016
      * TITLE:       Listing of Overrides for a Group
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     09/01/04
      *
      *            This listing is submitted from HP4016-Work With Group Overrides.
      *
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 02/01/05  LeAnne Fedor
      *           Recompile only. New fields (Sire Line Company and Prime Line) added
      *           to Datamart Group Header file.
      *
      * 08/02/06  LeAnne Fedor
      *           Recompile only. Two new fields (ration 8 feed pounds, ration paylean flag)
      *           added to Datamart Group Header file.
      *
      * 08/10/07  LeAnne Ramsey
      *           Recompile only. New "target" fields renamed in the Datamart Group
      *           Header file.
      *
      * 09/21/07  LeAnne Ramsey
      *           Recompile only. New "target" fields added/removed in the Datamart Group
      *           Header file.
      *
      * 07/06/09  LeAnne Ramsey
      *           Recompile only. Added new field 'Continuous Flow Flag' to Hog Group file.
      *
      * 10/18/10  LeAnne Ramsey  (E1079)
      *           Recompile only. Sire Line Company is now 2-characters instead
      *           of one.
      *
      * 10/16/13  LeAnne Ramsey (E2831)
      *           Recompile only. Added field 'MTech Reference'.
      *
      * 10/14/16  Barb Gutierrez
      *           Recompile only. Added fields Sold days in phase, Avg Sold days in phase,
      *           Wgt open date, and company number to HPPF034.         E6136
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fhsp018    if   e           k disk
      *    Farm site
      *
      *
     Fhsl034d   if   e           k disk
      *    Hog group
      *
      *
     Fhplf034c  if   e           k disk    rename(hgrec:hgrecdm) prefix(p1)
      *    Datamart FIN: Group header
      *
      *
     Fhsp130    if   e           k disk
      *    Group overrides
      *
      *
     Fqprint    o    f   80        printer oflind(*inof)
      *
      /EJECT
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Standard fields
      *
     d rtime           s              6  0
      *
      *
      * Print fields
      *
     D l1hgcd          s                   like(hghgcd)
     D l1gscd          s                   like(hggscd)
     D l1ppcd          s                   like(hgppcd)
     D l1blcd          s                   like(hgblcd)
     D l1rmcd          s                   like(hgrmcd)
     D l1fscd          s                   like(hgfscd)
     D l1fsnm          s                   like(fsfsnm)
      *
     D l1sicd          s                   like(gosicd)
     D l1obfs          s                   like(goobfs)
     D l1obpod         s                   like(fspod)
     D l1obcell        s                   like(fscell)
     D l1onfs          s                   like(goonfs)
     D l1oncell        s                   like(fscell)
      *
     D l1dmsicd        s                   like(p1hgsicd)
     D l1dmobfs        s                   like(p1hgobfs)
     D l1dmonfs        s                   like(p1hgonfs)
     D l1dmobpod       s                   like(p1hgobpod)
     D l1dmobcell      s                   like(p1hgobcell)
     D l1dmoncell      s                   like(p1hgoncell)
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *---------------------------------------------------------------
      * Local data area.
      *---------------------------------------------------------------
      *
     Dlda             uds                  dtaara(*lda)
     D  ldreport               1      1
     D  ldhgsn                 2      8  0
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * MAINLINE
      ****************************************************************
      *
      * Print report headings
      *
     C                   except    h1hdr
      *
      * Retrieve:
      *   1) group info
      *   2) override info
      *   3) datamart info
      *
     C                   exsr      $group
     C                   exsr      $override
     C                   exsr      $datamart
      * Print
     C                   except    l1dtl
      *
      * EOF processing
     C                   seton                                        lr
      /eject
      *---------------------------------------------------------------
      * Retrieve group info
      *---------------------------------------------------------------
      *
     C     $group        begsr
      *
     C     ldhgsn        chain     hsl034d                            92
     C                   if        *in92 = *off                                 If group hit
     C                   move      hghgcd        l1hgcd
     C                   move      hggscd        l1gscd
     C                   move      hgppcd        l1ppcd
     C                   z-add     hgfscd        l1fscd
     C                   move      hgblcd        l1blcd
     C                   move      hgrmcd        l1rmcd
      *
     C     hgfscd        chain     hsp018                             92
     C                   if        *in92 = *off
     C                   move      fsfsnm        l1fsnm
     C                   endif
      *
     C                   endif                                                  If group hit
      *
     C                   endsr
      /eject
      *----------------------------------------------------------------
      * Retrieve the override values for the group
      *----------------------------------------------------------------
      *
     C     $override     begsr
      *
     C     ldhgsn        chain     hsp130                             92
     C                   if        *in92 = *off                                 If found
     C                   move      gosicd        l1sicd
     C                   z-add     goobfs        l1obfs
     C                   z-add     goonfs        l1onfs
      *
      * Retrieve associated cell/pod info
      *
     C                   exsr      $farms
     C                   endif                                                  If found
      *
     C                   endsr
      /eject
      *----------------------------------------------------------------
      * Retrieve various data associated with farms
      *----------------------------------------------------------------
      *
      * Retrieve:
      *   1) origin bgf pod
      *   2) origin bgf cell
      *   3) nursery cell
      *
     C     $farms        begsr
      *
      * Origin BGF pod and cell
      *
     C                   if        l1obfs <> 0
     C     l1obfs        chain     hsp018                             92
     C                   if        *in92 = *off                                 If found
     C                   z-add     fspod         l1obpod
     C                   z-add     fscell        l1obcell
     C                   endif                                                  If found
     C                   endif
      *
      * Origin nursery cell
      *
     C                   if        l1onfs <> 0
     C     l1onfs        chain     hsp018                             92
     C                   if        *in92 = *off                                 If found
     C                   z-add     fscell        l1oncell
     C                   endif                                                  If found
     C                   endif
      *
     C                   endsr
      /eject
      *----------------------------------------------------------------
      * Retrieve the current Datamart values for a group
      *----------------------------------------------------------------
      *
     C     $datamart     begsr
      *
     C     ldhgsn        chain     hplf034c                           92
     C                   if        *in92 = *off
     C                   move      p1hgsicd      l1dmsicd
     C                   z-add     p1hgobfs      l1dmobfs
     C                   z-add     p1hgonfs      l1dmonfs
     C                   z-add     p1hgobpod     l1dmobpod
     C                   z-add     p1hgobcell    l1dmobcell
     C                   z-add     p1hgoncell    l1dmoncell
     C                   endif
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Retrieve time for report heading.
      *
     C                   time                    rtime
      *
     C                   endsr
      /EJECT
      *-------------------------------------------------------------
      * Report heading lines
      *-------------------------------------------------------------
      *
     Oqprint    e            h1hdr          1 01
     O                       sdpgm               10
     O                                           48 'HOG PRODUCTION SYSTEM'
     O                                           70 'DATE'
     O                       udate         y     80
      *
     O          e            h1hdr          1
     O                       sdusr               10
     O                                           46 'LISTING OF OVERRIDES FOR '
     O                                           53 'A GROUP'
     O                                           70 'TIME'
     O                       rtime               80 '  :  :  '
      *
     O          e            h1hdr          2
     O                                           70 'PAGE'
     O                       page          z     80
      *
      *
      *
      *-------------------------------------------------------------
      * Detail lines
      *-------------------------------------------------------------
      *
      * Section 1
     O          e            l1dtl       0  1
     O                                           15 'Hog group code:'
     O                       l1hgcd              24
     O                                           45 'Status code:'
     O                       l1gscd              48
      *
     O          e            l1dtl       0  1
     O                                           45 'Production phase:'
     O                       l1ppcd              51
      *
     O          e            l1dtl       0  1
     O                                           45 'Farm site:'
     O                       l1fscd        z     51
     O                       l1fsnm              79
      *
     O          e            l1dtl       0  1
     O                                           45 'Building/room:'
     O                       l1blcd              52
     O                       l1rmcd              59
      *
      * Section 2
      *
     O          e            l1dtl       2  1
     O                                           50 '  OVERRIDE VALUES   '
     O                                           80 'CURRENT DATAMART VALUES'
      *
     O          e            l1dtl       0  2
     O                                           50 '--------------------'
     O                                           80 '-----------------------'
      *
     O          e            l1dtl       0  2
     O                                           25 'Sire line code:'
     O                       l1sicd              35
     O                       l1dmsicd            62
      *
      *
     O          e            l1dtl       0  2
     O                                           25 'Origin BGF farm site:'
     O                       l1obfs        z     36
     O                       l1dmobfs      z     63
      *
     O          e            l1dtl       0  2
     O                                           25 'Origin nursery farm site:'
     O                       l1onfs        z     36
     O                       l1dmonfs      z     63
      *
      * Section 3
      *
     O          e            l1dtl       1  2
     O                                           25 'Origin BGF pod:'
     O                       l1obpod       z     36
     O                       l1dmobpod     z     61
      *
      *
     O          e            l1dtl       0  2
     O                                           25 'Origin BGF cell:'
     O                       l1obcell      z     36
     O                       l1dmobcell    z     61
      *
      *
     O          e            l1dtl       0  2
     O                                           25 'Origin nursery cell:'
     O                       l1oncell      z     36
     O                       l1dmoncell    z     61
      *
