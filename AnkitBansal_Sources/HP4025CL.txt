/***************************************************************** */
/*  PROGRAM NUMBER:  HP4025CL                                      */
/*  PROGRAM NAME:    LISTING OF NUR/FIN TARGETS                    */
/*  PROGRAMMER:      LEANNE RAMSEY                                 */
/*  CREATE DATE:     09/19/07                                      */
/*                                                                 */
/*  FUNCTION:        THIS CL IS SUBMITTED WITH QCMDEXC FROM        */
/*                   HP4025-SPECIYF OPTIONS FOR LISTING.           */
/*                   OPTIONS ARE PASSED THROUGH THE LDA.           */
/*                                                                 */
/*******************************************************************/
/*  MODIFIED:                                                      */
/*******************************************************************/

             PGM

             DCL        VAR(&QDATA) TYPE(*CHAR) LEN(80)

             DCL        VAR(&HOLD) TYPE(*CHAR) LEN(10)
             DCL        VAR(&COPYS) TYPE(*DEC) LEN(2)
             DCL        VAR(&OUTQ) TYPE(*CHAR) LEN(10)

             DCL        VAR(&LDCELL) TYPE(*DEC) LEN(2)
             DCL        VAR(&LDPOD)  TYPE(*DEC) LEN(3)
             DCL        VAR(&LDYEAR) TYPE(*DEC) LEN(4)

             CHGVAR     VAR(&LDCELL) VALUE(%SST(*LDA 1 2))
             CHGVAR     VAR(&LDPOD)  VALUE(%SST(*LDA 3 3))
             CHGVAR     VAR(&LDYEAR) VALUE(%SST(*LDA 6 4))

/*------------------------------------------------------------------*/
/*  SET UP PRINT FILE OVERRIDES                                      */
/*------------------------------------------------------------------*/

/* SET PARAMETERS FROM LDA                                           */

 HOLD:       IF         COND(%SST(*LDA 411 1) = 'Y') THEN(CHGVAR +
                          VAR(&HOLD) VALUE(*YES))
             ELSE       CMD(CHGVAR VAR(&HOLD) VALUE(*NO))

 COPYS:      CHGVAR     VAR(&COPYS) VALUE(%SST(*LDA 412 1))
             IF         COND(&COPYS = 0) THEN(CHGVAR VAR(&COPYS) +
                          VALUE(1))

 OUTQ:       CHGVAR     VAR(&OUTQ) VALUE(%SST(*LDA 413 10))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(%SST(*LDA 401 10)))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(*JOB))

/*------------------------------------------------------------------*/
/* SELECT THE TARGET RECORDS TO PROCESS                             */
/*------------------------------------------------------------------*/

             CHGVAR     VAR(&QDATA) VALUE(' ')

             CHGVAR     VAR(%SST(&QDATA 1 14)) VALUE('NYOBCELL *NE 0')

/* CELL                                                           */
             IF         COND(&LDCELL *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 16 17)) VALUE('*AND NYOBCELL +
                          *EQ')
             CHGVAR     VAR(%SST(&QDATA 34 2)) VALUE(&LDCELL)
             ENDDO
/* POD                                                            */
             IF         COND(&LDPOD *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 37 16)) VALUE('*AND NYOBPOD +
                          *EQ')
             CHGVAR     VAR(%SST(&QDATA 54 3)) VALUE(&LDPOD)
             ENDDO
/* YEAR                                                           */
             IF         COND(&LDYEAR *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 58 15)) VALUE('*AND NYCDYR +
                          *EQ')
             CHGVAR     VAR(%SST(&QDATA 74 4)) VALUE(&LDYEAR)
             ENDDO

             OVRDBF     FILE(HSP123) SHARE(*YES)
             OPNQRYF    FILE((HSP123)) QRYSLT(&QDATA) KEYFLD(*FILE)  +
                          SEQONLY(*YES)

             OVRPRTF    FILE(PRINT132) OUTQ(&OUTQ) COPIES(&COPYS) +
                          HOLD(&HOLD)

/*------------------------------------------------------------------*/
/* PRINT LISTING                                                    */
/*------------------------------------------------------------------*/

             CALL       PGM(HP6025)

             CLOF       OPNID(HSP123)
             DLTOVR     FILE(*ALL)

             ENDPGM
