     A*%%TS  SD  20050203  170155  ISLRAMS     REL-V5R2M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD7700
     A*  TITLE:         VIEW ALL FARM/TEMPLATE LINKS
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  DATE:          02/02/04
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A                                      CF04(04 'PROMPT')
     A                                      CF05(05 'REFRESH')
     A                                      CF07(07 'LISITING')
     A                                      CF17(17 'PRINT OPTIONS')
     A                                      CF20(20 'WRKSPLF')
     A*
     A*
     A*---------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP7700S1                  SFL
     A*%%TS  SD  20040202  093813  ISLRAMS     REL-V5R2M0  5722-WDS
     A  85                                  SFLNXTCHG
     A            HDRRN1         4Y 0H
     A            D1FSCD    R        O  9  3REFFLD(@@FSCD)
     A                                      EDTCDE(M)
     A            D1BTCD    R        O  9 11REFFLD(@@BTCD)
     A            D1BTDS    R        O  9 19REFFLD(HSRREF/@@BTDS *LIBL/HSPREF)
     A            D1CELL    R        O  9 47REFFLD(FSREC/FSCELL *LIBL/HSP018)
     A                                      EDTCDE(M)
     A            D1TFCD    R        O  9 52REFFLD(@@TFCD)
     A            D1PPCD    R        O  9 61REFFLD(@@PPCD)
     A            D1FSNM        11A  O  9 68
     A*
     A*
     A*---------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP7700C1                  SFLCTL(HP7700S1)
     A*%%TS  SD  20040202  092846  ISLRAMS     REL-V5R2M0  5722-WDS
     A                                      SFLSIZ(0050)
     A                                      SFLPAG(0013)
     A N82                                  ROLLUP(25 'PAGE FORWARD')
     A                                      SETOF(52)
     A                                      SETOF(53)
     A                                      SETOF(54)
     A                                      SETOF(55)
     A                                      RTNCSRLOC(&RCD &FLD)
     A                                      OVERLAY
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 26'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  2 22'VIEW ALL FARM/TEMPLATE LINKS'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  4  6'?'
     A                                      DSPATR(HI)
     A                                  4 13'?'
     A                                      DSPATR(HI)
     A                                  4 54'?'
     A                                      DSPATR(HI)
     A                                  4 62'?'
     A                                      DSPATR(HI)
     A                                  5 11'Budget'
     A                                      DSPATR(HI)
     A                                  5 46'----------- Farm Site -----------'
     A                                      DSPATR(HI)
     A                                  6  4'Farm'
     A                                      DSPATR(HI)
     A                                  6 10'Template'
     A                                      DSPATR(HI)
     A                                  6 52'Type of'
     A                                      DSPATR(HI)
     A                                  6 61'Prod'
     A                                      DSPATR(HI)
     A                                  7  4'Site'
     A                                      DSPATR(HI)
     A                                  7 12'Code'
     A                                      DSPATR(HI)
     A                                  7 19'Description'
     A                                      DSPATR(HI)
     A                                  7 46'Cell'
     A                                      DSPATR(HI)
     A                                  7 52'Farm'
     A                                      DSPATR(HI)
     A                                  7 61'Phase'
     A                                      DSPATR(HI)
     A                                  7 68'Name'
     A                                      DSPATR(HI)
     A            C1FSCD    R        B  8  3REFFLD(@@FSCD)
     A                                      EDTCDE(M)
     A                                      DSPATR(HI)
     A  53                                  DSPATR(PC)
     A            C1BTCD    R        B  8 11REFFLD(@@BTCD)
     A                                      DSPATR(HI)
     A  52                                  DSPATR(PC)
     A            C1BTDS    R        B  8 19REFFLD(HSRREF/@@BTDS *LIBL/HSPREF)
     A                                      CHGINPDFT(UL LC FE)
     A                                      DSPATR(HI)
     A            C1CELL    R        B  8 47REFFLD(HSRREF/@@CELL *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A            C1TFCD    R        B  8 52REFFLD(@@TFCD)
     A                                      DSPATR(HI)
     A  54                                  DSPATR(PC)
     A            C1PPCD    R        B  8 61REFFLD(@@PPCD)
     A                                      DSPATR(HI)
     A  55                                  DSPATR(PC)
     A*
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R HP7700K1
     A*%%TS  SD  20050203  170155  ISLRAMS     REL-V5R2M0  5722-WDS
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A                                 23 11'F4=Prompt'
     A                                 23 22'F5=Refresh'
     A                                 23 34'F7=Listing'
     A                                 23 46'F17=Print Options'
     A                                 23 65'F20=Spool Files'
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *---------------------------------------------------------------
     A          R HP7700ES                  SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE CONTROL FORMAT
      *---------------------------------------------------------------
     A          R HP7700EC                  SFLCTL(HP7700ES)
      *
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
