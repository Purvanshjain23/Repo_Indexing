     A*%%TS  SD  20010711  134536  ISLRAMS     REL-V4R4M0  5769-PW1
     A*****************************************************************
     A*
     A*  SYSTEM:        HOG PRODUCTION
     A*  DISPLAY FILE:  HPD509 - SELECT BIN SET FOR A FARM
     A*  CREATED BY:    LEANNE FEDOR
     A*  DATE:          07/10/01
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      PRINT
     A                                      CA12(12 'PREVIOUS')
     A*---------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP509S1                   SFL
     A*%%TS  SD  20010711  134536  ISLRAMS     REL-V4R4M0  5769-PW1
     A            HDRRN1    R        H      REFFLD(@@RRN)
     A            D1BICD    R        O  7  1REFFLD(@@BICD)
     A            D1BINS    R        O  7 10REFFLD(@@DS50)
     A*
     A*---------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP509C1                   SFLCTL(HP509S1)
     A*%%TS  SD  20010711  082745  ISLRAMS     REL-V4R4M0  5769-PW1
     A                                      SFLSIZ(0050)
     A                                      SFLPAG(0010)
     A                                      WINDOW(HP509EC)
     A                                      OVERLAY
     A                                      SFLCSRRRN(&HDRRN)
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A            HDPAG1    R        H      REFFLD(@@PAG)
     A                                      SFLRCDNBR
     A            HDRRN     R        H      REFFLD(@@RRN)
     A                                  1 23'SELECT BIN SET'
     A                                      DSPATR(HI)
     A                                  2  1'Position Cursor, Press Enter'
     A                                  4  1'Farm site:'
     A            C1FSCD    R        O  4 12REFFLD(FSREC/FSFSCD *LIBL/HSP018)
     A                                      EDTCDE(Z)
     A            C1FSNM    R        O  4 19REFFLD(FSREC/FSFSNM *LIBL/HSP018)
     A                                  6  1'Bin Set'
     A                                      DSPATR(HI)
     A                                  6 12'Bins'
     A                                      DSPATR(HI)
     A*---------------------------------------------------------------
     A* COMMAND LINE LINE
     A*---------------------------------------------------------------
     A          R HP509K1
     A                                      WINDOW(HP509EC)
     A                                      OVERLAY
     A  43                             17  1'Message program failed. Call IS.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 18  1'F12=Previous'
      *
     A*-------------------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
     A*-------------------------------------------------------------------------
     A          R HP509ES                   SFL SFLMSGRCD(19)
      *
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
     A*
     A*-------------------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*-------------------------------------------------------------------------
     A*
     A          R HP509EC                   SFLCTL(HP509ES)
     A*%%TS  SD  20010710  172459  ISLRAMS     REL-V4R4M0  5769-PW1
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A                                      WINDOW(*DFT 20 65)
     A                                      WDWBORDER((*DSPATR RI) (*CHAR '    -
     A                                          '))
     A            MSGPGM                    SFLPGMQ(10)
     A*
     A*---------------------------------------------------------------
     A* ASSUME FORMAT -- REQUIRED FOR WINDOWS
     A*---------------------------------------------------------------
     A          R ASSUME
     A*%%TS  SD  19980327  102611  ISLRAMS     REL-V3R7M0  5716-PW1
     A                                      ASSUME
     A                                      OVERLAY
     A                                 24 79' '
