      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Triumph Foods
      * PROGRAM:     TF645 - Margin: Product Split Listing
      * PROGRAMMER:  LeAnne Ramsey
      * CREATED:     10/11/06
      *
      * Function:    This is a listing of the Product Split records.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 10/04/07  LeAnne Ramsey
      *           Recomiled in Production. Alice got a level check over file
      *           PPAMREL1. Apparently, when they installed for Daily's they
      *           missed this program.
      *
      * 06/04/10  LeAnne Ramsey
      *           Switched from From/To Date selections to a CutOff Effective Date
      *           selection.
      *
      * 01/21/21  ISE            TSN593
      *           Replaced *loval with *start
      *
      /eject
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fombyrel1  if   e           k disk
      *    Item Default
      *
      *
     Fome8rel0  if   e           k disk
      *    Product type
      *
      *
     Fppamrel1  if   e           k disk
      *    TF Classification
      *
      *
     Ftfp045    if   e           k disk
      *  Product split (records selected with Open Query)
      *
      *
     Fprint198  o    f  198        printer oflind(*inof)
      *
      /eject
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Standard fields
      *
     d rtime           s              6  0
     D dash            s            198    inz(*all'-')
      *
      *
      * Control fields
      *
     D first           s              1    inz('Y')
     D svprcd          s                   like(psprcd)
      *
      *
      * Workfields for date manipulation
      *
     D wkisodate       s               D   datfmt(*iso)
      *
      *
      * Parms
      *
     D xxtfcgcd        s                   like(pstfcgcd)
     D xxtfcgds        s             15
      *
      *
      * Print fields
      *
     D l1efmdy         s              6  0
     D l1prds          s                   like(byavna)
     D l1istyds        s                   like(e8dctx)
     D l1tfcgds        s             15
     D l1tfclds        s                   like(amq0na)
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *---------------------------------------------------------------
      *  Local data area
      *---------------------------------------------------------------
      *
     d lda            uds                  dtaara(*lda)
      *
     D  ldccymd               15     22  0
     D  ldcmdy                23     28  0
      *
     D  ldprcd                29     35  0
     D  ldistycd              36     38  0
     D  ldtfcgcd              39     40
     D  ldtfclcd              41     43
      *
     D  ldaist                44     44
      *
     D  ldistyds              50     79
     D  ldtfcgds              80     94
     D  ldtfclds              95    124
     D  ldprds               125    154
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * Mainline
      ****************************************************************
      *
      * Print headings
     C                   exsr      $h1hdr

      * Process all Product Split records that meet the user's selections:
      *
593  C*    *loval        setll     tfp045
593  C     *start        setll     tfp045
      *
     C                   dou       *in91 = *on                                  Main do
     C                   read      tfp045                                 91
     C                   if        *in91 = *off                                 If not EOF
      *
      * Control breaks
     C                   select
     C                   when      first = yes
     C                   move      no            first
     C                   z-add     psprcd        svprcd
      *
     C                   when      psprcd <> svprcd
     C                   except    blank
     C                   z-add     psprcd        svprcd
     C                   endsl
      *
      * Flip Effective Date from CCYYMMDD to MMDDYY
      *
     C     *iso          test(d)                 psefdt                 92
     C                   if        *in92 = *off                                 If OK date
     C                   move      psefdt        wkisodate
     C     *mdy          move      wkisodate     l1efmdy
     C                   else
     C                   z-add     0             l1efmdy
     C                   endif                                                  If OK date
      *
      * Retrieve Descriptions
      *
     C                   exsr      $desc
      * Print
     C                   exsr      $l1dtl
     C                   endif                                                  If not EOF
     C                   enddo                                                  Main do
      *
      * EOF processing
     C                   if        first = yes
     C                   except    nodata
     C                   endif
      *
     C                   seton                                        lr
      /eject
      *---------------------------------------------------------------
      * Retrieve Descriptions
      *---------------------------------------------------------------
      *
     C     $desc         begsr
      *
      * Product Description
      *
     C     psprcd        chain     ombyrel1                           92
     C                   if        *in92 = *off
     C                   eval      l1prds  = byavna
     C                   else
     C                   eval      l1prds = 'Unknown'
     C                   endif
      *
      * Item structure type description
      *
     C     psistycd      chain     ome8rel0                           92
     C                   if        *in92 = *off
     C                   eval      l1istyds = e8dctx
     C                   else
     C                   eval      l1istyds = 'Unknow'
     C                   endif
      *
      * TF Class group
      *
     C                   call      'TF804'
     C                   parm      pstfcgcd      xxtfcgcd
     C                   parm      *blank        xxtfcgds
      *
     C                   if        xxtfcgds = *blank
     C                   eval      l1tfcgds = 'Unknown'
     C                   else
     C                   eval      l1tfcgds = xxtfcgds
     C                   endif
      *
      * TF Classification
      *
     C     pstfclcd      chain     ppamrel1                           92
     C                   if        *in92 = *off
     C                   eval      l1tfclds = amq0na
     C                   else
     C                   eval      l1tfclds = 'Unknown'
     C                   endif
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Print detail line
      *---------------------------------------------------------------
      *
     C     $l1dtl        begsr
      *
     C                   if        *inof = *on
     C                   exsr      $h1hdr
     C                   move      no            first
     C                   endif
      *
     C                   except    l1dtl
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Print report headings
      *---------------------------------------------------------------
      *
     C     $h1hdr        begsr
      *
     C                   except    h1hdr
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
     C                   time                    rtime
      *
      * Set indicators to control headings
      *
     C                   if        ldcmdy = 0
     C                   seton                                        98
     C                   endif
      *
     C                   if        ldprcd = 0
     C                   seton                                        97
     C                   endif
      *
     C                   if        ldistycd = 0
     C                   seton                                        96
     C                   endif
      *
     C                   if        ldtfcgcd = *blank
     C                   seton                                        94
     C                   endif
      *
     C                   if        ldtfclcd = *blank
     C                   seton                                        95
     C                   endif
      *
     C                   if        ldaist = *blank
     C                   seton                                        93
     C                   endif
      *
     C                   endsr
      /eject
      *-------------------------------------------------------------
      * Report lines
      *-------------------------------------------------------------
      *
     Oprint198  e            h1hdr          1 01
     O                       sdpgm               10
     O                                          110 'Product Split Listing'
     O                                          188 'DATE'
     O                       udate         y    198
      *
      *
     O          e            h1hdr          1
     O                       sdusr               10
     O                                          188 'TIME'
     O                       rtime              198 '  :  :  '
      *
     O          e            h1hdr          1
     O                                          188 'PAGE'
     O                       page          z    198
      *
      *
     O          e            h1hdr          1
     O                                           26 'CutOff effective date:'
     O              n98      ldcmdy              36 '  /  /  '
     O               98                          31 'All'
      *
      *
     O          e            h1hdr          1
     O                                           26 'Product Code:'
     O               97                          31 'All'
     O              n97      ldprcd        z     36
     O              n97      ldprds              68
      *
     O          e            h1hdr          1
     O                                           26 'Item structure type code:'
     O               96                          31 'All'
     O              n96      ldistycd      z     31
     O              n96      ldistyds            68
      *
     O          e            h1hdr          1
     O                                           26 'TF class group code:'
     O               94                          31 'All'
     O              n94      ldtfcgcd            30
     O              n94      ldtfcgds            53
      *
     O          e            h1hdr          1
     O                                           26 'TF classification code:'
     O               95                          31 'All'
     O              n95      ldtfclcd            31
     O              n95      ldtfclds            68
      *
     O          e            h1hdr          1
     O                                           26 'Active/Inactive status:'
     O               93                          31 'All'
     O              n93      ldaist              29
      *
      *
      *-------------------------------------------------------------
      * Column headings
      *-------------------------------------------------------------
      *
     O          e            h1hdr          1
     O                                           65 'Active/'
      *
     O          e            h1hdr          1
     O                                            7 'Product'
     O                                           53 'Effective'
     O                                           66 'Inactive'
     O                                           78 'Split'
      *
     O          e            h1hdr          1
     O                                            6 'Code'
     O                                           20 'Description'
     O                                           50 'Date'
     O                                           64 'Status'
     O                                           78 'Percent'
     O                                          100 'Item Structure Type'
     O                                          133 'TF Class Group'
     O                                          162 'TF Classification'
      *
     O          e            h1hdr          0
     O                       dash               198
      *
      *-------------------------------------------------------------
      * Detail line
      *-------------------------------------------------------------
      *
     O          e            l1dtl       1
     O                       psprcd        z      7
     O                       l1prds              39
     O                       l1efmdy             52 '  /  /  '
     O                       psaist              62
     O                       psspltpc      k     79
     O                       psistycd      m     85
     O                       l1istyds       b   117
     O                       pstfcgcd           121
     O                       l1tfcgds       b   138
     O                       pstfclcd           148
     O                       l1tfclds           180
      *
      /eject
      *-------------------------------------------------------------
      * Blank line
      *-------------------------------------------------------------
      *
     O          e            blank       1
      /eject
      *-------------------------------------------------------------
      * No data message line
      *-------------------------------------------------------------
      *
     O          e            nodata      1
     O                                           19 'No data meets your'
     O                                           39 'selection criteria.'
      /eject
