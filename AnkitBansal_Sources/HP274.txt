      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      HPS
      * PROGRAM:     HP274 - Retrieve a Hog Group for a Farm/Bldg/Room
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     01/16/03
      *
      * FUNCTION:    SYNON calls this program sending in a farm/building/room/date in the
      *              hope that we will send back a Hog Group.
      *
      *              If there are no OPEN groups in that farm/building/room,
      *                  we send back an value of 'ERROR' in the return parm
      *
      *              If there is only one OPEN group for that farm/building/room with
      *              an Open Date on/before the incoming date,
      *                  we send back that group
      *
      *              If there are multiple OPEN groups for that farm/building/room, with
      *              Open Dates on/before the incoming date,
      *                  We pop up (1 time) a select window of groups
      *                     If the user selects a group,
      *                            we send back that group
      *                     If the user does not select a group,
      *                            we send back a return parm value of 'SELECT'
      *
      *************************************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 07/02/09  LeAnne Ramsey
      *           Recompile only. Added new field 'Continuous Flow Flag' to Hog Group file.
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS:
      ****************************************************************
      *
     Fhsp034    if   e           k disk
      *  Hog group
      *
      *
     Fhsl034p   if   e           k disk    rename(hgrec:hgrecp)
      *  Hog group (open groups only)
      *
      /eject
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      *
      ****************************************************************
      * ARRAYS AND TABLES
      ****************************************************************
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Control fields
      *
     D count           s              3  0
     D first           s              1    inz('Y')
      *
      *
      * Workfields
      *
     D wkccyymmdd      s                   like(hgopdt)
      *
      *
      * Parm fields
      *
     D xxccyymmdd      s              8  0
     D xxreturn        s              7
     D xxsynondt       s              7  0
     D xxfscd          s                   like(hgfscd)
     D xxblcd          s                   like(hgblcd)
     D xxrmcd          s                   like(hgrmcd)
     D xxhgcd          s                   like(hghgcd)
     D xxsicd          s                   like(hggncd)
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * MAINLINE
      ****************************************************************
      *
      * Step 1: Are there ANY open groups in this Farm/Building/Room.
      *
     C     key01         chain     hsl034p                            92
     C                   if        *in92 = *on                                  If no open
     C                   eval      xxreturn = 'ERROR'
     C                   else
      *
      * Step 2: Are there multiple open groups in this Farm/Building/Room
      *         with an Open Date on/before the incoming date.
      *
     C     key01         setll     hsl034p                            92
      *
     C                   dou       *in90 = *on                                  Do count
     C     key01         reade     hsl034p                                90
     C                   if        *in90 = *off and hgopdt <= xxccyymmdd        If read
      *
     C                   if        first = yes
     C                   move      no            first
     C                   movel     hghgcd        xxhgcd
     C                   movel     hggncd        xxsicd
     C                   endif
      *
     C                   add       1             count
      *
     C                   endif                                                  If read
     C                   enddo                                                  Do count
      *
      * Step 3: If there was only 1 open group,
      *            mission accomplished, you're outta here!
      *         If there were no groups with an open date on/before the
      *            incoming date, it's an Error.
      *         If there were multiple groups, pop up a select
      *            window.
      *
     C                   select
     C                   when      count = 1
      *
     C                   when      count = 0
     C                   eval      xxreturn = 'ERROR'
      *
     C                   when      count > 1
     C                   move      *blank        xxhgcd
     C                   move      *blank        xxsicd
     C                   exsr      $popup
     C                   endsl
      *
     C                   endif                                                  If no open
      *
     C                   seton                                        lr
      /eject
      *---------------------------------------------------------------
      * Pop up a select window
      *---------------------------------------------------------------
      *
     C     $popup        begsr
      *
     C                   call      'HP5952'
     C                   parm                    xxfscd
     C                   parm                    xxblcd
     C                   parm                    xxrmcd
     C                   parm                    xxccyymmdd
     C                   parm      *blank        xxhgcd
      *
     C                   if        xxhgcd = *blank                              If no select
     C                   eval      xxreturn = 'SELECT'
     C                   else
      *
      * Retrieve Sire Line code
      *
     C     xxhgcd        chain     hsp034                             92
     C                   if        *in92 = *off
     C                   movel     hggncd        xxsicd
     C                   endif
     C                   endif                                                  If no select
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
     C     *entry        plist
     C                   parm                    xxreturn
     C                   parm                    xxsynondt
     C                   parm                    xxfscd
     C                   parm                    xxblcd
     C                   parm                    xxrmcd
     C                   parm                    xxhgcd
     C                   parm                    xxsicd
      *
      * Key lists
      *
     C     key01         klist
     C                   kfld                    xxfscd
     C                   kfld                    xxblcd
     C                   kfld                    xxrmcd
      *
      *
      * Format SYNON date into CCYYMMDD format
      *
     C                   call      'HP8011'
     C                   parm                    xxsynondt
     C                   parm                    xxccyymmdd
      *
     C                   endsr
      /eject
