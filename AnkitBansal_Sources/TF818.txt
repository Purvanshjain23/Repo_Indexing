      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Triumph Systems
      * PROGRAM:     TF818
      * TITLE:       Retrieve Beginning/Ending Dates for a Week
      * PROGRAMMER:  LeAnne Ramsey
      * CREATED:     11/30/06
      *
      * FUNCTION:  Given an incoming year/week, this program retrieves the
      *            beginning/ending dates for the week and returns them to the calling
      *            program in 3 formats:
      *               1) CCYYMMDD
      *               2) CYYMMDD
      *               3) MMDDYY
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 12/06/06  LeAnne Ramsey
      *           We changed the file name/text on the Calendar file.
      *
      * 09/10/07  LeAnne Ramsey
      *           Recompile only. We added "sales date" fields to the Calendar file.
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fcalcala   if   e           k disk
      *  Calendar file
      *
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      ****************************************************************
      * ARRAYS AND TABLES
      ****************************************************************
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Workfields
      *
     D wkcocd          s              3  0 inz(350)
     D wkdow           s              1  0
      *
      *
      * Workfields for date manipulation
      *
     D wkisodate       s               D   datfmt(*iso)
      *
      *
      * Parms
      *
     D xxhpsyr         s              4  0
     D xxhpswk         s              2  0
      *
     D xxwbdt          s              8  0
     D xxwbsyndt       s              7  0
     D xxwbmdy         s              6  0
      *
     D xxwedt          s              8  0
     D xxwesyndt       s              7  0
     D xxwemdy         s              6  0
      *
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * Mainline
      ****************************************************************
      *
      * Retrieve the beginning date (Sunday=1) for the Week.
      *
     C                   z-add     1             wkdow
     C     key01         chain     calcala                            92
     C                   if        *in92 = *off                                 If hit
     C                   z-add     catdt08       xxwbdt
     C                   z-add     catdtsy       xxwbsyndt
     C                   endif                                                  If hit
      *
      *
      * Retrieve the ending date (Saturday=7) for the Week.
      *
     C                   z-add     7             wkdow
     C     key01         chain     calcala                            92
     C                   if        *in92 = *off                                 If hit
     C                   z-add     catdt08       xxwedt
     C                   z-add     catdtsy       xxwesyndt
     C                   endif                                                  If hit
      *
      * Flip Beginning date to MMDDYY format.
      *
     C     *iso          test(d)                 xxwbdt                 92
     C                   if        *in92 = *off                                 If good date
     C     *iso          move      xxwbdt        wkisodate
     C     *mdy          move      wkisodate     xxwbmdy
     C                   endif                                                  If good date
      *
      * Flip Ending date to MMDDYY format.
      *
     C     *iso          test(d)                 xxwedt                 92
     C                   if        *in92 = *off                                 If good date
     C     *iso          move      xxwedt        wkisodate
     C     *mdy          move      wkisodate     xxwemdy
     C                   endif                                                  If good date
      *
     C                   seton                                        lr
      /EJECT
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Parm lists
      *
     C     *entry        plist
     C                   parm                    xxhpsyr
     C                   parm                    xxhpswk
     C                   parm                    xxwbdt
     C                   parm                    xxwbsyndt
     C                   parm                    xxwbmdy
     C                   parm                    xxwedt
     C                   parm                    xxwesyndt
     C                   parm                    xxwemdy
      *
      * Key lists
      *
     C     key01         klist
     C                   kfld                    wkcocd
     C                   kfld                    xxhpsyr
     C                   kfld                    xxhpswk
     C                   kfld                    wkdow
      *
     C                   endsr
      /EJECT
