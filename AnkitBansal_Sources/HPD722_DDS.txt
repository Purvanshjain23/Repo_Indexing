     A*%%TS  SD  19960411  083722  ISLRAMS     REL-V3R1M0  5763-PW1
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD722
     A*  TITLE:         VIEW REPORT CODES/CATEGORY
     A*  PROGRAMMER:    MARY PALMER
     A*  CREATED:       06/10/94
     A*
     A*  INDICATOR 90 IS ON WHEN THE REPORT CATEGORY THE USER SELECTED
     A*               FROM THE WORK WITH HAS BEEN DELETED BY ANOTHER
     A*               USER
     A*****************************************************************
     A* MODIFICATIONS
     A* 04/11/96  LEANNE FEDOR
     A*           THE FIELD RPOIFL-OPERATIONAL FLAG WAS NEVER USED.
     A*           TO AVOID RECOMPILING, WE HAVE LEFT THE FIELD IN
     A*           THIS PHYSICAL FILE AND IN THE FIELD REFERENCE FILE.
     A*           THE FIELD HAS BEEN REMOVED FROM THIS DSPF. THE
     A*           FIELD CAN BE REMOVED FROM THE FIELD REFERENCE FILE
     A*           AND HSP006 ANY TIME THAT YOU WANT TO RECOMPILE ALL
     A*           THE PROGRAMS.
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A N90                                  CF05(05 'REFRESH')
     A                                      CA12(12 'PREVIOUS')
     A N82N90                               ROLLUP(25 'PAGE FORWARD')
     A*
     A*---------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP722S1                   SFL
     A*%%TS  SD  19960411  083722  ISLRAMS     REL-V3R1M0  5763-PW1
     A  85                                  SFLNXTCHG
     A            HDRRN1         4Y 0H
     A            DFRPCD    R        O  8  5REFFLD(RPRPCD)
     A            DFRSDS    R        O  8 14REFFLD(RPRSDS)
     A            DFRLDS    R        O  8 29REFFLD(RPRLDS)
     A            DFUCFL    R        O  8 57REFFLD(RPUCFL)
     A            DFSEQ     R        O  8 67REFFLD(RPSEQ)
     A                                      EDTCDE(K)
     A*
     A*---------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP722C1                   SFLCTL(HP722S1)
     A*%%TS  SD  19960411  083722  ISLRAMS     REL-V3R1M0  5763-PW1
     A                                      SFLSIZ(0100)
     A                                      SFLPAG(0014)
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A                                      OVERLAY
     A            HDPAG1         4S 0H      SFLRCDNBR
     A                                  1  2'HP722'
     A                                      DSPATR(HI)
     A                                  1 25'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  2 27'VIEW REPORT CODES'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  4  2'Report category ...:'
     A            C1CACD    R        O  4 24REFFLD(CACACD)
     A            C1CADS    R        O  4 31REFFLD(CACADS)
     A                                  5 55'User'
     A                                      DSPATR(HI)
     A                                  6  4'Report'
     A                                      DSPATR(HI)
     A                                  6 16'Report'
     A                                      DSPATR(HI)
     A                                  6 52'Controlled'
     A                                      DSPATR(HI)
     A                                  6 66'Category'
     A                                      DSPATR(HI)
     A                                  7  5'Code'
     A                                      DSPATR(HI)
     A                                  7 14'Short Name'
     A                                      DSPATR(HI)
     A                                  7 29'Report Long Name'
     A                                      DSPATR(HI)
     A                                  7 56'Y/N'
     A                                      DSPATR(HI)
     A                                  7 69'Seq'
     A                                      DSPATR(HI)
     A*
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R HP722K1
     A*%%TS  SD  19940407  133822  ISLRAMS     REL-V2R2M0  5738-PW1
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A                                      DSPATR(HI)
     A                                 23  2'F3=Exit'
     A N90                             23 11'F5=Refresh'
     A                                 23 23'F12=Previous'
     A*
     A*---------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE FORMAT
     A*---------------------------------------------------------------
     A          R HP722ES                   SFL SFLMSGRCD(24)
     A*
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
     A*                                                                *********
     A*---------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*---------------------------------------------------------------
     A          R HP722EC                   SFLCTL(HP722ES)
     A*
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
