/***************************************************************** */
/*                                                                 */
/*  PROGRAM NUMBER:  HP4000CL                                      */
/*  PROGRAM NAME:    LISTING OF BGF DAILY PRODUCTION FOR A         */
/*                   FARM SITE                                     */
/*  PROGRAMMER:      LEANNE FEDOR                                  */
/*  CREATE DATE:     01/10/02                                      */
/*                                                                 */
/*  FUNCTION:        THIS CL IS SUBMITTED WITH QCMDEXC FROM        */
/*                   HP4020-WORK WITH BGF DAILY PRODUCTION         */
/*                   OPTIONS ARE PASSED THROUGH THE LDA.           */
/*                                                                 */
/*                   THE LISTING WILL BE GENERATED FOR             */
/*                   1 WEEK OF DATA.                               */
/*                                                                 */
/*******************************************************************/
/*  MODIFIED:                                                      */
/*******************************************************************/

             PGM

/* DEFINE THE VARIABLE REQUIRED IN THE COMMAND TO OVERRIDE THE     */
/* PRINT FILE TO A LASER PRINTER                                   */

             DCL        VAR(&CMD) TYPE(*CHAR) LEN(512)
             DCL        VAR(&QDATA) TYPE(*CHAR) LEN(34)

             DCL        VAR(&LDCDYR) TYPE(*DEC) LEN(4)
             DCL        VAR(&LDCDWK) TYPE(*DEC) LEN(2)

             DCL        VAR(&HOLD) TYPE(*CHAR) LEN(10)
             DCL        VAR(&COPYS) TYPE(*DEC) LEN(2)
             DCL        VAR(&OUTQ) TYPE(*CHAR) LEN(10)

             CHGVAR     VAR(&LDCDYR) VALUE(%SST(*LDA 6 4))
             CHGVAR     VAR(&LDCDWK) VALUE(%SST(*LDA 10 2))

/*------------------------------------------------------------------*/
/* SET UP/RUN OPEN QUERY TO SELECT ONLY BGF FARMS                   */
/*------------------------------------------------------------------*/

             CHGVAR     VAR(%SST(&QDATA 1 18)) VALUE('FSPPCD *EQ +
                          "BGF  "')

             OVRDBF     FILE(HSP018)  SHARE(*YES)
             OPNQRYF    FILE((HSP018)) QRYSLT(&QDATA) KEYFLD(*FILE) +
                          SEQONLY(*YES)

/*-------------------------------------------------------------------*/
/* SET UP/RUN OPEN QUERY TO SELECT ONLY DAILY RECORDS FOR THE USER'S */
/* SELECTED WEEK                                                     */
/*-------------------------------------------------------------------*/

/*  ALWAYS SELECT RECORDS FOR THE SPECIFIED CALENDAR YEAR        */

             CHGVAR     VAR(&QDATA) VALUE(' ')

             CHGVAR     VAR(%SST(&QDATA 1 10)) VALUE('BDCDYR *EQ')
             CHGVAR     VAR(%SST(&QDATA 12 4)) VALUE(&LDCDYR)

/*  ALWAYS SELECT RECORDS FOR THE SPECIFIED CALENDAR WEEK        */

             CHGVAR     VAR(%SST(&QDATA 17 15)) VALUE('*AND BDCDWK +
                          *EQ')
             CHGVAR     VAR(%SST(&QDATA 33 2)) VALUE(&LDCDWK)


             OVRDBF     FILE(HSL082A) SHARE(*YES)
             OPNQRYF    FILE((HSL082A)) QRYSLT(&QDATA) KEYFLD(*FILE) +
                          SEQONLY(*YES)

/*------------------------------------------------------------------*/
/*  SET UP PRINT FILE OVERRIDES                                      */
/*------------------------------------------------------------------*/

/* SET PARAMETERS FROM LDA                                           */

 HOLD:       IF         COND(%SST(*LDA 411 1) = 'Y') THEN(CHGVAR +
                          VAR(&HOLD) VALUE(*YES))
             ELSE       CMD(CHGVAR VAR(&HOLD) VALUE(*NO))

 COPYS:      CHGVAR     VAR(&COPYS) VALUE(%SST(*LDA 412 1))
             IF         COND(&COPYS = 0) THEN(CHGVAR VAR(&COPYS) +
                          VALUE(1))

 OUTQ:       CHGVAR     VAR(&OUTQ) VALUE(%SST(*LDA 413 10))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(%SST(*LDA 401 10)))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(*JOB))


/*------------------------------------------------------------------*/
/* OVERRIDE PRINT FILE                                              */
/*------------------------------------------------------------------*/

/* USE THE CUSTOMIZED SEABOARD COMMAND TO OVERRIDE THE PRINT FILE.  */
/* THIS COMMAND ALLOWS THE REPORT TO BE GENERATED ON A STANDARD LINE*/
/* PRINTER OR A LASER PRINTER. TO USE THIS OVERRIDE YOU MUST DECLARE*/
/* A COMMAND VARIABLE AND FOLLOW THE OVERRIDE WITH A CALL TO        */
/* QCMDEXC.                                                         */


             SBDPRTOVR  CMDVAR(&CMD) FILE(QPRINT) OUTQ(&OUTQ) +
                          WIDTH(132) LANDSCAPE(*NO) DRAWER(*PRTF) +
                          COPIES(&COPYS) HOLD(&HOLD)

             CALL       PGM(QCMDEXC) PARM(&CMD 512)


/*------------------------------------------------------------------*/
/*  CALL LISTING PROGRAM                                             */
/*------------------------------------------------------------------*/

             CALL       PGM(HP6020)

             CLOF       OPNID(HSP018)
             CLOF       OPNID(HSL082A)
             DLTOVR     FILE(*ALL)

