     A*%%TS  SD  20021114  095019  ISLRAMS     REL-V5R1M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION
     A*  DISPLAY FILE:  HPD7085
     A*  TITLE:         VIEW GROUP SIRE LINE/SOW SOURCES
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  DATE:          11/13/02
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(FE)
     A                                      PRINT
     A*---------------------------------------------------------------
     A* SUBFILE FORMAT 1
     A*---------------------------------------------------------------
     A*
     A          R HP7085S1                  SFL
     A*%%TS  SD  20021114  085800  ISLRAMS     REL-V5R1M0  5722-WDS
     A  85                                  SFLNXTCHG
     A            HDRRN1         4Y 0H
     A            D1DESC         9A  O  9  2DSPATR(HI)
     A            D1GG0     R        O  9 16REFFLD(HSRREF/@@GG5A *LIBL/HSPREF)
     A            D1SICD    R        O  9 28REFFLD(HSRREF/@@SICD *LIBL/HSPREF)
     A            D1RCHD    R        O  9 37REFFLD(@@HD05)
     A                                      EDTCDE(K)
     A            D1HDPR    R        O  9 47REFFLD(HSRREF/@@DOPR *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A            D1ALPHFSCD     7A  O  9 56
     A            D1CELL    R        O  9 70REFFLD(HSRREF/@@CELL *LIBL/HSPREF)
     A                                      EDTCDE(Z)
     A*
     A*----------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT  1
     A*----------------------------------------------------------------
     A*
     A          R HP7085C1                  SFLCTL(HP7085S1)
     A*%%TS  SD  20021114  095019  ISLRAMS     REL-V5R1M0  5722-WDS
     A                                      SFLSIZ(0050)
     A                                      SFLPAG(0013)
     A                                      CF03(03 'EXIT')
     A                                      SFLCSRRRN(&HDRR1)
     A                                      OVERLAY
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            HDRR1          5S 0H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 26'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  2 18'VIEW GROUP SIRE LINE/SOW SOURCE DA-
     A                                      TA'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  4  2'Hog group.:'
     A            C1HGCD    R        O  4 14REFFLD(@@HGCD)
     A                                  4 30'Farm:'
     A            C1FSCD    R        O  4 36REFFLD(HSRREF/@@FSCD *LIBL/HSPREF)
     A                                      EDTCDE(Z)
     A                                  4 42'Bldg:'
     A            C1BLCD    R        O  4 48REFFLD(HSRREF/@@BLCD *LIBL/HSPREF)
     A                                  4 55'Prod phase:'
     A            C1PPCD    R        O  4 67REFFLD(HSRREF/@@PPCD *LIBL/HSPREF)
     A                                  4 73'Sex:'
     A            C1SXCD    R        O  4 78REFFLD(HSRREF/@@SXCD *LIBL/HSPREF)
     A                                  5  2'Group type:'
     A            C1GTDS    R        O  5 14REFFLD(GTGTDS)
     A                                  5 30'Cell:'
     A            C1CELL    R        O  5 36REFFLD(HSRREF/@@CELL *LIBL/HSPREF)
     A                                      EDTCDE(Z)
     A                                  5 42'Room:'
     A            C1RMCD    R        O  5 48REFFLD(HSRREF/@@RMCD *LIBL/HSPREF)
     A                                  5 55'Prod type.:'
     A            C1PTCD    R        O  5 67REFFLD(HSRREF/@@PTCD *LIBL/HSPREF)
     A                                  7 12'Cognos Report'
     A                                      DSPATR(HI)
     A                                  7 28'Sire'
     A                                      DSPATR(HI)
     A                                  7 35'Received'
     A                                      DSPATR(HI)
     A                                  7 46'Percent'
     A                                      DSPATR(HI)
     A                                  7 56'Origin'
     A                                      DSPATR(HI)
     A                                  7 67'Origin'
     A                                      DSPATR(HI)
     A                                  8 15'Grouping'
     A                                      DSPATR(HI)
     A                                  8 28'Line'
     A                                      DSPATR(HI)
     A                                  8 39'Head'
     A                                      DSPATR(HI)
     A                                  8 46'of Total'
     A                                      DSPATR(HI)
     A                                  8 56'BGF Farm'
     A                                      DSPATR(HI)
     A                                  8 67'BGF Cell'
     A                                      DSPATR(HI)
     A*
     A*----------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*----------------------------------------------------------------
     A          R HP7085K1
     A*%%TS  SD  19950829  091241  ISLRAMS     REL-V3R0M5  5763-PW1
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
      *----------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *----------------------------------------------------------------
     A          R HP7085ES                  SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
     A*----------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*----------------------------------------------------------------
     A*
     A          R HP7085EC                  SFLCTL(HP7085ES)
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
