/*********************************************************************/
/*                                                                   */
/*  SYNOPSIS -  CALL ACCRUAL YTD SUMMARY BY CUSTOMER REPORT          */
/*                                                                   */
/*  SYSTEM    - ACCOUNTS RECEIVABLES                                 */
/*  PROGRAM   - PRINT  ACCRUAL YTD SUMMARY BY CUSTOMER REPORT        */
/*  DATE      - 06/25/2007                                           */
/*                                                                   */
/*  PARAMETERS:                                                      */
/*                                                                   */
/*  I    &FPERN      - FROM PERIOD                                   */
/*  I    &CPERN      - CURRENT PERIOD                                */
/*  I    &YEARN      - FISCAL YEAR                                   */
/*  I    &ACCRCODE   - ACCRUAL CODE (BKR)                            */
/*  I    &ACCRTYPE   - ACCRUAL TYPE                                  */
/*  I    &LDGRYN     - LEDGER ACCRUAL (Y/N)                          */
/*  I    &CUSTN      - CUSTOMER NUMBER PASSED                        */
/*                                                                   */
/*********************************************************************/
 PMIUUPC:    PGM        PARM(&OUTQ &HOLD &SAVE &NQSTS &NQ &FPERN +
                          &CPERN &YEARN &COPYN &ACCRTYPE &ACCRCODE +
                          &LDGRYN &CUSTN &EMAILFMT &EMAIL)

/* PARMS                                                             */
        DCL    &OUTQ       *CHAR (10)     /* OUTPUT QUEUE     */
        DCL    &HOLD       *CHAR (4)      /* HOLD STATUS      */
        DCL    &SAVE       *CHAR (4)      /* SAVE STATUS      */
        DCL    &NQSTS      *CHAR (1)      /* NQ RUN STATUS    */
        DCL    &NQ         *CHAR (7)      /* NIGHT QUEUE      */
        DCL    &YEARN      *DEC           /* FISCAL YEAR PASS */
        DCL    &FPERN      *DEC           /* FROM PERD PASS   */
        DCL    &CPERN      *DEC           /* CURRENT PERIOD   */
        DCL    &COPYN      *DEC           /* # COPIES PASS    */
        DCL    &CUSTN      *DEC           /* CUST NBR PASS    */
        DCL    &COPY       *DEC  (2 0)    /* # COPIES         */
        DCL    &ACCRTYPE   *CHAR (2)      /* ACCRUAL TYPE     */
        DCL    &ACCRCODE   *CHAR (3)      /* ACCRUAL CODE     */
        DCL    &LDGRYN     *CHAR (1)      /* LEDGER ACCRUAL Y/N */
        DCL    &CUST       *DEC  (7 0)    /* CUSTOMER NUMBER  */
        DCL    &RTNCDE     *CHAR (7)      /* RETURN CODE      */
        DCL    &FPER       *DEC  (2 0)    /* FROM PERIOD      */
        DCL    &CPER       *DEC  (2 0)    /* CURRENT PERIOD   */
        DCL    &CTRY       *DEC  (2 0)    /* CENTURY NUMERIC  */
        DCL    &YEAR       *DEC  (4 0)    /* FISCAL YEAR      */
        DCL    &YEARA      *CHAR (4)      /* FISCAL YEAR      */
        DCL    &CTRYA      *CHAR (2)      /* CENTURY ALPHA    */
        DCL    &FROMYRA    *CHAR (2)      /* FROM YEAR ALPHA  */
        DCL    &FROMYR     *DEC  (2 0)    /* FROM YEAR NUMERIC*/
        DCL    &EMAIL      *CHAR (50)     /* EMAIL ADDRESS    */
        DCL    &EMAILFMT   *CHAR (5)      /* EMAIL FORMAT     */
        DCL    &PRTF       *CHAR (10)     /* PRTF TO EMAIL    */
        DCL    &TEXT       *CHAR (50)     /* TEXT             */

/* OPNQRY FILE STATEMENT PARAMETERS                                  */

/*********************************************************************/
/* MONITOR FOR ERRORS                                                */
/*********************************************************************/
             MONMSG     MSGID(CPF0000 CPF9999)

/*********************************************************************/
/* CONVERT 15.5 VARIABLES TO 2.0 & 4.0, CONVERT NUMERIC TO CHARACTER */
/*********************************************************************/
             CHGVAR     VAR(&YEAR) VALUE(&YEARN)
             CHGVAR     VAR(&YEARA) VALUE(&YEAR)

             CHGVAR     VAR(&CTRYA) VALUE(%SST(&YEARA 1 2))
             CHGVAR     VAR(&CTRY) VALUE(&CTRYA)

             CHGVAR     VAR(&FROMYRA) VALUE(%SST(&YEARA 3 2))
             CHGVAR     VAR(&FROMYR) VALUE(&FROMYRA)

             CHGVAR     VAR(&FPER) VALUE(&FPERN)

             CHGVAR     VAR(&CPER) VALUE(&CPERN)

             CHGVAR     VAR(&RTNCDE) VALUE('       ')

             CHGVAR     VAR(&CUST) VALUE(&CUSTN)

/********************************************************************/
/* OVERRIDE THE PRINTER FILE. TO EMAIL, THE PRINTER FILE MUST BE    */
/* HOLD = *YES AND SAVE = *YES.                                     */
/********************************************************************/
             IF         COND(&EMAIL *NE '               ') THEN(DO)
             CHGVAR     VAR(&HOLD) VALUE(*YES)
             CHGVAR     VAR(&SAVE) VALUE(*YES)
             ENDDO

             CHGVAR     VAR(&COPY) VALUE(&COPYN)
             OVRPRTF    FILE(PMJAPFR$) OUTQ(&OUTQ) COPIES(&COPY) +
                          HOLD(&HOLD) SAVE(&SAVE)

             CALL       PGM(PMJAPFR) PARM(&RTNCDE &FPER &CPER +
                          &FROMYR &CTRY &FROMYRA &CTRYA &ACCRCODE +
                          &ACCRTYPE &LDGRYN &CUST)

/*-------------------------------------------------------------------*/
/* EMAIL THE REPORT IF REQUESTED                                     */
/*-------------------------------------------------------------------*/
             IF         COND(&EMAIL *NE '               ') THEN(DO)

/* EMAIL IF *XLS IS REQUESTED                                        */
             IF         COND(&EMAILFMT *EQ '*XLS') THEN(DO)
             CRTPF      FILE(QTEMP/ACCRLRPT) RCDLEN(178)
             CPYSPLF    FILE(PMJAPFR$) TOFILE(QTEMP/ACCRLRPT) +
                          SPLNBR(*LAST)

             EXECUTE    VIEW(ACCRLRPTBK) PCFMT(*XLS) REPLACE(*YES) +
                          RECIPIENT(&EMAIL) EMLMSG('ACCRUAL YTD +
                          SUMMARY-CUST in *XLS') TEXT('ACCRUAL YTD +
                          SUMMARY-CUST in *XLS')
             ENDDO

/* EMAIL IF *TXT IS REQUESTED                                        */
             IF         COND(&EMAILFMT *EQ '*TXT') THEN(DO)
             CRTPF      FILE(QTEMP/ACCRLRPT) RCDLEN(178)
             CPYSPLF    FILE(PMJAPFR$) TOFILE(QTEMP/ACCRLRPT) +
                          SPLNBR(*LAST)

             EXECUTE    VIEW(ACCRLRPTBK) PCFMT(*TXT) REPLACE(*YES) +
                          RECIPIENT(&EMAIL) EMLMSG('ACCRUAL YTD +
                          SUMMARY-CUST in *TXT') TEXT('ACCRUAL YTD +
                          SUMMARY-CUST in *TXT')
             ENDDO

/* EMAIL IF *PDF OR *RTF ARE REQUESTED                               */
/* THESE WILL RECEIVE THE CREATED REPORT AS AN ATTACHMENT            */
             ELSE       CMD(DO)
             CHGVAR     VAR(&PRTF) VALUE('PMJAPFR$')
             CHGVAR     VAR(&TEXT) VALUE('Accrual YTD Summary by +
                          CUSTOMER in' *BCAT &EMAILFMT)
             CALL       PGM(PMB8UPC) PARM(&PRTF &TEXT &EMAIL &EMAILFMT)
             ENDDO
             ENDDO

EXIT:  ENDPGM
