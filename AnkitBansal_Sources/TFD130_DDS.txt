     A*%%TS  SD  20050308  070933  ISLRAMS     REL-V5R2M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        TRIUMPH FOODS
     A*  DISPLAY FILE:  TFD130
     A*  TITLE:         REVISE CHARGE CODES
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  DATE:          03/02/05
     A*
     A*  INDICATOR 60 IS ON WHEN IN REVISE MODE
     A*  INDICATOR 61 IS ON WHEN IN CREATE MODE
     A*  INDICATOR 62 IS ON WHEN IN DELETE MODE
     A*  INDICATOR 63 IS ON WHEN THE CHARGE CODE HAS BEEN USED OR IT IS NOT
     A*                  USER-CONTROLLED AND YOU ARE PROTECTING SOME FIELDS
     A*  INDICATOR 64 IS ON WHEN THE CHARGE CODE IS NOT USER-CONTROLLED
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/TFPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A                                      CF04(04 'PROMPT')
     A  60N62                               CF05(05 'REFRESH')
     A  60N62                               CF11(11 'DELETE')
     A                                      CA12(12 'PREVIOUS')
     A*
     A*--------------------------------------------------------------
     A* RECORD FORMAT ONE
     A*--------------------------------------------------------------
     A          R TF130R1
     A*%%TS  SD  20050308  070933  ISLRAMS     REL-V5R2M0  5722-WDS
     A                                      SETOF(30)
     A                                      SETOF(31)
     A                                      SETOF(32)
     A                                      SETOF(33)
     A                                      SETOF(34)
     A                                      SETOF(35)
     A                                      SETOF(37)
     A                                      SETOF(38)
     A                                      SETOF(39)
     A                                      SETOF(53)
     A                                      SETOF(54)
     A                                      SETOF(55)
     A                                      SETOF(57)
     A                                      SETOF(58)
     A                                      SETOF(59)
     A                                      RTNCSRLOC(&RCD &FLD)
     A                                      OVERLAY
     A            RCD       R        H      REFFLD(@@RCD)
     A            FLD       R        H      REFFLD(@@FLD)
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 27'REVISE CHARGE CODE'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USR)
     A                                      DSPATR(HI)
     A            D1MODE         6A  O  2  2DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USR)
     A                                      DSPATR(HI)
     A                                  4  2'Charge code .....'
     A            D1CCCD    R        B  4 20REFFLD(@@CCCD)
     A  61N31                               DSPATR(HI)
     A  31                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A  60
     AO 62                                  DSPATR(PR)
     A  61                                  DSPATR(UL)
     A N61                              4 32'User-Controlled:'
     A                                      COLOR(PNK)
     A N61        D1UCFL    R        O  4 49REFFLD(TFREF/@@UCFL *LIBL/TFPREF)
     A                                      COLOR(PNK)
     A                                  6  2'Charge code description ...'
     A            D1CCDS    R        B  6 32REFFLD(TFREF/@@CCDS *LIBL/TFPREF)
     A                                      CHECK(LC)
     A N62N32                               DSPATR(HI)
     A  32                                  DSPATR(PC)
     A  32                                  DSPATR(RI)
     A  62                                  DSPATR(PR)
     A N62                                  DSPATR(UL)
     A                                  7  2'Company number ............'
     A            D1CONO    R        B  7 32REFFLD(@@CONO)
     A                                      EDTCDE(M)
     A  30                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A N62N30                               DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(PR)
     A            D1CONM    R        O  7 43REFFLD(TFREF/@@CONM *LIBL/TFPREF)
     A                                  8  2'G/L code-debit ............'
     A                                  8 30'?'
     A            D1GLDBCD  R        B  8 32REFFLD(TFREF/@@GLDBCD *LIBL/TFPREF)
     A  33
     AO 53                                  DSPATR(PC)
     A  33                                  DSPATR(RI)
     A N62N33                               DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(PR)
     A            D1GLDBCDDSR        O  8 43REFFLD(TFREF/@@GLDBCDDS *LIBL/TFPRE-
     A                                      F)
     A                                  9  2'G/L code-credit ...........'
     A                                  9 30'?'
     A            D1GLCRCD  R        B  9 32REFFLD(TFREF/@@GLCRCD *LIBL/TFPREF)
     A  34
     AO 54                                  DSPATR(PC)
     A  34                                  DSPATR(RI)
     A N62N34                               DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(PR)
     A            D1GLCRCDDSR        O  9 43REFFLD(TFREF/@@GLCRCDDS *LIBL/TFPRE-
     A                                      F)
     A                                 10  2'Active/inactive status ....'
     A                                 10 30'?'
     A            D1AIST    R        B 10 32REFFLD(TFREF/@@AIST *LIBL/TFPREF)
     A  39
     AO 59                                  DSPATR(PC)
     A  39                                  DSPATR(RI)
     A N62N39                               DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(PR)
     A  65                             12  2'Since this Charge Code has already-
     A                                       been used on an Invoice Charge,'
     A                                      DSPATR(HI)
     A  66                             12  2'Since this Charge Code is not User-
     A                                      -Controlled, the following fields'
     A                                      DSPATR(HI)
     A  65                             13  2'the following fields cannot be cha-
     A                                      nged.'
     A                                      DSPATR(HI)
     A  66                             13  2'cannot be changed.'
     A                                      DSPATR(HI)
     A                                 15  2'Charge code frequency .....'
     A N63                             15 30'?'
     A            D1CFQCD   R        B 15 32REFFLD(TFREF/@@CFQCD *LIBL/TFPREF)
     A  35
     AO 55                                  DSPATR(PC)
     A  35                                  DSPATR(RI)
     A N62N63N35                            DSPATR(HI)
     A N62N63                               DSPATR(UL)
     A  62
     AO 63                                  DSPATR(PR)
     A            D1CFQDS   R        O 15 43REFFLD(TFREF/@@CFQDS *LIBL/TFPREF)
     A                                 16  2'Include in invoice total ..'
     A N63                             16 30'?'
     A            D1INFL    R        B 16 32REFFLD(TFREF/@@INFL *LIBL/TFPREF)
     A  37
     AO 57                                  DSPATR(PC)
     A  37                                  DSPATR(RI)
     A N62N63N37                            DSPATR(HI)
     A N62N63                               DSPATR(UL)
     A  62
     AO 63                                  DSPATR(PR)
     A                                 17  2'System module .............'
     A N63                             17 30'?'
     A            D1SMCD    R        B 17 32REFFLD(TFREF/@@SMCD *LIBL/TFPREF)
     A  38
     AO 58                                  DSPATR(PC)
     A  38                                  DSPATR(RI)
     A N62N63N38                            DSPATR(HI)
     A N62N63                               DSPATR(UL)
     A  62
     AO 63                                  DSPATR(PR)
     A            D1SMDS    R        O 17 43REFFLD(TFREF/@@SMDS *LIBL/TFPREF)
     A*--------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*--------------------------------------------------------------
     A          R TF130K1
     A*%%TS  SD  19941026  154142  ISLRAMS     REL-V3R0M5  5763-PW1
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A N62N61                          23 11'F5=Refresh'
     A N62N61                          23 23'F11=Delete'
     A                                 23 35'F12=Previous'
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *---------------------------------------------------------------
     A          R TF130ES                   SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE CONTROL FORMAT
      *---------------------------------------------------------------
     A          R TF130EC                   SFLCTL(TF130ES)
      *
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
