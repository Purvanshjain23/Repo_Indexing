/********************************************************************/
/*                                                                  */
/*  SYNOPSIS - READ CARRIER FILE, PROCESS ONLY ACTIVE CARRIERS,     */
/*             CALL THE DAILY TRAFFIC REPORTS FROM JOB SCHEDULER.   */
/*             PRINT THREE DAILY REPORTS BY CARRIER THEN            */
/*             EMAIL THE REPORT TO THE CARRIER                      */
/*             THIS IS CALLED FROM THE JOB SCHEDULER                */
/*                                                                  */
/*  SYSTEM    - ORDER MANAGEMENT SYSTEM                             */
/*  PROGRAM   - PRINT DAILY TRAFFIC REPORTS BY CARRIER AND EMAIL    */
/*              BASIC PARMS ARE HARD CODED FOR REPORT TYPE 1 ONLY   */
/*  DATE      - 04/16/2015                                          */
/*  NOTES     - LOGIC COPIED FROM PBKWUPC FOR DATES                 */
/*              THIS PROGRAM CALLS PBQ3UPC TWICE FOR EACH CARRIER,  */
/*              ONCE FOR EACH COMPANY CODE (360 AND 960)            */
/*                                                                  */
/*  PARAMETERS RECEIVED:  NONE                                      */
/*                                                                  */
/********************************************************************/
/* 99/99/99 XXX X999999                                             */
/*                                                                  */
/********************************************************************/
 PBQWUPC:    PGM

/* PARMS                                                                      */
             DCL        &COMPNYN    *DEC
             DCL        &COMP       *DEC    3 0
             DCL        &PDATE      *DEC
             DCL        &PDATE2     *DEC    7 0
             DCL        &TDATE      *DEC            /* TODAY'S DATE */
             DCL        &TDATE2     *DEC    7 0     /* TODAY'S DATE */
             DCL        &DATE       *CHAR   7       /* TODAY'S DATE */
             DCL        &JOBDATE    *CHAR   6       /* TODAY'S DATE */
             DCL        &DAYS       *DEC    3 0
             DCL        &DAYS2      *DEC    8 0
             DCL        &RPTTYP     *CHAR   1
             DCL        &JOBDTE     *CHAR   7

             DCL        VAR(&COUNT) TYPE(*DEC) LEN(4 0)

             DCLF       FILE(OMFFREL0)

/* RTV TODAY'S DATE                                                 */
             RTVJOBA    DATE(&JOBDATE)
             CVTDAT     DATE(&JOBDATE) TOVAR(&JOBDTE) FROMFMT(*MDY) +
                          TOFMT(*CYMD) TOSEP(*NONE)
             CHGVAR     VAR(&DATE) VALUE(&JOBDTE)
             MONMSG     MSGID(CPF0000)

             CHGVAR     VAR(&TDATE) VALUE(&DATE)

/* RTV PRIOR SHIP DATE XF, COMPANY IS PASSED AS ZEROS               */
             CHGVAR     VAR(&COMPNYN) VALUE(360.0000)
             CHGVAR     VAR(&COMP) VALUE(&COMPNYN)
             CHGVAR     VAR(&DAYS) VALUE(1)
             CHGVAR     VAR(&TDATE2) VALUE(&TDATE)
             CALL       PGM(POE7XFR) PARM(' ' &COMP &TDATE2 &DAYS +
                          &PDATE2)

/* CLC INC/DEC DATE  SHR  XF, TODAY'S DATE, COMPANY, PRIOR DATE,    */
/*                            AND DURATION                          */
             CHGVAR     VAR(&DAYS2) VALUE(3)
             CALL       PGM(PDNKXFR) PARM(' ' &TDATE2 &COMP &PDATE2 +
                          &DAYS2)

             OVRDBF     FILE(OMFFREL0) TOFILE(*LIBL/OMFFREL0) +
                          MBR(*FIRST)
/*********************************************************************/
/* READ CARRIER FILE FOR ACTIVE CARRIERS AND PASS TO COMPANY        */
/* CARRIER FILE PROGRAM TO PROCESS AND EMAIL 3 DIFFERENT REPORTS    */
/* TO EACH CARRIER                                                  */
/*    LOADS NOT ON YARD REPORT                                      */
/*    LOADS LOADED NOT PICKED UP                                    */
/*    DAILY HOLDOVER REPORT                                         */
/*********************************************************************/
NEXTREC:     RCVF
             MONMSG     MSGID(CPF0864) EXEC(GOTO CMDLBL(END))
             CHGVAR     VAR(&COUNT) VALUE(&COUNT + 1)
             IF         COND(&FFVSST *NE 'A') THEN(GOTO +
                          CMDLBL(NEXTREC))

             IF         COND(&FFBZNA *EQ 'BUY') THEN(GOTO +
                          CMDLBL(NEXTREC))

             IF         COND(&COUNT *GE 100) THEN(GOTO CMDLBL(END))
/*------------------------------------------------------------------*/
/* DAILY TRAFFIC REPORTS, EMAILED TO EACH CARRIER                   */
/*  REPORT TYPE = 1 IS FOR DAILY HOLDOVER REPORT                    */
/*  FIRST RUN THE REPORTS FOR GUYMON, THEN RUN FOR ST JOE           */
/*------------------------------------------------------------------*/
 CALL:       CHGVAR     VAR(&PDATE) VALUE(&PDATE2)
             CHGVAR     VAR(&TDATE) VALUE(&TDATE2)
             CHGVAR     VAR(&RPTTYP) VALUE('1')

 CO360:      CHGVAR     VAR(&COMPNYN) VALUE(360.0000)
             CALL       PGM(PBQ3UPC) PARM(&PDATE &TDATE &RPTTYP +
                          &FFBZNA &COMPNYN)

 CO960:      CHGVAR     VAR(&COMPNYN) VALUE(960.0000)
             CALL       PGM(PBQ3UPC) PARM(&PDATE &TDATE &RPTTYP +
                          &FFBZNA &COMPNYN)
             GOTO       CMDLBL(NEXTREC)

 END:        DLTOVR     FILE(OMFFREL0)
             ENDPGM
