     A*%%TS  SD  20030225  073948  ISLRAMS     REL-V5R1M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD4099
     A*  TITLE:         SPECIFY OPTIONS: UPDATE HPS SALE LBS WJEM SCALE WAS
     A*                 BROKEN
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  CREATED:       02/24/03
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A  44                                  CF08(08 'SUBMIT')
     A                                      CF20(20 'WRKSPLF')
     A*
     A*---------------------------------------------------------------
     A* RECORD FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP4099R1
     A*%%TS  SD  20030225  073948  ISLRAMS     REL-V5R1M0  5722-WDS
     A                                      SETOF(36)
     A                                      SETOF(37)
     A                                      OVERLAY
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 25'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USR)
     A                                      DSPATR(HI)
     A                                  2 16'UPDATE HPS SALE LBS WHEN SCALE WAS-
     A                                       BROKEN'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@JOB)
     A                                      DSPATR(HI)
     A                                  4  2'From received date ..'
     A            D1FMDY    R     Y  B  4 25REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A N37                                  DSPATR(HI)
     A  37                                  DSPATR(PC)
     A  37                                  DSPATR(RI)
     A                                  5  2'To received date ....'
     A            D1TMDY    R     Y  B  5 25REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A N36                                  DSPATR(HI)
     A  36                                  DSPATR(PC)
     A  36                                  DSPATR(RI)
     A*
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R HP4099K1
     A*%%TS  SD  20030224  142409  ISLRAMS     REL-V5R1M0  5722-WDS
     A  43                             21  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A  44                             23 11'F8=Submit'
     A                                 23 22'F20=Spool Files'
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *---------------------------------------------------------------
     A          R HP4099ES                  SFL SFLMSGRCD(24)
      *
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
     A*---------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*---------------------------------------------------------------
     A*
     A          R HP4099EC                  SFLCTL(HP4099ES)
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
