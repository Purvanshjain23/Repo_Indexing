     A*%%TS  SD  20070713  125108  ISLRAMS     REL-V5R4M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        TRIUMPH FOODS
     A*  DISPLAY FILE:  TFD124
     A*  TITLE:         MAINTAIN WIP USAGE
     A*  PROGRAMMER:    LEANNE RAMSEY
     A*  DATE:          07/12/07
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/TFPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A                                      CF05(05 'REFRESH')
     A N89                                  CF09(09 'ACCEPT')
     A N89                                  CF13(13 'FOLD/DROP')
     A*
     A*-------------------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*-------------------------------------------------------------------------
     A          R TF124S1                   SFL
     A*%%TS  SD  20070713  124801  ISLRAMS     REL-V5R4M0  5722-WDS
     A  85                                  SFLNXTCHG
     A            HDRRN1    R        H      REFFLD(@@RRN)
     A            D1PRCD    R        O 10  2REFFLD(TFREF/@@PRCD *LIBL/TFPREF)
     A                                      EDTCDE(Z)
     A            D1WIPLB   R        B 10 10REFFLD(TFREF/@@SSLLB *LIBL/TFPREF)
     A                                      EDTCDE(K)
     A  31                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A N31                                  DSPATR(HI)
     A N31                                  DSPATR(UL)
     A            D1MQWG         9Y 0O 10 27EDTCDE(K)
     A            D1MSWG         9Y 0O 10 40EDTCDE(K)
     A            D1NAWG         9Y 0O 10 54EDTCDE(K)
     A            D1M5WG         9Y 0O 10 67EDTCDE(K)
     A            D1PRDS    R        O 11 10REFFLD(TFREF/@@ICCOM *LIBL/TFPREF)
     A                                      COLOR(PNK)
     A*
     A*-------------------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*-------------------------------------------------------------------------
     A          R TF124C1                   SFLCTL(TF124S1)
     A*%%TS  SD  20070713  125108  ISLRAMS     REL-V5R4M0  5722-WDS
     A                                      SFLSIZ(0050)
     A                                      SFLPAG(0006)
     A                                      SETOF(31)
     A N82                                  ROLLUP(25 'PAGE FORWARD')
     A                                      OVERLAY
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A  78                                  SFLFOLD(CF13)
     A  79                                  SFLDROP(CF13)
     A                                      SFLMODE(&FOLD)
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            FOLD           1A  H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 29'MAINTAIN WIP USAGE'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USR)
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@JOB)
     A                                      DSPATR(HI)
     A                                  3  2'Week-ending date:'
     A                                      DSPATR(HI)
     A            C1WEMDY   R        O  3 20REFFLD(TFREF/@@DT06 *LIBL/TFPREF)
     A                                      EDTWRD('  /  /  ')
     A                                  3 32'Warehouse company:'
     A                                      DSPATR(HI)
     A            C1CONO    R        O  3 51REFFLD(TFREF/@@CONO *LIBL/TFPREF)
     A                                  4 35'Warehouse code:'
     A                                      DSPATR(HI)
     A            C1WHS          3A  O  4 51
     A                                  5  6'Total WIP Usage Pounds'
     A                                      COLOR(BLU)
     A                                      DSPATR(UL)
     A            C1WIPLB       11  2O  6 10EDTCDE(K)
     A                                      COLOR(BLU)
     A                                  7 31'Beg Inv'
     A                                      DSPATR(HI)
     A                                  7 46'Floor'
     A                                      DSPATR(HI)
     A                                  7 60'Floor'
     A                                      DSPATR(HI)
     A                                  7 71'End Inv'
     A                                      DSPATR(HI)
     A                                  8  2'Item'
     A                                      DSPATR(HI)
     A                                  8 15'WIP'
     A                                      DSPATR(HI)
     A                                  8 19'Usage'
     A                                      DSPATR(HI)
     A                                  8 33'Final'
     A                                      DSPATR(HI)
     A                                  8 41'Production'
     A                                      DSPATR(HI)
     A                                  8 55'Production'
     A                                      DSPATR(HI)
     A                                  8 73'Final'
     A                                      DSPATR(HI)
     A                                  9  2'Code'
     A                                      DSPATR(HI)
     A                                  9 18'Pounds'
     A                                      DSPATR(HI)
     A                                  9 32'Pounds'
     A                                      DSPATR(HI)
     A                                  9 45'Pounds'
     A                                      DSPATR(HI)
     A                                  9 55'Adj Pounds'
     A                                      DSPATR(HI)
     A                                  9 72'Pounds'
     A                                      DSPATR(HI)
     A*
     A*-------------------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*-------------------------------------------------------------------------
     A          R TF124K1
     A*%%TS  SD  20070713  104137  ISLRAMS     REL-V5R4M0  5722-WDS
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A                                 23 11'F5=Refresh'
     A N89                             23 35'F9=Accept'
     A N89                             23 59'F13=Fold/Unfold'
      *
     A*-------------------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
     A*-------------------------------------------------------------------------
     A          R TF124ES                   SFL SFLMSGRCD(24)
      *
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
     A*-------------------------------------------------------------------------
      * ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*-------------------------------------------------------------------------
     A          R TF124EC                   SFLCTL(TF124ES)
      *
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
