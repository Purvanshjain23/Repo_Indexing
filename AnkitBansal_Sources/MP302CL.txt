/***************************************************************** */
/*                                                                 */
/*  ENVIRONMENT:     PORK DIVISION                                 */
/*  SYSTEM:          INTERFACE MP2 TO JDE                          */
/*  PROGRAM NUMBER:  MP302CL                                       */
/*  PROGRAM NAME:    MP2: BATCH EDIT OF UNPOSTED RECEIPTS          */
/*  PROGRAMMER:      LEANNE FEDOR                                  */
/*  CREATE DATE:     03/21/02                                      */
/*                                                                 */
/*  FUNCTION:        THIS CL RUNS A BATCH EDIT OVER UNPOSTED P.O.  */
/*                   HEADER/DETAIL FILES.                          */
/*                                                                 */
/*******************************************************************/
/* MODIFIED:                                                       */
/* 03/18/14  LeAnne Ramsey (E2992)                                 */
/*           Changed to handle multiple companies in a single      */
/*           environment.                                          */
/*******************************************************************/

             PGM

             DCL        VAR(&QDATA) TYPE(*CHAR) LEN(35)
             DCL        VAR(&HOLD)  TYPE(*CHAR) LEN(10)
             DCL        VAR(&COPYS) TYPE(*DEC)  LEN(2)
             DCL        VAR(&OUTQ)  TYPE(*CHAR) LEN(10)
             DCL        VAR(&ACONO) TYPE(*DEC)  LEN(3)

             CHGVAR     VAR(&ACONO) VALUE(%SST(*LDA 86 3))

/*-------------------------------------------------------------------*/
/*  SET UP PRINT FILE OVERRIDES AND OVERRIDE PRINT FILE              */
/*-------------------------------------------------------------------*/

 HOLD:       IF         COND(%SST(*LDA 411 1) = 'Y') THEN(CHGVAR +
                          VAR(&HOLD) VALUE(*YES))
             ELSE       CMD(CHGVAR VAR(&HOLD) VALUE(*NO))

 COPYS:      CHGVAR     VAR(&COPYS) VALUE(%SST(*LDA 412 1))
             IF         COND(&COPYS = 0) THEN(CHGVAR VAR(&COPYS) +
                          VALUE(1))

 OUTQ:       CHGVAR     VAR(&OUTQ) VALUE(%SST(*LDA 413 10))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(*JOB))

             OVRPRTF    FILE(QPRINT) TOFILE(PRINT198) OUTQ(&OUTQ) +
                          COPIES(&COPYS) HOLD(&HOLD) SHARE(*YES)

/*------------------------------------------------------------------*/
/* Extract data from PO detail                                      */
/*------------------------------------------------------------------*/

             CHGVAR     VAR(&QDATA) VALUE(' ')
             CHGVAR     VAR(%SST(&QDATA 1 14)) VALUE('PDPOFL *EQ "N"')

/* Accounting Company                                               */
             IF         COND(&ACONO *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 16 16)) VALUE('*AND PDACONO +
                          *EQ')
             CHGVAR     VAR(%SST(&QDATA 33 3)) VALUE(&ACONO)
             ENDDO

             OVRDBF     FILE(MPL103D) SHARE(*YES)
             OPNQRYF    FILE((MPL103D)) OPTION(*ALL) QRYSLT(&QDATA) +
                          KEYFLD(*FILE) SEQONLY(*YES)

/*-------------------------------------------------------------------*/
/*  RUN BATCH EDIT OVER P.O. HEADER/DETAIL FILES.                    */
/*-------------------------------------------------------------------*/

             CALL       PGM(MP302)

             CLOF       OPNID(MPL103D)
             DLTOVR     FILE(*ALL)

             ENDPGM

