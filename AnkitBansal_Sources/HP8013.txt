      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Hog Production System
      * PROGRAM:     HP8013 - Cost Movements: Delete Data for Year/Period
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     03/10/03
      *
      * Function:    Delete Costing data for a specific year/period.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 12/13/04  LeAnne Fedor
      *           Six of our suppport files have been renamed to be the 'workfiles'
      *           that they truly are:
      *                HSP113  became HSP352
      *                HSP114  became HSP357
      *                HSP115  became HSP358
      *                HSP116  became HSP359
      *                HSP117  became HSP360
      *                HSP119  became HSP361
      *           These files are now cleared at the beginning of the EOP process. So,
      *           we removed the delete logic that existed for them in this program.
      *
      * 02/29/16    Rose Centonze    E5290
      *             Added company code parm and chain to hsl034d to get company for selection
      *             for all files
      /eject
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fhsl047a   uf   e           k disk
      *    Group Period
      *
      *
     Fhsl055c   uf   e           k disk
      *    Period Costs for Hogs Moved Out
      *
      *
     Fhsl118a   uf   e           k disk
      *    Cost movements: Costed movements
      *
      *
     Fhsl120a   uf   e           k disk
      *    Cost movements: Costed movements expense codes
      *
     Fhsl034d   uf   e           k disk
      *    hog group              (hghgsn)
      /eject
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Parm fields
      *
     D xxacyr          s                   like(cmacyr)
     D xxacpe          s                   like(cmacpe)
      *
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * Mainline
      ****************************************************************
      *
      * Delete current period records from the files.
      *
      * Group period
      *
     C     key01         setll     hsl047a
      *
     C                   dou       *in90 = *on
     C     key01         reade     hsl047a                                90
     C                   if        *in90 = *off
     c     gphgsn        chain     hsl034d                            50           e5290
     C                   if        *in50 = *off and hgcocd = xxcocd                "
     C                   delete    gprec
     C                   endif                                                    "
     C                   endif
     C                   enddo
      *
      *
      * Period costs for hogs moved out
      *
     C     key01         setll     hsl055c
      *
     C                   dou       *in90 = *on
     C     key01         reade     hsl055c                                90
     C                   if        *in90 = *off
     c     pchgsn        chain     hsl034d                            50           e5290
     C                   if        *in50 = *off and hgcocd = xxcocd                "
     C                   delete    pcrec
     C                   endif
     C                   endif
     C                   enddo
      *
      *
      *
      *
      * Costed movements
      *
     C     key01         setll     hsl118a
      *
     C                   dou       *in90 = *on
     C     key01         reade     hsl118a                                90
     C                   if        *in90 = *off
     c     cmorhgsn      chain     hsl034d                            50           e5290
     C                   if        *in50 = *off and hgcocd = xxcocd                "
     C                   delete    cmrec
     C                   endif
     C                   endif
     C                   enddo
      *
      *
      * Origin group summarized expenses
      *
     C     key01         setll     hsl120a
      *
     C                   dou       *in90 = *on
     C     key01         reade     hsl120a                                90
     C                   if        *in90 = *off
     c     cxorhgsn      chain     hsl034d                            50           e5290
     C                   if        *in50 = *off and hgcocd = xxcocd                "
     C                   delete    cxrec
     C                   endif
     C                   endif
     C                   enddo
      *
      *
     C                   seton                                        lr
      /eject
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
     C     *entry        plist
     C                   parm                    xxacyr
     C                   parm                    xxacpe
     C                   parm                    xxcocd            3        e5290
      *
      * Key lists
      *
     C     key01         klist
     C                   kfld                    xxacyr
     C                   kfld                    xxacpe
      *
     C                   endsr
      /eject
