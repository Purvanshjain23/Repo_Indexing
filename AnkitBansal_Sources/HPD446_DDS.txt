     A*%%TS  SD  20180227  102130  ISJBADE     REL-V7R1M0  5770-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD446
     A*  TITLE:         WORK WITH SALES
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  DATE:          06/08/95
     A*  MODIFIED BY:   BARB GUTIERREZ         2/15/2016
     A*                 ADDED BUSINESS OFFICE SELECTION
     A*****************************************************************
     A* INDICATORS
     A* 99  USER IS NOT AUTHORIZED TO FUNCTION
     A*****************************************************************
     A* Modification history
     A*  1/30/2020 JBB E16108 - [ME] Delivery Tracking Split for HPS
     A*                         Change subfile option 7 from Edit Delivery
     A*                         Tracking to Display Delivery Tracking.
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A N99                                  CF04(04 'PROMPT')
     A N99                                  CF05(05 'REFRESH')
     A N99                                  CF06(06 'CREATE')
     A N99                                  CF17(17 'BOL')
     A N99                                  CF20(20 'WRKSPLF')
     A*
     A*----------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*----------------------------------------------------------------
     A          R HP446S1                   SFL
     A*%%TS  SD  20030121  134610  ISLRAMS     REL-V5R1M0  5722-WDS
     A  85                                  SFLNXTCHG
     A            HDRRN1         4Y 0H
     A            HDSCKLDT  R        H      REFFLD(@@DT08)
     A            DFOPT          2A  B 12  2CHGINPDFT(UL)
     A  30                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A N30                                  DSPATR(HI)
     A  99                                  DSPATR(PR)
     A  99                                  DSPATR(ND)
     A            DFMVSN    R        O 12  5REFFLD(MHMVSN)
     A                                      EDTCDE(4)
     A            DFMSCD    R        O 12 13REFFLD(MHMSCD)
     A            DFSMDY    R     Y  O 12 16REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  ')
     A            DFKMDY    R     Y  O 12 25REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  ')
     A            DFHGCD    R        O 12 34REFFLD(HGHGCD)
     A            DFSCHD    R     Y  O 12 42REFFLD(SGSCHD)
     A                                      EDTCDE(Z)
     A            DFFSCD    R        O 12 48REFFLD(FSFSCD)
     A                                      EDTCDE(Z)
     A            DFBLCD    R        O 12 54REFFLD(HGBLCD)
     A            DFPPCD    R        O 12 60REFFLD(HGPPCD)
     A            DFSTCD    R        O 12 66REFFLD(SHSTCD)
     A            DFCVNO    R        O 12 72REFFLD(CHCVNO)
     A                                      EDTCDE(Z)
     A*
     A*----------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*----------------------------------------------------------------
     A          R HP446C1                   SFLCTL(HP446S1)
     A*%%TS  SD  20160215  100055  ISBGUTI     REL-V6R1M0  5761-WDS
     A                                      SFLSIZ(0050)
     A                                      SFLPAG(0010)
     A N82N99                               ROLLUP(25 'PAGE FORWARD')
     A                                      SETOF(31)
     A                                      SETOF(39)
     A                                      SETOF(51)
     A                                      SETOF(52)
     A                                      SETOF(53)
     A                                      SETOF(54)
     A                                      SETOF(55)
     A                                      SETOF(56)
     A                                      SETOF(57)
     A                                      SETOF(59)
     A                                      RTNCSRLOC(&RCD &FLD)
     A                                      OVERLAY
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 26'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A            C1VERSION     18A  O  2  2DSPATR(HI)
     A                                  2 29'WORK WITH SALES'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  4  2'Business Office .............. ?'
     A            C1FSBO    R        B  4 35REFFLD(HGFSBO)
     A  31
     AO 51                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A N31                                  DSPATR(HI)
     A  99                                  DSPATR(PR)
     A            C1BODS    R        O  4 41REFFLD(BOBODS)
     A                                  5  2'Show current movements only ..'
     A                                  5 33'?'
     A                                      DSPATR(HI)
     A            C1CURFL   R        B  5 35REFFLD(HSRREF/@@FLAG *LIBL/HSPREF)
     A  59
     AO 39                                  DSPATR(PC)
     A  39                                  DSPATR(RI)
     A N39                                  DSPATR(HI)
     A  99                                  DSPATR(PR)
     A N99                              7  2'2=Revise'
     A N99                              7 12'5=View'
     A                                  7 20'7=Display Delivery Tracking'
     A                                  7 50'9=Change Status to FP'
     A N99                              8 14'?'
     A                                      DSPATR(HI)
     A                                  8 26'Sched'
     A                                      DSPATR(HI)
     A N99                              8 36'?'
     A                                      DSPATR(HI)
     A N99                              8 50'?'
     A                                      DSPATR(HI)
     A N99                              8 62'?'
     A                                      DSPATR(HI)
     A N99                              8 67'?'
     A                                      DSPATR(HI)
     A N99                              8 76'?'
     A                                      DSPATR(HI)
     A                                  9 18'Ship'
     A                                      DSPATR(HI)
     A                                  9 26'Kill'
     A                                      DSPATR(HI)
     A                                  9 42'Sched'
     A                                      DSPATR(HI)
     A                                  9 66'Sale'
     A                                      DSPATR(HI)
     A                                  9 75'Cust'
     A                                      DSPATR(HI)
     A                                 10  2'Op'
     A                                      DSPATR(HI)
     A                                 10  5'Movemnt'
     A                                      DSPATR(HI)
     A                                 10 13'St'
     A                                      DSPATR(HI)
     A                                 10 18'Date'
     A                                      DSPATR(HI)
     A                                 10 26'Date'
     A                                      DSPATR(HI)
     A                                 10 34'Group'
     A                                      DSPATR(HI)
     A                                 10 43'Head'
     A                                      DSPATR(HI)
     A                                 10 49'Farm'
     A                                      DSPATR(HI)
     A                                 10 54'Bldg'
     A                                      DSPATR(HI)
     A                                 10 60'Phase'
     A                                      DSPATR(HI)
     A                                 10 66'Type'
     A                                      DSPATR(HI)
     A                                 10 74'Number'
     A                                      DSPATR(HI)
     A            C1MVSN    R        B 11  5REFFLD(MHMVSN)
     A                                      EDTCDE(4)
     A                                      DSPATR(HI)
     A N39N59                               DSPATR(PC)
     A  99                                  DSPATR(PR)
     A  99                                  DSPATR(ND)
     A            C1MSCD    R        B 11 13REFFLD(MHMSCD)
     A                                      DSPATR(HI)
     A  51                                  DSPATR(PC)
     A  99                                  DSPATR(PR)
     A  99                                  DSPATR(ND)
     A            C1SMDY    R     Y  B 11 16REFFLD(@@DT06)
     A                                      DSPATR(HI)
     A                                      EDTWRD('  /  /  ')
     A            C1KMDY    R     Y  B 11 25REFFLD(@@DT06)
     A                                      DSPATR(HI)
     A                                      EDTWRD('  /  /  ')
     A            C1HGCD    R        B 11 34REFFLD(HGHGCD)
     A                                      DSPATR(HI)
     A  52                                  DSPATR(PC)
     A  99                                  DSPATR(PR)
     A  99                                  DSPATR(ND)
     A            C1FSCD    R        B 11 48REFFLD(HGFSCD)
     A                                      EDTCDE(Z)
     A                                      DSPATR(HI)
     A  53                                  DSPATR(PC)
     A  99                                  DSPATR(PR)
     A  99                                  DSPATR(ND)
     A            C1BLCD    R        B 11 54REFFLD(HGBLCD)
     A                                      DSPATR(HI)
     A  99                                  DSPATR(PR)
     A  99                                  DSPATR(ND)
     A            C1PPCD    R        B 11 60REFFLD(HGPPCD)
     A                                      DSPATR(HI)
     A  54                                  DSPATR(PC)
     A  99                                  DSPATR(PR)
     A  99                                  DSPATR(ND)
     A            C1STCD    R        B 11 66REFFLD(SHSTCD)
     A                                      DSPATR(HI)
     A  56                                  DSPATR(PC)
     A  99                                  DSPATR(PR)
     A  99                                  DSPATR(ND)
     A            C1CVNO    R        B 11 72REFFLD(CHCVNO)
     A                                      DSPATR(HI)
     A                                      EDTCDE(Z)
     A  55                                  DSPATR(PC)
     A  99                                  DSPATR(PR)
     A  99                                  DSPATR(ND)
     A*----------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*----------------------------------------------------------------
     A          R HP446K1
     A*%%TS  SD  19960206  100810  ISLRAMS     REL-V3R1M0  5763-PW1
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A                                 23 11'F4=Prompt'
     A                                 23 22'F5=Refresh'
     A                                 23 34'F6=Create'
P408 A**                               23 45'F17=Print BOL'
  |  A                                 23 45'F17=Prt/Email BOL'
  |  A**                               23 60'F20=Spool Files'
P408 A                                 23 64'F20=Spool Files'
      *----------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *----------------------------------------------------------------
     A          R HP446ES                   SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
      *----------------------------------------------------------------
      * ERROR MESSAGE SUBFILE CONTROL FORMAT
      *----------------------------------------------------------------
     A          R HP446EC                   SFLCTL(HP446ES)
      *
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
