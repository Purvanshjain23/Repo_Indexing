      *****************  RPG PROGRAM HEADING  ************************
      * SYSTEM:      HOG PRODUCTION
      * PROGRAM:     HP347
      * TITLE:       BUILD PHASE WORKFILES--CLOSED GROUP ANALYSIS REPORTS
      * PROGRAMMER:  LEANNE FEDOR
      * CREATED:     06/30/97
      *
      * FUNCTION:   THIS PROGRAM BUILDS THE TWO PHASE WORKFILES
      *
      *
      *             WORKFILE HSP334 HOLDS ONE RECORD PER PHASE.
      *
      *             WORKFILE HSP335 HOLDS MULTIPLE RECORDS PER PHASE--
      *             ONE FOR EACH 'OPERATING COST', 'OTHER COST', AND
      *             'PREMIUM/DISCOUNT' ASSOCIATED WITH THE GROUP.
      *
      *             THE REPORT SELECTIONS ARE IN THE LDA AND QCMDEXC IS
      *             USED TO SUBMIT THE JOBS.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      * 11/17/97  LEANNE FEDOR
      *           PER BLAKE DAY, THE PHASE TOTAL FOR 'COST OF TRANSFERS
      *           OUT' WILL NOW BE A SIMPLE ACCUMULATION OF THE GROUP
      *           VALUES. IT WILL NOT BE CALCULATED USING THE FORMULA
      *           THAT IS USED AT THE GROUP LEVEL.
      *
      * 08/17/00  LeAnne Fedor
      *           Added logic to populate the new workfile fields for
      *           'adjusted transfers in'.
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     FHSL045A   IF   E           K DISK
      *    PREMUIM/DISCOUNT          (KEY: PDRPCD)
      *
     FHSP332    IF   E           K DISK
      *    WORKFILE FOR GROUP
      *    (KEY: GPPCD GFSBO GPTCD GFSCD GHGCD)
      *    (ONE RECORD PER GROUP)
      *
     FHSL333A   IF   E           K DISK
      *    WORKFILE FOR GROUP COSTS   (KEY: WGPPCD WGCACD WGRPCD WGHGCD)
      *    (MULTIPLE RECORDS PER GROUP)
      *
     FHSP334    O    E           K DISK
      *    WORKFILE FOR PHASES        (KEY: PPPCD)
      *    (ONE RECORD PER PHASE)
      *
     FHSP335    O    E           K DISK
      *    WORKFILE FOR PHASE COSTS   (KEY: WPPPCD WPCACD WPSEQ)
      *    (MULTIPLE RECORDS PER PHASE)
      /EJECT
      ****************************************************************
      * INPUT SPECS
      ****************************************************************
      *
      *---------------------------------------------------------------
      * STANDARD PROGRAM STATUS DATA STRUCTURE
      *---------------------------------------------------------------
      *    EXTERNALLY DEFINED AS UTPGFR (RECORD FORMAT: PGMDSR)
     D PGMDS         ESDS                  EXTNAME(UTPGFR)
      *
      *
      *---------------------------------------------------------------
      *  NAMED CONSTANTS
      *---------------------------------------------------------------
      *
     D YES             C                   CONST('Y')
     D NO              C                   CONST('N')
      *
     D                 DS
     D  DUMMYD                 1      1
      *  THE PRECEDING DUMMY DATA STRUCTURE IS ONLY TO KEEP THE I-SPEC
      *  INFO FROM RUNNING OVER INTO THE 'CALC SPECS' ON THE COMPILE
      *  LISTING.  IT SERVES NO OTHER PURPOSE AND CAN BE DELETED
      *  AT ANY TIME!!!
      /EJECT
      ****************************************************************
      * MAIN LINE
      ****************************************************************
      *
      * THE MAIN LINE LOGIC WILL BE PERFORMED TWICE--ONCE FOR NURSERY
      * GROUPS AND A SECOND TIME FOR GROW FINISH GROUPS.
      *
     C                   MOVEL     'NUR  '       WKPPCD
      *
     C                   DO        2                                            MAIN DO
      *
     C                   MOVEL     WKPPCD        PPPCD
     C                   MOVEL     NO            PROCFL
      *
      * PROCESS GROUP WORKFILE THAT HAS THE SINGLE RECORD PER
      * GROUP MAKING ALL ACCUMULATIONS.
      *
     C                   EXSR      $SACUM
      *
      * IF ANY GROUPS WERE PROCESSED, THEN:
      *   1) PERFORM PHASE TOTAL CALCULATIONS
      *   2) PROCESS THE MULTIPLE RECORD GROUP FILE
      *
     C     PROCFL        IFEQ      YES
     C                   EXSR      $STOT
     C                   EXSR      $GMULT
     C                   ENDIF
      *
      *
      * CLEAR PHASE RECORDS FOR NEXT PASS
      *
     C                   EXSR      $CLEAR
      *
      * SET UP FOR THE NEXT PASS THROUGH THIS DO LOOP
      *
     C                   MOVEL     'GF   '       WKPPCD
     C                   ENDDO                                                  MAIN DO
      *
      *
      *--------------------------------------------------------------
      * EOF PROCESSING
      *--------------------------------------------------------------
      *
      *
     C                   SETON                                        LR
      /EJECT
      *--------------------------------------------------------------
      * $SACUM - PROCESS GROUP WORKFILE WITH SINGLE RECORD PER GROUP
      *--------------------------------------------------------------
      *
     C     $SACUM        BEGSR
      *
      *
     C                   MOVEL     NO            PROCFL
     C     WKPPCD        SETLL     HSP332
      *
     C     *IN91         DOUEQ     *ON                                          DO SINGLE
     C     WKPPCD        READE     HSP332                                 91
     C     *IN91         IFEQ      *OFF                                         IF NOT EOF
      *
     C                   MOVEL     YES           PROCFL
      *
      * MOVED VALUES
     C                   ADD       GMVHD         PMVHD
     C                   ADD       GMVLB         PMVLB
      *
      * TRANSFERRED IN
     C                   ADD       GTIHD         PTIHD
     C                   ADD       GTILB         PTILB
     C                   ADD       GTIAM         PTIAM
      *
      *
      * TRANSFERRED IN SAME PHASE
      *
     C                   ADD       GISHD         PISHD
     C                   ADD       GISLB         PISLB
      *
      *
      * TRANSFERRED IN DIFF PHASE
      *
     C                   ADD       GIDHD         PIDHD
     C                   ADD       GIDLB         PIDLB
      *
      * TRANSFERRED IN ADJUSTED
      *
     C                   ADD       GIAJHD        PIAJHD
     C                   ADD       GIAJLB        PIAJLB
      *
      * TRANSFERRED OUT
      *
     C                   ADD       GTOHD         PTOHD
     C                   ADD       GTOLB         PTOLB
     C                   ADD       GTOAM         PTOAM
      *
      *
      * TRANSFERRED OUT SAME PHASE
      *
     C                   ADD       GOSHD         POSHD
     C                   ADD       GOSLB         POSLB
      *
      *
      * TRANSFERRED OUT DIFFERENT PHASE
      *
     C                   ADD       GODHD         PODHD
     C                   ADD       GODLB         PODLB
      *
      *
      * DAILY DEAD AND DOA
      *
     C                   ADD       GDDHD         PDDHD
     C                   ADD       GDDLB         PDDLB
     C                   ADD       GDLB          PDLB
      *
      *
      * INVENTORY ADJUSTMENT
      *
     C                   ADD       GIAHD         PIAHD
      *
      * MARKET SALES
      *
     C                   ADD       GMKHD         PMKHD
     C                   ADD       GMKLB         PMKLB
     C                   ADD       GMKSLB        PMKSLB
     C                   ADD       GMKAM         PMKAM
      *
      *
      * MARKET SALES + TRANSFER OUTS
      *
     C                   ADD       GMTHD         PMTHD
     C                   ADD       GMTLB         PMTLB
      *
      * FEEDER PIG SALES
      *
     C                   ADD       GFPHD         PFPHD
     C                   ADD       GFPLB         PFPLB
     C                   ADD       GFPAM         PFPAM
      *
      *
      * BUTCHER HOG SALES
      *
     C                   ADD       GBUHD         PBUHD
     C                   ADD       GBULB         PBULB
     C                   ADD       GBUAM         PBUAM
      *
      *
      * LIVE HOG SALES
      *
     C                   ADD       GLVHD         PLVHD
     C                   ADD       GLVLB         PLVLB
     C                   ADD       GLVAM         PLVAM
      *
      * PREMIUMS/DISCOUNT
      *
     C                   ADD       GPDAM         PPDAM
      * SOLD
      *
     C                   ADD       GSLHD         PSLHD
     C                   ADD       GSLLB         PSLLB
     C                   ADD       GSLAM         PSLAM
      *
      *
      * CULL SALES
      *
     C                   ADD       GCUHD         PCUHD
     C                   ADD       GCULB         PCULB
     C                   ADD       GCUAM         PCUAM
     C                   ADD       GCUHAM        PCUHAM
      *
      *
      * TOTAL OUT
     C                   ADD       GOTHD         POTHD
     C                   ADD       GOTLB         POTLB
      *
      *
      * WEIGHT GAINED
     C                   ADD       GWGLB         PWGLB
     C                   ADD       GMWGLB        PMWGLB
      *
      *
      *
      * FEED
     C                   ADD       GFCLB         PFCLB
     C                   ADD       GFCAM         PFCAM
     C                   ADD       GFCHAM        PFCHAM
     C                   ADD       GFHMAM        PFHMAM
      *
      *
      * PIG DAYS
     C                   ADD       GPDAYS        PPDAYS
      *
      *
      * BACKFAT
     C                   ADD       GBFLB         PBFLB
     C                   ADD       GBFQT         PBFQT
      *
      *
      * LEAN PERCENT
     C                   ADD       GLNLB         PLNLB
     C                   ADD       GLNQT         PLNQT
      *
      *
      * LOIN EYE
     C                   ADD       GLOLB         PLOLB
     C                   ADD       GLOQT         PLOQT
      *
      *
      * CARCASS YIELD
     C                   ADD       GYCRLB        PYCRLB
     C                   ADD       GYLVLB        PYLVLB
      *
      *
      *
      * PHASE COST
     C                   ADD       GP$AM         PP$AM
      *
      *
      * DEDUCTION/ADD-ON
     C                   ADD       GDAAM         PDAAM
      *
      *
      *
      * OPERATING COST
     C                   ADD       GOPCAM        POPCAM
      *
      *
      * OTHER COST
     C                   ADD       GOTCAM        POTCAM
      *
      *
      * TOTAL COST
     C                   ADD       GTCAM         PTCAM
      *
      * COST OF TRANSFERS OUT
      *  NOTE: AS OF 11/17/97 PER BLAKE DAY, THE PHASE TOTAL FOR
      *  COST OF TRANSFERS OUT WILL BE A SIMPLE ACCUMULATION OF
      *  THE GROUP VALUES---IT WILL NOT BE CALCULATED USING THE
      *  FORMULA THAT WAS USED AT THE GROUP LEVEL!!
      *  BUT, FLIP THE SIGN BEFORE ACCUMULATING SINCE IT WAS
      *  FLIPPED TO NEGATIVE FOR PRINTING IN THE PREVIOUS WORKFILE
      *  BUILD PROGRAM.
      *
     C     -1            MULT      GTOCAM        WKAM
     C                   ADD       WKAM          PTOCAM
      *
     C                   ENDIF                                                  IF NOT EOF
     C                   ENDDO                                                  DO SINGLE
      *
     C                   ENDSR
      /EJECT
      *--------------------------------------------------------------
      * $STOT - CALCULATE PHASE TOTALS
      *--------------------------------------------------------------
      *
     C     $STOT         BEGSR
      *
      *
      * MOVED HUNDRED WEIGHT
      *
     C     PMVLB         DIV(H)    100           PMVCW
      *
      *
      *-------------------------------
      * AVERAGE POUNDS PLACED
      *-------------------------------
      *  TRANI LBS / TRANI HEAD
      *
     C     PTIHD         IFNE      0
     C     PTILB         DIV(H)    PTIHD         PTIALB
     C                   ENDIF
      *
      *-------------------------------
      * TRANSFER IN DOLLARS PER CWT
      *-------------------------------
      *
     C                   SELECT
      *
      * FOR NURSERY GROUPS,
      * CALCULATE DOLLARS PER HEAD BASED ON MOVED HEAD
      * CALCULATE DOLLARS PER CWT BASED ON MOVED CWT
      *
     C     WKPPCD        WHENEQ    'NUR  '
     C     PMVHD         IFNE      0
     C     PTIAM         DIV(H)    PMVHD         PTI$HD
     C                   ENDIF
      *
     C     PMVCW         IFNE      0
     C     PTIAM         DIV(H)    PMVCW         PTI$CW
     C                   ENDIF
      *
      * FOR GROW FINISH GROUPS,
      * CALCULATE DOLLARS PER HEAD BASED ON MARKET SALES + TRANSFER OUT
      * CALCULATE DOLLARS PER CWT BASED ON MARKET SALES + TRANSFER OUT
      *
     C     WKPPCD        WHENEQ    'GF   '
     C     PMTHD         IFNE      0
     C     PTIAM         DIV(H)    PMTHD         PTI$HD
     C                   ENDIF
      *
     C     PMTCW         IFNE      0
     C     PTIAM         DIV(H)    PMTCW         PTI$CW
     C                   ENDIF
     C                   ENDSL
      *
      *
      *-----------------------------------
      * AVERAGE POUNDS IN FOR SAME PHASE
      *------------------------------------
      *
      * TRAN IN POUNDS SAME PHASE / TRAN IN HEAD SAME PHASE
      *
     C     PISHD         IFNE      0
     C     PISLB         DIV(H)    PISHD         PISALB
     C                   ENDIF
      *
      *
      *---------------------------------------
      * AVERAGE POUNDS IN FOR DIFFERENT PHASE
      *---------------------------------------
      * TRAN IN POUNDS DIFF PHASE / TRAN IN HEAD DIFF PHASE
      *
     C     PIDHD         IFNE      0
     C     PIDLB         DIV(H)    PIDHD         PIDALB
     C                   ENDIF
      *
      *
      *---------------------------------------
      * AVERAGE POUNDS ADJUSTED
      *---------------------------------------
      * Adjusted in pounds / adjusted in head
      *
     C     PIAJHD        IFNE      0
     C     PIAJLB        DIV(H)    PIAJHD        PIAJALB
     C                   ENDIF
      *
      *---------------------------------------
      * TRANSFERRED OUT CWT
      *---------------------------------------
      *
     C     PTOLB         DIV(H)    100           PTOCW
      *
      *
      *---------------------------------------
      * TOTAL AVERAGE POUNDS OUT =
      *---------------------------------------
      *   TOTAL TRAN OUT LBS/ TOTAL TRAN OUT HEAD
      *
     C     PTOHD         IFNE      0
     C     PTOLB         DIV(H)    PTOHD         PTOALB
     C                   ENDIF
      *
      *
      *---------------------------------------
      * AVERAGE POUNDS OUT SAME PHASE =
      *---------------------------------------
      *   TRANS OUT SAME PHASE LBS / TRANS OUT SAME PHASE HEAD
      *
     C     POSHD         IFNE      0
     C     POSLB         DIV(H)    POSHD         POSALB
     C                   ENDIF
      *
      *
      *---------------------------------------
      * AVERAGE POUNDS OUT DIFFERENT PHASE =
      *---------------------------------------
      *   TRANS OUT DIFF PHASE LBS / TRANS OUT DIFF PHASE HEAD
      *
     C     PODHD         IFNE      0
     C     PODLB         DIV(H)    PODHD         PODALB
     C                   ENDIF
      *
      *
      *---------------------------------------
      * AVERAGE DAILY DEAD AND DOA POUNDS
      *---------------------------------------
      *
     C     PDDHD         IFNE      0
     C     PDDLB         DIV(H)    PDDHD         PDDALB
     C                   ENDIF
      *
      *
      *---------------------------------------
      * MARKET SALES CWT
      *---------------------------------------
      *
     C     PMKLB         DIV(H)    100           PMKCW
      *
      *
      *---------------------------------------
      * FEEDER PIG SALES CWT
      *---------------------------------------
      *
     C     PFPLB         DIV(H)    100           PFPCW
      *
      *---------------------------------------
      * FEEDER PIG DOLLARS PER HEAD
      *---------------------------------------
      *
     C     PFPHD         IFNE      0
     C     PFPAM         DIV(H)    PFPHD         PFP$HD
     C                   ENDIF
      *
      *---------------------------------------
      * FEEDER PIG DOLLARS PER CWT
      *---------------------------------------
      *
     C     PFPCW         IFNE      0
     C     PFPAM         DIV(H)    PFPCW         PFP$CW
     C                   ENDIF
      *
      *
      *---------------------------------------
      * BUTCHER HOG SALES CWT
      *---------------------------------------
      *
     C     PBULB         DIV(H)    100           PBUCW
      *
      *---------------------------------------
      * BUTCHER HOG SALES DOLLARS PER CWT
      *---------------------------------------
      *
     C     PBUCW         IFNE      0
     C     PBUAM         DIV(H)    PBUCW         PBU$CW
     C                   ENDIF
      *
      *---------------------------------------
      * BUTCHER HOG SALES DOLLARS PER HEAD
      *---------------------------------------
      *
     C     PBUHD         IFNE      0
     C     PBUAM         DIV(H)    PBUHD         PBU$HD
     C                   ENDIF
      *
      *
      *---------------------------------------
      * MARKET LIVE SALES CWT
      *---------------------------------------
      *
     C     PLVLB         DIV(H)    100           PLVCW
      *
      *---------------------------------------
      * MARKET LIVE SALES DOLLARS PER CWT
      *---------------------------------------
      *
     C     PLVCW         IFNE      0
     C     PLVAM         DIV(H)    PLVCW         PLV$CW
     C                   ENDIF
      *
      *---------------------------------------
      * MARKET LIVE SALES DOLLARS PER HEAD
      *---------------------------------------
      *
     C     PLVHD         IFNE      0
     C     PLVAM         DIV(H)    PLVHD         PLV$HD
     C                   ENDIF
      *
      *
      *---------------------------------------
      * MARKET SALES + TRANSFERS OUT CWT
      *---------------------------------------
      *
     C     PMTLB         DIV(H)    100           PMTCW
      *
      *
      *---------------------------------------
      * SALES CWT
      *---------------------------------------
      *
     C     PSLLB         DIV(H)    100           PSLCW
      *
      *
      *---------------------------------------
      * SALES DOLLARS PER CWT
      *---------------------------------------
      *
     C     PSLCW         IFNE      0
     C     PSLAM         DIV(H)    PSLCW         PSL$CW
     C                   ENDIF
      *
      *---------------------------------------
      * SALES DOLLARS PER HEAD
      *---------------------------------------
      *
     C     PSLHD         IFNE      0
     C     PSLAM         DIV(H)    PSLHD         PSL$HD
     C                   ENDIF
      *
      *---------------------------------------
      * AVERAGE SOLD POUNDS
      *---------------------------------------
      *
     C     PSLHD         IFNE      0
     C     PSLLB         DIV(H)    PSLHD         PSLALB
     C                   ENDIF
      *
      *---------------------------------------
      * AVERAGE CULL SALE POUNDS
      *---------------------------------------
      *
     C     PCUHD         IFNE      0
     C     PCULB         DIV(H)    PCUHD         PCUALB
     C                   ENDIF
      *
      *---------------------------------------
      * CULL SALES PER HEAD AND PER CWT VALUES
      *---------------------------------------
      *
     C                   SELECT
      *
      * FOR NURSERY GROUPS,
      * CALCULATE DOLLARS PER HEAD BASED ON MOVED HEAD
      * CALCULATE DOLLARS PER CWT BASED ON MOVED CWT
      *
     C     WKPPCD        WHENEQ    'NUR  '
     C     PMVHD         IFNE      0
     C     PCUHAM        DIV(H)    PMVHD         PCU$HD
     C                   ENDIF
      *
     C     PMVCW         IFNE      0
     C     PCUHAM        DIV(H)    PMVCW         PCU$CW
     C                   ENDIF
      *
      * FOR GROW FINISH GROUPS,
      * CALCULATE DOLLARS PER HEAD BASED ON MARKET SALES + TRANSFER OUT
      * CALCULATE DOLLARS PER CWT BASED ON MARKET SALES + TRANSFER OUT
      *
     C     WKPPCD        WHENEQ    'GF   '
     C     PMTHD         IFNE      0
     C     PCUHAM        DIV(H)    PMTHD         PCU$HD
     C                   ENDIF
      *
     C     PMTCW         IFNE      0
     C     PCUHAM        DIV(H)    PMTCW         PCU$CW
     C                   ENDIF
     C                   ENDSL
      *
      *---------------------------------------
      * AVERAGE TOTAL OUT POUNDS
      *---------------------------------------
      *
     C     POTHD         IFNE      0
     C     POTLB         DIV(H)    POTHD         POTALB
     C                   ENDIF
      *
      *
      *
      *---------------------------------------
      * POUNDS GAINED PER HEAD
      *---------------------------------------
      *
      * FOR NURSERY
      * CALCULATE POUNDS GAINED PER HEAD OUT
      *
     C                   SELECT
     C     WKPPCD        WHENEQ    'NUR  '
     C     POTHD         ANDNE     0
     C     PWGLB         DIV(H)    POTHD         PWGALB
      *
      * FOR GROW FINISH
      * CALCULATE THE POUNDS GAINED PER MARKET HEAD SOLD
      *
     C     WKPPCD        WHENEQ    'GF   '
     C     PMKHD         ANDNE     0
     C     PMWGLB        DIV(H)    PMKHD         PWGALB
     C                   ENDSL
      *
      *
      *---------------------------------------
      * AVERAGE DAILY GAIN =
      *---------------------------------------
      *     WEIGHT GAIN /  PIG DAYS
      *
      * NOTE: FOR GROW FINISH GROUPS, USE THE MARKET SALE
      *       ONLY WEIGHT GAIN
      *
     C                   SELECT
     C     WKPPCD        WHENEQ    'NUR  '
     C     PPDAYS        ANDNE     0
     C     PWGLB         DIV(H)    PPDAYS        PDGALB
      *
     C     WKPPCD        WHENEQ    'GF   '
     C     PPDAYS        ANDNE     0
     C     PMWGLB        DIV(H)    PPDAYS        PDGALB
     C                   ENDSL
      *
      *
      *---------------------------------------
      * ADJUSTED AVERAGE DAILY GAIN
      *---------------------------------------
      *
     C                   SELECT
      *
      * FOR NURSERY GROUPS
      *  (WEIGHT GAINED + DAILY DEAD LBS) / PIG DAYS
      *
     C     WKPPCD        WHENEQ    'NUR  '
     C     PPDAYS        ANDNE     0
     C     PWGLB         ADD       PDLB          WKF152
     C     WKF152        DIV(H)    PPDAYS        PDGJLB
      *
      * FOR GROW FINISH GROUPS =
      *  (MARKET SALES WEIGHT GAINED + DAILY DEAD LBS) /
      *              MARKET SALES PIG DAYS
      *
     C     WKPPCD        WHENEQ    'GF   '
     C     PPDAYS        ANDNE     0
     C     PMWGLB        ADD       PDLB          WKF152
     C     WKF152        DIV(H)    PPDAYS        PDGJLB
     C                   ENDSL
      *
      *
      *---------------------------------------
      * FEED COST PER CWT
      *---------------------------------------
      *  (FEED CONSUMED AMT) / POUNDS
      *
     C                   SELECT
     C     WKPPCD        WHENEQ    'NUR  '
     C     PMVCW         ANDNE     0
     C     PFCHAM        DIV(H)    PMVCW         PFC$CW
      *
     C     WKPPCD        WHENEQ    'GF   '
     C     PMTCW         ANDNE     0
     C     PFCHAM        DIV(H)    PMTCW         PFC$CW
     C                   ENDSL
      *
      *
      *---------------------------------------------
      * FEED CONVERSION = FEED POUNDS / WEIGHT GAIN
      *---------------------------------------------
      *
     C     PWGLB         IFNE      0
     C     PFCLB         DIV(H)    PWGLB         PFDCV
     C                   ENDIF
      *
      *
      *---------------------------------------------
      * ADJUSTED FEED CONVERSION
      *---------------------------------------------
      *    FEED POUNDS / (WEIGHT GAIN + DAILY DEAD POUNDS)
      *
     C     PWGLB         ADD       PDLB          WKF152
     C     WKF152        IFNE      0
     C     PFCLB         DIV(H)    WKF152        PJFDCV
     C                   ENDIF
      *
      *
      *---------------------------------------------
      * AVERAGE FEED COST PER TON
      *---------------------------------------------
      *
     C     PFCLB         DIV(H)    2000          WKF152
     C     WKF152        IFNE      0
     C     PFHMAM        DIV(H)    WKF152        PAFC$T
     C                   ENDIF
      *
      *---------------------------------------------
      * AVERAGE DAYS FED
      *---------------------------------------------
      *
     C                   SELECT
     C     WKPPCD        WHENEQ    'NUR  '
     C     PTIHD         IFNE      0
     C     PPDAYS        DIV(H)    PTIHD         PAFDAY
     C                   ENDIF
      *
     C     WKPPCD        WHENEQ    'GF   '
     C     PMKHD         IFNE      0
     C     PPDAYS        DIV(H)    PMKHD         PAFDAY
     C                   ENDIF
     C                   ENDSL
      *
      *
      *-------------------------------------------------------------
      * FEED COST PER POUND OF GAIN
      *-------------------------------------------------------------
      *  (FEED CONSUMED AMT + FDMFG AMT) / WEIGHT GAINED
      *
      *  NOTE: WEIGHT GAINED FOR GROW FINISH IS ONLY
      *        FOR MARKET SALES
      *
     C                   SELECT
     C     WKPPCD        WHENEQ    'NUR  '
     C     PWGLB         IFNE      0
     C     PFHMAM        DIV(H)    PWGLB         PFC$GN
     C                   ENDIF
      *
     C     WKPPCD        WHENEQ    'GF   '
     C     PMWGLB        IFNE      0
     C     PFHMAM        DIV(H)    PMWGLB        PFC$GN
     C                   ENDIF
     C                   ENDSL
      *
      *
      *---------------------------------------------
      * FEED COST PER HEAD
      *---------------------------------------------
      *  (FEED CONSUMED AMT) / HEAD
      *
     C                   SELECT
     C     WKPPCD        WHENEQ    'NUR  '
     C     PMVHD         ANDNE     0
     C     PFCHAM        DIV(H)    PMVHD         PFC$HD
      *
     C     WKPPCD        WHENEQ    'GF   '
     C     PMTHD         ANDNE     0
     C     PFCHAM        DIV(H)    PMTHD         PFC$HD
     C                   ENDSL
      *
      *---------------------------------------------
      * BACKFAT
      *---------------------------------------------
      *
     C     PBFLB         IFNE      0
     C     PBFQT         DIV(H)    PBFLB         PBFIN
     C                   ENDIF
      *
      *---------------------------------------------
      * LEAN PERCENT
      *---------------------------------------------
      *
     C     PLNLB         IFNE      0
     C     PLNQT         DIV(H)    PLNLB         PLNPR
     C                   ENDIF
      *
      *---------------------------------------------
      * LOIN EYE
      *---------------------------------------------
      *
     C     PLOLB         IFNE      0
     C     PLOQT         DIV(H)    PLOLB         PLOIN
     C                   ENDIF
      *
      *---------------------------------------------
      * CARCASS YIELD
      *---------------------------------------------
      *
     C     PYLVLB        IFNE      0
     C     PYCRLB        DIV       PYLVLB        WKF154
     C     WKF154        MULT      100           PYIELD
     C                   ENDIF
      *
      *
      *---------------------------------------------
      * PHASE COST PER POUND OF GAIN
      *---------------------------------------------
      *  NOTE: FOR GROW FINISH GROUPS, USE WEIGHT GAIN FOR
      *        MARKET SALES ONLY
      *
     C                   SELECT
     C     WKPPCD        WHENEQ    'NUR  '
     C     PWGLB         IFNE      0
     C     PP$AM         DIV(H)    PWGLB         PP$GN
     C                   ENDIF
      *
     C     WKPPCD        WHENEQ    'GF   '
     C     PMWGLB        IFNE      0
     C     PP$AM         DIV(H)    PMWGLB        PP$GN
     C                   ENDIF
     C                   ENDSL
      *
      *---------------------------------------------
      * DEDUCTION/ADD-ON DOLLARS
      *---------------------------------------------
      *
     C                   SELECT
      *
      * FOR NURSERY GROUPS,
      * CALCULATE DOLLARS PER HEAD BASED ON FEEDER PIG SALES
      * CALCULATE DOLLARS PER CWT BASED ON FEEDER PIG SALES
      *
     C     WKPPCD        WHENEQ    'NUR  '
     C     PFPHD         IFNE      0
     C     PDAAM         DIV(H)    PFPHD         PDA$HD
     C                   ENDIF
      *
     C     PFPCW         IFNE      0
     C     PDAAM         DIV(H)    PFPCW         PDA$CW
     C                   ENDIF
      *
      * FOR GROW FINISH GROUPS,
      * CALCULATE DOLLARS PER HEAD BASED ON MARKET SALES
      * CALCULATE DOLLARS PER CWT BASED ON MARKET SALES
      *
     C     WKPPCD        WHENEQ    'GF   '
     C     PMKHD         IFNE      0
     C     PDAAM         DIV(H)    PMKHD         PDA$HD
     C                   ENDIF
      *
     C     PMKCW         IFNE      0
     C     PDAAM         DIV(H)    PMKCW         PDA$CW
     C                   ENDIF
     C                   ENDSL
      *
      *
      *
      *-----------------------
      * TOTAL NET SALES
      *-----------------------
      *
     C                   SELECT
      *
      * FOR NURSERY GROUPS:
      *   TOTAL NET IS THE SUM OF FEEDER PIG SALES AND DEDUCTIONS/ADDONS
      *
     C     WKPPCD        WHENEQ    'NUR  '
     C     PFPAM         ADD       PDAAM         PTNAM
      *
     C     PFPHD         IFNE      0
     C     PTNAM         DIV(H)    PFPHD         PTN$HD
     C                   ENDIF
      *
     C     PFPCW         IFNE      0
     C     PTNAM         DIV(H)    PFPCW         PTN$CW
     C                   ENDIF
      *
      * FOR GROW FINISH GROUPS,
      *   TOTAL NET IS THE SUM OF MARKET SALES, FPS, & DEDUCTIONS/ADDONS
      *
     C     WKPPCD        WHENEQ    'GF   '
     C     PSLAM         ADD       PDAAM         PTNAM
      *
     C     PSLHD         IFNE      0
     C     PTNAM         DIV(H)    PSLHD         PTN$HD
     C                   ENDIF
      *
     C     PSLCW         IFNE      0
     C     PTNAM         DIV(H)    PSLCW         PTN$CW
     C                   ENDIF
     C                   ENDSL
      *
      *
      *
      *-----------------------
      * TOTAL OPERATING COST
      *-----------------------
      *
     C                   SELECT
      *
      * FOR NURSERY GROUPS,
      * CALCULATE DOLLARS PER HEAD BASED ON MOVED HEAD
      * CALCULATE DOLLARS PER CWT BASED ON MOVED CWT
      *
     C     WKPPCD        WHENEQ    'NUR  '
     C     PMVHD         IFNE      0
     C     POPCAM        DIV(H)    PMVHD         POP$HD
     C                   ENDIF
      *
     C     PMVCW         IFNE      0
     C     POPCAM        DIV(H)    PMVCW         POP$CW
     C                   ENDIF
      *
      * FOR GROW FINISH GROUPS,
      * CALCULATE DOLLARS PER HEAD BASED ON MARKET SALES + TRANSFER OUT
      * CALCULATE DOLLARS PER CWT BASED ON MARKET SALES + TRANSFER OUT
      *
     C     WKPPCD        WHENEQ    'GF   '
     C     PMTHD         IFNE      0
     C     POPCAM        DIV(H)    PMTHD         POP$HD
     C                   ENDIF
      *
     C     PMTCW         IFNE      0
     C     POPCAM        DIV(H)    PMTCW         POP$CW
     C                   ENDIF
     C                   ENDSL
      *
      *
      *-----------------------
      * TOTAL OTHER COST
      *-----------------------
      *
     C                   SELECT
      *
      * FOR NURSERY GROUPS,
      * CALCULATE DOLLARS PER HEAD BASED ON MOVED HEAD
      * CALCULATE DOLLARS PER CWT BASED ON MOVED CWT
      *
     C     WKPPCD        WHENEQ    'NUR  '
     C     PMVHD         IFNE      0
     C     POTCAM        DIV(H)    PMVHD         POT$HD
     C                   ENDIF
      *
     C     PMVCW         IFNE      0
     C     POTCAM        DIV(H)    PMVCW         POT$CW
     C                   ENDIF
      *
      * FOR GROW FINISH GROUPS,
      * CALCULATE DOLLARS PER HEAD BASED ON MARKET SALES + TRANSFER OUT
      * CALCULATE DOLLARS PER CWT BASED ON MARKET SALES + TRANSFER OUT
      *
     C     WKPPCD        WHENEQ    'GF   '
     C     PMTHD         IFNE      0
     C     POTCAM        DIV(H)    PMTHD         POT$CW
     C                   ENDIF
      *
     C     PMTCW         IFNE      0
     C     POTCAM        DIV(H)    PMTCW         POT$CW
     C                   ENDIF
     C                   ENDSL
      *
      *--------------------
      * TOTAL COST
      *--------------------
      *
     C                   SELECT
      *
      * FOR NURSERY GROUPS,
      * CALCULATE DOLLARS PER HEAD BASED ON MOVED HEAD
      * CALCULATE DOLLARS PER CWT BASED ON MOVED CWT
      *
     C     WKPPCD        WHENEQ    'NUR  '
     C     PMVHD         IFNE      0
     C     PTCAM         DIV(H)    PMVHD         PTC$HD
     C                   ENDIF
      *
     C     PMVCW         IFNE      0
     C     PTCAM         DIV(H)    PMVCW         PTC$CW
     C                   ENDIF
      *
      * FOR GROW FINISH GROUPS,
      * CALCULATE DOLLARS PER HEAD BASED ON MARKET SALES + TRANSFER OUT
      * CALCULATE DOLLARS PER CWT BASED ON MARKET SALES + TRANSFER OUT
      *
     C     WKPPCD        WHENEQ    'GF   '
     C     PMTHD         IFNE      0
     C     PTCAM         DIV(H)    PMTHD         PTC$HD
     C                   ENDIF
      *
     C     PMTCW         IFNE      0
     C     PTCAM         DIV(H)    PMTCW         PTC$CW
     C                   ENDIF
     C                   ENDSL
      *
      *-----------------------
      * COST OF TRANSFERS OUT
      *-----------------------
      *
     C                   EXSR      $TOCST
      *
      *-------------------------------
      * NET PROFIT BEFORE TAXES MARGIN
      *-------------------------------
      * NET PROFIT =NET SALES - COST OF SALES
      *
     C     PTNAM         SUB       PCSAM         PNPAM
      *
      * IF NET PROFIT IS NOT ZERO, CALCULATE
      *   PROFIT PER HEAD = NET SALES PER HEAD - COST OF SALES PER HEAD
      *   PROFIT PER CWT  = NET SALES PER CWT - COST OF SALES PER CWT
      *
     C     PNPAM         IFNE      0
     C     PTN$HD        SUB       PCS$HD        PNP$HD
     C     PTN$CW        SUB       PCS$CW        PNP$CW
     C                   ENDIF
      *
      *
      * WRITE PHASE TOTAL RECORD
      *
     C                   WRITE     PREC
      *
     C                   ENDSR
      /EJECT
      *-----------------------------------------------------------------
      * COSTS - COST OF TRANSFERS-OUT
      *-----------------------------------------------------------------
      *
     C     $TOCST        BEGSR
      *
      *
      * SUBTRACT TRANSFER OUT COST FROM TOTAL COST TO GET
      * COST OF SALES
      *
     C     PTCAM         SUB       PTOCAM        PCSAM
      *
     C                   SELECT
      *
      * FOR NURSERY GROUPS,
      * CALCULATE COST OF SALES DOLLARS PER HEAD BASED ON SOLD HEAD
      * CALCULATE COST OF SALES DOLLARS PER CWT BASED ON SOLD HEAD
      *
     C     WKPPCD        WHENEQ    'NUR  '
     C     PSLHD         IFNE      0
     C     PCSAM         DIV(H)    PSLHD         PCS$HD
     C                   ENDIF
      *
     C     PSLCW         IFNE      0
     C     PCSAM         DIV(H)    PSLCW         PCS$CW
     C                   ENDIF
      *
      * FOR GROW FINISH GROUPS,
      * CALCULATE COST OF SALES PER HEAD BASED ON MARKET SALES
      * CALCULATE COST OF SALES PER CWT BASED ON MARKET SALES
      *
     C     WKPPCD        WHENEQ    'GF   '
     C     PMKHD         IFNE      0
     C     PCSAM         DIV(H)    PMKHD         PCS$HD
     C                   ENDIF
      *
     C     PMKCW         IFNE      0
     C     PCSAM         DIV(H)    PMKCW         PCS$CW
     C                   ENDIF
     C                   ENDSL
      *
      *
      * FLIP THE SIGN ON THE TRANSFER OUT DOLLARS. THEY
      * ARE PRINTED AS A NEGATIVE.
      *
     C     -1            MULT      PTOCAM        PTOCAM
      *
      * CALCULATE THE TRANSFER OUT COST PER HEAD
      *
     C     PTOHD         IFNE      0
     C     PTOCAM        DIV(H)    PTOHD         PTO$HD
     C                   ENDIF
      *
      * CALCULATE THE TRANSFER OUT COST PER CWT
      *
     C     PTOCW         IFNE      0
     C     PTOCAM        DIV(H)    PTOCW         PTO$CW
     C                   ENDIF
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $GMULT - PROCESS THE GROUP FILE WITH MULTIPLE RECORDS
      *----------------------------------------------------------------
      *
     C     $GMULT        BEGSR
      *
     C                   MOVEL     YES           FIRST
     C                   MOVEL     WKPPCD        WPPPCD
     C     WKPPCD        SETLL     HSL333A
      *
     C     *IN91         DOUEQ     *ON                                          DO MULTIPLE
     C     WKPPCD        READE     HSL333A                                91
     C     *IN91         IFEQ      *OFF                                         IF NOT EOF
      *
     C     FIRST         IFEQ      YES
     C                   MOVEL     WGCACD        SVCACD
     C                   MOVEL     WGRPCD        SVRPCD
     C                   Z-ADD     WGSEQ         SVSEQ
     C                   MOVEL     WGDS          SVDS
     C                   MOVEL     NO            FIRST
     C                   ENDIF
      *
     C     WGCACD        IFNE      SVCACD
     C     WGRPCD        ORNE      SVRPCD
     C                   EXSR      $WR335
     C                   ENDIF
      *
     C                   ADD       WGAM          WPAM
      *
     C                   ENDIF                                                  IF NOT EOF
     C                   ENDDO                                                  DO MULTIPLE
      *
      *
      * WRITE OUT LAST RECORD IF NECESSARY
      *
     C     WPAM          IFNE      0
     C                   EXSR      $WR335
     C                   ENDIF
      *
     C                   ENDSR
      /EJECT
      *--------------------------------------------------------------------
      * $WR335 - WRITE A RECORD TO THE PHASE WORKFILE WITH MULTIPLE RECORDS
      *--------------------------------------------------------------------
      *
     C     $WR335        BEGSR
      *
      * THE PER HEAD AND PER CWT CALCULATIONS USE 'BUTCHER SALES'
      * VALUES FOR THE PREMIUMS/DISCOUNTS. FOR THE OTHER RECORDS,
      * THE CALCULATIONS VARY BY PRODUCTION PHASE.
      * SO, USING THE REPORT CODE, DETERMINE IF IT EXISTS IN THE
      * PREMIUM/DISCOUNT FILE.
      *
     C     SVRPCD        CHAIN     HSL045A                            92
     C     *IN92         IFEQ      *OFF                                         IF PREM/DIS
      *
     C     PBUHD         IFNE      0
     C     WPAM          DIV(H)    PBUHD         WP$HD
     C                   ENDIF
      *
     C     PBUCW         IFNE      0
     C     WPAM          DIV(H)    PBUCW         WP$CW
     C                   ENDIF
     C                   ELSE
      *
      * NOT A PREMIUM/DISCOUNT RECORD
      *
     C                   SELECT
      *
      * FOR NURSERY GROUPS,
      * CALCULATE DOLLARS PER HEAD BASED ON MOVED HEAD
      * CALCULATE DOLLARS PER CWT BASED ON MOVED CWT
      *
     C     WKPPCD        WHENEQ    'NUR  '
     C     PMVHD         IFNE      0
     C     WPAM          DIV(H)    PMVHD         WP$HD
     C                   ENDIF
      *
     C     PMVCW         IFNE      0
     C     WPAM          DIV(H)    PMVCW         WP$CW
     C                   ENDIF
      *
      * FOR GROW FINISH GROUPS,
      * CALCULATE DOLLARS PER HEAD BASED ON MARKET SALES + TRANSFER OUT
      * CALCULATE DOLLARS PER CWT BASED ON MARKET SALES + TRANSFER OUT
      *
     C     WKPPCD        WHENEQ    'GF   '
     C     PMTHD         IFNE      0
     C     WPAM          DIV(H)    PMTHD         WP$HD
     C                   ENDIF
      *
     C     PMTCW         IFNE      0
     C     WPAM          DIV(H)    PMTCW         WP$CW
     C                   ENDIF
     C                   ENDSL
     C                   ENDIF                                                  IF PREM/DIS
      *
     C                   MOVEL     SVCACD        WPCACD
     C                   MOVEL     SVRPCD        WPRPCD
     C                   Z-ADD     SVSEQ         WPSEQ
     C                   MOVEL     SVDS          WPDS
     C                   WRITE     WPREC
      *
     C                   MOVEL     WGCACD        SVCACD
     C                   MOVEL     WGRPCD        SVRPCD
     C                   Z-ADD     WGSEQ         SVSEQ
     C                   MOVEL     WGDS          SVDS
     C                   Z-ADD     0             WPAM
     C                   Z-ADD     0             WP$HD
     C                   Z-ADD     0             WP$CW
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $CLEAR - CLEAR WORKFILE RECORDS AND WORKFIELDS
      *----------------------------------------------------------------
      *
     C     $CLEAR        BEGSR
      *
     C                   CLEAR                   PREC
     C                   CLEAR                   WPREC
      *
     C                   ENDSR
      /EJECT
      *--------------------------------------------------------------
      * *INZSR - INITIALIZATION SUBROUTINE
      *--------------------------------------------------------------
      *
     C     *INZSR        BEGSR
      *
      * KEY FIELDS
      *
      * CONTROL FIELDS
      *
     C                   MOVEL     NO            PROCFL            1
     C                   MOVEL     YES           FIRST             1
      *
     C     *LIKE         DEFINE    GPPCD         WKPPCD
     C     *LIKE         DEFINE    GTOCAM        WKAM
     C     *LIKE         DEFINE    WGRPCD        SVRPCD
     C     *LIKE         DEFINE    WGCACD        SVCACD
     C     *LIKE         DEFINE    WGSEQ         SVSEQ
     C     *LIKE         DEFINE    WGDS          SVDS
      *
      * WORKFIELDS FOR INTERIM CALCULATIONS AND ACCUMULATIONS
      *
     C                   Z-ADD     0             WKF152           15 2
     C                   Z-ADD     0             WKF154           15 4
      *
     C                   ENDSR
      /EJECT
