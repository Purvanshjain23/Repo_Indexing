     A*%%TS  SD  20010611  110451  ISLRAMS     REL-V4R4M0  5769-PW1
     A*****************************************************************
     A*
     A*  SYSTEM:        HOG PRODUCTION
     A*  DISPLAY FILE:  HPD517 - SELECT SUMMARY LEVEL
     A*  RPG PROGRAM:   HP517  - SELECT SUMMARY LEVEL
     A*  CREATED BY:    LEANNE FEDOR
     A*  DATE:          05/14/01
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      PRINT
     A                                      CA12(12 'PREVIOUS')
     A N82                                  ROLLUP(25 'PAGE FORWARD')
     A*---------------------------------------------------------------
     A* REFERENCE WINDOW RECORD FORMAT 1
     A*---------------------------------------------------------------
     A          R HP517R1
     A*%%TS  SD  20010515  110143  ISLRAMS     REL-V4R4M0  5769-PW1
     A                                      TEXT('Reference window')
     A                                      WINDOW(*DFT 10 40)
     A                                      WDWBORDER((*DSPATR RI) (*CHAR '    -
     A                                          '))
     A*---------------------------------------------------------------
     A* RECORD FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP517R2
     A*%%TS  SD  19940315  135528  ISRCENT     REL-V2R2M0  5738-PW1
     A                                      TEXT('Written in RPG pgm to prevent-
     A                                       screen from clearing')
     A                                      CLRL(*NO)
     A                                      OVERLAY
     A*---------------------------------------------------------------
     A* RECORD FORMAT TWO
     A*---------------------------------------------------------------
     A* THIS FORMAT IS NOT WRITTEN IN THE RPG PGM.  IT PREVENTS THE
     A* CALLING_PROGRAM_SCREEN FROM BEING CLEARED.
     A*---------------------------------------------------------------
     A          R HP517R3
     A                                      TEXT('Dummy format not written in R-
     A                                      PG pgm.')
     A                                      ASSUME
     A                                 12  1' '
     A*---------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP517S1                   SFL
     A*%%TS  SD  19960322  152534  ISLRAMS     REL-V3R1M0  5763-PW1
     A                                      TEXT('subfile record format one')
     A            HDRRN1         4S 0H
     A            DFSLCD    R        O  4  1REFFLD(@@FLAG)
     A            DFSLDS        37A  O  4  4
     A*---------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP517C1                   SFLCTL(HP517S1)
     A*%%TS  SD  20010611  110451  ISLRAMS     REL-V4R4M0  5769-PW1
     A                                      SFLSIZ(0005)
     A                                      SFLPAG(0004)
     A                                      WINDOW(HP517R1)
     A                                      TEXT('control format one')
     A                                      OVERLAY
     A                                      SFLCSRRRN(&HDRRN)
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND(*MORE)
     A                                      RMVWDW
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            HDRRN          5S 0H
     A                                  1 10'SELECT SUMMARY LEVEL'
     A                                      DSPATR(HI)
     A                                  2  1'Position Cursor, Press Enter'
     A*---------------------------------------------------------------
     A* COMMAND LINE
     A*---------------------------------------------------------------
     A          R HP517K1
     A                                      WINDOW(HP517R1)
     A                                  9  1'F12=Previous'
