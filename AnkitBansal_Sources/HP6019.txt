      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Hog Production System
      * PROGRAM:     HP6019
      * TITLE:       Missing Sire Line Doses Report
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     05/04/04
      *
      * FUNCTION:  Batch program to report farms missing sire line doses.
      *            This report is submitted from HP4011-Work With Weekly Sire
      *            Line Doses.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 10/16/13  LeAnne Ramsey (E2831)
      *           Recompile only. Added field 'MTech Reference'.
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fhsp018    if   e           k disk
      *   Farm site (open query selects only BGF farms)
      *
      *
     Fhsl081a   if   e           k disk
      *    Weekly sire line doses
      *
      *
     Fhsp3019   if   e           k disk
      *    Workfile: Weeks in date range
      *
      *
     Fqprint    o    f   80        printer oflind(*inof)
      *
      /EJECT
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Standard fields
      *
     D prtfl           s              1    inz('N')
     D dash            s             80
     d rtime           s              6  0
      *
      *
      * Print fields
      *
     D h1fsnm          s                   like(fsfsnm)
      *
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *---------------------------------------------------------------
      * Local data area.
      *---------------------------------------------------------------
      *
     Dlda             uds                  dtaara(*lda)
     D  ldfscd                 2      6  0
     D  ldcell                13     14  0
      *
     D  ldfcdyr               15     18  0
     D  ldfcdwk               19     20  0
     D  ldtcdyr               21     24  0
     D  ldtcdwk               25     26  0
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * MAINLINE
      ****************************************************************
      *
      * Print headings first time thru.
      *
     C                   except    h1hdr
      *
      * Read all farms selected by the open query.
      *
     C     *loval        setll     hsp018
      *
     C                   dou       *in90 = *on                                  Main do
     C                   read      hsp018                                 90
     C                   if        *in90 = *off                                 If not EOF
      *
      * Print a blank line before each new farm.
      *
     C                   except    blank
      *
      * For each farm, read each workfile record and chain to the
      * doses file to see if there is a dose for that week.
      *
     C                   exsr      $doses
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Main do
      *
      *---------------------------------------------------------------
      * EOF processing
      *---------------------------------------------------------------
      * If the file was empty, print the standard 'no data...' message
      *
     C                   if        prtfl = no
     C                   except    nodata
     C                   endif
      *
     C                   seton                                        lr
      /EJECT
      *----------------------------------------------------------------------------------
      * Process each workfile weekly record; check if the farm has doses for that week
      *----------------------------------------------------------------------------------
      *
     C     $doses        begsr
      *
     C     *loval        setll     hsp3019                                91
      *
     C                   dou       *in91 = *on                                  Do weeks
     C                   read      hsp3019                                91
     C                   if        *in91 = *off                                 If not EOF
      *
     C     key01         chain     hsl081a                            92
     C                   if        *in92 = *on                                  If no dose
      *
      * Print line for missing dose week
      *
     C                   if        *inof = *on
     C                   except    h1hdr
     C                   endif
      *
     C                   except    l1dtl
     C                   movel     yes           prtfl
     C                   endif                                                  If no dose
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Do weeks
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Key lists
      *
     C     key01         klist
     C                   kfld                    fsfscd
     C                   kfld                    w1cdyr
     C                   kfld                    w1cdwk
      *
      *
      * Set up a line of underscores and retrieve time for report heading.
      *
     C                   movel     *all'-'       dash
     C                   time                    rtime
      *
      *
      * Setup the user selections in the headings.
      *
      * Cell
     C                   if        ldcell = 0
     C                   seton                                        86
     C                   endif
      *
      * Farm site
     C                   if        ldfscd = 0
     C                   seton                                        89
     C                   else
     C     ldfscd        chain     hsp018                             92
     C                   if        *in92 = *off
     C                   movel(p)  fsfsnm        h1fsnm
     C                   else
     C                   movel(p)  'UNKNOWN'     h1fsnm
     C                   endif
     C                   endif
      *
     C                   endsr
      /EJECT
      *-------------------------------------------------------------
      * Report heading lines
      *-------------------------------------------------------------
      *
     Oqprint    e            h1hdr          1 01
     O                       sdpgm               10
     O                                           52 'HOG PRODUCTION SYSTEM'
     O                                           70 'DATE'
     O                       udate         y     80
      *
     O          e            h1hdr          1
     O                       sdusr               10
     O                                           49 'MISSING SIRE LINE DOSES'
     O                                           56 'REPORT'
     O                                           70 'TIME'
     O                       rtime               80 '  :  :  '
      *
     O          e            h1hdr          2
     O                                           70 'PAGE'
     O                       page          z     80
      *
      *
     O          e            h1hdr          1
     O                                           27 'From calendar year/week ..'
     O                                           28 ':'
     O                       ldfcdyr       z     34
     O                       ldfcdwk       z     38
      *
      *
     O          e            h1hdr          1
     O                                           27 'To calendar year/week ....'
     O                                           28 ':'
     O                       ldtcdyr       z     34
     O                       ldtcdwk       z     38
      *
      *
     O          e            h1hdr          1
     O                                           27 'Cell .....................'
     O                                           28 ':'
     O              N86      ldcell        z     34
     O               86                          34 'All'
      *
     O          e            h1hdr          2
     O                                           27 'Farm site ................'
     O                                           28 ':'
     O              N89      ldfscd        z     34
     O              N89      fsfsnm              61
     O               89                          34 'All'
      *
      *
     O          e            h1hdr          1
     O                                            5 'FARM'
     O                                           24 '-CALENDAR-'
     O                                           38 'WEEK-ENDING'
     O          e            h1hdr          1
     O                                            5 'SITE'
     O                                           12 'CELL'
     O                                           18 'YEAR'
     O                                           24 'WEEK'
     O                                           35 'DATE'
      *
     O          e            h1hdr          1
     O                       dash                80
      *
      *-------------------------------------------------------------
      * Detail line
      *-------------------------------------------------------------
      *
     O          e            l1dtl       1
     O                       fsfscd        z      5
     O                       fscell        z     11
     O                       w1cdyr        z     18
     O                       w1cdwk        z     23
     O                       w1wemdy             37 '  /  /  '
      *
      *-------------------------------------------------------------
      * Blank line
      *-------------------------------------------------------------
      *
     O          e            blank       1
      *
      *-------------------------------------------------------------
      * No data message line
      *-------------------------------------------------------------
      *
     O          e            nodata      1
     O                                           19 'NO DATA MEETS YOUR'
     O                                           38 'SELECTION CRITERIA'
