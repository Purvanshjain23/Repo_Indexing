      *****************  RPG PROGRAM HEADING  ************************
      * SYSTEM:      HOG PRODUCTION
      * PROGRAM:     HP626
      * TITLE:       LISTING OF USERS
      * PROGRAMMER:  LEANNE FEDOR
      * CREATED:     02/13/95
      *
      * FUNCTION: BATCH PROGRAM TO LIST THE RECORDS IN THE
      *           USERS FILE. CALLED FROM HP426-WORK WITH USERS.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 03/27/96  LEANNE FEDOR
      *           RECOMPILE ONLY. A NEW FIELD 'DELETE MOVEMENTS FLAG'
      *           WAS ADDED TO THE AUTHORIZED USERS FILE.
      *
      * 04/30/96  LEANNE FEDOR
      *           RECOMPILE ONLY. A NEW FIELD 'MOVEMENT STATUS'
      *           WAS ADDED TO THE AUTHORIZED USERS FILE TO SUPPORT
      *           THE LOGIC FOR DELETING MOVEMENTS.
      *
      * 05/14/97  LEANNE FEDOR
      *           OUT QUEUE WAS ADDED TO USERS FILE.  ADDED NEW
      *           FIELD TO LISTING.
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     FHSP001    IF   E           K DISK
      *    USERS                     (KEY: AUUSCD)
      *
     FQPRINT    O    F  132        PRINTER OFLIND(*INOF)
      *
      /EJECT
      ****************************************************************
      * INPUT SPECS
      ****************************************************************
      *
      *---------------------------------------------------------------
      * STANDARD PROGRAM STATUS DATA STRUCTURE
      *---------------------------------------------------------------
      *    EXTERNALLY DEFINED AS UTPGFR (RECORD FORMAT: PGMDSR)
     D PGMDS         ESDS                  EXTNAME(UTPGFR)
      *
      *---------------------------------------------------------------
      *  NAMED CONSTANTS
      *---------------------------------------------------------------
      *
     D YES             C                   CONST('Y')
     D NO              C                   CONST('N')
      *
     D                 DS
     D  DUMMYD                 1      1
      *  THE PRECEDING DUMMY DATA STRUCTURE IS ONLY TO KEEP THE I-SPEC
      *  INFO FROM RUNNING OVER INTO THE 'CALC SPECS' ON THE COMPILE
      *  LISTING.  IT SERVES NO OTHER PURPOSE AND CAN BE DELETED
      *  AT ANY TIME!!!
      /EJECT
      ****************************************************************
      * MAIN LINE
      ****************************************************************
      *
     C                   EXCEPT    HRHDR
     C     *LOVAL        SETLL     HSP001
     C                   SETOFF                                       92
      *
     C     *IN92         DOUEQ     *ON                                          MAIN DO
     C                   READ      HSP001                                 92
     C     *IN92         IFEQ      *OFF                                         IF NOT EOF
      *
     C     *INOF         IFEQ      *ON
     C                   EXCEPT    HRHDR
     C                   ENDIF
     C                   EXCEPT    DTLINE
      *
     C                   MOVEL     YES           PRTFLG
      *
     C                   ENDIF                                                  IF NOT EOF
     C                   ENDDO                                                  MAIN DO
      *
      *---------------------------------------------------------------
      *  EOF PROCESSING
      *---------------------------------------------------------------
      *  IF THE FILE WAS EMPTY AND NO RECORDS WERE PRINTED,
      *  PRINT THE STANDARD 'NO DATA...' ERROR MESSAGE
      *
     C     PRTFLG        IFEQ      NO
     C                   EXCEPT    NODATA
     C                   ENDIF
      *
     C                   SETON                                        LR
      /EJECT
      *---------------------------------------------------------------
      * *INZSR - INITIALIZATION SUBROUTINE
      *---------------------------------------------------------------
      *
     C     *INZSR        BEGSR
      *
      * SET PRINT FLAG TO DETERMINE IF ANY RECORDS WERE PRINTED
      * RETRIEVE TIME FOR REPORT HEADING
      *
     C                   MOVEL     NO            PRTFLG            1
     C                   MOVEL     *ALL'-'       DASH            132
     C                   TIME                    RTIME             6 0
      *
     C                   ENDSR
      /EJECT
      *-------------------------------------------------------------
      * REPORT HEADING LINES
      *-------------------------------------------------------------
      *
     OQPRINT    E            HRHDR          1 01
     O                       SDPGM               10
     O                                           76 'HOG PRODUCTION SYSTEM'
     O                                          122 'DATE'
     O                       UDATE         Y    132
      *
     O          E            HRHDR          1
     O                       SDUSR               10
     O                                           73 'LISTING OF USERS'
     O                                          122 'TIME'
     O                       RTIME              132 '  :  :  '
      *
     O          E            HRHDR          2
     O                                          122 'PAGE'
     O                       PAGE          Z    132
      *
     O          E            HRHDR          1
     O                                          132 'REQUIRES'
      *
     O          E            HRHDR          1
     O                                           69 'OUTPUT'
     O                                           79 'REST'
     O                                           87 'MODIFY'
     O                                          107 '-- RESTRICTED ---'
     O                                          117 'SECURITY'
     O                                          132 'FUNCTION'
      *
     O          E            HRHDR          1
     O                                            9 'USER CODE'
     O                                           15 'NAME'
     O                                           68 'QUEUE'
     O                                           79 'USER'
     O                                           86 'DATA'
     O                                          101 'MAINTENANCE'
     O                                          107 'VIEW'
     O                                          117 'LOCATION'
     O                                          132 'AUTHORIZATION'
      *
     O          E            HRHDR          1
     O                       DASH               132
      *
      *-------------------------------------------------------------
      *  DETAIL LINE
      *-------------------------------------------------------------
      *
     O          E            DTLINE      1
     O                       AUUSCD              10
     O                       AUUSNM              61
     O                       AUOUTQ              73
     O                       AURUFL              78
     O                       AUMDFL              85
     O                       AURMFL              96
     O                       AURVFL             104
     O                       AUSLCD             116
     O                       AUAFFL             129
      *
      *-------------------------------------------------------------
      * NO DATA MESSAGE LINE
      *-------------------------------------------------------------
      *
     O          E            NODATA      1
     O                                           19 'NO DATA MEETS YOUR'
     O                                           38 'SELECTION CRITERIA'
