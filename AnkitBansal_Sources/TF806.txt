      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Triumph Foods
      * PROGRAM:     TF806 - Retrieve Invoice Amounts
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     03/14/05
      *
      * Function:    This function retrieves for an Invoice:
      *                 Invoice Amount  (records where Include in Invoice Total = Yes)
      *                 Memo Amount     (records where Include in Invoice Total = No)
      *
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      /eject
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Ftfl021c   if   e           k disk
      *    Invoice charges
      *
      /eject
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      *---------------------------------------------------------------
      * CONSTANTS
      *---------------------------------------------------------------
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      *---------------------------------------------------------------
      * STANDALONE FIELDS
      *---------------------------------------------------------------
      *
      *
      * Parm fields
      *
     D xxinsn          s                   like(icinsn)
     D xxinam          s                   like(icicam)
     D xxmemoam        s                   like(icicam)
      *
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * Mainline
      ****************************************************************
      *
      * Read all Invoice Charge records for this Invoice.
      *
     C     xxinsn        setll     tfl021c
      *
     C                   dou       *in90 = *on                                  Do charges
     C     xxinsn        reade     tfl021c                                90
     C                   if        *in90 = *off                                 If not eof
      *
     C                   select
     C                   when      icinfl = yes
     C                   add       icicam        xxinam
     C                   other
     C                   add       icicam        xxmemoam
     C                   endsl
      *
     C                   endif                                                  If not eof
     C                   enddo                                                  Do charges
      *
     C                   seton                                        lr
      /eject
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
     C     *entry        plist
     C                   parm                    xxinsn
     C                   parm                    xxinam
     C                   parm                    xxmemoam
      *
     C                   endsr
      /eject
