     H/TITLE E1I04131 - Retrieve Next Payment ID for Enterprise 1 copied from X04131
     H* ----------------------------------------------------------------
     H*
     H*    Copyright (c) 1993
     H*    J. D. Edwards & Company
     H*
     H*        This unpublished material is proprietary to
     H*        J. D. Edwards & Company.  All rights reserved.
     H*        The methods and techniques described herein are
     H*        considered trade secrets and/or confidential.
     H*        Reproduction or distribution, in whole or in part,
     H*        is forbidden except by express written permission
     H*        of J. D. Edwards & Company.
     H*
     H* ----------------------------------------------------------------
     F*
     F*    PROGRAM REVISION LOG
     F*    --------------------
     F*
     F*          Date     Programmer     Nature of Revision
     F*        --------   ----------  ------------------------------------
AUTHRF*        04/14/94   WALSH       SAR # 00728659
     F*        08/01/96   KST         SAR # 01294670
     F*        05/06/97   ARNOLD      SAR # 01554136
     F*        08/29/97   KST         SAR # 01723948
     F*        03/31/20   BRAD BADEN  SDN465  TFS# 16169
     F*
     F*****************************************************************
     F*
     F*    GENERAL COMMENTS
     F*    ----------------
     F*
     F*     This program is designed to retrieve the next available
     F*     payment ID number in the A/P Matching Document File
     F*     (F0413). Once this number has been retrieved this
     F*     program will also write a blank record to the F0413
     F*     file for this Payment ID. We must do this so another
     F*     payment run occurring at the same time does not retrieve
     F*     the same payment ID. This program will only write the
     F*     blank record, it is up to the calling program to update
     F*     the record correctly. We suggest that this program only
     F*     be invoked immediatly before a write is performed to
     F*     either the F0413 or the Matching Document Detail file
     F*     (F0414).
     F*
     F*****************************************************************
     F*
     F*  Matching Document File
     F*
     F*F0413     UF A E           K DISK
     FE10413L1  UF A E           K DISK
     C****************************************************************
     C*    MAINLINE PROGRAM
     C*    ----------------
     C*
     C*    Required program parameters.
     C*
     C     *ENTRY        PLIST
     C                   PARM                    PSPYID
     C     *LIKE         DEFINE    RMPYID        PSPYID
     C*
     C*----------------------------------------------------------------
     C*
     C*    Retrieve next available payment ID number
     C*
     C                   SETON                                          99
     C     *IN99         DOWEQ     '1'
     C*    *HIVAL        SETGT     I0413
     C*                  READP     I0413                                9981
     C     *HIVAL        SETGT     I0413L1
     C                   READP     I0413L1                              9981
     C                   ENDDO
     C*
     C*----------------------------------------------------------------
     C*
     C*    If no records exist in the F0413 default the number
     C*    to 1.
     C*
B1   C     *IN81         IFEQ      '1'
     C                   Z-ADD     1             RMPYID
E1   C                   ENDIF
     C*
     C*----------------------------------------------------------------
     C*
     C*    If the payment id is not the highest possible value
     C*    increment and return the number.
     C*
B1   C     *IN81         IFEQ      '0'
     C     RMPYID        ANDNE     *HIVAL
     C*
     C                   ADD       1             RMPYID
     C*
     C*----------------------------------------------------------------
     C*
     C*    If the payment id is the highest possible value then
     C*    find the next available number from the beginning of
     C*    the F0413.
     C*
X1   C                   ELSE
     C                   Z-ADD     0             RMPYID
     C                   SETON                                        81
B2   C     *IN81         DOWEQ     '1'
     C                   ADD       1             RMPYID
     C*    RMPYID        SETLL     I0413                                  81
     C     RMPYID        SETLL     I0413L1                                81
E2   C                   ENDDO
E1   C                   ENDIF
     C*
     C                   Z-ADD     RMPYID        PSPYID
     C*
     C*----------------------------------------------------------------
     C*
     C*    Write a blank payment header record so that a
     C*    coorespoding payment run will not select the same ID.
     C*
     C*                  CLEAR                   I0413
     C                   CLEAR                   I0413L1
     C                   Z-ADD     PSPYID        RMPYID
     C*                  WRITE     I0413                                81
     C                   WRITE     I0413L1                              81
     C*
     C*----------------------------------------------------------------
     C*
     C*                  UNLOCK    F0413
     C                   UNLOCK    E10413L1
     C                   SETON                                        RT
     C                   SETOFF                                       LR
