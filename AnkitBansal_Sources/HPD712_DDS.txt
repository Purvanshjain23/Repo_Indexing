     A*%%TS  SD  20030624  094427  ISLRAMS     REL-V5R2M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD712
     A*  TITLE:         VIEW EXPENSE CODES
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  DATE:          06/23/03
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CF03(03 'EXIT')
     A                                      CF04(04 'PROMPT')
     A                                      CF05(05 'REFRESH')
     A                                      CF12(12 'PREVIOUS')
     A                                      CF20(20 'WRKSPLF')
     A*
     A*
     A*---------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP712S1                   SFL
     A*%%TS  SD  20030623  142121  ISLRAMS     REL-V5R2M0  5722-WDS
     A  85                                  SFLNXTCHG
     A            HDRRN1         4Y 0H
     A            D1EXSEQ   R        O 10  3REFFLD(HSRREF/@@SEQ *LIBL/HSPREF)
     A                                      EDTCDE(L)
     A            D1EXCD    R        O 10 13REFFLD(HSRREF/@@EXCD *LIBL/HSPREF)
     A            D1EXDS    R        O 10 23REFFLD(HSRREF/@@DS20 *LIBL/HSPREF)
     A            D1RPCD    R        O 10 46REFFLD(HSRREF/@@RPCD *LIBL/HSPREF)
     A*
     A*---------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP712C1                   SFLCTL(HP712S1)
     A*%%TS  SD  20030624  094427  ISLRAMS     REL-V5R2M0  5722-WDS
     A                                      SFLSIZ(0050)
     A                                      SFLPAG(0012)
     A N82                                  ROLLUP(25 'PAGE FORWARD')
     A                                      SETOF(51)
     A                                      RTNCSRLOC(&RCD &FLD)
     A                                      OVERLAY
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 28'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  2 30'VIEW EXPENSE CODES'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  4  2'Expense class ..:'
     A            C1ECCD    R        O  4 21REFFLD(HSRREF/@@ECCD *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A            C1ECDS    R        O  4 28REFFLD(HSRREF/ECECDS *LIBL/HSPREF)
     A                                  5  2'Expense subclass:'
     A            C1ESCD    R        O  5 21REFFLD(HSRREF/@@ESCD *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A            C1ESDS    R        O  5 28REFFLD(HSRREF/ESESDS *LIBL/HSPREF)
     A                                  6 48'?'
     A                                      DSPATR(HI)
     A                                  7  2'Exp Code'
     A                                      DSPATR(HI)
     A                                  7 13'Expense'
     A                                      DSPATR(HI)
     A                                  7 23'Expense Code'
     A                                      DSPATR(HI)
     A                                  7 46'Report'
     A                                      DSPATR(HI)
     A                                  8  2'Sequence'
     A                                      DSPATR(HI)
     A                                  8 13'Code'
     A                                      DSPATR(HI)
     A                                  8 23'Description'
     A                                      DSPATR(HI)
     A                                  8 46'Code'
     A                                      DSPATR(HI)
     A            C1EXSEQ   R        B  9  3REFFLD(HSRREF/@@SEQ *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A                                      EDTCDE(M)
     A            C1EXCD    R        B  9 13REFFLD(HSRREF/@@EXCD *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A            C1EXDS    R        B  9 23REFFLD(HSRREF/@@DS20 *LIBL/HSPREF)
     A                                      CHECK(LC)
     A                                      DSPATR(HI)
     A            C1RPCD    R        B  9 46REFFLD(HSRREF/@@RPCD *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A  51                                  DSPATR(PC)
     A*
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R HP712K1
     A*%%TS  SD  20030618  165633  ISLRAMS     REL-V5R2M0  5722-WDS
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A N99                             23 11'F4=Prompt'
     A                                 23 22'F5=Refresh'
     A                                 23 34'F12=Previous'
     A                                 23 48'F20=Spool Files'
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *---------------------------------------------------------------
     A          R HP712ES                   SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE CONTROL FORMAT
      *---------------------------------------------------------------
     A          R HP712EC                   SFLCTL(HP712ES)
      *
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
