/********************************************************************/
/*                                                                  */
/*  SYNOPSIS - PROMPT FOR PRINT OPTIONS & SUBMIT PRINT CL PROGRAM   */
/*             PASSING COMPANY, ACTUAL SHIP DATE RANGE, &           */
/*             WAREHOUSES                                           */
/*                                                                  */
/*  SYSTEM    - ORDER MANAGEMENT SYSTEM                             */
/*  PROGRAM   - PDMFUPC - PROMPT FOR PRINT OPTIONS & SUBMIT REPORT  */
/*  DATE      - 12/17/97                                            */
/*                                                                  */
/*  PARAMETERS -                                                    */
/*                                                                  */
/*      &RETURN    B    Return Status                               */
/*      &APP       I    Application Code                            */
/*      &PGM       I    Program to Submit                           */
/*      &DESC      I    Program Description                         */
/*      &OUTQ      B    Report Output Queue                         */
/*      &HOLD      B    Hold Spool File (Yes/No)                    */
/*      &SAVE      B    Save Spool File (Yes/No)                    */
/*      &NQ_STS    B    Night Queue Status (                        */
/*      &NQ        B    Job Queue for Night Processing              */
/*      &CMPNBR    B    Company Number                              */
/*      &FDATE     B    From Date                                   */
/*      &TDATE     B    To Date                                     */
/*      &COPIES    B    Number of Copies                            */
/*      &WHSE1     B    Selection Warehouse 1                       */
/*      &WHSE2     B    Selection Warehouse 2                       */
/*      &WHSE3     B    Selection Warehouse 3                       */
/*      &WHSE4     B    Selection Warehouse 4                       */
/*      &WHSE5     B    Selection Warehouse 5                       */
/*      &WHSE6     B    Selection Warehouse 6                       */
/*      &WHSE7     B    Selection Warehouse 7                       */
/*      &WHSE8     B    Selection Warehouse 8                       */
/*      &WHSE9     B    Selection Warehouse 9                       */
/*      &WHSE10    B    Selection Warehouse 10                      */
/*      &SOFLAG    B    Warehouse Select/Omit Flag                  */
/*      &EXPORT    B    Export(Y) or Domestic(N) or Both(blank)     */
/*      &SORT      B    Sort by Cntry/State or State only           */
/********************************************************************/
 PDERUPC:    PGM  PARM(&RETURN &APP &PGM &DESC &OUTQ &HOLD +
                       &SAVE &NQ_STS &NQ &CMPNBR &FDATE &TDATE +
                       &COPIES &WHSE1 &WHSE2 &WHSE3 &WHSE4 +
                       &WHSE5 &WHSE6 &WHSE7 &WHSE8 &WHSE9 +
                       &WHSE10 &SOFLAG &EXPORT)

/*************************/
/*** DECLARE VARIABLES ***/
/*************************/

        /*** I/O Parameters ***/
             DCL  VAR(&RETURN)   TYPE(*CHAR) LEN(7)
             DCL  VAR(&APP)      TYPE(*CHAR) LEN(6)
             DCL  VAR(&PGM)      TYPE(*CHAR) LEN(10)
             DCL  VAR(&DESC)     TYPE(*CHAR) LEN(25)
             DCL  VAR(&OUTQ)     TYPE(*CHAR) LEN(10)
             DCL  VAR(&HOLD)     TYPE(*CHAR) LEN(4)
             DCL  VAR(&SAVE)     TYPE(*CHAR) LEN(4)
             DCL  VAR(&NQ_STS)   TYPE(*CHAR) LEN(1)
             DCL  VAR(&NQ)       TYPE(*CHAR) LEN(10)
             DCL  VAR(&CMPNBR)   TYPE(*DEC)  LEN(15 5)
             DCL  VAR(&FDATE)    TYPE(*DEC)  LEN(15 5)
             DCL  VAR(&TDATE)    TYPE(*DEC)  LEN(15 5)
             DCL  VAR(&COPIES)   TYPE(*DEC)  LEN(15 5)
             DCL  VAR(&WHSE1)    TYPE(*CHAR) LEN(3)
             DCL  VAR(&WHSE2)    TYPE(*CHAR) LEN(3)
             DCL  VAR(&WHSE3)    TYPE(*CHAR) LEN(3)
             DCL  VAR(&WHSE4)    TYPE(*CHAR) LEN(3)
             DCL  VAR(&WHSE5)    TYPE(*CHAR) LEN(3)
             DCL  VAR(&WHSE6)    TYPE(*CHAR) LEN(3)
             DCL  VAR(&WHSE7)    TYPE(*CHAR) LEN(3)
             DCL  VAR(&WHSE8)    TYPE(*CHAR) LEN(3)
             DCL  VAR(&WHSE9)    TYPE(*CHAR) LEN(3)
             DCL  VAR(&WHSE10)   TYPE(*CHAR) LEN(3)
             DCL  VAR(&SOFLAG)   TYPE(*CHAR) LEN(1)
             DCL  VAR(&EXPORT)   TYPE(*CHAR) LEN(1)
             DCL  VAR(&SORT)     TYPE(*CHAR) LEN(1)

        /*** Parameters for Print Options Program ***/
             DCL  VAR(&P_COPIES) TYPE(*DEC)  LEN(2 0)
             DCL  VAR(&PAPER)    TYPE(*CHAR) LEN(1)

        /*** Internal Fields for Error Message Handling ***/
             DCL  VAR(&MSG)      TYPE(*CHAR) LEN(512)
             DCL  VAR(&MSGID)    TYPE(*CHAR) LEN(7)
             DCL  VAR(&MSGL)     TYPE(*DEC) LEN(5 0)

        /*** Internal Work Fields ***/
             DCL  VAR(&JOBQ)     TYPE(*CHAR) LEN(10)

/**************************/
/*** Monitor for Errors ***/
/**************************/
             MONMSG MSGID(CPF0000) EXEC(GOTO CMDLBL(ERROR_TRAP))

/****************************************/
/*** Call Print Options Prompt Screen ***/
/****************************************/

        /*** Set Parameters ***/
             CHGVAR VAR(&P_COPIES) VALUE(&COPIES)
             CHGVAR VAR(&PAPER) VALUE('1')

        /*** Call Prompt ***/
             CALL PGM(PDV8PVR) +
                  PARM(&RETURN   +
                       &DESC     +
                       &OUTQ     +
                       &HOLD     +
                       &SAVE     +
                       &P_COPIES +
                       &NQ_STS   +
                       &NQ       +
                       &APP      +
                       &PAPER)
             CHGVAR VAR(&COPIES) VALUE(&P_COPIES)
             IF COND(&RETURN *NE ' ') THEN(DO)
                GOTO CMDLBL(USERQUIT)
             ENDDO

/***************************************/
/*** Submit Print Control CL Program ***/
/***************************************/

        /*** Determine & Set Job Queue ***/
             IF COND(&NQ_STS *EQ 'Y') THEN(DO)
                CHGVAR VAR(&JOBQ) VALUE(&NQ)
             ENDDO
             ELSE CMD(DO)
                CHGVAR VAR(&JOBQ) VALUE('PRIORITY')
             ENDDO

        /*** Submit Report ***/
             SBMJOB     CMD(CALL PGM(&PGM) PARM(&CMPNBR &FDATE +
                          &TDATE &WHSE1 &WHSE2 &WHSE3 &WHSE4 &WHSE5 +
                          &WHSE6 &WHSE7 &WHSE8 &WHSE9 &WHSE10 +
                          &SOFLAG &EXPORT &OUTQ &HOLD &SAVE +
                          &COPIES)) JOB(&PGM) JOBQ(&JOBQ)

        /*** Proceed to Normal Program End ***/
             GOTO CMDLBL(END)

/*****************/
/*** User Quit ***/
/*****************/
 USERQUIT:   CHGVAR VAR(&RETURN) VALUE('Y2U0005')
             GOTO CMDLBL(END)

/******************************/
/*** Error Handling Routine ***/
/******************************/
 ERROR_TRAP:
             RCVMSG MSGTYPE(*EXCP) MSGDTA(&MSG) MSGDTALEN(&MSGL) +
                    MSGID(&MSGID)
             MONMSG MSGID(CPF0000) EXEC(GOTO CMDLBL(END))

             SNDPGMMSG MSGID(&MSGID) MSGF(QCPFMSG) +
                       MSGDTA(%SST(&MSG 1 &MSGL)) MSGTYPE(*ESCAPE)
             MONMSG MSGID(CPF0000) EXEC(GOTO CMDLBL(END))

 END:
             ENDPGM
