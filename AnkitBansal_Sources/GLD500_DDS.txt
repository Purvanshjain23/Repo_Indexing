     A*%%TS  SD  20080721  163213  ISLRAMS     REL-V5R4M0  5722-WDS
     A*****************************************************************
     A*
     A*  SYSTEM:        CORPORATE
     A*  DISPLAY FILE:  GLD500 SELECT TAX COMPANY
     A*  CREATED BY:    LEANNE RAMSEY
     A*  DATE:          07/21/08
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/JDERFMST)
     A                                      PRINT
     A                                      CF12(12 'PREVIOUS')
     A N82                                  ROLLUP(25 'PAGE FORWARD')
     A*---------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*---------------------------------------------------------------
     A          R GL500S1                   SFL
     A*%%TS  SD  20080721  163213  ISLRAMS     REL-V5R4M0  5722-WDS
     A            HDRRN1         5S 0H
     A            D1ENV     R        O  7  2REFFLD(MRCDE1)
     A            D1CONO    R        O  7 15REFFLD(MRCO)
     A*
     A*---------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*---------------------------------------------------------------
     A          R GL500C1                   SFLCTL(GL500S1)
     A*%%TS  SD  20080721  163213  ISLRAMS     REL-V5R4M0  5722-WDS
     A                                      SFLSIZ(0100)
     A                                      SFLPAG(0010)
     A                                      WINDOW(GL500EC)
     A                                      OVERLAY
     A                                      SFLCSRRRN(&HDRRN)
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            HDRRN          5S 0H
     A            SDPGM         10A  O  1  1DSPATR(HI)
     A                                  1 13'SELECT TAX COMPANY'
     A                                      DSPATR(HI)
     A                                  3  1'Position Cursor, Press Enter'
     A                                      DSPATR(HI)
     A                                  5  2'Environment'
     A                                      DSPATR(HI)
     A                                  5 15'Company'
     A                                      DSPATR(HI)
     A            C1ENV     R        B  6  2REFFLD(MRCDE1)
     A                                      DSPATR(HI)
     A*---------------------------------------------------------------
     A* COMMAND LINE LINE
     A*---------------------------------------------------------------
     A          R GL500K1
     A                                      WINDOW(GL500EC)
     A                                      OVERLAY
     A  43                             17  1'Message program failed. Call IS.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 18  1'F12=Previous'
      *
     A*-------------------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
     A*-------------------------------------------------------------------------
     A          R GL500ES                   SFL SFLMSGRCD(19)
      *
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
     A*
     A*-------------------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*-------------------------------------------------------------------------
     A*
     A          R GL500EC                   SFLCTL(GL500ES)
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A                                      WINDOW(*DFT 20 35)
     A                                      WDWBORDER((*DSPATR RI) (*CHAR '    -
     A                                          '))
     A            MSGPGM                    SFLPGMQ(10)
     A*
     A*---------------------------------------------------------------
     A* ASSUME FORMAT -- REQUIRED FOR WINDOWS
     A*---------------------------------------------------------------
     A          R ASSUME
     A                                      ASSUME
     A                                      OVERLAY
     A                                 24 79' '
