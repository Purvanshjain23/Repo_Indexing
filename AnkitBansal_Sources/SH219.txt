      *****************  RPG PROGRAM HEADING  ************************
      *
      * ENVIRONMENT: Pork Division
      * SYSTEM:      Datamart--Sales
      * PROGRAM:     SH219 - Datamart Sales: Accrual Rates
     4* PROGRAMMER:  LeAnne Ramsey
      * CREATED:     01/10/14
      *
      * Per Kevin Smith, we will only build this for Foods....not Dailys.
      * We are going to clear/rebuild this Accrual Rates file nightly.
      *
      * We added this file for Adam Gross. He/Doug are pulling Overages in
      * from the pc side and Doug is joining this new accrual file to
      * the SHP218-Company/Customer file that we created last week in Cognos
      * to give Adam the Absorbed Freight rate. So, ultimately, Adam will
      * have Absorbed Freight, Accrual Rate and Overages in Cognos.
      *
      * I will call an External Function that Rose C. wrote just for this
      * to get the Accrual Rate. This may not match what the users see on
      * screen PMMXDFR-Display Pricing Details since the logic there does
      * an inexplicable movement from a 4 decimal to 11,2 with half adjust and then
      * back to a 4 decimal. Our logic does/will match the actual accrual
      * calc used for orders/invoicing.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * Date      Programmer
      *
      * 03/17/14  LeAnne Ramsey (E3051)
      *           We were originally only processing Trim items..Item
      *           Structure 720. Adam Gross is now having us add
      *           Loin Items (Item Structure 200).
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fcaeorel2  if   e           k disk
      *  Company/Customer (LF selects only Active records)
      *
      *
     Fcabzrel1  if   e           k disk
      *  Company/Item     (OPNQRY selects Active, FG, Item Structure Type = 720 Trim)
      *                                            or Item Structure Type = 200 Loin)
      *
      *
     Fshp219    o    e           k disk
      *  Datamart: Accrual Rates
      *
      /EJECT
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      *---------------------------------------------------------------
      *  Named Constants
      *---------------------------------------------------------------
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      *---------------------------------------------------------------
      * Standalone fields
      *---------------------------------------------------------------
      *
      * Parms
     D xxreturn        s              7a
     D xxcono          s                   like(arcono)
     D xxitcd          s                   like(aritcd)
     D xxshcuno        s              7  0
     D xxaccrt         s                   like(araccrt)
      *
      ****************************************************************
      * Data Structures
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * Mainline
      ****************************************************************
      * To call Rose's external function, I have to pass in Company/Ship
      * to Customer/Item. There is no Synon file that holds all 3 of these
      * fields. So, I will drive/process like the Synon program we
      * are trying to mimic (PMMXDFR-Display Pricing Details).
      *
      * We will drive this by reading the Synon Company/Customer file.
      * (Note: The LF selects only Active records.)
      *
     C     *loval        setll     caeorel2
     C                   dou       *in90 = *on                                  Main do loop
     C                   read      caeorel2                               90
     C                   if        *in90 = *off                                 If not eof
      *
      * For each customer, go process records from the Company/Item.
      *
     C                   exsr      $items
     C                   endif                                                  If not eof
     C                   enddo                                                  Main do loop
      *
     C                   seton                                        lr
      /EJECT
      *-----------------------------------------------------------------------------------------
      * Process some Items for a Company  o
      *-----------------------------------------------------------------------------------------
      * An OPNQRY has selected only the Active, FG Items for Item Structure Type
      * 720-Trim and Item Structure Type 200-Loins from this Company/Item file.
      *
     C     $items        begsr
      *
     C     eoaic3        setll     cabzrel1
     C                   dou       *in91 = *on                                  Do Items loop
     C     eoaic3        reade     cabzrel1                               91
     C                   if        *in91 = *off                                 If not eof
      *
      * Call Rose's program to get the Accrual Rate per LB.
      *
     C                   call      'PBLLXFR'
     C                   parm      *blank        xxreturn
     C                   parm      eoaic3        xxcono
     C                   parm      bzhgcd        xxitcd
     C                   parm      eobkc7        xxshcuno
     C                   parm      0             xxaccrt
      *
      * If accrual rate is not zero for this Company/Customer/Item, then
      * write a Datamart record.
      *
     C                   if        xxaccrt <> 0                                 If rate
     C                   move      'S'           arcorg
     C                   z-add     eoaic3        arcono
     C                   z-add     eobkc7        arshcuno
     C                   z-add     bzhgcd        aritcd
     C                   z-add     xxaccrt       araccrt
     C                   write     arrec
     C                   endif                                                  If rate
      *
     C                   endif                                                  If not eof
     C                   enddo                                                  Do Items loop
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      *
     C                   endsr
      /EJECT
