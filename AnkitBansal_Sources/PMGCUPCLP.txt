/********************************************************************/
/*                                                                  */
/*  SYNOPSIS -  Process for clain reporting of workfile for         */
/*              download data or send the data via email            */
/*              WEEKLY SELECTION                                    */
/*                                                                  */
/*  COMPANY   - Seaboard Corp                                       */
/*  SYSTEM    - A/R Claims                                          */
/*  DATE      - 03/13/09                                            */
/*                                                                  */
/********************************************************************/
/*  MODIFICATIONS:                                                  */
/*  MASON, S 03/13/09 CREATED                                       */
/*                                                                  */
/*    PARM           &RTN    7        RETURN CODE                   */
/*    PARM           &RSNC   3        Reason Code                   */
/*    PARM           &CLMSS  1        Claim Status                  */
/*    PARM           &SLSPR  3        Salesperson to                */
/*    PARM           &ITEM   70       Claim Item Code               */
/*    PARM           &SHPCS  70       Claim Ship to C               */
/*    PARM           &ITNCM  30       Claim Item Comp               */
/*    PARM           &CLSTN  70       Claim Start Num               */
/*    PARM           &CLENN  70       Claim End Numbe               */
/*    PARM           &CLEST  70       Claim Start Ent               */
/*    PARM           &CLEEN  70       Claim End Entry               */
/*    PARM           &CLMST  70       Memo Posting St               */
/*    PARM           &CLMEN  70       Memo Posting En               */
/*    PARM           &RPTTY  1        Report or File                */
/*    PARM           &EMAIL 50        Email Address                 */
/********************************************************************/

             PGM        PARM(&RSNC &CLMSS &SLSPR &ITEM &SHPCS &ITNCM +
                          &CLSTN &CLENN &CLEST &CLEEN &CLMST &CLMEN +
                          &RPTTY &EMAIL)

             DCL        VAR(&RTN)      TYPE(*CHAR) LEN(7)
             DCL        VAR(&RSNC)     TYPE(*CHAR) LEN(3)
             DCL        VAR(&CLMSS)    TYPE(*DEC) LEN(1)
             DCL        VAR(&SLSPR)    TYPE(*DEC) LEN(3)
             DCL        VAR(&ITEM)     TYPE(*DEC) LEN(7 0)
             DCL        VAR(&SHPCS)    TYPE(*DEC) LEN(7 0)
             DCL        VAR(&ITNCM)    TYPE(*DEC) LEN(3 0)
             DCL        VAR(&CLSTN)    TYPE(*DEC) LEN(7 0)
             DCL        VAR(&CLENN)    TYPE(*DEC) LEN(7 0)
             DCL        VAR(&CLEST)    TYPE(*DEC) LEN(7 0)
             DCL        VAR(&CLEEN)    TYPE(*DEC) LEN(7 0)
             DCL        VAR(&CLMST)    TYPE(*DEC) LEN(7 0)
             DCL        VAR(&CLMEN)    TYPE(*DEC) LEN(7 0)
             DCL        VAR(&RPTTY)    TYPE(*CHAR) LEN(1)
             DCL        VAR(&EMAIL)    TYPE(*CHAR) LEN(50)

             /* Internal Fields */
             DCL  VAR(&ALREADY)  TYPE(*CHAR) LEN(1)
             DCL  VAR(&DOCUMENT) TYPE(*CHAR) LEN(10)
             DCL  VAR(&PCFILE)   TYPE(*CHAR) LEN(32)
             DCL  VAR(&PCFMT)    TYPE(*CHAR) LEN(10) VALUE(*XLS)
             DCL  VAR(&PCEXT)    TYPE(*CHAR) LEN(3) VALUE(XLS)
             DCL  VAR(&EMAIL)    TYPE(*CHAR) LEN(50)
             DCL  VAR(&RPTTYP)   TYPE(*CHAR) LEN(1)
             DCL  VAR(&TEXT)     TYPE(*CHAR) LEN(80)


/********************************************************************/
/* CREATE TEMPORARY FILE/LOGICALS IN QTEMP                          */
/********************************************************************/

             DLTF       FILE(QTEMP/PDNACP*)
             MONMSG     MSGID(CPF2105)
             MONMSG     MSGID(CPF2125)

             CRTDUPOBJ  OBJ(PDNACPP) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP) DATA(*NO)
             CRTDUPOBJ  OBJ(PDNACPL0) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP) DATA(*NO)
             CRTDUPOBJ  OBJ(PDNACPL1) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP) DATA(*NO)
             CRTDUPOBJ  OBJ(PDNACPL2) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP) DATA(*NO)

             DLTF       FILE(QTEMP/PDNBCP*)
             MONMSG     MSGID(CPF2105)
             MONMSG     MSGID(CPF2125)

             CRTDUPOBJ  OBJ(PDNBCPP) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP) DATA(*NO)
             CRTDUPOBJ  OBJ(PDNBCPL0) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP) DATA(*NO)
             CRTDUPOBJ  OBJ(PDNBCPL1) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP) DATA(*NO)

             OVRDBF     FILE(PDNACPP) TOFILE(QTEMP/PDNACPP)
             OVRDBF     FILE(PDNACPL2) TOFILE(QTEMP/PDNACPL2)
             OVRDBF     FILE(PDNACPL1) TOFILE(QTEMP/PDNACPL1)
             OVRDBF     FILE(PDNACPL0) TOFILE(QTEMP/PDNACPL0)

             OVRDBF     FILE(PDNBCPP) TOFILE(QTEMP/PDNBCPP)
             OVRDBF     FILE(PDNBCPL1) TOFILE(QTEMP/PDNBCPL1)
             OVRDBF     FILE(PDNBCPL0) TOFILE(QTEMP/PDNBCPL0)

   /*----------------------------*/
   /* Build Work File            */
   /*----------------------------*/
             CALL       PGM(PDUTXFR) PARM(&RTN &RSNC &CLMSS &SLSPR +
                          &ITEM &SHPCS &ITNCM &CLSTN &CLENN &CLEST +
                          &CLEEN &CLMST &CLMEN &RPTTY &EMAIL)

   /*----------------------------*/
   /* Add SEQUEL Library to List */
   /*----------------------------*/
             CHGVAR VAR(&ALREADY) VALUE('N')
             ADDLIBLE LIB(SEQUEL) POSITION(*LAST)
             MONMSG MSGID(CPF2103) EXEC(DO)       /* already in libl */
                CHGVAR VAR(&ALREADY) VALUE('Y')
             ENDDO

             IF         COND(&EMAIL *NE '       ') THEN(GOTO +
                          CMDLBL(EMAIL))
   /*--------------------------------------------*/
   /* Convert "A/R Claim Download Wrkf"          */
   /*--------------------------------------------*/
             EXECUTE  VIEW(PDNACPPWK) +
                     TOFLR(GUY.IS) +
                     TODOC(CLMDTL) +
                     PCFMT(&PCFMT) +
                     REPLACE(*YES)
   /*----------------------------------------------*/
   /* Convert "A/R Claim Reas Dwnld Wrkf"          */
   /*----------------------------------------------*/
             EXECUTE  VIEW(PDNBCPPWK) +
                     TOFLR(GUY.IS) +
                     TODOC(CLMRSN) +
                     PCFMT(&PCFMT) +
                     REPLACE(*YES)

   /*---------------------------------*/
   /* Remove SEQUEL Library from List */
   /*---------------------------------*/
             IF COND(&ALREADY *EQ 'N') THEN(DO)
                RMVLIBLE   LIB(SEQUEL)
             ENDDO

   /*------------------------------------*/
   /* FTP documents to EDIPC/PORK/CLAIMS */
   /*------------------------------------*/
             CHGVAR VAR(&DOCUMENT) VALUE('CLMDTL')
             CHGVAR VAR(&PCFILE)   VALUE('CLMDTL.' *CAT &PCEXT)
             CALL PGM(FTPCLAIMS) PARM(&DOCUMENT &PCFILE)

             CHGVAR VAR(&DOCUMENT) VALUE('CLMRSN')
             CHGVAR VAR(&PCFILE)   VALUE('CLMRSN.' *CAT &PCEXT)
             CALL PGM(FTPCLAIMS) PARM(&DOCUMENT &PCFILE)

             GOTO       CMDLBL(END)

EMAIL:
/*********************************************************************/
/********************  SEND EMAIL QUERY   ****************************/
/*********************************************************************/


             IF         COND(&EMAIL *NE '       ') THEN(DO)

             CHGVAR     VAR(&TEXT) VALUE('CLMDTL WF Weekly A/R Claim data')
             EXECUTE  VIEW(PDNACPPWK) PCFMT(*XLS) REPLACE(*YES) +
                          RECIPIENT(&EMAIL) EMLMSG(&TEXT)

             CHGVAR     VAR(&TEXT) VALUE('CLMRSN WF Weekly A/R +
                          Claim Reason')
             EXECUTE    VIEW(PDNBCPPWK) PCFMT(*XLS) REPLACE(*YES) +
                          RECIPIENT(&EMAIL) EMLMSG(&TEXT)

             ENDDO

END:
             ENDPGM
