     H/TITLE API WIM Inb Man Adjst UP  Execute user program
     H Option(*NoDebugIO:*SrcStmt:*NoUnref) AlwNull(*UsrCtl) dftactgrp(*No)
     H Debug(*Yes)
      *
     H****************** ILE RPG PROGRAM ***********************************************************
     H* SYSTEM:      Seaboard Foods
     H* PROGRAM:     WIMMNLADJ
     H* PROGRAM DSC: API WIM Inb Man Adjst UP  Execute user program
     H* PROGRAMMER:  Danny Nguyen
     H* CREATED:     07/26/2024
     H* PROJECT:     WI568 - Warehouse Inventory Management (WIM)
     H* PURPOSE:     This is a wrapper program to receive the Inbound Manual Adjustment API
     H*              (inbound-manual-adjustment) call which will then call RPG program to update
     H*              the Legacy Inventory files:
     H*                CAB0REP - Item Balance
     H*                CAB1REP - Item Balance Detail
     H*                CAB7CPP - Item Transaction Detail
     H*
     H**********************************************************************************************
     H* MODIFICATIONS:
     H**********************************************************************************************
     H* DATE      INT  PROJECT  DESCRIPTION
     H* --------  ---  -------  --------------------------------------------------------------------
     H*
     H**********************************************************************************************
      *
      *
      ************************************
      * InbMnlAdjDS data structure
      ************************************
     DInbMnlAdjDS      DS                  Qualified Inz
     D                                     Template
     Dcmp_nbr                         3P 0
     Ditem_code                       7P 0
     Dwhs_cde                         3A
     Dtrans_id                        3A
     Dtrans_type                      3A
     Dtrans_date                      7P 0
     Dprod_date                       7P 0
     Dtrans_source                    2A
     Dtrans_desc                     30A
     Drsn_cde                         3A
     Dord_nbr                         7P 0
     Dshft_nbr                        1A
     Dtrans_wgt                      11P 2
     Dtrans_qty                      11P 2
     Dupd_pgm                         3A
     Dera_env_cde                     3A
     Dresp_cde                        7A

      *****************************************************************
      * Interfaces
      *****************************************************************
      *
      * Support Interface for WIM Inbound Manual Adjustment API.
     DWIMMNLADJ        PR                  ExtPgm('WIMMNLADJ')
     D  InbMADS                            likeDS(InbMnlAdjDS)
     D                                     Dim(10) Options(*NOPASS)

     DWIMMNLADJ        PI
     D  InbMADS                            likeDS(InbMnlAdjDS)
     D                                     Dim(10) Options(*NOPASS)

      * Support Interface for PVGQXFR Program Call.
     DPVGQXFR          PR                  ExtPgm('PVGQXFR')
     D  dftretrncd                         likeDS(P1PARM)
     D  invtransDS                         likeDS(P2PARM)

      * QCMDEXC Prototype.
     D Exc_Cmd         PR                  Extpgm('QCMDEXC')
     D  CmdStr                      200A   Options(*VarSize) Const
     D  CmdLen                       15  5 Const

      *
      * Standalone Variables
     D i               S              3P 0 inz
     D CoNum           S              3P 0 inz
     D command         S            512A
     D FirstTimeThru   S              1A   inz('Y')
      *

      * Parameter declarations
     D P1PARM          DS                  Inz
      * O :  *Return code
     D  P1RTN                  1      7
     D P2PARM          DS            88    Inz
      * RCD: PAR API Inb Manual Adj
      * I :  Company Number
     D  P2AIC3                 1      2P 0
      * I :  Item Code
     D  P2HGCD                 3      6P 0
      * I :  Ship From Warehouse
     D  P2VPCD                 7      9
      * I :  WIM Inventory Trans ID
     D  P2NOSC                10     12
      * I :  WIM Inventory Trans Type
     D  P2NPSC                13     15
      * I :  Transaction Date
     D  P2A9DT                16     19P 0
      * I :  Production Date
     D  P2ELDT                20     23P 0
      * I :  Item Transaction Source
     D  P2F1ST                24     25
      * I :  Transaction Description
     D  P2VQNA                26     55
      * I :  Transaction Reason Code
     D  P2A1AB                56     58
      * I :  Order Number
     D  P2C4NB                59     62P 0
      * I :  Shift Number
     D  P2XLCD                63     63
      * I :  Transaction Weight
     D  P2AHWG                64     69P 2
      * I :  Transaction Quantity
     D  P2A5QT                70     75P 2
      * I :  Update Program Code USR
     D  P2FPAB                76     78
      * O :  Response Code USR
     D  P2XTU1                82     88
      *
      *
      *****************************************************************
      // Mainline Processing
      *****************************************************************
      /free

      // Read Array
        For i = 1 to 10;
          If InbMADS(i).trans_id = 'A  ' or          // Adjustment or Receipt
             InbMADS(i).trans_id = 'R  ';

            If FirstTimeThru = 'Y';
              FirstTimeThru = 'N';
              // Call Program to Set Library List.
              command = 'CALL PGM(PVGUUPC) PARM((' +
                         InbMADS(i).era_env_cde + '))';
              Exc_Cmd(%trim(command): %len(%trim(command)));
            EndIf;

             P2AIC3 = InbMADS(i).cmp_nbr;
             P2HGCD = InbMADS(i).item_code;
             P2VPCD = InbMADS(i).whs_cde;
             P2NOSC = InbMADS(i).trans_id;
             P2NPSC = InbMADS(i).trans_type;
             P2A9DT = InbMADS(i).trans_date;
             P2ELDT = InbMADS(i).prod_date;
             P2F1ST = InbMADS(i).trans_source;
             P2VQNA = InbMADS(i).trans_desc;
             P2A1AB = InbMADS(i).rsn_cde;
             P2C4NB = InbMADS(i).ord_nbr;
             P2XLCD = InbMADS(i).shft_nbr;
             P2AHWG = InbMADS(i).trans_wgt;
             P2A5QT = InbMADS(i).trans_qty;
             P2FPAB = InbMADS(i).upd_pgm;
             P2XTU1 = InbMADS(i).resp_cde;

         // Call Program to Update Legacy Inventory Files.
             Callp PVGQXFR(P1PARM: P2PARM);

         // Set Response Code in Array.
             InbMADS(i).resp_cde = P2XTU1;

          Else;
             Leave;
          EndIf;
        EndFor;

        *InLr = '1';
        Return;

