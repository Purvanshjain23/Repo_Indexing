     A*%%TS  SD  20090429  084127  ISLRAMS     REL-V5R4M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD462
     A*  TITLE:         SPECIFY FEED TICKET COSTINGS PRELIMINARY/FINAL
     A*  PROGRAMMER:    ROSE CENTONZE
     A*  DATE:          04/15/94
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A N99                                  CF04(04 'PROMPT')
     A N99 44                               CF08(08 'SUBMIT')
     A N99                                  CF17(17 'PRINT OPTIONS')
     A N99                                  CF20(20 'WRKSPLF')
     A*
     A*--------------------------------------------------------------*
     A* RECORD FORMAT ONE
     A*--------------------------------------------------------------*
     A          R HP462R1
     A*%%TS  SD  20090429  084127  ISLRAMS     REL-V5R4M0  5722-WDS
     A                                      SETOF(31)
     A                                      SETOF(32)
     A                                      SETOF(33)
     A                                      SETOF(51)
     A                                      SETOF(52)
     A                                      OVERLAY
     A                                      RTNCSRLOC(&RCD &FLD)
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 27'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  2 23'SPECIFY FEED TICKETS TO COST'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  4  2'Business office ............'
     A                                  4 31'?'
     A            DFFMBO    R        B  4 33REFFLD(RCFMBO)
     A N31                                  DSPATR(HI)
     A  31
     AO 51                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A            DFBODS    R        O  4 43REFFLD(BOBODS)
     A                                  5  2'Cut-off delivery date ......'
     A            DFMDY     R     Y  B  5 33REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A N32                                  DSPATR(HI)
     A  32                                  DSPATR(PC)
     A  32                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A                                  7  2'Edit or final ..............'
     A                                  7 31'?'
     A            DFEFFL         1A  B  7 33TEXT('edit/post flag')
     A N33                                  DSPATR(HI)
     A  33                                  DSPATR(RI)
     A  33
     AO 52                                  DSPATR(PC)
     A  99                                  DSPATR(PR)
     A            DFEFDS        10A  O  7 43TEXT('edit/post text')
     A  87
     AO 88                             10  2'ALERT! IMPORTANT MESSAGE:'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                      COLOR(RED)
     A*
     A  87                             11  2'HPS Feed Delivery Dollars have alr-
     A                                      eady been uploaded to JDE for Perio-
     A                                      d'
     A                                      DSPATR(HI)
     A  87        DFFDPER        2  0O 11 73DSPATR(HI)
     A  88                             12  2'HPS Feed Pickup Dollars have alrea-
     A                                      dy been uploaded to JDE for Period'
     A                                      DSPATR(HI)
     A  88        DFFPPER        2  0O 12 71DSPATR(HI)
     A*
     A*--------------------------------------------------------------*
     A* COMMAND KEY RECORD 1
     A*--------------------------------------------------------------*
     A          R HP462K1
     A*%%TS  SD  19940419  093319  ISRCENT     REL-V2R2M0  5738-PW1
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A N99                             23 11'F4=Prompt'
     A  44N99                          23 22'F8=Submit'
     A  44N99     DFEFDS        10A  O 23 32TEXT('edit/post text')
     A N99                             23 44'F17=Print Options'
     A N99                             23 63'F20=Spool Files'
      *
      *--------------------------------------------------------------*
      * ERROR MESSAGE SUBFILE FORMAT
      *--------------------------------------------------------------*
     A          R HP462ES                   SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
     A*
     A*--------------------------------------------------------------*
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*--------------------------------------------------------------*
     A*
     A          R HP462EC                   SFLCTL(HP462ES)
     A*%%TS  SD  19940415  103325  ISRCENT     REL-V2R2M0  5738-PW1
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
