     A*%%TS  SD  19950222  145502  ISLRAMS     REL-V3R0M5  5763-PW1
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD134
     A*  TITLE:         MAINTAIN AUTHORIZED USERS FOR A FUNCTION
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  CREATED:       02/14/95
     A*
     A*  INDICATOR 60 IS ON WHEN IN REVISE MODE
     A*  INDICATOR 61 IS ON WHEN IN CREATE MODE
     A*
     A*  INDICATOR 62 IS ON WHEN USER HAS DELETED OR CREATED A RECORD
     A*               IN THE SUBFILE
     A*  INDICATOR 90 IS ON WHEN THE FUNCTION THAT WAS SELECTED FROM
     A*               THE WORK WITH HAS BEEN DELETED BY ANOTHER PERSON
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A  61                                  CF04(04 'PROMPT')
     A N90                                  CF05(05 'REFRESH')
     A  60N90                               CF06(06 'CREATE')
     A                                      CA12(12 'PREVIOUS')
     A*
     A*---------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP134S1                   SFL
     A*%%TS  SD  19950214  083333  ISLRAMS     REL-V3R0M5  5763-PW1
     A                                      SETOF(30)
     A                                      SETOF(31)
     A                                      SETOF(51)
     A                                      SETOF(62)
     A  85                                  SFLNXTCHG
     A            HDRRN1         4Y 0H
     A            DFOPT          2A  B 10  2CHGINPDFT(UL)
     A  61
     AO 62
     AO 90                                  DSPATR(PR)
     A  61
     AO 90                                  DSPATR(ND)
     A  30                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A N61N62N90
     A N30                                  DSPATR(HI)
     A N61N62N90
     A N30 60                               DSPATR(UL)
     A            DFUSCD    R        B 10  7REFFLD(AUUSCD)
     A  60
     AO 62
     AO 90                                  DSPATR(PR)
     A  31                                  DSPATR(RI)
     A  31
     AO 51                                  DSPATR(PC)
     A N60N62N90
     A N31 61                               DSPATR(HI)
     A N60N62N90
     A N31 61                               DSPATR(UL)
     A            DFUSNM    R        O 10 19REFFLD(AUUSNM)
     A            DFACT          8A  O 10 72
     A*
     A*---------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP134C1                   SFLCTL(HP134S1)
     A*%%TS  SD  19950222  145502  ISLRAMS     REL-V3R0M5  5763-PW1
     A                                      SFLSIZ(0100)
     A                                      SFLPAG(0012)
     A N82N90                               ROLLUP(25 'PAGE FORWARD')
     A                                      SFLCSRRRN(&HDRR1)
     A                                      OVERLAY
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A                                      RTNCSRLOC(&RCD &FLD)
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            HDRR1          5S 0H
     A            RCD           10A  H
     A            FLD           10A  H
     A                                  1  2'HP134'
     A                                      DSPATR(HI)
     A                                  1 26'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A            DFMODE    R        O  2  2REFFLD(@@MODE)
     A                                      DSPATR(HI)
     A                                  2 17'MAINTAIN AUTHORIZED USERS FOR A FU-
     A                                      NCTION'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  4  2'Function ..:'
     A            C1FNCD    R        O  4 16REFFLD(FNFNCD)
     A            C1FNDS    R        O  4 28REFFLD(FNFNDS)
     A N61                              6  2'4=Delete'
     A  61                              7 11'?'
     A                                      DSPATR(HI)
     A N61                              8  2'Opt'
     A                                      DSPATR(HI)
     A                                  8  7'User Code'
     A                                      DSPATR(HI)
     A                                  8 19'Name'
     A                                      DSPATR(HI)
     A  60        C1USCD    R        B  9  7REFFLD(AUUSCD)
     A                                      DSPATR(HI)
     A*
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R HP134K1
     A*%%TS  SD  19950213  131521  ISLRAMS     REL-V3R0M5  5763-PW1
     A                                      SETOF(60)
     A                                      SETOF(61)
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A                                      DSPATR(HI)
     A                                 23  2'F3=Exit'
     A  61                             23 11'F4=Prompt'
     A N90                             23 22'F5=Refresh'
     A  60N90                          23 34'F6=Create'
     A                                 23 34'F12=Previous'
     A*
     A*---------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE FORMAT
     A*---------------------------------------------------------------
     A          R HP134ES                   SFL SFLMSGRCD(24)
     A*
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
     A*                                                                *********
     A*---------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*---------------------------------------------------------------
     A          R HP134EC                   SFLCTL(HP134ES)
     A*
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
