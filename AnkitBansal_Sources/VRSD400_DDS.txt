     A*****************************************************************
     A* VRSD400: VOTER REGISTRATION COUNTY SELECTION
     A* CREATED: BARB GUTIERREZ  3/18/96
     A*****************************************************************
     A*
     A                                      DSPSIZ(24 80 *DS3)
     A                                      CHGINPDFT(CS UL)
     A                                      MSGLOC(24)
     A                                      PRINT
     A                                      HELP
     A                                      CF03(03 'EXIT')
     A  19                                  CF21(21 'PRINT LIST')
     A          R VRS400R1
     A                                      OVERLAY
     A                                  1  3'VRS400'
     A                                  1 25'Voter Registration System'
     A                                      DSPATR(HI)
     A            USRNAM        10A  O  1 70
     A                                  2 30'Print Labels'
     A                                      DSPATR(HI)
     A            WRKSTN        10A  O  2 70
     A                                  8 23'County........'
     A            DFCOUN        10A  B  8 39CHECK(FE)
     A                                  9 39'(Leave Blank for All)'
     A                                 11 23'Selection ....'
     A            DFSEL          1A  B 11 39CHECK(FE)
     A  31                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A                                 11 43'(L=Print Labels Only)'
     A                                 12 43'(R=Print Report Only)'
     A                                 13 43'(B=Print Both Labels and Report)'
     A                                 15 23'Mailing Group.'
     A            DFGRP          5   B 15 39CHECK(FE)
     A  32                                  DSPATR(PC)
     A  32                                  DSPATR(RI)
     A                                 15 46'(CPROX=Close Proximity)'
     A                                 16 46'(GEN=General Public)'
     A*--------------------------------------------------------------*
     A*  COMMAND LINE ONE
     A*--------------------------------------------------------------*
     A          R VRS400K1
     A                                      OVERLAY
     A                                 23 16'Enter-Continue'
     A                                      DSPATR(HI)
     A                                 23 36'F3-Exit'
     A                                      DSPATR(HI)
     A  19                             23 49'F21-Print'
     A                                      DSPATR(HI)
      *--------------------------------------------------------------*
      *  MESSAGE SUBFILE RECORD
      *--------------------------------------------------------------*
     A          R VRS400ES                  TEXT('PGMMSGQ SUBFILE RECORD.')
      *                                                                ---------
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            PGMSGQ                    SFLPGMQ
      *
      *--------------------------------------------------------------*
      *  MESSAGE SUBFILE CONTROL RECORD
      *--------------------------------------------------------------*
     A          R VRS400EC                  SFLCTL(VRS400ES)
     A                                      SFLSIZ(0002)
     A                                      SFLPAG(0001)
     A                                      TEXT('PGMMSGQ SUBFILE CONTROL')
     A                                      OVERLAY
     A                                      BLINK
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N89                                  SFLEND
     A            PGMSGQ                    SFLPGMQ
