/***************************************************************** */
/*                                                                 */
/*  PROGRAM NUMBER:  PLAYUPR                                       */
/*  PROGRAM NAME:    Truck Delivery Times Report                   */
/*  PROGRAMMER:      Julius High                                   */
/*  CREATE DATE:     12/15/2015                                    */
/*                                                                 */
/*  FUNCTION:        This CLP  generates the Truck Delivery Times  */
/*                   Report via SEQUEL Viewpoint that is emailed.  */
/*                                                                 */
/*******************************************************************/
/*  Modification History                                           */
/*                                                                 */
/*                                                                 */
/*   Date     Pgmr     Description                                 */
/* -------- ---------- --------------------------------------------*/
/* 11/05/20   ISE      Recompiled due to change in Buy Order Number*/
/***************************************************************** */
             PGM        PARM(&COMPA &PRDDTFMA &PRDDTTOA &EMAILADR)

/*-----------------------------------------------------------------*/
/*                      PARAMETER                                  */
/*-----------------------------------------------------------------*/
/* Function Specific                                               */
             DCL        VAR(&COMPA) TYPE(*CHAR) LEN(3)
             DCL        VAR(&PRDDTFMA) TYPE(*CHAR) LEN(7)
             DCL        VAR(&PRDDTTOA) TYPE(*CHAR) LEN(7)
             DCL        VAR(&EMAILADR) TYPE(*CHAR) LEN(50)

/*-----------------------------------------------------------------*/
/* Program Variables                                               */
             DCL        VAR(&COMPN) TYPE(*DEC) LEN(3 0)
             DCL        VAR(&PRDDTFM) TYPE(*DEC) LEN(7 0)
             DCL        VAR(&PRDDTTO) TYPE(*DEC) LEN(7 0)

             DCL        VAR(&USER) TYPE(*CHAR) LEN(10)
             DCL        VAR(&USRCLS) TYPE(*CHAR) LEN(10)

/* Setup to email the Truck Delivery Times Report                  */
             DCL        VAR(&TEXT) TYPE(*CHAR) LEN(50)
             DCL        VAR(&TEXTDTL) TYPE(*CHAR) LEN(50)

             CHGVAR     VAR(&TEXT) VALUE('Truck Delivery Times')

/* Retrieve Job Attributes                                           */
             RTVJOBA    USER(&USER)
             RTVUSRPRF  USRPRF(&USER) USRCLS(&USRCLS)

/*-----------------------------------------------------------------*/
/* Initialization                                                  */
             CHGVAR     VAR(&COMPN)   VALUE(&COMPA)
             CHGVAR     VAR(&PRDDTFM) VALUE(&PRDDTFMA)
             CHGVAR     VAR(&PRDDTTO) VALUE(&PRDDTTOA)
/*-----------------------------------------------------------------*/
/*                           MAIN                                  */
/*-----------------------------------------------------------------*/
/* Overide user print options                                      */

/*------------------------------------------------------------------*/
/* Create WF Truck Delivery Times   (Work File) in QTEMP            */

             CHKOBJ     OBJ(QTEMP/PLCHCPP) OBJTYPE(*FILE)
             MONMSG     MSGID(CPF9801) EXEC(DO)
/*                            object not found                      */
             CRTDUPOBJ  OBJ(PLCHCPP) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)
             CRTDUPOBJ  OBJ(PLCHCPL0) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)
             CRTDUPOBJ  OBJ(PLCHCPL1) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)

                          ENDDO

/********************************************************************/
/* PLX9XFR - Build WF Truck Delivery Times - PLCHCPP workfile       */
/* in QTEMP which is used to generate the Excel Spreadsheet with    */
/* SEQUEL Viewpoint.                                                */
/*                                                                  */
             CALL       PGM(PLX9XFR) PARM(' ' &COMPN &PRDDTFM +
                           &PRDDTTO)
/*                                                                  */
/*****************************************t. ************************/
/* Create Excel Spreadsheet with SEQUEL Viewpoint and email to      */
/* receipient(s).                                                   */
/* OVRDBF is required to allow EXECUTE VIEW to read file in QTEMP   */
/*                                                                  */
             OVRDBF     FILE(PLCHCPP) TOFILE(QTEMP/PLCHCPP)

             EXECUTE    VIEW(*LIBL/TRKDELV2) PCFMT(*XLS) +
                          RECIPIENT(&EMAILADR) EMLMSG(&TEXT) +
                          TEXT(&TEXT)

/* Clear workfile in QTEMP                                     */
             CLRPFM     FILE(QTEMP/PLCHCPP)

/*-----------------------------------------------------------------*/
/*                      PROGRAM END                                */
/*-----------------------------------------------------------------*/
 ENDPGM:
             DLTOVR     FILE(*ALL)
             ENDPGM
