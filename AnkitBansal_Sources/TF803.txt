      *
      * SYSTEM:      Triumph Foods
      * PROGRAM:     TF803
      * TITLE:       Retrieve Startup Date
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     12/16/04
      *
      * FUNCTION:  This function retrieves the 'startup date' from the SYNON System Values
      *            file. It then converts it into 3 formats to pass out to the calling program.
      *                   1) CCYYMMDD      (8,0)
      *                   2) Synon CYYMMDD (7,0)
      *                   3) JDE Julian    (6,0)
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 11/29/06  LeAnne Ramsey
      *           Replaced the calls to the generic date conversion programs
      *           with ILE date logic.
      *
      * 04/29/21  Rose Centonze SDN440 E1
      *           Call new e1 E10028A instead of x0028.  Pgm hasnt been used since 7/6/2018
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fcadprel1  if   e           k disk
      *    Synon file CADPREP: System values
      *
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      *
      ****************************************************************
      * ARRAYS AND TABLES
      ****************************************************************
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Workfields for date manipulation
      *
     D wkisodate       s               D   datfmt(*iso)
     D wkmdy           s              6  0
      *
      *
      * Parms
      *
     D xxdt            s              8  0
     D xxsyndt         s              7  0
     D xxjdedt         s              6  0
     D xxccyymmdd      s              8  0
      *
      *
      * For call to JDE date routine
      *
     d xxsidat         s              6
     d*xxedat          s              8                                         sdn440
     d xxedat          s              6  0                                      sdn440
     d xxffmt          s              7
     d xxtfmt          s              7
     d xxsep           s              7
     d xxertst         s              1
     d xxctryalph      s              2
      *
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * Mainline
      ****************************************************************
      *
      * First, retrieve the Startup Date record from the Synon System Values
      * file and plop the 15,5 value into the 8,0 parm field to pass back out. The
      * date will be in CCYYMMDD format.
      *
     C     'TFSTARTDT '  chain     cadprel1                           92
     C                   if        *in92 = *off
     C                   z-add     dpm8nb        xxdt
     C                   endif
      *
      * If the retrieved date is a valid date:
      *
     C     *iso          test(d)                 xxdt                   92
     C                   if        *in92 = *off                                 If OK date
      *
      * Flip it to Synon format
      *
     C     *iso          move      xxdt          wkisodate
     C     *cymd         move      wkisodate     xxsyndt
      *
      * Now, convert the retrieved date into the JDE julian format.
      * (You have to send it in as MMDDYY; so, flip it before calling
      * the JDE routine.)
      *
     C*    *mdy          move      wkisodate     wkmdy
     C*                  move      wkmdy         xxsidat
      * sdn440 - comment this call
     C*                  call      'X0028'
     C*                  parm                    xxsidat
     C*                  parm      *blank        xxedat
     C*                  parm      '*SYSVAL'     xxffmt
     C*                  parm      '*JUL   '     xxtfmt
     C*                  parm      '*NONE'       xxsep
     C*                  parm      *blank        xxertst
     C*                  parm      *blank        xxctryalph
      *
      *
      * SDN440  convert to ccyymmdd
     C     *cymd         move      xxsyndt       wkisodate
     c                   move      wkisodate     xxccyymmdd
     C                   call      'E10028A'
     C                   parm                    xxccyymmdd
     C                   parm      0             xxedat
      *
     c                   move      xxedat        xxsidat
     C                   move      xxsidat       xxjdedt
     C                   endif                                                  If OK date
      *
     C                   seton                                        lr
      /EJECT
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Parm lists
      *
     C     *entry        plist
     C                   parm                    xxdt
     C                   parm                    xxsyndt
     C                   parm                    xxjdedt
      *
     C                   endsr
      /EJECT
