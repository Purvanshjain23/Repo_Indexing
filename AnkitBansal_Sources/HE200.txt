      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Hog Procurement and Evaluation
      *              Datamart
      * PROGRAM:     HE200 - Build HPE Datamart Weight Distribution Workfile
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     10/30/00
      *
      * FUNCTION:    We create this workfile so that we can sort/rollup tattoo headers
      *              based on the 'load type'. Load type is in the HPS system...
      *              not the HPE system.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      * 11/03/20  ISE         H16853 - Changed the length of Buy Order Number from 5 to 7
      *                       Program Recompiled.
      *
      /eject
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
      *
     Fpka1cplq  if   e           k disk
      *  HPE Tattoo Header (records selected in open query)
      *
      *
     Fhsp084    if   e           k disk
      *  HPS Sales Movement Header
      *
      *
     Fhep300    o    e           k disk
      *     Datamart HPE: Weight Distribution Workfile
      *
      /eject
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      *
      ****************************************************************
      * ARRAYS AND TABLES
      ****************************************************************
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * MAINLINE
      ****************************************************************
      *
      * Process the HPE tattoo header records selected by the open query
      *
     C                   dou       *in90 = *on                                  Main do
     C                   read      pka1cplq                               90
     C                   if        *in90 = *off                                 If not EOF
      *
      * Don't process a record that has heldover head but zero for a
      * heldover completed date.
      *
     C                   if        a1h9nb <> 0 and a1bsdt = 0                   If heldover
     C                   else
      *
      * Populate workfile fields that are a direct map from the HPE tattoo
      * header.
     C                   exsr      $directmap
      *
      * Retrieve farm site/load type from HPS
      *
     C                   exsr      $ltcd
      *
      * Write workfile record
      *
     C                   write     whrec
     C                   clear                   whrec
      *
     C                   endif                                                  If heldover
     C                   endif                                                  If not EOF
     C                   enddo                                                  Main do
      *
     C                   seton                                        lr
      /eject
      *--------------------------------------------------------------------------
      * Populate workfile fields that are a direct map from the HPE tattoo header
      *--------------------------------------------------------------------------
      *
     C     $directmap    begsr
      *
      * Company number
     C                   z-add     a1hmnb        whhmnb
      * Buy order number
     C                   z-add     a1bnnb        whbnnb
      * Load number
     C                   z-add     a1bonb        whbonb
      * Tattoo number
     C                   z-add     a1cvnb        whcvnb
      * Kill date
     C                   z-add     a1b0dt        whb0dt
      * Head received
     C                   z-add     a1cwnb        whcwnb
      * Head killed
     C                   z-add     a1fenb        whfenb
      * Adj live weight
     C                   z-add     a1lrnb        whlrnb
      * Hot weight
     C                   z-add     a1e0nb        whe0nb
      * Live weight
     C                   z-add     a1cynb        whcynb
      * Avg loin eye depth
     C                   z-add     a1f3nb        whf3nb
      * Avg backfat
     C                   z-add     a1f2nb        whf2nb
      * Avg lean percent
     C                   z-add     a1f4nb        whf4nb
      *
     C                   endsr
      /eject
      *----------------------------------------------------------------
      * Retrieve farm site/load type from HPS using movement number
      *----------------------------------------------------------------
      *
     C     $ltcd         begsr
      *
     C     a1oxnb        chain     hsp084                             92
     C                   if        *in92 = *off                                 If found
     C                   move      shltcd        whltcd
     C                   z-add     shfscd        whfscd
     C                   else
     C                   move      *blank        whltcd
     C                   z-add     a1thhd        whfscd
     C                   endif                                                  If found
      *
     C                   endsr
      /eject
