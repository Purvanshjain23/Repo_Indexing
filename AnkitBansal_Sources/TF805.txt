      *
      * SYSTEM:      Triumph Foods
      * PROGRAM:     TF805
      * TITLE:       Validate GL Code
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     03/02/05
      *
      * FUNCTION:  We have multiples places where we either validate the G/L Code or
      *            retrieve the Description for a code. So, to keep from having several
      *            programs accessing the Synon file, I wrote this program.
      *
      *            So, 'code' and 'company' come in as entry parms.
      *
      *            We pass back out: description, system reference, status
      *            So, a 'blank' description signals to the calling program that
      *            the 'code' is invalid. The calling program issue messages based
      *            on the returned parms.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      * 99/99/99
      *
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fpkcwrel1  if   e           k disk
      *   GL account codes
      *
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      *-----------------------------------------------------------------
      * CONSTANTS
      *-----------------------------------------------------------------
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      *-----------------------------------------------------------------
      * STANDALONE FIELDS
      *-----------------------------------------------------------------
      *
      * Parms
      *
     D xxcono          s              3  0
     D xxglcd          s              8
     D xxglcdds        s             25
     D xxstatus        s              1
     D xxsystem        s              3
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * Mainline
      ****************************************************************
      *
      * Retrieve the record for the incoming Company/GL Code.
      *
     C     key01         chain     pkcwrel1                           92
     C                   if        *in92 = *off                                 If hit
     C                   move      cwggtx        xxglcdds
     C                   move      cwlfst        xxsystem
     C                   move      cwajst        xxstatus
     C                   else
     C                   move      *blank        xxglcdds
     C                   move      *blank        xxsystem
     C                   move      *blank        xxstatus
     C                   endif                                                  If hit
      *
     C                   seton                                        lr
      /EJECT
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Parm lists
      *
     C     *entry        plist
     C                   parm                    xxcono
     C                   parm                    xxglcd
     C                   parm                    xxglcdds
     C                   parm                    xxsystem
     C                   parm                    xxstatus
      *
      * Key lists
      *
     C     key01         klist
     C                   kfld                    xxcono
     C                   kfld                    xxglcd
      *
     C                   endsr
      /EJECT
