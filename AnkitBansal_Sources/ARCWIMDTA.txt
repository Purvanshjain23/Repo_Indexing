     H******************************************************************************
     H* Company: Seaboard Foods Inc.
     H* Author : Ankit Bansal
     H* Desc   : ARCWIMDTA - Archive WIM Staging Tables
     H* Date   : 01/29/2025
     H******************************************************************************

     H******************************************************************************
     H* Control Level specifications
     H******************************************************************************
     H Option(*NoDebugIO)

     F******************************************************************************
     F* File Level specifications
     F******************************************************************************

     D******************************************************************************
     D* Declaration specifications
     D******************************************************************************
     D ArcWIMDta       PR                  ExtPgm('ARCWIMDTA')
     D FromLib                       10A
     D ToLib                         10A
     D NoOfDays                       3P 0
     D Date1                          7P 0 Options(*NoPass)

     D ArcWIMDta       PI
     D FromLib                       10A
     D ToLib                         10A
     D NoOfDays                       3P 0
     D Date1                          7P 0 Options(*NoPass)

     D SqlStmt         S            200A
     D DateField       S               D
     C******************************************************************************
     C* Main Processing Logic
     C******************************************************************************
      /Free

        // Set SQL commit options
        Exec Sql Set Option Commit = *NONE, CloSqlCsr = *ENDMOD;

        If %Parms = 3
          And NoOfDays > 0
          And FromLib <> *Blanks
          And ToLib <> *Blanks;

          //‚WIM_PFS_INV_RESTORATION (PUCYCPP) - Archive and Delete
          SqlStmt = 'Insert Into ' + %Trim(ToLib) + '/PUCYCPP  +
                     Select * from ' + %Trim(FromLib) + '/PUCYCPP +
                     WHERE Date(PFS_TRANSACTION_DATE,''YYYYMMDD'') <= +
                       Current Date - ' + %Char(NoOfDays) + ' Days';

          Exec Sql Execute Immediate :SqlStmt;

          SqlStmt = 'Delete From ' + %Trim(FromLib) + '/PUCYCPP +
                     WHERE Date(PFS_TRANSACTION_DATE,''YYYYMMDD'') <= +
                       Current Date - ' + %Char(NoOfDays) + ' Days';

          Exec Sql Execute Immediate :SqlStmt;

          //‚WIM_PFS_INV_RST_HOLD (PUC0CPP) - Archive and Delete
          SqlStmt = 'Insert Into ' + %Trim(ToLib) + '/PUC0CPP  +
                     Select * from ' + %Trim(FromLib) + '/PUC0CPP +
                     Where PFS_Trans_Date_Timestamp <=    +
                       Current Date - ' + %Char(NoOfDays) + ' Days';

          Exec Sql Execute Immediate :SqlStmt;

          SqlStmt = 'Delete From ' + %Trim(FromLib) + '/PUC0CPP +
                     Where PFS_Trans_Date_Timestamp <=    +
                       Current Date - ' + %Char(NoOfDays) + ' Days';

          Exec Sql Execute Immediate :SqlStmt;

        ElseIf %Parms = 4
          And FromLib <> *Blanks
          And ToLib <> *Blanks;

          Test(DE) *CYMD Date1;
          If Not %Error;
            DateField = %Date(Date1:*CYMD);

            //‚WIM_PFS_INV_RESTORATION (PUCYCPP) - Archive and Delete
            SqlStmt = 'Insert Into ' + %Trim(ToLib) + '/PUCYCPP  +
                       Select * from ' + %Trim(FromLib) + '/PUCYCPP +
                       WHERE Date(PFS_TRANSACTION_DATE,''YYYYMMDD'') <= '''  +
                         %Char(DateField) + '''';

            Exec Sql Execute Immediate :SqlStmt;

            SqlStmt = 'Delete From ' + %Trim(FromLib) + '/PUCYCPP +
                       WHERE Date(PFS_TRANSACTION_DATE,''YYYYMMDD'') <= '''  +
                         %Char(DateField) + '''';

            Exec Sql Execute Immediate :SqlStmt;

            //‚WIM_PFS_INV_RST_HOLD (PUC0CPP) - Archive and Delete
            SqlStmt = 'Insert Into ' + %Trim(ToLib) + '/PUC0CPP  +
                       Select * from ' + %Trim(FromLib) + '/PUC0CPP +
                       Where PFS_Trans_Date_Timestamp <= ''' +
                         %Char(DateField) + '''';

            Exec Sql Execute Immediate :SqlStmt;

            SqlStmt = 'Delete From ' + %Trim(FromLib) + '/PUC0CPP +
                       Where PFS_Trans_Date_Timestamp <= ''' +
                         %Char(DateField) + '''';

            Exec Sql Execute Immediate :SqlStmt;

          EndIf;

        EndIf;

        *InLr = *On;
        Return;
      /End-Free
