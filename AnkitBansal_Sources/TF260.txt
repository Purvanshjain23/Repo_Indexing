      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Triumph Foods
      * PROGRAM:     Revenue: Affiliated Sales Create Log  (aka: Contract Sales)
      * PROGRAMMER:  LeAnne Ramsey
      * CREATED:     07/08/09
      *
      *
      * FUNTION: Determine if this is the "FIRST" time......
      *
      * When Seaboard started up its partnership with a company (not Yarto) in Mexico,
      * they agreed to a Sales Price that was in conflict with the Triumph/Seaboard
      * agreement. The conflict was resolved by adding new OMS Sales Accrual logic
      * just for our Mexico partner.  Apparently, there was no place in the
      * existing OMS Inter-Company Pricing process to add this new logic.
      *
      * So, the FIRST time the users kick off the TF Revenue Close function for
      * a WEEK (regardless of whether they submit a Preliminary or a Final),
      * we will call an OMS Synon program to build the Sales History Detail Accrual
      * data and update the Sales History file.
      *
      * To control this First-Time/1-Time Only logic, we have created this new
      * file (Contract Sales Create Log). We will update it from the TF Revenue Close
      * flow. This file will let us see when the Sales Accrual Data was built...
      * because, inevitably, someone is going to be asking
      * "When was the Sales Accrual data built?"
      *
      * So, again, this program/file really have nothing to do with the TF Revenue
      * Close function. We just needed the Sales History file to be updated with the
      * Affiliated Sales Accrual stuff BEFORE the Revenue Close runs. And, the Synon
      * folks felt putting the logic into the TF410CL as FirstTime/1Time logic
      * was the best approach.
      *
      * There are rumors that we will use this same logic for Dailys.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      /eject
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Ftfp060    uf a e           k disk
      *   Affiliated sales create log
      *
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      ****************************************************************
      * ARRAYS AND TABLES
      ****************************************************************
      *
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Work fields for date manipulation
      *
     D wkisodate       s               d   datfmt(*iso)
      *
      *
      * Parm fields
      *
     d xxfirst         s              1
      *
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *---------------------------------------------------------------
      *  Local data area
      *---------------------------------------------------------------
      *
     d lda            uds                  dtaara(*lda)
     d  ldcedt                22     29  0
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * MAINLINE
      ****************************************************************
      *
      * Is this the "first time" that the user has run the TF Weekly Revenue Close
      * for this WeekEnding Date? (ie: Do we have a record yet for this week in our
      * Create Log file?)
      *
     C     ldcedt        chain     tfp060                             92
     C                   if        *in92 = *on                                  If first time
      *
      * Set flag to control the call to the OMS program from the Revenue Close.
      *
     C                   move      yes           xxfirst
      *
      * Populate fields and write "create log" record
      *
     C                   z-add     ldcedt        aswedt
     C                   move      sdusr         ascrusr
     C                   time                    ascrtm
      *
     C     *mdy          move      udate         wkisodate
     C                   move      wkisodate     ascrdt
      *
     C                   write     asrec
     C                   endif                                                  If first time
      *
     C                   seton                                        lr
      /eject
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
     C     *entry        plist
     C                   parm                    xxfirst
     C                   endsr
      /eject
