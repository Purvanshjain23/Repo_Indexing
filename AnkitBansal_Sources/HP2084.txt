      *****************  RPG PROGRAM HEADING  ************************
     h option(*SRCSTMT:*NODEBUGIO)
      ****************************************************************
      *
      * SYSTEM:      Hog Production System
      * PROGRAM:     HP2084 - Update Sales Movement Header Extension for BOL print
      * PROGRAMMER:  Brad Baden
      * CREATED:     12/11/2017
      *
      * FUNCTION:    This function will update the Sales Movement Extension
      *              file HSP841.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * xx/xx/xx  xxxxxxxxxxx  Exxxxxx
      *           xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
      *           xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
      * 11/04/20  ISE         H16853
      *           Recompile only. Increased Buy Order field length from 5 to 7.
      *
      ****************************************************************
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fhsl0841a  uf a e           k disk
      *  Sales Movement Header Extension
      *
      *
      /EJECT
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      *
      ****************************************************************
      * ARRAYS AND TABLES
      ****************************************************************
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Synon return code
      *
     D returncd        s              7
      *
      * Workfields
      *
     D wkcocd          s                   like(sxcocd)                         HPS Company Code
     D wkschdlvdt      s                   like(sxscdvdt)                       HPS Date 8.0
     D wkdate          s              8  0
     D wktime          s              6  0
      *
      *
      * Parms
      *
     D xxcono          s              3  0                                      HPE Company#
     D xxmvsn          s                   like(sxmvsn)
     D xxscdvdt        s              7  0                                      Synon Date 7.0
     D xxscdvtm        s                   like(sxscdvtm)
     D xxscseqno       s                   like(sxscseqno)
     D xxbyordno       s                   like(sxbyordno)
     D xxboldno        s                   like(sxboldno)
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      ****************************************************************
      * Data areas
      ****************************************************************
      * None
      ****************************************************************
      *
     Dnextmvsn         s              7  0 dtaara(damvsn)
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ***************************************************************************************
      * MAINLINE
      ***************************************************************************************
      *
      * Only process if all of the incoming parameters are not zero.
      *
     C                   if        xxcono     <> *zeros and
     C                             xxscdvdt   <> 0 and
     C                             xxscdvtm   <> 0 and
     C                             xxscseqno  <> 0 and
     C                             xxbyordno  <> 0 and
     C                             xxboldno   <> 0
      *
     C                   exsr      $wrtsmx
     C                   endif
      *
     C                   eval      *inlr = *on
     C                   return
      *
      ***************************************************************************************
      * $WRTSMX - Write Sales Movement Extension record in HSP0841
      ***************************************************************************************
      *
     C     $wrtsmx       begsr
      *
     C     xxmvsn        chain     hsl0841a
      *
      * Load database field values
     C                   eval      sxmvsn    = xxmvsn
     C                   eval      sxscdvdt  = %dec(%char(%date(xxscdvdt:*cymd):
     C                                         *iso0):8:0)
     C                   eval      sxscdvtm  = xxscdvtm
     C                   eval      sxscseqno = xxscseqno
     C                   eval      sxcocd    = %char(xxcono)
     C                   eval      sxbyordno = xxbyordno
     C                   eval      sxboldno  = xxboldno
     C                   eval      sxcupono  = (xxbyordno * 1000) + xxboldno
     C                   eval      sxmbrfl   = *blank
      *
      * Load audit info for update
     C                   if        %found(hsl0841a)
     C                   eval      sxupdt    = %dec(%char(%date(wkdate:*usa):
     C                                         *iso0):8:0)
     C                   eval      sxuptm    = wktime
     C                   eval      sxupusr   = sdusr
      *
     C                   update    sxrec
      *
     C                   else
      *
      * Load audit info for create
     C                   eval      sxcrdt    = %dec(%char(%date(wkdate:*usa):
     C                                         *iso0):8:0)
     C                   eval      sxcrtm    = wktime
     C                   eval      sxcrusr   = sdusr
     C                   eval      sxupdt    = *zeros
     C                   eval      sxuptm    = *zeros
     C                   eval      sxupusr   = *blanks
      *
     C                   write     sxrec
      *
     C                   endif                                                  fi %found
      *
     C                   endsr
      *
      ***************************************************************************************
      * *INZSR - Initialization routine
      ***************************************************************************************
      *
     C     *inzsr        begsr
      *
     C     *entry        plist
     C                   parm                    returncd
     C                   parm                    xxcono
     C                   parm                    xxmvsn
     C                   parm                    xxscdvdt
     C                   parm                    xxscdvtm
     C                   parm                    xxscseqno
     C                   parm                    xxbyordno
     C                   parm                    xxboldno
      *
     C                   time                    timestamp        14 0
     C                   movel     timestamp     wktime
     C                   move      timestamp     wkdate
      *
     C                   endsr
