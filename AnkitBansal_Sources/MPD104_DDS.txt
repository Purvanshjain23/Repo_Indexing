     A*%%TS  SD  20140306  134323  ISLRAMS     REL-V6R1M0  5761-WDS
     A*****************************************************************
     A*
     A* ENVIRONMENT:  PORK DIVISION
     A* SYSTEM:       AS/400
     A* FILE:         MPD104
     A* DESCRIPTION:  MP2: MAINTAIN INVOICE
     A* PROGRAMMER:   LEANNE FEDOR
     A* DATE:         04/01/02
     A*
     A* MODIFIED BY:  BARB GUTIERREZ 03/29/2011
     A*               QTY AND RATE INCREASED.  HAD TO INCREASE SCREEN SIZE FROM
     A*               24 X 80 TO 27 X 132 TO ACCOMODATE LARGER FIELDS.
     A*               E001398.
     A*
     A*  INDICATOR 60 IS ON WHEN IN REVISE MODE
     A*  INDICATOR 61 IS ON WHEN IN CREATE MODE
     A*  INDICATOR 62 IS ON WHEN IN DELETE MODE
     A*  INDICATOR 99 IS ON WHEN ALL ITEMS HAVE ALREADY BEEN INVOICE--PROTECT
     A*                                                               EVERYTHING
     A*****************************************************************
     A* MODIFICATIONS LOG:
     A*----------------------------------------------------------------
     A* 05/26/21 S17068 Danny Nguyen
     A*          Unit Cost Per Item field length changed from 9.4 to 11.4.
     A*          The following field lengths changed from 9.2 to 11.2:
     A*          Invoiced Amount, Amount To Apply, Applied Amount,
     A*          Sales Tax Amount & Shipping/Handling Amt.
     A*          Shifted 'Unit Cost Per Item' to the right 1 position.
     A*          Shifted 'App Amt' to the right 2 positions.
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(27 132 *DS4)
     A                                      REF(*LIBL/MP2REF)
     A                                      CHGINPDFT(FE)
     A                                      PRINT
     A                                      CF03(03 'EXIT')
     A N99                                  CF05(05 'REFRESH')
     A N99                                  CF09(09 'ACCEPT')
     A  60N99                               CF11(11 'DELETE')
     A N99                                  CF13(13 'FOLD/DROP')
     A*
     A*----------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*----------------------------------------------------------------
     A          R MP104S1                   SFL
     A*%%TS  SD  20110330  130215  ISBGUTI     REL-V6R1M0  5761-WDS
     A  85                                  SFLNXTCHG
     A            HDRRN1         4Y 0H
     A            HDRCNO    R        H      REFFLD(@@RCNO)
     A            HDSERNO   R        H      REFFLD(@@SERNO)
     A            HDOTHQT   R        H      REFFLD(@@INQT)
     A            D1APPLY        1A  B 14  2
     A  36                                  DSPATR(PC)
     A  36                                  DSPATR(RI)
     A N36N62                               DSPATR(HI)
     A N36N62                               DSPATR(UL)
     A  62
     AO 99                                  DSPATR(PR)
     A            D1ITEMDS      23A  O 14  4
     A            D1TRQT    R        O 14 28REFFLD(@@TRQT)
     A                                      EDTCDE(M)
     A            D1BALQT   R        O 14 41REFFLD(@@TRQT)
     A                                      EDTCDE(M)
17068A**          D1ITEMRT  R        O 14 54REFFLD(@@ITEMRT)
17068A            D1ITEMRT  R        O 14 53REFFLD(@@ITEMRT)
     A                                      EDTCDE(M)
     A            D1INQT    R        B 14 67REFFLD(@@INQT)
     A                                      EDTCDE(M)
     A  34                                  DSPATR(PC)
     A  34                                  DSPATR(RI)
     A N34N62                               DSPATR(HI)
     A N34N62                               DSPATR(UL)
     A  62
     AO 99                                  DSPATR(PR)
     A            D1OVFL    R        B 14 81REFFLD(@@OVFL)
     A  35                                  DSPATR(PC)
     A  35                                  DSPATR(RI)
     A N35N62                               DSPATR(HI)
     A N35N62                               DSPATR(UL)
     A  62
     AO 99                                  DSPATR(PR)
     A            D1APAM    R        B 14 85REFFLD(@@APAM)
     A                                      EDTCDE(M)
     A  37                                  DSPATR(PC)
     A  37                                  DSPATR(RI)
     A N37N62                               DSPATR(HI)
     A N37N62                               DSPATR(UL)
     A  62
     AO 99                                  DSPATR(PR)
     A                                 15  8'Item:'
     A            D1ITEMNO      16A  O 15 14
     A                                 15 34'Com:'
     A            D1COM     R        B 15 39REFFLD(@@COM)
     A                                      CHECK(LC)
     A  39                                  DSPATR(PC)
     A  39                                  DSPATR(RI)
     A N39N62                               DSPATR(HI)
     A N39N62                               DSPATR(UL)
     A  62
     AO 99                                  DSPATR(PR)
     A*
     A*---------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*---------------------------------------------------------------
     A*
     A          R MP104C1                   SFLCTL(MP104S1)
     A*%%TS  SD  20140306  134323  ISLRAMS     REL-V6R1M0  5761-WDS
     A                                      SFLSIZ(0100)
     A                                      SFLPAG(0004)
     A                                      SFLCSRRRN(&HDRR1)
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  88                                  SFLFOLD(CF13)
     A  89                                  SFLDROP(CF13)
     A                                      SFLMODE(&FOLD)
     A  82                                  SFLEND
     A                                      SETOF(30)
     A                                      SETOF(31)
     A                                      SETOF(32)
     A                                      SETOF(33)
     A                                      SETOF(34)
     A                                      SETOF(35)
     A                                      SETOF(36)
     A                                      SETOF(37)
     A                                      SETOF(38)
     A                                      SETOF(39)
     A                                      OVERLAY
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            FOLD           1A  H
     A            HDRR1          5S 0H
     A            SDPGM         10A  O  1  2DSPATR(HI)
     A                                  1 26'MP2 INTERFACE TO JDE'
     A                                      DSPATR(HI)
     A            SDUSR         10A  O  1 70DSPATR(HI)
     A            C1MODE         6A  O  2  2DSPATR(HI)
     A                                  2 28'MAINTAIN INVOICE'
     A                                      DSPATR(HI)
     A            SDJOB         10A  O  2 70DSPATR(HI)
     A                                  4  2'Vendor number :'
     A            C1JDVNNO  R        O  4 18REFFLD(@@JDVNNO)
     A                                      EDTCDE(M)
     A            C1JDVNNM  R        O  4 28REFFLD(MPREC/@@JDVNNM *LIBL/MP2REF)
     A                                  5  2'Purchase order:'
     A            C1PONO    R        O  5 18REFFLD(@@PONO)
     A                                  5 58'Amount to apply .....:'
     A            C1TAAM    R        B  5 83REFFLD(@@TAAM)
     A                                      EDTCDE(K)
     A  30                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A N30N62                               DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62
     AO 99                                  DSPATR(PR)
     A                                  6  2'Purchasing center/Accounting compa-
     A                                      ny:'
     A            C1PCID    R        O  6 40REFFLD(@@PCID)
     A            C1ACONO   R        O  6 45REFFLD(@@ACONO)
     A                                      EDTCDE(Z)
     A                                  6 58'Sales tax ...........:'
     A            C1STAM    R        B  6 83REFFLD(@@STAM)
     A                                      EDTCDE(K)
     A N62                                  DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62
     AO 99                                  DSPATR(PR)
     A                                  7 58'Shipping/handling ...:'
     A            C1SHAM    R        B  7 83REFFLD(@@SHAM)
     A                                      EDTCDE(K)
     A N62                                  DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  99                                  DSPATR(PR)
     A                                  8  2'Invoice number ..'
     A            C1INNO    R        B  8 20REFFLD(@@INNO)
     A  31                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A N31N62                               DSPATR(HI)
     A  60
     AO 61                                  DSPATR(UL)
     A  62
     AO 99                                  DSPATR(PR)
     A                                  8 58'Total invoice amount :'
     A            C1TOTINAM R        B  8 83REFFLD(@@INAM)
     A                                      EDTCDE(K)
     A  33                                  DSPATR(PC)
     A  33                                  DSPATR(RI)
     A N33N62                               DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62
     AO 99                                  DSPATR(PR)
     A                                  9  2'Invoice date ....'
     A            C1MDY          6Y 0B  9 20EDTWRD('  /  /  ')
     A N32N62                               DSPATR(HI)
     A  32                                  DSPATR(RI)
     A  32                                  DSPATR(PC)
     A N62                                  DSPATR(UL)
     A  62
     AO 99                                  DSPATR(PR)
     A                                  9 58'Invoiced amount .....:'
     A            C1INAM    R        O  9 83REFFLD(@@INAM)
     A                                      EDTCDE(K)
     A                                 10 58'Unapplied amount ....:'
     A            C1UNAM    R        O 10 83REFFLD(@@INAM)
     A                                      EDTCDE(K)
     A  38                                  DSPATR(RI)
     A                                 11  2'Comment..'
     A            C1COM         30A  B 11 12CHECK(LC)
     A N62                                  DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62
     AO 99                                  DSPATR(PR)
     A                                 11 58'Total applied dollars:'
     A            C1APAM    R        O 11 83REFFLD(@@APAM)
     A                                      EDTCDE(K)
     A  37                                  DSPATR(RI)
     A                                 12  2'Apply'
     A                                      DSPATR(HI)
     A                                 13  2'Y/N'
     A                                      DSPATR(HI)
     A                                 13  8'Item Description'
     A                                      DSPATR(HI)
     A                                 13 29'Tran Qty'
     A                                      DSPATR(HI)
     A                                 13 42'Bal Qty'
     A                                      DSPATR(HI)
17068A**                               13 55'Unit Cost'
17068A                                 13 56'Unit Cost'
     A                                      DSPATR(HI)
     A                                 13 69'Inv Qty'
     A                                      DSPATR(HI)
     A                                 13 81'OV'
     A                                      DSPATR(HI)
17068A**                               13 88'App Amt'
     A                                 13 90'App Amt'
     A                                      DSPATR(HI)
     A*
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R MP104K1
     A*%%TS  SD  20020422  105518  ISLRAMS     REL-V5R1M0  5722-WDS
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A                                      DSPATR(HI)
     A                                 23  2'F3=Exit'
     A N99                             23 11'F5=Refresh'
     A N99                             23 23'F9=Accept'
     A  60N99                          23 34'F11=Delete'
     A N99                             23 46'F13=Fold/Unfold Comment'
     A*
     A*---------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE FORMAT
     A*---------------------------------------------------------------
     A          R MP104ES                   SFL SFLMSGRCD(24)
     A*
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
     A*
     A*---------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*---------------------------------------------------------------
     A          R MP104EC                   SFLCTL(MP104ES)
     A*
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
