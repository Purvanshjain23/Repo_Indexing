     A*%%TS  SD  20250731  094420  ISJMIST     REL-V7R5M0  5770-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD414
     A*  TITLE:         WORK WITH TRUCKERS
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  DATE:          01/06/03
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A                                      CF04(04 'PROMPT')
     A                                      CF05(05 'REFRESH')
     A                                      CF06(06 'CREATE')
     A                                      CF07(07 'LISTING')
     A                                      CF17(17 'PRINT OPTIONS')
     A                                      CF20(20 'WRKSPLF')
     A*
     A*
     A*---------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP40262S1                 SFL
     A*%%TS  SD  20250722  072258  ISJMIST     REL-V7R5M0  5770-WDS
     A  85                                  SFLNXTCHG
     A            HDRRN1         4Y 0H
     A            D1OPT          2A  B  9  2CHGINPDFT(UL)
     A  30                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A N30                                  DSPATR(HI)
     A            D1RGCD    R        O  9  8REFFLD(HSRREF/@@RGCD *LIBL/HSPREF)
     A            D1RGDS    R        O  9 17REFFLD(HSRREF/RGRGDS *LIBL/HSPREF)
     A*
     A*---------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP40262C1                 SFLCTL(HP40262S1)
     A*%%TS  SD  20250722  084437  ISJMIST     REL-V7R5M0  5770-WDS
     A N82                                  ROLLUP(25 'PAGE FORWARD')
     A                                      SETOF(30)
     A                                      SETOF(51)
     A                                      SETOF(52)
     A                                      RTNCSRLOC(&RCD &FLD)
     A                                      OVERLAY
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A                                      SFLSIZ(0050)
     A                                      SFLPAG(0012)
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 27'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  2 28'WORK WITH REGIONS'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  4  2'2=Revise'
     A                                  6  7'Region'
     A                                      DSPATR(HI)
     A                                  7  2'Opt'
     A                                      DSPATR(HI)
     A                                  7  8'Code'
     A                                      DSPATR(HI)
     A                                  7 17'Description'
     A                                      DSPATR(HI)
     A            C1RGCD    R        B  8  8REFFLD(HSRREF/@@RGCD *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A            C1RGDS    R        B  8 17REFFLD(HSRREF/RGRGDS *LIBL/HSPREF)
     A                                      CHECK(LC)
     A                                      DSPATR(HI)
     A*
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R HP40262K1
     A*%%TS  SD  20250731  094420  ISJMIST     REL-V7R5M0  5770-WDS
     A  43                             21  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 22  2'F3=Exit'
     A                                 22 11'F5=Refresh'
     A                                 22 23'F6=Create'
     A                                 22 34'F7=Listing'
     A                                 22 46'F17=Print Options'
     A                                 23 46'F20=Spool Files'
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *---------------------------------------------------------------
     A          R HP40262ES                 SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
     A*
     A*---------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*---------------------------------------------------------------
     A*
     A          R HP40262EC                 SFLCTL(HP40262ES)
     A*%%TS  SD  20250722  072258  ISJMIST     REL-V7R5M0  5770-WDS
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ(10)
