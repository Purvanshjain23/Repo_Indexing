/*********************************************************************/
/*  PROGRAM NUMBER:  PVCLUPC                                         */
/*  PROGRAM NAME:    SEND PFS MANIFEST HEADER/DETAIL DOWNLOAD FILES  */
/*  PROGRAMMER:      DANNY NGUYEN                                    */
/*  PROJECT:         WI616 - CARLISLE PALLET ID SCAN INTO INVENTORY  */
/*  CREATE DATE:     03/27/24                                        */
/*                                                                   */
/*  FUNCTION:        THIS CLP CREATES A FILE IN QTEMP FOR SENDING    */
/*                   TO THE PLANT FLOOR SYSTEM SERVER                */
/*                                                                   */
/*********************************************************************/
/* SAMPLE CALL STATEMENT                                             */
/* ------------------------------------------------------------------*/
/* CALL PGM(PVCLUPC) PARM(&COA &LIB &PGM &UPDDELPGM &ENV)            */
/*                                                                   */
/* CALL PGM(PVCLUPC) PARM(360 PRKTESTF PVCHXFR PVCIXFR '- TEST'      */
/*                                                                   */
/* ------------------------------------------------------------------*/
/*                              FILE    UPDIDX   PGM     UPDDELPGM   */
/*                              ------- -------- ------- ---------   */
/* PFS MANIFEST HEADER          PPBRCPP          PVCHXFR PVCIXFR     */
/* PFS MANIFEST DETAIL          PPBNCPP          PVCHXFR PVCIXFR     */
/*                                                                   */
/*********************************************************************/
/* MAINTENANCE LOG:                                                  */
/*-------------------------------------------------------------------*/
/* DATE     BY  PROJ      CHANGE DESCRIPTION                         */
/* -------- --- --------- ------------------------------------------ */
/* 3/27/24  DN  WI616     ALLOW TO CREATE THE PFS MANIFEST HEADER    */
/*                        (PPBRCPP*) & PFS MANIFEST DETAIL (PPBNCPP*)*/
/*                        IN THE SUFFIX TABLES.                      */
/*                                                                   */
/*********************************************************************/

             PGM        PARM(&COA &LIB &PGM &UPDDELPGM &ENV)

/*-----------------------------------------------------------------*/
/* PARAMETER                                                       */
/*-----------------------------------------------------------------*/
             DCL        VAR(&COA) TYPE(*CHAR) LEN(3)
             DCL        VAR(&LIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&PGM) TYPE(*CHAR) LEN(10)
             DCL        VAR(&UPDDELPGM) TYPE(*CHAR) LEN(10)
             DCL        VAR(&ENV) TYPE(*CHAR) LEN(6)

/*-----------------------------------------------------------------*/
/* PROGRAM VARIABLES                                               */
             DCL        VAR(&CO) TYPE(*DEC) LEN(3 0)
/*-----------------------------------------------------------------*/
             DCL        VAR(&NBRCURRCD) TYPE(*DEC) LEN(10 0) VALUE(0)
             DCL        VAR(&FILE) TYPE(*CHAR) LEN(10)
             DCL        VAR(&HDRFILE) TYPE(*CHAR) LEN(10) +
                          VALUE('PPBRCPP')
             DCL        VAR(&DTLFILE) TYPE(*CHAR) LEN(10) +
                          VALUE('PPBNCPP')
             DCL        VAR(&DLSFXFIL) TYPE(*CHAR) LEN(10)
             DCL        VAR(&SENTFILE) TYPE(*CHAR) LEN(10)
             DCL        VAR(&ENDPGM) TYPE(*CHAR) LEN(1)
         /* HDR & DTL SUFFIXED FILES */
             DCL        VAR(&HDRSFX360) TYPE(*CHAR) LEN(10) +
                          VALUE('PPBRCPPGUY')
             DCL        VAR(&HDRSFX440) TYPE(*CHAR) LEN(10) +
                          VALUE('PPBRCPPSTF')
             DCL        VAR(&HDRSFX960) TYPE(*CHAR) LEN(10) +
                          VALUE('PPBRCPPSTJ')
             DCL        VAR(&DTLSFX360) TYPE(*CHAR) LEN(10) +
                          VALUE('PPBNCPPGUY')
             DCL        VAR(&DTLSFX440) TYPE(*CHAR) LEN(10) +
                          VALUE('PPBNCPPSTF')
             DCL        VAR(&DTLSFX960) TYPE(*CHAR) LEN(10) +
                          VALUE('PPBNCPPSTJ')
         /* HDR & DTL SENT FILES */
             DCL        VAR(&SNTFILHDR) TYPE(*CHAR) LEN(10) +
                          VALUE('PPBRCPPSNT')
             DCL        VAR(&SNTFILDTL) TYPE(*CHAR) LEN(10) +
                          VALUE('PPBNCPPSNT')
         /* FLAG TO SEE IF SUFFIX FILE WAS UPDATED */
             DCL        VAR(&UPDSFX360) TYPE(*CHAR) LEN(1) VALUE('N')
             DCL        VAR(&UPDSFX440) TYPE(*CHAR) LEN(1) VALUE('N')
             DCL        VAR(&UPDSFX960) TYPE(*CHAR) LEN(1) VALUE('N')

/*-----------------------------------------------------------------*/
/* INITIALIZE                                                      */
/*-----------------------------------------------------------------*/
             CHGVAR     VAR(&CO) VALUE(&COA)

/*-----------------------------------------------------------------*/
/* QTEMP SETUP FOR SUFFIXED HDR/DTL TABLES.                        */
/*-----------------------------------------------------------------*/
/* SUFFIXED FILES */
    /* PPBRCPP - PFS MANIFEST HEADER */
       /* GUYMON */
             CHKOBJ     OBJ(QTEMP/PPBRCPPGUY) OBJTYPE(*FILE)
             MONMSG     MSGID(CPF9801) EXEC(DO) /* OBJECT NOT FOUND */
             CRTDUPOBJ  OBJ(PPBRCPPGUY) FROMLIB(&LIB) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)
             ENDDO
             CLRPFM     FILE(QTEMP/PPBRCPPGUY)

       /* SIOUX CITY */
             CHKOBJ     OBJ(QTEMP/PPBRCPPSTF) OBJTYPE(*FILE)
             MONMSG     MSGID(CPF9801) EXEC(DO) /* OBJECT NOT FOUND */
             CRTDUPOBJ  OBJ(PPBRCPPSTF) FROMLIB(&LIB) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)
             ENDDO
             CLRPFM     FILE(QTEMP/PPBRCPPSTF)

       /* ST. JOSEPH */
             CHKOBJ     OBJ(QTEMP/PPBRCPPSTJ) OBJTYPE(*FILE)
             MONMSG     MSGID(CPF9801) EXEC(DO) /* OBJECT NOT FOUND */
             CRTDUPOBJ  OBJ(PPBRCPPSTJ) FROMLIB(&LIB) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)
             ENDDO
             CLRPFM     FILE(QTEMP/PPBRCPPSTJ)

       /* SENT FILE */
             CHKOBJ     OBJ(QTEMP/PPBRCPPSNT) OBJTYPE(*FILE)
             MONMSG     MSGID(CPF9801) EXEC(DO) /* OBJECT NOT FOUND */
             CRTDUPOBJ  OBJ(PPBRCPPSNT) FROMLIB(&LIB) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)
             ENDDO
             CLRPFM     FILE(QTEMP/PPBRCPPSNT)

    /* PPBNCPP - PFS MANIFEST DETAIL */
       /* GUYMON */
             CHKOBJ     OBJ(QTEMP/PPBNCPPGUY) OBJTYPE(*FILE)
             MONMSG     MSGID(CPF9801) EXEC(DO) /* OBJECT NOT FOUND */
             CRTDUPOBJ  OBJ(PPBNCPPGUY) FROMLIB(&LIB) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)
             ENDDO
             CLRPFM     FILE(QTEMP/PPBNCPPGUY)

       /* SIOUX CITY */
             CHKOBJ     OBJ(QTEMP/PPBNCPPSTF) OBJTYPE(*FILE)
             MONMSG     MSGID(CPF9801) EXEC(DO) /* OBJECT NOT FOUND */
             CRTDUPOBJ  OBJ(PPBNCPPSTF) FROMLIB(&LIB) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)
             ENDDO
             CLRPFM     FILE(QTEMP/PPBNCPPSTF)

       /* ST. JOSEPH */
             CHKOBJ     OBJ(QTEMP/PPBNCPPSTJ) OBJTYPE(*FILE)
             MONMSG     MSGID(CPF9801) EXEC(DO) /* OBJECT NOT FOUND */
             CRTDUPOBJ  OBJ(PPBNCPPSTJ) FROMLIB(&LIB) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)
             ENDDO
             CLRPFM     FILE(QTEMP/PPBNCPPSTJ)

       /* SENT FILE */
             CHKOBJ     OBJ(QTEMP/PPBNCPPSNT) OBJTYPE(*FILE)
             MONMSG     MSGID(CPF9801) EXEC(DO) /* OBJECT NOT FOUND */
             CRTDUPOBJ  OBJ(PPBNCPPSNT) FROMLIB(&LIB) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)
             ENDDO
             CLRPFM     FILE(QTEMP/PPBNCPPSNT)

/*-----------------------------------------------------------------*/
/* CREATE RECORDS IN QTEMP FOR SUFFIXED FILES.                     */
/*-----------------------------------------------------------------*/

             IF         COND(&CO *EQ 0) THEN(DO)
               CALL       PGM(&PGM) PARM(' ')
               MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(PGMCALL))
             ENDDO
             ELSE DO
               CALL       PGM(&PGM) PARM((' ') (&COA) (&UPDSFX360) +
                            (&UPDSFX440) (&UPDSFX960))
               MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(PGMCALL))
             ENDDO

/*-----------------------------------------------------------------*/
/* PRE-POST PROCESS:                                               */
/*-----------------------------------------------------------------*/
    /* VALIDATE THERE ARE RECORDS TO PROCESS ELSE END PROGRAM */
             IF         COND((&UPDSFX360 *EQ 'N') *AND (&UPDSFX440 +
                          *EQ 'N') *AND (&UPDSFX960 *EQ 'N')) THEN(DO)
             /* NO DATA TO PROCESS */
             GOTO       CMDLBL(ENDPGM)
             ENDDO

/*-----------------------------------------------------------------*/
/* POST PROCESS:                                                   */
/*-----------------------------------------------------------------*/
    /* GUYMON */
             IF         COND(&UPDSFX360 *EQ 'Y') THEN(DO)
             CHGVAR     VAR(&FILE) VALUE(&HDRFILE)
             CHGVAR     VAR(&DLSFXFIL) VALUE(&HDRSFX360)
             CHGVAR     VAR(&SENTFILE) VALUE(&SNTFILHDR)
             CALLSUBR   SUBR(CPYFRQTEMP) /* FOR HEADER */
               IF         COND(&ENDPGM *EQ 'Y') THEN(DO)
               GOTO       CMDLBL(ENDPGM)
               ENDDO

             CHGVAR     VAR(&FILE) VALUE(&DTLFILE)
             CHGVAR     VAR(&DLSFXFIL) VALUE(&DTLSFX360)
             CHGVAR     VAR(&SENTFILE) VALUE(&SNTFILDTL)
             CALLSUBR   SUBR(CPYFRQTEMP) /* FOR DETAIL */
               IF         COND(&ENDPGM *EQ 'Y') THEN(DO)
               GOTO       CMDLBL(ENDPGM)
               ENDDO
             ENDDO

    /* SIOUX CITY */
             IF         COND(&UPDSFX440 *EQ 'Y') THEN(DO)
             CHGVAR     VAR(&FILE) VALUE(&HDRFILE)
             CHGVAR     VAR(&DLSFXFIL) VALUE(&HDRSFX440)
             CHGVAR     VAR(&SENTFILE) VALUE(&SNTFILHDR)
             CALLSUBR   SUBR(CPYFRQTEMP) /* FOR HEADER */
               IF         COND(&ENDPGM *EQ 'Y') THEN(DO)
               GOTO       CMDLBL(ENDPGM)
               ENDDO

             CHGVAR     VAR(&FILE) VALUE(&DTLFILE)
             CHGVAR     VAR(&DLSFXFIL) VALUE(&DTLSFX440)
             CHGVAR     VAR(&SENTFILE) VALUE(&SNTFILDTL)
             CALLSUBR   SUBR(CPYFRQTEMP) /* FOR DETAIL */
               IF         COND(&ENDPGM *EQ 'Y') THEN(DO)
               GOTO       CMDLBL(ENDPGM)
               ENDDO
             ENDDO

    /* ST. JOSEPH */
             IF         COND(&UPDSFX960 *EQ 'Y') THEN(DO)
             CHGVAR     VAR(&FILE) VALUE(&HDRFILE)
             CHGVAR     VAR(&DLSFXFIL) VALUE(&HDRSFX960)
             CHGVAR     VAR(&SENTFILE) VALUE(&SNTFILHDR)
             CALLSUBR   SUBR(CPYFRQTEMP) /* FOR HEADER */
               IF         COND(&ENDPGM *EQ 'Y') THEN(DO)
               GOTO       CMDLBL(ENDPGM)
               ENDDO

             CHGVAR     VAR(&FILE) VALUE(&DTLFILE)
             CHGVAR     VAR(&DLSFXFIL) VALUE(&DTLSFX960)
             CHGVAR     VAR(&SENTFILE) VALUE(&SNTFILDTL)
             CALLSUBR   SUBR(CPYFRQTEMP) /* FOR DETAIL */
               IF         COND(&ENDPGM *EQ 'Y') THEN(DO)
               GOTO       CMDLBL(ENDPGM)
               ENDDO
             ENDDO

/*-------------------------------------------------------------*/
/* ADD QTEMP RECORDS FROM HDR & DTL SUFFIX FILES TO SENT FILES */
/*-------------------------------------------------------------*/
    /* GUYMON */
             IF         COND(&UPDSFX360 *EQ 'Y') THEN(DO)
             CHGVAR     VAR(&DLSFXFIL) VALUE(&HDRSFX360)
             CHGVAR     VAR(&SENTFILE) VALUE(&SNTFILHDR)
             CALLSUBR   SUBR(CPYTOSENT) /* FOR HEADER */
               IF         COND(&ENDPGM *EQ 'Y') THEN(DO)
               GOTO       CMDLBL(ENDPGM)
               ENDDO

             CHGVAR     VAR(&DLSFXFIL) VALUE(&DTLSFX360)
             CHGVAR     VAR(&SENTFILE) VALUE(&SNTFILDTL)
             CALLSUBR   SUBR(CPYTOSENT) /* FOR DETAIL */
               IF         COND(&ENDPGM *EQ 'Y') THEN(DO)
               GOTO       CMDLBL(ENDPGM)
               ENDDO
             ENDDO

    /* SIOUX CITY */
             IF         COND(&UPDSFX440 *EQ 'Y') THEN(DO)
             CHGVAR     VAR(&DLSFXFIL) VALUE(&HDRSFX440)
             CHGVAR     VAR(&SENTFILE) VALUE(&SNTFILHDR)
             CALLSUBR   SUBR(CPYTOSENT) /* FOR HEADER */
               IF         COND(&ENDPGM *EQ 'Y') THEN(DO)
               GOTO       CMDLBL(ENDPGM)
               ENDDO

             CHGVAR     VAR(&DLSFXFIL) VALUE(&DTLSFX440)
             CHGVAR     VAR(&SENTFILE) VALUE(&SNTFILDTL)
             CALLSUBR   SUBR(CPYTOSENT) /* FOR DETAIL */
               IF         COND(&ENDPGM *EQ 'Y') THEN(DO)
               GOTO       CMDLBL(ENDPGM)
               ENDDO
             ENDDO

    /* ST. JOSEPH */
             IF         COND(&UPDSFX960 *EQ 'Y') THEN(DO)
             CHGVAR     VAR(&DLSFXFIL) VALUE(&HDRSFX960)
             CHGVAR     VAR(&SENTFILE) VALUE(&SNTFILHDR)
             CALLSUBR   SUBR(CPYTOSENT) /* FOR HEADER */
               IF         COND(&ENDPGM *EQ 'Y') THEN(DO)
               GOTO       CMDLBL(ENDPGM)
               ENDDO

             CHGVAR     VAR(&DLSFXFIL) VALUE(&DTLSFX960)
             CHGVAR     VAR(&SENTFILE) VALUE(&SNTFILDTL)
             CALLSUBR   SUBR(CPYTOSENT) /* FOR DETAIL */
               IF         COND(&ENDPGM *EQ 'Y') THEN(DO)
               GOTO       CMDLBL(ENDPGM)
               ENDDO
             ENDDO

/*------------------------------------------------------------*/
/* DELETE PROCESSED RECORDS FROM PFS MANIFEST HDR & DTL FILES.*/
/*------------------------------------------------------------*/
             IF         COND(&CO *EQ 0) THEN(DO)
               CALL       PGM(&UPDDELPGM) PARM(' ' 'D')
               MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(DELRCD))
               ENDDO
             ELSE DO
               CALL       PGM(&UPDDELPGM) PARM(' ' &COA 'D')
               MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(DELRCD))
             ENDDO

             GOTO       CMDLBL(ENDPGM)


/*-----------------------------------------------------------------*/
/*ˆ                     ERROR PROCESSING                           */
/*-----------------------------------------------------------------*/
 CRTDUPPF:
             SNDPGRMSG  TOPGR(PRKPFSDWN) MSG('ERROR:  Create +
                          duplicate file in qtemp for' *BCAT &FILE +
                          *BCAT &ENV)
             CHGJOB     LOG(4 00 *SECLVL)
             GOTO       CMDLBL(ENDPGM)

 CRTDUPLF:
         /*  SNDPGRMSG  TOPGR(PRKPFSDWN) MSG('ERROR:  Create +
                          duplicate file in qtemp for' *BCAT +
                          &UPDIDX *BCAT &ENV)                        */
         /*  CHGJOB     LOG(4 00 *SECLVL)                            */
         /*  GOTO       CMDLBL(ENDPGM)                               */

 PGMCALL:
             SNDPGRMSG  TOPGR(PRKPFSDWN) MSG('ERROR:  Call to create +
                          QTEMP suffixed file for' *BCAT &FILE +
                          *BCAT &ENV)
             CHGJOB     LOG(4 00 *SECLVL)
             GOTO       CMDLBL(ENDPGM)

 CPYSNT:
             SNDPGRMSG  TOPGR(PRKPFSDWN) MSG('ERROR:  Copy QTEMP +
                          records to sent file' *BCAT &SENTFILE +
                          *BCAT &ENV)
             CHGJOB     LOG(4 00 *SECLVL)
             GOTO       CMDLBL(ENDPGM)

 DELRCD:
             SNDPGRMSG  TOPGR(PRKPFSDWN) MSG('ERROR:  Delete +
                          processed records from file' *BCAT &FILE +
                          *BCAT &ENV)
             CHGJOB     LOG(4 00 *SECLVL)
             GOTO       CMDLBL(ENDPGM)


/**********************************************************************/
/*SUBROUTINES:                                                       */
/**********************************************************************/
    /*------------------------------------------------------------*/
    /*CPYFRQTEMP: COPY FROM QTEMP SUFFIX FILES INTO THE DOWNLOAD */
    /*CARLISLE SUFFIX FILES.                                     */
    /*CHECK IF DOWNLOAD SUFFIX FILE HAVE ANY EXISTING RECORDS    */
    /*FROM PRIOR TRANSMISSION.                                   */
    /*------------------------------------------------------------*/
             SUBR       SUBR(CPYFRQTEMP)

             RTVMBRD    FILE(&LIB/&DLSFXFIL) NBRCURRCD(&NBRCURRCD)

             /* CREATE THE SUFFIXED FILES IF THE FILE IS EMPTY  */
             /* IF THE FILES IS NOT EMPTY, CARLISLE HAS NOT     */
             /* PULLED DOWN THE DATA FROM THE SUFFIXED FILE YET */
             IF         COND(&NBRCURRCD *EQ 0) THEN(DO)
               /* ADD RECORDS TO THE SUFFIXED FILE */
               CPYF       FROMFILE(QTEMP/&DLSFXFIL) +
                            TOFILE(&LIB/&DLSFXFIL) MBROPT(*ADD) +
                            FMTOPT(*MAP *DROP)
               MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(CPYSNT))
               ENDDO
             ELSE DO
               /* RECORDS STILL EXISTS IN THE DOWNLOAD SUFFIX FILE, */
               /* UPDATE PFS PROCESS STATUS BACK TO '0'             */
               /* (NOT PROCESSED) IN PFS MANIFEST HDR & DTL.        */
               IF         COND(&CO *EQ 0) THEN(DO)
                  CALL       PGM(&UPDDELPGM) PARM(' ' 'U')
                  MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(UPDRCD))
               /*‚SEABOARD USER IS NOT ACTIVE, DO NOT USE */
                  SNDPGRMSG  TOPGR(PRKPFSDWN) MSG('WARNING:' *BCAT &FILE +
                               *BCAT 'not downloaded due to Carlisle +
                               have not picked up the data in the suffix +
                               table:' *BCAT &DLSFXFIL *BCAT &ENV)
                  CHGVAR     VAR(&ENDPGM) VALUE('Y')
                  ENDDO
               ELSE DO
                  CALL       PGM(&UPDDELPGM) PARM(' ' &COA 'U')
                  MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(UPDRCD))
                  SNDPGRMSG  TOPGR(PRKPFSDWN) MSG('WARNING:' *BCAT &FILE +
                               *BCAT 'not downloaded due to Carlisle +
                               have not picked up the data in the suffix +
                               table:' *BCAT &DLSFXFIL *BCAT &ENV)
                  CHGVAR     VAR(&ENDPGM) VALUE('Y')
               ENDDO
             ENDDO

             GOTO       CMDLBL(SKIP)

CPYSNT:      SNDPGRMSG  TOPGR(PRKPFSDWN) MSG('ERROR:  Copy QTEMP +
                          records to sent file' *BCAT &SENTFILE +
                          *BCAT &ENV)
             CHGJOB     LOG(4 00 *SECLVL)
             CHGVAR     VAR(&ENDPGM) VALUE('Y')
             GOTO       CMDLBL(SKIP)

UPDRCD:      SNDPGRMSG  TOPGR(PRKPFSDWN) MSG('ERROR:  Update process +
                          status back to unprocessed for file' +
                          *BCAT &FILE *BCAT &ENV)
             CHGJOB     LOG(4 00 *SECLVL)
             CHGVAR     VAR(&ENDPGM) VALUE('Y')

SKIP:
             ENDSUBR

    /*------------------------------------------------------*/
    /*CPYTOSENT: COPY THE SUFFIXED FILES TO THE SENT FILE. */
    /*------------------------------------------------------*/
             SUBR       SUBR(CPYTOSENT)

             CPYF       FROMFILE(QTEMP/&DLSFXFIL) +
                          TOFILE(&LIB/&SENTFILE) MBROPT(*ADD) +
                          FMTOPT(*MAP *DROP)
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(CPYSNT2))

             GOTO       CMDLBL(SKIPCPY2)

CPYSNT2:     SNDPGRMSG  TOPGR(PRKPFSDWN) MSG('ERROR:  Copy QTEMP +
                          records to sent file' *BCAT &SENTFILE +
                          *BCAT &ENV)
             CHGJOB     LOG(4 00 *SECLVL)
             CHGVAR     VAR(&ENDPGM) VALUE('Y')

SKIPCPY2:
             ENDSUBR

/*-----------------------------------------------------------------*/
/*                      PROGRAM END                                */
/*-----------------------------------------------------------------*/
 ENDPGM:

             ENDPGM
