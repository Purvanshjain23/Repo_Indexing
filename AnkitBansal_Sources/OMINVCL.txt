/***************************************************************** */
/*                                                                 */
/*  PROGRAM NUMBER  -> OMINVCL                                     */
/*                                                                 */
/*  PROGRAM NAME    -> CALL PROGRAMS TO RUN INVOICING INTERFACE    */
/*                                                                 */
/*  PROGRAM PURPOSE -> THIS CLP IS USED TO CALL THE PROGRAMS TO    */
/*                     INTERFACE INVOICES TO JDE.  PROGRAM ONE IS  */
/*                     RAN THAT BUILDS A WORKFILE AND PRINTS A     */
/*                     REPORT.  PROGRAM TWO READS THE WORKFILE AND */
/*                     WRITES A BATCH TO JDE AS UNPOSTED.  PROGRAM */
/*                     THREE IS A REPORT OVER THE WORKFILE IN      */
/*                     ACCOUNT ID SEQUENCE.                        */
/*                     THE WORKFILE NEEDS CLEARED EACH TIME.       */
/*                                                                 */
/*  PROGRAMMER      -> BARB GUTIERREZ           11/01/95           */
/*                                                                 */
/*  MODIFIED BY     -> LEANNE FEDOR             11/19/03           */
/*                     ADDED THE STANDARD PRINT FILE OVERRIDE LOGIC*/
/*                     TO ALLOW ME TO MAKE THE NEW LISTING PROGRAM */
/*                     PRINT AT 198 CHARACTERS INSTEAD OF 132.     */
/*  MODIFIED BY     -> BARB GUTIERREZ           07/22/04           */
/*                     CHANGED REPORT OMINVJDE3 TO PRINT AT 198    */
/*                     CHARACTERS INSTED OF 132 BY MOVING THE      */
/*                     DLTOVR(QPRINT) DOWN TO THE BOTTOM OF THE CL.*/
/*                                                                 */
/*  MODIFIED BY     -> ALICE BROWNFIELD         11/08/05           */
/*                     CHANGED TO ACCOMODATE THE TRIUMPH FOODS     */
/*                     NEW POSTING REQUIREMENTS.                   */
/*                     CHANGED PRINTING OVERRIDES TO MAKE THEM     */
/*                     WPRK BETTER FOR LASER PRINTERS.             */
/*                                                                 */
/*  MODIFIED BY     -> BARB GUTIERREZ           02/27/06           */
/*                     DUPLICATE OMINVJDE3 TO TRIUMPH IF HAVE      */
/*                     ANY 960 BATCHES. REMOVED SBDPRTOVR COMMAND. */
/*                     IT IS NO LONGER NEEDED DUE TO THE NEW       */
/*                     TECHNOLOGY OF PRINTERS.                     */
/*  MODIFIED BY     -> BARB GUTIERREZ           02/28/06           */
/*                     FIXED PROBLEM WITH DLTOVR OF QPRINT.        */
/*                                                                 */
/*  MODIFIED BY     -> LARA BUSER               05/09/06           */
/*                     RETRIEVE USER PROFILE. IF GROUP PROFILE IS  */
/*                     SBDPGMR THEN DUP SPOOLED FILE TO A TEST     */
/*                     ENVIRONMENT AND PUT ON HOLD.                */
/*  MODIFIED BY     -> PURVA DROGE              10/26/07           */
/*                     ADDED DUPSPLF FOR DAILY'S.                  */
/*  MODIFIED BY     -> LARA BUSER               04/20/10 P000198   */
/*                     CHANGE TEST OUTQ FOR PROGRAMMERS.           */
/*******************************************************************/
OMINVCL:     PGM   (&RETRN &CO)

/* DEFINE THE STANDARD VARIALBE FOR OVERRIDING THE PRINT FILE      */

             DCL        VAR(&CMD) TYPE(*CHAR) LEN(512)

             DCL        VAR(&RETRN) TYPE(*CHAR) LEN(7)
             DCL        VAR(&CO) TYPE(*DEC) LEN(3 0)
             DCL        VAR(&ACO) TYPE(*CHAR) LEN(3)

             DCL        VAR(&USER) TYPE(*CHAR) LEN(10)
             DCL        VAR(&GRPPRF) TYPE(*CHAR) LEN(10)

             DCL        VAR(&PLT) TYPE(*CHAR) LEN(1)
             DCL        VAR(&ORG) TYPE(*CHAR) LEN(1)

             RTVJOBA    USER(&USER)
             RTVUSRPRF  USRPRF(&USER) GRPPRF(&GRPPRF)

/*  CLEAR THE WORKFILE                                               */

             CLRPFM     FILE(OMP300)

/*  CHANGE COMPANY TO ALPHA                                          */

             CHGVAR     VAR(&ACO) VALUE(&CO)

/*  EXECUTE THE PROGRAM THAT WILL BUILD THE WORKFILE                 */

             CALL       PGM(OMINVJDE1) PARM(&RETRN &ACO)

/*  EXECUTE THE PROGRAM TO WRITE THE BATCH TO JDE                    */

             CALL       PGM(OMINVJDE2) PARM(&RETRN &ACO)

/*  OVERRIDE PRINT FILE AND EXECUTE PROGRAM TO PRINT WORKFILE LISTING*/
/*  OMINVJDE4 WILL COMBINE ALL COMPANIES IN THE WORK FILES.  IT      */
/*  PRINTS BY ACTUA SHIP DATE AND ORDER NUMBER.                      */

             OVRPRTF    FILE(PRINT198) HOLD(*YES) SAVE(*YES)
             CALL       PGM(OMINVJDE4) PARM(&RETRN &ACO)

/*  EXECUTE THE PROGRAM TO PRINT REPORT IN ACCOUNT ID SEQUENCE       */

             CALL       PGM(OMINVJDE3) PARM(&RETRN &ACO)

/* IF HAVE RECORDS FOR TRIUMPH FOODS, DUPLICATE THE REPORT TO        */
/* THEIR OUTQ AND TO TIM'S OUTQ.                                     */
/* *** HOWEVER, IF A PROGRAMMER IS RUNNING THIS PROGRAM THEN DUP     */
/* THE SPOOL FILE TO A TEST OUTQ AND PUT IT ON HOLD.                 */

             IF         COND(&GRPPRF *EQ 'SBDPGMR') THEN(DO)
             DUPSPLF    FILE(PRINT198) OUTQ(ISPRT01) SPLNBR(*LAST) +
                          HOLD(*YES) SAVE(*YES)
             GOTO       CMDLBL(DELETEOVR)
             ENDDO

             IF COND(&CO *EQ 960) THEN(DO)
             DUPSPLF FILE(PRINT198) OUTQ(TFPRTDUP) +
                          SPLNBR(*LAST) HOLD(*NO)
             DUPSPLF    FILE(PRINT198) OUTQ(ISPRT08) SPLNBR(*LAST) +
                          HOLD(*YES) SAVE(*YES)
             ENDDO

             IF COND(&CO *EQ 363) THEN(DO)
             DUPSPLF FILE(PRINT198) OUTQ(DSPRTDUP) +
                          SPLNBR(*LAST) HOLD(*NO)
             ENDDO

             IF COND(&CO *EQ 364) THEN(DO)
             DUPSPLF FILE(PRINT198) OUTQ(DMPRTDUP) +
                          SPLNBR(*LAST) HOLD(*NO)
             ENDDO

 DELETEOVR:  DLTOVR     FILE(PRINT198)

 EXIT:       ENDPGM
