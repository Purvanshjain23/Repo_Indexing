/***************************************************************** */
/*                                                                   */
/*  PROGRAM NUMBER:  HP4440CL                                        */
/*  PROGRAM NAME:    HOG GROUP REPORTS/DOWNLOADS                     */
/*  PROGRAMMER:      LEANNE FEDOR                                    */
/*  CREATE DATE:     12/30/05                                        */
/*                                                                   */
/*  FUNCTION:        THIS CL IS SUBMITTED WITH QCMDEXC FROM          */
/*                   HP4440-SPECIFY GROUP RERPOT/DOWNLAOD SELECTIONS.*/
/*                                                                   */
/*                   OPTIONS ARE PASSED THROUGH THE LDA.             */
/*                                                                   */
/*******************************************************************/

             PGM

             DCL        VAR(&ERROR) TYPE(*CHAR) LEN(1)
             DCL        VAR(&CMD) TYPE(*CHAR) LEN(512)

             DCL        VAR(&HOLD) TYPE(*CHAR) LEN(10)
             DCL        VAR(&COPYS) TYPE(*DEC) LEN(2)
             DCL        VAR(&OUTQ) TYPE(*CHAR) LEN(10)

             DCL        VAR(&LDHGSN) TYPE(*DEC)  LEN(7)
             DCL        VAR(&LDOPT1) TYPE(*CHAR) LEN(1)
             DCL        VAR(&LDOPT2) TYPE(*CHAR) LEN(1)
             DCL        VAR(&LDOPT3) TYPE(*CHAR) LEN(1)
             DCL        VAR(&LDOPT4) TYPE(*CHAR) LEN(1)
             DCL        VAR(&LDOPT5) TYPE(*CHAR) LEN(1)
             DCL        VAR(&LDEMAIL) TYPE(*CHAR) LEN(50)

             CHGVAR     VAR(&ERROR) VALUE('N')
             CHGVAR     VAR(&LDHGSN) VALUE(%SST(*LDA 6 7))
             CHGVAR     VAR(&LDOPT1) VALUE(%SST(*LDA 52 1))
             CHGVAR     VAR(&LDOPT2) VALUE(%SST(*LDA 53 1))
             CHGVAR     VAR(&LDOPT3) VALUE(%SST(*LDA 54 1))
             CHGVAR     VAR(&LDOPT4) VALUE(%SST(*LDA 55 1))
             CHGVAR     VAR(&LDOPT5) VALUE(%SST(*LDA 56 1))
             CHGVAR     VAR(&LDEMAIL) VALUE(%SST(*LDA 100 50))

/*------------------------------------------------------------------*/
/* SET OVERRIDES FOR PRINT FILES                                    */
/*------------------------------------------------------------------*/

 HOLD:       IF         COND(%SST(*LDA 411 1) = 'Y') THEN(CHGVAR +
                          VAR(&HOLD) VALUE(*YES))
             ELSE       CMD(CHGVAR VAR(&HOLD) VALUE(*NO))

 COPYS:      CHGVAR     VAR(&COPYS) VALUE(%SST(*LDA 412 1))
             IF         COND(&COPYS = 0) THEN(CHGVAR VAR(&COPYS) +
                          VALUE(1))

 OUTQ:       CHGVAR     VAR(&OUTQ) VALUE(%SST(*LDA 413 10))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(%SST(*LDA 401 10)))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(*JOB))

/*-------------------------------------------------------------------*/
/*  BUILD WORKFILES                                                  */
/*-------------------------------------------------------------------*/

/*  Create/build the Feed Ticket workfile if the user has selected   */
/*  any of the following options.                                    */
/*            1) Hog Group Review with Costs                         */
/*            2) Hog Group Review without Costs                      */
/*            5) Download Feed                                       */

             IF         COND(&LDOPT1 *NE ' ' *OR &LDOPT2 *NE ' ' *OR +
                          &LDOPT5 *NE ' ') THEN(DO)
             CRTDUPOBJL OBJ(HSP386) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)
             CALL HP386
             ENDDO

/*  Create/build the Movement workfile if the user has selected      */
/*  any of the following options.                                    */
/*            1) Hog Group Review with Costs                         */
/*            2) Hog Group Review without Costs                      */
/*            5) Download Movements                                  */

             IF         COND(&LDOPT1 *NE ' ' *OR &LDOPT2 *NE ' ' *OR +
                          &LDOPT4 *NE ' ') THEN(DO)
             CRTDUPOBJL OBJ(HSP381) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)
             CALL HP381
             ENDDO

/*-------------------------------------------------------------------*/
/*  HOG GROUP REVIEWS (WITH AND WITHOUT COSTS)                       */
/*-------------------------------------------------------------------*/

             IF         COND(&LDOPT1 *NE ' ' *OR &LDOPT2 *NE ' ') +
                          THEN(DO)
/* Print overrides                                              */

             SBDPRTOVR  CMDVAR(&CMD) FILE(QPRINT) OUTQ(&OUTQ) +
                          WIDTH(198) LANDSCAPE(*YES) DRAWER(2) +
                          COPIES(&COPYS) HOLD(&HOLD)

             CALL       PGM(QCMDEXC) PARM(&CMD 512)


/* Hog Group Review with Costs                                     */

             IF         COND(&LDOPT1 *NE ' ') THEN(DO)
             CHGDTAARA  DTAARA(*LDA (20 1)) VALUE('Y')
             CALL       PGM(HP686)
             ENDDO

/* Hog Group Review without Costs                                  */

             IF         COND(&LDOPT2 *NE ' ') THEN(DO)
             CHGDTAARA  DTAARA(*LDA (20 1)) VALUE('N')
             CALL       PGM(HP686)
             ENDDO

             DLTOVR     FILE(QPRINT)

             ENDDO


/*-------------------------------------------------------------------*/
/*  HOG GROUP MOVEMENTS                                              */
/*-------------------------------------------------------------------*/

             IF         COND(&LDOPT3 *NE ' ') THEN(DO)

             SBDPRTOVR  CMDVAR(&CMD) FILE(QPRINT) OUTQ(&OUTQ) +
                          WIDTH(80) COPIES(&COPYS) HOLD(&HOLD)
             CALL       PGM(QCMDEXC) PARM(&CMD 512)

             CALL       PGM(HP630)
             DLTOVR     FILE(QPRINT)
             ENDDO


/*------------------------------------------------------------------------*/
/* RUN A SEQUEL VIEW TO CREATE THE FEED MOVEMENT SPREADSHEET AND EMAIL IT */
/*------------------------------------------------------------------------*/

             IF         COND(&LDOPT4 *NE ' ') THEN(DO)

             EXECUTE    VIEW(*LIBL/HP381V) PCFMT(*XLS) REPLACE(*YES) +
                          RECIPIENT(&LDEMAIL) EMLMSG('HPS Hog Group +
                          Review: Movement Data') TEXT('HPS Hog +
                          Group Review: Movement Data')
             MONMSG     MSGID(CPF0000)
             ENDDO

/*-----------------------------------------------------------------------*/
/* RUN A SEQUEL VIEW TO CREATE THE FEED TICKET SPREADSHEET AND EMAIL IT */
/*-----------------------------------------------------------------------*/

             IF         COND(&LDOPT5 *NE ' ') THEN(DO)

             EXECUTE    VIEW(*LIBL/HP386V) PCFMT(*XLS) REPLACE(*YES) +
                          RECIPIENT(&LDEMAIL) EMLMSG('HPS Hog Group +
                          Review: Feed Ticket Data') TEXT('HPS Hog +
                          Group Review: Feed Ticket Data')
             MONMSG     MSGID(CPF0000)
             ENDDO

             DLTOVR     FILE(*ALL)

             ENDPGM

