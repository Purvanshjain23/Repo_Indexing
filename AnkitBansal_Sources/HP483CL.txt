/*****************************************************************  */
/*                                                                  */
/*  PROGRAM NUMBER --> HP483CL                                      */
/*                                                                  */
/*  PROGRAM NAME ----> CHECK REGISTER EDIT                          */
/*                                                                  */
/*  PROGRAM PURPOSE -> THIS CL CALLS THE PROGRAM TO PRINT THE CHECK */
/*                     REGISTER EDIT                                */
/*                                                                  */
/*  PROGRAMMER ------> ROSE MARTIN               08/10/94          */
/***************************************************************** */
/*  MODIFICATIONS:                                                 */
/*  09/02/94  LEANNE FEDOR                                         */
/*            REWRITTEN TO USE AN OPNQRY AND NO WORKFILE.          */
/*                                                                  */
/*  09/30/94  MARY PALMER SELECT AND SORT ON KILL DATE INSTEAD OF   */
/*            OF CHECK DATE. USE JOINED FILE HSJ063C (CHECK HEADER  */
/*            HEADER + CHECK DETAIL)                                */
/*                                                                  */
/*  06/30/95  LEANNE FEDOR                                         */
/*            FOR MOVEMENT REWRITE, CHANGED QUERY TO RUN OVER THE  */
/*            CHECK DETAIL FILE.                                   */
/*                                                                 */
/* 04/25/96  LEANNE FEDOR                                          */
/*           ADDED CUSTOMIZED PRINTER OVERRIDE COMMAND TO FORCE    */
/*           REPORT TO PRINT ON LASER PRINTER.                     */
/*                                                                 */
/* 06/19/97  LEANNE FEDOR                                          */
/*           CHANGED OUT QUEUE LOGIC.                              */
/*                                                                 */
/* 06/02/04  LEANNE FEDOR                                          */
/*           ADDED LOGIC TO ACCOMMODATE CALLING THIS FROM          */
/*           THE W/W CHECKS FUNCTION TO PRINT A SINGLE CHECK.      */
/*                                                                 */
/* 11/09/17  BRAD BADEN   E010812                                  */
/*           ADDED BUSINESS OFFICE TO THE LDA.  CHANGED OPNQRY     */
/*           TO USE JOIN FILE HSJ064J AND BUSINESS OFFICE.         */
/*******************************************************************/

             PGM

/* DEFINE THE VARIABLE REQUIRED IN THE COMMAND TO OVERRIDE THE     */
/* PRINT FILE TO A LASER PRINTER                                   */

             DCL        VAR(&CMD) TYPE(*CHAR) LEN(512)

             DCL        VAR(&QDATA) TYPE(*CHAR) LEN(118)
             DCL        VAR(&LDFYMD) TYPE(*DEC) LEN(8)
             DCL        VAR(&LDTYMD) TYPE(*DEC) LEN(8)
             DCL        VAR(&LDCVNO) TYPE(*DEC) LEN(8)
             DCL        VAR(&LDCHNO) TYPE(*DEC) LEN(7)
             DCL        VAR(&LD435) TYPE(*CHAR) LEN(1)
             DCL        VAR(&LDBOCD) TYPE(*CHAR) LEN(5)

             DCL        VAR(&HOLD) TYPE(*CHAR) LEN(10)
             DCL        VAR(&COPYS) TYPE(*DEC) LEN(2)
             DCL        VAR(&OUTQ) TYPE(*CHAR) LEN(10)

             CHGVAR     VAR(&LDFYMD) VALUE(%SST(*LDA 7 8))
             CHGVAR     VAR(&LDTYMD) VALUE(%SST(*LDA 21 8))
             CHGVAR     VAR(&LDCVNO) VALUE(%SST(*LDA 29 8))
             CHGVAR     VAR(&LDCHNO) VALUE(%SST(*LDA 37 7))
             CHGVAR     VAR(&LD435) VALUE(%SST(*LDA 44 1))
             CHGVAR     VAR(&LDBOCD) VALUE(%SST(*LDA 45 5))

/********************************************************************/
/*  WHEN THIS REPORT IS SUBMITTED FROM HP483-SPECIFY CHECK REGISTER */
/********************************************************************/

             IF         COND(&LD435 *EQ 'N') THEN(DO)

/*  ALWAYS SELECT RECORDS WITH SALES ACCRUAL DATE IN THE RANGE     */

             CHGVAR     VAR(%SST(&QDATA 1 10)) VALUE('CDSADT *GE')
             CHGVAR     VAR(%SST(&QDATA 12 8)) VALUE(&LDFYMD)
             CHGVAR     VAR(%SST(&QDATA 21 15)) VALUE('*AND CDSADT +
                          *LE')
             CHGVAR     VAR(%SST(&QDATA 37 8)) VALUE(&LDTYMD)

/* ADD A SELECTION ON CUSTOMER NUMBER IF THE USER ENTERED ONE    */

             IF         COND(&LDCVNO *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 46 15)) VALUE('*AND CDCVNO +
                          *EQ')
             CHGVAR     VAR(%SST(&QDATA 62 8)) VALUE(&LDCVNO)
             ENDDO

/* ADD A SELECTION ON CHECK NUMBER IF THE USER ENTERED ONE       */

             IF         COND(&LDCHNO *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 71 15)) VALUE('*AND CDCHNO +
                          *EQ')
             CHGVAR     VAR(%SST(&QDATA 87 7)) VALUE(&LDCHNO)
             ENDDO

/* ADD A SELECTION ON BUSINESS OFFICE                            */

             CHGVAR     VAR(%SST(&QDATA 95 17)) VALUE('*AND HGFSBO +
                          *EQ ''')
             CHGVAR     VAR(%SST(&QDATA 112 5)) VALUE(&LDBOCD)
             CHGVAR     VAR(%SST(&QDATA 117 1)) VALUE('''')

             ENDDO

/*******************************************************************/
/*  WHEN THIS REPORT IS SUBMITTED FROM HP435-W/W CHECKS            */
/*******************************************************************/

             IF         COND(&LD435 *EQ 'Y') THEN(DO)

/*  ALWAYS SELECT RECORDS FOR THIS CUSTOMER/CHECK NUMBER/CHECK DATE */

             CHGVAR     VAR(%SST(&QDATA 1 10)) VALUE('CDCVNO *EQ')
             CHGVAR     VAR(%SST(&QDATA 12 8)) VALUE(&LDCVNO)

             CHGVAR     VAR(%SST(&QDATA 21 15)) VALUE('*AND CDCHNO *EQ')
             CHGVAR     VAR(%SST(&QDATA 37 7)) VALUE(&LDCHNO)

             CHGVAR     VAR(%SST(&QDATA 45 15)) VALUE('*AND CDCHDT *EQ')
             CHGVAR     VAR(%SST(&QDATA 61 8)) VALUE(&LDFYMD)

             CHGVAR     VAR(%SST(&QDATA 70 17)) VALUE('*AND HGFSBO +
                          *EQ ''')
             CHGVAR     VAR(%SST(&QDATA 87 5)) VALUE(&LDBOCD)
             CHGVAR     VAR(%SST(&QDATA 92 1)) VALUE('''')

             ENDDO

/*******************************************************************/
/*  PERFORM THE OPNQRY ON THE CHECK DETAIL FILE                     */
/*******************************************************************/

             OVRDBF     FILE(HSJ064J) SHARE(*YES)
             OPNQRYF    FILE((HSJ064J)) QRYSLT(&QDATA) +
                          KEYFLD((HGFSBO) (CDSADT) (CDCVNO) +
                          (CDCHNO)) SEQONLY(*YES)

/*******************************************************************/
/* SET UP PRINT OPTIONS                                            */
/*******************************************************************/

 HOLD:       IF         COND(%SST(*LDA 411 1) = 'Y') THEN(CHGVAR +
                          VAR(&HOLD) VALUE(*YES))
             ELSE       CMD(CHGVAR VAR(&HOLD) VALUE(*NO))

COPYS:       CHGVAR     VAR(&COPYS) VALUE(%SST(*LDA 412 1))
             IF         COND(&COPYS = 0) THEN(CHGVAR VAR(&COPYS) +
                          VALUE(1))

OUTQ:        CHGVAR     VAR(&OUTQ) VALUE(%SST(*LDA 413 10))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(%SST(*LDA 401 10)))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(*JOB))

/*------------------------------------------------------------------*/
/* OVERRIDE PRINT FILE                                              */
/*------------------------------------------------------------------*/

/* USE THE CUSTOMIZED SEABOARD COMMAND TO OVERRIDE THE PRINT FILE.  */
/* THIS COMMAND ALLOWS THE REPORT TO BE GENERATED ON A STANDARD LINE*/
/* PRINTER OR A LASER PRINTER. TO USE THIS OVERRIDE YOU MUST DECLARE*/
/* A COMMAND VARIABLE AND FOLLOW THE OVERRIDE WITH A CALL TO        */
/* QCMDEXC.                                                         */

/* THIS REPORT IS CONDENSED PRINT OF 15CPI ON 198 COLUMNS OF PRINT  */
/* FORCE IT TO PRINT OUT OF DRAWER 2 ON LANDSCAPE                   */

             SBDPRTOVR  CMDVAR(&CMD) FILE(QPRINT) OUTQ(&OUTQ) +
                          WIDTH(198) LANDSCAPE(*YES) DRAWER(2) +
                          COPIES(&COPYS) HOLD(&HOLD)

             CALL       PGM(QCMDEXC) PARM(&CMD 512)


/*-------------------------------------------------------------------*/
/* PRINT THE CHECK REGISTER                                          */
/*-------------------------------------------------------------------*/

             CALL       PGM(HP683)
             DLTOVR     FILE(*ALL)

             ENDPGM
