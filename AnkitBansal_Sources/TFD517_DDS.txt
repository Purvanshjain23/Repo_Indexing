     A*%%TS  SD  20050816  090113  ISLRAMS     REL-V5R3M0  5722-WDS
     A*****************************************************************
     A*
     A*  SYSTEM:        TRIUMPH FOODS
     A*  DISPLAY FILE:  TFD517 - SELECT DATA SOURCE
     A*  CREATED BY:    LEANNE FEDOR
     A*  DATE:          08/16/05
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/TFPREF)
     A                                      PRINT
     A                                      CA12(12 'PREVIOUS')
     A N82                                  ROLLUP(25 'PAGE FORWARD')
     A*---------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*---------------------------------------------------------------
     A          R TF517S1                   SFL
     A*%%TS  SD  20050307  103201  ISLRAMS     REL-V5R2M0  5722-WDS
     A            HDRRN1    R        H      REFFLD(@@RRN)
     A            D1DSCD    R        O  5  1REFFLD(@@DSCD)
     A            D1DSDS    R        O  5  8REFFLD(@@DSDS)
     A*
     A*---------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*---------------------------------------------------------------
     A          R TF517C1                   SFLCTL(TF517S1)
     A*%%TS  SD  20050816  090113  ISLRAMS     REL-V5R3M0  5722-WDS
     A                                      SFLSIZ(0005)
     A                                      SFLPAG(0004)
     A                                      WINDOW(TF517EC)
     A                                      OVERLAY
     A                                      SFLCSRRRN(&HDRRN)
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A            HDPAG1    R        H      REFFLD(@@PAG)
     A                                      SFLRCDNBR
     A            HDRRN     R        H      REFFLD(@@RRN)
     A            SDPGM     R        O  1  1REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 13'SELECT DATA SOURCE'
     A                                      DSPATR(HI)
     A                                  2  1'Position Cursor, Press Enter'
     A                                  4  1'Code'
     A                                      DSPATR(HI)
     A                                  4  8'Description'
     A                                      DSPATR(HI)
     A*---------------------------------------------------------------
     A* COMMAND LINE LINE
     A*---------------------------------------------------------------
     A          R TF517K1
     A                                      WINDOW(TF517EC)
     A                                      OVERLAY
     A  43                              9  1'Message program failed. Call IS.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 10  1'F12=Previous'
      *
     A*-------------------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
     A*-------------------------------------------------------------------------
     A          R TF517ES                   SFL SFLMSGRCD(10)
      *
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
     A*
     A*-------------------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*-------------------------------------------------------------------------
     A*
     A          R TF517EC                   SFLCTL(TF517ES)
     A*%%TS  SD  19980327  105628  ISLRAMS     REL-V3R7M0  5716-PW1
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A                                      WINDOW(*DFT 12 35)
     A                                      WDWBORDER((*DSPATR RI) (*CHAR '    -
     A                                          '))
     A            MSGPGM                    SFLPGMQ(10)
     A*
     A*---------------------------------------------------------------
     A* ASSUME FORMAT -- REQUIRED FOR WINDOWS
     A*---------------------------------------------------------------
     A          R ASSUME
     A*%%TS  SD  19980327  102611  ISLRAMS     REL-V3R7M0  5716-PW1
     A                                      ASSUME
     A                                      OVERLAY
     A                                 24 79' '
