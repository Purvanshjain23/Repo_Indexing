      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      HOG PRODUCTION
      * PROGRAM:     HP8007
      * TITLE:       Retrieve Saturday Week-ending Date
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     03/11/02
      *
      * FUNCTION:  Given an incoming date, this program retrieves the Saturday week-ending
      *            date and the HPS calendar year/week associated with the date.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      * 99/99/99
      *
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
      *
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      *
      ****************************************************************
      * ARRAYS AND TABLES
      ****************************************************************
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      *
      * Work fields for date manipulation
      *
     D wkyymmdd        s              6  0
      *
      *
      * Parms
      *
     D xxcocd          s              3  0
     D xxreturn        s              7a
      *
     D xxccyymmdd      s              8  0
     D xxcyymmddi      s              7  0
     D xxcyymmddo      s              7  0
     D xxsatdt         s              8  0
      *
     D xxjulian        s              5  0
     D xxhpscdyr       s              4  0
     D xxhpscdwk       s              2  0
     D xxhpspicdt      s              5  0
      *
     D xxcdyr          s              4  0
     D xxcdwk          s              2  0
     D xxday           s              1a
      *
     D xxacyr          s              4  0
     D xxacpe          s              2  0
     D xxacwk          s              2  0
      *
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * Mainline
      ****************************************************************
      *
      * Get the incoming date into the SYNON format.
      *
     C                   z-add     xxccyymmdd    wkyymmdd
     C                   if        xxccyymmdd >= 20000000
     C     wkyymmdd      add       1000000       xxcyymmddi
     C                   else
     C                   z-add     wkyymmdd      xxcyymmddi
     C                   endif
      *
      *
      * Call a SYNON program to retrieve Saturday week-ending date associated with this
      * date from the OMS Calendar file.
      *
     C                   call      'POJNXFR'
     C                   parm      *blank        xxreturn
     C                   parm      350           xxcocd
     C                   parm                    xxcyymmddi
     C                   parm      0             xxcyymmddo
     C                   parm      0             xxcdyr
     C                   parm      0             xxcdwk
      *
      *
      * If you got a Saturday date, turn around and call another SYNON progarm
      * to get the following info associated with that Saturday.
      *   1) HPS calendar year
      *   2) HPS calendar week
      *
     C                   if        xxreturn = *blank                            If got Saturday
     C                   call      'POF3XFR'
     C                   parm      *blank        xxreturn
     C                   parm                    xxcocd
     C                   parm                    xxcyymmddo
     C                   parm      0             xxjulian
     C                   parm      0             xxacyr
     C                   parm      0             xxacpe
     C                   parm      0             xxacwk
     C                   parm      *blank        xxday
     C                   parm      0             xxcdwk
     C                   parm      0             xxhpspicdt
     C     xxcdyr        parm      0             xxhpscdyr
     C     xxcdwk        parm      0             xxhpscdwk
      *
      *
     C                   if        xxreturn = *blank                            If got year/week
     C                   z-add     xxcyymmddo    wkyymmdd
     C                   z-add     wkyymmdd      xxsatdt
     C                   if        xxcyymmddo >= 1000000
     C                   add       20000000      xxsatdt
     C                   else
     C                   add       19000000      xxsatdt
     C                   endif
     C                   endif                                                  If got year/week
     C                   endif                                                  If got Saturday
      *
     C                   seton                                        lr
      /EJECT
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Parm lists
      *
     C     *entry        plist
     C                   parm                    xxccyymmdd
     C                   parm                    xxsatdt
     C                   parm                    xxcdyr
     C                   parm                    xxcdwk
      *
     C                   endsr
      /EJECT
