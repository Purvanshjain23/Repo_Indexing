      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Hog Production System
      * PROGRAM:     HP6708-Item Master Listing
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     07/19/04
      *
      * Function:    This function lists the Items in the HPS Master file.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 10/13/04  LeAnne Fedor
      *           Recompile only. Cost Per Unit Amount was changed from
      *           9,2 to 9,4.
      *
      * 01/21/21  ISE                TSN593
      *           Replaced *loval with *start
      *
      /eject
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fhsp180    if   e           k disk
      *    Item types
      *
      *
     Fhsp181    if   e           k disk
      *    Items
      *
      *
     Fqprint    o    f  198        printer oflind(*inof)
      *
      /eject
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      *----------------------------------------------------------------
      * CONSTANTS
      *----------------------------------------------------------------
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      *----------------------------------------------------------------
      * ARRAYS AND TABLES
      *----------------------------------------------------------------
      *
      *----------------------------------------------------------------
      * STANDALONE FIELDS
      *----------------------------------------------------------------
      *
      * Standard fields
      *
     D dash            s            198    inz(*all'-')
     d rtime           s              6  0
      *
      *
      * Control fields
      *
     D first           s              1    inz('Y')
      *
      *
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *---------------------------------------------------------------
      * Data areas
      *---------------------------------------------------------------
      *
      * Local data area.
      *
     Dlda             uds                  dtaara(*lda)
      *
     D  ldver                 01      1
     D  lditcd                02     26
     D  ldbgit                27     51
     D  ldittycd              52     57
     D  ldaist                58     58
     D  ldbgfl                59     59
     D  ldcritfl              60     60
      *
     D  ldverds              101    130
     D  lditds               131    180
     D  ldittyds             181    210
     D  ldaistds             211    218
     D  ldbgflds             219    221
     D  ldcritflds           222    224
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * Mainline
      ****************************************************************
      *
      * Print headings
     C                   except    hrhdr
      *
      * Read/print each record in the HPS Item Master file.
      *
593  C*    *loval        setll     hsp181
593  C     *start        setll     hsp181
      *
     C                   dou       *in90 = *on                                  Do items
     C                   read      hsp181                                 90
     C                   if        *in90 = *off                                 If not EOF
     C                   move      no            first
     C                   exsr      $prtrec
     C                   endif                                                  If not EOF
     C                   enddo                                                  Do items
      *
      * EOF processing
      *
     C                   if        first = yes
     C                   except    nodata
     C                   endif
      *
     C                   seton                                        lr
      /eject
      *---------------------------------------------------------------
      * Print a record
      *---------------------------------------------------------------
      *
     C     $prtrec       begsr
      *
      * Print headings if necessary
      *
     C                   if        *inof = *on
     C                   except    hrhdr
     C                   endif
      *
      * Print detail record
      *
     C                   except    l1dtl
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Set up heading info.
      *
     C                   time                    rtime
      *
     C                   if        lditcd = *blank
     C                   seton                                        91
     C                   endif
      *
     C                   if        ldbgit = *blank
     C                   seton                                        92
     C                   endif
      *
     C                   if        ldittycd = *blank
     C                   seton                                        93
     C                   endif
      *
     C                   if        ldaist = *blank
     C                   seton                                        94
     C                   endif
      *
     C                   if        ldbgfl = *blank
     C                   seton                                        95
     C                   endif
      *
     C                   if        ldcritfl = *blank
     C                   seton                                        96
     C                   endif
      *
     C                   endsr
      /eject
      *-------------------------------------------------------------
      * Report heading lines
      *-------------------------------------------------------------
      *
     Oqprint    e            hrhdr          1 01
     O                       sdpgm               10
     O                                          100 'HOG PRODUCTION SYSTEM'
     O                                          188 'DATE'
     O                       udate         y    198
      *
     O          e            hrhdr          1
     O                       sdusr               10
     O                                           95 'ITEM LISTING'
     O                                          188 'TIME'
     O                       rtime              198 '  :  :  '
      *
     O          e            hrhdr          2
     O                       ldverds            105
     O                                          188 'PAGE'
     O                       page          z    198
      *
     O          e            hrhdr       0  1
     O                                           16 'Item number ...:'
     O                       lditcd              42
     O               91                          20 'All'
     O              n91      lditds              94
      *
     O          e            hrhdr       0  1
     O                                           16 'Budget item ...:'
     O                       ldbgit              42
     O               92                          20 'All'
      *
     O          e            hrhdr       0  1
     O                                           16 'Item type code :'
     O                       ldittycd            23
     O               93                          20 'All'
     O              n93      ldittyds            74
      *
     O          e            hrhdr       0  1
     O                                           16 'Active/inactive:'
     O                       ldaist              18
     O               94                          20 'All'
     O              n94      ldaistds            31
      *
     O          e            hrhdr       0  1
     O                                           16 'Budget flag ...:'
     O                       ldbgfl              18
     O               95                          20 'All'
     O              n95      ldbgflds            26
      *
     O          e            hrhdr       0  2
     O                                           16 'Critical flag .:'
     O                       ldcritfl            18
     O               96                          20 'All'
     O              n96      ldcritflds          26
     O
      *
      *-------------------------------------------------------------
      * Column headings
      *-------------------------------------------------------------
      *
     O          e            hrhdr       0  1
     O                                          119 'Item'
     O                                          127 'Crit'
     O                                          133 'Bdgt'
     O                                          167 'A/I'
     O                                          174 'Stock'
     O                                          198 'Cost per Unit'
      *
     O          e            hrhdr       0  1
     O                                           11 'Item Number'
     O                                           43 'Item Description'
     O                                           83 'UOM'
     O                                          103 'Volume'
     O                                          110 'UOW'
     O                                          119 'Type'
     O                                          127 'Flag'
     O                                          133 'Flag'
     O                                          148 'Budget Item'
     O                                          167 'St'
     O                                          177 'Location'
     O                                          198 'Amount'
      *
     O          e            hrhdr       0  2
     O                       dash               198
      *
      *-------------------------------------------------------------
      * Detail line
      *-------------------------------------------------------------
      *
     O          e            l1dtl       0  1
     O                       imitcd              25
     O                       imitds              77
     O                       imuom               88
     O                       imvol         k    104
     O                       imuow              112
     O                       imittycd           121
     O                       imcritfl           125
     O                       imbgfl             132
     O                       imbgit             162
     O                       imaist             166
     O                       imstkloc           184
     O                       imcpuam       k    198
      *
      *-------------------------------------------------------------
      * No data message line
      *-------------------------------------------------------------
      *
     O          e            nodata      1
     O                                           19 'NO DATA MEETS YOUR'
     O                                           38 'SELECTION CRITERIA'
      /eject
