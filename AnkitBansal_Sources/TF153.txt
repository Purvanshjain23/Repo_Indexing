      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Triumph Foods
      * PROGRAM:     TF153
      * TITLE:       Inventory: Revise Inventory Cost
      * PROGRAMMER:  LeAnne Ramsey
      * CREATED:     03/30/07
      *
      * FUNCTION: Uses an edit record format to add/change/delete records.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 12/10/07  LeAnne Ramsey
      *           New field (Freezer Interest Rate %) added to file and screen.
      *
      * 08/15/08  LeAnne Ramsey
      *           Users are no longer doing Holding/Inflow calcs.
      *           Holding Cost/Lb and Inflow Cost/Lb were removed from file/screen/
      *           program.
      /eject
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Ftfd153    cf   e             workstn infds(iofeed)
      *
      *
     Ftfp099    if   e           k disk
      *   Function control
      *
      *
     Ftfp053    uf a e           k disk
      *   Invnetory costs
      *
      /eject
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D create          c                   'CREATE'
     D revise          c                   'REVISE'
     D delete          c                   'DELETE'
     D yes             c                   'Y'
     D no              c                   'N'
     D set1            c                   'SET1  '
     D edit1           c                   'EDIT1 '
     D scrn1           c                   'SCRN1 '
     D exit            c                   'EXIT  '
     D update          c                   'UPDATE'
      *
      *
      ****************************************************************
      * ARRAYS AND TABLES
      ****************************************************************
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Standard fields
      *
     D mode            s              6
     D pgm             s             10
     D msgfl           s             10
     D routne          s              6
     D rtncd           s              2
     D action          s                   like(mode)
     D maxmsg          s              2  0  inz(20)
      *
      *
      * Control fields
      *
     D svwedt          s                   like(inefdt)
     D svcbdt          s                   like(fncbdt)
      *
      *
      *
      * Workfields for date manipulation
      *
     D wkisodate       s               D   datfmt(*iso)
     D wkdt            s              8  0
      *
      *
      * Parms
      *
     D xxwedt          s                   like(inefdt)
     D xxsatdt         s              8  0
     D xxdt            s              8  0
     D xxyr            s              4  0
     D xxpe            s              4  0
     D xxwk            s              2  0
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * standard error message handling data structures
      *---------------------------------------------------------------
      *
      * For error message handling, a packed index (3) is required.
      * For program readability, define a corresponding error count
      * field called 'error'
      *
     D                 ds                  inz
     D  error                         2p 0
     D   e                            2p 0 overlay(error)
      *
      * This data structure supplies the name of the message file to
      * the message handling CL program.  The field name MSGFIL must be
      * constant. The value in quotes is the name of the specific
      * message file containing the user defined messages.
      *
     D                 ds                  inz
     D  msgfil                       10    inz('HSMSGF    ')
      *
      *---------------------------------------------------------------
      *  Standard message data structures
      *---------------------------------------------------------------
      *
      * The following 3 data structures are used to speed message
      * handling since it is faster to clear data structures than
      * arrays.  Each is associated with a standard message array.
      *
     D mgi             ds                  inz
     D  mgid                          7    dim(20)
      *
     D mgd             ds                  inz
     D  mgdt                         50    dim(20)
      *
     D mgwk            ds                  inz
     D  mgw                           1    dim(50)
      *
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *---------------------------------------------------------------
      * Standard workstation information data structure
      *---------------------------------------------------------------
      *    externally defined as UTWSFR (record format: UTIDFRR)
     D iofeed        e ds                  extname(utwsfr)
      *
      *
      *---------------------------------------------------------------
      * Standard database file information data structure
      *---------------------------------------------------------------
      *    externally defined as UTDBGR (record format: FDBCKD)
     D dbfeed        e ds                  extname(utdbfr)
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /eject
      ****************************************************************
      * MAINLINE
      ****************************************************************
      *
     C                   eval      routne = set1
      *
     C     *inlr         doweq     *off                                         Main do loop
     C     routne        caseq     set1          $set1
     C     routne        caseq     scrn1         $scrn1
     C     routne        caseq     edit1         $edit1
     C     routne        caseq     update        $upd1
     C     routne        caseq     exit          $exit
     C                   endcs
     C                   enddo                                                  Main do loop
      /eject
      *----------------------------------------------------------------
      * Set environment for screen 1
      *----------------------------------------------------------------
      *
     C     $set1         begsr
      *
      * If incoming mode is REVISE or DELETE, then retrieve the database
      * record.  Do not lock the record.  Typically, in DELETE mode, these
      * values will not be displayed. However, if there is an error on the
      * delete action, the panel will be displayed with messages.
      *
     C                   select
     C                   when      mode = create                                WH create
     C                   z-add     0             d1fipc
      *
     C                   when      mode = revise or mode = delete               WH revise/delete
     C     svwedt        chain(n)  tfp053                             92
     C                   if        *in92 = *on                                  If no hit
     C                   seton                                        62
     C                   if        error < maxmsg
     C                   add       1             error
     C                   eval      mgid(e) = 'HS09008'
     C                   endif
     C                   else
      *
     C                   z-add     inyr          d1yr
     C                   z-add     inwk          d1wk
     C                   z-add     infipc        d1fipc
      *
      * Flip Effective Date to MMDDYY format
      *
     C     *iso          test(d)                 inefdt                 92
     C                   if        *in92 = *off                                 If OK date
     C                   move      inefdt        wkisodate
     C     *mdy          move      wkisodate     d1efmdy
     C                   else
     C                   z-add     0             d1efmdy
     C                   endif                                                  If OK date
     C                   endif                                                  If no hit
     C                   endsl
      *
      * If the user is deleting and there were no errors when retrieving the
      * record, go on to the update routine without displaying the screen.
      * Otherwise, display the screen.
      *
     C                   if        error = 0 and action = delete                If no error
     C                   eval      routne = update
     C                   else
     C                   eval      routne = scrn1
     C                   endif                                                  If no error
      *
     C                   endsr
      /eject
      *----------------------------------------------------------------
      * Perform operator I/O for screen 1
      *----------------------------------------------------------------
      *
     C     $scrn1        begsr
      *
      * Write the error messages from the error arrays to the
      * error message subfile
      *
     C                   exsr      $wrmsg
      *
      * Set indicators to control input/output fields and available
      * function keys.
     C                   exsr      $ind
      *
      *
      * Write screen 1 to CRT
      *
     C                   write     tf153k1
     C                   write     tf153ec
     C                   exfmt     tf153r1
      *
      * Clear messages
     C                   exsr      $clmsg
      *
      *
      * Reset action to be equal to the incoming mode.  This is required
      * in case the user took F11-Delete and got an error on the
      * deletion and now needs to be able to change the record and take
      * an action other than delete
      *
     C                   eval      action = mode
      *
      * Get user's input and set routine
      *
     C                   select
     C                   when      *in03 = *on                                  F3-exit
     C                   eval      rtncd = '03'
     C                   eval      routne = exit
      *
     C                   when      *in05 = *on                                  F5-refresh
     C                   eval      routne = set1
      *
     C                   when      *in11 = *on or mode = delete                 F11-delete
     C                   eval      action = delete
     C                   eval      routne = update
     C                   other
      *
      * User has pressed enter
      *
     C                   eval      routne = edit1
     C                   endsl
      *
     C                   endsr
      /eject
      *----------------------------------------------------------------
      * Set indicators to control/protect screen fields
      *----------------------------------------------------------------
      *
     C     $ind          begsr
      *
     C                   select
     C                   when      mode = revise
     C                   seton                                        60
      *
     C                   when      mode = create
     C                   seton                                        61
      *
     C                   when      mode = delete
     C                   seton                                        62
     c                   endsl
      *
     C                   endsr
      /eject
      *----------------------------------------------------------------
      * Edit input fields for screen 1
      *----------------------------------------------------------------
      *
     C     $edit1        begsr
      *
      * *IN62 will be on if the record the user is trying to access has
      * been deleted by another user.  So, only perform the edits for the
      * fields on the screen if the indicator is off
      *
     C                   if        *in62 = *off                                 If here
      *
     C     'INVENTORY '  chain     tfp099                             92
     C                   if        *in92 = *off                                 If hit
      *
      * In Create Mode, validate:
      *   1) Effective date
      *
     C                   if        mode = create
     C                   exsr      $efmdy
     C                   endif
      *
      * Always validate:
      *   1) freezer interest rate (%)
      *   2) Last check to see if Inventory Clse is running
      *
     C                   exsr      $fipc
     C                   exsr      $last
     C                   endif                                                  If hit
     C                   endif                                                  If here
      *
      * If there are no errors, attempt the write/update; otherwise,
      * redisplay the screen.  If a record has been deleted out from
      * under the user (*IN62 is ON), then the error message will be
      * issued in the update subroutine.  So, always perform this code.
      *
     C                   if        error = 0
     C                   exsr      $upd1
     C                   else
     C                   eval      routne = scrn1
     C                   endif
      *
     C                   endsr
      /eject
      *------------------------------------------------------------------------------------
      * Validate Effective Date (in CREATE mode only)
      *------------------------------------------------------------------------------------
      *
      * Validations:
      *  1) required
      *  2) must be a valid date
      *  3) must be on/after the Week-beginning Date of the Current "Inventory" week.
      *  4) a record cannot already exist with this date
      *
     C     $efmdy        begsr
      *
     C                   z-add     0             d1yr
     C                   z-add     0             d1wk
     C                   z-add     0             svwedt
      *
     C                   select
     C                   when      d1efmdy = 0
     C                   seton                                        30
     C                   if        error < maxmsg
     C                   add       1             error
     C                   eval      mgid(e) = 'HS09001'
     C                   endif
      *
     C                   when      d1efmdy < 0
     C                   seton                                        30
     C                   if        error < maxmsg
     C                   add       1             error
     C                   eval      mgid(e) = 'HS09000'
     C                   endif
     C                   other
      *
     C     *mdy          test(d)                 d1efmdy                92
     C                   if        *in92 = *on                                  If bad date
     C                   seton                                        30
     C                   if        error < maxmsg
     C                   add       1             error
     C                   eval      mgid(e) = 'HS09004'
     C                   endif
     C                   else
      *
      * Flip the date from MMDDYY to CCYYMMDD and continue editing.
      *
     C     *mdy          move      d1efmdy       wkisodate
     C                   move      wkisodate     wkdt
      *
      * Date must be on/after the Beginning Date of the Current Week.
      *
     C                   if        wkdt < svcbdt                                If prior
     C                   seton                                        30
     C                   if        error < maxmsg
     C                   add       1             error
     C                   eval      mgid(e) = 'HS08123'
     C                   endif
     C                   else
      *
      * Retrieve Saturday week-ending date associated with the date entered.
      *
     C                   call      'TF811'
     C                   parm      wkdt          xxdt
     C                   parm      0             xxsatdt
     C     d1yr          parm      0             xxyr
     C                   parm      0             xxpe
     C     d1wk          parm      0             xxwk
      *
      * Get returned 'week-ending date' into MMDDYY format.
      *
     C     *iso          test(d)                 xxsatdt                92
     C                   if        *in92 = *on                                  If bad Saturday
     C                   seton                                        30
     C                   if        error < maxmsg
     C                   add       1             error
     C                   eval      mgid(e) = 'HS09229'
     C                   endif
     C                   else
      *
      * Plop this Saturday date over the users date (We want our
      * "Effective Date" to be a week-ending date.
      *
     C                   z-add     xxsatdt       svwedt
     C                   move      xxsatdt       wkisodate
     C     *mdy          move      wkisodate     d1efmdy
     C                   endif                                                  If bad Saturday
     C                   endif                                                  If prior
     C                   endif                                                  If bad date
     C                   endsl
      *
      * If the date is OK, check that no record already exists for this date.
      *
     C                   if        *in30 = *off                                 If ok so far
     C     svwedt        chain(n)  tfp053                             92
     C                   if        *in92 = *off                                 If already
     C                   seton                                        30
     C                   if        error < maxmsg
     C                   add       1             error
     C                   eval      mgid(e) = 'HS08124'
     C                   endif
     C                   endif                                                  If already
     C                   endif                                                  If ok so far
      *
     C                   endsr
      /eject
      *-------------------------------------------------------------------------------------
      * Freezer Interest Rate (%)
      *-------------------------------------------------------------------------------------
      *
      * Validations:
      *  1) cannot be negative
      *  2) in reality, it probably cannot be zero. BUT, since we already have freezer
      *     records without this new field/logic AND to make it easy for them to do-away-
      *     with-this later, we will allow zero until someone squawks
      *
     C     $fipc         begsr
      *
     C                   select
     C                   when      d1fipc = 0
      *
     C                   when      d1fipc < 0
     C                   seton                                        33
     C                   if        error < maxmsg
     C                   add       1             error
     C                   eval      mgid(e) = 'HS09000'
     C                   endif
     C                   endsl
      *
     C                   endsr
      /eject
      *-------------------------------------------------------------------------------------
      * Make 1 final check
      *-------------------------------------------------------------------------------------
      *
      * We don't want users changing a record if a "Final" Inventory Close is
      * running that will be using that record.
      *
     C     $last         begsr
      *
     C                   if        fnsbfl = yes and
     C                             fnpfcd = 'F' and
     C                             svwedt <= fncedt and
     C                             error < maxmsg
     C                   add       1             error
     C                   eval      mgid(e) = 'HS08122'
     C                   endif
      *
     C                   endsr
      /eject
      *----------------------------------------------------------------
      * Write/update/delete record in database file
      *----------------------------------------------------------------
      *
     C     $upd1         begsr
      *
      * We do not have to perform deletion integrity checks because we do not even
      * let the user into this program if they cannot delete.
      *
     C                   if        action = delete
     C                   endif
      *
      * If user action is revising or deleting, determine that the
      * record he is accessing still exists prior to attempting to
      * update.  Lock the record for update.
      *
     C                   if        error = 0 and                                If OK
     C                             (action = delete or action = revise)
     C     svwedt        chain     tfp053                             92
     C                   if        *in92 = *on                                  If not found
     C                   seton                                        62
     C                   if        error < maxmsg
     C                   add       1             error
     C                   eval      mgid(e) = 'HS09008'
     C                   endif
     C                   endif                                                  If not found
     C                   endif                                                  If OK
      *
      *
      * If there are no errors, delete, revise or create the record.
      *
     C                   if        error = 0                                    If no error
      *
     c                   select
     C                   when      action = delete
     C                   delete    inrec
     C                   other
      *
      * Move DSPF fields to database file fields
      *
     C                   z-add     svwedt        inefdt
     C                   z-add     d1yr          inyr
     C                   z-add     d1wk          inwk
     C                   z-add     d1fipc        infipc
      *
     C                   if        action = create
     C                   write     inrec
     C                   else
     C                   update    inrec
     C                   endif                                                  If create
     C                   endsl
     C                   endif                                                  If no error
      *
      * Always redisplay when:
      *     1) there are errors
      *
     C                   select
     C                   when      error > 0
     C                   movel     scrn1         routne
     C                   other
      *
     C                   eval      rtncd = '00'
     C                   movel     exit          routne
     C                   endsl
      *
     C                   endsr
      /eject
      *-------------------------------------------------------------------------------------
      * Write error messages
      *-------------------------------------------------------------------------------------
      *
     C     $wrmsg        begsr
      *
     C                   call      'UT80045J'                           43
     C                   parm                    mgid
     C                   parm                    mgdt
     C                   parm                    error
     C                   parm      sdpgm         pgm
     C                   parm      msgfil        msgfl
      *
      * If call to UT80045J failed, a message hardcoded in the DSPF
      * command line format will be set on. So, redisplay screen.
      *
     C                   if        *in43 = *on
     C                   eval      routne = set1
     C                   endif
      *
     C                   endsr
      *
      *----------------------------------------------------------------
      * Clear messages
      *----------------------------------------------------------------
      *
     C     $clmsg        begsr
      *
     C                   call      'UT80045J'                           43
     C                   parm      *blank        mgi
     C                   parm      *blank        mgd
     C                   parm      *zero         error
     C                   parm      sdpgm         pgm
     C                   parm      msgfil        msgfl
      *
      * If call to UT80045J failed, a message hardcoded in the DSPF
      * command line format will be set on; so, redisplay screen.
      *
     C                   if        *in43 = *on
     C                   eval      routne = set1
     C                   endif
      *
     C                   endsr
      /eject
      *----------------------------------------------------------------
      * Set on last record indicator and end job
      *----------------------------------------------------------------
      *
     C     $exit         begsr
     C                   seton                                        lr
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Parm Lists
      *
     C     *entry        plist
     C     d1mode        parm                    mode
     C     svwedt        parm                    xxwedt
     C                   parm                    rtncd
      *
     C                   movel     mode          action
      *
      * Key lists
      *  None
      *
      *
      * Retrieve the Inventory record from the Control file.
      *
     C     'INVENTORY '  chain     tfp099                             92
     C                   if        *in92 = *off                                 If hit
     C                   z-add     fncbdt        svcbdt
      *
      * If in Create mode,
      *   1) Default the Current End Date from the Function Control "Inventory"
      *      record into the Effective Date.
      *
     C                   if        mode = create                                If create
     C                   z-add     fncedt        svwedt
     C                   z-add     fncemdy       d1efmdy
     C                   z-add     fncyr         d1yr
     C                   z-add     fncwk         d1wk
     C                   endif                                                  If create
     C                   endif                                                  If hit
      *
      *
      * The following standard code must be included to make the
      * standard error message handling program function properly.
      * This code sets and clears the program message queue.
      *
     C                   movel     '*'           msgpgm
     C                   exsr      $clmsg
      *
     C                   endsr
      /eject
