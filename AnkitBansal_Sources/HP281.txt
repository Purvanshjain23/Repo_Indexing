      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Hog Production System
      * PROGRAM:     Safedata: Write Reported Daily Activity Receiving File to Editing File
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     10/22/02
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 08/23/05  LeAnne Fedor
      *           Increased record length from 34 to 41 characters to accommodate 1 new
      *           field: Man Hours
      *
      * 05/14/09  LeAnne Ramsey
      *           Per Sami Wilson, we will no longer capture Projected Destroyed Head.
      *           We will not change the length of the file we receive from Safedata;
      *           they will simply send zeros in the positions.
      *
      * 04/06/10  LeAnne Ramsey
      *           We did not have any delete logic over the editing file; it was
      *           getting very large. So, added delete logic over HSP262.
      /eject
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fhsp281    if   e           k disk
      *  Safedata: Reported daily activity receiving file
      *
      *
     Fhsp282    uf a e           k disk
      *  Safedata: Reported daily activity editing file
      *
      /eject
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      ****************************************************************
      * ARRAYS AND TABLES
      ****************************************************************
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      *
      * Workfields for date manipulation
      *
     D wkdldt          s              8  0
     D wkccyymmdd      s              8  0
     D wkcymdiso       s               D   datfmt(*iso)
     D wkuldt          s                   like(u2uldt)
     D wkultm          s                   like(u2ultm)
      *
      * Workfields
      *
     D wknum70         s              7  0
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *
      *---------------------------------------------------------------
      * Break out single-field input record into individual fields
      *---------------------------------------------------------------
      *
     D inputrec        ds
     D   allfields                   41
      *
     D   xxfscd                       5    overlay(allfields:1)
     D   xxdydt                       8    overlay(allfields:6)
     D   xxddhd                       5    overlay(allfields:14)
     D   xxdshd                       5    overlay(allfields:19)
      ** xxpdshd Projected Destroyed  5    overlay(allfields:24) ** removed 05/14/09
     D   xxmanhr                      7    overlay(allfields:29)
      *
     D   xxsdino                      6    overlay(allfields:36)
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * MAINLINE
      ****************************************************************
      *
      * Read each record in the Receiving file and extract fields for the
      * record you will write to the editing file.
      *
      *
     C                   dou       *in90 = *on                                  Main do
     C                   read      hsp281                                 90
     C                   if        *in90 = *off                                 If not EOF
      *
     C                   move(p)   u1field       inputrec
      *
      * Test all fields for numerics; set non-numeric fields to 0.
      *
     C                   testn                   xxfscd               92
     C                   if        *in92 = *on                                  If numeric
     C                   move      xxfscd        u2fscd
     C                   else
     C                   z-add     0             u2fscd
     C                   endif                                                  If numeric
      *
      *
     C                   z-add     0             u2dydt
     C                   testn                   xxdydt               92
     C                   if        *in92 = *on                                  If numeric
     C                   movel     xxdydt        wkccyymmdd
     C     *iso          test(d)                 wkccyymmdd             92
     C                   if        *in92 = *off                                 If good date
     C     *iso          movel     wkccyymmdd    wkcymdiso
     C                   movel     wkcymdiso     u2dydt
     C                   endif                                                  If good date
     C                   endif                                                  If numeric
      *
      * Dead head
      *
     C                   testn                   xxddhd               92
     C                   if        *in92 = *on                                  If numeric
     C                   move      xxddhd        u2ddhd
     C                   else
     C                   z-add     0             u2ddhd
     C                   endif                                                  If numeric
      *
      * Destroyed head
      *
     C                   testn                   xxdshd               92
     C                   if        *in92 = *on                                  If numeric
     C                   move      xxdshd        u2dshd
     C                   else
     C                   z-add     0             u2dshd
     C                   endif                                                  If numeric
      *
      * Get the 'implied decimals' into Man Hours
      *
     C                   testn                   xxmanhr              92
     C                   if        *in92 = *on
     C                   move      xxmanhr       wknum70
     C     wknum70       mult      .01           u2manhr
     C                   else
     C                   z-add     0             u2manhr
     C                   endif
      *
      * Put 'system date/time' into the 'create date/time' fields
      *
     C                   z-add     wkuldt        u2uldt
     C                   z-add     wkultm        u2ultm
      *
      *
      * Always write a record to the editing file even if you have zero head
      * in ALL 3 head fields.
      *
     C                   write     u2rec
      *
     C                   clear                   u2rec
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Main do
      *
     C                   seton                                        lr
      /eject
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Put system date/time into work fields to use as 'upload date/time'.
      *
     C     *mdy          movel     udate         wkcymdiso
     C                   movel     wkcymdiso     wkuldt
     C                   time                    wkultm
      *
      *
      * Calc a 'delete' date that is 180 days prior to System Date. Then, delete
      * records older than this date from the Editing file.
      *
     C     wkcymdiso     subdur    180:*days     wkcymdiso
     C                   move      wkcymdiso     wkdldt
      *
     C     *loval        setll     hsp282
     C                   dou       *in91 = *on or u2dydt > wkdldt               Do deaths
     C                   read      hsp282                                 91
     C                   if        *in91 = *off
     C                   delete    u2rec
     C                   endif
     C                   enddo                                                  Do deaths
      *
     C                   clear                   u2rec
      *
     C                   endsr
      /eject
