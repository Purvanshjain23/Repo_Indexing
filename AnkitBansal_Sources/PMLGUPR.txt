     H/TITLE EUP Bld Prod Rebate Dtl   Execute user program
     H DATFMT(*YMD) DATEDIT(*YMD) DEBUG(*YES)
     Y* ADDLIBLE LIB(DFDATA) POSITION(*LAST)
     Y* ADDLIBLE LIB(COMMON) POSITION(*LAST)
     Y* ADDLIBLE LIB(DLY029) POSITION(*LAST)
     Z* DFTACTGRP(*NO) BNDDIR(YBNDDIR) DBGVIEW(*SOURCE)
     Z* CVTOPT(*DATETIME) ACTGRP(*CALLER) OPTIMIZE(*BASIC)
      *
     FREBATEBG  if   e           k disk
      *  JRD Product Rebate Masterfile
      *
     FITMCPKL1  if   e           k disk
      *  Daily's CoPack Item/Seaboard Item Cross Ref
      *
     FPPAHREL1  uf a e           k disk
      *  Accrual Code Detail       Retrieval index
      *
     FJRCUSTS   if   e           k disk
      *  JRD Customer Masterfile AS/400 Format
      *
     FCSTMSTL1  if   e           k disk
      *  Active Customer Conversion - By DLYCST
      *
     FOMHOREL1  uf a e           k disk
      *  Customer Accrual          Update index
      *
      * Data structures:
     D PGMDS         ESDS                  EXTNAME(Y2PGDSP)
      * Modified Program data structure
     D JBDTTM          DS
      * Job date/time
     D  ##JDT                  1      7  0
     D  ##JCC                  1      1  0
     D  ##JYY                  2      3  0
     D  ##JMM                  4      5  0
     D  ##JDD                  6      7  0
     D  ##JTM                  8     13  0
     D  ##JHH                  8      9  0
     D  ##JNN                 10     11  0
     D  ##JSS                 12     13  0
      *
      * Standalone fields
     Dcontinue         s              1A
      *
      *****************************************************************
      * Entry parameters
     C     *ENTRY        PLIST
     C                   PARM                    p1ojcd            3            Accrual Code
     C                   PARM                    p1ttst            1            All Products (Y/N)
     C                   PARM                    p1bkpr            9 6          Accrual Rate
     C                   PARM                    p1shsx            2            Accrual Rate Type
     C                   PARM                    p1qfsx            1            Accrual Detail Searc
      /EJECT
      *****************************************************************
      * Initialize
     C                   EXSR      ZZINIT
      *
      * Build Accrual Code Detail
     C                   exsr      $acrdtl
      *
      * Build Customer Accrual for Product Rebate
     C                   exsr      $cstacr
      *
      *----------------------------------------------------------------
      * Exit program
      * Terminate program
     C                   SETON                                        LR
      *
      * Exit program
     C                   RETURN
      *
      *================================================================
      /EJECT
      *---------------------------------------------------------------
      * Build Accrual Code Detail
      *---------------------------------------------------------------
      *
     C     $acrdtl       begsr
      *
     C     *loval        setll     rebateb
     C                   read      rebateb                                95
     C                   dow       *in95 = *off                                 do loop
      *
      * Get Daily's CoPack Item/Seaboard Item Cross Ref
     C                   exsr      $itmcpk
      *
      * Populate rest of fields
     C                   eval      ahojcd = p1ojcd                              Accrual Code
      * Item Code filled in $itmcpk
     C                   eval      ahshsx = 'W'                                 Accrual Rate Type
     C                   eval      ahbkpr = rebate                              Accrual Rate
     C                   eval      ahvsst = 'A'                                 Record Status
     C                   eval      ahmjdt = ##JDT                               Create Date
     C                   eval      ahbetm = ##JTM                               Create Time
     C                   eval      ahccvn = ##USR                               Create User
     C                   eval      ahcdvn = ##PGM                               Create Program
     C                   eval      ahmkdt = *zeros                              Change Date
     C                   eval      ahbftm = *zeros                              Change Time
     C                   eval      ahcevn = *blanks                             Change User
     C                   eval      ahcfvn = *blanks                             Change Program
      *
      * Check for duplicate record in Accrual Code Detail
     C     key01         setll     @AHREUR                                90
      *
      * If record not found, add
     C                   if        *in90 = *off
      *
     C                   write     @AHREUR
     C                   endif
      *
      * Next record
     C                   read      rebateb                                95
     C                   enddo                                                  do loop
      *
     C                   endsr
     C/EJECT
      *---------------------------------------------------------------
      * Get Daily's CoPack Item/Seaboard Item Cross Ref
      *---------------------------------------------------------------
      *
     C     $itmcpk       begsr
      *
     C                   eval      dlyprd = prod                                Dailys Item
     C     dlyprd        chain     ITMCPK                             91
      *
      * If record found use SBDPRD, else use PROD from REBATEB file
     C                   if        *in91 = *off                                 91 = off
     C                   eval      ahhgcd = sbdprd                              Item Code
     C                   else
     C                   eval      ahhgcd = prod                                Item Code
      *
     C                   endif                                                  91 = off
      *
     C                   endsr
     C/EJECT
      *---------------------------------------------------------------
      * Build Customer Accrual for Product Rebate
      *---------------------------------------------------------------
      *
     C     $cstacr       begsr
      *
      * Process JRCUSTS - JRD Customer Masterfile AS/400 Format
     C     *loval        setll     jrcust
     C                   read      jrcust                                 95
     C                   dow       *in95 = *off                                 do loop
      * Determine if JRCUSTS record should be processed
     C                   exsr      $cstmst
      *
      * Continue processing record?
     C                   if        continue = 'Y'                               continue = Y
      *
      * Create Customer Accrual
     C                   exsr      $cusacc
      *
      * Create Customer Accrual Items
     C                   call      'PMK0XFR'
     C                   parm                    w0rtn             7            Return code
     C                   parm                    hobkc7                         Ship To Customer
     C                   parm                    hoojcd                         Accrual Code
     C                   parm                    hoguny                         CAH Sequence No
      *
     C                   endif                                                  continue = Y
      * Next record
     C                   read      jrcust                                 95
     C                   enddo                                                  do loop
      *
     C                   endsr
     C/EJECT
      *---------------------------------------------------------------
      * Get Ship to Customer
      *---------------------------------------------------------------
      *
     C     $cstmst       begsr
      *
      * Set default values for flags
     C                   eval      continue = 'N'
      *
     C     cust          chain     cstmst                             91
      *
      * If record found
     C                   if        *in91 = *off                                 91 = off
     C                   eval      hobkc7 = shipto                              Ship to Customer
     C                   eval      continue = 'Y'
      *
     C                   endif                                                  91 = off
      *
     C                   endsr
     C/EJECT
      *---------------------------------------------------------------
      * Create Customer Accrual
      *---------------------------------------------------------------
      *
     C     $cusacc       begsr
      *
      * Populate rest of fields
     C                   eval      hobkc7 = shipto                              Ship To Customer
     C                   eval      hoojcd = p1ojcd                              Accrual Code
     C                   eval      hoguny = 1                                   CAH Sequence No
     C                   eval      hobfdt = ##JDT                               Period Beginning Dte
     C                   eval      hoa2dt = 1991231                             Period Ending Date
     C                   eval      hobkpr = p1bkpr                              Accrual Rate
     C                   eval      hottst = p1ttst                              All Products (Y/N)
     C                   eval      hoshsx = p1shsx                              Accrual Rate Type
     C                   eval      hoqdsx = *blanks                             Comm Threshold Prd
     C                   eval      hopjdt = *zeros                              CAH Unused Date 1
     C                   eval      hopkdt = *zeros                              CAH Unused Date 2
     C                   eval      hoecvl = *zeros                              Comm Min Amt /Period
     C                   eval      hoqesx = *blanks                             Comm Retroactive sts
     C                   eval      hoqfsx = p1qfsx                              Accrual Detail Searc
     C                   eval      hohhaa = 'UNIPRO'                            Broker Code
     C                   eval      hohqvl = *zeros                              Max Accrual Amount
     C                   eval      hoqgsx = '1'                                 Commission/Not Comm
     C                   eval      hoqhsx = *blanks                             CAH Unused sts 3
     C                   eval      hoqisx = *blanks                             CAH Unused sts 4
     C                   eval      hoqjsx = *blanks                             CAH Unused sts 5
     C                   eval      houvst = *blanks                             EDI status 1
     C                   eval      houwst = *blanks                             EDI status 2
     C                   eval      houxst = *blanks                             EDI status 3
     C                   eval      hoaatm = ##JTM                               Job Time
     C                   eval      hoayna = ##USR                               User Id
     C                   eval      hoa0na = ##JOB                               Job Name
     C                   eval      hoaxdt = ##JDT                               Job Date
      *
      * Check for duplicate record in Customer Accrual
     C     key02         setll     @HORELB                                90
      * If record not found, add
     C                   if        *in90 = *off
     C                   write     @HORELB
      *
     C                   endif
      *
     C                   endsr
      /EJECT
     CSR   ZZINIT        BEGSR
      *================================================================
      * Initialisation
      *================================================================
      * Retrieve job attributes
     C                   CALL      'Y2RTJCR'
     C                   PARM                    PGMDS
      * Setup job date/time
     C                   Z-ADD     ##SD7         ##JDT
     C                   TIME                    ##JTM
      *
      * klist for Accrual Code Detail       Retrieval index
     C     KEY01         klist
     C                   kfld                    ahojcd
     C                   kfld                    ahhgcd
      *
      * klist for Customer Accrual          Retrieval index
     C     KEY02         klist
     C                   kfld                    hobkc7
     C                   kfld                    hoojcd
     C                   kfld                    hoguny
      *
     C                   eval        w0rtn  = *blanks
      *================================================================
     CSR   ZZEXIT        ENDSR
