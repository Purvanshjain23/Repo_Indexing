     A*%%TS  SD  20021106  131359  ISLRAMS     REL-V5R1M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION
     A*  DISPLAY FILE:  HPD4431
     A*  TITLE:         SPECIFY OPTIONS: REPORTED DAILY ACTIVITY-NO DATA
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  DATE:          11/06/02
     A*
     A*  NOTE:  THIS IS A POP-UP WINDOW. THE WINDOW DEFINITION IS IN
     A*         RECORD FORMAT HP500EC
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CF03(03 'EXIT')
     A                                      CF08(08 'SUBMIT')
     A*
     A*----------------------------------------------------------------
     A* RECORD FORMAT ONE
     A*----------------------------------------------------------------
     A          R HP4431R1
     A*%%TS  SD  20021106  131359  ISLRAMS     REL-V5R1M0  5722-WDS
     A                                      WINDOW(HP4431EC)
     A                                      TEXT('INPUT SCREEN')
     A                                      SETOF(32)
     A                                      SETOF(34)
     A                                      OVERLAY
     A            SDPGM     R        O  1  1REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 13'SPECIFY OPTIONS: REPORTED DAILY AC-
     A                                      TIVITY-NO DATA REPORT'
     A                                      DSPATR(HI)
     A                                  3  1'Daily date ..'
     A            D1MDY     R        B  3 15REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  ')
     A  32                                  DSPATR(PC)
     A  32                                  DSPATR(RI)
     A N32                                  DSPATR(HI)
     A                                  4  1'Cell ........'
     A            D1CELL    R        B  4 15REFFLD(HSRREF/@@CELL *LIBL/HSPREF)
     A                                      EDTCDE(M)
     A  34                                  DSPATR(PC)
     A  34                                  DSPATR(RI)
     A N34                                  DSPATR(HI)
     A                                  4 30'(Blank for All)'
     A*
     A*----------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*----------------------------------------------------------------
     A          R HP4431K1
     A*%%TS  SD  20000605  133618  ISLRAMS     REL-V4R2M0  5769-PW1
     A                                      WINDOW(HP4431EC)
     A                                      TEXT('COMMAND KEYS')
     A                                      OVERLAY
     A  43                              7  2'Message program failed. Call IS.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                  8  2'F3=Exit'
     A                                  8 22'F8=Submit'
      *
      *----------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *----------------------------------------------------------------
     A          R HP4431ES
     A                                      TEXT('Error message subfile')
     A                                      SFL SFLMSGRCD(09)
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
     A*
     A*----------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT WITH WINDOW DEFINITION
     A*----------------------------------------------------------------
     A          R HP4431EC                  SFLCTL(HP4431ES)
     A*%%TS  SD  20000607  150524  ISLRAMS     REL-V4R2M0  5769-PW1
     A                                      TEXT('Error message subfile')
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A                                      WINDOW(*DFT 10 70)
     A                                      WDWBORDER((*DSPATR RI) (*CHAR '    -
     A                                          '))
     A            MSGPGM                    SFLPGMQ(10)
      *
      *----------------------------------------------------------------
      * ASSUME FORMAT - REQUIRED FOR WINDOWS
      *----------------------------------------------------------------
      *
     A          R ASSUME
     A                                      TEXT('ASSUME FORMAT FOR WINDOWS')
     A                                      ASSUME OVERLAY
     A                                 24 79' '
