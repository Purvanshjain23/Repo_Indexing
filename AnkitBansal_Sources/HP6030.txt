      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Hog Production
      * PROGRAM:     HP6030 - Reported Daily Activity Listing
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     10/22/02
      *
      * FUNCTION:    Prints the data from the new Reported Daily Activity file
      *
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 08/24/05  LeAnne Fedor
      *           New field "Man Hours" was added to Reported Daily Activity
      *           file. Added new field to listing.
      *
      * 10/19/07  LeAnne Ramsey
      *           Recompile only. The users are switching "manhours" from being
      *           "total" manhours for the day to being the "average" manhours
      *           for the day. So, added the word "average" to the listing.
      *
      * 09/19/08  LeAnne Ramsey
      *           Recompile only.
      *           The users are switching "average manhours" back to "total manhours".
      *           So, removed the word "average" from the screen text.
      *
      * 05/15/09  LeAnne Ramsey
      *           Per Sami Wilson, removed Projected Destroyed Head.
      *
      * 10/16/13  LeAnne Ramsey (E2831)
      *           Recompile only. Added field 'MTech Reference'.
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fhsl018j   if   e           k disk
      *    Farm site (records selected in open query)
      *
      *
     Fhsp103    if   e           k disk
      *    Reported daily activity
      *    (records selected by open query)
      *
     Fqprint    o    f  132        printer oflind(*inof)
      *
      /eject
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      *
      ****************************************************************
      * ARRAYS AND TABLES
      ****************************************************************
      *
     D arddhd          s              9  0 dim(1)
     D ardshd          s              9  0 dim(1)
     D armanhr         s             11  2 dim(1)
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Standard fields
      *
     D dash            s            132    inz(*all'-')
     d rtime           s              6  0
      *
      *
      * Control fields
      *
     D first           s              1    inz('Y')
     D svcell          s                   like(fscell)
     D svfscd          s                   like(fsfscd)
     D farmlines       s              5  0
      *
      *
      * Workfields for date manipulation
      *
     D wkisodate       s               D   datfmt(*iso)
      *
      *
      * Array index
      *
     d x               s              1  0
      *
      *
      * Print fields
      *
     d l1mdy           s              6  0
      *
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *---------------------------------------------------------------
      * Local data area.
      *---------------------------------------------------------------
      *
     Dlda             uds                  dtaara(*lda)
     D  ldfsbo                 1      5
     D  ldfcymd                6     13  0
     D  ldtcymd               14     21  0
     D  ldfscd                22     26  0
     D  ldfsnm                27     51
      *
     D  ldfmdy                52     57  0
     D  ldtmdy                58     63  0
     D  ldcell                64     65  0
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /eject
      ****************************************************************
      * Mainline
      ****************************************************************
      *
      * Print heading first time thru
      *
     C                   exsr      $hrhdr
      *
      * Process all Farms selected by the open query
      *
     C     *loval        setll     hsl018j                                90
      *
     C                   dou       *in90 = *on                                  Main do
     C                   read      hsl018j                                90
     C                   if        *in90 = *off                                 If not EOF
      *
      * Read/print all the daily activity records for the farm
      *
     C                   exsr      $detail
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Main do
      *
      *---------------------------------------------------------------
      * EOF PROCESSING
      *---------------------------------------------------------------
      *
     C                   select
     C                   when      first = yes
     C                   except    nodata
     C                   other
     C                   exsr      $t1tot
     C                   endsl
      *
     C                   seton                                        lr
      /eject
      *-------------------------------------------------------------------------------------
      * Process all records for the farm in Reported Daily Activity file
      *-------------------------------------------------------------------------------------
      *
     C     $detail       begsr
      *
     C     fsfscd        setll     hsp103
     C                   seton                                        87
      *
     C                   dou       *in91 = *on                                  Do detail
     C     fsfscd        reade     hsp103                                 91
     C                   if        *in91 = *off                                 If not EOF
      *
      * Control break logic
      *
     C                   select
     C                   when      first = yes
     C                   z-add     fscell        svcell
     C                   z-add     fsfscd        svfscd
     C                   move      no            first
      *
     C                   when      fscell <> svcell
     C                   exsr      $t1tot
      *
     C                   when      fsfscd <> svfscd
     C                   exsr      $blank
     C                   endsl
      *
      * Flip daily date from CCYYMMDD to MMDDYY
      *
     C     *iso          test(d)                 rddydt                 92
     C                   if        *in92 = *off                                 If OK date
     C                   move      rddydt        wkisodate
     C     *mdy          move      wkisodate     l1mdy
     C                   endif                                                  If OK date
      *
      * Accumlate values into total arrays
      *
     C                   add       rdddhd        arddhd
     C                   add       rddshd        ardshd
     C                   add       rdmanhr       armanhr
      *
      * Print detail line
      *
     C                   if        *inof = *on
     C                   exsr      $hrhdr
     C                   endif
     C                   except    l1dtl
     C                   setoff                                       8788
     C                   add       1             farmlines
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Do detail
      *
     C                   endsr
      /eject
      *--------------------------------------------------------------------
      * Print a blank line after a farm that had more than one detail line
      *--------------------------------------------------------------------
      *
     C     $blank        begsr
      *
     C                   if        farmlines > 1
     C                   except    blank
     C                   endif
      *
     C                   z-add     fsfscd        svfscd
     C                   z-add     0             farmlines
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Print total lines
      *---------------------------------------------------------------
      *
     C     $t1tot        begsr
      *
     C                   z-add     1             x
      *
     C                   if        *inof = *on
     C                   exsr      $hrhdr
     C                   endif
     C                   except    t1tot
      *
     C                   z-add     fscell        svcell
     C                   z-add     fsfscd        svfscd
     C                   z-add     0             farmlines
      *
     C                   seton                                        88
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Print report headings
      *---------------------------------------------------------------
      *
     C     $hrhdr        begsr
      *
     C                   except    hrhdr
      *
      * Set on the indicators that control printing of the cell and farm number
      * on the detail line.
      *
     C                   seton                                        8788
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      *
      * Set up report headings
      *
      * Farm site
     C                   if        ldfscd = 0
     C                   seton                                        81
     C                   endif
      *
      * Cell
     C                   if        ldcell = 0
     C                   seton                                        82
     C                   endif
      *
     C                   time                    rtime
      *
     C                   endsr
      /eject
      *-------------------------------------------------------------
      * Report heading lines
      *-------------------------------------------------------------
      *
     Oqprint    e            hrhdr          1 01
     O                       sdpgm               10
     O                                           75 'HOG PRODUCTION SYSTEM'
     O                                          122 'DATE'
     O                       udate         y    132
      *
     O          e            hrhdr          1
     O                       sdusr               10
     O                                           73 'REPORTED DAILY ACTIVITY '
     O                                           80 'LISTING'
     O                                          122 'TIME'
     O                       rtime              132 '  :  :  '
      *
     O          e            hrhdr          1
     O                                          122 'PAGE'
     O                       page          z    132
      *
      *
     O          e            hrhdr       1  1
     O                                           19 'From daily date ...'
     O                       ldfmdy        y     29
      *
     O          e            hrhdr          1
     O                                           19 'To daily date .....'
     O                       ldtmdy        y     29
      *
      *
     O          e            hrhdr          1
     O                                           19 'Cell ..............'
     O              n82      ldcell        z     23
     O               82                          24 'ALL'
      *
      *
     O          e            hrhdr          2
     O                                           19 'Farm site .........'
     O              n81      ldfscd        m     27
     O               81                          24 'ALL'
     O                       ldfsnm              56
      *
      *-------------------------------------------------------------
      * Column headings
      *-------------------------------------------------------------
      *
     O          e            hrhdr          1
     O                                           11 'FARM'
     O                                           20 'DAILY'
     O                                           37 'DEAD'
     O                                           54 'DESTROYED'
      *
     O          e            hrhdr          1
     O                                            4 'CELL'
     O                                           11 'SITE'
     O                                           20 'DATE'
     O                                           37 'HEAD'
     O                                           54 'HEAD'
     O                                           74 'MANHOURS'
     O                                           88 'FARM NAME'
      *
     O          e            hrhdr          1
     O                       dash               132
      *
      *-------------------------------------------------------------
      * Detail line
      *-------------------------------------------------------------
      *
     O          e            l1dtl          1
     O               88      fscell        z      3
     O               87      rdfscd        z     11
     O                       l1mdy          b    22 '  /  /  '
     O                       rdddhd        k     38
     O                       rddshd        k     55
     O                       rdmanhr       k     75
     O               87      fsfsnm             104
      *
      *
      *-------------------------------------------------------------
      * Total line
      *-------------------------------------------------------------
      *
     O          e            t1tot       0
     O                                           37 '-----------'
     O                                           54 '-----------'
     O                                           74 '--------------'
      *
     O          e            t1tot       1  2
     O                                           19 'Total for Cell:'
     O                       svcell        z     22
     O                       arddhd (x)    kb    38
     O                       ardshd (x)    kb    55
     O                       armanhr(x)    kb    75
      *
      *-------------------------------------------------------------
      * Blank line
      *-------------------------------------------------------------
      *
     O          e            blank          1
      *
      *-------------------------------------------------------------
      * No data message line
      *-------------------------------------------------------------
      *
     O          e            nodata         1
     O                                           19 'NO DATA MEETS YOUR'
     O                                           38 'SELECTION CRITERIA'
