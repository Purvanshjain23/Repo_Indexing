/***************************************************************** */
/*                                                                 */
/*  ENVIRONMENT:     PORK DIVISION                                 */
/*  SYSTEM:          INTERFACE MP2 TO JDE                          */
/*  PROGRAM NUMBER:  MP6511CL                                      */
/*  PROGRAM NAME:    MP2: LIST OF OVERRIDDEN ITEMS                 */
/*                   (UNPOSTED INVOICES)                           */
/*  PROGRAMMER:      LEANNE RAMSEY                                 */
/*  CREATE DATE:     10/20/06                                      */
/*                                                                 */
/*  FUNCTION:        THIS CL IS SUBMITTED FROM THE FUNCTION THAT   */
/*                   POSTS INVOICES TO JDE. WE COULD NOT USE THE   */
/*                   EXISTING LOGIC BECAUSE IT WAS BASED ON THE    */
/*                   'JDE BATCH NUMBER'---AND THE USER WANTED      */
/*                   TO GET THIS LISTING DURING THE "EDIT" RUN     */
/*                   BEFORE THE "POST" RUN. THE JDE BATCH NUMBER   */
/*                   IS NOT ASSIGNED UNTIL THE 'POST' RUN.         */
/*                                                                 */
/*******************************************************************/
/* MODIFICATIONS:                                                  */
/*                                                                 */
/* 03/13/08  LEANNE RAMSEY                                         */
/*           REPLACE QPRINT WITH PRINT198.                         */
/*******************************************************************/

             PGM

             DCL        VAR(&QDATA) TYPE(*CHAR) LEN(65)

             DCL        VAR(&FLAG)  TYPE(*CHAR) LEN(1)
             DCL        VAR(&DATA)  TYPE(*CHAR) LEN(1)
             DCL        VAR(&BATNO) TYPE(*DEC) LEN(8)
             DCL        VAR(&FMDY) TYPE(*DEC) LEN(6)
             DCL        VAR(&TMDY) TYPE(*DEC) LEN(6)
             DCL        VAR(&JDVNNO) TYPE(*DEC) LEN(8)
             DCL        VAR(&JDVNNM) TYPE(*CHAR) LEN(40)

             DCL        VAR(&HOLD) TYPE(*CHAR) LEN(10)
             DCL        VAR(&COPYS) TYPE(*DEC) LEN(2)
             DCL        VAR(&OUTQ) TYPE(*CHAR) LEN(10)

             CHGVAR     VAR(&FLAG)   VALUE('E')
             CHGVAR     VAR(&DATA)   VALUE(' ')
             CHGVAR     VAR(&BATNO)  VALUE(0)
             CHGVAR     VAR(&FMDY)   VALUE(0)
             CHGVAR     VAR(&TMDY)   VALUE(0)
             CHGVAR     VAR(&JDVNNO) VALUE(0)
             CHGVAR     VAR(&JDVNNM) VALUE(' ')

/*-------------------------------------------------------------------*/
/* SELECT INVOICE DETAIL RECORDS THAT HAVE BEEN 'OVERRIDDEN'         */
/*-------------------------------------------------------------------*/

             CHGVAR     VAR(&QDATA) VALUE(' ')
             CHGVAR     VAR(%SST(&QDATA 1 14)) VALUE('IDOVFL +
                          *EQ "Y"')

             OVRDBF     FILE(MPP105)  SHARE(*YES)
             OPNQRYF    FILE((MPP105)) QRYSLT(&QDATA) KEYFLD(*FILE) +
                          SEQONLY(*YES)

/*------------------------------------ -----------------------------*/
/*  CREATE WORKFILE AND CALL PROGRAM TO POPULATE IT                 */
/*------------------------------------------------------------------*/

             CRTDUPOBJL OBJ(MPP351) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)

             CALL       PGM(MP3511)

             CLOF       OPNID(MPP105)
             DLTOVR     FILE(MPP105)

/*------------------------------------------------------------------*/
/* SET OVERRIDES FOR PRINT FILES                                    */
/*------------------------------------------------------------------*/

 HOLD:       IF         COND(%SST(*LDA 411 1) = 'Y') THEN(CHGVAR +
                          VAR(&HOLD) VALUE(*YES))
             ELSE       CMD(CHGVAR VAR(&HOLD) VALUE(*NO))

 COPYS:      CHGVAR     VAR(&COPYS) VALUE(%SST(*LDA 412 1))
             IF         COND(&COPYS = 0) THEN(CHGVAR VAR(&COPYS) +
                          VALUE(1))

 OUTQ:       CHGVAR     VAR(&OUTQ) VALUE(%SST(*LDA 413 10))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(%SST(*LDA 401 10)))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(*JOB))

/*------------------------------------------------------------------*/
/* OVERRIDE PRINT FILE                                              */
/*------------------------------------------------------------------*/

             OVRPRTF    FILE(PRINT198) OUTQ(&OUTQ) COPIES(&COPYS) +
                          HOLD(&HOLD)

/*------------------------------------------------------------------*/
/* CALL PRINT PROGRAM                                               */
/*------------------------------------------------------------------*/

             CALL       PGM(MP651) PARM(&FLAG &DATA &BATNO &FMDY +
                          &TMDY &JDVNNO &JDVNNM)

             DLTOVR     FILE(*ALL)

             ENDPGM

