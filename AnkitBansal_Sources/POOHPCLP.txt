/********************************************************************/
/*                                                                  */
/*  SYNOPSIS - PRINT FREIGHT COMPARISON REPPRT FROM HISTORY FILE.   */
/*                                                                  */
/*                                                                  */
/*                                                                  */
/*  COMPANY   - SEABOARD COPRORATION                                */
/*  SYSTEM    - ORDER MANAGEMENT                                    */
/*  USER NAME - PURVA DROGE (COPIED FROM PDMOCPCLP)                 */
/*  DATE      - 12/13/01                                            */
/*                                                                  */
/*      PARAMETERS RECEIVED:                                        */
/*                                                                  */
/*          &OUTQ       - OUTPUT QUEUE                              */
/*          &COPEIS     - NUMBER OF COPIES                          */
/*          &NIGHTQ     - NIGHT QUEUE                               */
/*          &HOLD       - HOLD PRINT FLAG                           */
/*          &SAVE       - SAVE PRINT FLAG                           */
/*          &PGM        - PROGRAM NAME                              */
/*          &COMPL      - COMPANY NUMBER                            */
/*          &FDATEL     - FROM DATE                                 */
/*          &TDATEL     - TO DATE                                   */
/*          &FLAG       - SELECT REPORT FLAG                        */
/*          &FLAG       - PAID FREIGHT FLAG                         */
/*          &CARRIER    - NOT USED NOW                              */
/*          &PCUST      - PARENT CUSTOMER                           */
/*          &ARCUST     - A/R CUSTOMER                              */
/*          &SCUST      - SHIPTO CUSTOMER                           */
/********************************************************************/
/********************************************************************/
/*  RMC 2/11/02 ADDED 2 PARMS FOR SELECTION: &EXCLPD =EXCLUDE CARRIER */
/*              PAID, &EXCLCT=EXCLUDE COLLECT TRUCKS                 */
/*  RMC 9/17/02 ADDED 1 PARM  FOR SELECTION: &DTLSUM=DETL OR SUMMARY  */
/*              BUT IT IS NOT USED IN THIS REPORT SELECTION         */
/*  SLM 1/11/04 Allow Company to be optional                         */
/*  RMC 11/15/06 ADDED LOAD PARM -- BUT NOT USED IN THIS PGM         */
/*  LJB 11/24/2009 CHANGED &EXCLPD TO &PAIDYN - E=EXCLUDE CARRIERS   */
/*      PAID IN FULL, I=INCLUDE ONLY PAID IN FULL CARRIERS,          */
/*      BLANKS = ALL                                                 */
/* LJB 12/17/2009 E000322  IF EMAIL IS REQUESTED, BUILD THE WORK    */
/*      FILE IN QTEMP. IF IT IS ALREADY THERE, CLEAR IT.            */
/*  RMC 08/14/13 E2719  DO FTP IF &DOC8 IS ENTERED                    */
/*  PIO 06/05/23 - REPLACE OPNQRYF WITH OPNSQLF FOR OMHSTPP FILE      */
/*                 ADDED NEEDED OVRDBF AROUND THE COMMAND             */
/*                 CHANGE *AND TO AND, *EQ TO =, *GE TO >=, *LE TO <=,*/
/*                 *GT TO > IN &G                                     */
/********************************************************************/
 PDMOPCLP:   PGM        PARM(&PGM &COMPL &FDATEL &TDATEL &FLAG1 +
                          &FLAG2 &CARRIER &PCUST &ARCUST &SCUST +
                          &PAIDYN &EXCLCT &DTLSUM &OUTQ &HOLD &SAVE +
                          &COPIESL &NIGHTQ &LOADL &DOC8 &EMAILADDR)

        DCL    &COMP      *DEC    (3 0)           /*  COMPANY #     */
        DCL    &COMPL     *DEC                    /*  FOR PASSING   */
        DCL    &FDATE     *DEC    (7 0)          /*  FROM DATE      */
        DCL    &FDATEL    *DEC                   /*  FOR PASSING    */
        DCL    &TDATE     *DEC    (7 0)          /*  THROUGH DATE   */
        DCL    &TDATEL    *DEC                   /*  FOR PASSING    */
        DCL    &PCST      *DEC    (7 0)          /*  PARENT CUSTOMER*/
        DCL    &PCUST     *DEC                   /*  FOR PASSING    */
        DCL    &ARCST     *DEC    (7 0)          /*  A/R CUSTOMER   */
        DCL    &ARCUST    *DEC                   /*  FOR PASSING    */
        DCL    &SCST      *DEC    (7 0)          /*  SHIPTO CUST    */
        DCL    &SCUST     *DEC                   /*  FOR PASSING    */
        DCL    &LOADL     *DEC                   /*  LOAD NOT USED  */
        DCL    &FLAG1     *CHAR   1               /* SELECT REPORT  */
        DCL    &FLAG2     *CHAR   1               /* PAID FREIGHT   */
        DCL    &PAIDYN     *CHAR   (1)           /* CARRIER PAID SELECTION    */
        DCL    &EXCLCT     *CHAR   (1)           /* EXCL CARRIER TRUCKS       */
        DCL    &DTLSUM     *CHAR   (1)           /* DETAIL/SUMMARY            */
        DCL    &CARRIER   *CHAR   3               /* NOT USED NOW   */
        DCL    &COPIES    *DEC    (3 0)           /*  # OF COPIES   */
        DCL    &COPIESL   *DEC                    /*  FOR PASSING   */
        DCL    &OUTQ      *CHAR   10              /*  OUTPUT QUEUE  */
        DCL    &NIGHTQ    *CHAR   10              /*  NIGHT JOB QUE.*/
        DCL    &HOLD       *CHAR   (4)           /* Hold Status               */
        DCL    &SAVE       *CHAR   (4)           /* Save Status               */
        DCL    &PGM       *CHAR   10              /*  PGM NAME      */
        DCL    &COMPA     *CHAR   3               /*  COMPANY ALPHA */
        DCL    &TDATEA    *CHAR   7               /*  TO   DT ALPHA */
        DCL    &FDATEA    *CHAR   7               /*  TO   DT ALPHA */
        DCL    &PCSTA     *CHAR   7               /*  PARCST  ALPHA */
        DCL    &ARCSTA    *CHAR   7               /*  ARCST   ALPHA */
        DCL    &SCSTA     *CHAR   7               /*  SHIPCST ALPHA */
        DCL    &DOC8      *CHAR   8               /*  FTP DOCUMENT  */
        DCL    &EMAILADDR *CHAR   50              /* EMAIL ADDRESS  */
        DCL    &COMMAND    *CHAR   (512)

/* SEND PROGRAM MESSAGE PARAMETERS  */

        DCL    &PGMO      *CHAR   10              /*  PRINTER FILE  */
        DCL     &A        *CHAR   20              /* QRY YTD DATE   */
        DCL     &B        *CHAR   25              /* QRY TO DATE    */
        DCL     &C        *CHAR   25              /* QRY COMPANY    */
        DCL     &D        *CHAR   25              /* QRY            */
        DCL     &E        *CHAR   25              /* QRY            */
        DCL     &F        *CHAR   25              /* QRY            */
        DCL     &G        *CHAR  145              /* QRY ALL        */

             CHGVAR    VAR(&COMP) VALUE(&COMPL) /* Change to length +
                          for print program */
             CHGVAR    VAR(&COMPA) VALUE(&COMP) /* Change to alpha +
                          for message data */
             CHGVAR    VAR(&FDATE) VALUE(&FDATEL) /* Change to length +
                          for print program */
             CHGVAR    VAR(&FDATEA) VALUE(&FDATE) /* Change to alpha +
                          for query data */
             CHGVAR    VAR(&TDATE) VALUE(&TDATEL) /* Change to length +
                          for print program */
             CHGVAR    VAR(&TDATEA) VALUE(&TDATE) /* Change to alpha +
                          for query data */
             CHGVAR    VAR(&PCST) VALUE(&PCUST) /* Change to length +
                          for print program */
             CHGVAR    VAR(&PCSTA) VALUE(&PCST) /* Change to alpha +
                          for query data */
             CHGVAR    VAR(&ARCST) VALUE(&ARCUST) /* Change to length +
                          for print program */
             CHGVAR    VAR(&ARCSTA) VALUE(&ARCST) /* Change to alpha +
                          for query data */
             CHGVAR    VAR(&SCST) VALUE(&SCUST) /* Change to length +
                          for print program */
             CHGVAR    VAR(&SCSTA) VALUE(&SCST) /* Change to alpha +
                          for query data */
             CHGVAR    VAR(&COPIES) VALUE(&COPIESL) /* change to +
                          length for print program */

/********************************************************************/
/* APPEND $ TO THE PROGRAM NAME                                     */
/********************************************************************/
             CHGVAR     VAR(&PGMO) VALUE(&PGM *TCAT '$')

/*********************************************************************/
/*   CREATE OPEN QUERY FILE TO BE USED FOR REPORT                    */
/*********************************************************************/
   /*PIO     CHGVAR     VAR(&A) VALUE('F4GNDT *GE ' *CAT + +
                         &FDATEA)                         */
   /*PIO*/   CHGVAR     VAR(&A) VALUE('F4GNDT >=  ' *CAT +
                         &FDATEA)

   /*PIO     CHGVAR     VAR(&B) VALUE('*AND F4GNDT *LE ' *CAT + +
                         &TDATEA)                              */
   /*PIO*/   CHGVAR     VAR(&B) VALUE(' AND F4GNDT <=  ' *CAT +
                         &TDATEA)

  /*PIO      CHGVAR     VAR(&C) VALUE('*AND F4AIC3 *EQ ' *CAT + +
                         &COMPA)                              */
  /*PIO*/    CHGVAR     VAR(&C) VALUE(' AND F4AIC3  =  ' *CAT +
                         &COMPA)

             IF         COND(&PCSTA *NE '0000000') THEN(DO)
  /*PIO      CHGVAR     VAR(&D) VALUE('*AND F4BTC7 *EQ ' *CAT + +
                         &PCSTA)                               */
  /*PIO*/    CHGVAR     VAR(&D) VALUE(' AND F4BTC7  =  ' *CAT +
                         &PCSTA)
             ENDDO

             IF         COND(&ARCSTA *NE '0000000') THEN(DO)
  /*PIO      CHGVAR     VAR(&D) VALUE('*AND F4ANC7 *EQ ' *CAT + +
                         &ARCSTA)                              */
  /*PIO*/    CHGVAR     VAR(&D) VALUE(' AND F4ANC7  =  ' *CAT +
                         &ARCSTA)
             ENDDO

             IF         COND(&SCSTA *NE '0000000') THEN(DO)
  /*PIO      CHGVAR     VAR(&D) VALUE('*AND F4BKC7 *EQ ' *CAT + +
                         &SCSTA)                              */
  /*PIO*/    CHGVAR     VAR(&D) VALUE(' AND F4BKC7  =  ' *CAT +
                         &SCSTA)
             ENDDO

             IF         COND(&COMPA *NE '000') THEN(DO)
             CHGVAR     VAR(&G) VALUE(&A *BCAT &B *BCAT &C *BCAT &D +
                                         *BCAT &E *BCAT &F)
             ENDDO
             IF         COND(&COMPA *EQ '000') THEN(DO)
             CHGVAR     VAR(&G) VALUE(&A *BCAT &B *BCAT &D +
                                         *BCAT &E *BCAT &F)
             ENDDO

/*********************************************************************/
/*   SELECT QUERY BY REPORT BEING RUN                                */
/*********************************************************************/
   /*PIO     OVRDBF     FILE(OMHSTPN3) SHARE(*YES)  +
             OPNQRYF    FILE(OMHSTPN3) +            +
                        QRYSLT(&G) +                +
                        KEYFLD(*FILE)              */

  /*PIO*/    OVRDBF     FILE(OMHSTPP) TOFILE(OMHSTPP) LVLCHK(*NO)
             OPNSQLF    SQL('Select * from OMHSTPP Where' *BCAT &G +
                          *BCAT ' ORDER BY F4AIC3 ASC, F4U2ST ASC, +
                          F4ITSX ASC, F4RMNB ASC, F4C4NB ASC') +
                          OPNID(OMHSTPN3) OPTION(*ALL) SEQONLY(*NO) +
                          RCDFMT(@HSTPLJ) ALWCPY(*IFRQD) /*PIO*/
  /*PIO*/    OVRDBF     FILE(OMHSTPN3) TOFILE(OMHSTPP) LVLCHK(*NO) +
                          OVRSCOPE(*JOB) SHARE(*YES) OPNSCOPE(*JOB)


/********************************************************************/
/* Override printer file with user specified values                 */
/********************************************************************/
             SBDPRTOVR  CMDVAR(&COMMAND) FILE(&PGMO) OUTQ(&OUTQ) +
                          COPIES(&COPIES) HOLD(&HOLD) +
                          SAVE(&SAVE)

             CALL       PGM(QCMDEXC) PARM(&COMMAND 512)
/********************************************************************/
/* 12/10/2009 LJB E000322                                           */
/* IF EMAIL IS REQUESTED, BUILD THE WORK FILE IN QTEMP. IF IT IS    */
/* ALREADY THERE, CLEAR IT.                                         */
/********************************************************************/
             DLTF       FILE(QTEMP/PNFWCPL0)
             MONMSG     MSGID(CPF0000)
             DLTF       FILE(QTEMP/PNFWCPL1)
             MONMSG     MSGID(CPF0000)
             DLTF       FILE(QTEMP/PNFWCPP)
             MONMSG     MSGID(CPF0000)

/* CUSTOMER HEADINGS WORKFILE                                       */
             CRTDUPOBJ  OBJ(PNFWCPP) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)
             CRTDUPOBJ  OBJ(PNFWCPL0) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)
             CRTDUPOBJ  OBJ(PNFWCPL1) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)

/*********************************************************************/
/*   CALL LISTING PROGRAM                                            */
/*********************************************************************/
             CALL       PGM(&PGM) PARM('' &FDATE &TDATE &FLAG1 +
                          &FLAG2 &SCST &ARCST &PCST &COMP &PAIDYN +
                          &EXCLCT)

             IF         COND(&EMAILADDR *NE '            ') THEN(DO)
             EXECUTE    VIEW(PNFWCPPV1) MBROPT(*REPLACE) PCFMT(*XLS) +
                          RECIPIENT(&EMAILADDR) EMLMSG('Freight +
                          Comparison Report by Export Status/Load +
                          Type')
             ENDDO

FTP:         IF         COND(&DOC8 *NE '        ') THEN(DO)
             CHGVAR     VAR(&EMAILADDR) +
                          VALUE('ftp:download.pork/transportation/' +
                          *CAT &DOC8 *TCAT '.xls')
             EXECUTE    VIEW(PNFWCPPV1) MBROPT(*REPLACE) PCFMT(*XLS) +
                          RECIPIENT(&EMAILADDR)
             ENDDO


             CLOF       OPNID(OMHSTPN3)
   /*PIO     DLTOVR     FILE(OMHSTPN3)   */
             DLTOVR     FILE(OMHSTPN3) LVL(*JOB) /*PIO*/

 ENDPGM
