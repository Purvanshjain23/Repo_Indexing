/********************************************************************/
/*                                                                  */
/*  SYNOPSIS - PRINT PROGRAM WITH SELECTIONS FOR LOAD TALLY         */
/*             COMPANY, DATES, LOAD, CARRIER                        */
/*                                                                  */
/*  SYSTEM    - ORDER MANAGEMENT SYSTEM                             */
/*  PROGRAM   - PRINT LOAD TALLY REPORT                             */
/*  DATE      - 12/10/97                                            */
/*                                                                  */
/*      PARAMETERS RECEIVED:                                        */
/*          &COMPNYN    - COMPANY NUMBER - BLANK FOR ALL            */
/*          &FRMDATN    - FROM SHIP DATE                            */
/*          &THRDATN    - THROUGH SHIP DATE                         */
/*          &CARRIER    - CARRIER CODE                              */
/*          &LOADT      - LOAD TYPE                                 */
/*          &REPORT     - REPORT TYPE                               */
/*          &OUTQ       - OUTPUT QUEUE                              */
/*          &HOLD       - HOLD PRINT                                */
/*          &SAVE       - SAVE PRINT                                */
/*          &COPYN      - NUMBER OF COPIES                          */
/*          &WHSE1-10   - WAREHOUSE 1 THRU 10 (ALL ARE OPTIONAL)    */
/********************************************************************/
/*  RMC  7/19/02  ADDED STATE CODE TO PARM AND SELECTION            */
/*  RMC  1/03/03  ADDED WEEKEN=Y IF WEEKENDING, =N IF MONTHENDING   */
/*  RMC  8/21/03  ADDED &REPORT:1=BY SCHED SHIP AND 2=BY DEADL DPRT */
/*  RMC  5/14/04  PKTR01 MAKE CO OPTIONAL IN QRYSLT                 */
/*  LJB 10/30/06  L61017B ADD YEAR AND PASS TO CALLED PROGRAM       */
/*  LJB 11/10/08  E00076 ADD OPTION 3 FOR ACTUAL SHIPPED DATE       */
/*  LJB 02/15/10  E000528  ADD 10 WAREHOUSE PARMS (OPTIONAL)        */
/*  PIO 05/17/23  REPLACE OPNQRYF WITH OPNSQLF FOR OMFJCPP FILE     */
/*                ADDED NEEDED OVRDBF AROUND THE COMMAND            */
/*                CHANGE *AND TO AND, *EQ TO =, *GE TO >=, *LE TO <=*/
/*                *GT TO > IN &D                                    */
/********************************************************************/
 PDF5PCLP:   PGM        PARM(&COMPNYN &FRMDATN &THRDATN &WEEKEN +
                          &CARRIER &LOADT &STATE &REPORT &OUTQ +
                          &HOLD &SAVE &COPYN &YEARN &WHSE1 &WHSE2 +
                          &WHSE3 &WHSE4 &WHSE5 &WHSE6 &WHSE7 &WHSE8 +
                          &WHSE9 &WHSE10)

/* PARMS                                                                      */
        DCL    &COMPNYN    *DEC                  /* Company Number (Passed)   */
        DCL    &FRMDATN    *DEC                  /* FROM SHIP DATE (Passed)   */
        DCL    &THRDATN    *DEC                  /* THRU SHIP DATE (PASSED)   */
        DCL    &CARRIER    *CHAR   (3)           /* CARRIER CODE              */
        DCL    &LOADT      *CHAR   (1)           /* LOAD TYPE                 */
        DCL    &WEEKEN     *CHAR   (1)           /* WEEKEND=Y,MONTH=N         */
        DCL    &STATE      *CHAR   (2)           /* STATE                     */
        DCL    &REPORT     *CHAR   (1)           /* REPORT TYPE              */
        DCL    &DESC       *CHAR   (25)          /* DESCRIPTION               */
        DCL    &COPYN      *DEC                  /* No. of Copies             */
        DCL    &OUTQ       *CHAR   (10)          /* Output Queue              */
        DCL    &HOLD       *CHAR   (4)           /* Hold Status               */
        DCL    &SAVE       *CHAR   (4)           /* Save Status               */
        DCL    &YEARN      *DEC                  /* YEAR PASSED   */
        DCL    &COMMAND    *CHAR   (512)
        DCL    &WHSE1      *CHAR   (3)           /* WAREHOUSE        */
        DCL    &WHSE2      *CHAR   (3)           /* WAREHOUSE        */
        DCL    &WHSE3      *CHAR   (3)           /* WAREHOUSE        */
        DCL    &WHSE4      *CHAR   (3)           /* WAREHOUSE        */
        DCL    &WHSE5      *CHAR   (3)           /* WAREHOUSE        */
        DCL    &WHSE6      *CHAR   (3)           /* WAREHOUSE        */
        DCL    &WHSE7      *CHAR   (3)           /* WAREHOUSE        */
        DCL    &WHSE8      *CHAR   (3)           /* WAREHOUSE        */
        DCL    &WHSE9      *CHAR   (3)           /* WAREHOUSE        */
        DCL    &WHSE10     *CHAR   (3)           /* WAREHOUSE        */

/* INTERNAL FIELDS                                                            */
        DCL    &COPY       *DEC    (2 0)         /* Number of copies          */
        DCL    &COMPANY    *DEC    (3 0)         /* Company Number            */
        DCL    &FRMDAT     *DEC    (7 0)         /* FROM SHIP DATE            */
        DCL    &THRDAT     *DEC    (7 0)         /* THRU SHIP DATE            */
        DCL    &LOAD       *DEC    (7 0)         /* LOAD NUMBER               */
        DCL    &YEAR       *DEC    (4 0)         /* YEAR          */

/* OPNQRYF FIELDS                                                             */
        DCL    &COMPA      *CHAR    3            /*                           */
        DCL    &FRMDATA    *CHAR    7            /* FROM SHIP DATE (Passed)   */
        DCL    &THRDATA    *CHAR    7            /* THRU SHIP DATE (Passed)   */
        DCL     &A        *CHAR   40              /* QRY COMPANY    */
        DCL     &B        *CHAR   50              /* QRY FROM DATE  */
        DCL     &C        *CHAR   50              /* QRY TO DATE    */
        DCL     &D        *CHAR  120              /* QRY ALL        */

/********************************************************************/
/* CHANGE VARIABLES FOR USE IN PROGRAM                              */
/********************************************************************/
             CHGVAR     VAR(&COMPANY) VALUE(&COMPNYN)
             CHGVAR     VAR(&COMPA) VALUE(&COMPANY)
             CHGVAR     VAR(&FRMDAT) VALUE(&FRMDATN)
             CHGVAR     VAR(&FRMDATA) VALUE(&FRMDAT)
             CHGVAR     VAR(&THRDAT) VALUE(&THRDATN)
             CHGVAR     VAR(&THRDATA) VALUE(&THRDAT)
             CHGVAR     VAR(&COPY) VALUE(&COPYN)
             CHGVAR     VAR(&YEAR) VALUE(&YEARN)

/*********************************************************************/
/*   CREATE OPEN QUERY FILE TO BE USED FOR LOAD TALLY                */
/*********************************************************************/
             IF         COND(&COMPA *NE '000') THEN(DO)
   /*PIO     CHGVAR     VAR(&A) VALUE('FJAIC3 *EQ ' *CAT + +
                         &COMPA *BCAT '*AND')            */
   /*PIO*/   CHGVAR     VAR(&A) VALUE('FJAIC3  =  ' *CAT +
                         &COMPA *BCAT ' AND')
                         ENDDO

/* REPORT FOR SCHEDULED SHIP DATE                                    */
SCHDSHIP: IF COND(&REPORT = '1') THEN(DO)
  /*PIO      CHGVAR     VAR(&B) VALUE('((FJAODT *GE ' *CAT + +
                         &FRMDATA *CAT '  AND FJAODT  <= +   +
                         ' *CAT &THRDATA *CAT ')')         */
  /*PIO*/    CHGVAR     VAR(&B) VALUE('((FJAODT >=  ' *CAT +
                         &FRMDATA *CAT '  AND FJAODT  <= +
                         ' *CAT &THRDATA *CAT ')')

  /*PIO      CHGVAR     VAR(&C) VALUE('*OR (FJEIDT *GE ' *CAT  + +
                         &FRMDATA *CAT ' *AND FJEIDT *LE '    +  +
                         *CAT &THRDATA *CAT '))')           */
  /*PIO*/    CHGVAR     VAR(&C) VALUE(' OR (FJEIDT >=  ' *CAT  +
                         &FRMDATA *CAT '  AND FJEIDT <=  '    +
                         *CAT &THRDATA *CAT '))')
             CHGVAR     VAR(&D) VALUE(&A *BCAT &B *BCAT &C)

/* RUN QUERY TO READ LOAD HEADER BY SCHEDULED SHIP DATE RANGE        */
   /*PIO     OVRDBF     FILE(OMFJCPLM) SHARE(*YES)              +
             OPNQRYF    FILE((OMFJCPLM)) QRYSLT(&D) KEYFLD((FJBZNA) + +
                          (FJAODT))                        */

  /*PIO*/    OVRDBF     FILE(OMFJCPP) TOFILE(OMFJCPP) LVLCHK(*NO)
             OPNSQLF    SQL('Select * from OMFJCPP where ' *BCAT &D +
                          *BCAT ' ORDER BY FJBZNA ASC, FJAODT ASC') +
                          OPNID(OMFJCPLM) OPTION(*ALL) SEQONLY(*NO) +
                          RCDFMT(@FJCPRX) ALWCPY(*IFRQD) /*PIO*/
   /*PIO*/   OVRDBF     FILE(OMFJCPLM) TOFILE(OMFJCPP) LVLCHK(*NO) +
                          OVRSCOPE(*JOB) SHARE(*YES) OPNSCOPE(*JOB)

          ENDDO

/* REPORT FOR DEADLINE DEPART DATE                                   */
DEADLDPT: IF COND(&REPORT = '2') THEN(DO)
  /*PIO      CHGVAR     VAR(&B) VALUE('((FJKZDT *GE ' *CAT + +
                         &FRMDATA *CAT ' *AND FJKZDT *LE +   +
                         ' *CAT &THRDATA *CAT '))')         */
  /*PIO*/    CHGVAR     VAR(&B) VALUE('((FJKZDT >=  ' *CAT +
                         &FRMDATA *CAT '  AND FJKZDT <=  +
                         ' *CAT &THRDATA *CAT '))')
             CHGVAR     VAR(&D) VALUE(&A *BCAT &B)

/* RUN QUERY TO READ LOAD HEADER BY DEADLINE DEPART DATE RANGE       */
    /*PIO    OVRDBF     FILE(OMFJCPLM) SHARE(*YES)           +
             OPNQRYF    FILE((OMFJCPLM)) QRYSLT(&D) KEYFLD((FJBZNA) + +
                          (FJKZDT))                               */

  /*PIO*/    OVRDBF     FILE(OMFJCPP) TOFILE(OMFJCPP) LVLCHK(*NO)
             OPNSQLF    SQL('SELECT * FROM OMFJCPP WHERE ' *BCAT &D +
                          *BCAT ' ORDER BY FJBZNA ASC, FJAODT ASC') +
                          OPNID(OMFJCPLM) OPTION(*ALL) SEQONLY(*NO) +
                          RCDFMT(@FJCPRX) ALWCPY(*IFRQD) /*PIO*/
  /*PIO*/    OVRDBF     FILE(OMFJCPLM) TOFILE(OMFJCPP) LVLCHK(*NO) +
                          OVRSCOPE(*JOB) SHARE(*YES) OPNSCOPE(*JOB)

          ENDDO

/* REPORT FOR ACTUAL SHIPPED DATE                                    */
ACTUAL:   IF COND(&REPORT = '3') THEN(DO)
     /*PIO   CHGVAR     VAR(&B) VALUE('((FJF9DT *GE ' *CAT + +
                         &FRMDATA *CAT ' *AND FJF9DT *LE +   +
                         ' *CAT &THRDATA *CAT '))')        */
   /*PIO*/   CHGVAR     VAR(&B) VALUE('((FJF9DT >=  ' *CAT +
                         &FRMDATA *CAT '  AND FJF9DT <=  +
                         ' *CAT &THRDATA *CAT '))')
             CHGVAR     VAR(&D) VALUE(&A *BCAT &B)

/* RUN QUERY TO READ LOAD HEADER BY ACTUAL SHIPPED DATE RANGE        */
    /*PIO    OVRDBF     FILE(OMFJCPLM) SHARE(*YES)             +
             OPNQRYF    FILE((OMFJCPLM)) QRYSLT(&D) KEYFLD((FJBZNA) ++
                          (FJF9DT))             */

  /*PIO*/    OVRDBF     FILE(OMFJCPP) TOFILE(OMFJCPP) LVLCHK(*NO)
             OPNSQLF    SQL('SELECT * FROM OMFJCPP WHERE ' *BCAT &D +
                          *BCAT ' ORDER BY FJBZNA ASC, FJAODT ASC') +
                          OPNID(OMFJCPLM) OPTION(*ALL) SEQONLY(*NO) +
                          RCDFMT(@FJCPRX) ALWCPY(*IFRQD)
  /*PIO*/    OVRDBF     FILE(OMFJCPLM) TOFILE(OMFJCPP) LVLCHK(*NO) +
                          OVRSCOPE(*JOB) SHARE(*YES) OPNSCOPE(*JOB)

          ENDDO

/********************************************************************/
/* OVERRIDE PRINTER FILE WITH USER SPECIFIED SELECTION VALUES       */
/********************************************************************/
             SBDPRTOVR  CMDVAR(&COMMAND) FILE(PDF6PFR$) OUTQ(&OUTQ) +
                          COPIES(&COPY) HOLD(&HOLD) SAVE(&SAVE)

             CALL       PGM(QCMDEXC) PARM(&COMMAND 512)

/*********************************************************************/
/*   CALL PRINT PROGRAM -
/*********************************************************************/
             CALL       PGM(PDF6PFR) PARM(' ' &COMPANY &THRDAT +
                          &CARRIER &LOADT &STATE &FRMDAT &WEEKEN +
                          &REPORT &YEAR &WHSE1 &WHSE2 &WHSE3 &WHSE4 +
                          &WHSE5 &WHSE6 &WHSE7 &WHSE8 &WHSE9 &WHSE10)

             CLOF       OPNID(OMFJCPLM)
   /*PIO     DLTOVR     FILE(OMFJCPLM)  */
             DLTOVR     FILE(OMFJCPLM) LVL(*JOB) /*PIO*/

 END:        ENDPGM
