/********************************************************************/
/*                                                                  */
/*  SYNOPSIS - PRINT FREIGHT COMPARISON EXCEPTION FROM HISTORY FILE.   */
/*                                                                  */
/*                                                                  */
/*                                                                  */
/*  COMPANY   - SEABOARD COPRORATION                                */
/*  SYSTEM    - ORDER MANAGEMENT                                    */
/*  USER NAME - ROSE CENTONZE                                       */
/*  DATE      - 06/26/03                                            */
/*                                                                  */
/*      PARAMETERS RECEIVED:                                        */
/*                                                                  */
/*          &OUTQ       - OUTPUT QUEUE                             */
/*          &COPEIS     - NUMBER OF COPIES                          */
/*          &NIGHTQ     - NIGHT QUEUE                               */
/*          &HOLD       - HOLD PRINT FLAG                           */
/*          &SAVE       - SAVE PRINT FLAG                           */
/*          &PGM        - PROGRAM NAME                              */
/*          &COMPL      - COMPANY NUMBER                            */
/*          &FDATEL     - FROM DATE                                 */
/*          &TDATEL     - TO DATE                                   */
/*          &FLAG1      - SELECT REPORT FLAG                        */
/*          &FLAG2      - PAID FREIGHT FLAG                         */
/*          &SLSRTE     - SALES ROUTE                               */
/*          &PCUST      - PARENT CUSTOMER                           */
/*          &ARCUST     - A/R CUSTOMER                              */
/*          &SCUST      - SHIPTO CUSTOMER                           */
/*          &DTLSUM     - DETAIL/SUMMARY                            */
/********************************************************************/
/********************************************************************/
/*  RMC 2/11/02 ADDED 2 PARMS FOR SELECTION: &EXCLPD =EXCLUDE CARRIER */
/*              PAID, &EXCLCT=EXCLUDE COLLECT TRUCKS                 */
/*  RMC 9/17/02 ADDED 1 PARM  FOR SELECTION: &DTLSUM=DETL OR SUMMARY  */
/*  RMC 6/26/03 COPIED FROM POR9PCLP TO PRINT EXCEPTIONS -           */
/*                IE. FREIGHT DIFFS < 0                              */
/*  RMC 11/15/06 ADDED LOAD PARM -- BUT NOT USED IN THIS PGM          */
/********************************************************************/

 POWOPCLP:   PGM        PARM(&PGM &COMPL &FDATEL &TDATEL &FLAG1 +
                          &FLAG2 &SLSRTE  &PCUST &ARCUST &SCUST +
    &EXCLPD &EXCLCT &DTLSUM &OUTQ &HOLD &SAVE &COPIESL &NIGHTQ &LOADL)

        DCL    &COMP      *DEC    (3 0)           /*  COMPANY #     */
        DCL    &COMPL     *DEC                    /*  FOR PASSING   */
        DCL    &FDATE     *DEC    (7 0)          /*  FROM DATE      */
        DCL    &FDATEL    *DEC                   /*  FOR PASSING    */
        DCL    &TDATE     *DEC    (7 0)          /*  THROUGH DATE   */
        DCL    &TDATEL    *DEC                   /*  FOR PASSING    */
        DCL    &PCST      *DEC    (7 0)          /*  PARENT CUSTOMER*/
        DCL    &PCUST     *DEC                   /*  FOR PASSING    */
        DCL    &ARCST     *DEC    (7 0)          /*  A/R CUSTOMER   */
        DCL    &ARCUST    *DEC                   /*  FOR PASSING    */
        DCL    &SCST      *DEC    (7 0)          /*  SHIPTO CUST    */
        DCL    &SCUST     *DEC                   /*  FOR PASSING    */
        DCL    &LOADL     *DEC                   /*  LOAD NOT USED  */
        DCL    &FLAG1     *CHAR   1               /* SELECT REPORT  */
        DCL    &FLAG2     *CHAR   1               /* PAID FREIGHT   */
        DCL    &EXCLPD     *CHAR   (1)           /* EXCL CARRIER PD           */
        DCL    &EXCLCT     *CHAR   (1)           /* EXCL CARRIER TRUCKS       */
        DCL    &DTLSUM     *CHAR   (1)           /* DETAIL/SUMMARY            */
        DCL    &SLSRTE    *CHAR   3               /* SALES ROUTE    */
        DCL    &COPIES    *DEC    (3 0)           /*  # OF COPIES   */
        DCL    &COPIESL   *DEC                    /*  FOR PASSING   */
        DCL    &OUTQ      *CHAR   10              /*  OUTPUT QUEUE  */
        DCL    &NIGHTQ    *CHAR   10              /*  NIGHT JOB QUE.*/
        DCL    &HOLD       *CHAR   (4)           /* Hold Status               */
        DCL    &SAVE       *CHAR   (4)           /* Save Status               */
        DCL    &PGM       *CHAR   10              /*  PGM NAME      */
        DCL    &COMPA     *CHAR   3               /*  COMPANY ALPHA */
        DCL    &TDATEA    *CHAR   7               /*  TO   DT ALPHA */
        DCL    &FDATEA    *CHAR   7               /*  TO   DT ALPHA */
        DCL    &PCSTA     *CHAR   7               /*  PARCST  ALPHA */
        DCL    &ARCSTA    *CHAR   7               /*  ARCST   ALPHA */
        DCL    &SCSTA     *CHAR   7               /*  SHIPCST ALPHA */
        DCL    &COMMAND    *CHAR   (512)

/* SEND PROGRAM MESSAGE PARAMETERS  */

        DCL    &PGMO      *CHAR   10              /*  PRINTER FILE  */
        DCL     &A        *CHAR   20              /* QRY YTD DATE   */
        DCL     &B        *CHAR   25              /* QRY TO DATE    */
        DCL     &C        *CHAR   25              /* QRY COMPANY    */
        DCL     &D        *CHAR   25              /* QRY            */
        DCL     &E        *CHAR   25              /* QRY            */
        DCL     &F        *CHAR   25              /* QRY            */
        DCL     &G        *CHAR  145              /* QRY ALL        */

             CHGVAR    VAR(&COMP) VALUE(&COMPL) /* Change to length +
                          for print program */
             CHGVAR    VAR(&COMPA) VALUE(&COMP) /* Change to alpha +
                          for message data */
             CHGVAR    VAR(&FDATE) VALUE(&FDATEL) /* Change to length +
                          for print program */
             CHGVAR    VAR(&FDATEA) VALUE(&FDATE) /* Change to alpha +
                          for query data */
             CHGVAR    VAR(&TDATE) VALUE(&TDATEL) /* Change to length +
                          for print program */
             CHGVAR    VAR(&TDATEA) VALUE(&TDATE) /* Change to alpha +
                          for query data */
             CHGVAR    VAR(&PCST) VALUE(&PCUST) /* Change to length +
                          for print program */
             CHGVAR    VAR(&PCSTA) VALUE(&PCST) /* Change to alpha +
                          for query data */
             CHGVAR    VAR(&ARCST) VALUE(&ARCUST) /* Change to length +
                          for print program */
             CHGVAR    VAR(&ARCSTA) VALUE(&ARCST) /* Change to alpha +
                          for query data */
             CHGVAR    VAR(&SCST) VALUE(&SCUST) /* Change to length +
                          for print program */
             CHGVAR    VAR(&SCSTA) VALUE(&SCST) /* Change to alpha +
                          for query data */
             CHGVAR    VAR(&COPIES) VALUE(&COPIESL) /* change to +
                          length for print program */

/********************************************************************/
/* APPEND $ TO THE PROGRAM NAME                                     */
/********************************************************************/

             CHGVAR     VAR(&PGMO) VALUE(&PGM *TCAT '$')

/*********************************************************************/
/*   CREATE OPEN QUERY FILE TO BE USED FOR REPORT                    */
/*********************************************************************/

             CHGVAR     VAR(&A) VALUE('F4GNDT *GE ' *CAT +
                         &FDATEA)

             CHGVAR     VAR(&B) VALUE('*AND F4GNDT *LE ' *CAT +
                         &TDATEA)

             CHGVAR     VAR(&C) VALUE('*AND F4AIC3 *EQ ' *CAT +
                         &COMPA)

             IF         COND(&PCSTA *NE '0000000') THEN(DO)
             CHGVAR     VAR(&D) VALUE('*AND F4BTC7 *EQ ' *CAT +
                         &PCSTA)
             ENDDO

             IF         COND(&ARCSTA *NE '0000000') THEN(DO)
             CHGVAR     VAR(&D) VALUE('*AND F4ANC7 *EQ ' *CAT +
                         &ARCSTA)
             ENDDO

             IF         COND(&SCSTA *NE '0000000') THEN(DO)
             CHGVAR     VAR(&D) VALUE('*AND F4BKC7 *EQ ' *CAT +
                         &SCSTA)
             ENDDO

/* WITH COMPANY */
             IF         COND(&COMPA *NE '000') THEN(DO)
             CHGVAR     VAR(&G) VALUE(&A *BCAT &B *BCAT &C *BCAT &D +
                                         *BCAT &E *BCAT &F)
             ENDDO

/* WITHOUT COMPANY */
             IF         COND(&COMPA *EQ '000') THEN(DO)
             CHGVAR     VAR(&G) VALUE(&A *BCAT &B *BCAT &D +
                                         *BCAT &E *BCAT &F)
             ENDDO

/*********************************************************************/
/*   SELECT QUERY BY REPORT BEING RUN                                */
/*********************************************************************/

             OVRDBF     FILE(OMHSTPMF) SHARE(*YES)
             OPNQRYF    FILE(OMHSTPMF) +
                        QRYSLT(&G) +
                        KEYFLD(*FILE)

/********************************************************************/
/* Override printer file with user specified values                 */
/********************************************************************/

             SBDPRTOVR  CMDVAR(&COMMAND) FILE(&PGMO) OUTQ(&OUTQ) +
                          COPIES(&COPIES) HOLD(&HOLD) +
                          SAVE(&SAVE)

             CALL       PGM(QCMDEXC) PARM(&COMMAND 512)
/*********************************************************************/
/*   CALL LISTING PROGRAM                                            */
/*********************************************************************/

             CALL       PGM(&PGM) PARM('' &FDATE &TDATE &DTLSUM &FLAG1 +
                          &FLAG2 &SCST &ARCST &PCST &COMP &SLSRTE  +
                               &EXCLPD &EXCLCT)

             CLOF       OPNID(OMHSTPMF)
             DLTOVR     FILE(OMHSTPMF)

 ENDPGM
