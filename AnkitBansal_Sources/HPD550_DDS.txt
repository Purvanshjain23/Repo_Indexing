     A*%%TS  SD  19950420  160650  ISLRAMS     REL-V3R0M5  5763-PW1
     A*****************************************************************
     A*
     A*  SYSTEM:        HOG PRODUCTION
     A*  DISPLAY FILE:  HPD550 - SELECT DISPOSED HOG GROUP BY B/O
     A*  RPG PROGRAM:   HP550  - SELECT DISPOSED HOG GROUP BY B/O
     A*  CREATED BY:    LEANNE FEDOR
     A*  DATE:          06/17/94
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA12(12 'PREVIOUS')
     A N82                                  ROLLUP(25 'PAGE FORWARD')
     A*--------------------------------------------------------------*
     A* REFERENCE WINDOW RECORD FORMAT 1
     A*--------------------------------------------------------------*
     A          R HP550R1
     A*%%TS  SD  19950420  160650  ISLRAMS     REL-V3R0M5  5763-PW1
     A                                      TEXT('Reference window')
     A                                      WINDOW(*DFT 16 41)
     A                                      WDWBORDER((*DSPATR RI) (*CHAR '    -
     A                                          '))
     A*--------------------------------------------------------------*
     A* RECORD FORMAT ONE
     A*--------------------------------------------------------------*
     A          R HP550R2
     A                                      TEXT('Written in RPG pgm to prevent-
     A                                       screen from clearing')
     A                                      CLRL(*NO)
     A                                      OVERLAY
     A*--------------------------------------------------------------*
     A* RECORD FORMAT TWO
     A*--------------------------------------------------------------*
     A* THIS FORMAT IS NOT WRITTEN IN THE RPG PGM.  IT PREVENTS THE
     A* CALLING_PROGRAM_SCREEN FROM BEING CLEARED.
     A*--------------------------------------------------------------*
     A          R HP550R3
     A                                      TEXT('Dummy format not written in R-
     A                                      PG pgm.')
     A                                      ASSUME
     A                                 11  1' '
     A*--------------------------------------------------------------*
     A* SUBFILE FORMAT ONE
     A*--------------------------------------------------------------
     A          R HP550S1                   SFL
     A*%%TS  SD  19950420  160650  ISLRAMS     REL-V3R0M5  5763-PW1
     A                                      TEXT('subfile record format one')
     A            HDRRN1         4S 0H
     A            DFFSCD    R        O  6  1REFFLD(HSRREF/HGFSCD *LIBL/HSPREF)
     A                                      EDTCDE(M)
     A            DFHGCD    R        O  6  8REFFLD(HGHGCD)
     A            DFBLCD    R        O  6 17REFFLD(HSRREF/HGBLCD *LIBL/HSPREF)
     A            DFRMCD    R        O  6 24REFFLD(HSRREF/HGRMCD *LIBL/HSPREF)
     A            DFPPCD    R        O  6 34REFFLD(HGPPCD)
     A*--------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*--------------------------------------------------------------
     A          R HP550C1                   SFLCTL(HP550S1)
     A*%%TS  SD  19950420  160650  ISLRAMS     REL-V3R0M5  5763-PW1
     A                                      SFLSIZ(0050)
     A                                      SFLPAG(0008)
     A                                      WINDOW(HP550R1)
     A                                      TEXT('control format one')
     A                                      OVERLAY
     A                                      SFLCSRRRN(&HDRRN)
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND(*MORE)
     A                                      RMVWDW
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            HDRRN          5S 0H
     A                                  1  8'SELECT DISPOSED HOG GROUP'
     A                                      DSPATR(HI)
     A                                  2  1'Position Cursor, Press Enter'
     A                                  3 31'Production'
     A                                      DSPATR(HI)
     A                                  4  1'Farm'
     A                                      DSPATR(HI)
     A                                  4  8'Group'
     A                                      DSPATR(HI)
     A                                  4 17'Bldg'
     A                                      DSPATR(HI)
     A                                  4 24'Room'
     A                                      DSPATR(HI)
     A                                  4 34'Phase'
     A                                      DSPATR(HI)
     A            C1FSCD    R        B  5  1REFFLD(HSRREF/HGFSCD *LIBL/HSPREF)
     A                                      EDTCDE(M)
     A                                      DSPATR(HI)
     A            C1PPCD    R        B  5 34REFFLD(HGPPCD)
     A                                      DSPATR(HI)
     A          R HP550K1
     A*%%TS  SD  19940506  091739  ISRCENT     REL-V2R2M0  5738-PW1
     A                                      WINDOW(HP550R1)
     A                                      OVERLAY
     A                                 15  1'F12=Previous'
