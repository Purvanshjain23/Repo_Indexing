      *************************************************************
      *
      *  PROGRAM NUMBER:  PKT0UPR
      *  PROGRAM NAME:    RANK TATTOO D/A TO PRT CRC SUMMARY
      *  PROGRAMMER:      ROSE MARTIN
      *  CREATE DATE:     06/22/95
      *
      *  FUNCTION:        THIS RPG READS THE TATTOO D/A FOR THE TATTOO
      *                   SENT THROUGH PARMS AND LOADS THE ARRAY TO BE
      *                   PRINTED PER THE D/A RANKING IN THE STD D/A
      *                   FILE.
      *                   FOR DEDUCTIONS (- SIGN), THE LOWEST 5 RANKING
      *                   DEDUCTIONS ARE IN PARMS 1 THRU 5. THE 6TH
      *                   PARM IS AN ACCUMULATION OF ANY ADDITIONAL
      *                   DEDUCTIONS.
      *                   FOR ADDITIONS (+ SIGN), THE LOWEST 1 RANKING
      *                   ADDITION IS IN PARM 7. THE 8TH PARM IS
      *                   AN ACCUMULATION OF ANY ADDITIONAL ADDITIONS.
      *
     **************************************************************
      *  Modification History
      *
      *  SLM 032397 THIS PROGRAM WAS USING PKCDCL1 CHANGED TO
      *             USE PKCDCPL7 THIS IS THE RTV INDEX FOR
      *             THE PRODUCER'S TATTOO DED/ADD
      *
      **************************************************************
     FPKCDCPL7IF  E           K        DISK
      * TATTOO D/A      KEY: CDHMNB CDBNNB CDBONB CDCVNB CDB0DT CDBQCD
     FPKBBREL0IF  E           K        DISK
      * DED/ADD STD     KEY: BBBQCD
     E                    MISC    1   1 20
     E                    DA         99  7 2 DS     20
     E                    AMT         8  7 2 DESC   20   AMT / DESC
     I            DS
     I                                        1  56 AMT
     I                                        1   72A1
     I                                        8  142A2
     I                                       15  212A3
     I                                       22  282A4
     I                                       29  352A5
     I                                       36  422A6
     I                                       43  492A7
     I                                       50  562A8
     I            DS
     I                                        1 160 DESC
     I                                        1  20 D1
     I                                       21  40 D2
     I                                       41  60 D3
     I                                       61  80 D4
     I                                       81 100 D5
     I                                      101 120 D6
     I                                      121 140 D7
     I                                      146 160 D8
     ******
     C           *ENTRY    PLIST
     C                     PARM           R0RTN   7
     C                     PARM           WUHMNB  30        COMPANY
     C                     PARM           WUBNNB  50        BO
     C                     PARM           WUBONB  50        LOAD
     C                     PARM           WUCVNB  50        TATTOO
     C                     PARM           DESC1  20
     C                     PARM           DESC2  20
     C                     PARM           DESC3  20
     C                     PARM           DESC4  20
     C                     PARM           DESC5  20
     C                     PARM           DESC6  20
     C                     PARM           DESC7  20
     C                     PARM           DESC8  20
     C                     PARM           AMT1    72
     C                     PARM           AMT2    72
     C                     PARM           AMT3    72
     C                     PARM           AMT4    72
     C                     PARM           AMT5    72
     C                     PARM           AMT6    72
     C                     PARM           AMT7    72
     C                     PARM           AMT8    72
      *
     C           KEY1      KLIST
     C                     KFLD           WUHMNB            COMPANY
     C                     KFLD           WUBNNB            BO
     C                     KFLD           WUBONB            LOAD
     C                     KFLD           WUCVNB            TATTOO
      *
     C                     CLEARAMT
     C                     CLEARDESC
      *
      * READ TATTOO D/A FILE
      *
     C           KEY1      SETLLPKCDCPL7
     C           *IN70     DOWEQ*OFF
     C           KEY1      READEPKCDCPL7                 70
     C           *IN70     IFEQ *OFF
      *
      * CHAIN STD D/A FILE FOR RANKING AND DESC
      *
     C           CDBQCD    CHAINPKBBREL0             71
     C           *IN71     IFEQ *OFF
     C                     Z-ADDBBOTNB    X       30       ARRAY ELEMNT
      *
      * ACCUM TATTOO D/A AMT IN THE RANKING ELEMENT NBR.
      *
     C           CDJBST    IFEQ '+'
     C                     ADD  CDA0VA    DA,X
     C                     MOVELBBBLTX    DS,X
     C                     ENDIF
     C           CDJBST    IFEQ '-'
     C                     SUB  CDA0VA    DA,X
     C                     MOVELBBBLTX    DS,X
     C                     ENDIF
      *
     C                     ENDIF
     C                     ENDIF
     C                     ENDDO
      *
     C                     Z-ADD0         Y       30
     C                     Z-ADD6         Z       30
      ****
      *  LOAD THE DEDUCTION PARMS 1 THRU 6 WITH 6 BEING THE 6TH
      *    HIGHEST RANKING DEDUCTION AND ABOVE AND THE ADDITIONS PARMS
      *    7 AND 8 WITH 8 BEING THE 2ND HIGHEST RANKING ADDITION AND UP
      *
     C                     DO   99        X
      *
     C           DA,X      IFLT 0
      *  MOVE THE DEDUCTIONS INTO PARMS 1 THRU 6 WITH 6 BEING THE 6TH
      *    HIGHEST RANKING DEDUCTION AND ABOVE.
     C                     ADD  1         Y
     C           Y         IFGE 6
     C           AMT,6     IFNE 0
     C                     MOVELMISC,1    DESC,6
     C                     ENDIF
     C                     ADD  DA,X      AMT,6
     C                     ELSE
     C                     ADD  DA,X      AMT,Y
     C                     MOVELDS,X      DESC,Y
     C                     ENDIF
     C                     ELSE
      ****
      *  MOVE THE ADDITIONS INTO PARMS 7 AND 8 WITH 8 BEING THE
      *    2ND HIGHEST RANKING ADDITION AND UP.
      *
     C           DA,X      IFGT 0
     C                     ADD  1         Z
     C           Z         IFGE 8
     C           AMT,8     IFNE 0
     C                     MOVELMISC,1    DESC,8
     C                     ELSE
     C                     MOVELDS,X      DESC,8
     C                     ENDIF
     C                     ADD  DA,X      AMT,8
     C                     ELSE
     C                     ADD  DA,X      AMT,Z
     C                     MOVELDS,X      DESC,Z
     C                     ENDIF
     C                     ENDIF
     C                     ENDIF
     C                     ENDDO
      ***
     C                     MOVELDESC,1    DESC1
     C                     Z-ADDAMT,1     AMT1
     C                     MOVELDESC,2    DESC2
     C                     Z-ADDAMT,2     AMT2
     C                     MOVELDESC,3    DESC3
     C                     Z-ADDAMT,3     AMT3
     C                     MOVELDESC,4    DESC4
     C                     Z-ADDAMT,4     AMT4
     C                     MOVELDESC,5    DESC5
     C                     Z-ADDAMT,5     AMT5
     C                     MOVELDESC,6    DESC6
     C                     Z-ADDAMT,6     AMT6
     C                     MOVELDESC,7    DESC7
     C                     Z-ADDAMT,7     AMT7
     C                     MOVELDESC,8    DESC8
     C                     Z-ADDAMT,8     AMT8
     C                     RETRN
**
MISCELLANEOUS
