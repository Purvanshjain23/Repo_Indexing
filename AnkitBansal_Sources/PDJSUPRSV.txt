/***************************************************************** */
/*                                                                 */
/*  PROGRAM NUMBER:  PDLQUPR                                       */
/*  PROGRAM NAME:    Print Production Schedule/Plan                */
/*  PROGRAMMER:      Purva Patel                                   */
/*  CREATE DATE:     03/07/96                                      */
/*                                                                 */
/*  FUNCTION:        This CLP is submitted with an Execute Message */
/*                   from Display 1st Level Production.            */
/*                                                                 */
/*                   USER OPTIONS ARE PASSED THROUGH THE PARMS.    */
/*                                                                 */
/*******************************************************************/
/*  Modification History                                           */
/*  R CENTONZE  04/28/97 ADDED PARM REPORT TYPE   (PPS043)         */
/*  L BUSER     01/23/08 FP1235 CREATED A NEW MAKE SHEET REPORT.   */
/*              DON'T PASS THE WAREHOUSE CODE INTO THE MAKE SHEET. */
/*  L BUSER     04/14/08 FP1235 ADD PARM TO EMAIL THE MAKE SHEET   */
/*              TO "MAKESHEET" DISTRIBUTION LIST. VALID ONLY IF    */
/*              REPORT TYPE = "M".                                 */
/*  S MASON     10/28/08 E00243 Pass in Report typ = T or B        */
/*              T= Trim Schedule & B= Belly Schedule               */
/*  S MASON     11/17/08 CREATE THE FILES IN QTEMP FOR ALL         */
/*  S MASON     11/20/08 REMOVE WORK FILE FROM QTEMP DUE           */
/*              THIS BEING RUN BACK TO BACK AND FILE COULD         */
/*              STILL EXISTS IN THE NIGHTLY RUNS                   */
/*              ON DEMAND SELECTION IS OK.                         */
/*  S MASON     11/25/08 ADD DISTRIBUTION LIST FOR NEW             */
/*              BELLY & TRIM REPORTS                               */
/*  S MASON     12/03/08 ON BELLY & RIM REPORTS THAT               */
/*              IF EMAILED CHANGE THE "FROM ADDRESS"               */
/*              TO BE FROM STEVEN BURKE@SEABOARDFOODS.COM          */
/*  S MASON     12/08/08 PASS IN SELECTION TO PRINT HOLD OVERS     */
/*  S MASON     12/10/08 PASS BACK THE NEXT PRODUCTION DATE        */
/*              FROM THE BUILD OF THE WORK FILE                    */
/*  R CENTONZE  11/29/11 PASS IN SELECTION TO PRINT SUMMARY BOX    */
/*                       FOR "T"RIM RPT WHICH IS NEW PBA3PFR PGM  */
/*******************************************************************/
             PGM        PARM(&RTN &PDEV &HOLD &SAVE &XCOPIES &PGM +
                             &PGMPRTF &APP &XCOMP &PRDTYP &XDATE +
                             &CMTSTS &RTYPE &WHSE &EMAIL &HOLDOV &SUMBOX)

/*-----------------------------------------------------------------*/
/*                      PARAMETER                                  */
/*-----------------------------------------------------------------*/
/* Print Options                                                   */
             DCL        VAR(&RTN) TYPE(*CHAR) LEN(7)
             DCL        VAR(&PDEV) TYPE(*CHAR) LEN(10)
             DCL        VAR(&HOLD) TYPE(*CHAR) LEN(4)
             DCL        VAR(&SAVE) TYPE(*CHAR) LEN(4)
             DCL        VAR(&XCOPIES) TYPE(*CHAR) LEN(15)
             DCL        VAR(&PGM) TYPE(*CHAR) LEN(10)
             DCL        VAR(&PGMPRTF) TYPE(*CHAR) LEN(10)
             DCL        VAR(&APP) TYPE(*CHAR) LEN(6)
/* Email distribution list.                                        */
/* Company value code name.                                        */
             DCL        VAR(&VALDIST) TYPE(*CHAR) LEN(10)
/* Name of the distriubtion list that will be used                 */
             DCL        VAR(&DISTLST) TYPE(*CHAR) LEN(40)
             DCL        VAR(&DISTNME) TYPE(*CHAR) LEN(60)

/* Function Specific                                               */
             DCL        VAR(&XCOMP) TYPE(*CHAR) LEN(15)
             DCL        VAR(&PRDTYP) TYPE(*CHAR) LEN(1)
             DCL        VAR(&XDATE) TYPE(*CHAR) LEN(7)
             DCL        VAR(&MDATE) TYPE(*CHAR) LEN(10)
             DCL        VAR(&CMTSTS) TYPE(*CHAR) LEN(1)
             DCL        VAR(&RTYPE) TYPE(*CHAR) LEN(1)
             DCL        VAR(&WHSE) TYPE(*CHAR) LEN(3)
             DCL        VAR(&EMAIL) TYPE(*CHAR) LEN(1)
             DCL        VAR(&HOLDOV) TYPE(*CHAR) LEN(1)
             DCL        VAR(&SUMBOX) TYPE(*CHAR) LEN(1)
             DCL        VAR(&DATA) TYPE(*CHAR) LEN(10) /* DATA +
                          FILES      */
             DCL        VAR(&DATAH) TYPE(*CHAR) LEN(10) /* HIST +
                          FILES      */

/*-----------------------------------------------------------------*/
/*                    PROGRAM VARIABLES                            */
/*-----------------------------------------------------------------*/
             DCL        VAR(&COPIES) TYPE(*DEC) LEN(2 0)

             DCL        VAR(&COMP) TYPE(*DEC) LEN(3 0)
             DCL        VAR(&COMPA) TYPE(*CHAR) LEN(3)
             DCL        VAR(&DATE) TYPE(*DEC) LEN(7 0)
             DCL        VAR(&NDATE) TYPE(*DEC) LEN(7 0)
             DCL        VAR(&NDATE2) TYPE(*DEC) LEN(7 0)

             DCL        VAR(&DESC) TYPE(*CHAR) LEN(50)

             DCL        VAR(&USER) TYPE(*CHAR) LEN(10)
             DCL        VAR(&USRCLS) TYPE(*CHAR) LEN(10)

/*                                                                   */
             RTVJOBA    USER(&USER)
             RTVUSRPRF  USRPRF(&USER) USRCLS(&USRCLS)
/*-----------------------------------------------------------------*/
/*                     INITIALIZATION                              */
/*-----------------------------------------------------------------*/
             CHGVAR     VAR(&COPIES) VALUE(&XCOPIES)
             CHGVAR     VAR(&COMP) VALUE(&XCOMP)
             CHGVAR     VAR(&COMPA) VALUE(&COMP)
             CHGVAR     VAR(&DATE) VALUE(&XDATE)
             CVTDAT     DATE(&XDATE) TOVAR(&MDATE) FROMFMT(*CYMD) +
                          TOFMT(*MDYY) TOSEP(/)
             CHGVAR     VAR(&DESC) VALUE('Make Sheet for Company' +
                          *BCAT &COMPA *BCAT 'for' *BCAT &MDATE)

/*-----------------------------------------------------------------*/
/*                           MAIN                                  */
/*-----------------------------------------------------------------*/

             OVRPRTF    FILE(&PGMPRTF) OUTQ(&PDEV) COPIES(&COPIES) +
                          HOLD(&HOLD) SAVE(&SAVE)

/* FOR "DAY" AND "VARIANCE" REPORTS, PASS THE WAREHOUSE CODE        */
             IF         COND((&RTYPE *EQ 'D') *OR (&RTYPE *EQ 'V')) +
                          THEN(CALL PGM(&PGM) PARM(' ' &COMP +
                          &PRDTYP &DATE &WHSE &APP &CMTSTS &RTYPE))

/* FOR "MAKE SHEET" REPORT, DO NOT PASS THE WAREHOUSE CODE          */
/* IF EMAIL FLAG = "E" THEN EMAIL THE REPORT TO THE MAKESHEET       */
/* DISTRIBUTION LIST (SENDS TO EXCHANGE SERVER DIST LIST).          */
             IF         COND(&RTYPE *EQ 'M') THEN(DO)
             CALL       PGM(&PGM) PARM(' ' &COMP &PRDTYP &DATE &APP +
                          &CMTSTS &RTYPE)

                IF        COND(&EMAIL *EQ 'E') THEN(DO)
                ESEND/ESNDMAIL RECIPIENT(MAKESHEET) SUBJECT(&DESC) +
                          ATTLIST((* *PDF *N &PGMPRTF))
                ENDDO
             ENDDO

/* ---------------------------------------------------------------  */
/* Trim Production Schedule or Belly Production Schedule            */
/* ---------------------------------------------------------------  */
/* FOR "T Trim Schedule or B for Belly Schedule reports             */
             IF         COND((&RTYPE *EQ 'B') *OR (&RTYPE *EQ 'T')) +
                          THEN(DO)
/* copy files to qtemp     */


             CALL      PGM(CAQXXFR) PARM(' ' &DATA &DATAH)

/********************************************************************/
/* 11/20/2008 SUSAN MASON                                           */
/* ADDED ADDITION LOGIC DUE TO ERROR ON BB                          */
/********************************************************************/
/* COPY TO QTEMP, BUT IF ALREADY THERE THEN WILL GO TO CPYFEND      */
/* NIGHTLY RUN EXECUTE OF THIS JOB STREAM BACK TO BACK              */
/* THE DATA IS IN QTEMP, BUT QTEMP DOES NOT GO AWAY                 */
/* NO NEED TO DELETE THIS WORK FILE IN QTEMP                        */
/* BECAUSE PROGRAM THAT CREATES THE WORK FILE WILL REMOVE ALL       */
/* RECORDS BEFORE STARTING.                                         */
/* WHEN JOB IS SELECT ON DEMAND THEN THE FILES IN QTEMP WILL        */
/* BE GONE.                                                         */
/********************************************************************/

             CRTDUPOBJ  OBJ(PMEUCPP) FROMLIB(&DATA) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)

             MONMSG     MSGID(CPF2130) EXEC(GOTO CMDLBL(CPYFEND))

             CRTDUPOBJ  OBJ(PMEUCPL0) FROMLIB(&DATA) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)
             CRTDUPOBJ  OBJ(PMEUCPL1) FROMLIB(&DATA) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)
             CRTDUPOBJ  OBJ(PMEUCPL2) FROMLIB(&DATA) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)
             CRTDUPOBJ  OBJ(PMEUCPL3) FROMLIB(&DATA) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)
             CRTDUPOBJ  OBJ(PMEUCPL4) FROMLIB(&DATA) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)
             CRTDUPOBJ  OBJ(PMEUCPL5) FROMLIB(&DATA) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)
             CRTDUPOBJ  OBJ(PMEUCPL6) FROMLIB(&DATA) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)
             CRTDUPOBJ  OBJ(PMEUCPL7) FROMLIB(&DATA) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)

 CPYFEND:

/* create work file        */
/* Restrict by Company number, Warehouse Code, Schedule Ship Date,   */


             CALL       PGM(PMAPXFR) PARM(' ' &COMP &WHSE &DATE +
                          &RTYPE &HOLDOV &NDATE &NDATE2)
/* Send in Report Type                                               */
/* Print report            */


BELLY:       IF         COND(&RTYPE = 'B') THEN(DO)
             CALL       PGM(PMAYPFR) PARM(' ' &WHSE &COMP &DATE +
                          &CMTSTS &RTYPE &HOLDOV &NDATE &NDATE2)
             ENDDO
TRIM:        IF         COND(&RTYPE = 'T') THEN(DO)
             CALL       PGM(PBA3PFR) PARM(' ' &WHSE &COMP &DATE +
                          &CMTSTS &RTYPE &HOLDOV &NDATE &NDATE2 &SUMBOX)
             ENDDO
/* ---------------------------------------------------------------  */
/* EMAIL  *** EMAIL ***                                             */
/* build the subject line                                           */
/* build the company value record to be retrieve                    */
/* Get the company value                                            */
/* Use &DISTLST as the distro list in the receipent of the esend    */
/* command                                                          */
/* ---------------------------------------------------------------  */
                IF        COND(&EMAIL *EQ 'E') THEN(DO)
/* ---------------------------------------------------------------  */

             CHGVAR     VAR(&DESC) VALUE('                      ')

/* Special instruction for the Belly report                         */
             IF         COND(&RTYPE *EQ 'B') THEN(DO)
             CHGVAR     VAR(&VALDIST) VALUE('PSBELLYRPT')
             CHGVAR     VAR(&DESC) VALUE('Belly Schedule Company' +
                          *BCAT &COMPA *BCAT 'for' *BCAT &MDATE)
             ENDDO


/* Special instruction for the Trim report                          */
             IF         COND(&RTYPE *EQ 'T') THEN(DO)
             CHGVAR     VAR(&VALDIST) VALUE('PSTRIMRPT ')
             CHGVAR     VAR(&DESC) VALUE('Trim Schedule Company' +
                          *BCAT &COMPA *BCAT 'for' *BCAT &MDATE)
             ENDDO

             CALL       PGM(POMTXFR) PARM(' ' &COMP &VALDIST &DISTLST)
             CHGVAR     VAR(&DISTNME) VALUE('Distribution List: ' +
                          *BCAT &DISTLST)
/* ---------------------------------------------------------------  */

/* Email of the Belly Report or of the Trim Report.                 */

             ESEND/ESNDMAIL RECIPIENT(&DISTLST) SUBJECT(&DESC) +
                          MSG(&DISTNME) OVRUSROPT(PDSBURK) +
                          ATTLIST((* *PDF *N &PGMPRTF))

                ENDDO

             ENDDO
/*-----------------------------------------------------------------*/
/*                      PROGRAM END                                */
/*-----------------------------------------------------------------*/
 ENDPGM:
             DLTOVR     FILE(*ALL)
             ENDPGM
