      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Triumph Foods
      * PROGRAM:     TF607
      * TITLE:       Cash: Cash Distribution Balance Report
      *              Cash Distribution
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     10/05/05
      *
      * FUNCTION:  Batch program to print the Cash Distribution Balance records with
      *            their associated Cash Distribution Detail records.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 11/06/06  LeAnne Ramsey
      *           Recompile only. New fields were added to TFP011-Cash Distribution Balance.
      *
      * 10/29/07  LeAnne Ramsey
      *           Moved the total accumulation logic outside of the Revenue print subroutine.
      *
      * 03/19/08  Alice Brownfield
      *           Changed calculation for the "Aggregate Gross Revenue Balance" to use GROSS
      *           instead of NET fields.
      *
      * 04/24/08  LeAnne Ramsey
      *           Added fields to Cash Distribution Detail:
      *             1) system generated flag (yes/no)
      *             2) adjustment comment
      *             3) AGG adjustment amount
      *
      * 04/28/08  LeAnne Ramsey
      *           Added SBD Cash Distribution Adjustment Amount and TF Cash
      *           Distribution Adjustment Amount into the appropriate SBF/TF
      *           column.
      *
      * 04/29/08  Alice Brownfield
      *           Changed calculation of Aggregate Gross Revenue $$$ (l1aam)
      *           to use the NET instead of GROSS fields.
      *
      * 05/30/08  LeAnne Ramsey
      *           Changed calculation of Aggregate Gross Revenue $$$ (l1aam)
      *           back to the original calc using GROSS fields.
      *
      * 06/04/08  LeAnne Ramsey
      *           Changed calculation of Aggregate Gross Revenue $$$ (l1aam)
      *           back to NET. Using GROSS with our "manual" adjustments fixes
      *           some columns but screws up others. So, we are still in the
      *           catch-22 situation. We'll leave the calc at NET until we come up
      *           with different "manual adjustment" logic/files.
      *
      * 04/13/09  LeAnne Ramsey
      *           Added a "stop date" selection (optional). If the user enters a Stop Date,
      *           the report will not print any data beyond this date.
      *
      * 04/16/09  LeAnne Ramsey
      *           Removed code that was commented out (no longer used).
      *
      * 09/10/09  Alice Brownfield
      *           E00493-field name changes only in TFP011-Cash Distribution Balance; no logic
      *           changes.
      /eject
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Ftfl011d   if   e           k disk
      *    Cash distribution balance (records selected with open query)
      *
      *
     Ftfl013b   if   e           k disk
      *    Cash distribution detail (records selected with open query)
      *
      *
     Fprint198  o    f  198        printer oflind(*inof)
      *
      /EJECT
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Standard fields
      *
     D dash            s            198    inz(*all'-')
     d rtime           s              6  0
      *
      *
      * Control fields
      *
     D first           s              1    inz('Y')
     D haddata         s              1    inz('N')
     D count           s              5  0
      *
      *
      *
      * Workfields
      *
     D wkrcexfl        s              1
      *
      *
      * Workfields for Date Manipulation
      *
     D wkisodate       s               d   datfmt(*iso)
      *
      *
      * Print fields
      *
     d l1rwemdy        s              6  0
     d l1aam           s                   like(cbanpram)
     d l1sam           s                   like(cbanpram)
     d l1tam           s                   like(cbanpram)
     d l1nam           s                   like(cbanpram)
      *
     d l2cdmdy         s              6  0
     d l2cdam          s                   like(cdasxcdam)
     d l2leftam        s                   like(cdasxcdam)
     d l2abalam        s                   like(cdasxcdam)
     d l2abalsxam      s                   like(cdasxcdam)
     d l2sbalam        s                   like(cdasxcdam)
     d l2tbalam        s                   like(cdasxcdam)
     d l2nbalam        s                   like(cdasxcdam)
      *
     d t1desc          s             17
      *
      *
      * Array index
      *
     d x               s              1  0
      *
      ****************************************************************
      * ARRAYS AND TABLES
      ****************************************************************
      *
     D arcdam          s             11  2 dim(3)
     D arasxcdam       s             11  2 dim(3)
     D arscdam         s             11  2 dim(3)
     D artcdam         s             11  2 dim(3)
      *
     D ar1balam        s             11  2 dim(3)
     D ar2balam        s             11  2 dim(3)
     D ar3balam        s             11  2 dim(3)
     D ar4balam        s             11  2 dim(3)
     D ar5balam        s             11  2 dim(3)
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *
      *---------------------------------------------------------------
      * Local data area.
      *---------------------------------------------------------------
      *
     Dlda             uds                  dtaara(*lda)
      *
     D  ldfmdy                 1      6  0
     D  ldfdt                  7     14  0
     D  ldfwemdy              15     20  0
     D  ldfwedt               21     28  0
      *
     D  ldiecd                29     29
     D  ldieds                30     55
      *
     D  ldrtcd                56     56
     D  ldrtds                57     73
      *
     D  ldtmdy                74     79  0
     D  ldtdt                 80     87  0
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ***************************************************************************************
      * MAINLINE
      ***************************************************************************************
      *
     C                   select
     C                   when      ldiecd = 'E'
     C                   exsr      $exempt
      *
     C                   when      ldiecd = 'N'
     C                   exsr      $nonexempt
      *
     C                   when      ldiecd = 'B'
     C                   exsr      $exempt
     C                   exsr      $nonexempt
     C                   endsl
      *
      * If you did both Exempt/Non-Exempt and had data on either section,
      * print a report total.
      *
     C                   if        ldiecd = 'B' and haddata = yes
     C                   eval      t1desc = '    Report Total:'
     C                   z-add     3             x
     C                   seton                                        80
     C                   exsr      $t1tot
     C                   endif
      *
      * EOF processing
      *
     C                   seton                                        lr
      /EJECT
      *---------------------------------------------------------------
      * Exempt data
      *---------------------------------------------------------------
      *
     C     $exempt       begsr
      *
     C                   move      yes           first
     C                   move      yes           wkrcexfl
     C                   seton                                        87
     C                   exsr      $h1hdr
      *
     C                   exsr      $process
      *
     C                   setoff                                       87
     C                   if        first = yes
     C                   except    nodata
     C                   else
      *
     C                   move      yes           haddata
     C                   z-add     2             x
     C                   eval      t1desc = '    Exempt Total:'
     C                   seton                                        80
     C                   exsr      $t1tot
     C                   setoff                                       80
     C                   exsr      $clear
     C                   endif
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Non-exempt data
      *---------------------------------------------------------------
      *
     C     $nonexempt    begsr
      *
     C                   move      yes           first
     C                   move      no            wkrcexfl
     C                   seton                                        88
     C                   exsr      $h1hdr
      *
     C                   exsr      $process
      *
     C                   setoff                                       88
     C                   if        first = yes
     C                   except    nodata
     C                   else
      *
     C                   move      yes           haddata
     C                   z-add     2             x
     C                   eval      t1desc = 'Non-Exempt Total:'
     C                   seton                                        80
     C                   exsr      $t1tot
     C                   setoff                                       80
     C                   exsr      $clear
     C                   endif
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Process the Cash Distribution Balance and Detail
      *---------------------------------------------------------------
      *
     C     $process      begsr
      *
      * Read each Cash Distribution Balance record selected by the open query.
      *
     C     wkrcexfl      setll     tfl011d
      *
     C                   dou       *in91 = *on                                  Do loop
     C     wkrcexfl      reade     tfl011d                                91
     C                   if        *in91 = *off                                 If not eof
      *
     C                   move      no            first
      *
      * Print the Cash Distribution Balance record
      *
     C                   exsr      $l1dtl
      *
      * Accumulate values for the "Remaining Balances" total line.
      *
     C                   add       l1aam         ar1balam
     C                   add       cbasxam       ar2balam
     C                   add       l1sam         ar3balam
     C                   add       l1tam         ar4balam
     C                   add       l1nam         ar5balam
      *
      * Retrieve/print each Cash Distribution Detail line.
      *
     C                   exsr      $detail
      *
      * If you had more than a single 'detail' record, print the total
      * for this 'balance' record.
      *
     C                   z-add     1             x
      *
     C                   if        count > 1
     C                   seton                                        85
     C                   eval      t1desc = '           Total:'
     C                   exsr      $t1tot
     C                   setoff                                       85
     C                   endif
      *
     C                   exsr      $clear
      *
     C                   endif                                                  If not eof
     C                   enddo                                                  Do loop
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Retrieve/print each Cash Distribution Detail record
      *---------------------------------------------------------------
      *
     C     $detail       begsr
      *
     C                   seton                                        89
     C                   z-add     0             count
      *
      *
      * Initialize the 'balance' fields to the values from the "Revenue"
      * line.
     C                   z-add     l1aam         l2abalam
     C                   z-add     cbasxam       l2abalsxam
     C                   z-add     l1sam         l2sbalam
     C                   z-add     l1tam         l2tbalam
     C                   z-add     l1nam         l2nbalam
      *
      *
      * Retrieve/print Cash Distribution Detail records
      *
     C     key01         setll     tfl013b
      *
     C                   dou       *in93 = *on                                  Do detail
     C     key01         reade     tfl013b                                93
     C                   if        *in93 = *off                                 If not eof
      *
     C                   add       1             count
      *
      * Flip the Cash Week-Ending date into MMDDYY format.
      *
     C     *iso          test(d)                 cdddt                  92
     C                   if        *in92 = *off                                 If OK
     C                   move      cdddt         wkisodate
     C     *mdy          move      wkisodate     l2cdmdy
     C                   endif                                                  If OK
      *
      * Calculate "Cash Distribution":
      *   SBF Cash Distributed Amount + TF Cash Distributed Amount +
      *          AGG Sales Expense Cash Distributed Amount +
      *          AGG Adjustment Amount (manually entered to fix stuff)
      *
     C                   eval      l2cdam = cdscdam + cdtcdam + cdasxcdam +
     C                                      cdaajam
      *
      *
      * Calculate "Aggregate Gross Revenue $$$ Balance":
      *   Balance - Cash Distribution
      *
     C                   eval      l2abalam = l2abalam - l2cdam
      *
      *
      * Calculate "Aggregate Sales Expense $$$ Balance":
      *   Balance - Agg Sales Expense Cash Distributed Amount
      *
     C                   eval      l2abalsxam = l2abalsxam - cdasxcdam
      *
      *
      * Calculate "Cash Sales Expense Distributed Net Left":
      *   Cash Distribution - Agg Sales Expense Cash Distributed Amount
      *
     C                   eval      l2leftam = l2cdam - cdasxcdam
      *
      *
      * Calculate "SBF Balance to Pay":
      *   Balance - SBF Cash Distributed Amount
      *
     C                   eval      l2sbalam = l2sbalam - cdscdam
      *
      *
      * Calculate "TF Balance to Pay":
      *   Balance - TF Cash Distributed Amount
      *
     C                   eval      l2tbalam = l2tbalam - cdtcdam
      *
      *
      * Calculate "Total Net Balance to Pay:'
      *   SBF Balance to Pay + TF Balance to Pay
      *
     C     l2sbalam      add       l2tbalam      l2nbalam
      *
      * Accumulate values for Totals:
      *
     C                   add       l2cdam        arcdam
     C                   add       cdasxcdam     arasxcdam
     C                   add       cdscdam       arscdam
     C                   add       cdtcdam       artcdam
      *
      * Reduce the "Remaining Balances" amounts that will print in the Total Line.
      *
     C                   sub       l2cdam        ar1balam
     C                   sub       cdasxcdam     ar2balam
     C                   sub       cdscdam       ar3balam
     C                   sub       cdtcdam       ar4balam
     C                   eval      ar5balam = ar5balam - cdscdam - cdtcdam
      *
      * If this is a Manual Adjustment record, print the word Manual in the
      * right-hand margin.
     C                   if        cdsysfl = no
     C                   seton                                        86
     C                   else
     C                   setoff                                       86
     C                   endif
      *
      * Print Cash Distribution Detail line.
      *
     C                   exsr      $l2dtl
      *
     C                   endif                                                  If not eof
     C                   enddo                                                  Do detail
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Clear array for specified index
      *---------------------------------------------------------------
      *
     C     $clear        begsr
      *
     C                   z-add     0             arcdam(x)
     C                   z-add     0             arasxcdam(x)
     C                   z-add     0             arscdam(x)
     C                   z-add     0             artcdam(x)
      *
     C                   z-add     0             ar1balam(x)
     C                   z-add     0             ar2balam(x)
     C                   z-add     0             ar3balam(x)
     C                   z-add     0             ar4balam(x)
     C                   z-add     0             ar5balam(x)
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Print detail line one
      *---------------------------------------------------------------
      *
     C     $l1dtl        begsr
      *
     C                   if        *inof = *on
     C                   exsr      $h1hdr
     C                   endif
      *
      * Flip the Week-ending Revenue date into MMDDYY format.
      *
     C     *iso          test(d)                 cbwedt                 92
     C                   if        *in92 = *off                                 If OK
     C                   move      cbwedt        wkisodate
     C     *mdy          move      wkisodate     l1rwemdy
     C                   endif                                                  If OK
      *
      *
      * Set the "Aggregate Gross Revenue Balance" equal to:
      *     Aggregate NET Product Revenue Amount
      *   + Aggregate NET Claim Amount
      *   + Aggregate Total Sales Expense Amount
      *   + Aggregate NET AR Adjustment Amount
      *   + Agg Cash Dist Balance Adj amount
      *
     C                   eval      l1aam = cbanpram + cbanclam + cbasxam
     C                                     + cbaaranam + cbacdbaam
      *
      *
      * Set "SBF Balance Remaining Amount" equal to:
      *     SBF Net Product Revenue Amount
      *   + SBF Net Claim Amount
      *   + SBF AR Adjustment NET Amount
      *   + SBF Cash Dist Balance Adj amount
      *
     C                   eval      l1sam = cbsnpram + cbsnclam + cbsaranam
     C                                     + cbscdbaam
      *
      *
      * Set "TF Balance Remaining Amount" equal to:
      *     TF Net Product Revenue Amount
      *   + TF Net Claim Amount
      *   + TF AR Adjustment NET Amount
      *   + TF Cash Dist Balance Adj amount
      *
     C                   eval      l1tam = cbtnpram + cbtnclam + cbtaranam
     C                                     + cbtcdbaam
      *
      * Set "Total Net Balance to Pay" equal to:
      *     SBF Balance Remaining Amount + TF Balance Remaining Amount
      *
     C     l1sam         add       l1tam         l1nam
      *
      * Print blank lines
     C                   except    blank
      *
      * Print the detail line twice to make it bold.
      *
     C                   except    l1dtl
     C                   except    l1dtl
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Print detail line two
      *---------------------------------------------------------------
      *
     C     $l2dtl        begsr
      *
     C                   if        *inof = *on
     C                   exsr      $l1dtl
     C                   endif
      *
     C                   except    l2dtl
      *
     C                   setoff                                       89
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Print report heading
      *---------------------------------------------------------------
      *
     C     $h1hdr        begsr
      *
     C                   except    h1hdr
     C                   seton                                        89
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Print total line for a Cash Distribution Balance record
      *---------------------------------------------------------------
      *
     C     $t1tot        begsr
      *
     C                   if        *inof = *on
     C                   exsr      $h1hdr
     C                   exsr      $l1dtl
     C                   endif
      *
     C                   except    t1tot
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Key lists
      *
     C     key01         klist
     C                   kfld                    cbwedt
     C                   kfld                    wkrcexfl
      *
      * Retrieve time for report heading.
      *
     C                   time                    rtime
      *
      *
      * Set indicators to control printing:
      *
      *      Start date
     C                   if        ldfmdy <> 0
     C                   seton                                        83
     C                   endif
      *      Stop date
     C                   if        ldtdt > 0 and ldtdt < 99999999
     C                   seton                                        84
     C                   endif
      *
      *
     C                   endsr
      /EJECT
      *-------------------------------------------------------------
      * Report heading lines
      *-------------------------------------------------------------
      *
     Oprint198  e            h1hdr          1 01
     O                       sdpgm               10
     O                                          105 'CASH DISTRIBUTION BALANCE'
     O                                          112 ' REPORT'
     O                                          188 'DATE'
     O                       udate         y    198
      *
     O          e            h1hdr          1
     O                       sdusr               10
     O               87                         107 'Cash Distribution--Exempt'
     O               88                         101 'Cash Distribution--'
     O               88                         111 'Non-Exempt'
     O                                          188 'TIME'
     O                       rtime              198 '  :  :  '
      *
     O          e            h1hdr          1
     O                                          188 'PAGE'
     O                       page          z    198
      *
      *
     O          e            h1hdr          1
     O                                           23 'Start date:'
     O               83      ldfmdy              33 '  /  /  '
     O                                           53 'Week-ending date:'
     O                       ldfwemdy            63 '  /  /  '
      *
      *
     O          e            h1hdr          1
     O                                           23 'Stop date:'
     O               84      ldtmdy              33 '  /  /  '
      *
      *
     O          e            h1hdr          1
     O                                           23 'Exempt/Non-Exempt/Both:'
     O                       ldiecd              26
      *
     O          e            h1hdr          2
     O                                           23 'Report code:'
     O                       ldrtcd              26
     O                       ldrtds              45
      *
      *-------------------------------------------------------------
      * Column headings
      *-------------------------------------------------------------
      *
     O          e            h1hdr          1
     O                                           16 'Week'
      *
     O                                           51 'Agg Gross'
     O                                           86 'Aggregate'
     O                                          103 'Cash-Sales'
     O                                          120 'SBF'
     O                                          154 'TF'
      *
     O          e            h1hdr          1
     O                                           17 'Ending'
     O                                           34 'Cash'
     O                                           51 'Revenue $$$-'
     O                                           68 'Sales Exp %-'
     O                                           86 'Sales Expense'
     O                                          103 'Exp Distr'
     O                                          120 'Net Revenue %'
     O                                          137 'SBF Balance'
     O                                          154 'Net Revenue %'
     O                                          171 'TF Balance'
     O                                          188 'Total Net'
      *
     O          e            h1hdr          1
     O                                            6 'Source'
     O                                           16 'Date'
     O                                           34 'Distribution'
     O                                           51 'Balance'
     O                                           68 'Distributed'
     O                                           86 '$$$-Balance'
     O                                          103 '= Net Left'
     O                                          120 '-Distributed'
     O                                          137 'To Pay'
     O                                          154 '-Distributed'
     O                                          171 'to Pay'
     O                                          188 'Balance to Pay'
      *
     O          e            h1hdr          0
     O                       dash               198
      *
      *
      *-------------------------------------------------------------
      * Detail line for Cash Distribution Balance records
      *-------------------------------------------------------------
      *
     O          e            l1dtl       0  0
     O                                            8 'Revenue:'
     O                       l1rwemdy            18 '  /  /  '
     O                       l1aam         k     52
     O                       cbagsxpc            69 '  0.    -%'
     O                       cbasxam       k     87
     O                       cbsnprpc           121 '  0.    -%'
     O                       l1sam         k    138
     O                       cbtnprpc           155 '  0.    -%'
     O                       l1tam         k    172
     O                       l1nam         k    189
      *
      *
      *-------------------------------------------------------------
      * Detail line for Cash Distribution Detail records
      *-------------------------------------------------------------
      *
     O          e            l2dtl       1  0
     O               89                           8 'Cash:'
     O                       l2cdmdy             18 '  /  /  '
     O                       l2cdam        k     35
     O                       l2abalam      j     52
     O                       cdasxcdam     k     69
     O                       l2abalsxam    j     87
     O                       l2leftam      j    104
     O                       cdscdam       k    121
     O                       l2sbalam      j    138
     O                       cdtcdam       k    155
     O                       l2tbalam      j    172
     O                       l2nbalam      j    189
     O               86                         198 'Manual'
      *
      *-------------------------------------------------------------
      * Total line
      *-------------------------------------------------------------
      *
     O          e    85      t1tot       1  1
     O                                           34 '--------------'
     O                                           68 '--------------'
     O                                          120 '--------------'
     O                                          154 '--------------'
      *
     O          e   n85      t1tot       3
      *
     O          e            t1tot          0
     O                       t1desc              19
     O                       arcdam(x)     k     35
     O               80      ar1balam(x)   k     52
     O                       arasxcdam(x)  k     69
     O               80      ar2balam(x)   k     87
     O                       arscdam(x)    k    121
     O               80      ar3balam(x)   k    138
     O                       artcdam(x)    k    155
     O               80      ar4balam(x)   k    172
     O               80      ar5balam(x)   k    189
      *
      *-------------------------------------------------------------
      * Blank
      *-------------------------------------------------------------
      *
     O          e            blank       3
      *
      *-------------------------------------------------------------
      * No data message line
      *-------------------------------------------------------------
      *
     O          e            nodata      1
     O                                           19 'No data meets your'
     O                                           39 'selection criteria.'
      /eject
