     A*%%TS  SD  20080731  151023  ISLRAMS     REL-V5R4M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        MEAT COST
     A*  DISPLAY FILE:  ROD416
     A*  TITLE:         WORK WITH MEAT COST OVERRIDES
     A*  PROGRAMMER:    LEANNE RAMSEY
     A*  DATE:          07/31/08
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/ROPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A                                      CF04(04 'PROMPT')
     A                                      CF05(05 'REFRESH')
     A                                      CF06(06 'CREATE')
     A                                      CF07(07 'LISTING')
     A                                      CF13(13 'FOLD/DROP')
     A                                      CF17(17 'PRINT OPTIONS')
     A                                      CF20(20 'WRKSPLF')
     A*
     A*
     A*---------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*---------------------------------------------------------------
     A          R RO416S1                   SFL
     A*%%TS  SD  20080731  094602  ISLRAMS     REL-V5R4M0  5722-WDS
     A  85                                  SFLNXTCHG
     A            HDEFDT    R        H      REFFLD(@@EFDT)
     A            HDRRN1         4Y 0H
     A            D1OPT          2A  B  9  2CHGINPDFT(UL)
     A  30                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A N30                                  DSPATR(HI)
     A            D1EFDTMDY R        O  9  7REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  ')
     A            D1ITCD    R        O  9 18REFFLD(@@ITCD)
     A                                      EDTCDE(M)
     A            D1OVITCD  R        O  9 28REFFLD(@@ITCD)
     A                                      EDTCDE(M)
     A            D1AIST    R        O  9 41REFFLD(TFREF/@@AIST *LIBL/TFPREF)
     A            D1ITDS    R        O 10 18REFFLD(@@ITDS)
     A                                      COLOR(PNK)
     A            D1OVITDS  R        O 10 50REFFLD(@@ITDS)
     A                                      COLOR(BLU)
     A*
     A*---------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*---------------------------------------------------------------
     A          R RO416C1                   SFLCTL(RO416S1)
     A*%%TS  SD  20080731  094602  ISLRAMS     REL-V5R4M0  5722-WDS
     A                                      SFLSIZ(0050)
     A                                      SFLPAG(0006)
     A N82                                  ROLLUP(25 'PAGE FORWARD')
     A                                      SETOF(30)
     A                                      SETOF(51)
     A                                      SETOF(52)
     A                                      SETOF(53)
     A                                      RTNCSRLOC(&RCD &FLD)
     A                                      OVERLAY
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A  78                                  SFLFOLD(CF13)
     A  79                                  SFLDROP(CF13)
     A                                      SFLMODE(&FOLD)
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            FOLD           1A  H
     A            RCD       R        H      REFFLD(@@RCD)
     A            FLD       R        H      REFFLD(@@FLD)
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 24'WORK WITH MEAT COST OVERRIDES'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USR)
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@JOB)
     A                                      DSPATR(HI)
     A                                  3  2'2=Revise'
     A                                  3 12'4=Delete'
     A                                  4 21'?'
     A                                      DSPATR(HI)
     A                                  4 31'?'
     A                                      DSPATR(HI)
     A                                  4 41'?'
     A                                      DSPATR(HI)
     A                                  5 28'Override'
     A                                      DSPATR(HI)
     A                                  5 39'Active/'
     A                                      DSPATR(HI)
     A                                  6  7'Effective'
     A                                      DSPATR(HI)
     A                                  6 20'Item'
     A                                      DSPATR(HI)
     A                                  6 30'Item'
     A                                      DSPATR(HI)
     A                                  6 38'Inactive'
     A                                      DSPATR(HI)
     A                                  7  2'Opt'
     A                                      DSPATR(HI)
     A                                  7  9'Date'
     A                                      DSPATR(HI)
     A                                  7 20'Code'
     A                                      DSPATR(HI)
     A                                  7 30'Code'
     A                                      DSPATR(HI)
     A                                  7 39'Status'
     A                                      DSPATR(HI)
     A            C1EFDTMDY R        B  8  7REFFLD(TFREF/@@DT06 *LIBL/TFPREF)
     A                                      EDTWRD('  /  /  ')
     A                                      DSPATR(HI)
     A            C1ITCD    R        B  8 18REFFLD(TFREF/@@PRCD *LIBL/TFPREF)
     A                                      DSPATR(HI)
     A  51                                  DSPATR(PC)
     A            C1OVITCD  R        B  8 28REFFLD(TFREF/@@PRCD *LIBL/TFPREF)
     A                                      DSPATR(HI)
     A  52                                  DSPATR(PC)
     A            C1AIST    R        B  8 41REFFLD(TFREF/@@AIST *LIBL/TFPREF)
     A                                      DSPATR(HI)
     A  53                                  DSPATR(PC)
     A*
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R RO416K1
     A*%%TS  SD  20080731  151023  ISLRAMS     REL-V5R4M0  5722-WDS
     A  43                             21  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 22  2'F3=Exit'
     A N99                             22 11'F4=Prompt'
     A                                 22 22'F5=Refresh'
     A                                 22 34'F6=Create'
     A                                 22 45'F7=Listing'
     A                                 22 57'F13=Fold/Drop'
     A                                 23 38'F17=Print Options'
     A                                 23 57'F20=Spool Files'
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *---------------------------------------------------------------
     A          R RO416ES                   SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE CONTROL FORMAT
      *---------------------------------------------------------------
     A          R RO416EC                   SFLCTL(RO416ES)
      *
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
