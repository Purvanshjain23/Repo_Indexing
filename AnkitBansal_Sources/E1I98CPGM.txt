/* ---------------------------------------------------------------- */
/* --------- RETURN CALLING PROGRAM NAME -------------------------- */
/* --------- Copied from J98CPGM         -------------------------- */
/* ---------------------------------------------------------------- */
             PGM        (&SENDER &CALLER)
             DCL        VAR(&SENDER ) TYPE(*CHAR) LEN( 10)
             DCL        VAR(&CALLER ) TYPE(*CHAR) LEN( 10)
             DCL        VAR(&SENDER1) TYPE(*CHAR) LEN( 80)
             DCL        VAR(&MRK    ) TYPE(*CHAR) LEN(  4)
/*                                                                   */
/* --------- Global CPF error intercept ---------------------------- */
/*                                                                   */
             MONMSG     MSGID(CPF0000 CPC0000 CPD0000) EXEC(GOTO +
                          CMDLBL(ABEND))
/*                                                                   */
/* --------- Retrieve calling program name. ------------------------ */
/*                                                                   */
/*     This technique utilizes the SNDPGMMSG command in conjunction  */
/*     with the information returned in the SENDER keyword structure */
/*     on the RCVMSG command as follows:                             */
/*                                                                   */
/*      1-10    Job name                                             */
/*     11-20    User Id                                              */
/*     21-26    Job number                                           */
/*     27-36    Sending programs name(i.e. this program)             */
/*     37-38    Unused                                               */
/*     39-42    Sending programs statement number                    */
/*     43-49    Job Date                                             */
/*     50-55    Job Time                                             */
/* -----------------------------------------------------------       */
/* |   56-65    Receiving programs name(CALLING program)     |       */
/* -----------------------------------------------------------       */
/*     66-69    Receiving programs statement number                  */
/*                                                                   */
/*     By altering the TOPGMQ keyword on the SNDPGMMSG command you   */
/*     can essentially retreive a jobs entire program invocation     */
/*     stack (i.e. TOPGMQ(*PRV &CALLER)) where the starting program  */
/*     is each calling program for each iteration of this process.   */
/*     This also requires altering the PGMQ keyword on the RCVMSG    */
/*     command at the same time (i.e.  PGMQ(*PRV &CALLER)).          */
/*                                                                   */
/*     NOTE:  Retrieving an entire invocation stack will not work if */
/*            there are any recurring programs, such as QCMD, in     */
/*            the invocation stack.                                  */
/*                                                                   */
/*            When the calling program "*EXT" is returned you have   */
/*            reached the beginning of the stack.                    */
/*                                                                   */
/*                                                                   */
             SNDPGMMSG  MSG('X') TOPGMQ(*PRV &SENDER) MSGTYPE(*RQS) +
                          KEYVAR(&MRK)
             RCVMSG     PGMQ(*PRV &SENDER) MSGKEY(&MRK) RMV(*YES) +
                          SENDER(&SENDER1)
             CHGVAR     VAR(&CALLER) VALUE(%SST(&SENDER1 56 10))
             GOTO       EOJ
/*                                                                   */
/* --------- Abnormal end of job processing ------------------------ */
/*                                                                   */
ABEND:
             SNDPGMMSG  MSGID(JDE9999) MSGF(QJDEMSG) +
                          MSGDTA('J98CPGM ') TOPGMQ(*EXT)
             MONMSG     MSGID(CPF0000 CPC0000 CPD0000)
             CHGJOB     LOG(4 00 *SECLVL)
             MONMSG     MSGID(CPF0000 CPC0000 CPD0000)
/*                                                                   */
/* --------- End of job processing --------------------------------- */
/*                                                                   */
EOJ:
             RCLRSC
             MONMSG     MSGID(CPF0000 CPC0000 CPD0000)
             ENDPGM
