      *****************  RPG PROGRAM HEADING  ************************
      * SYSTEM:      HOG PRODUCTION
      * PROGRAM:     HP616
      * TITLE:       DELETED MOVEMENT LISTING
      * PROGRAMMER:  LEANNE FEDOR
      * CREATED:     03/29/96
      *
      *  FUNCTION:   THIS PROGRAM PRINTS A LISTING OF THE AUDIT
      *              INFORMATION KEPT ON DELETED SALES AND TRANSFER
      *              MOVEMENTS.
      *
      *              THIS PROGRAM IS SUBMITTED BY HP416CL WHICH IS
      *              FROM HP416 THROUGH QCMDEXC.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      * 01/11/99  LeAnne Fedor
      *           Recompile only. New field 'scheduled kill date' added
      *           to deleted movements file.
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     FHSP080    IF   E             DISK
      *   DELETED MOVEMENTS          (KEY: DMMVSN)
      *   (RECORDS SELECTED IN OPEN QUERY)
      *
     FQPRINT    O    F  198        PRINTER OFLIND(*INOF)
      /EJECT
      ****************************************************************
      * TABLE/ARRAY SPECIFICATIONS
      ****************************************************************
      *
      *---------------------------------------------------------------
      *  STANDARD MESSAGE ARRAYS
      *---------------------------------------------------------------
     D MGID            S              7    DIM(20)                              MSG ID ARRAY
     D MGDT            S             50    DIM(20)                              MSG PARMS
     D MGW             S              1    DIM(50)                              MSG WORK PARMS
      *
      ****************************************************************
      * INPUT SPECS
      ****************************************************************
      *---------------------------------------------------------------
      *  DATE FORMATS
      *---------------------------------------------------------------
      *
      * DATE IN MMDDYY FORMAT
      *
     D                 DS
     D  WKMDY                  1      6  0
     D  WKMM                   1      2  0
     D  WKDD                   3      4  0
     D  WKYY                   5      6  0
      *
      * DATE IN CCYYMMDD FORMAT
      *
     D                 DS
     D  WKCYMD                 1      8  0
     D  WKCEN                  1      2  0
     D  WKYER                  3      4  0
     D  WKMON                  5      6  0
     D  WKDAY                  7      8  0
      *
      *---------------------------------------------------------------
      *  LDA -  WITH THE REQUESTED PRINT SELECTIONS
      *---------------------------------------------------------------
      *
     D                UDS
     D  LDUSCD                 1     10
      *
     D  LDFDYM                11     18  0
     D  LDTDYM                19     26  0
     D  LDFSYM                27     34  0
     D  LDTSYM                35     42  0
      *
     D  LDMTCD                43     43
      *
     D  LDORBO                44     48
     D  LDORPT                49     53
     D  LDORPP                54     58
     D  LDORFS                59     63  0
      *
     D  LDDNBO                64     68
     D  LDDNPT                69     73
     D  LDDNPP                74     78
     D  LDDNFS                79     83  0
      *
     D  LDCVNO                84     91  0
      *
     D  LDFDMD                92     97  0
     D  LDTDMD                98    103  0
     D  LDFSMD               104    109  0
     D  LDTSMD               110    115  0
      *
     D  LDMTDS               116    135
      *
     D  LDORBD               136    165
     D  LDORTD               166    175
     D  LDORPD               176    205
     D  LDORFN               206    230
      *
     D  LDDNBD               231    260
     D  LDDNTD               261    270
     D  LDDNPD               271    300
     D  LDDNFN               301    325
      *
     D  LDCNAM               326    355
     D  LDUSNM               356    385
      *
     D  LDOUTU               401    410
     D  LDHOLD               411    411
     D  LDCOPY               412    412  0
     D  LDOUTQ               413    422
      *
      *
      *---------------------------------------------------------------
      * STANDARD PROGRAM STATUS DATA STRUCTURE
      *---------------------------------------------------------------
      *    EXTERNALLY DEFINED AS UTPGFR (RECORD FORMAT: PGMDSR)
     D PGMDS         ESDS                  EXTNAME(UTPGFR)
      *
      *---------------------------------------------------------------
      *  NAMED CONSTANTS
      *---------------------------------------------------------------
      *
     D YES             C                   CONST('Y')
     D NO              C                   CONST('N')
      *
     D                 DS
     D  DUMMYD                 1      1
      *  THE PRECEDING DUMMY DATA STRUCTURE IS ONLY TO KEEP THE I-SPEC
      *  INFO FROM RUNNING OVER INTO THE 'CALC SPECS' ON THE COMPILE
      *  LISTING.  IT SERVES NO OTHER PURPOSE AND CAN BE DELETED
      *  AT ANY TIME!!!
      /EJECT
      ****************************************************************
      * MAIN LINE
      ****************************************************************
      *
      *
     C     *IN90         DOUEQ     *ON                                          MAIN DO
     C                   READ      HSP080                                 90
     C     *IN90         IFEQ      *OFF                                         IF NOT EOF
      *
     C     FIRST         IFEQ      YES
     C                   EXCEPT    HRHDR
     C                   MOVEL     NO            FIRST
     C                   ENDIF
      *
      * FLIP SCHEDULED DATE TO MDY FOR PRINTING
      *
     C                   Z-ADD     DMSCDT        WKCYMD
     C                   EXSR      $TOMDY
     C                   Z-ADD     WKMDY         DTSMDY
      *
      * FLIP SHIPPED DATE TO MDY FOR PRINTING
      *
     C                   Z-ADD     DMSHDT        WKCYMD
     C                   EXSR      $TOMDY
     C                   Z-ADD     WKMDY         DTHMDY
      *
      * FLIP RECEIVED DATE TO MDY FOR PRINTING
      *
     C                   Z-ADD     DMRCDT        WKCYMD
     C                   EXSR      $TOMDY
     C                   Z-ADD     WKMDY         DTRMDY
      *
      * FLIP DELETED DATE TO MDY FOR PRINTING
      *
     C                   Z-ADD     DMDMDT        WKCYMD
     C                   EXSR      $TOMDY
     C                   Z-ADD     WKMDY         DTDMDY
      *
      * PRINT DETAIL
      *
     C     *INOF         IFEQ      *ON                                          IF OVERFLOW
     C                   EXCEPT    HRHDR
     C                   ENDIF                                                  IF OVERFLOW
     C                   EXCEPT    DTLINE
      *
     C                   ENDIF                                                  IF NOT EOF
     C                   ENDDO                                                  MAIN DO
      *
      *----------------------------------------------------------------
      * EOF PROCESSING
      *----------------------------------------------------------------
      *  IF NO DETAIL RECORDS MATCHED THE SELECTION CRITERIA, THEN
      *  PRINT THE STANDARD 'NO DATA...' ERROR MESSAGE; OTHERWISE,
      *  PRINT TOTALS
      *
     C     FIRST         IFEQ      YES
     C                   EXCEPT    HRHDR
     C                   EXCEPT    NODATA
     C                   ENDIF
      *
     C                   SETON                                        LR
      /EJECT
      *----------------------------------------------------------------
      * $TOMDY - FLIP DATE FROM YYMMDD TO MMDDYY
      *----------------------------------------------------------------
      *
     C     $TOMDY        BEGSR
      *
     C                   Z-ADD     WKMON         WKMM
     C                   Z-ADD     WKDAY         WKDD
     C                   Z-ADD     WKYER         WKYY
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * *INZSR - INITIALIZATION SUBROUTINE
      *----------------------------------------------------------------
      *
     C     *INZSR        BEGSR
      *
      * DEFINE PRINT FIELDS FOR DETAIL LINE
      *
     C     *LIKE         DEFINE    LDFDMD        DTSMDY
     C     *LIKE         DEFINE    LDFDMD        DTHMDY
     C     *LIKE         DEFINE    LDFDMD        DTRMDY
     C     *LIKE         DEFINE    LDFDMD        DTDMDY
      *
      *----------------------------------------------------------------
      * SET UP REPORT HEADINGS
      *----------------------------------------------------------------
      *
      * ALL USERS
      *
     C     LDUSCD        IFEQ      *BLANK
     C                   MOVEL(P)  'ALL'         LDUSCD
     C                   ENDIF
      *
      * MOVEMENT TYPE
      *
     C     LDMTCD        IFEQ      *BLANK
     C                   MOVEL(P)  'ALL'         LDMTDS
     C                   ENDIF
      *
      * ALL DELETION DATES
      *
     C     LDFDYM        IFEQ      0
     C                   SETON                                        83
     C                   ENDIF
      *
      * ALL SCHEDULED DATES
      *
     C     LDFSYM        IFEQ      0
     C                   SETON                                        84
     C                   ENDIF
      * ALL CUSTOMERS
      *
     C     LDCVNO        IFEQ      0
     C                   SETON                                        89
     C                   ENDIF
      *
      * ALL ORIGIN BUSINESS OFFICES
      *
     C     LDORBO        IFEQ      *BLANK
     C                   MOVEL(P)  'ALL'         LDORBO
     C                   ENDIF
      *
      * ALL DESTINATION BUSINESS OFFICES
      *
     C     LDDNBO        IFEQ      *BLANK
     C                   MOVEL(P)  'ALL'         LDDNBO
     C                   ENDIF
      *
      * ALL ORIGIN FARMS
      *
     C     LDORFS        IFEQ      0                                                         B001
     C                   SETON                                        81
     C                   ENDIF
      *
      * ALL DESTINATION FARMS
      *
     C     LDDNFS        IFEQ      0                                                         B001
     C                   SETON                                        82
     C                   ENDIF
      *
      * ALL ORIGIN PHASES
      *
     C     LDORPP        IFEQ      *BLANK
     C                   MOVEL(P)  'ALL'         LDORPP
     C                   ENDIF
      *
      * ALL DESTINATION PHASES
      *
     C     LDDNPP        IFEQ      *BLANK
     C                   MOVEL(P)  'ALL'         LDDNPP
     C                   ENDIF
      *
      * ALL ORIGIN PRODUCTION TYPES
      *
     C     LDORPT        IFEQ      *BLANK
     C                   MOVEL(P)  'ALL'         LDORPT
     C                   ENDIF
      *
      * ALL DESTINATION PRODUCTION TYPES
      *
     C     LDDNPT        IFEQ      *BLANK
     C                   MOVEL(P)  'ALL'         LDDNPT
     C                   ENDIF
      *
      * RETRIEVE TIME FOR REPORT HEADING
      * SET UP A FIRST TIME FLAG
      * SET UP A DASHED LINE FOR UNDERLINING THE HEADING
      *
     C                   TIME                    RTIME             6 0
     C                   MOVEL     YES           FIRST             1
     C                   MOVEL     *ALL'-'       DASH            198
     C                   ENDSR
      /EJECT
      *-------------------------------------------------------------
      * REPORT HEADING LINES
      *-------------------------------------------------------------
      *
     OQPRINT    E            HRHDR          1 01
     O                       SDPGM               10
     O                                          108 'HOG PRODUCTION SYSTEM'
     O                                          188 'DATE'
     O                       UDATE         Y    198
      *
     O          E            HRHDR          1
     O                       SDUSR               10
     O                                          102 'DELETED MOVEMENTS'
     O                                         +  1 'LISTING'
     O                                          188 'TIME'
     O                       RTIME              198 '  :  :  '
      *
     O          E            HRHDR          1
     O                                          188 'PAGE'
     O                       PAGE          Z    198
      *
      *
     O          E            HRHDR          1
     O                                           22 'USER CODE ............'
     O                       LDUSCD              34
     O                       LDUSNM              66
      *
     O          E            HRHDR          1
     O                                           22 'FROM DELETED DATE ....'
     O              N83      LDFDMD              32 '  /  /  '
     O               83                          27 'ALL'
      *
      *
     O          E            HRHDR          1
     O                                           22 'TO DELETED DATE ......'
     O              N83      LDTDMD              32 '  /  /  '
     O               83                          27 'ALL'
     O                                          102 '*********'
     O                                          120 'ORIGIN SELECTIONS'
     O                                          130 '*********'
     O                                          140 '******'
     O                                          163 'DESTINATION SELECTIONS'
     O                                          171 '*******'
      *
     O          E            HRHDR          1
     O                                           22 'FROM SCHEDULED DATE ..'
     O              N84      LDFSMD              32 '  /  /  '
     O               84                          27 'ALL'
      *
     O                                           91 'BUSINESS OFFICE ...'
     O                       LDORBO              98
     O                       LDORBD             130
     O                       LDDNBO             139
     O                       LDDNBD             171
      *
     O          E            HRHDR          1
     O                                           22 'TO SCHEDULED DATE ....'
     O              N84      LDTSMD              32 '  /  /  '
     O               84                          27 'ALL'
      *
     O                                           91 'PRODUCTION TYPE ...'
     O                       LDORPT              98
     O                       LDORTD             110
     O                       LDDNPT             139
     O                       LDDNTD             151
      *
     O          E            HRHDR          1
     O                                           22 'MOVEMENT TYPE ........'
     O                       LDMTDS              44
      *
     O                                           91 'PRODUCTION PHASE ..'
     O                       LDORPP              98
     O                       LDORPD             130
     O                       LDDNPP             139
     O                       LDDNPD             171
      *
     O          E            HRHDR          1
     O                                           22 'CUSTOMER NUMBER ......'
     O               89                          27 'ALL'
     O              N89      LDCVNO        Z     32
     O                       LDCNAM              66
      *
     O                                           91 'FARM SITE .........'
     O              N81      LDORFS        M     99
     O               81                          96 'ALL'
     O                       LDORFN             125
     O              N82      LDDNFS        M    140
     O               82                         137 'ALL'
     O                       LDDNFN             166
      *
      *
      *-------------------------------------------------------------
      * COLUMN HEADING LINES
      *-------------------------------------------------------------
      *
     O          E            HRHDR       2  1
     O                                          128 '------------'
     O                                          135 'ORIGIN'
     O                                          148 '------------'
      *
     O                                          162 '---------'
     O                                          174 'DESTINATION'
     O                                          184 '---------'
      *
     O          E            HRHDR          1
     O                                            8 'MOVEMENT'
     O                                           19 'SCHEDULED'
     O                                           29 'SHIPPED'
     O                                           39 'RECEIVED'
     O                                           60 '-----------'
     O                                           73 '-------------'
     O                                           82 'DELETION'
     O                                           90 '-------'
     O                                          108 '------------------'
     O                                          114 'MOVE'
      *
     O                                          121 'FARM'
     O                                          134 'BUSINESS'
     O                                          140 'PROD'
     O                                          147 'PROD'
      *   DESTINATION
     O                                          158 'FARM'
     O                                          168 'BUSINESS'
     O                                          175 'PROD'
     O                                          183 'PROD'
     O                                          198 'CUSTOMER'
      *
     O          E            HRHDR          1
     O                                            8 'NUMBER'
     O                                           17 'DATE'
     O                                           27 'DATE'
     O                                           37 'DATE'
     O                                           47 'STATUS'
     O                                           55 'DATE'
     O                                           63 'TIME'
     O                                           70 'USER'
     O                                           85 'COMMENT'
     O                                          114 'TYPE'
      *   ORIGIN
     O                                          121 'SITE'
     O                                          132 'OFFICE'
     O                                          140 'TYPE'
     O                                          148 'PHASE'
      *   DESTINATION
     O                                          158 'SITE'
     O                                          166 'OFFICE'
     O                                          175 'TYPE'
     O                                          184 'PHASE'
     O                                          198 'NUMBER'
      *
     O          E            HRHDR          1
     O                       DASH               198
      *
      *-------------------------------------------------------------
      * DETAIL LINE
      *-------------------------------------------------------------
      *
     O          E            DTLINE         1
     O                       DMMVSN        Z      8
     O                       DTSMDY              19 '  /  /  '
     O                       DTHMDY              29 '  /  /  '
     O                       DTRMDY              39 '  /  /  '
     O                       DMMSCD              45
     O                       DTDMDY              57 '  /  /  '
     O                       DMDMTM              64 '  :  '
     O                       DMUSCD              76
     O                       DMDMCM             108
     O                       DMMTCD             112
      * ORIGIN INFO
     O                       DMORFS        Z    121
     O                       DMORBO             131
     O                       DMORPT             141
     O                       DMORPP             148
      *
      * DESTINATION INFO FOR A TRANSFER
      *
     O                       DMDNFS        Z    158
     O                       DMDNBO             165
     O                       DMDNPT             176
     O                       DMDNPP             184
      *
     O                       DMCVNO        Z    198
      *
      *
      *-------------------------------------------------------------
      * NO DATA MESSAGE LINE
      *-------------------------------------------------------------
     O          E            NODATA         1
     O                                           19 'NO DATA MEETS YOUR'
     O                                           38 'SELECTION CRITERIA'
