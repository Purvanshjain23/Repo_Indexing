/*********************************************************************/
/*  PROGRAM  - PNKNUPC                                               */
/*  SYNOPSIS - Print INTER-COMPANY SALES ADJUSTMENT REPORT AND       */
/*             UPDATE SALES HISTORY, HISTORY DTL ACCRUAL AND         */
/*             SHIPPING DTL ACCRUAL                                  */
/*  DEVELOPER- LARA BUSER                                            */
/*  SYSTEM   - ORDER MANAGEMENT SYSTEM                               */
/*  DATE     - 07/09/2009                                            */
/*                                                                   */
/*********************************************************************/
/*                 MAINTENANCE LOG                                   */
/*-------------------------------------------------------------------*/
/* DATE     PMGR CHANGE DESCRIPTION                                  */
/* 99/99/99 XXX  XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX */
/* -------- ---  --------------------------------------------------- */
/*********************************************************************/
 PNKNUPC:    PGM        PARM(&TDATE)

             DCL        VAR(&OUTQ) TYPE(*CHAR) LEN(10)
             DCL        VAR(&HOLD) TYPE(*CHAR) LEN(4)
             DCL        VAR(&SAVE) TYPE(*CHAR) LEN(4)
             DCL        VAR(&COPYN) TYPE(*DEC) VALUE(1)
             DCL        VAR(&ERROR1) TYPE(*CHAR) LEN(1)
             DCL        VAR(&TYPE) TYPE(*CHAR) LEN(2)
             DCL        VAR(&FDATE) TYPE(*DEC) LEN(7 0)
             DCL        VAR(&TDATE) TYPE(*DEC) LEN(7 0)
             DCL        VAR(&TDATEA) TYPE(*CHAR) LEN(7)
             DCL        VAR(&COPY) TYPE(*DEC) LEN(2 0)
             DCL        VAR(&DATE) TYPE(*CHAR) LEN(10)
             DCL        VAR(&DISTL) TYPE(*CHAR) LEN(10)
             DCL        VAR(&SYSVALUE) TYPE(*CHAR) LEN(10)

/* ERROR MESSAGE FIELDS                                                       */
             DCL        VAR(&MSG) TYPE(*CHAR) LEN(512)
             DCL        VAR(&MSGID) TYPE(*CHAR) LEN(7)
             DCL        VAR(&MSGL) TYPE(*DEC) LEN(5 0)

/*------------------------------------------------------------------*/
/*   INITIALIZE VARIABLES                                           */
/*------------------------------------------------------------------*/
             CHGVAR     VAR(&COPY) VALUE(&COPYN)
             CHGVAR     VAR(&HOLD) VALUE(*YES)
             CHGVAR     VAR(&SAVE) VALUE(*YES)
             CHGVAR     VAR(&TDATEA) VALUE(&TDATE)
             CHGVAR     VAR(&TYPE) VALUE(IS)

/*------------------------------------------------------------------*/
/* PRINT IC SALES ADJUSTMENT REPORT, UPDATE FILES, EMAIL REPORT     */
/*   RETRIEVE OUTQ FROM SYSTEM VALUES FILE                          */
/*   RETRIEVE DISTRIBUTION LIST FROM SYSTEM VALUES FILE             */
/*------------------------------------------------------------------*/
             CHGVAR     VAR(&SYSVALUE) VALUE(SLSADJPRT)
             CALL       PGM(PPJXXFR) PARM('' &SYSVALUE &OUTQ)
             OVRPRTF    FILE(PNKLPFR$) OUTQ(&OUTQ) COPIES(&COPY) +
                          HOLD(&HOLD) SAVE(&SAVE)
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(MSGTRAP))

/* PRINT THE REPORT AND UPDATE THE FILES                            */
             CALL       PGM(PNKLPFR) PARM(' ' &TYPE &TDATE)
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(MSGTRAP))

/* EMAIL THE REPORT TO THE DISTRIBUTION LIST                        */
/*   RETRIEVE DISTRIBUTION LIST FROM SYSTEM VALUES FILE             */
             ADDLIBLE   LIB(ESEND) POSITION(*LAST)
             MONMSG     MSGID(CPF0000)
             CHGVAR     VAR(&SYSVALUE) VALUE(ISADISTL)
             CALL       PGM(PPJXXFR) PARM('' &SYSVALUE &DISTL)
             CVTDAT     DATE(&TDATEA) TOVAR(&DATE) FROMFMT(*CYMD) +
                          TOFMT(*MDYY) TOSEP(/)
             ESNDMAIL   RECIPIENT(&DISTL) SUBJECT('Inter-Company +
                          Sales Adjustment Report') +
                          MSG('Inter-Company Sales Adjustment +
                          Report for week ending' *BCAT &DATE *CAT +
                          ', sent to Distribution List: ' *CAT +
                          &DISTL) ATTLIST((* *PDF *N PNKLPFR$))
             GOTO       CMDLBL(END)

/*----------------------RECEIVE MESSAGES----------------------------*/
 MSGTRAP:    RCVMSG     MSGTYPE(*EXCP) MSGDTA(&MSG) MSGDTALEN(&MSGL) +
                          MSGID(&MSGID)
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(END))

             SNDPGMMSG  MSGID(&MSGID) MSGF(QCPFMSG) MSGDTA(%SST(&MSG +
                          1 &MSGL)) MSGTYPE(*ESCAPE)

/*------------------------------------------------------------------*/
 END:        ENDPGM
             DLTOVR     FILE(*ALL)
