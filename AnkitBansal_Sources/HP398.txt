      *****************  RPG PROGRAM HEADING  ************************
      * SYSTEM:      HOG PRODUCTION
      * PROGRAM:     HP398
      * TITLE:       BUILD WORKFILES--CLOSED GROUP RATION REPORT
      * PROGRAMMER:  LEANNE FEDOR
      * CREATED:     09/18/95
      *
      *  FUNCTION:   THIS PROGRAM RETRIEVES DETAIL RATION INFO FOR
      *              CLOSED GROUPS AND CREATES TWO WORKFILES:
      *
      *                    HSP307 - WORKFILE OF GROUP RATIONS
      *                    HSP308 - WORKFILE OF COLUMNS
      *
      *              THE ULTIMATE REPORT WILL PRINT A DETAIL LINE FOR
      *              EACH CLOSED GROUP. THE REPORT COLUMNS SHOW RATION
      *              DATA PER RATION WITH THE 'RATION' CODE APPEARING
      *              AS THE COLUMN HEADING. THE REPORT IS LIMITED TO
      *              16 UNIQUE RATIONS. IF MORE THAN 16 RATIONS ARE
      *              CONSUMED IN THE PRODUCTION PHASE/TYPE, THEN THE
      *              LAST COLUMN WITH BE 'OTHER' AND INCLUDE SUMMARIZED
      *              DATA FOR ALL RATIONS EXCEEDING 16.
      *
      *              THIS WORK PROGRAM RETRIEVES ALL RATIONS EATEN BY A
      *              GROUP  AND DETERMINES WHICH 16 RATIONS WILL
      *              BE USED FOR UNIQUE COLUMN HEADINGS FOR THE
      *              PRODUCTION PHASE/TYPE.  IF SOME RATIONS MUST BE
      *              LUMPED INTO THE 'OTHER' CATEGORY, THE RATIONS WITH
      *              THE LEAST TOTAL POUNDS OF FEED CONSUMED FOR THE
      *              PRODUCTION PHASE/TYPE WILL BE LUMPED TOGETHER.
      *
      *              REPORT SELECTIONS ARE PASSED IN THE LDA.
      *              THIS PROGRAM IS SUBMITTED WITH QCMDEXC FROM HP498
      *              USING CLP HP498CL.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      * 03/19/99  LEANNE FEDOR
      *           FIXED BUG WITH LOGIC IN SUBROUTINE THAT CREATED THE
      *           'OTHER' COLUMN FOR RATIONS. THE LOGIC WAS NOT
      *           SAVING THE CONTROL BREAK VALUES CORRECTLY.
      *
      * 08/09/96  LEANNE FEDOR
      *           RECOMPILE ONLY. ADDED TWO NEW FIELDS TO HSP387:
      *                ADJUSTED FEED CONVERSION
      *                AVERAGE CALORIES PER POUND OF FEED
      *
      * 08/14/96  LEANNE FEDOR
      *           RECOMPILE ONLY. ADDED A NEW FIELD TO HSP387:
      *                ADJUSTED AVERAGE DAILY GAIN
      *
      * 03/09/98  LeAnne Fedor
      *           Recompile only. Feed pounds and feed amount fields
      *           increased in size per Nathan Malone.
      *
      * 08/17/00  LeAnne Fedor
      *           Recompile only. A new field 'adjusted transfer in head'
      *           was added to the group workfile HSP387.
      *
      * 01/30/01  LeAnne Fedor
      *           Recompile only.  Production type was removed from feed files.
      *
      * 03/26/20  Danny Nguyen - P405 - Farm Number Increase
      *           Recompile only. Increased Bin Code (@@BNCD) in HSPREF file from 5A to 6A.
      *
      * 01/21/21  ISE            TSN593
      *           Replaced *loval with *start
      *
      * 05/10/22 Eric L SDN736 Recompiled ticket nbr increase (TKNO & RTNO 7.0 TO 9.0)
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     FHSL038E   IF   E           K DISK
      *   FEED TICKET DETAIL          (KEY: FDHGSN)
      *
     FHSP307    UF A E           K DISK
      *    WORKFILE OF GROUP RATIONS  (KEY: WRHGSN WRRNCD)
      *
     FHSL307A   IF   E           K DISK
     F                                     RENAME(WRREC:WRRECA)
      *    WORKFILE OF GROUP RATIONS  (KEY: WRPPCD WRPTCD WRRNCD)
      *
     FHSP308    O    E             DISK
      *    WORKFILE OF COLUMNS        (KEY: WCPPCD WCPTCD WCRNCD)
      *
     FHSL308A   UF A E           K DISK
     F                                     RENAME(WCREC:WCRECA)
      *    WORKFILE OF COLUMNS
      *    (KEY: WCPPCD WCPTCD WCFDLB-DESCENDING)
      *
     FHSP387    IF   E             DISK
      *    WORKFILE OF GROUPS         (KEY: WGHGCD)
      *
      /EJECT
      ****************************************************************
      * INPUT SPECS
      ****************************************************************
      *
      *---------------------------------------------------------------
      * STANDARD PROGRAM STATUS DATA STRUCTURE
      *---------------------------------------------------------------
      *    EXTERNALLY DEFINED AS UTPGFR (RECORD FORMAT: PGMDSR)
     D PGMDS         ESDS                  EXTNAME(UTPGFR)
      *
      *---------------------------------------------------------------
      *  LDA -  WITH THE REQUESTED PRINT SELECTIONS
      *---------------------------------------------------------------
      *
     D                UDS
     D  LDFSBO                 1      5
     D  LDFYMD                 6     13  0
     D  LDTYMD                14     21  0
     D  LDPTCD                22     26
     D  LDPPCD                27     31
     D  LDFSCD                32     36  0
     D  LDCELL                50     51  0
      *
      *---------------------------------------------------------------
      *  NAMED CONSTANTS
      *---------------------------------------------------------------
      *
     D YES             C                   CONST('Y')
     D NO              C                   CONST('N')
      *
      /EJECT
      ****************************************************************
      * MAIN LINE
      ****************************************************************
      *
      * READ EACH HOG GROUP RECORD IN THE HOG GROUP WORKFILE CREATED
      * IN THE PREVIOUS JOB STEP IN WORK PROGRAM HP388. RETRIEVE ALL
      * RATION DETAIL FOR EACH GROUP AND WRITE A WORKFILE RECORD FOR
      * EACH GROUP/RATION.
      *
     C     *IN90         DOUEQ     *ON                                          MAIN DO
     C                   READ      HSP387                                 90
     C     *IN90         IFEQ      *OFF                                         IF NOT EOF
      *
     C                   EXSR      $FEED
      *
     C                   ENDIF                                                  IF NOT EOF
     C                   ENDDO                                                  MAIN DO
      *
      *--------------------------------------------------------------
      * EOF PROCESSING
      *--------------------------------------------------------------
      *
      * CREATE THE RECORDS FOR THE 'COLUMN' WORKFILE.
      *
     C                   EXSR      $COL
      *
      * CLEAN-UP THE 'COLUMN' WORKFILE DELETING UNIQUE RATION RECORDS
      * THAT EXCEED THE 16-COLUMN LIMIT AND CREATING A NEW 'OTHER'
      * RECORD.
     C                   EXSR      $OTHER
      *
     C                   SETON                                        LR
      /EJECT
      *----------------------------------------------------------------
      * $FEED - FEED CONSUMED PER RATION FOR EACH GROUP
      *----------------------------------------------------------------
      *
      * THIS SUBROUTINE CREATES A RATION WORKFILE RECORD FOR EACH
      * GROUP/RATION COMBINATION.
      *
     C     $FEED         BEGSR
      *
     C                   MOVEL     WGPPCD        WRPPCD
     C                   MOVEL     WGPTCD        WRPTCD
     C                   Z-ADD     WGHGSN        WRHGSN
      *
      * PROCESS ALL FEED TICKET DETAIL RECORDS FOR THE GROUP
      *
     C     WGHGSN        SETLL     HSL038E
      *
     C     *IN91         DOUEQ     *ON                                          DO FEED
     C     WGHGSN        READE     HSL038E                                91
     C     *IN91         IFEQ      *OFF                                         IF NOT EOF
      *
      * IF A WORKFILE RECORD FOR THIS GROUP/RATION ALREADY
      * EXISTS, ACCUMULATE THE FEED POUNDS; OTHERWISE, WRITE
      * A NEW RECORD.
      *
     C     K1P307        CHAIN     HSP307                             92
     C     *IN92         IFEQ      *OFF
     C                   ADD       FDFDLB        WRFDLB
     C                   UPDATE    WRREC
     C                   ELSE
     C                   MOVEL     FDRNCD        WRRNCD
     C                   Z-ADD     FDFDLB        WRFDLB
     C                   WRITE     WRREC
     C                   ENDIF
      *
     C                   ENDIF                                                  IF NOT EOF
     C                   ENDDO                                                  DO FEED
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $COL - ACCUMULATE FEED POUNDS BY PHASE/TYPE/RATION
      *----------------------------------------------------------------
      *
      * READ THE GROUP RATION WORKFILE YOU HAVE JUST CREATED AND
      * WRITE A PRODUCTION PHASE/TYPE/RATION RECORD FOR EACH
      * UNIQUE COMBINATION TO THE 'COLUMN' WORKFILE. THESE RECORDS
      * WILL ULTIMATELY BE USED AS THE COLUMNS ON THE REPORT.
      *
     C     $COL          BEGSR
      *
593  C*    *LOVAL        SETLL     HSL307A
593  C     *START        SETLL     HSL307A
      *
     C     *IN90         DOUEQ     *ON                                          DO RATIONS
     C                   READ      HSL307A                                90
     C     *IN90         IFEQ      *OFF                                         IF NOT EOF
      *
     C     FIRST         IFEQ      YES
     C                   MOVEL     NO            FIRST
     C                   MOVEL     WRPPCD        SVPPCD
     C                   MOVEL     WRPTCD        SVPTCD
     C                   MOVEL     WRRNCD        SVRNCD
     C                   Z-ADD     0             WKLB
     C                   ENDIF
      *
     C     WRPPCD        IFNE      SVPPCD
     C     WRPTCD        ORNE      SVPTCD
     C     WRRNCD        ORNE      SVRNCD
     C                   EXSR      $WR308
     C                   ENDIF
      *
     C                   ADD       WRFDLB        WKLB
      *
     C                   ENDIF                                                  IF NOT EOF
     C                   ENDDO                                                  DO RATIONS
      *
      * WRITE OUT LAST RECORD
      *
     C     FIRST         IFEQ      NO
     C                   EXSR      $WR308
     C                   ENDIF
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $OTHER - ELIMINATE EXCESS RATIONS
      *----------------------------------------------------------------
      *
      * THE PRINTED REPORT ONLY HAS ROOM FOR 17 COLUMNS OF RATIONS.
      * THE FIRST 16 COLUMNS WILL BE FOR INDIVIDUAL RATIONS AND THE
      * 17TH COLUMN ON THE REPORT WILL BE 'OTHER'. 'OTHER' WILL BE A
      * SUMMARY OF FEED DATA FOR ALL RATIONS EXCEEDING 16.
      *
      * IF MORE THAN 16 RECORDS (RATIONS) EXIST FOR A PRODUCTION
      * PHASE/TYPE, DELETE THE WORKFILE RECORDS FOR THE RATIONS WITH
      * THE LEAST TOTAL CONSUMED POUNDS FOR THE PRODUCTION PHASE/TYPE
      * AND CREATE A NEW RECORD FOR 'OTHER'.
      *
     C     $OTHER        BEGSR
      *
     C                   MOVE      *BLANK        WCPPCD
     C                   MOVE      *BLANK        WCPTCD
     C     K2L308        SETLL     HSL308A
     C                   MOVEL     YES           FIRST
      *
     C     *IN90         DOUEQ     *ON                                          DO COLUMNS
     C                   READ      HSL308A                                90
     C     *IN90         IFEQ      *OFF                                         IF NOT EOF
      *
     C     FIRST         IFEQ      YES
     C                   MOVEL     NO            FIRST
     C                   MOVEL     WCPPCD        SVPPCD
     C                   MOVEL     WCPTCD        SVPTCD
     C                   Z-ADD     0             COUNT
     C                   Z-ADD     0             WKLB
     C                   ENDIF
      *
      * IF NECESSARY, CREATE A COLUMN RECORD FOR 'OTHER'; BUT SET THE
      * POUNDS TO ZERO SO THAT THE 'OTHER' COLUMN WILL ALWAYS PRINT
      * LAST ACROSS THE PAGE. BEFORE WRITING THE RECORD, SAVE THE
      * PHASE AND TYPE OF THE NEXT RECORD TO PROCESS IN WORKFIELDS.
      * THE WORKFIELD VALUES WILL BE MOVED TO THE SAVE FIELDS LATER
      * FOR CONTROL BREAK LOGIC.
      *
     C     WCPPCD        IFNE      SVPPCD                                       IF BREAK
     C     WCPTCD        ORNE      SVPTCD
     C                   MOVEL     WCPPCD        WKPPCD
     C                   MOVEL     WCPTCD        WKPTCD
      *
     C     WKLB          IFGT      0                                            IF OTHER ONE
     C                   MOVEL     SVPPCD        WCPPCD
     C                   MOVEL     SVPTCD        WCPTCD
     C                   MOVEL     'OTHER'       WCRNCD
     C                   Z-ADD     0             WCFDLB
     C                   WRITE     WCRECA
     C                   ENDIF                                                  IF OTHER ONE
      *
     C                   Z-ADD     0             COUNT
     C                   Z-ADD     0             WKLB
     C                   MOVEL     WKPPCD        SVPPCD
     C                   MOVEL     WKPTCD        SVPTCD
     C                   ENDIF                                                  IF BREAK
      *
     C                   ADD       1             COUNT
      *
     C     COUNT         IFGT      16
     C                   ADD       WCFDLB        WKLB
     C                   DELETE    WCRECA
     C                   ENDIF
      *
     C                   ENDIF                                                  IF NOT EOF
     C                   ENDDO                                                  DO COLUMNS
      *
      * WRITE OUT THE LAST 'OTHER' RECORD IF REQUIRED
      *
     C     WKLB          IFGT      0
     C                   MOVEL     SVPPCD        WCPPCD
     C                   MOVEL     SVPTCD        WCPTCD
     C                   MOVEL     'OTHER'       WCRNCD
     C                   Z-ADD     0             WCFDLB
     C                   WRITE     WCRECA
     C                   ENDIF
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $WR308 - WRITE A RECORD TO THE 'COLUMNS' WORKFILE
      *----------------------------------------------------------------
      *
     C     $WR308        BEGSR
      *
     C                   MOVEL     SVPPCD        WCPPCD
     C                   MOVEL     SVPTCD        WCPTCD
     C                   MOVEL     SVRNCD        WCRNCD
     C                   Z-ADD     WKLB          WCFDLB
     C                   WRITE     WCREC
     C                   MOVEL     WRPPCD        SVPPCD
     C                   MOVEL     WRPTCD        SVPTCD
     C                   MOVEL     WRRNCD        SVRNCD
      *
     C                   Z-ADD     0             WKLB
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * *INZSR - INITIALIZATION SUBROUTINE
      *----------------------------------------------------------------
      *
     C     *INZSR        BEGSR
      *
      * KEY FIELDS
      *
      * WORKFILE FOR GROUP RATIONS
      *
     C     K1P307        KLIST
     C                   KFLD                    WGHGSN
     C                   KFLD                    FDRNCD
      *
      * WORKFILE FOR COLUMNS
      *
     C     K2L308        KLIST
     C                   KFLD                    WCPPCD
     C                   KFLD                    WCPTCD
      *
      * WORKFIELDS
     C                   Z-ADD     0             WKLB             15 0
      *
      * SAVE FIELDS AND FLAGS TO CONTROL PROCESSING
      *
     C     *LIKE         DEFINE    WRPPCD        SVPPCD
     C     *LIKE         DEFINE    WRPTCD        SVPTCD
     C     *LIKE         DEFINE    WRRNCD        SVRNCD
     C     *LIKE         DEFINE    WRPPCD        WKPPCD
     C     *LIKE         DEFINE    WRPTCD        WKPTCD
     C                   MOVEL     YES           FIRST             1
     C                   Z-ADD     0             COUNT             3 0
      *
     C                   ENDSR
      /EJECT
