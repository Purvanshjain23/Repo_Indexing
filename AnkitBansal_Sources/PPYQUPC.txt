/********************************************************************/
/*                                                                  */
/*  PROGRAM  - PPYQUPC                                              */
/*  SYNOPSIS - PRINT PROGRAM WITH SELECTIONS FOR LATE DELIVERY RPT. */
/*             COMPANY, DATES, LOAD, CARRIER, LATE REASON,          */
/*             EXCLUDE EXPORT                                       */
/*                                                                  */
/*             THIS PROGRAM IS CALLED FROM PDUCUPC WHICH SUBMITS    */
/*             USING A VARIABLE &PGM. SOME OF THE PARMS ARE USED    */
/*             HERE AND SOME IN ONE OF THE OTHER TWO PROGRAMS.      */
/*                                                                  */
/*  SYSTEM   - ORDER MANAGEMENT SYSTEM                              */
/*  PROGRAM  - PRINT LATE DELIVERY REPORT BY ACTUAL SHIPPED DATE    */
/*  DATE     - 06/14/2006                                           */
/*                                                                  */
/*  PARAMETERS RECEIVED:                                            */
/*                                                                  */
/*    &COMPNYN    - COMPANY NUMBER - BLANK FOR ALL                  */
/*    &FRMDATN    - FROM SHIP DATE                                  */
/*    &THRDATN    - THROUGH SHIP DATE                               */
/*    &LOADN      - LOAD NUMBER                                     */
/*    &QTR        - QUARTER                                         */
/*    &YEARN      - YEAR                                            */
/*    &SALESP     - SALES PERSON (CURRENTLY BLANKS)                 */
/*    &CARRIER    - CARRIER CODE                                    */
/*    &CUST       - CUST                                            */
/*    &LOADT      - LOAD TYPE                                       */
/*    &OUTQ       - OUTPUT QUEUE                                    */
/*    &HOLD       - HOLD PRINT                                      */
/*    &SAVE       - SAVE PRINT                                      */
/*    &COPYN      - NUMBER OF COPIES                                */
/*    &NIGHTQ     - NIGHT OUTPUT QUEUE                              */
/*    &SELRPT     - REPORT SELECTION TYPE                           */
/*    &UOM        - UNIT OF MEASURE                                 */
/*    &OTRGROUP   - ON TIME REASON CODE GROUP CODE                  */
/*    &EXPORTYN   - INCLUDE EXPORTS ON THE REPORT - YES OR NO       */
/*    &OTREAS     - ON TIME REASON CODE                             */
/*    &OTRDEPT    - ON TIME REASON DEPARTMENT CODE                  */
/*    &EMAIL      - EMAIL ADDRESS                                   */
/*                                                                  */
/********************************************************************/
/*  LJB 3/25/2006 ADDED ON TIME REASON AND ON TIME DEPARTMENT       */
/*                CODES TO PARMS AND PASS TO CALLED PROGRAMS        */
/*  LJB 11/06/2014 REACTIVATED SALESPERSON                          */
/*  LJB 04/16/2015 PASS IN EMAIL, EMAIL REPORT IF NOT BLANKS        */
/*  RMC 02/13/2020 PASS IN XLS FLAG, AND EMAIL XLS IF NOT EMAIL NOT '' */
/*                 AND THIS XLS FLAG IS YES, ELSE EMAIL THE RPT PDF  */
/* PIO 2/26/24 DDSDDL - REPLACE OPNQRYF WITH OPNSQLF FOR OMFJCPP FILE*/
/*                      ADDED NEEDED OVRDBF AROUND THE COMMAND.      */
/*                      CHANGE *AND TO AND, *EQ TO = , *GE TO >=,    */
/*                      *LE TO <= IN &G                              */
/*                                                                   */
/********************************************************************/
 PPYQUPC:    PGM        PARM(&COMPNYN &FRMDATN &THRDATN &LOADN &QTR +
                          &YEARN &SALESP &CUST &CARRIER &LOADT +
                          &OUTQ &HOLD &SAVE &COPYN &NIGHTQ &SELRPT +
                          &UOM &OTRGROUP &EXPORTYN &OTREAS &OTRDEPT +
                 &EMXLS   &EMAIL1 &EMAIL2)

/* PARMS                                                            */
        DCL    &COMPNYN    *DEC          /* COMPANY NUMBER (PASSED) */
        DCL    &FRMDATN    *DEC          /* FROM SHIP DATE (PASSED) */
        DCL    &THRDATN    *DEC          /* THRU SHIP DATE (PASSED) */
        DCL    &LOADN      *DEC          /* LOAD NUMBER    (PASSED) */
        DCL    &CUST       *DEC          /* CUST           (PASSED) */
        DCL    &QTR        *CHAR   (1)   /* QUARTER                 */
        DCL    &YEARN      *DEC          /* YEAR           (PASSED) */
        DCL    &SALESP     *CHAR   (3)   /* SALESPERSON             */
        DCL    &CARRIER    *CHAR   (3)   /* CARRIER CODE            */
        DCL    &LOADT      *CHAR   (1)   /* LOAD TYPE               */
        DCL    &DESC       *CHAR   (25)  /* DESCRIPTION             */
        DCL    &COPYN      *DEC          /* NO. OF COPIES           */
        DCL    &OUTQ       *CHAR   (10)  /* OUTPUT QUEUE            */
        DCL    &HOLD       *CHAR   (4)   /* HOLD STATUS             */
        DCL    &SAVE       *CHAR   (4)   /* SAVE STATUS             */
        DCL    &NIGHTQ     *CHAR   (10)  /* NIGHT JOB QUEUE         */
        DCL    &COMMAND    *CHAR   (512)
        DCL    &SELRPT     *CHAR   (1)   /* REPORT SELECTOR         */
        DCL    &OTRGROUP   *CHAR   (1)   /* ON TIME REASON GROUP    */
        DCL    &PRTF       *CHAR   (7)   /* PGM NAME FOR SBDPRTOVR  */
        DCL    &PRTFO      *CHAR   (8)   /* PGM NAME FOR SBDPRTOVR  */
        DCL    &UOM        *CHAR   (2)   /* UNIT OF MEASURE         */
        DCL    &EXPORTYN   *CHAR   (1)   /* EXCLUDE EXPORTS Y OR N  */
        DCL    &OTREAS     *CHAR   (3)   /* ON TIME REASON CODE     */
        DCL    &OTRDEPT    *CHAR   (2)   /* ON TIME REASON DEPT     */
        DCL    &EMXLS      *CHAR   (1)  /* EMAIL XLS VERSION 2-13-20  */
        DCL    &EMAIL      *CHAR   (50)  /* EMAIL ADDRESS           */
        DCL    &EMAIL1     *CHAR   (25)  /* EMAIL ADDRESS           */
        DCL    &EMAIL2     *CHAR   (25)  /* EMAIL ADDRESS           */

/* INTERNAL FIELDS                                                  */
        DCL    &COPY       *DEC    (2 0) /* NUMBER OF COPIES        */
        DCL    &COMPANY    *DEC    (3 0) /* COMPANY NUMBER          */
        DCL    &FRMDAT     *DEC    (7 0) /* FROM SHIP DATE          */
        DCL    &THRDAT     *DEC    (7 0) /* THRU SHIP DATE          */
        DCL    &LOAD       *DEC    (7 0) /* LOAD NUMBER             */
        DCL    &USER       *CHAR   (10)  /* USER PROFILE            */
        DCL    &GRPPRF     *CHAR   (10)  /* GROUP PROFILE           */

/* OPNQRYF FIELDS                                                   */
        DCL    &COMPA      *CHAR    3    /* ACK COMPLETE            */
        DCL    &FRMDATA    *CHAR    7    /* FROM SHIP DATE (PASSED) */
        DCL    &THRDATA    *CHAR    7    /* THRU SHIP DATE (PASSED) */

        DCL     &A        *CHAR   40     /* QRY COMPANY             */
        DCL     &B        *CHAR   50     /* QRY FROM AND TO DATE    */
        DCL     &D        *CHAR  120     /* QRY ALL                 */
/********************************************************************/
/* CHANGE VARIABLES FOR USE IN PROGRAM                              */
/********************************************************************/
             MONMSG     MSGID(CPF0000)
             RTVJOBA    USER(&USER)
             RTVUSRPRF  USRPRF(&USER) GRPPRF(&GRPPRF)

             CHGVAR     VAR(&COMPANY) VALUE(&COMPNYN)
             CHGVAR     VAR(&COMPA) VALUE(&COMPANY)
             CHGVAR     VAR(&FRMDAT) VALUE(&FRMDATN)
             CHGVAR     VAR(&FRMDATA) VALUE(&FRMDAT)
             CHGVAR     VAR(&THRDAT) VALUE(&THRDATN)
             CHGVAR     VAR(&THRDATA) VALUE(&THRDAT)
             CHGVAR     VAR(&LOAD) VALUE(&LOADN)
             CHGVAR     VAR(&COPY) VALUE(&COPYN)

/*------------------------------------------------------------------*/
/* STORES THE COUNTS FOR SUMMARY PERCENTAGES (BOTTOM OF REPORT)     */
/*------------------------------------------------------------------*/
             CRTDUPOBJ  OBJ(PPB6CPP) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)
             CRTDUPOBJ  OBJ(PPB6CPL0) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)
             CRTDUPOBJ  OBJ(PPB6CPL1) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)
             CRTDUPOBJ  OBJ(PPB6CPL2) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)
/*------------------------------------------------------------------*/
/* WORKFILE FOR EMAILING THE XLS VERSION                          */
/*------------------------------------------------------------------*/
             CRTDUPOBJ  OBJ(PDM7CPP) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)
             CRTDUPOBJ  OBJ(PDM7CPL0) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)
             CRTDUPOBJ  OBJ(PDM7CPL1) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)

/*-------------------------------------------------------------------*/
/* CREATE OPEN QUERY FILE TO SELECT BY ACTUAL SHIP DATE AND/OR       */
/* COMPANY NUMBER                                                    */
/*-------------------------------------------------------------------*/
             IF         COND(&COMPA *GT '000') THEN(DO)
   /*PIO     CHGVAR     VAR(&A) VALUE('FJAIC3 *EQ ' *CAT +  +
                         &COMPA *BCAT '*AND')              */
   /*PIO*/   CHGVAR     VAR(&A) VALUE('FJAIC3  =  ' *CAT +
                         &COMPA *BCAT ' AND')
                         ENDDO

   /*PIO     CHGVAR     VAR(&B) VALUE('(FJF9DT *GE ' *CAT +  +
                         &FRMDATA *CAT ' *AND FJF9DT *LE +   +
                         ' *CAT &THRDATA *CAT ')')          */
   /*PIO*/   CHGVAR     VAR(&B) VALUE('(FJF9DT >=  ' *CAT +
                         &FRMDATA *CAT '  AND FJF9DT <=  +
                         ' *CAT &THRDATA *CAT ')')

             CHGVAR     VAR(&D) VALUE(&A *BCAT &B)

/*  RUN QUERY TO READ LOAD HEADER BY ACTUAL SHIP DATE, CARRIER       */

    /*PIO    OVRDBF     FILE(OMFJCPME) SHARE(*YES)      +
             OPNQRYF    FILE((OMFJCPME)) QRYSLT(&D) KEYFLD(*FILE) */

    /*PIO*/  OVRDBF     FILE(OMFJCPP) TOFILE(OMFJCPP) LVLCHK(*NO)
             OPNSQLF    SQL('Select * from OMFJCPP Where  ' *BCAT +
                          &D     *BCAT ' Order by FJF9DT ASC, +
                          FJBZNA ASC ') OPNID(OMFJCPME) +
                          OPTION(*ALL) SEQONLY(*NO) RCDFMT(@FJCPGC) +
                          ALWCPY(*IFRQD)
    /*PIO*/  OVRDBF     FILE(OMFJCPME) TOFILE(OMFJCPP) LVLCHK(*NO) +
                         OVRSCOPE(*JOB) SHARE(*YES) OPNSCOPE(*JOB)


/*-------------------------------------------------------------------*/
/* OVERRIDE PRINTER FILE WITH USER SPECIFIED SELECTION VALUES        */
/*-------------------------------------------------------------------*/
             OVRPRTF    FILE(PPYMPFR$) OUTQ(&OUTQ) COPIES(&COPY) +
                          HOLD(&HOLD) SAVE(&SAVE)

             IF         COND(&EMAIL *NE '                   ') THEN(DO)
             CHGVAR     VAR(&HOLD) VALUE(*YES)
             CHGVAR     VAR(&SAVE) VALUE(*YES)
             ENDDO
/*-------------------------------------------------------------------*/
/*   CALL PRINT PROGRAM -
/*-------------------------------------------------------------------*/
             CALL       PGM(PPYMPFR) PARM(' ' &LOAD &COMPANY +
                          &CARRIER &LOADT &THRDAT &UOM &QTR &YEARN +
                          &FRMDATN &THRDATN &SALESP &CUST &EXPORTYN +
                &EMXLS    &OTREAS &OTRGROUP &OTRDEPT)

             CHGVAR     VAR(&EMAIL) VALUE(&EMAIL1 *TCAT &EMAIL2)
             IF         COND(&EMAIL1 *NE '                   ') THEN(DO)
             IF         COND(&EMXLS *NE 'Y') +
                          THEN(ESEND/ESNDMAIL RECIPIENT(&EMAIL) +
                          SUBJECT('Late Delivery Report') +
                          ATTLIST((* *DFT *N PPYMPFR$)))

             IF         COND(&EMXLS *EQ 'Y') THEN(EXECUTE +
                          VIEW(LDACTDV) PCFMT(*XLS) +
                          RECIPIENT(&EMAIL) EMLMSG('Late Delivery +
                          Report-by Actual Ship'))

                          ENDDO

             CLOF       OPNID(OMFJCPME)
    /*PIO    DLTOVR     FILE(OMFJCPME)    */
             DLTOVR     FILE(OMFJCPME) LVL(*JOB) /*PIO*/

/**** FOR TESTING ONLY - SAVE WORKFILE CONTENTS               +
             IF         COND(&GRPPRF *EQ 'SBDPGMR') THEN(DO)  +
             CPYF       FROMFILE(PPB6CPP) +                   +
                          TOFILE(OMSDEVGEN/PPB6CPPSV) +
                          MBROPT(*REPLACE) CRTFILE(*YES)      +
             MONMSG     MSGID(CPF9999)                         +
             ENDDO                                   ****/

 END:        ENDPGM
