     A*%%TS  SD  20021109  102847  ISLRAMS     REL-V5R1M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD4085
     A*  TITLE:         SPECIFY FOR GROUP SIRE LINE/SOW SOURCE INQUIRY
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  DATE:          11/06/02
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A N99                                  CF04(04 'PROMPT')
     A N99                                  CF20(20 'WRKSPLF')
     A*
     A*---------------------------------------------------------------
     A* RECORD FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP4085R1
     A*%%TS  SD  20021109  102847  ISLRAMS     REL-V5R1M0  5722-WDS
     A                                      SETOF(31)
     A                                      SETOF(32)
     A                                      SETOF(33)
     A                                      SETOF(34)
     A                                      SETOF(51)
     A                                      SETOF(52)
     A                                      SETOF(53)
     A                                      SETOF(54)
     A                                      OVERLAY
     A                                      RTNCSRLOC(&RCD &FLD)
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 27'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  2 15'SPECIFY FOR GROUP SIRE LINE/SOW SO-
     A                                      URCE INQUIRY'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  4  2'You must enter one of the followin-
     A                                      g:'
     A                                      DSPATR(HI)
     A                                  6  7'Tattoo number ..........'
     A                                  6 32'?'
     A            D1TANO    R        B  6 34REFFLD(HSRREF/@@NU50 *LIBL/HSPREF)
     A                                      EDTCDE(M)
     A N32                                  DSPATR(HI)
     A  32                                  DSPATR(RI)
     A  32
     AO 52                                  DSPATR(PC)
     A  99                                  DSPATR(PR)
     A                                  7  7'Kill date ..............'
     A                                  7 32'?'
     A            D1KMDY    R        B  7 34REFFLD(HSRREF/@@DT06 *LIBL/HSPREF)
     A                                      EDTWRD('  /  /  -')
     A N33                                  DSPATR(HI)
     A  33                                  DSPATR(RI)
     A  33
     AO 53                                  DSPATR(PC)
     A  99                                  DSPATR(PR)
     A                                  9  3'-- OR --'
     A                                      DSPATR(HI)
     A                                 11  7'Sales movement number ..'
     A                                 11 32'?'
     A            D1MVSN    R        B 11 34REFFLD(HSRREF/@@MVSN *LIBL/HSPREF)
     A                                      EDTCDE(M)
     A N31                                  DSPATR(HI)
     A  31
     AO 51                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A                                 13  3'-- OR --'
     A                                      DSPATR(HI)
     A                                 15  7'Hog group ..............'
     A                                 15 32'?'
     A            D1HGCD    R        B 15 34REFFLD(HSRREF/@@HGCD *LIBL/HSPREF)
     A N34                                  DSPATR(HI)
     A  34
     AO 54                                  DSPATR(PC)
     A  34                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A*
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R HP4085K1
     A*%%TS  SD  19990929  101044  ISLRAMS     REL-V4R2M0  5769-PW1
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A                                 23 11'F4=Prompt'
     A                                 23 22'F20=Spool Files'
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *---------------------------------------------------------------
     A          R HP4085ES                  SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *----------------------------------------------------------------
      * ERROR MESSAGE SUBFILE CONTROL FORMAT
      *----------------------------------------------------------------
     A          R HP4085EC                  SFLCTL(HP4085ES)
      *
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
