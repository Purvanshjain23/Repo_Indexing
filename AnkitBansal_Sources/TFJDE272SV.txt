      *****************  RPG PROGRAM HEADING  ************************
      * SYSTEM:      Triumph Foods / JD Edwards
      * PROGRAM:     TFJDE272
      * TITLE:       Upload Claim Adjustment to JDE
      * PROGRAMMER:  LeAnne Ramsey
      * CREATED:     03/07/06
      *
      *  FUNCTION:   This program uploads Claim Adjustment data to JDE.
      *
      *              The program is called from the Weekly Revenue Close function when
      *              it is a "Final" run.
      *
      *              Seaboard is Company 360
      *              Triumph  is Company 960
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 11/30/06  LeAnne Ramsey
      *           Added a "week" parm on the call to the generic "calendar" program.
      *
      * 11/13/07  LeAnne Ramsey
      *           Logic was blanking out the Week-ending Date in the header after
      *           printing. SO, it was only printing on the first page. Removed
      *           the "blank-after" edit from the output line.
      *
      * 01/21/08  LeAnne Ramsey
      *           Added a parm of A=Accounting on the call to generic program TF815
      *           so that we will get back the Accounting Year/Period instead of the
      *           Production (HPS) Year/Period. (The program will return zero for the
      *           Accounting week---which we don't use.)
      *
      * 05/11/09  LeAnne Ramsey
      *           Changed the LDA positions for Printing (hold/save/outq/copies).
      *
      * 05/28/09  LeAnne Ramsey
      *           Added Company and Report totals for 3 columns:
      *             1) Total Gross Claim Amount
      *             2) Seaboard Net Claim Amount
      *             5) Triumph Net Claim Amount
      /EJECT
      ****************************************************************
      * File specifications
      ****************************************************************
      *
     Ff0011     uf a e           k disk
      *   JDE Batch control record
      *
      *
     Ff0901     if   e             disk
      *   JDE Account master
      *
      *
     Ff0911     uf a e             disk
      *   JDE Account ledger
      *
      *
     Ftfp312    if   e           k disk
      *    Workfile: Claim adjustment Upload to JDE
      *
      *
     Fprint198  o    f  198        printer oflind(*inof)
      *
      /EJECT
      ****************************************************************
      * Definition specifications
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Named Constants
      *---------------------------------------------------------------
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      *---------------------------------------------------------------------
      * Standalone fields
      *---------------------------------------------------------------------
      *
      * Standard fields
      *
     D dash            s            198    inz(*all'-')
     d rtime           s              6  0
      *
      *
      * Control fields
      *
     d return          s              7
     d first           s              1    inz('Y')
     d svcono          s                   like(w1cono)
     d svco5a          s                   like(w1co5a)
     d svbatnbr        s                   like(w1batnbr)
     d svdocnbr        s                   like(w1docnbr)
      *
      *
      * Workfields for date manipulation
      *
     D wkisodate       s               D   datfmt(*iso)
      *
      *
      * Workfields
      *
     d wkuser          s                   like(gluser)
      *
     d wkbatnbr        s                   like(glicu)
     d wksbatnbr       s                   like(glicu)
     d wktbatnbr       s                   like(glicu)
      *
     d wkbatcnt        s              5  0
     d wkdoccnt        s              5  0
      *
     d wkaid           s              8
     D wkmcu           s                   like(gmmcu)
     D wksub           s                   like(gmsub)
     D wkobj           s                   like(gmobj)
     d wkani           s             29
      *
     d wkbatval        s             15  0
     d wksoffam        s             15  2
     d wktoffam        s             15  2
     d wkam            s             15  2
      *
     d wktime          s             12  0
     d wkdate          s              6  0
      *
      *
      *
      * Parm fields
      *
     D xxacctco        s              3  0
     D xxnumco         s              3  0
      *
     d xxsubtype       s              1
     d xxbalsheet      s              1
      *
     d xxapfl          s              1
     D xxdt            s              8  0
     D xxyr            s              4  0
     D xxpe            s              2  0
     D xxwk            s              2  0
      *
      *
      * For call to JDE date routine
      *
     d xxsidat         s              6
     d xxedat          s              8
     d xxffmt          s              7
     d xxtfmt          s              7
     d xxsep           s              7
     d xxertst         s              1
     d xxctry          s              2
      *
      *
      * For call to JDE account routine
      *
     d xxbatnbr        s                   like(glicu)
     d xxsy            s              4
     d xxidx           s              2
     d xxsym           s              1
     d xxomod          s              1
     d xximod          s              1
     d xxerrm          s              4
      *
     d xxglcode        s              8
     d xxani           s                   like(glani)
     d xxicu           s                   like(glicu)
     d xxdoc           s                   like(gldoc)
      *
      *
      * Print fields
      *
     d h1mdy           s              6  0
      *
     d h2conm          s              8
     d h2batnbr        s                   like(glicu)
     d h2docnbr        s                   like(gldoc)
      *
     d l1acct          s             28
     d l1desc          s             30
     d l1dbam          s                   like(w1dbam)
     d l1cram          s                   like(w1cram)
      *
     d t1desc          s             14
      *
      * Array index
      *
     d x               s              1  0
      *
      ****************************************************************
      * Arrays
      ****************************************************************
      *
     D aragclam        s             11  2 dim(3)
     D arsnclam        s             11  2 dim(3)
     D artnclam        s             11  2 dim(3)
      *
      ****************************************************************
      * Data structures
      ****************************************************************
      *
      *
     d i0901ds         ds           500
     d xxco                                like(gmco)
     d xxaid                               like(gmaid)
     d xxmcu                               like(gmmcu)
     d xxobj                               like(gmobj)
     d xxsub                               like(gmsub)
     d xxans                               like(gmans)
     d xxdl01                              like(gmdl01)
     d xxlda                               like(gmlda)
     d xxbpc                               like(gmbpc)
     d xxpec                               like(gmpec)
     d xxbill                              like(gmbill)
     d xxcrcd                              like(gmcrcd)
     d xxum                                like(gmum)
     d xxr001                              like(gmr001)
     d xxr002                              like(gmr002)
     d xxr003                              like(gmr003)
     d xxr004                              like(gmr004)
     d xxr005                              like(gmr005)
     d xxr006                              like(gmr006)
     d xxr007                              like(gmr007)
     d xxr008                              like(gmr008)
     d xxr009                              like(gmr009)
     d xxr010                              like(gmr010)
     d xxr011                              like(gmr011)
     d xxr012                              like(gmr012)
     d xxr013                              like(gmr013)
     d xxr014                              like(gmr014)
     d xxr015                              like(gmr015)
     d xxr016                              like(gmr016)
     d xxr017                              like(gmr017)
     d xxr018                              like(gmr018)
     d xxr019                              like(gmr019)
     d xxr020                              like(gmr020)
     d xxr021                              like(gmr021)
     d xxr022                              like(gmr022)
     d xxr023                              like(gmr023)
     d xxobja                              like(gmobja)
     d xxsuba                              like(gmsuba)
     d xxwcmp                              like(gmwcmp)
     d xxcct                               like(gmcct)
     d xxerc                               like(gmerc)
     d xxhtc                               like(gmhtc)
     d xxqlda                              like(gmqlda)
     d xxccc                               like(gmccc)
     d xxfmod                              like(gmfmod)
     d xxuser                              like(gmuser)
     d xxpid                               like(gmpid)
     d xxjobn                              like(gmjobn)
     d xxupmj                              like(gmupmj)
     d xxupmt                              like(gmupmt)
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *---------------------------------------------------------------
      * Break CCYY into CC and YY fields
      *---------------------------------------------------------------
      *
     D wkyear          ds
     D   wkccyy                       4  0
     D   wkcc                         2  0 overlay(wkccyy:1)
     D   wkyy                         2  0 overlay(wkccyy:3)
      *
      *----------------------------------------------------------------
      * LDA - local data area
      *----------------------------------------------------------------
      *
     d lda            uds                  dtaara(*lda)
     D  ldcyr                  1      4  0
     D  ldcwk                  5      6  0
     D  ldcbdt                 7     14  0
     D  ldcbsyn               15     21  0
     D  ldcedt                22     29  0
     D  ldcesyn               30     36  0
     D  ldpfcd                37     37
      *
      * Set up date fields that are used in generating reports that are generated
      * on-demand as well as from this close function.
      *
     D  ldfmdy                39     44  0
     D  ldtmdy                45     50  0
     D  ldfcymd               52     59  0
     D  ldtcymd               60     67  0
      *
     D  ldoutq               401    410
     D  ldhold               411    414
     D  ldsave               415    418
     D  ldcopy               419    419  0
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      *-------------------------------------------------------------------------------------
      * Mainline
      *-------------------------------------------------------------------------------------
      *
      * Print report heading
      *
     C                   exsr      $h1hdr
      *
      * Process/print each record in the workfile.
      *
     C     *loval        setll     tfp312
      *
     C                   dou       *in90 = *on                                  Main do
     C                   read      tfp312                                 90
     C                   if        *in90 = *off                                 If not EOF
      *
      * Control breaks
     C                   select
     C                   when      first = yes
     C                   move      no            first
     C                   z-add     w1cono        svcono
     C                   move      w1co5a        svco5a
     C                   z-add     w1batnbr      svbatnbr
     C                   z-add     w1docnbr      svdocnbr
     C                   exsr      $h2hdr
      *
     C                   when      svcono <> w1cono
     C                   exsr      $offset
     C                   z-add     1             x
     C                   evalr     t1desc = 'Company Total:'
     C                   exsr      $t1tot
     C                   z-add     w1cono        svcono
     C                   move      w1co5a        svco5a
     C                   z-add     w1batnbr      svbatnbr
     C                   z-add     w1docnbr      svdocnbr
     C                   exsr      $h2hdr
     C                   endsl
      *
     C                   add       1             wkdoccnt
      *
      * Write the JDE Account Ledger record
      *
     C                   exsr      $acctreg
      *
      * Set up print fields and print the workfile record
      *
     C                   z-add     w1cram        l1cram
     C                   z-add     w1dbam        l1dbam
      *
     C                   seton                                        87
     C                   exsr      $l1dtl
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Main do
      *
      *----------------
      * EOF processing
      *----------------
     C                   select
     C                   when      first = yes
     C                   except    nodata
     C                   other
      *
     C                   exsr      $offset
     C                   exsr      $batch
      * Company total
     C                   z-add     1             x
     C                   evalr     t1desc = 'Company Total:'
     C                   exsr      $t1tot
      * Report total
     C                   except    blank
     C                   seton                                        88
     C                   z-add     2             x
     C                   evalr     t1desc = 'Report Total:'
     C                   exsr      $t1tot
     C                   endsl
      *
     C                   seton                                        lr
      /eject
      *----------------------------------------------------------------
      * Retrieve regular Account info (ie: not "offset" account)
      *----------------------------------------------------------------
      *
     C     $acctreg      begsr
      *
     C                   move      *blank        wkaid
     C                   move      *blank        wkmcu
     C                   move      *blank        wkobj
     C                   move      *blank        wksub
      *
     C                   select
     C                   when      w1cono = 960
     C                   eval      gluser = 'TFGLPOST'
     C                   eval      gltorg = 'TFGLPOST'
     C                   z-add     w1batnbr      wktbatnbr
     C                   add       w1am          wktoffam
     C                   other
      *
     C                   eval      gluser = sdusr
     C                   eval      gltorg = sdusr
     C                   z-add     w1batnbr      wksbatnbr
     C                   add       w1am          wksoffam
     C                   endsl
      *
      *
      * Retrieve Account I.D. associated with Company/"TFAJCL" from the
      * GL Master Codes file.
      *
     C                   call      'PKTIXFR'
     C                   parm                    return
     C                   parm      w1cono        xxnumco
     C                   parm      'TFAJCL  '    xxglcode
     C     wkaid         parm      *blank        xxaid
     C                   parm      *blank        xxsubtype
     C                   parm      *blank        xxbalsheet
     C                   parm      *zero         xxacctco
      *
      * Retrieve cost center/object/sub associated with Account ID just retrieved.
      * (1 = Short Act I.D., 9 = Individual cost center/object/sub.)
      *
     C                   call      'X0901'
     C                   parm                    xxsym
     C                   parm      '9'           xxomod
     C                   parm      '1'           xximod
     C                   parm      wkaid         xxani
     C     wkmcu         parm      *blank        xxmcu
     C     wkobj         parm      *blank        xxobj
     C     wksub         parm      *blank        xxsub
     C                   parm      *blank        xxerrm
     C                   parm                    i0901ds
      *
      * If retrieval was successful,
      *  1) replace the SUB with the GL Sub Code of the record you want to write
      *  2) go back to JDE with the new combination of Cost Center/Object/Sub
      *     to get the Account ID associated with that combination
      *
      *
     C                   if        xxerrm = *blank                              If successful
     C                   movel     w1glsub       wksub
      *
     C                   call      'X0901'
     C                   parm                    xxsym
     C                   parm      '1'           xxomod
     C                   parm      '9'           xximod
     C     wkaid         parm      *blank        xxani
     C                   parm      wkmcu         xxmcu
     C                   parm      wkobj         xxobj
     C                   parm      wksub         xxsub
     C                   parm      *blank        xxerrm
     C                   parm                    i0901ds
     C                   endif                                                  If successful
      *
      * Company number needs to be the company that is associated with this
      * cost center/object/sub code.
      *
     C                   move      xxco          glco
      *
      * Hardcode explanation and plop "amount" into a workfield
      *
     C                   eval      glexa = 'TF Claim Adjustment'
     C                   z-add     w1am          wkam
      *
      * Write a JDE record---even if the account retrieval was NOT successful
      *
     C                   exsr      $wrt911
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Write a JDE Account Ledger record
      *---------------------------------------------------------------
      *
     C     $wrt911       begsr
      *
     C                   z-add     svbatnbr      glicu
     C                   z-add     svdocnbr      gldoc
     C                   move      svco5a        glkco
      *
     C                   move      wkmcu         glmcu
     C                   move      wkobj         globj
     C                   move      wksub         glsub
     C                   move      wkaid         glaid
      *
      * Retrieve the structured Account Number.
      *
     C                   move      *blank        wkani
      *
     C                   call      'X0901'
     C                   parm                    xxsym
     C                   parm      '2'           xxomod
     C                   parm      '9'           xximod
     C     wkani         parm                    xxani
     C                   parm      glmcu         xxmcu
     C                   parm      globj         xxobj
     C                   parm      glsub         xxsub
     C                   parm                    xxerrm
     C                   parm                    i0901ds
      *
     C                   move      xxdl01        l1desc
      *
      * Populate the Account Number
      *
     C                   if        xxerrm = *blank
     C                   movel     wkani         glani
     C                   else
     C                   move      *blank        glani
     C                   endif
      *
      * Increase the journal entry line number
      *
     C                   z-add     wkdoccnt      gljeln
      *
      * Remove the decimal
      *
     C     100           mult      wkam          glaa
      *
     C                   write     i0911
      *
     C                   endsr
      /eject
      *--------------------------------------------------------------------------------------
      * Write an offset record
      *--------------------------------------------------------------------------------------
      *
     C     $offset       begsr
      *
     C                   select
     C                   when      svcono = 360
     C                   eval      xxglcode = 'OFFAR362'
     C     -1            mult      wksoffam      wkam
      *
     C                   when      svcono = 960
     C                   eval      xxglcode = 'OFFAR960'
     C     -1            mult      wktoffam      wkam
     C                   endsl
      *
     C                   add       1             wkdoccnt
      *
      * Clear account workfields.
      *
     C                   move      *blank        wkaid                          Account ID
     C                   move      *blank        wkmcu                          Cost Center
     C                   move      *blank        wkobj                          Object
     C                   move      *blank        wksub                          Sub Code
      *
      *
      * Retrieve Account I.D. from the GL Master Codes file.
      *
     C                   call      'PKTIXFR'
     C                   parm                    return
     C                   parm      960           xxnumco
     C                   parm                    xxglcode
     C     wkaid         parm      *blank        xxaid
     C                   parm      *blank        xxsubtype
     C                   parm      *blank        xxbalsheet
     C                   parm      *zeros        xxacctco
      *
      * Retrieve cost center/object/sub associated with Account ID just retrieved.
      * (1 = Short Act I.D., 9 = Individual cost center/object/sub.)
      *
     C                   call      'X0901'
     C                   parm                    xxsym
     C                   parm      '9'           xxomod
     C                   parm      '1'           xximod
     C                   parm      wkaid         xxani
     C     wkmcu         parm      *blank        xxmcu
     C     wkobj         parm      *blank        xxobj
     C     wksub         parm      *blank        xxsub
     C                   parm      *blank        xxerrm
     C                   parm                    i0901ds
      *
      * Hardcode explanation
      *
     C                   eval      glexa = 'TF Claim Adjustment--Offset'
      *
      * Company number needs to be the company that is associated with this
      * cost center/object/sub code.
      *
     C                   move      xxco          glco
      *
      * Write record even if account retrieval was NOT successful
      *
     C                   exsr      $wrt911
      *
      * Set up Credit/Debit print field and print
      *
     C                   if        wkam < 0
     C                   z-add     wkam          l1cram
     C                   else
     C                   z-add     wkam          l1dbam
     C                   endif
      *
     C                   setoff                                       87
     C                   exsr      $l1dtl
      *
     C                   endsr
      /eject
      *--------------------------------------------------------------------------------------
      * Write 2 JDE Batch Control records: 1 for Seaboard, 1 for Triumph
      *--------------------------------------------------------------------------------------
      *
     C     $batch        begsr
      *
      * Write a JDE Batch Control record for Triumph
      *   1) the batch amount is just the Offset amount with no
      *      sign flipping; but, remove the decimal position.
      *
     C     100           mult      wktoffam      wkbatval
     C                   z-add     wktbatnbr     wkbatnbr
     C                   eval      wkuser = 'TFGLPOST'
     C                   exsr      $wrtbat
      *
      * Write a JDE Batch Control record for Seaboard
      *   1) the batch amount is just the Offset amount with no
      *      sign flipping; but, remove the decimal position.
      *
     C     100           mult      wksoffam      wkbatval
     C                   z-add     wksbatnbr     wkbatnbr
     C                   eval      wkuser = sdusr
     C                   exsr      $wrtbat
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Write a batch record
      *---------------------------------------------------------------
      *
     C     $wrtbat       begsr
      *
      * Write a batch header record to batch control file
      *
     C     key01         chain     f0011                              92
     C                   if        *in92 = *on                                  If no hit
     C                   z-add     wkbatcnt      icndo
     C                   z-add     wkbatcnt      icdocn
     C                   z-add     wkbatval      icaicu
     C                   z-add     wkbatval      icame
     C                   z-add     wkbatnbr      icicu
      *
     C                   move      *blank        icist
     C                   movel     wkuser        icuser
     C                   move      glicut        icicut
     C                   move      gldicj        icdicj
     C                   move      'A'           iciapp
     C                   move      'Y'           icbal
     C                   move      ' '           icbalj
     C                   move      'N'           icpob
     C                   move      'Y'           iciboi
      *
     C                   write     i0011
     C                   endif                                                  If no hit
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Get dates into JDE format
      *---------------------------------------------------------------
      *
     C     $date         begsr
      *
     C                   call      'X0028'
     C                   parm                    xxsidat
     C                   parm      *blank        xxedat
     C                   parm                    xxffmt
     C                   parm      '*JUL   '     xxtfmt
     C                   parm      '*NONE'       xxsep
     C                   parm      *blank        xxertst
     C                   parm      *blank        xxctry
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Print report headings
      *---------------------------------------------------------------
      *
     C     $h1hdr        begsr
      *
     C                   except    h1hdr
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Print Company heading
      *---------------------------------------------------------------
      *
     C     $h2hdr        begsr
      *
     C                   if        *inof = *on
     C                   exsr      $h1hdr
     C                   endif
      *
     C                   z-add     svbatnbr      h2batnbr
     C                   z-add     svdocnbr      h2docnbr
      *
     C                   select
     C                   when      svcono = 360
     C                   eval      h2conm = 'Seaboard'
      *
     C                   when      svcono = 960
     C                   eval      h2conm = 'Triumph'
     C                   endsl
      *
     C                   except    h2hdr
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Print detail line
      *---------------------------------------------------------------
      *
     C     $l1dtl        begsr
      *
     C                   select
     C                   when      wkaid = *blank
     C                   eval      l1acct = 'Unknown'
     C                   eval      l1desc = 'Error on Account ID retrieval'
      *
     C                   when      xxerrm = *blank
     C                   eval      l1acct = %trim(wkmcu) + '.' +
     C                                      %trim(wkobj) + '.' +
     C                                      %trim(wksub)
     C                   other
     C                   eval      l1acct = 'Unknown'
     C                   eval      l1desc = 'Cost Center/Obj/Sub error'
     C                   endsl
      *
     C                   if        *inof = *on
     C                   exsr      $h2hdr
     C                   endif
      *
     C                   except    l1dtl
      *
      * Accumulate for totals
     C                   if        *in87 = *on
     C                   add       w1agclam      aragclam
     C                   add       w1snclam      arsnclam
     C                   add       w1tnclam      artnclam
     C                   endif
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Print total line
      *---------------------------------------------------------------
      *
     C     $t1tot        begsr
      *
     C                   if        *inof = *on
     C                   exsr      $h1hdr
     C                   endif
      *
     C                   except    t1tot
      *
      * Clear array entries
     C                   z-add     0             aragclam(x)
     C                   z-add     0             arsnclam(x)
     C                   z-add     0             artnclam(x)
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Key lists
      *
     C     key01         klist
     C                   kfld                    glicut
     C                   kfld                    wkbatnbr
      *
      * Flip the Week-Ending Date to MMDDYY format.
      *
     C     *iso          test(d)                 ldcedt                 92
     C                   if        *in92 = *off                                 If OK date
     C                   move      ldcedt        wkisodate
     C     *mdy          move      wkisodate     h1mdy
     C                   endif                                                  If OK date
      *
     C                   time                    rtime
      *
      * Get system date into the format required by JDE by calling JDE's routine.
      *
     C                   time                    wktime
     C                   move      wktime        wkdate
     C                   move      wkdate        xxsidat
     C                   move      '*SYSVAL'     xxffmt
      *
      * Set up defaults for records in F0911-Account Ledger File
      *
     C                   exsr      $date
     C                   move      xxsidat       glupmj
     C                   move      xxsidat       gldsyj
     C                   move      xxsidat       gldicj
      *
      * Retrieve the Accounting Year/Period associated with the Week-Ending Date.
      *
     C                   call      'TF815'
     C                   parm      ldcedt        xxdt
     C                   parm      'A'           xxapfl
     C                   parm      0             xxyr
     C     glpn          parm      0             xxpe
     C                   parm      0             xxwk
      *
     C                   z-add     xxyr          wkccyy
     C                   z-add     wkcc          glctry
     C                   z-add     wkyy          glfy
      *
      * Get Weekending Date into JDE format by calling JDE date routine.
      *
     C                   move      ldcedt        xxsidat
     C                   move      '*YMD   '     xxffmt
     C                   exsr      $date
     C                   move      xxsidat       gldgj
     C                   move      xxsidat       gldsvj                         Service Tax date
      *
      * Set up some defaults
      *
     C                   movel     sdjob         gljobn
     C                   time                    glticu
     C                   time                    glupmt
      *
     C                   move      '2'           glam
     C                   move      'AA'          gllt
     C                   move      '00000'       glpkco
     C                   move      '00000'       glokco
     C                   move      '000'         glpsfx
     C                   move      'JE'          gldct
     C                   move      'G '          glicut
     C                   move      'R'           glrcnd
      *
     C                   endsr
      /eject
      *-------------------------------------------------------------
      * Report heading lines
      *-------------------------------------------------------------
      *
     Oprint198  e            h1hdr          1 01
     O                       sdpgm               10
     O                                          106 'CLAIM POST REPORT'
     O                                          188 'DATE'
     O                       udate         y    198
      *
     O          e            h1hdr          1
     O                       sdusr               10
     O                                          105 'JOURNAL ENTRIES'
     O                                          188 'TIME'
     O                       rtime              198 '  :  :  '
      *
      *
     O          e            h1hdr          2
     O                                          188 'PAGE'
     O                       page          z    198
      *
     O          e            h1hdr          2
     O                                           17 'Week-ending date:'
     O                       h1mdy               28 '  /  /  '
      *
      *
      *
      *-------------------------------------------------------------
      * Column headings
      *-------------------------------------------------------------
      *
     O          e            h1hdr       0  1
     O                                          100 'GL '
     O                                          110 'Product'
     O                                          120 'Claim'
     O                                          126 'Line'
     O                                          132 'Memo'
     O                                          140 'CR/DB'
     O                                          163 'Total Gross'
     O                                          181 'Seaboard Net'
     O                                          197 'Triumph Net'
      *
     O          e            h1hdr       0  1
     O                                           14 'Account Number'
     O                                           41 'Description'
     O                                           76 'Debit'
     O                                           94 'Credit'
     O                                          100 'Sub'
     O                                          108 'Code'
     O                                          119 'Nbr'
     O                                          126 'Nbr'
     O                                          132 'Type'
     O                                          142 'Memo Nbr'
     O                                          163 'Claim Amount'
     O                                          181 'Claim Amount'
     O                                          197 'Claim Amount'
      *
     O          e            h1hdr       0  2
     O                       dash               198
      *
      *
      *-------------------------------------------------------------
      * Company header
      *-------------------------------------------------------------
      *
     O          e            h2hdr       0  0
     O                       h2conm               8
     O                                           25 'JDE Batch Nbr:'
     O                       h2batnbr      z     35
     O                                           54 'JDE Document Nbr:'
     O                       h2docnbr      z     64
      *
     O          e            h2hdr       0  2
     O                       h2conm               8
     O                                           25 'JDE Batch Nbr:'
     O                       h2batnbr      z     35
     O                                           54 'JDE Document Nbr:'
     O                       h2docnbr      z     64
      *
      *
      *-------------------------------------------------------------
      * Detail line
      *-------------------------------------------------------------
      *
     O          e            l1dtl       0  1
     O                       l1acct         b    28
     O                       l1desc         b    60
     O                       l1dbam        kb    77
     O                       l1cram        kb    95
     O               87      w1glsub            100
     O               87      w1prcd        z    110
     O               87      w1clno        z    120
     O               87      w1cllnno      z    126
     O               87      w1mmty             131
     O               87      w1mmno        z    142
     O               87      w1agclam      k    164
     O               87      w1snclam      k    182
     O               87      w1tnclam      k    198
      *
      *-------------------------------------------------------------
      * Total line
      *-------------------------------------------------------------
      *
     O          e            t1tot       0
     O              n88                         163 '--------------'
     O              n88                         181 '--------------'
     O              n88                         197 '--------------'
      *
     O          e            t1tot       1
     O                       t1desc             147
     O                       aragclam(x)   k    164
     O                       arsnclam(x)   k    182
     O                       artnclam(x)   k    198
      *
      *-------------------------------------------------------------
      * Blank line
      *-------------------------------------------------------------
      *
     O          e            blank       0  1
      *
      *-------------------------------------------------------------
      * No data message line
      *-------------------------------------------------------------
      *
     O          e            nodata         1
     O                                           20 'There were no claim '
     O                                           40 'adjustments to post.'
      /eject
