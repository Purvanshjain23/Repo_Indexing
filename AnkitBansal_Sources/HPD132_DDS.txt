     A*%%TS  SD  19960411  083133  ISLRAMS     REL-V3R1M0  5763-PW1
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD132
     A*  TITLE:         MAINTAIN REPORT CODES
     A*  PROGRAMMER:    MARY PALMER
     A*  CREATED:       06/08/94
     A*
     A*  INDICATOR 60 IS ON WHEN IN REVISE MODE
     A*  INDICATOR 61 IS ON WHEN IN CREATE MODE
     A*  INDICATOR 62 IS ON WHEN USER HAS DELETED OR CREATED A RECORD
     A*               IN THE SUBFILE
     A*  INDICATOR 90 IS ON WHEN THE REPORT CODE THE USER SELECTED
     A*               FROM THE WORK WITH HAS BEEN DELETED BY ANOTHER
     A*               USER
     A*****************************************************************
     A* MODIFICATIONS
     A* 04/11/96  LEANNE FEDOR
     A*           THE FIELD RPOIFL-OPERATIONAL FLAG WAS NEVER USED.
     A*           TO AVOID RECOMPILING, WE HAVE LEFT THE FIELD IN
     A*           THIS PHYSICAL FILE AND IN THE FIELD REFERENCE FILE.
     A*           THE FIELD HAS BEEN REMOVED FROM THIS DSPF AND THE
     A*           PROGRAM CHANGED TO SET THE FIELD TO BLANKS.  THE
     A*           FIELD CAN BE REMOVED FROM THE FIELD REFERENCE FILE
     A*           AND HSP006 ANY TIME THAT YOU WANT TO RECOMPILE ALL
     A*           THE PROGRAMS.
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A N90                                  CF05(05 'REFRESH')
     A  60N90                               CF06(06 'CREATE')
     A                                      CA12(12 'PREVIOUS')
     A N82N90                               ROLLUP(25 'PAGE FORWARD')
     A*
     A*--------------------------------------------------------------*
     A* SUBFILE FORMAT ONE
     A*--------------------------------------------------------------*
     A          R HP132S1                   SFL
     A*%%TS  SD  19960411  082530  ISLRAMS     REL-V3R1M0  5763-PW1
     A                                      SETOF(30)
     A                                      SETOF(31)
     A                                      SETOF(32)
     A                                      SETOF(33)
     A                                      SETOF(35)
     A                                      SETOF(62)
     A  85                                  SFLNXTCHG
     A            HDRRN1         4Y 0H
     A            HDUCFL         1A  H
     A            DFOPT          2A  B 10  2CHGINPDFT(UL)
     A  61
     AO 62
     AO 90                                  DSPATR(PR)
     A  61
     AO 90                                  DSPATR(ND)
     A  30                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A N30N61N90                            DSPATR(HI)
     A  60N30N90                            DSPATR(UL)
     A            DFRPCD    R        B 10  8REFFLD(RPRPCD)
     A  60
     AO 62
     AO 90                                  DSPATR(PR)
     A  31                                  DSPATR(RI)
     A  31                                  DSPATR(PC)
     A N31N60                               DSPATR(HI)
     A  61N31N90                            DSPATR(UL)
     A            DFRSDS    R        B 10 16REFFLD(RPRSDS)
     A                                      CHECK(LC)
     A  62
     AO 90                                  DSPATR(PR)
     A  32                                  DSPATR(RI)
     A  32                                  DSPATR(PC)
     A N32                                  DSPATR(HI)
     A N32N90                               DSPATR(UL)
     A            DFRLDS    R        B 10 29REFFLD(RPRLDS)
     A                                      CHECK(LC)
     A  62
     AO 90                                  DSPATR(PR)
     A  33                                  DSPATR(RI)
     A  33                                  DSPATR(PC)
     A N33                                  DSPATR(HI)
     A N33N90                               DSPATR(UL)
     A            DFUCFL    R        O 10 54REFFLD(RPUCFL)
     A            DFSEQ     R        B 10 62REFFLD(RPSEQ)
     A                                      EDTCDE(K)
     A  62
     AO 90                                  DSPATR(PR)
     A  35                                  DSPATR(RI)
     A  35                                  DSPATR(PC)
     A N35                                  DSPATR(HI)
     A N35N90                               DSPATR(UL)
     A            DFACT          8A  O 10 71
     A*
     A*--------------------------------------------------------------*
     A* SUBFILE CONTROL FORMAT ONE
     A*--------------------------------------------------------------*
     A          R HP132C1                   SFLCTL(HP132S1)
     A*%%TS  SD  19960411  082530  ISLRAMS     REL-V3R1M0  5763-PW1
     A                                      SFLSIZ(0100)
     A                                      SFLPAG(0012)
     A                                      SFLCSRRRN(&HDRR1)
     A                                      OVERLAY
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A                                      RTNCSRLOC(&RCD &FLD)
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            HDRR1          5S 0H
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 25'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A            DFMODE    R        O  2  2REFFLD(@@MODE)
     A                                      DSPATR(HI)
     A                                  2 25'MAINTAIN REPORT CODES'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  4  2'Report category ...:'
     A            C1CACD    R        O  4 24REFFLD(CACACD)
     A            C1CADS    R        O  4 31REFFLD(CACADS)
     A N61                              6  2'4=Delete'
     A                                  7 53'User'
     A                                      DSPATR(HI)
     A                                  8  8'Report'
     A                                      DSPATR(HI)
     A                                  8 18'Report'
     A                                      DSPATR(HI)
     A                                  8 49'Controlled'
     A                                      DSPATR(HI)
     A                                  8 61'Category'
     A                                      DSPATR(HI)
     A N61                              9  2'Opt'
     A                                      DSPATR(HI)
     A                                  9  9'Code'
     A                                      DSPATR(HI)
     A                                  9 16'Short Name'
     A                                      DSPATR(HI)
     A                                  9 29'Report Long Name'
     A                                      DSPATR(HI)
     A                                  9 53'Y/N'
     A                                      DSPATR(HI)
     A                                  9 61'Sequence'
     A                                      DSPATR(HI)
     A*
     A*--------------------------------------------------------------*
     A* COMMAND KEY RECORD 1
     A*--------------------------------------------------------------*
     A          R HP132K1
     A*%%TS  SD  19960411  083133  ISLRAMS     REL-V3R1M0  5763-PW1
     A                                      SETOF(60)
     A                                      SETOF(61)
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A                                      DSPATR(HI)
     A                                 23  2'F3=Exit'
     A N90                             23 11'F5=Refresh'
     A  60N90                          23 23'F6=Create'
     A                                 23 34'F12=Previous'
     A*
     A*--------------------------------------------------------------*
     A* ERROR MESSAGE SUBFILE FORMAT
     A*--------------------------------------------------------------*
     A          R HP132ES                   SFL SFLMSGRCD(24)
     A*
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
     A*                                                                *********
     A*--------------------------------------------------------------*
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*--------------------------------------------------------------*
     A          R HP132EC                   SFLCTL(HP132ES)
     A*
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
