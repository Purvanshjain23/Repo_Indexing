/********************************************************************/
/*                                                                  */
/*  SYNOPSIS -  BUILD STATEMENT FOR OPEN QUERY FILE THAT WILL       */
/*              BE PASSED TO A PRINT PROGRAM TO WRITE TO A WORK     */
/*              FILE. THIS WORK FILE WILL BE USED BY SEQUEL TO      */
/*              BUILD AND EMAIL AN EXCEL SPREADSHEET. THIS USES     */
/*              OPEN QUERY FILES OVER ITEM TRANSACTION DETAIL       */
/*              AND PRODUCT AVAILABILITY GROUP FILES                */
/*                                                                  */
/*  COMPANY   - SEABOARD CORP                                       */
/*  SYSTEM    - INVENTORY CONTROL                                   */
/*  DATE      - 05/30/2008                                          */
/*                                                                  */
/*  NOTE      - THIS PROGRAM DOES NOT PRINT A REPORT, IT BUILDS     */
/*              A WORK FILE THAT IS DOWNLOADED INTO AN              */
/*              EXCEL SPREADSHEET THAT IS THEN EMAILED              */
/*                                                                  */
/********************************************************************/
/*  MODIFICATIONS:                                                  */
/*                                                                  */
/********************************************************************/
/*  PARAMETERS RECEIVED:                                            */
/*                                                                  */
/*   &PGM        - PROGRAM NAME                                     */
/*   &COMPN      - COMPANY NUMBER                                   */
/*   &FDATEN     - FROM DATE                                        */
/*   &TDATEN     - TO DATE                                          */
/*   &FTIMEN     - FROM TIME                                        */
/*   &TTIMEN     - TO TIME                                          */
/*   &WHSE       - WAREHOUSE CODE                                   */
/*   &ITEMN      - ITEM CODE                                        */
/*   &TTYPE1     - TRANSACTION TYPE 1                               */
/*   &TTYPE2     - TRANSACTION TYPE 2                               */
/*   &TTYPE3     - TRANSACTION TYPE 3                               */
/*   &TTYPE4     - TRANSACTION TYPE 4                               */
/*   &RSN1       - REASON CODE 1                                    */
/*   &RSN2       - REASON CODE 2                                    */
/*   &RSN3       - REASON CODE 3                                    */
/*   &RSN4       - REASON CODE 4                                    */
/*   &SHFT       - SHIFT NUMBER (BLANK = ALL)                       */
/*   &AVLGRP     - AVAIL GROUP     (BLANK = ALL FG)                 */
/*   &TRNID      - INVENTORY TRANS. ID (BLANK = ALL)                */
/*   &EMAIL      - EMAIL ADDRESS                                    */
/*   &FORMAT     - EMAIL FORMAT                                     */
/********************************************************************/
/*  RMC      05/25/22 DDSDDL  REPLACE OPNQRYF WITH OPNSQLF        */
/*           ADDED NEEDED OVRDBF AROUND THE COMMAND.              */
/*           CHANGE *AND/*OR TO AND/OR                            */
/********************************************************************/
 PMU7UPC:    PGM        PARM(&PGM &COMPN &FDATEN &TDATEN &WHSE +
                          &ITEMN &TTYPE1 &TTYPE2 &TTYPE3 &TTYPE4 +
                          &RSN1 &RSN2 &RSN3 &RSN4 &SHFT &AVLGRP +
                          &FTIMEN &TTIMEN &TRNID &EMAIL &FORMAT)

             DCL        &COMPN      *DEC         /*  CO. #           */
             DCL        &COMP       *DEC     LEN(3 0)
             DCL        &FDATEN     *DEC        /*  FROM DATE        */
             DCL        &FDATE      *DEC     LEN(7 0)
             DCL        &FDATEA     *CHAR    7  /* TO DTE FOR QRY    */
             DCL        &TDATEN     *DEC        /*  TO DATE          */
             DCL        &TDATE      *DEC     LEN(7 0)
             DCL        &TDATEA     *CHAR    7  /* FRM DT FOR QRY    */
             DCL        &FTIMEN     *DEC        /*  FROM TIME        */
             DCL        &FTIME      *DEC     LEN(6 0)
             DCL        &FTIMEA     *CHAR    6  /* TO TIM FOR QRY    */
             DCL        &TTIMEN     *DEC        /*  TO TIME          */
             DCL        &TTIME      *DEC     LEN(6 0)
             DCL        &TTIMEA     *CHAR    6  /* FRM TIM FOR QRY   */
             DCL        &ITEMN      *DEC         /*  ITEM            */
             DCL        &ITEM       *DEC     LEN(7 0)
             DCL        &ITEMA      *CHAR    7  /* ITEM FOR QRY      */
             DCL        &SHFT       *CHAR    1   /* SHIFT            */
             DCL        &AVLGRP     *CHAR    3   /* AVAIL GROUP      */
             DCL        &TRNID      *CHAR    1   /* TRANS. ID        */
             DCL        &PGM        *CHAR    10 /*  PGM NAME         */
             DCL        &COMPA      *CHAR    3  /* CO. # FOR MSG     */
             DCL        &WHSE       *CHAR    3  /* WAREHOUSE         */
             DCL        &TTYPE1     *CHAR    2  /* TRANS. TYPE       */
             DCL        &TTYPE2     *CHAR    2  /* TRANS. TYPE       */
             DCL        &TTYPE3     *CHAR    2  /* TRANS. TYPE       */
             DCL        &TTYPE4     *CHAR    2  /* TRANS. TYPE       */
             DCL        &RSN1       *CHAR    3  /*  REASON CODE      */
             DCL        &RSN2       *CHAR    3  /*  REASON CODE      */
             DCL        &RSN3       *CHAR    3  /*  REASON CODE      */
             DCL        &RSN4       *CHAR    3  /*  REASON CODE      */
             DCL        &EMAIL      *CHAR    50 /*  EMAIL ADDRESS    */
             DCL        &FORMAT     *CHAR    5  /*  EMAIL FORMAT     */
             DCL        &RETURN     *CHAR    7  /*  RETURN CODE      */
             DCL        &TEXT       *CHAR    80 /*  EMAIL TEXT       */
/* QUERY SELECT STATEMENT DEFINITIONS                                */
             DCL        &A          *CHAR    25 /* FROM DATE         */
             DCL        &B          *CHAR    36 /* TO DATE           */
             DCL        &C          *CHAR    37 /* COMPANY           */
             DCL        &C1         *CHAR    37 /* ITEM              */
             DCL        &D          *CHAR    35 /* WAREHOUSE         */
             DCL        &F          *CHAR    35 /* TRANS TYPE 1      */
             DCL        &G          *CHAR    35 /* TRANS TYPE 2      */
             DCL        &H          *CHAR    35 /* TRANS TYPE 3      */
             DCL        &I          *CHAR    35 /* TRANS TYPE 4      */
             DCL        &J          *CHAR    1  /* SINGLE CLOSE QUOTE*/
             DCL        &K          *CHAR    35 /* REASON 1          */
             DCL        &L          *CHAR    35 /* REASON 2          */
             DCL        &M          *CHAR    35 /* REASON 3          */
             DCL        &N          *CHAR    35 /* REASON 4          */
             DCL        &O          *CHAR    1  /* SINGLE CLOSE QUOTE*/
             DCL        &P          *CHAR    600 /* CONCATED STATEMT */
             DCL        &S          *CHAR    35 /* SHIFT             */
             DCL        &T          *CHAR    35 /* AVAILABILITY GROUP*/
             DCL        &U          *CHAR    75 /* FROM DATE & TIME  */
             DCL        &V          *CHAR    75 /* TO DATE & TIME    */
             DCL        &W          *CHAR    37 /* CO# FOR PA GROUP  */
             DCL        &Y          *CHAR    75 /* FG & WIP FOR PAGRP*/
             DCL        &X          *CHAR    19 /* TRANS ID          */
             DCL        &SELECT     *CHAR    1  /* TRANS TYPE TEST   */
             DCL        &SELECT2    *CHAR    1  /* REASON TEST       */
             DCL        &FINGOODS   *CHAR    3   /* FG               */
             DCL        &WIP        *CHAR    3   /* WIP              */

/********************************************************************/
/* FORMAT NUMERIC TO ALPHA FOR QUERY                                */
/********************************************************************/
             CHGVAR     VAR(&COMP) VALUE(&COMPN)
             CHGVAR     VAR(&COMPA) VALUE(&COMP)
             CHGVAR     VAR(&FDATE) VALUE(&FDATEN)
             CHGVAR     VAR(&FDATEA) VALUE(&FDATE)
             CHGVAR     VAR(&TDATE) VALUE(&TDATEN)
             CHGVAR     VAR(&TDATEA) VALUE(&TDATE)
             CHGVAR     VAR(&FTIME) VALUE(&FTIMEN)
             CHGVAR     VAR(&FTIMEA) VALUE(&FTIME)
             CHGVAR     VAR(&TTIME) VALUE(&TTIMEN)
             CHGVAR     VAR(&TTIMEA) VALUE(&TTIME)
             CHGVAR     VAR(&ITEM) VALUE(&ITEMN)
             CHGVAR     VAR(&ITEMA) VALUE(&ITEM)
             CHGVAR     VAR(&FINGOODS) VALUE('ALL')
             CHGVAR     VAR(&WIP) VALUE('WIP')

/********************************************************************/
/* CREATE TEMPORARY FILE/LOGICALS IN QTEMP                          */
/********************************************************************/
             DLTF       FILE(QTEMP/PMDZCPL0)
             MONMSG     MSGID(CPF2105)
             DLTF       FILE(QTEMP/PMDZCPL1)
             MONMSG     MSGID(CPF2105)
             DLTF       FILE(QTEMP/PMDZCPP)
             MONMSG     MSGID(CPF2105)

             CRTDUPOBJ  OBJ(PMDZCPP) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP) NEWOBJ(PMDZCPP) CST(*NO) +
                          TRG(*NO)
             MONMSG     MSGID(CPF0000) EXEC(CLRPFM FILE(PMDZCPP))
             OVRDBF     FILE(PMDZCPP) TOFILE(QTEMP/PMDZCPP)

             CRTDUPOBJ  OBJ(PMDZCPL0) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP) CST(*NO) TRG(*NO)
             OVRDBF     FILE(PMDZCPL0) TOFILE(QTEMP/PMDZCPL0)

             CRTDUPOBJ  OBJ(PMDZCPL1) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP) CST(*NO) TRG(*NO)
             OVRDBF     FILE(PMDZCPL1) TOFILE(QTEMP/PMDZCPL1)

/* DEFAULT TRANSACTION TYPES IF ALL ARE BLANKS                       */
             IF         COND((&TTYPE1 *EQ '  ') *AND (&TTYPE2 *EQ '  +
                          ') *AND (&TTYPE3 *EQ '  ') *AND (&TTYPE4 +
                          *EQ '  ')) THEN(DO)
             CHGVAR     VAR(&TTYPE1) VALUE('RR')
             CHGVAR     VAR(&TTYPE2) VALUE('RW')
             ENDDO

/* DEFAULT PRODUCT AVAILABILITY GROUP IF IT IS BLANKS                */
             IF         COND(&AVLGRP *EQ '  ') THEN(CHGVAR +
                          VAR(&AVLGRP) VALUE('F&W'))
/*********************************************************************/
/*   CREATE OPEN QUERY FILE TO BE USED TO DISPLAY DETAILS            */
/*********************************************************************/
/* FROM TRANSACTION DATE & TIME                                      */
             CHGVAR     VAR(&A) VALUE('((B7B4DT > ' *CAT &FDATEA +
                          *CAT ')')
             CHGVAR     VAR(&U) VALUE('OR (B7B4DT = ' *CAT &FDATEA +
                     *CAT ' AND B7AATM >=' *CAT &FTIMEA *CAT '))')

/* TO TRANSACTION DATE & TIME                                        */
             CHGVAR     VAR(&B) VALUE('AND ((B7B4DT < ' *CAT +
                          &TDATEA *CAT ')')
             CHGVAR     VAR(&V) VALUE('OR (B7B4DT = ' *CAT &TDATEA +
                      *CAT ' AND B7AATM <= ' *CAT &TTIMEA *CAT '))')

/* COMPANY NUMBER                                                    */
             IF         COND(&COMPA *NE '000') THEN(DO)
             CHGVAR     VAR(&C) VALUE('AND B7AIC3 = ' *CAT &COMPA)
             ENDDO

/* ITEM CODE                                                         */
             IF         COND(&ITEMA *NE '0000000') THEN(DO)
             CHGVAR     VAR(&C1) VALUE('AND B7HGCD = ' *CAT &ITEMA)
             ENDDO

/* WAREHOUSE CODE                                                    */
             IF         COND(&WHSE *NE '   ') THEN(DO)
             CHGVAR     VAR(&D) VALUE('AND B7AJCD = "' *CAT &WHSE +
                        *CAT '"')
             ENDDO

/* INVENTORY TRANSACTION TYPE 1                                      */
             IF         COND(&TTYPE1 *NE '  ') THEN(DO)
             CHGVAR     VAR(&F) VALUE('AND (B7BSST = "' *CAT +
                         &TTYPE1 *CAT '"')
             CHGVAR     VAR(&SELECT) VALUE('Y')
             ENDDO

/* INVENTORY TRANSACTION TYPE 2                                      */
             IF         COND(&TTYPE2 *NE '  ') THEN(DO)
             IF         COND(&SELECT *EQ 'Y') THEN(DO)
             CHGVAR     VAR(&G) VALUE('OR B7BSST = "' *CAT +
                         &TTYPE2 *CAT '"')
             ENDDO
             ELSE DO
             CHGVAR     VAR(&G) VALUE('AND (B7BSST = "' *CAT +
                         &TTYPE2 *CAT '"')
             CHGVAR     VAR(&SELECT) VALUE('Y')
             ENDDO
             ENDDO

/* INVENTORY TRANSACTION TYPE 3                                      */
             IF         COND(&TTYPE3 *NE '  ') THEN(DO)
             IF         COND(&SELECT *EQ 'Y') THEN(DO)
             CHGVAR     VAR(&H) VALUE('OR B7BSST = "' *CAT +
                         &TTYPE3 *CAT '"')
             ENDDO
             ELSE DO
             CHGVAR     VAR(&H) VALUE('AND (B7BSST = "' *CAT +
                         &TTYPE3 *CAT '"')
             CHGVAR     VAR(&SELECT) VALUE('Y')
             ENDDO
             ENDDO

/* INVENTORY TRANSACTION TYPE 4                                      */
             IF         COND(&TTYPE4 *NE '  ') THEN(DO)
             IF         COND(&SELECT *EQ 'Y') THEN(DO)
             CHGVAR     VAR(&I) VALUE('OR B7BSST = "' *CAT +
                         &TTYPE4 *CAT '"')
             ENDDO
             ELSE DO
             CHGVAR     VAR(&I) VALUE('AND (B7BSST = "' *CAT +
                         &TTYPE4 *CAT '"')
             CHGVAR     VAR(&SELECT) VALUE('Y')
             ENDDO
             ENDDO

/* CLOSE THE QUOTED STRING                                           */
             IF         COND(&SELECT *EQ 'Y') THEN(DO)
             CHGVAR     VAR(&J) VALUE(')')
             ENDDO

/* REASON CODE 1                                                     */
             IF         COND(&RSN1 *NE '   ') THEN(DO)
             CHGVAR     VAR(&K) VALUE('AND (B7CQCD = "' *CAT +
                         &RSN1 *CAT '"')
             CHGVAR     VAR(&SELECT2) VALUE('Y')
             ENDDO

/* REASON CODE 2                                                     */
             IF         COND(&RSN2 *NE '   ') THEN(DO)
             IF         COND(&SELECT2 *EQ 'Y') THEN(DO)
             CHGVAR     VAR(&L) VALUE('OR B7CQCD = "' *CAT +
                         &RSN2 *CAT '"')
             ENDDO
             ELSE DO
             CHGVAR     VAR(&L) VALUE('AND (B7CQCD = "' *CAT +
                         &RSN2 *CAT '"')
             CHGVAR     VAR(&SELECT2) VALUE('Y')
             ENDDO
             ENDDO

/* REASON CODE 3                                                     */
             IF         COND(&RSN3 *NE '   ') THEN(DO)
             IF         COND(&SELECT2 *EQ 'Y') THEN(DO)
             CHGVAR     VAR(&M) VALUE('OR B7CQCD ="' *CAT +
                         &RSN3 *CAT '"')
             ENDDO
             ELSE DO
             CHGVAR     VAR(&M) VALUE('AND (B7CQCD = "' *CAT +
                         &RSN3 *CAT '"')
             CHGVAR     VAR(&SELECT2) VALUE('Y')
             ENDDO
             ENDDO

/* REASON CODE 4                                                     */
             IF         COND(&RSN4 *NE '   ') THEN(DO)
             IF         COND(&SELECT2 *EQ 'Y') THEN(DO)
             CHGVAR     VAR(&N) VALUE('OR B7CQCD = "' *CAT +
                         &RSN4 *CAT '"')
             ENDDO
             ELSE DO
             CHGVAR     VAR(&N) VALUE('AND (B7CQCD = "' *CAT +
                         &RSN4 *CAT '"')
             CHGVAR     VAR(&SELECT2) VALUE('Y')
             ENDDO
             ENDDO

/* CLOSE THE QUOTED STRING                                           */
             IF         COND(&SELECT2 *EQ 'Y') THEN(DO)
             CHGVAR     VAR(&O) VALUE(')')
             ENDDO

/* SHIFT                                                             */
             IF         COND(&SHFT *NE ' ') THEN(DO)
             CHGVAR     VAR(&S) VALUE('AND B7XLCD = "' *CAT +
                         &SHFT *CAT '"')
             ENDDO

/* INVENTORY TRANS. ID                                               */
             IF         COND(&TRNID *NE ' ') THEN(DO)
             CHGVAR     VAR(&X) VALUE('AND B7W9ST = "' *CAT +
                         &TRNID *CAT '"')
             ENDDO

/* PRODUCT AVAILABILITY GROUP                                        */
/*  IF "F&W" THEN TEST FOR BOTH FINISHED GOODS AND WIP. OTHERWISE,   */
/*  TEST FOR THE PA GROUP THAT WAS PASSED IN.                        */
             IF         COND(&AVLGRP *NE ' ') THEN(DO)
             IF         COND(&AVLGRP *EQ 'F&W') THEN(DO)
             CHGVAR     VAR(&Y) VALUE('AND ((KLV4CD = "' *CAT +
                          &FINGOODS *CAT '") OR (KLV4CD = "' +
                          *CAT &WIP *CAT '"))')
             ENDDO
             ELSE DO
             CHGVAR     VAR(&Y) VALUE('AND KLV4CD = "' *CAT +
                         &AVLGRP *CAT '"')
             ENDDO
             ENDDO

/* COMPANY NUMBER FOR PRODUCT AVAILABILITY GROUP                     */
             IF         COND(&COMPA *NE '000') THEN(DO)
             CHGVAR     VAR(&W) VALUE('AND KLAIC3 = ' *CAT &COMPA)
             ENDDO

/* BUILD QUERY SELECT STATEMENT                                      */
             CHGVAR     VAR(&P) VALUE(&A *BCAT &U *BCAT &B *BCAT &V +
                          *BCAT &C *BCAT &C1 *BCAT &D *BCAT &F +
                          *BCAT &G *BCAT &H *BCAT &I *BCAT &J *BCAT +
                          &K *BCAT &L *BCAT &M *BCAT &N *BCAT &O +
                          *BCAT &S *BCAT &X *BCAT &Y *BCAT &W)

/********************  PRINT DETAIL REPORT  **************************/
/* AVAILABILITY GROUP FILE                                           */
 /*          OVRDBF     FILE(PDKLREL1) SHARE(*YES)                  */
 /*          OVRDBF     FILE(CAB7CPM4) SHARE(*YES)                  */

   /*        OPNQRYF    FILE((CAB7CPM4) (PDKLREL1)) FORMAT(CAB7CPM4) +
                          QRYSLT(&P) KEYFLD(*FILE) UNIQUEKEY(*NONE) +
                          JFLD((B7AIC3 KLAIC3) (B7HGCD KLHGCD)) +
                          SEQONLY(*YES)                                */

/*   OPEN SQL                                                      */
             OVRDBF     FILE(CAB7CPP ) TOFILE(CAB7CPP) LVLCHK(*NO)
             OPNSQLF    SQL('SELECT * FROM  cab7cpp,pdklrep      +
                          JOIN BY  B7AIC3.CAB7CPP=KLAIC3.PDKLREP +
                          AND B7HGCD.CAB7CPP=KLHGCD.PDKLREP WHERE' +
                          *BCAT &P *BCAT 'ORDER   BY    b7aic3 +
                          Asc,b7ajcd Asc,b7hgcd Asc,b7xlcd Asc, +
                          b7w9st asc, b7bsst asc, b7cqcd asc') +
                          OPNID(CAB7CPM4) RCDFMT(@B7CPTE) +
                          ALWCPY(*IFRQD) MSG(*YES) JTYPE(*INNER) +
                          JORDER(*FILE)

   /*  Fromfile is the file in the program, tofile is 1st file in the opnsqlf */
     /*  For the override to be SEEN BY ALL PROGRAMS, THE OVRDBF +
                command must specify OVRSCOPE(*JOB) and OPNSCOPE(*JOB). */

             OVRDBF     FILE(CAB7CPM4) TOFILE(CAB7CPP) LVLCHK(*NO) +
                          OVRSCOPE(*JOB) SHARE(*YES) OPNSCOPE(*JOB) +
                          SEQONLY(*NO)
             OVRDBF     FILE(PDKLREP) OVRSCOPE(*JOB) SHARE(*YES) +
                          OPNSCOPE(*JOB)


/* CALL THE PRINT PROGRAM THAT WILL WRITE THE TEMP FILE RECORDS      */
             CALL       PGM(&PGM) PARM(&RETURN &AVLGRP)

/********************  EMAIL SUMMARY QUERY  **************************/
/* RUN THE SCRIPT THAT WILL CREATE & EMAIL THE EXCEL SPREADSHEETS    */

/* >>>> FOR TESTING <<<<< */
/********    CPYF       FROMFILE(QTEMP/PMDZCPP) +
                          TOFILE(LARAB/PMDZCPPSV) MBROPT(*REPLACE) +
                          CRTFILE(*YES)               ****************/
/* >>>> FOR TESTING <<<<< */

             CHGVAR     VAR(&TEXT) VALUE('Item Transaction Journal +
                          Download for' *bcat &fdatea *bcat +
                          &ftimea *bcat 'through' *bcat &tdatea +
                          *bcat &ttimea)

             IF         COND(&EMAIL *NE '       ') THEN(EXECUTE +
                          VIEW(PMDZCPPA) PCFMT(*XLS) REPLACE(*YES) +
                          RECIPIENT(&EMAIL) EMLMSG(&TEXT))

/* CLOSE THE QUERY FILES AND DELETE THE OVERRIDES                    */
 END:        CLOF       OPNID(CAB7CPM4)
             MONMSG     MSGID(CPF0000)
             DLTOVR     FILE(CAB7CPM4)
             MONMSG     MSGID(CPF0000)
             DLTOVR     FILE(PDKLREL1)
             MONMSG     MSGID(CPF0000)
             DLTOVR *ALL
             DLTSPLF    FILE(PMU6PFR$)

             ENDPGM
