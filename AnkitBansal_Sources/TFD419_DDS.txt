     A*%%TS  SD  20080813  163451  ISLRAMS     REL-V5R4M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        TRIUMPH FOODS
     A*  DISPLAY FILE:  TFD419
     A*  TITLE:         WORK WITH PRODUCT EXCEPTIONS
     A*  PROGRAMMER:    LEANNE RAMSEY
     A*  DATE:          09/07/06
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/TFPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A                                      CF04(04 'PROMPT')
     A                                      CF05(05 'REFRESH')
     A                                      CF06(06 'CREATE')
     A                                      CF07(07 'LISTING')
     A                                      CF13(13 'FOLD/DROP')
     A                                      CF20(20 'WRKSPLF')
     A*
     A*
     A*---------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*---------------------------------------------------------------
     A          R TF419S1                   SFL
     A*%%TS  SD  20080813  163451  ISLRAMS     REL-V5R4M0  5722-WDS
     A  85                                  SFLNXTCHG
     A            HDWEDT    R        H      REFFLD(@@WEDT)
     A            HDRRN1         4Y 0H
     A            D1OPT          2A  B  9  2CHGINPDFT(UL)
     A  30                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A N30                                  DSPATR(HI)
     A            D1WEDTMDY R        O  9  7REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  ')
     A            D1PRCD    R        O  9 17REFFLD(TFREF/@@PRCD *LIBL/TFPREF)
     A                                      EDTCDE(M)
     A            D1MIXFL   R        O  9 27REFFLD(TFREF/@@MIXFL *LIBL/TFPREF)
     A            D1VOLFL   R        O  9 31REFFLD(TFREF/@@VOLFL *LIBL/TFPREF)
     A            D1SYSFL   R        O  9 35REFFLD(TFREF/@@SYSFL *LIBL/TFPREF)
     A            D1CGCD    R        O  9 39REFFLD(TFREF/@@CGCD *LIBL/TFPREF)
     A            D1TFCGCD  R        O  9 45REFFLD(TFREF/@@TFCGCD *LIBL/TFPREF)
     A            D1TFCLCD  R        O  9 50REFFLD(TFREF/@@TFCLCD *LIBL/TFPREF)
     A            D1ITYCD   R        O  9 55REFFLD(TFREF/@@ITYCD *LIBL/TFPREF)
     A            D1PESN    R        O  9 61REFFLD(TFREF/@@PESN *LIBL/TFPREF)
     A                                      EDTCDE(Z)
     A            D1PGM     R        O  9 70REFFLD(TFREF/@@PGM *LIBL/TFPREF)
     A                                 10 12'Comment'
     A                                      COLOR(BLU)
     A            D1EXCOM   R        O 10 20REFFLD(@@EXCOM)
     A                                      COLOR(PNK)
     A                                 10 47'IS Type/Group/Class'
     A                                      COLOR(BLU)
     A            D1ISTYCD  R        O 10 67REFFLD(TFREF/@@ISTYCD *LIBL/TFPREF)
     A                                      EDTCDE(Z)
     A                                      COLOR(PNK)
     A            D1ISGRCD  R        O 10 71REFFLD(TFREF/@@ISGRCD *LIBL/TFPREF)
     A                                      COLOR(PNK)
     A                                      EDTCDE(Z)
     A            D1ISCLCD  R        O 10 75REFFLD(TFREF/@@ISCLCD *LIBL/TFPREF)
     A                                      COLOR(PNK)
     A                                      EDTCDE(Z)
     A*
     A*---------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*---------------------------------------------------------------
     A          R TF419C1                   SFLCTL(TF419S1)
     A*%%TS  SD  20080813  163451  ISLRAMS     REL-V5R4M0  5722-WDS
     A                                      SFLSIZ(0150)
     A                                      SFLPAG(0006)
     A N82                                  ROLLUP(25 'PAGE FORWARD')
     A                                      SETOF(30)
     A                                      SETOF(51)
     A                                      SETOF(52)
     A                                      SETOF(53)
     A                                      SETOF(54)
     A                                      SETOF(55)
     A                                      SETOF(56)
     A                                      SETOF(57)
     A                                      RTNCSRLOC(&RCD &FLD)
     A                                      OVERLAY
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A  78                                  SFLFOLD(CF13)
     A  79                                  SFLDROP(CF13)
     A                                      SFLMODE(&FOLD)
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            FOLD           1A  H
     A            RCD       R        H      REFFLD(@@RCD)
     A            FLD       R        H      REFFLD(@@FLD)
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 25'WORK WITH PRODUCT EXCEPTIONS'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USR)
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@JOB)
     A                                      DSPATR(HI)
     A                                  3  2'2=Revise'
     A                                  3 12'4=Delete'
     A                                  3 22'5=View'
     A                                  4 20'?'
     A                                      DSPATR(HI)
     A                                  4 27'?'
     A                                      DSPATR(HI)
     A                                  4 31'?'
     A                                      DSPATR(HI)
     A                                  4 35'?'
     A                                      DSPATR(HI)
     A                                  4 40'?'
     A                                      DSPATR(HI)
     A                                  4 46'?'
     A                                      DSPATR(HI)
     A                                  4 51'?'
     A                                      DSPATR(HI)
     A                                  5  9'Week'
     A                                      DSPATR(HI)
     A                                  5 34'Sys'
     A                                      DSPATR(HI)
     A                                  5 39'Meat'
     A                                      DSPATR(HI)
     A                                  5 45'TF'
     A                                      DSPATR(HI)
     A                                  5 50'TF'
     A                                      DSPATR(HI)
     A                                  5 55'Item'
     A                                      DSPATR(HI)
     A                                  5 61'Product'
     A                                      DSPATR(HI)
     A                                  6  8'Ending'
     A                                      DSPATR(HI)
     A                                  6 17'Product'
     A                                      DSPATR(HI)
     A                                  6 26'Mix'
     A                                      DSPATR(HI)
     A                                  6 30'Vol'
     A                                      DSPATR(HI)
     A                                  6 34'Gen'
     A                                      DSPATR(HI)
     A                                  6 39'Cost'
     A                                      DSPATR(HI)
     A                                  6 45'Cls'
     A                                      DSPATR(HI)
     A                                  6 50'Cls'
     A                                      DSPATR(HI)
     A                                  6 55'Type'
     A                                      DSPATR(HI)
     A                                  6 61'Except'
     A                                      DSPATR(HI)
     A                                  6 70'Program'
     A                                      DSPATR(HI)
     A                                  7  2'Opt'
     A                                      DSPATR(HI)
     A                                  7  9'Date'
     A                                      DSPATR(HI)
     A                                  7 18'Code'
     A                                      DSPATR(HI)
     A                                  7 26'Flg'
     A                                      DSPATR(HI)
     A                                  7 30'Flg'
     A                                      DSPATR(HI)
     A                                  7 34'Flg'
     A                                      DSPATR(HI)
     A                                  7 39'Grp'
     A                                      DSPATR(HI)
     A                                  7 45'Grp'
     A                                      DSPATR(HI)
     A                                  7 50'Cde'
     A                                      DSPATR(HI)
     A                                  7 55'Code'
     A                                      DSPATR(HI)
     A                                  7 61'Number'
     A                                      DSPATR(HI)
     A                                  7 70'Name'
     A                                      DSPATR(HI)
     A            C1WEDTMDY R        B  8  7REFFLD(TFREF/@@DT06 *LIBL/TFPREF)
     A                                      EDTWRD('  /  /  ')
     A                                      DSPATR(HI)
     A            C1PRCD    R        B  8 17REFFLD(TFREF/@@PRCD *LIBL/TFPREF)
     A                                      DSPATR(HI)
     A  51                                  DSPATR(PC)
     A            C1MIXFL   R        B  8 27REFFLD(TFREF/@@MIXFL *LIBL/TFPREF)
     A                                      DSPATR(HI)
     A  52                                  DSPATR(PC)
     A            C1VOLFL   R        B  8 31REFFLD(TFREF/@@VOLFL *LIBL/TFPREF)
     A                                      DSPATR(HI)
     A  53                                  DSPATR(PC)
     A            C1SYSFL   R        B  8 35REFFLD(TFREF/@@SYSFL *LIBL/TFPREF)
     A                                      DSPATR(HI)
     A  54                                  DSPATR(PC)
     A            C1CGCD    R        B  8 39REFFLD(TFREF/@@CGCD *LIBL/TFPREF)
     A                                      DSPATR(HI)
     A  55                                  DSPATR(PC)
     A            C1TFCGCD  R        B  8 45REFFLD(TFREF/@@TFCGCD *LIBL/TFPREF)
     A                                      DSPATR(HI)
     A  56                                  DSPATR(PC)
     A            C1TFCLCD  R        B  8 50REFFLD(TFREF/@@TFCLCD *LIBL/TFPREF)
     A                                      DSPATR(HI)
     A  57                                  DSPATR(PC)
     A            C1ITYCD   R        B  8 55REFFLD(TFREF/@@ITYCD *LIBL/TFPREF)
     A                                      DSPATR(HI)
     A            C1PESN    R        B  8 61REFFLD(TFREF/@@PESN *LIBL/TFPREF)
     A                                      DSPATR(HI)
     A            C1PGM     R        B  8 70REFFLD(TFREF/@@PGM *LIBL/TFPREF)
     A                                      DSPATR(HI)
     A*
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R TF419K1
     A*%%TS  SD  20080610  165218  ISLRAMS     REL-V5R4M0  5722-WDS
     A  43                             21  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 22  2'F3=Exit'
     A N99                             22 11'F4=Prompt'
     A                                 22 22'F5=Refresh'
     A                                 22 34'F6=Create'
     A                                 22 45'F7=Listing'
     A                                 22 57'F13=Fold/Drop'
     A                                 23 57'F20=Spool Files'
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *---------------------------------------------------------------
     A          R TF419ES                   SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE CONTROL FORMAT
      *---------------------------------------------------------------
     A          R TF419EC                   SFLCTL(TF419ES)
      *
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
