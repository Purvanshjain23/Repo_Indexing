/***************************************************************** */
/*  ENVIRONMENT -----> HOG PRODUCTION SYSTEM                       */
/*  PROGRAM NUMBER --> HP4074CL                                    */
/*  PROGRAM NAME ----> COMMUTER HERD MOVEMENT LOG                  */
/*  PROGRAMMER ------> LEANNE RAMSEY                               */
/*  DATE ------------> 12/21/11                                    */
/*                                                                 */
/*  PROGRAM PURPOSE -> THIS CL CALLS A PROGRAM TO BUILD A WORKFILE */
/*                     OF TRANSFER MOVEMENTS AND THEN CALLS A      */
/*                     PROGRAM TO PRINT THE WORKFILE.              */
/*                                                                 */
/*******************************************************************/
/* MODIFIED:                                                       */
/* 10/19/16  LARA BUSER (E7606)                                    */
/*    DATE TYPE SELECTOR FLAG IS PASSED INTO THIS PROGRAM          */
/*    USING THE *LDA. THIS FLAG WILL BE PASSED TO THE PRINT PROGRAM*/
/*    'S' = SELECT BY SCHEDULED DATE.                              */
/*    'R' = SELECT BY RECEIVED DATE.                               */
/*    NEW LOGICAL, DUP HSL3074A INTO QTEMP                         */
/* 09/13/22 EL 103390 COMMUTER HERD MOVEMENT LOG SELECTION            */
/*             COMMENT OUT CPYF AS LIBRARY LARAB D/N EXIST ANY MORE   */
/*******************************************************************/
             PGM

             DCL        VAR(&QDATA) TYPE(*CHAR) LEN(100)
             DCL        VAR(&LDFSBO) TYPE(*CHAR) LEN(5)
             DCL        VAR(&LDFYMD) TYPE(*DEC) LEN(8)
             DCL        VAR(&LDTYMD) TYPE(*DEC) LEN(8)
             DCL        VAR(&LDDTSEL) TYPE(*CHAR) LEN(1)

             DCL        VAR(&HOLD) TYPE(*CHAR) LEN(10)
             DCL        VAR(&COPYS) TYPE(*DEC) LEN(2)
             DCL        VAR(&OUTQ) TYPE(*CHAR) LEN(10)

             CHGVAR     VAR(&LDFSBO) VALUE(%SST(*LDA 1 5))
             CHGVAR     VAR(&LDFYMD) VALUE(%SST(*LDA 6 8))
             CHGVAR     VAR(&LDTYMD) VALUE(%SST(*LDA 14 8))
             CHGVAR     VAR(&LDDTSEL) VALUE(%SST(*LDA 260 1))

/*-------------------------------------------------------------------*/
/* CREATE WORKFILE                                                   */
/*-------------------------------------------------------------------*/

             CRTDUPOBJL OBJ(HSP3074) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)
             CRTDUPOBJL OBJ(HSL3074A) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)

/*-------------------------------------------------------------------*/
/*  SETUP QUERY OVER THE TRANSFER HEADER FILE                        */
/*-------------------------------------------------------------------*/
/*  SELECT TRANSFERS WITH A RECEIVED DATE IN THE USER'S RANGE        */

/*  IF REPORT IS FOR RECEIVED DATE RANGE:                            */
             IF         COND(&LDDTSEL *EQ 'R') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 1 10)) VALUE('MHRCDT *GE')
             CHGVAR     VAR(%SST(&QDATA 12 8)) VALUE(&LDFYMD)
             CHGVAR     VAR(%SST(&QDATA 21 15)) VALUE('*AND MHRCDT +
                          *LE')
             CHGVAR     VAR(%SST(&QDATA 37 8)) VALUE(&LDTYMD)
             ENDDO

/*  IF REPORT IS FOR SCHEDULED DATE RANGE:                           */
             IF         COND(&LDDTSEL *EQ 'S') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 1 10)) VALUE('MHSCDT *GE')
             CHGVAR     VAR(%SST(&QDATA 12 8)) VALUE(&LDFYMD)
             CHGVAR     VAR(%SST(&QDATA 21 15)) VALUE('*AND MHSCDT +
                          *LE')
             CHGVAR     VAR(%SST(&QDATA 37 8)) VALUE(&LDTYMD)
             ENDDO

/* IF BUSINESS OFFICE                                                */

             IF         COND(&LDFSBO *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 46 18)) VALUE('*AND (MHORBO +
                          *EQ "')
             CHGVAR     VAR(%SST(&QDATA 64 5)) VALUE(&LDFSBO)
             CHGVAR     VAR(%SST(&QDATA 69 1)) VALUE('"')
             CHGVAR     VAR(%SST(&QDATA 71 16)) VALUE('*OR MHDNBO +
                          *EQ "')
             CHGVAR     VAR(%SST(&QDATA 87 5)) VALUE(&LDFSBO)
             CHGVAR     VAR(%SST(&QDATA 92 2)) VALUE('")')
             ENDDO

             OVRDBF     FILE(HSP074) SHARE(*YES)
             OPNQRYF    FILE((HSP074)) QRYSLT(&QDATA) KEYFLD(*FILE) +
                          SEQONLY(*YES)

/*-------------------------------------------------------------------*/
/*  BUILD THE WORKFILE                                               */
/*-------------------------------------------------------------------*/

             CALL PGM(HP3074)
/* FOR TESTING PURPOSES                                              */
/* CPYF HSP3074 LARAB/HSP3074 CRTFILE(*YES) MBROPT(*REPLACE)         */
/*           MONMSG     MSGID(CPF9999 CPF0000)                       */

             CLOF       OPNID(HSP074)
             DLTOVR     FILE(HSP074)

/*-------------------------------------------------------------------*/
/* SET UP PRINT OVERRIDES                                            */
/*-------------------------------------------------------------------*/

 HOLD:       IF         COND(%SST(*LDA 411 1) = 'Y') THEN(CHGVAR +
                          VAR(&HOLD) VALUE(*YES))
             ELSE       CMD(CHGVAR VAR(&HOLD) VALUE(*NO))

 COPYS:      CHGVAR     VAR(&COPYS) VALUE(%SST(*LDA 412 1))
             IF         COND(&COPYS = 0) THEN(CHGVAR VAR(&COPYS) +
                          VALUE(1))

OUTQ:        CHGVAR     VAR(&OUTQ) VALUE(%SST(*LDA 413 10))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(%SST(*LDA 401 10)))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(*JOB))

/*------------------------------------------------------------------*/
/* OVERRIDE PRINT FILE                                              */
/*------------------------------------------------------------------*/

             OVRPRTF    FILE(PRINT80)  OUTQ(&OUTQ) COPIES(&COPYS) +
                          HOLD(&HOLD)

/*-------------------------------------------------------------------*/
/* PRINT THE REPORT                                                  */
/*-------------------------------------------------------------------*/

             CALL       PGM(HP6074)
             DLTOVR     FILE(*ALL)

             ENDPGM

