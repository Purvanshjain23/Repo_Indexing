      *****************  RPG PROGRAM HEADING  ***********************
      *
      * SYSTEM:      HOG PRODUCTION
      * PROGRAM:     HP145
      * TITLE:       MAINTAIN SCHEDULED SALE MOVEMENT
      * PROGRAMMER:  LEANNE FEDOR
      * CREATED:     09/21/95
      *
      * FUNCTION: THIS PROGRAM USES A SUBFILE TO MAINTAIN RECORDS
      *           IN THE SALES FILES (HEADER AND DETAIL)
      *           FOR SCHEDULED SALES MOVEMENTS ONLY.
      *
      *           THE PROGRAM WILL CREATE, DELETE AND UPDATE RECORDS
      *           FOR A SPECIFIC MOVEMENT NUMBER.
      *
      *           THIS PROGRAM IS CALLED FROM:
      *                HP445-WORK WITH SCHEDULED SALES MOVEMENTS.
      *
      *           IT DOES NOT HAVE SELECTORS OR POSITIONERS.
      *           IT HAS F4-PROMPT LOGIC.
      *           IT USES THE STANDARD MESSAGE HANDLING PROGRAM.
      *
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      * 10/04/95  LEANNE FEDOR
      *           ADDED BUILDING TO SUBFILE AS I/O FIELD. CHANGED GROUP
      *           LOGIC TO DEFAULT A GROUP IF THERE IS ONLY ONE OPEN
      *           GROUP IN THE BUILDING OR FORCE THE USER TO SELECT A
      *           GROUP IF THERE ARE MULTIPLE GROUPS IN THE BUILDING.
      *
      * 03/27/96  LEANNE FEDOR
      *           ADDED SECURITY CHECK TO DETERMINE IF USER IS
      *           AUTHORIZED TO DELETE MOVEMENT.
      *
      * 04/01/96  LEANNE FEDOR
      *           MODIFIED THE DELETION LOGIC TO:
      *            1) CALL A CONFIRM DELETE PROGRAM WHERE A COMMENT
      *               MUST BE ENTERED.
      *            2) WRITE AUDIT INFO FOR THE DELETED MOVEMENT TO
      *               THE NEW 'DELETED MOVEMENT' FILE.
      *
      * 04/30/96  LEANNE FEDOR
      *           CHANGED DELETION SECURITY LOGIC. DELETION ERROR
      *           MESSAGES ARE NOW ISSUED FROM THE DELETION SECURITY
      *           PROGRAM.
      *
      * 05/08/96  LEANNE FEDOR
      *           MADE CHANGES TO FACILITATE NEW JDE RELEASE CONVERSION
      *           CHANGED SEARCH TYPE TO 3A; CALL NEW GENERIC
      *           PROGRAM HPJDEV TO VALIDATE INSTEAD OF HPJDEA SINCE
      *           NO VENDOR ADDRESS IS REQUIRED.
      *
      * 10/10/97  LEANNE FEDOR
      *           CHANGED F10=ACCEPT TO F9=ACCEPT SO THAT THE ACCEPT KEY
      *           IS PHYSICALLY ON THE KEYBOARD FARTHER FROM THE F11
      *           DELETE KEY.
      *
      * 01/11/99  LeAnne Fedor
      *           New field 'scheduled kill date' was added to sales movement
      *           header file. Added logic to a) edit and b) populate field.
      *
      * 10/21/99  LeAnne Fedor
      *           We added a new option 4=delete to the Work Withs.  So, if the
      *           incoming mode is delete, do not display any screens.  Simply
      *           perform the deletion logic and exit. (Note: use CANCELLED for
      *           the required comment value.
      *
      * 11/09/99  LeAnne Fedor
      *           Replaced join file of building/group with logical of group.
      *           Added room to subfile line.
      *
      * 10/17/00  LeAnne Fedor
      *           Added new field 'load type' in sales movement header
      *           file to screen.
      *
      * 02/05/01  LeAnne Fedor
      *           In preparation for allowing groups on a farm to have different
      *           production types, we added the group 'production type' as output only
      *           in the subfile. Also, we added an edit that all groups on a movement
      *           must have the same production type----it does not have to match the
      *           production type of the farm in the farm master.
      *
      * 07/17/02  LeAnne Fedor
      *           In preparation for allowing groups on a farm to have different
      *           production phases, we added the group 'production phase' as output only
      *           in the subfile. Also, we added an edit that all groups on a movement
      *           must have the same production phase---it does not have to match the
      *           production phase of the farm in the farm master.
      *
      * 08/01/02  LeAnne Fedor
      *           Added edit to prevent a sale out of a group whose 'open' date is after
      *           any/all of the following dates: scheduled ship date, scheduled kill date.
      *
      * 12/17/02  LeAnne Fedor
      *           We removed 'production phase/type' from the Sales Movement Header. So,
      *           we removed the edit that all groups on a movement must have the same
      *           type/phase.
      *
      * 01/07/03  LeAnne Fedor
      *           Added edit that 'trucker' must be active.
      *
      * 01/15/03  LeAnne Fedor
      *           Removed 'sex' and 'genetic line' from Sales Movement Detail file.
      *           Removed 'shipped in/out times' from Sales Movement Header file.
      *
      * 01/21/03  LeAnne Fedor
      *           Added logic for 2 'versions': full and restricted. The incoming parm
      *           controls the version.
      *
      *           Also, load type is now a required field.
      *
      *           Also, for 1) Market Sales that 2) have a Scheduled Kill Date, we will use
      *           this Schedueld Kill Date for the Movement Event Date. Otherwise, we will
      *           stick with putting the Scheduled Ship Date into the Movement Event Date.
      *
      * 04/01/04  LeAnne Fedor
      *           You can no longer have Nursery groups on Market Sales; you can
      *           have BGF and GF groups.
      *
      * 01/18/06  LeAnne Fedor
      *           Recompile only.
      *           Field "Bin Set" was removed from HSP020-Building Rooms file
      *
      * 07/02/09  LeAnne Ramsey
      *           Recompile only. Added new field 'Continuous Flow Flag' to Hog Group file.
      *
      * 10/15/13  LeAnne Ramsey (E2831)
      *           Recompile only. Added field 'MTech Reference'.
      *
      * 10/21/13  LeAnne Ramsey (E2831)
      *           Added MTech Reference as a parm on the calls to:
      *               HP538-Select Trucker
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     FHPD145    CF   E             WORKSTN SFILE(HP145S1:HDRRN1)
     F                                     INFDS(IOFEED)
      *
      *
     FHSP018    IF   E           K DISK
      *    FARM SITE
      *
      *
     FHSP019    IF   E           K DISK
      *    FARM BUILDINGS
      *
      *
     Fhsp020    if   e           k disk
      *    building rooms
      *
      *
     FHSP034    IF   E           K DISK
      *  HOG GROUPS
      *
      *
     FHSL034D   IF   E           K DISK    RENAME(HGREC:HGRECD)
      *  HOG GROUPS
      *
      *
     Fhsl034p   if   e           k disk    rename(hgrec:hgrecp)
      *  Hog groups
      *
      *
     Fhsl034r   if   e           k disk    rename(hgrec:hgrecr)
      *  Hog groups
      *
      *
     FHSP046    IF   E           K DISK
      *    TRUCKER ID
      *
      *
     FHSP058    UF A E           K DISK
      *  MOVEMENT EVENT
      *
     FHSP066    IF   E           K DISK
      *    SALES TYPE
      *
      *
     Fhsp067    if   e           k disk
      *    Load type
      *
      *
     FHSP080    O    E             DISK
      *  DELETED MOVEMENTS
      *
      *
     FHSP084    UF A E           K DISK
      *  SALE HEADER
      *
      *
     FHSP085    UF A E           K DISK
      *  SALE DETAIL
      *
      /EJECT
      ****************************************************************
      * TABLE AND ARRAY SPECIFICATIONS
      ****************************************************************
      *
      *---------------------------------------------------------------
      *  STANDARD MESSAGE ARRAYS
      *---------------------------------------------------------------
      *
      *---------------------------------------------------------------
      * HOG GROUP ARRAY USED TO VALIDATE THAT A GROUP IS ON A MOVEMENT
      * ONLY ONE TIME
      *---------------------------------------------------------------
      *
     D GRP             S              7    DIM(50)                              GROUPS
      /EJECT
      ****************************************************************
      * INPUT SPECIFICATIONS
      ****************************************************************
      *
      *---------------------------------------------------------------
      * STANDARD ERROR MESSAGE HANDLING DATA STRUCTURES
      *---------------------------------------------------------------
      *
      * FOR ERROR MESSAGE HANDLING, A PACKED INDEX (E) IS REQUIRED.
      * FOR PROGRAM READABILITY, DEFINE A CORRESPONDING ERROR COUNT
      * FIELD CALLED 'ERROR'. ALSO DEFINE CURSOR LOCATION FIELDS.
      *
     D                 DS                  INZ
     D  ERROR                  1      2P 0
     D  E                      1      2P 0
      *
      * THIS DATA STRUCTURE SUPPLIES THE NAME OF THE MESSAGE FILE TO
      * THE MESSAGE HANDLING CL PROGRAM.  THE FIELD NAME MSGFIL MUST BE
      * CONSTANT. THE VALUE IN QUOTES IS THE NAME OF THE SPECIFIC
      * MESSAGE FILE CONTAINING THE USER DEFINED MESSAGES.
      *
     D                 DS                  INZ
     D  MSGFIL                 1     10    INZ('HSMSGF    ')
      *
      * THE FOLLOWING 3 DATA STRUCTURES ARE USED TO SPEED MESSAGE
      * HANDLING SINCE IT IS FASTER TO CLEAR DATA STRUCTURES THAN
      * ARRAYS.  EACH IS ASSOCIATED WITH A STANDARD MESSAGE ARRAY.
      *
     D MGI             DS           140    INZ
     D  MGID                   1    140
     D                                     DIM(20)                              MSG ID ARRAY
      *
     D MGD             DS          1000    INZ
     D  MGDT                   1   1000
     D                                     DIM(20)                              MSG PARMS
      *
     D MGWK            DS            50    INZ
     D  MGW                    1     50
     D                                     DIM(50)                              MSG WORK PARMS
      *
      *---------------------------------------------------------------
      * STANDARD PROGRAM STATUS DATA STRUCTURE
      *---------------------------------------------------------------
      *    EXTERNALLY DEFINED AS UTPGFR (RECORD FORMAT: PGMDSR)
     D PGMDS         ESDS                  EXTNAME(UTPGFR)
      *
      *---------------------------------------------------------------
      * STANDARD WORKSTATION INFORMATION DATA STRUCTURE
      *---------------------------------------------------------------
      *    EXTERNALLY DEFINED AS UTWSFR (RECORD FORMAT: UTIDFRR)
     D IOFEED        E DS                  EXTNAME(UTWSFR)
      *
      *---------------------------------------------------------------
      * STANDARD DATABASE FILE INFORMATION DATA STRUCTURE
      *---------------------------------------------------------------
      *    EXTERNALLY DEFINED AS UTDBFR (RECORD FORMAT: FDBCKD)
     D DBFEED        E DS                  EXTNAME(UTDBFR)
      *
      *---------------------------------------------------------------
      *  DATE FORMATS
      *---------------------------------------------------------------
      *
      * DATE IN MMDDYY FORMAT
      *
     D                 DS
     D  WKMDY                  1      6  0
     D  WKMM                   1      2  0
     D  WKDD                   3      4  0
     D  WKYY                   5      6  0
      *
      * DATE IN CCYYMMDD FORMAT
      *
     D                 DS
     D  WKCYMD                 1      8  0
     D  WKCEN                  1      2  0
     D  WKYER                  3      4  0
     D  WKMON                  5      6  0
     D  WKDAY                  7      8  0
      *
      *---------------------------------------------------------------
      *  TIME FORMAT
      *---------------------------------------------------------------
      *
     D                 DS
     D  HHMMSS                 1      6  0
     D  HHMM                   1      4  0
      *
      *---------------------------------------------------------------
      *  DATA AREA FOR NEXT MOVEMENT NUMBER
      *---------------------------------------------------------------
      *
     D                 DS
     D  DAMVSN                 1      7  0
      *
      *---------------------------------------------------------------
      *  DEFINITION FOR EXTERNAL DATA AREA 'DAAPER'
      *---------------------------------------------------------------
     D                 DS
     D  DAAPER                 1     24
     D  DACCYY                 1      4
     D  DAPER                  5      6
     D  DABPDT                 7     14  0
     D  DAEPDT                15     22  0
     D  DAPGFL                23     23
     D  DAPPFL                24     24
      *
      *---------------------------------------------------------------
      *  NAMED CONSTANTS
      *---------------------------------------------------------------
      *
     D CREATE          C                   CONST('CREATE')
     D REVISE          C                   CONST('REVISE')
     D DELETE          C                   CONST('DELETE')
     D VIEW            C                   CONST('VIEW  ')
     D SELECT          C                   CONST('SELECT')
     D YES             C                   CONST('Y')
     D NO              C                   CONST('N')
     D SET1            C                   CONST('SET1  ')
     D EDIT1           C                   CONST('EDIT1 ')
     D SCRN1           C                   CONST('SCRN1 ')
     D EXIT            C                   CONST('EXIT  ')
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * MAINLINEE
      ****************************************************************
      *
     C     *INLR         DOWEQ     *OFF                                         MAIN DO LOOP
      *
     C     ROUTNE        CASEQ     SET1          $SET1
     C     ROUTNE        CASEQ     SCRN1         $SCRN1
     C     ROUTNE        CASEQ     EDIT1         $EDIT1
      *
     C     ROUTNE        CASEQ     EXIT          $EXIT
     C                   ENDCS
     C                   ENDDO                                                  MAIN DO LOOP
      /EJECT
      *----------------------------------------------------------------
      * $SET1 - SUBROUTINE TO SET ENVIRONMENT FOR SCREEN 1
      *----------------------------------------------------------------
      *
     C     $SET1         BEGSR
      *
      * IF THE USER IS IN CREATE MODE, CLEAR THE MOVEMENT
      * NUMBER BUT LEAVE REMAINING FIELDS.
      *
     C                   SELECT
     C     MODE          WHENEQ    CREATE                                       WH CREATE
     C                   Z-ADD     0             C1MVSN
     C                   OTHER
      *
      * IF THE USER IS REVISING, VERIFY THAT THE SALES MOVEMENT
      *    THAT THE USER SELECTED STILL EXISTS AND SETUP THE HEADER
      *    INFO FOR THE CONTROL RECORD
      *
     C     C1MVSN        CHAIN(N)  HSP084                             90
     C     *IN90         IFEQ      *OFF                                         IF FOUND
     C                   EXSR      $HDR
     C                   ELSE
     C                   MOVEL     YES           NOTFLG
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS00592'     MGID(E)
     C                   CLEAR                   MGW
     C                   MOVEL     C1MVSN        XXMVSN
     C                   MOVEA     XXMVSN        MGW(1)
     C                   MOVEA     MGW           MGDT(E)
     C                   ENDIF
     C                   ENDIF                                                  IF FOUND
     C                   ENDSL
      *
      *  CLEAR/FILL THE SUBFILE
      *
     C                   EXSR      $CLR1
     C                   EXSR      $FRSH1
      *
      *  SET ROUTINE TO DISPLAY SCREEN
      *
     C                   MOVEL     SCRN1         ROUTNE
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * $SCRN1 - SUBROUTINE TO PERFORM OPERATOR I/O FOR SCREEN 1
      *---------------------------------------------------------------
      *
     C     $SCRN1        BEGSR
      *
      * SET THE PAGE OF THE SUBFILE TO DISPLAY
      *
     C                   SELECT
     C     WKFLOW        WHENEQ    0
     C     ERROR         ANDEQ     0
     C                   Z-ADD     1             HDPAG1
      *
     C     RESET         WHENEQ    YES
     C     WKFLOW        IFGT      HDRRN1
     C                   ELSE
     C                   Z-ADD     WKFLOW        HDPAG1
     C                   ENDIF
     C                   ENDSL
      *
      * SET THE INDICATORS TO CONTROL SCREEN FIELDS
      *
     C                   EXSR      $IND
      *
      * IF THE USER HAS EDITED THE DATA, THEN ISSUE THE 'ACCEPT
      * CHANGES' MESSAGE.
      *
     C     ERROR         IFEQ      0
     C     EDITFL        ANDEQ     YES
     C                   Z-ADD     1             ERROR
     C                   MOVEL     'HS09026'     MGID(E)
     C                   ENDIF
      *
      * WRITE THE ERROR MESSAGES FROM THE ERROR ARRAYS TO THE
      * ERROR MESSAGE SUBFILE
      *
     C                   EXSR      $WRMSG
      *
      *  WRITE SCREEN 1 TO CRT
      *
     C                   WRITE     HP145K1
     C                   WRITE     HP145EC
     C                   EXFMT     HP145C1
      *
      * RETRIEVE THE LOWEST SUBFILE RRN ON THE SCREEN FROM THE DSPF
      * FEEDBACK AREA (IDFLOW) AND MOVE IT TO A WORK FIELD.  THIS VALUE
      * WILL BE USED LATER TO CONTROL THE REPOSITIONING OF THE SUBFILE.
      *
     C                   Z-ADD     IDFLOW        WKFLOW
     C                   MOVEL     NO            RESET
     C                   MOVEL     NO            EDITFL
      *
      * CLEAR ALL MESSAGES
      *
     C                   EXSR      $CLMSG
      *
      * GET USER'S ENTRY AND RESET STANDARD RESPONSE
      *
     C                   SELECT
     C     *IN03         WHENEQ    *ON                                          F3-EXIT
     C                   MOVEL     '03'          RTNCD
     C                   MOVEL     EXIT          ROUTNE
      *
     C     *IN04         WHENEQ    *ON                                          F4-PROMPT
     C                   EXSR      $F4S1
     C                   MOVEL     SCRN1         ROUTNE
      *
     C     *IN05         WHENEQ    *ON                                          F5-REFRESH
     C                   MOVEL     YES           RESET
     C                   MOVEL     SET1          ROUTNE
      *
     C     *IN09         WHENEQ    *ON                                          F9-ACCEPT
      *
      *----------------------------------------------------------------
      * REVISING AN EXISTING SALES MOVEMENT
      *----------------------------------------------------------------
      *
     C     MODE          IFEQ      REVISE
     C                   EXSR      $EDIT1
     C     ERROR         IFEQ      0
      *
      * DELETE EXISTING SALES HEADER AND ALL SALES DETAIL RECORDS
      * AND ALL MOVEMENT EVENT RECORDS
      *
     C                   EXSR      $DLTSH
     C                   EXSR      $DLTSD
     C                   EXSR      $DLTME
      *
      * CREATE NEW SALES HEADER AND DETAIL RECORDS AND NEW
      * MOVEMENT EVENT RECORDS FOR EACH DETAIL RECORD.
      *
     C                   EXSR      $CRTSH
     C                   EXSR      $CRTSD
      *
     C                   MOVEL     EXIT          ROUTNE
     C                   ELSE
     C                   MOVEL     SCRN1         ROUTNE
     C                   ENDIF
     C                   ENDIF
      *
      *----------------------------------------------------------------
      * CREATING A NEW SALES MOVEMENT
      *----------------------------------------------------------------
      *
     C     MODE          IFEQ      CREATE
     C                   EXSR      $EDIT1
     C     ERROR         IFEQ      0
      *
      * 1) CREATE THE SALES HEADER RECORD
      * 2) CREATE ALL DETAIL RECORDS FOR THIS SALE
      *    WITH A MOVEMENT EVENT RECORD FOR EACH DETAIL RECORD.
      *
     C                   EXSR      $CRTSH
     C                   EXSR      $CRTSD
      *
      * ISSUE THE MESSAGE THAT THE SALES MOVEMENT WAS CREATED
      *
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS00594'     MGID(E)
     C                   CLEAR                   MGW
     C                   MOVEL     DAMVSN        XXMVSN
     C                   MOVEA     XXMVSN        MGW(1)
     C                   MOVEA     MGW           MGDT(E)
     C                   ENDIF
      *
     C                   MOVEL     SET1          ROUTNE
     C                   ELSE
     C                   MOVEL     SCRN1         ROUTNE
     C                   ENDIF
     C                   ENDIF
      *
      *----------------------------------------------------------------
      * DELETING A SALES MOVEMENT
      *----------------------------------------------------------------
      *
     C     *IN11         WHENEQ    *ON                                          F11-DELETE
     C                   EXSR      $DLT11
      *
     C     *IN12         WHENEQ    *ON                                          F12-PREVIOUS
     C                   MOVEL     '12'          RTNCD
     C                   MOVEL     EXIT          ROUTNE
      *
     C     *IN25         WHENEQ    *ON                                          F25-ROLLUP
     C     IDFLOW        ADD       WKPLIN        WKDIFF
     C     HDRRN1        IFGE      WKDIFF                                       IF SOMETHING
     C                   ELSE
     C                   Z-ADD     COUNT         HDRRN1
     C                   EXSR      $SETOF
     C                   EXSR      $CLRLN
     C                   DO        WKPLIN                                       DO FILL
     C                   EXSR      $BLANK
     C                   ENDDO                                                  DO FILL
     C                   ENDIF                                                  IF SOMETHING
     C                   MOVEL     SCRN1         ROUTNE
      *
     C     NOTFLG        WHENEQ    YES
     C                   MOVEL     SET1          ROUTNE
     C                   OTHER
      *
      * USER PRESSED 'ENTER'
      *
     C                   EXSR      $EDIT1
     C                   ENDSL
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * $EDIT1 - SUBROUTINE TO EDIT INPUT FIELDS FOR SCREEN 1
      *---------------------------------------------------------------
      *
     C     $EDIT1        BEGSR
      *
     C                   MOVEL     YES           EDITFL
      *
      * EDIT THE CONTROL RECORD
      *
     C                   EXSR      $EDC1
      *
      * EDIT THE SUBFILE IF THERE ARE NO ERRORS IN THE CONTROL
      * RECORD. IF THERE ARE ERRORS IN THE CONTROL RECORD, YOU
      * NEED TO REWRITE ANY SUBFILE RECORDS WITH THE ERROR INDICATORS
      * OFF.
      *
     C                   IF        ERROR = 0
     C                   EXSR      $EDS1
     C                   ELSE
     C                   EXSR      $OFFIN
     C                   ENDIF
      *
     C                   IF        ERROR = 0
     C                   Z-ADD     1             HDPAG1
     C                   ENDIF
      *
     C                   MOVEL     SCRN1         ROUTNE
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * $EDC1 - SUBROUTINE TO EDIT THE CONTROL RECORD INFO ON SCREEN 1
      *---------------------------------------------------------------
      *
     C     $EDC1         BEGSR
      *
      * Validate:
      *    1) Sales type
      *    2) Load type
      *    3) CUSTOMER
      *    4) SCHEDULED SHIP DATE
      *    5) Scheduled kill date (if sales type was valid)
      *    6) FARM SITE
     C                   exsr      $stcd
     C                   exsr      $ltcd
     C                   EXSR      $CVNO
     C                   EXSR      $SCDT
      *
     C                   if        *in47 = *off                                 If sales type OK
     C                   exsr      $sckldt
     C                   endif                                                  If sales type OK
      *
     C                   EXSR      $FSCD
     C                   EXSR      $TICD
      *
      *
      * SCHEDULED HEAD TO SHIP
      *
     C     C1SCHD        IFLT      0
     C                   SETON                                        79
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS09000'     MGID(E)
     C                   ENDIF
     C                   ENDIF
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * Validate sales typoe
      *----------------------------------------------------------------
      *
     C     $stcd         begsr
      *
     C                   SELECT
     C     C1STCD        WHENEQ    *BLANK                                       WH NO TYPE
     C                   SETON                                        47
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS09001'     MGID(E)
     C                   ENDIF
     C                   OTHER
      *
     C     C1STCD        CHAIN     HSP066                             92
     C     *IN92         IFEQ      *ON                                          IF BAD TYPE
     C                   SETON                                        47
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS00635'     MGID(E)
     C                   CLEAR                   MGW
     C                   MOVEA     C1STCD        MGW(1)
     C                   MOVEA     MGW           MGDT(E)
     C                   ENDIF
     C                   ENDIF                                                  IF BAD TYPE
     C                   ENDSL
      *
     C                   endsr
      /EJECT
      *----------------------------------------------------------------
      * Validate Load Type
      *----------------------------------------------------------------
      *
      * Validations:
      *   1) optional
      *   2) must be valid in load type master
      *
     C     $ltcd         begsr
      *
     C                   eval      c1ltcd = %triml(c1ltcd)
      *
     C                   select
     C                   when      c1ltcd = *blank
     C                   seton                                        30
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS09001'     mgid(e)
     C                   endif
     C                   other
      *
     C     c1ltcd        chain     hsp067                             92
     C                   if        *in92 = *on                                  If no hit
     C                   seton                                        30
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS00188'     mgid(e)
     C                   clear                   mgw
     C                   movea     c1ltcd        mgw(1)
     C                   movea     mgw           mgdt(e)
     C                   endif
     C                   endif                                                  If no hit
      *
     C                   endsl
      *
     C                   endsr
      /EJECT
      *----------------------------------------------------------------
      * $CVNO - VALIDATE CUSTOMER NUMBER
      *----------------------------------------------------------------
      *
     C     $CVNO         BEGSR
      *
      * VALIDATE THE CUSTOMER
      *
     C                   MOVE      *BLANK        C1CNAM
     C     C1CVNO        IFEQ      0                                            IF NO CUST
     C                   SETON                                        32
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS09001'     MGID(E)
     C                   ENDIF
     C                   ELSE
      *
     C                   Z-ADD     C1CVNO        INAJD
     C                   MOVEL(P)  'C  '         INAT1
     C                   EXSR      $JDEV
      *
     C     JDEFL         IFEQ      YES
     C                   SETON                                        32
     C                   ELSE
     C                   MOVEL     OUALPH        C1CNAM
     C                   ENDIF
     C                   ENDIF                                                  IF NO CUST
      *
      *
      * If this is the Restricted version, the user is not allowed to access
      * Market Sales for Seaboard.
      *
     C                   if        version = 'R' and                            If SBD market
     C                             c1stcd = 'MRKTS' and c1cvno = 360516
     C                   seton                                        3247
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS00885'     mgid(e)
     C                   clear                   mgw
     C                   movea     dfmode        mgw(1)
     C                   movea     c1stcd        mgw(7)
     C                   movel     c1cvno        xxalphcvno
     C                   movea     xxalphcvno    mgw(12)
     C                   movea     mgw           mgdt(e)
     C                   endif
     C                   endif                                                  If SBD market
      *
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $FSCD - EDIT FARM SITE
      *----------------------------------------------------------------
      *
     C     $FSCD         BEGSR
      *
     C                   MOVE      *BLANK        C1FSNM
     C     C1FSCD        IFEQ      0                                            IF NO FARM
     C                   SETON                                        31
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS09001'     MGID(E)
     C                   ENDIF
     C                   ELSE
      *
      * CHECK THAT FARM SITE EXISTS IN THE FARM SITE MASTER FILE
      *
     C     C1FSCD        CHAIN     HSP018                             92
     C     *IN92         IFEQ      *OFF                                         IF GOOD FARM
     C                   MOVEL     FSFSNM        C1FSNM
     C                   ELSE
      *
      * INVALID FARM SITE
     C                   SETON                                        31
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS00131'     MGID(E)
     C                   CLEAR                   MGW
     C                   MOVE      C1FSCD        xxalphfscd
     C                   MOVEA     xxalphfscd    MGW(1)
     C                   MOVEA     MGW           MGDT(E)
     C                   ENDIF
     C                   ENDIF                                                  IF GOOD FARM
     C                   ENDIF                                                  IF NO FARM
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * Validate Trucker
      *---------------------------------------------------------------
      *
      * Validations:
      *   1) required
      *   3) must exist in Trucker master file
      *   3) must be 'active'
      *
     C     $ticd         begsr
      *
     C                   MOVE      *BLANK        C1TINM
      *
     C                   select
     C                   when      c1ticd = *blank
     C                   other
      *
     C     C1TICD        CHAIN     HSP046                             92
     C     *IN92         IFEQ      *ON                                          IF BAD
     C                   SETON                                        33
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS00235'     MGID(E)
     C                   CLEAR                   MGW
     C                   MOVEA     C1TICD        MGW(1)
     C                   MOVEA     MGW           MGDT(E)
     C                   ENDIF
     C                   ELSE
      *
     C                   MOVEL     TITINM        C1TINM
      *
     C                   if        tiaist <> 'A'                                If not active
     C                   seton                                        33
     C                   if        error < maxmsg
     C                   add       1             error
     C                   exsr      $err1
     C                   eval      mgid(e) = 'HS00231'
     C                   clear                   mgw
     C                   movea     c1ticd        mgw(1)
     C                   movea     mgw           mgdt(e)
     C                   endif
     C                   endif                                                  If not active
     C                   ENDIF                                                  IF BAD
     C                   endsl
      *
     C                   endsr
      /EJECT
      *----------------------------------------------------------------
      * $SCDT - EDIT SCHEDULED SHIP DATE
      *----------------------------------------------------------------
      *
     C     $SCDT         BEGSR
      *
     C                   SELECT
     C     C1MDY         WHENEQ    0
     C                   SETON                                        76
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVE      'HS00607'     MGID(E)
     C                   ENDIF
     C                   OTHER
      *
      * VALIDATE THAT THE DATE IS VALID AND ADD CENTURY VALUES
      * TO THE WORK FIELD FOR LATER USE IN THE UPDATING ROUTINE.
      *
     C                   Z-ADD     C1MDY         PFRM8
     C                   EXSR      $DATE
     C     PRTRN         IFNE      *BLANK                                       IF INVALID
     C                   SETON                                        76
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVE      'HS09004'     MGID(E)
     C                   ENDIF
     C                   ELSE
     C                   Z-ADD     PTO8          WKSCDT
     C                   ENDIF                                                  IF INVALID
      *
      * IF THE DATE IS VALID,
      *     CHECK THAT IT IS NOT IN A CLOSED PERIOD
      *     IF IT IS IN THE CURRENT PERIOD,
      *         CHECK THAT THE PERIOD CLOSE PROGRAMS ARE NOT RUNNING
      *
     C     *IN76         IFEQ      *OFF                                         IF GOOD DATE
      *
     C                   SELECT
     C     WKSCDT        WHENLT    DABPDT
     C                   SETON                                        76
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS09007'     MGID(E)
     C                   ENDIF
      *
     C     WKSCDT        WHENGE    DABPDT
     C     WKSCDT        ANDLE     DAEPDT
     C     DAPPFL        ANDNE     *BLANK
     C                   SETON                                        76
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS09016'     MGID(E)
     C                   ENDIF
     C                   ENDSL
      *
     C                   ENDIF                                                  IF GOOD DATE
     C                   ENDSL
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $sckldt - Edit scheduled kill date
      *----------------------------------------------------------------
      *
      * Validations:
      *  1) date must be blank if the sale is not a 'market' sale
      *  2) date is required for market sales when the customer is Guymon
      *  3) date is optional for market sales for customers other than Guymon
      *  4) if entered, date must be valid and
      *       a) cannot be in a closed period
      *       b) cannot be earlier than scheduled ship date
      *
      *
     C     $sckldt       begsr
      *
      *
     C                   select
      *
      * Date must be blank if the sale is not a 'market' sale
      *
     C                   when      c1stcd <> 'MRKTS' and
     C                             c1scklmdy <> 0
     C                   seton                                        75
     C                   if        error < maxmsg
     C                   add       1             error
     C                   move      'HS00760'     mgid(e)
     C                   endif
      *
      * Date is required for Guymon 'market' sales
      *
     C                   when      c1stcd = 'MRKTS' and
     C                             c1scklmdy = 0 and
     C                             c1cvno = 360516
     C                   seton                                        75
     C                   if        error < maxmsg
     C                   add       1             error
     C                   move      'HS00761'     mgid(e)
     C                   endif
      *
      * Validate that the date is valid and add century values to
      * the workfield for later use in the updating routine.
      *
     C                   when      c1scklmdy <> 0
     C                   z-add     c1scklmdy     pfrm8
     C                   exsr      $date
     C                   if        prtrn <> *blank                              If invalid
     C                   seton                                        75
     C                   if        error < maxmsg
     C                   add       1             error
     C                   move      'HS09004'     mgid(e)
     C                   endif
     C                   else
     C                   z-add     pto8          wksckldt
      *
      * If the date is valid,
      *   1) if scheduled ship date was valid, check that scheduled
      *      kill date is not prior to scheduled ship date.
      *   2) check that it is not in a closed period
      *   3) If it is in the current period,
      *      check that the period close programs are not running
      *
      *
     C                   select
      *
     C                   when      *in76 = *off and
     C                             wksckldt < wkscdt
     C                   seton                                        75
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS00762'     mgid(e)
     C                   endif
      *
     C                   when      wksckldt < dabpdt
     C                   seton                                        75
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS09007'     mgid(e)
     C                   endif
      *
     C                   when      wksckldt >= dabpdt and
     C                             wksckldt <= daepdt and
     C                             dappfl <> *blank
     C                   seton                                        75
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS09016'     mgid(e)
     C                   endif
     C                   endsl
      *
     C                   endif                                                  If invalid
     C                   endsl
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * $CLR1 - SUBROUTINE TO CLEAR THE SUBFILE ON SCREEN 1
      *---------------------------------------------------------------
      *
     C     $CLR1         BEGSR
      *
      * CLEAR CONTROL RECORD ACCUMULATORS
      *
     C                   Z-ADD     0             C1SCHD
      *
      * CLEAR WORKFIELD ACCUMULATORS
      *
     C                   Z-ADD     0             WKSCHD
      *
     C                   Z-ADD     0             HDRRN1
     C                   Z-ADD     0             COUNT
      *
     C                   CLEAR                   HP145S1
     C                   SETON                                        83        SFLCLR
     C                   WRITE     HP145C1
     C                   SETOFF                                       818283
      *                                                    81=SFLDSP
      *                                                    82=SFLEND
      *                                                    83=SFLCLR
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * $FRSH1 - SUBROUTINE TO REFRESH/FILL THE SUBFILE ON SCREEN 1
      *---------------------------------------------------------------
      *
     C     $FRSH1        BEGSR
      *
      * SET INDICATORS TO CONTROL/PROTECT SCREEN FIELDS
      *
     C                   EXSR      $IND
      *
      * LOADING THE SUBFILE IN THIS PROGRAM IS DIFFERENT THAN IN THE
      * SHELL PROGRAMS AND DEPENDS ON THE 'MODE'.
      * FOR CREATE MODE, WE LOAD A PAGE OF BLANK LINES.
      * FOR REVISE MODE, WE LOAD A PAGE OF DATABASE RECORDS AND, IF WE
      *                  HIT EOF, WE FINISH THE PAGE WITH BLANK LINES.
      *
     C                   SELECT
     C     MODE          WHENEQ    CREATE
     C                   EXSR      $SETOF
     C                   EXSR      $CLRLN
     C                   DO        WKPLIN
     C                   EXSR      $BLANK
     C                   ENDDO
      *
     C     MODE          WHENEQ    REVISE
     C     C1MVSN        SETLL     HSP085
     C     *IN82         DOUEQ     *ON
     C                   EXSR      $LOAD1
     C                   ENDDO
      *
      * FILL UP THE SUBFILE PAGE WITH BLANK LINES IF NECESSARY
      *
     C     WKPLIN        IFNE      0
     C     WKPLIN        ANDNE     COUNT
     C     COUNT         DIV       WKPLIN        WKFLD
     C                   MVR                     WKRMDR
     C     WKPLIN        SUB       WKRMDR        WKDIFF
     C                   EXSR      $SETOF
     C                   EXSR      $CLRLN
     C                   DO        WKDIFF
     C                   EXSR      $BLANK
     C                   ENDDO
     C                   ENDIF
      *
     C                   ENDSL
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * $HDR - SETUP THE SALES HEADER DATA FOR THE CONTROL RECORD
      *---------------------------------------------------------------
      *
     C     $HDR          BEGSR
      *
     C                   MOVEL     SHTPCD        HDTPCD
     C                   MOVEL     SHSTCD        C1STCD
     C                   MOVEL     SHLTCD        C1LTCD
     C                   Z-ADD     SHFSCD        C1FSCD
     C                   Z-ADD     SHCVNO        C1CVNO
     C                   MOVEL     SHTICD        C1TICD
      *
      * RETRIEVE FARM SITE NAME
      *
     C     C1FSCD        CHAIN     HSP018                             92
     C     *IN92         IFEQ      *OFF
     C                   MOVEL     FSFSNM        C1FSNM
     C                   ELSE
     C                   MOVEL(P)  'UNKNOWN'     C1FSNM
     C                   ENDIF
      *
      * RETRIEVE TRUCKER NAME
      *
     C     C1TICD        IFNE      *BLANK
     C     C1TICD        CHAIN     HSP046                             92
     C     *IN92         IFEQ      *OFF
     C                   MOVEL     TITINM        C1TINM
     C                   ELSE
     C                   MOVEL(P)  'UNKNOWN'     C1TINM
     C                   ENDIF
     C                   ENDIF
      *
      * RETRIEVE JDE CUSTOMER NAME
      *
     C     SHCVNO        IFNE      0
     C                   Z-ADD     SHCVNO        INAJD
     C                   MOVEL(P)  'C  '         INAT1
     C                   EXSR      $JDEV
     C                   MOVEL     OUALPH        C1CNAM
     C                   ENDIF
      *
      * FLIP SCHEDULED SHIP DATE TO MDY FORMAT
      *
     C                   Z-ADD     SHSCDT        WKCYMD
     C                   EXSR      $TOMDY
     C                   Z-ADD     WKMDY         C1MDY
      *
      * Flip scheduled kill date to mmddyy format
      *
     C                   z-add     shsckldt      wkcymd
     C                   exsr      $tomdy
     C                   z-add     wkmdy         c1scklmdy
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * $LOAD1- SUBROUTINE TO LOAD THE SUBFILE ON SCREEN 1
      *---------------------------------------------------------------
      *
     C     $LOAD1        BEGSR
      *
      *  RETRIEVE SALE DETAIL RECORDS FOR THE SALES MOVEMENT.
      *
     C     C1MVSN        READE(N)  HSP085                                 82
     C     *IN82         IFEQ      *OFF                                         IF NOT EOF
     C                   SETON                                        85
      *
      * CLEAR FIELDS IN SUBFILE LINE
      *
     C                   EXSR      $CLRLN
      *
      * ACCUMULATE HEAD VALUES FOR CONTROL RECORD
      *
     C                   ADD       SGSCHD        C1SCHD
      *
      *  SET UP HIDDEN SUBFILE LINE FIELDS
      *
     C                   Z-ADD     SGHGSN        HDHGSN
      *
      *  SET UP SUBFILE LINE FIELDS
      *
     C                   Z-ADD     SGSCHD        DFSCHD
      *
      * RETRIEVE ALPHA HOG GROUP CODE, GROUP STATUS, BUILDING
      *
     C                   EXSR      $GRPCD
      *
      * RESET INDICATORS
     C                   EXSR      $IND
      *
     C                   ADD       1             COUNT
     C                   ADD       1             HDRRN1
      *
     C                   WRITE     HP145S1
     C                   SETON                                        81
     C                   Z-ADD     HDRRN1        HDPAG1
     C                   ENDIF                                                  IF NOT EOF
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $GRPCD - RETRIEVE THE USER-ASSIGNED ALPHA HOG GROUP CODE
      *----------------------------------------------------------------
      *
     C     $GRPCD        BEGSR
      *
     C     SGHGSN        CHAIN     HSL034D                            92
     C     *IN92         IFEQ      *ON
     C                   MOVEL     'UNKNOWN'     DFHGCD
     C                   ELSE
     C                   MOVEL     HGHGCD        DFHGCD
     C                   MOVEL     HGBLCD        DFBLCD
     C                   MOVEL     HGRMCD        DFRMCD
     C                   MOVEL     HGGSCD        DFGSCD
     C                   MOVEL     HGPTCD        DFPTCD
     C                   MOVEL     HGPPCD        DFPPCD
     C                   ENDIF
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * $BLANK- SUBROUTINE TO LOAD BLANK SUBFILE RECORDS FOR SCREEN 1
      *---------------------------------------------------------------
      *
     C     $BLANK        BEGSR
      *
     C                   EXSR      $IND
      *
     C                   ADD       1             COUNT
     C                   ADD       1             HDRRN1
      *
     C                   WRITE     HP145S1
     C                   SETON                                        81
     C                   Z-ADD     HDRRN1        HDPAG1
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * $CLRLN - CLEAR SUBFILE LINE
      *---------------------------------------------------------------
      *
      * THIS SUBROUTINE INITIALIZES THE FIELDS IN THE SUBFILE RECORD
      *
     C     $CLRLN        BEGSR
      *
     C                   Z-ADD     0             HDHGSN
      *
     C                   MOVE      *BLANK        DFBLCD
     C                   MOVE      *BLANK        DFRMCD
     C                   MOVE      *BLANK        DFHGCD
     C                   MOVE      *BLANK        DFGSCD
     C                   MOVE      *BLANK        DFPTCD
     C                   MOVE      *BLANK        DFPPCD
     C                   Z-ADD     0             DFSCHD
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * $OFFIN - REWRITE THE SUBFILE RECORDS WITH THE INDICATORS OFF
      *---------------------------------------------------------------
      *
      * THIS SUBROUTINE KEEPS THE CURSOR FROM BEING IN THE SUBFILE
      * WHILE THE DISPLAYED ERROR MESSAGE IS FOR THE CONTROL RECORD.
      *
     C     $OFFIN        BEGSR
      *
      * READ/PROCESS ALL THE CHANGED SUBFILE RECORDS
      *
     C                   SETOFF                                       80
     C     *IN80         DOUEQ     *ON                                          DO PROCESS
     C                   READC     HP145S1                                80
     C     *IN80         IFEQ      *OFF                                         IF REC READ
      *
      * SETON MDT
     C                   SETON                                        85
      *
      * SET OFF THE ERROR INDICATORS IN THE SUBFILE
      * RESET INDICATORS
      *
     C                   EXSR      $SETOF
     C                   EXSR      $IND
     C                   UPDATE    HP145S1
     C                   ENDIF                                                  IF REC READ
     C                   ENDDO                                                  DO PROCESS
      * SET MDT
     C                   SETOFF                                       85
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * $EDS1 - SUBROUTINE TO READ AND EDIT CHANGED SUBFILE RECORDS
      *          FOR SCREEN 1
      *---------------------------------------------------------------
      *
     C     $EDS1         BEGSR
      *
      * INITIALIZE THE GROUP ARRAY AND INDEXES
      *
     C                   MOVE      *BLANK        GRP
     C                   Z-ADD     0             Y
     C                   Z-ADD     0             X
      *
      * CLEAR THE TOTAL ACCUMULATORS AT THE TOP OF THE SCREEN
      *
     C                   Z-ADD     0             WKSCHD
     C                   MOVEL     NO            WKEDIT
      *
      * READ/PROCESS ALL THE CHANGED SUBFILE RECORDS
      *
     C                   SETOFF                                       80
     C     *IN80         DOUEQ     *ON                                          DO PROCESS
     C                   READC     HP145S1                                80
     C     *IN80         IFEQ      *OFF                                         IF REC READ
      *
      * SETON MDT
     C                   SETON                                        85
      *
      * SET OFF THE ERROR INDICATORS IN THE SUBFILE
      *
     C                   EXSR      $SETOF
      *
      * IF THERE ARE NO USER ENTRIES FOR THE CHANGED SUBFILE RECORD,
      * CLEAR THE OUTPUT-ONLY SUBFILE FIELDS
      *
     C     DFBLCD        IFEQ      *BLANK                                       IF ENTRY
     C     DFRMCD        ANDEQ     *BLANK
     C     DFHGCD        ANDEQ     *BLANK
     C     DFSCHD        ANDEQ     0
     C                   MOVE      *BLANK        DFGSCD
     C                   MOVE      *BLANK        DFPTCD
     C                   MOVE      *BLANK        DFPPCD
     C                   ELSE
      *
      * ACCUMULATE SCHEDULED HEAD INTO A WORKFIELD FOR
      * COMPARISON WITH THE TOTAL VALUES KEYED BY THE USER
      *
     C                   ADD       DFSCHD        WKSCHD
     C                   MOVEL     YES           WKEDIT
      *
      * If group is blank, clear output only group status.
      *
     C                   if        dfhgcd = *blank
     C                   move      *blank        dfgscd
     C                   move      *blank        dfptcd
     C                   move      *blank        dfppcd
     C                   endif
      *
      * The user must at least enter a building or a group. If neither are
      * entered, issue message; otherwise, validate and attempt to retrieve/
      * default in the missing values.
      *
     C                   if        dfblcd = *blank and                          If no bldg/group
     C                             dfhgcd = *blank
     C                   seton                                        3544
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS09132'     mgid(e)
     C                   exsr      $err1
     C                   endif
     C                   else
      *
     C                   exsr      $blcd
      *
      * Edit room if a) it is not blank and b) it was not edited previously
      * in the logic that attempted to default a group for a building.
      * (The check on indicator 36 keeps you from issuing a second invalid
      * room message.)
     C                   if        dfrmcd <> *blank and *in36 = *off
     C                   exsr      $rmcd
     C                   endif
      *
     C                   exsr      $hgcd
     C                   endif                                                  If no bldg/group
      *
      *
      * VALIDATE SCHEDULED HEAD
      *
     C                   SELECT
     C     DFSCHD        WHENEQ    0
     C                   SETON                                        74
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS09001'     MGID(E)
     C                   EXSR      $ERR1
     C                   ENDIF
      *
     C     DFSCHD        WHENLT    0
     C                   SETON                                        74
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS09000'     MGID(E)
     C                   EXSR      $ERR1
     C                   ENDIF
     C                   ENDSL
      *
     C                   ENDIF                                                  IF ENTRY
      *
      * RESET INDICATORS
     C                   EXSR      $IND
     C                   UPDATE    HP145S1
     C                   EXSR      $SETOF
     C                   ENDIF                                                  IF REC READ
     C                   ENDDO                                                  DO PROCESS
      * SET MDT
     C                   SETOFF                                       85
      *
      *---------------------------------------------------------------
      * AFTER ALL SUBFILE RECORDS HAVE BEEN EDITED,
      * ISSUE AN ERROR MESSAGE IF THERE ARE NO SUBFILE ENTRIES
      *---------------------------------------------------------------
      *
     C     WKEDIT        IFEQ      NO                                           IF NO ENTRY
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS00593'     MGID(E)
     C                   ENDIF
     C                   ENDIF                                                  IF NO ENTRY
      *
      * IF THERE ARE NO ERRORS ON THE SUBFILE, CHECK THAT THE
      * ACCUMULATED HEAD VALUES MATCH THE USER-KEYED VALUES.
      *
     C     ERROR         IFEQ      0                                            IF NO ERROR
      *
     C     C1SCHD        IFNE      WKSCHD                                       IF NE SCHED
     C                   SETON                                        79
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS00624'     MGID(E)
     C                   CLEAR                   MGW
     C                   MOVE      C1SCHD        XXHD
     C                   MOVEA     XXHD          MGW(1)
     C                   MOVE      WKSCHD        XXHD6
     C                   MOVEA     XXHD6         MGW(6)
     C                   MOVEA     MGW           MGDT(E)
     C                   ENDIF
     C                   ENDIF                                                  IF NE SCHED
      *
     C                   ENDIF                                                  IF NO ERROR
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $BLCD - EDIT BUILDING
      *----------------------------------------------------------------
      *
     C     $BLCD         BEGSR
      *
      * If building is blank and hog group is entered, default building and
      * room from hog group.
      *
     C                   if        dfblcd = *blank and dfhgcd <> *blank
     C     dfhgcd        chain     hsp034                             92
     C                   if        *in92 = *off
     C                   move      hgblcd        dfblcd
     C                   move      hgrmcd        dfrmcd
     C                   move      hggscd        dfgscd
     C                   move      hgptcd        dfptcd
     C                   move      hgppcd        dfppcd
     C                   endif
     C                   endif
      *
      * Validations:
      *  1) building is required
      *  2) building must exist on the specified farm site
      *
     C                   SELECT
     C     DFBLCD        WHENEQ    *BLANK
     C                   SETON                                        35
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS09001'     MGID(E)
     C                   EXSR      $ERR1
     C                   ENDIF
     C                   OTHER
      *
     C     key01         CHAIN     HSP019                             92
     C     *IN92         IFEQ      *ON                                          IF BAD BLDG
     C                   SETON                                        35
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS00116'     MGID(E)
     C                   CLEAR                   MGW
     C                   MOVEA     DFBLCD        MGW(1)
     C                   MOVE      C1FSCD        xxalphfscd
     C                   MOVEA     xxalphfscd    MGW(6)
     C                   MOVEA     MGW           MGDT(E)
     C                   EXSR      $ERR1
     C                   ENDIF
     C                   ENDIF                                                  IF BAD BLDG
     C                   ENDSL
      *
      *
      * Attempt to default in a group when:
      *   1) building is valid and group is blank and
      *   2) room is blank or valid
      *
     C                   if        *in35 = *off and dfhgcd = *blank             If valid building
      *
     C                   if        dfrmcd <> *blank
     C                   exsr      $rmcd
     C                   endif
      *
     C                   if        *in36 = *off                                 If room OK/blank
     C                   exsr      $group
     C                   endif                                                  If room OK/blank
     C                   endif                                                  If valid building
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $rmcd - validate room
      *----------------------------------------------------------------
      *
      * Validations:
      *  1) required
      *  2) must exist in the building on the farm site
      *  3) must have an active status
      *
     C     $rmcd         begsr
      *
     C                   select
     C                   when      dfrmcd = *blank
     C                   seton                                        36
     C                   if        error < maxmsg
     C                   add       1             error
     C                   exsr      $err1
     C                   eval      mgid(e) = 'HS09001'
     C                   endif
     C                   other
      *
      * Check that the room exists in the building on the farm site
     *
     C     key02         chain     hsp020                             92
     C                   if        *in92 = *on                                  If no hit
     C                   seton                                        36
     C                   if        error < maxmsg
     C                   add       1             error
     C                   exsr      $err1
     C                   eval      mgid(e) = 'HS00117'
     C                   clear                   mgw
     C                   movea     dfrmcd        mgw(1)
     C                   movea     dfblcd        mgw(6)
     C                   move      c1fscd        xxalphfscd
     C                   movea     xxalphfscd    mgw(11)
     C                   movea     mgw           mgdt(e)
     C                   endif
     C                   else
     *
      * Room must have an 'active' status
     *
     C                   if        brrmst <> 'A'                                If not active
     C                   seton                                        36
     C                   if        error < maxmsg
     C                   add       1             error
     C                   exsr      $err1
     C                   eval      mgid(e) = 'HS00726'
     C                   clear                   mgw
     C                   movea     dfrmcd        mgw(1)
     C                   movea     mgw           mgdt(e)
     C                   endif
     C                   endif                                                  If not active
     C                   endif                                                  If no hit
      *
     C                   endsl
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Attempt to find a default group in a building
      *---------------------------------------------------------------
      *
      * If only one group is open in that building or building/room, default
      * it in. But, if multiple groups are open, issue a message to the
      * user to select a group.
      *
     C     $group        begsr
      *
      * If you find an open group, repeat the 'find' to see if there is a
      * 'second' open group.
      *
     C                   exsr      $findone
     C                   if        *in92 = *off                                 If open group
     C                   exsr      $findtwo
      *
     C                   if        *in92 = *off                                 If mult group
     C                   seton                                        44
     C                   if        error < maxmsg
     C                   add       1             error
     C                   clear                   mgw
     C                   movea     dfblcd        mgw(1)
      *
     C                   select
     C                   when      dfrmcd = *blank
     C                   movel     'HS00721'     mgid(e)
     C                   other
     C                   movel     'HS00765'     mgid(e)
     C                   movea     dfrmcd        mgw(6)
     C                   endsl
      *
     C                   movea     mgw           mgdt(e)
     C                   exsr      $err1
     C                   endif
     C                   else
      *
      * Only one group is open; default in its values---unless this is a
      * Market Sale/Nursery Group combination--which we no longer allow.
      *
     C                   if        c1stcd = 'MRKTS' and hgppcd = 'NUR  '        If MRKTS/NUR
     C                   else
     C                   movel     hghgcd        dfhgcd
     C                   movel     hgrmcd        dfrmcd
     C                   movel     hggscd        dfgscd
     C                   move      hgptcd        dfptcd
     C                   move      hgppcd        dfppcd
     C                   endif                                                  If MRKTS/NUR
      *
     C                   endif                                                  If mult group
     C                   else
      *
      * No groups are open in this building/room
      *
     C                   seton                                        44
     C                   if        error < maxmsg
     C                   add       1             error
     C                   clear                   mgw
     C                   move      c1fscd        xxalphfscd
     C                   movea     xxalphfscd    mgw(1)
     C                   movea     dfblcd        mgw(6)
      *
     C                   select
     C                   when      dfrmcd = *blank
     C                   movel     'HS00722'     mgid(e)
     C                   other
     C                   movel     'HS00766'     mgid(e)
     C                   movea     dfrmcd        mgw(11)
     C                   endsl
      *
     C                   movea     mgw           mgdt(e)
     C                   exsr      $err1
     C                   endif
     C                   endif                                                  If open group
      *
     C                   endsr
      /EJECT
      *----------------------------------------------------------------
      * Find the first open group
      *----------------------------------------------------------------
      *
      * Chain with farm/building or farm/building/room using:
      *   KEY 1: farm/building when room is blank
      *   KEY 2: farm/building/room when room is populated.
      *
      * The file you chain to depends on the Type of Sale value:
      *   MRKTS:        chain to a file that has only BGF and GF groups---no Nursery groups
      *   FPS or CULLS: chain to a file that has all groups--BGF, NUR, GF
      *
     C     $findone      begsr
      *
      * Room is blank
      *
     C                   select
     C                   when      dfrmcd = *blank
      *
     C                   if        c1stcd = 'MRKTS'
     C     key01         chain     hsl034r                            92
     C                   else
     C     key01         chain     hsl034p                            92
     C                   endif
     C                   other
      *
      * Room is populated
      *
     C                   if        c1stcd = 'MRKTS'
     C     key02         chain     hsl034r                            92
     C                   else
     C     key02         chain     hsl034p                            92
     C                   endif
     C                   endsl
      *
     C                   endsr
      /EJECT
      *----------------------------------------------------------------
      * Find the second open group
      *----------------------------------------------------------------
      *
      * Chain with farm/building or farm/building/room using:
      *   KEY 1: farm/building when room is blank
      *   KEY 2: farm/building/room when room is populated.
      *
      * The file you chain to depends on the Type of Sale value:
      *   MRKTS:        chain to a file that has only BGF and GF groups---no Nursery groups
      *   FPS or CULLS: chain to a file that has all groups--BGF, NUR, GF
      *
     C     $findtwo      begsr
      *
      * Room is blank
      *
     C                   select
     C                   when      dfrmcd = *blank
      *
     C                   if        c1stcd = 'MRKTS'
     C     key01         reade     hsl034r                                92
     C                   else
     C     key01         reade     hsl034p                                92
     C                   endif
     C                   other
      *
      * Room is populated
      *
     C                   if        c1stcd = 'MRKTS'
     C     key02         reade     hsl034r                                92
     C                   else
     C     key02         reade     hsl034p                                92
     C                   endif
     C                   endsl
      *
     C                   endsr
      /EJECT
      *----------------------------------------------------------------
      * EDIT HOG GROUP
      *----------------------------------------------------------------
      *
     C     $HGCD         BEGSR
      *
     C                   MOVE      *BLANK        DFGSCD
     C                   MOVE      *BLANK        DFPTCD
     C                   MOVE      *BLANK        DFPPCD
      *
     C                   SELECT
     C     DFHGCD        WHENEQ    *BLANK
     C                   SETON                                        44
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS09001'     MGID(E)
     C                   EXSR      $ERR1
     C                   ENDIF
     C                   OTHER
      *
     C     DFHGCD        CHAIN     HSP034                             92
     C     *IN92         IFEQ      *ON                                          IF BAD GROUP
     C                   SETON                                        44
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS00261'     MGID(E)
     C                   CLEAR                   MGW
     C                   MOVEA     DFHGCD        MGW(1)
     C                   MOVEA     MGW           MGDT(E)
     C                   EXSR      $ERR1
     C                   ENDIF
     C                   ELSE
      *
     C                   MOVEL     HGGSCD        DFGSCD
     C                   MOVEL     HGPTCD        DFPTCD
     C                   MOVEL     HGPPCD        DFPPCD
      *
      * Nursery groups are not allowed on Market Sales.
      *
     C                   if        dfppcd = 'NUR  ' and c1stcd = 'MRKTS'
     C                   seton                                        44
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS01211'     mgid(e)
     C                   clear                   mgw
     C                   movea     dfhgcd        mgw(1)
     C                   movea     mgw           mgdt(e)
     C                   exsr      $err1
     C                   endif
     C                   endif
      *
      * GROUP MUST ALWAYS HAVE A STATUS OF OPENED WITH AN OPEN DATE ON OR BEFORE
      * ALL DATES THAT ARE NOT ZERO.
      *
     C     HGGSCD        IFNE      'OP'                                         IF STATUS
     C                   SETON                                        44
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS00608'     MGID(E)
     C                   CLEAR                   MGW
     C                   MOVEA     DFHGCD        MGW(1)
     C                   MOVEA     HGGSCD        MGW(8)
     C                   MOVEA     MGW           MGDT(E)
     C                   EXSR      $ERR1
     C                   ENDIF
     C                   ELSE
      *
     C                   IF        C1MDY <> 0 AND                               If sched
     C                             HGOPDT > WKSCDT
     C                   SETON                                        44
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS00488'     MGID(E)
     C                   CLEAR                   MGW
      *
     C                   MOVEA     DFHGCD        MGW(1)
      *
     C                   Z-ADD     HGOPDT        WKCYMD
     C                   EXSR      $TOMDY
     C                   MOVE      WKMDY         XXDT06
     C                   MOVEA     XXDT06        MGW(8)
      *
     C                   MOVE      C1MDY         XXDT06
     C                   MOVEA     XXDT06        MGW(14)
     C                   MOVEA     MGW           MGDT(E)
     C                   EXSR      $ERR1
     C                   ENDIF
     C                   ENDIF                                                  If sched
      *
     C                   IF        C1SCKLMDY <> 0 and                           If kill date
     C                             HGOPDT > WKSCKLDT
     C                   SETON                                        44
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS00455'     MGID(E)
     C                   CLEAR                   MGW
      *
     C                   MOVEA     DFHGCD        MGW(1)
      *
     C                   Z-ADD     HGOPDT        WKCYMD
     C                   EXSR      $TOMDY
     C                   MOVE      WKMDY         XXDT06
     C                   MOVEA     XXDT06        MGW(8)
      *
     C                   MOVE      C1SCKLMDY     XXDT06
     C                   MOVEA     XXDT06        MGW(14)
     C                   MOVEA     MGW           MGDT(E)
     C                   EXSR      $ERR1
     C                   ENDIF
     C                   ENDIF                                                  If kill date
      *
     C                   ENDIF                                                  IF STATUS
      *
      * GROUP MUST BE ON THE FARM
      *
     C     HGFSCD        IFNE      C1FSCD                                       IF DIFF FARM
     C                   SETON                                        44
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS00468'     MGID(E)
     C                   CLEAR                   MGW
     C                   MOVEA     DFHGCD        MGW(1)
     C                   MOVE      HGFSCD        xxalphfscd
     C                   MOVEA     xxalphfscd    MGW(8)
     C                   MOVE      C1FSCD        xxalphfscd
     C                   MOVEA     xxalphfscd    MGW(13)
     C                   MOVEA     MGW           MGDT(E)
     C                   EXSR      $ERR1
     C                   ENDIF
     C                   ENDIF                                                  IF DIFF FARM
      *
      *
      * Group building must match subfile building---if there is a building
      * on the subfile.
     C                   select
     C                   when      dfblcd = *blank
     C                   movel     hgblcd        dfblcd
     C                   other
     C                   if        dfblcd <> hgblcd                             If mismatch building
     C                   seton                                        44
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS00481'     mgid(e)
     C                   clear                   mgw
     C                   movea     dfhgcd        mgw(1)
     C                   movea     hgblcd        mgw(8)
     C                   movea     dfblcd        mgw(13)
     C                   movea     mgw           mgdt(e)
     C                   exsr      $err1
     C                   endif
     C                   endif                                                  If mismatch building
     C                   endsl
      *
      *
      * Group room must match subfile room---if there is a room
      * on the subfile.
     C                   select
     C                   when      dfrmcd = *blank
     C                   movel     hgrmcd        dfrmcd
     C                   other
     C                   if        dfrmcd <> hgrmcd                             If mismatch room
     C                   seton                                        44
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS00482'     mgid(e)
     C                   clear                   mgw
     C                   movea     dfhgcd        mgw(1)
     C                   movea     hgrmcd        mgw(8)
     C                   movea     dfrmcd        mgw(13)
     C                   movea     mgw           mgdt(e)
     C                   exsr      $err1
     C                   endif
     C                   endif                                                  If mismatch room
     C                   endsl
      *
      * SEE IF GROUP HAS ALREADY BEEN ENTERED ON MOVEMENT.
      * GROUPS CAN ONLY BE ON SALES MOVEMENT ONE TIME.
      *
     C                   Z-ADD     1             X
     C     DFHGCD        LOOKUP    GRP(X)                                 98
     C     *IN98         IFEQ      *ON
     C                   SETON                                        44
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS00640'     MGID(E)
     C                   CLEAR                   MGW
     C                   MOVEA     DFHGCD        MGW(1)
     C                   MOVEA     MGW           MGDT(E)
     C                   EXSR      $ERR1
     C                   ENDIF
     C                   ENDIF
      *
     C                   ENDIF                                                  IF BAD GROUP
     C                   ENDSL
      *
      *----------------------------------------------------------------
      * NO ERRORS WERE FOUND ON THE GROUP,
      *  1) PUT NUMERIC GROUP NUMBER IN HIDDEN SUBFILE FIELD
      *  2) MOVE GROUP GENETIC CODE TO HIDDEN SUBFILE FIELD
      *  3) MOVE GROUP SEX CODE TO HIDDEN SUBFILE FIELD
      *  4) MOVE ALPHA GROUP CODE TO ARRAY
      *
     C     *IN44         IFEQ      *OFF                                         IF ALL OK
     C                   Z-ADD     HGHGSN        HDHGSN
     C                   ADD       1             Y
     C                   MOVEL     DFHGCD        GRP(Y)
     C                   ENDIF                                                  IF ALL OK
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $IND - RESET INDICATORS
      *----------------------------------------------------------------
      *
     C     $IND          BEGSR
      *
      * THE DSPATR LOGIC USED IN THE DSPF REQUIRES AT LEAST ONE
      * INDICATOR TO BE ON.
     C                   SETON                                        42
      *
      * IF THE SALE WAS DELETED OUT FROM UNDER THE USER, THEN SET
      * *IN90 BACK ON TO CONTROL AVAILABLE FUNCTION KEYS AND INPUT
      * FIELDS.
      *
     C     NOTFLG        IFEQ      YES                                          IF NOT FOUND
     C                   SETON                                        90
     C                   ELSE
      *
      *  SET UP FOR USER'S ACTION
      *
     C                   SELECT
     C     DFMODE        WHENEQ    REVISE                                       WH REVISE
     C                   SETON                                        60
     C     DFMODE        WHENEQ    CREATE                                       WH CREATE
     C                   SETON                                        61
     C                   ENDSL
      *
     C                   ENDIF                                                  IF NOT FOUND
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $SETOF - SET OFF THE INDICATORS ON THE SUBFILE
      *----------------------------------------------------------------
      *
     C     $SETOF        BEGSR
      *
     C                   SETOFF                                       353644
     C                   SETOFF                                       545556
     C                   SETOFF                                       74
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $DATE -  MANIPULATE DATES WITH DATE UTILITY
      *----------------------------------------------------------------
      *
     C     $DATE         BEGSR
      *
     C                   Z-ADD     0             PTO8
     C                   MOVEL     'MDY '        PFRFMT
     C                   MOVEL     'CYMD'        PTOFMT
     C                   Z-ADD     0             PDAYS
     C                   MOVE      *BLANK        PCODE
     C                   MOVE      *BLANK        PRTRN
      *
     C                   CALL      'UT80060R'
     C                   PARM                    PFRM8             8 0
     C                   PARM                    PTO8              8 0
     C                   PARM                    PFRFMT            4
     C                   PARM                    PTOFMT            4
     C                   PARM                    PDAYS             4 0
     C                   PARM                    PCODE             1
     C                   PARM                    PRTRN             1
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $JDEV - SUBROUTINE TO CALL THE EXTERNAL PROGRAM TO VALIDATE JDE
      *         ADDRESS NUMBERS.
      *----------------------------------------------------------------
      *
     C     $JDEV         BEGSR
      *
     C                   MOVEL     NO            JDEFL                          JDE ERRORS
      *
     C                   CALL      'HPJDEV'
     C                   PARM                    MGI                            MSG ID'S
     C                   PARM                    MGD                            MSG DATA FLD
     C                   PARM                    ERROR                          # ERR MSGS
     C                   PARM                    INAJD                          JDE ADDR #
     C                   PARM                    INAT1                          JDE ADDR TYP
     C                   PARM                    JDEFL                          ERROR FLAG
     C                   PARM      *BLANK        OUALPH                         JDE NAME
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $DLT11 - DELETE A MOVEMENT WHEN THE USER TAKES F11=DELETE
      *----------------------------------------------------------------
      *
     C     $DLT11        BEGSR
      *
      * FIRST, CHECK USER'S AUTHORITY TO DELETE MOVEMENTS
      *
     C                   CALL      'HPSECD'
     C                   PARM                    MGID
     C                   PARM                    MGDT
     C                   PARM                    ERROR
     C                   PARM                    SDUSR
     C                   PARM                    SHMSCD
      *
     C     ERROR         IFNE      0                                            IF CAN'T DLT
     C                   MOVEL     SCRN1         ROUTNE
     C                   ELSE
      *
      * THE USER IS AUTHORIZED TO DELETE MOVEMENT, SO
      * CALL THE 'CONFIRM DELETE' PROGRAM WHERE THE USER
      * IS REQUIRED TO ENTER A DELETION COMMENT.
      *
     C                   CALL      'HP113'
     C                   PARM                    C1MVSN
     C                   PARM      *BLANK        XXDMCM
     C                   PARM      *BLANK        RTNCD
      *
      * IF THE RETURN CODE IS ZERO, THE THE USER IS DELETING.
      * WHEN DELETING THE HEADER, WRITE THE AUDIT INFO TO
      * THE 'DELETED MOVEMENT' FILE FIRST.
      *
     C     RTNCD         IFEQ      '00'                                         IF DELETE
     C                   EXSR      $DLTSH
     C                   EXSR      $DLTSD
     C                   EXSR      $DLTME
     C                   MOVEL     EXIT          ROUTNE
     C                   ELSE
     C                   MOVEL     SCRN1         ROUTNE
     C                   ENDIF                                                  IF DELETE
      *
     C                   ENDIF                                                  IF CAN'T DLT
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $DLTSH - DELETE THE SALES MOVEMENT HEADER RECORD
      *----------------------------------------------------------------
      *
     C     $DLTSH        BEGSR
      *
     C     C1MVSN        CHAIN     HSP084                             92
     C     *IN92         IFEQ      *OFF                                         IF FOUND
      *
      * IF THE USER IS DELETING THE MOVEMENT (F11 HAS BEEN TAKEN OR
      * THE INCOMING MODE WAS "DELETE",
      * WRITE THE AUDIT INFO TO THE 'DELETED MOVEMENT' FILE PRIOR
      * TO DELETING THE HEADER RECORD.
      *
     C                   IF        *IN11 = *ON OR MODE = DELETE
     C                   EXSR      $CRTDM
     C                   ENDIF
      * DELETE HEADER
     C                   DELETE    SHREC
     C                   ENDIF                                                  IF FOUND
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $DLTSD - DELETE THE SALES MOVEMENT DETAIL RECORDS
      *----------------------------------------------------------------
      *
     C     $DLTSD        BEGSR
      *
     C     C1MVSN        SETLL     HSP085
     C                   SETOFF                                       95
     C     *IN95         DOUEQ     *ON                                          DO DETAIL
     C     C1MVSN        READE     HSP085                                 95
     C     *IN95         IFEQ      *OFF                                         IF FOUND
     C                   DELETE    SGREC
     C                   ENDIF                                                  IF FOUND
     C                   ENDDO                                                  DO DETAIL
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $DLTME - DELETE ANY/ALL MOVEMENT EVENT RECORDS
      *----------------------------------------------------------------
      *
     C     $DLTME        BEGSR
      *
     C                   SETOFF                                       95
     C     C1MVSN        SETLL     HSP058
      *
     C     *IN95         DOUEQ     *ON                                          DO EVENTS
     C     C1MVSN        READE     HSP058                                 95
     C     *IN95         IFEQ      *OFF                                         IF EVENT
     C                   DELETE    MEREC
     C                   ENDIF                                                  IF EVENT
     C                   ENDDO                                                  DO EVENTS
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $CRTDM - CREATE AN AUDIT RECORD FOR THE DELETED MOVEMENT
      *----------------------------------------------------------------
      *
     C     $CRTDM        BEGSR
      *
     C                   Z-ADD     SHMVSN        DMMVSN
     C                   MOVEL     SDUSR         DMUSCD
     C                   Z-ADD     WKUDAT        DMDMDT
     C                   TIME                    HHMMSS
     C                   Z-ADD     HHMM          DMDMTM
     C                   MOVEL     XXDMCM        DMDMCM
      *
     C                   MOVEL     'S'           DMMTCD
     C                   MOVEL     SHMSCD        DMMSCD
     C                   Z-ADD     SHSCDT        DMSCDT
     C                   Z-ADD     SHSHDT        DMSHDT
     C                   Z-ADD     SHRCDT        DMRCDT
     C                   Z-ADD     SHCVNO        DMCVNO
      *
     C                   MOVEL     SHFSBO        DMORBO
     C                   Z-ADD     SHFSCD        DMORFS
     C                   MOVEL     *BLANK        DMORPT
     C                   MOVEL     *BLANK        DMORPP
      *
     C                   MOVE      *BLANK        DMDNBO
     C                   MOVE      *BLANK        DMDNPT
     C                   MOVE      *BLANK        DMDNPP
     C                   Z-ADD     0             DMDNFS
      *
     C                   WRITE     DMREC
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $CRTSH - CREATE A SALES MOVEMENT HEADER RECORD
      *----------------------------------------------------------------
      *
     C     $CRTSH        BEGSR
      *
      * IF YOU ARE CREATING A NEW SALES MOVEMENT, ASSIGN A MOVEMENT
      * NUMBER.
     C     MODE          IFEQ      CREATE
     C     *LOCK         IN        DAMVSN
     C                   ADD       1             DAMVSN
     C                   OUT       DAMVSN
     C                   Z-ADD     DAMVSN        SHMVSN
     C                   ELSE
     C                   Z-ADD     C1MVSN        SHMVSN
     C                   ENDIF
      *
     C                   MOVEL     'SC'          SHMSCD
     C                   MOVEL     C1STCD        SHSTCD
     C                   MOVEL     C1LTCD        SHLTCD
     C                   MOVEL     HDTPCD        SHTPCD
      *
     C                   MOVEL     C1FSCD        SHFSCD
     C                   MOVEL     FSFSBO        SHFSBO
      *
     C                   Z-ADD     C1CVNO        SHCVNO
     C                   MOVEL     C1TICD        SHTICD
     C                   Z-ADD     WKSCDT        SHSCDT
     C                   z-add     wksckldt      shsckldt
      *
     C                   Z-ADD     0             SHSHDT
     C                   Z-ADD     0             SHRCDT
     C                   Z-ADD     0             SHEPDT
     C                   Z-ADD     0             SHPODT
     C                   Z-ADD     0             SHPTCT
      *
     C                   Z-ADD     WKUDAT        SHUPDT
     C                   TIME                    SHUPTM
     C                   MOVEL     SDPGM         SHPGM
      *
     C                   WRITE     SHREC
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $CRTSD - CREATE SALES MOVEMENT DETAIL RECORDS
      *----------------------------------------------------------------
      *
     C     $CRTSD        BEGSR
      *
      * SETUP THE VALUES FOR FIELDS THAT WON'T CHANGE PER LINE
      *
     C     MODE          IFEQ      CREATE
     C                   Z-ADD     DAMVSN        SGMVSN
     C                   ELSE
     C                   Z-ADD     C1MVSN        SGMVSN
     C                   ENDIF
      *
      *---------------------------------------------------------------
      * READ/PROCESS ALL THE CHANGED SUBFILE RECORDS
      *---------------------------------------------------------------
      *
     C                   SETOFF                                       80
     C     *IN80         DOUEQ     *ON                                          DO READ C
     C                   READC     HP145S1                                80
     C     *IN80         IFEQ      *OFF                                         IF REC READ
     C     DFHGCD        ANDNE     *BLANK
      *
     C                   Z-ADD     HDHGSN        SGHGSN
     C                   Z-ADD     DFSCHD        SGSCHD
     C                   Z-ADD     0             SGSHHD
     C                   Z-ADD     0             SGLVHD
     C                   Z-ADD     0             SGLVLB
     C                   Z-ADD     0             SGDOHD
     C                   Z-ADD     0             SGDOLB
      *
     C                   WRITE     SGREC
      *
      * CREATE A MOVEMENT EVENT RECORD FOR THE GROUP
      *
     C                   EXSR      $CRTME
      *
     C                   ENDIF                                                  IF REC READ
     C                   ENDDO                                                  DO READ C
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $CRTME - CREATE MOVEMENT EVENT RECORDS
      *----------------------------------------------------------------
      *
     C     $CRTME        BEGSR
      *
      *
      * SET UP UPDATE TIME STAMP
      *
     C                   Z-ADD     WKUDAT        MEUPDT
     C                   TIME                    MEUPTM
     C                   MOVEL     SDUSR         MEUSCD
     C                   MOVEL     SDPGM         MEPGM
      *
      * SETUP THE MOVEMENT NUMBER AND STATUS
      *
     C     MODE          IFEQ      CREATE
     C                   Z-ADD     DAMVSN        MEMVSN
     C                   ELSE
     C                   Z-ADD     C1MVSN        MEMVSN
     C                   ENDIF
      *
     C                   Z-ADD     0             MEHSN
     C                   Z-ADD     C1CVNO        MECVNO
     C                   Z-ADD     0             MELNNO
      *
      * WRITE AN EVENT RECORD FOR THE GROUP
      *
     C                   Z-ADD     HDHGSN        MEHGSN
     C                   MOVEL     'S'           MEMTCD
     C                   MOVEL     'O'           MEODFL
      *
      * When 1) it is a Market Sale and 2) there is a scheduled kill date,
      * put the Scheduled Kill Date into the Movement Date field. Otherwise, use
      * the Scheduled Ship Date.
      *
     C                   if        c1stcd = 'MRKTS' and wksckldt <> 0
     C                   z-add     wksckldt      memedt
     C                   else
     C                   z-add     wkscdt        memedt
     C                   endif
      *
     C                   WRITE     MEREC
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $F4S1 - F4 PROMPT FOR SCREEN 1
      *----------------------------------------------------------------
      *
     C     $F4S1         BEGSR
      *
     C                   SELECT
      *
      *----------------------------------------------------------------
      * PROMPTED FIELDS IN THE CONTROL RECORD
      *----------------------------------------------------------------
      *
      * SALES TYPE
      *
     C     FLD           WHENEQ    'C1STCD'
     C                   CALL      'HP544'
     C                   PARM      *blank        XXSTCD
     C                   PARM      *blank        XXSTDS
     C                   SETON                                        57
     C     XXSTCD        IFNE      *BLANK                                       IF SELECTED
     C                   MOVEL     XXSTCD        C1STCD
     C                   ENDIF                                                  IF SELECTED
      *
      *
      * LOAD TYPE
      *
     C     FLD           WHENEQ    'C1LTCD'
     C                   CALL      'HP5012'
     C                   PARM      *blank        XXLTCD
     C                   PARM      *blank        XXLTDS
     C                   SETON                                        50
     C                   if        xxltcd <> *blank                             IF SELECTED
     C                   MOVEL     XXLTCD        C1LTCD
     C                   ENDIF                                                  IF SELECTED
      *
      *
      *
      * TRUCKER   (active truckers only)
      *
     C     FLD           WHENEQ    'C1TICD'
     C                   CALL      'HP538'
     C                   PARM      'A'           XXAIST
     C                   PARM      *blank        XXTICD
     C                   PARM      *blank        XXTINM
     C                   PARM      *blank        XXMTECHREF
     C                   SETON                                        53
     C     XXTICD        IFNE      *BLANK                                       IF SELECTED
     C                   MOVEL     XXTICD        C1TICD
     C                   MOVEL     XXTINM        C1TINM
     C                   ENDIF                                                  IF SELECTED
      *
      *  FARM SITE
      *
     C     FLD           WHENEQ    'C1FSCD'
     C                   CALL      'HP5040'
     C                   PARM      'A'           xxaist
     C                   PARM      0             xxfscd
     C                   PARM      *blank        XXFSNM
     C                   SETON                                        51
     C     xxfscd        IFNE      0                                            IF SELECTED
     C                   Z-ADD     xxfscd        C1FSCD
     C                   MOVEL     XXFSNM        C1FSNM
     C                   ENDIF                                                  IF SELECTED
      *
      * CUSTOMER
      *
     C     FLD           WHENEQ    'C1CVNO'
     C                   CALL      'HP513'
     C                   PARM      WKCOCD        XXCOCD                         COMPANY
     C                   PARM      'C  '         INAT1                          SEARCH TYPE
     C                   PARM      *ZERO         OUAJD                          SELECTED NO.
     C                   PARM      *BLANK        OUALPH                         NAME
      *
     C                   SETON                                        52
     C     OUAJD         IFNE      0                                            IF SELECTED
     C                   Z-ADD     OUAJD         C1CVNO
     C                   MOVEL     OUALPH        C1CNAM
     C                   ENDIF                                                  IF SELECTED
      *
      *----------------------------------------------------------------
      * PROMPTED FIELDS IN THE SUBFILE
      *----------------------------------------------------------------
      *
      *------------
      * BUILDING
      *------------
      *
     C     FLD           WHENEQ    'DFBLCD'
     C                   CALL      'HP523'
     C                   PARM      c1fscd        xxfscd
     C                   PARM      *blank        XXBLCD
     C                   PARM      *blank        XXBLDS
     C                   EXSR      $RRN1
     C     *IN97         IFEQ      *OFF
     C     XXBLCD        IFNE      *BLANK                                       IF SELECTED
     C                   MOVEL     XXBLCD        DFBLCD
     C                   ENDIF                                                  IF SELECTED
     C                   SETON                                        8555
     C                   UPDATE    HP145S1
     C                   SETOFF                                       8555
      *
      * RESET THE SUBFILE RELATIVE RECORD NUMBER TO WHAT IT WAS BEFORE
      * THE USER TOOK F4 (IT WILL BE OUT-OF-SYNCH IF THE USER HAS
      * SELECTED A RECORD THUS CAUSING THE SUBFILE RECORD TO BE
      * RETRIEVED AND UPDATED.)  BUT, SET THE PAGE OF THE SUBFILE TO
      * BE DISPLAYED TO THE PAGE ON WHICH THE USER TOOK F4.
      *
     C                   Z-ADD     COUNT         HDRRN1
     C                   Z-ADD     HDRR1         HDPAG1
     C                   ENDIF
      *
      *----------------------
      * Room
      *----------------------
     C                   when      fld = 'DFRMCD'
     C                   exsr      $rrn1
     C                   if        *in97 = *off                                 If hit
     C                   call      'HP524'
     C                   parm                    c1fscd
     C                   parm                    dfblcd
     C                   parm      *blank        xxrmcd
     C                   if        xxrmcd <> *blank
     C                   movel     xxrmcd        dfrmcd
     C                   endif
     C                   seton                                        8556
     C                   update    hp145s1
     C                   setoff                                       8556
      *
      * RESET THE SUBFILE RELATIVE RECORD NUMBER TO WHAT IT WAS BEFORE
      * THE USER TOOK F4 (IT WILL BE OUT-OF-SYNCH IF THE USER HAS
      * SELECTED A RECORD THUS CAUSING THE SUBFILE RECORD TO BE
      * RETRIEVED AND UPDATED.)  BUT, SET THE PAGE OF THE SUBFILE TO
      * BE DISPLAYED TO THE PAGE ON WHICH THE USER TOOK F4.
      *
     C                   z-add     count         hdrrn1
     C                   z-add     hdrr1         hdpag1
     C                   endif                                                  If hit
      *
      *
      *----------------------
      * Hog Group
      *----------------------
      *
     C     FLD           WHENEQ    'DFHGCD'
     C                   EXSR      $RRN1
     C     *IN97         IFEQ      *OFF
      *
     C                   select
     C                   when      c1stcd = 'MRKTS'
     C                   call      'HP5951'
     C                   parm                    c1fscd
     C                   parm                    c1fsnm
     C                   parm      dfblcd        xxblcd
     C                   parm      dfrmcd        xxrmcd
     C                   parm      *blank        xxhgcd
     C                   parm      *blank        xxgscd
     C                   parm      *blank        xxptcd
     C                   parm      *blank        xxppcd
     C                   other
      *
     C                   CALL      'HP5950'
     C                   PARM                    c1fscd
     C                   PARM                    c1fsnm
     C                   PARM      dfblcd        XXBLCD
     C                   PARM      dfrmcd        XXRMCD
     C                   PARM      *blank        XXHGCD
     C                   PARM      *blank        XXGSCD
     C                   PARM      *blank        XXPTCD
     C                   PARM      *blank        XXPPCD
     C                   endsl
      *
     C     XXHGCD        IFNE      *BLANK                                       IF SELECTED
     C                   MOVEL     XXHGCD        DFHGCD
     C                   MOVEL     XXBLCD        DFBLCD
     C                   MOVEL     XXRMCD        DFRMCD
     C                   MOVEL     XXGSCD        DFGSCD
     C                   MOVEL     XXPTCD        DFPTCD
     C                   MOVEL     XXPPCD        DFPPCD
     C                   ENDIF                                                  IF SELECTED
     C                   SETON                                        8554
     C                   UPDATE    HP145S1
     C                   SETOFF                                       8554
      *
      * RESET THE SUBFILE RELATIVE RECORD NUMBER TO WHAT IT WAS BEFORE
      * THE USER TOOK F4 (IT WILL BE OUT-OF-SYNCH IF THE USER HAS
      * SELECTED A RECORD THUS CAUSING THE SUBFILE RECORD TO BE
      * RETRIEVED AND UPDATED.)  BUT, SET THE PAGE OF THE SUBFILE TO
      * BE DISPLAYED TO THE PAGE ON WHICH THE USER TOOK F4.
      *
     C                   Z-ADD     COUNT         HDRRN1
     C                   Z-ADD     HDRR1         HDPAG1
     C                   ENDIF
     C                   OTHER
      *
      * F4 NOT VALID FOR THIS FIELD SO ISSUE MESSAGE
      *
     C     ERROR         IFLT      MAXMSG                                       IF FIRST ERR
     C                   ADD       1             ERROR
     C                   MOVEL     'HS09011'     MGID(E)
     C                   ENDIF                                                  IF FIRST ERR
     C                   ENDSL
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * $ERR1  - SUBROUTINE TO SET THE SUBFILE TO REDISPLAY AT
      *          THE FIRST SCREEN THAT HAS ERRORS
      *---------------------------------------------------------------
      *
     C     $ERR1         BEGSR
      *
     C     ERROR         IFEQ      1
     C                   Z-ADD     HDRRN1        HDPAG1
     C                   ENDIF
      *
     C                   ENDSR
      *
      *---------------------------------------------------------------
      * $EXIT - SUBROUTINE TO SET ON LR AND END JOB
      *---------------------------------------------------------------
      *
     C     $EXIT         BEGSR
     C                   SETON                                        LR
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * $TOMDY - FLIP DATE FROM CCYYMMDD TO MMDDYY
      *---------------------------------------------------------------
      *
     C     $TOMDY        BEGSR
      *
     C                   Z-ADD     WKMON         WKMM
     C                   Z-ADD     WKDAY         WKDD
     C                   Z-ADD     WKYER         WKYY
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * $WRMSG - SUBROUTINE TO WRITE ERROR MESSAGES
      *---------------------------------------------------------------
      *
     C     $WRMSG        BEGSR
      *
     C                   CALL      'UT80045J'                           43
     C                   PARM                    MGID
     C                   PARM                    MGDT
     C                   PARM                    ERROR
     C                   PARM      SDPGM         PGM
     C                   PARM      MSGFIL        MSGFL
      *
      * IF CALL TO UT80045J FAILED, A MESSAGE HARDCODED IN THE DSPF
      * COMMAND KEY RECORD WILL BE SET ON. SO, REDISPLAY SCREEN TO USER
      *
     C     *IN43         IFEQ      *ON
     C                   MOVEL     SET1          ROUTNE
     C                   ENDIF
      *
     C                   ENDSR
      *
      *---------------------------------------------------------------
      * $CLMSG - SUBROUTINE TO CLEAR MESSAGES
      *---------------------------------------------------------------
      *
     C     $CLMSG        BEGSR
      *
     C                   CALL      'UT80045J'                           43
     C                   PARM      *BLANK        MGI
     C                   PARM      *BLANK        MGD
     C                   PARM      *ZERO         ERROR
     C                   PARM      SDPGM         PGM
     C                   PARM      MSGFIL        MSGFL
      *
      * IF CALL TO UT80045J FAILED, A MESSAGE HARDCODED IN THE DSPF
      * COMMAND KEY RECORD WILL BE SET ON. SO, REDISPLAY SCREEN TO USER
      *
     C     *IN43         IFEQ      *ON
     C                   MOVEL     SET1          ROUTNE
     C                   ENDIF
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $RRN1  - ROUTINE TO FIND/RETRIEVE THE SUBFILE RECORD TO UPDATE
      *          WHEN RETURNING FROM A SELECT PROGRAM ON SCREEN 1
      *----------------------------------------------------------------
      *
      * HDRR1 IS THE HIDDEN FIELD ASSOCIATED WITH THE KEYWORD SFLCSRRRN
      * ON THE CONTROL RECORD IN THE DSPF.
      * HDRRN1 IS THE VALUE ASSOCIATED WITH THE KEYWORD 'KSFILE' IN
      * THE F-SPECS FOR THE WORKSTATION.
      *
     C     $RRN1         BEGSR
      *
     C                   SETOFF                                       97
      *
     C     HDRR1         IFNE      0
     C                   Z-ADD     HDRR1         HDRRN1
     C     HDRRN1        CHAIN     HP145S1                            97
     C                   ENDIF
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * *INZSR - INITIALIZATION SUBROUTINE
      *---------------------------------------------------------------
      *
     C     *INZSR        BEGSR
      *
      * PARM LISTS
      *
     C     *ENTRY        PLIST
     C                   PARM                    MODE
     C                   PARM                    WKCOCD            3
     C                   PARM                    C1MVSN
     C                   PARM                    RTNCD
     C                   parm                    version           1
     C                   parm                    c1version
      *
      * KEY LISTS
      *
      *
     C     key01         klist
     C                   kfld                    c1fscd
     C                   kfld                    dfblcd
      *
     C     key02         klist
     C                   kfld                    c1fscd
     C                   kfld                    dfblcd
     C                   kfld                    dfrmcd
      *
      * GLOBAL DEFINES
      *
     C                   MOVE      *BLANK        PGM              10            USED IN MSG
     C                   MOVE      *BLANK        MSGFL            10            USED IN MSG
     C                   MOVE      *BLANK        ROUTNE            6            ROUTINE
     C                   Z-ADD     0             MAXRRN            4 0          REPOSITION
     C                   MOVEL     NO            RESET             1            REPOSITION
     C                   MOVEL     NO            NOTFLG            1
     C                   MOVEL     '00'          RTNCD             2            RETURN CODE
     C     *LIKE         DEFINE    DFMODE        MODE                           SFL POSITION
     C     *LIKE         DEFINE    IDFLOW        WKFLOW                         SFL POSITION
     C                   MOVEL     NO            EDITFL            1
      *
      * ARRAY INDEXES
     C                   Z-ADD     0             X                 3 0
     C                   Z-ADD     0             Y                 3 0
      *
      *
      *
      * ALPHA AND NUMERIC FIELDS FOR USE AS PARMS
      *
     C     *LIKE         DEFINE    DMDMCM        XXDMCM
     C                   MOVE      *BLANK        XXMVSN            7
     C                   MOVE      *BLANK        XXCOCD            3
     C     *LIKE         DEFINE    HGRMCD        XXRMCD
     C     *LIKE         DEFINE    HGBLCD        XXBLCD
     C     *like         define    fbblds        XXBLDS
     C     *LIKE         DEFINE    HGFSBO        XXFSBO
     C     *LIKE         DEFINE    HGHGCD        XXHGCD
     C     *LIKE         DEFINE    HGGSCD        XXGSCD
     C     *LIKE         DEFINE    HGPTCD        XXPTCD
     C     *LIKE         DEFINE    HGPPCD        XXPPCD
     C     *LIKE         DEFINE    FSFSCD        xxfscd
     C                   MOVE      *BLANK        XXALPHFSCD        5
     C                   MOVE      *BLANK        XXALPHCVNO        8
     C     *LIKE         DEFINE    FSFSNM        XXFSNM
     C     *LIKE         DEFINE    TITICD        XXTICD
     C     *LIKE         DEFINE    TITINM        XXTINM
     C                   MOVE      *BLANK        XXAIST            1
     C                   MOVE      *BLANK        XXHD              5
     C                   MOVE      *BLANK        XXHD6             6
     C     *LIKE         DEFINE    C1STCD        XXSTCD
     C                   MOVE      *BLANK        XXSTDS           20
     C     *LIKE         DEFINE    C1LTCD        XXLTCD
     C                   MOVE      *BLANK        XXLTDS           25
     C                   MOVE      *BLANK        XXDT06            6
     C     *LIKE         DEFINE    FSMTECHREF    XXMTECHREF
      *
      * WORK FIELDS
      *
     C     *LIKE         DEFINE    DMDMDT        WKUDAT
     C     *LIKE         DEFINE    SHSCDT        WKSCDT
     C     *LIKE         DEFINE    SHSCKLDT      wksckldt
      *
      * ACCUMULATORS FOR SUBFILE HEAD
      *
     C     *LIKE         DEFINE    SGSCHD        WKSCHD          + 1
      *
      * JDE FIELDS
     C                   MOVE      *BLANK        JDEFL             1            JDE ERR FLAG
     C                   MOVE      *BLANK        INAT1             3            JDE ADDR TYP
     C                   MOVE      *BLANK        OUALPH           30            JDE NAME
     C     *LIKE         DEFINE    SHCVNO        INAJD                          JDE ADDR IN
     C     *LIKE         DEFINE    SHCVNO        OUAJD                          JDR ADDR OUT
      *
     C                   MOVEL     NO            WKEDIT            1            SFL RECORDS
      *
      * TO SUPPORT F5-REFRESH, LOGIC REQUIRES THE NUMBER OF LINES ON
      * THE SUBFILE PAGE AND ALSO THE NUMBER OF LINES LESS 1.  BY
      * SETTING THESE VALUES UP AS CONSTANT WORK FIELDS, THE PROCESSING
      * LOGIC CAN BE STANDARDIZED BETWEEN PROGRAMS.
      *
     C                   Z-ADD     04            WKPLIN            2 0          SFLPAG
     C     WKPLIN        SUB       1             WKRLIN            2 0          SFLPAG - 1
     C                   Z-ADD     0             COUNT             2 0
     C     *LIKE         DEFINE    COUNT         WKDIFF
     C     *LIKE         DEFINE    COUNT         WKRMDR
     C     *LIKE         DEFINE    COUNT         WKFLD
      *
      * THIS PROGRAM DIFFERS FROM THE SHELL PROGRAMS IN THAT F5=REFRESH
      * ALWAYS PLACES THE USER AT THE FIRST PAGE OF THE SUBFILE
      *
     C                   Z-ADD     WKPLIN        MAXRRN
      *
      *
      * FLIP JOB DATE TO CCYYMMDD FOR UPDATING DELETED RECORD INFO
      *
     C                   Z-ADD     UDATE         PFRM8
     C                   EXSR      $DATE
     C     PRTRN         IFEQ      *BLANK                                       IF VALID
     C                   Z-ADD     PTO8          WKUDAT
     C                   ELSE
     C                   Z-ADD     0             WKUDAT
     C                   ENDIF                                                  IF VALID
      *
      * We typically set the initial routine at the beginning of the
      * mainline section.  But, having it there was overriding my exit logic
      * when the incoming mode was DELETE.  So, I moved it to here.
      *
     C                   MOVEL     SET1          ROUTNE
      *
      *
      * THE USER CAN COME INTO THIS PROGRAM SEVERAL DIFFERENT WAYS.
      * CHECK THE ENTRY MODE AND SET THE APPROPRIATE INDICATORS TO
      * CONTROL THE SCREEN DISPLAY.
      *
     C                   SELECT
     C     MODE          WHENEQ    CREATE
     C                   MOVEL     CREATE        DFMODE
     C                   SETON                                        61
      *
     C     MODE          WHENEQ    REVISE
     C                   MOVEL     REVISE        DFMODE
     C                   SETON                                        60
      *
      * When incoming mode is DELETE, all authority checks will have already
      * been done in the calling program.  So, just perform the deletes and
      * get out without displaying any screens. Set the required 'comment'
      * value to CANCELLED.
      *
     C     MODE          WHENEQ    DELETE
     C                   EVAL      XXDMCM = 'CANCELLED'
     C                   EXSR      $DLTSH
     C                   EXSR      $DLTSD
     C                   EXSR      $DLTME
     C                   MOVEL     EXIT          ROUTNE
     C                   ENDSL
      *
      * BRING IN THE DATA AREA THAT CONTAINS THE CURRENT ACCOUNTING
      * PERIOD
      *
     C     *DTAARA       DEFINE                  DAAPER
     C                   IN        *DTAARA
      *
      * DEFINE THE DATA AREA THAT CONTAINS THE MOVEMENT NUMBER
      *
     C     *DTAARA       DEFINE                  DAMVSN
      *
      * THE FOLLOWING STANDARD CODE MUST BE INCLUDED TO MAKE THE
      * STANDARD ERROR MESSAGE HANDLING PROGRAM FUNCTION PROPERLY.
      * THIS CODE SETS AND CLEARS THE PROGRAM MESSAGE QUEUE.
      *
     C                   Z-ADD     20            MAXMSG            2 0
     C                   MOVEL     '*'           MSGPGM                         SET QUEUE
     C                   EXSR      $CLMSG                                       CLEAR MSG
      *
     C                   ENDSR
      /EJECT
