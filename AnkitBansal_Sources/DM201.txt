      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      All
      * PROGRAM:     DM201 - Update the Datamart Calendar File
      * PROGRAMMER:  Alice
      * CREATED:     06/16/04
      *
      * FUNCTION:    This program reads the Datamart Calendar file and
      *              updates the "Week Exception Flag" to "Y" if needed.
      *
      *              When the "Week Exception Flag" = "Y"es this indicates
      *              that the date we in the record is in a week that is combined
      *              with another week in Cognos.
      *
      *              Because the accounting date that we use in Cognos MUST
      *              be in the Calendar MONTH associated with the PERIOD #,
      *              some of the we must use the same date as as date in a
      *              different week.
      *
      *              For instance:
      *              Week 2003/22 (05/25/2003 thru 05/31/2003)
      *              Week 2003/22 (accounting dates =  06/01/2003)
      *
      *              Week 2003/23 (06/01/2003 thru 06/07/2003)
      *              Week 2003/23 (06/01/2003 thru 06/07/2003)
      *
      *              In the above case Week 22 will be combined with
      *              Week 23 in Cognos because everything will go
      *              into 06/01/2003.
      *
      * we are basically comparing the last day of the week record
      * to the first day of the next week to see if they are the
      * same.  If they are we need to set both week's flags to a "Y"es
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 12/06/06  LeAnne Ramsey
      *           We changed the file name/text on the Calendar file since this file
      *           is now used in non-Datamart functions.
      *           We also added 3 fields:
      *             1) Transaction Date 6,0   (to hold the date in MMDDYY format)
      *             2) Accounting Date 6,0    (to hold the date in MMDDYY format)
      *             3) HPS Period
      *
      * 09/10/07  LeAnne Ramsey
      *           Recompile only. Added "sales date" fields.
      /eject
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fcapcal    if   e           k disk
      *     Calendar File (Key:  caco#, Catdtsy)
      *
     Fcalcala   uf   e           k disk    rename(carec:carecla)
      *     Datamart All: Calendar File (Key:  caco#, cahpsyr, cahpswk, ctdtdow)
      *
      /eject
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      *
      ****************************************************************
      * ARRAYS AND TABLES
      ****************************************************************
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
     D wkcono          s                   like(caco#)
     D wkhpsyr         s                   like(cahpsyr)
     D wkhpswk         s                   like(cahpswk)
      *
     D svhpsyr1        s                   like(cahpsyr)
     D svhpswk1        s                   like(cahpswk)
     D svhpsyr2        s                   like(cahpsyr)
     D svhpswk2        s                   like(cahpswk)
     D svacdtsy        s                   like(caacdtsy)
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * MAINLINE
      ****************************************************************
      *
      * Read the DM calendar file and update the flag accordingly
      *
     C     wkcono        setll     capcal                                 90
     C                   dou       *in90 = *on                                  Main do
     C     wkcono        reade     capcal                                 90
     C                   if        *in90 = *off                                 If not EOF
      *
     C                   select
      *
      *  if day of week = 7, then save the accounting date
      *
     C                   when      catdtdow = 7                                 Day of Week = 7
     C                   move      cahpsyr       svhpsyr1                       year 1
     C                   move      cahpswk       svhpswk1                       week 1
     C                   move      caacdtsy      svacdtsy                       accounting date
      *
      *  if day of week = 1, then compare the saved accounting date to the
      *     new one.  If they are the same we need to update both weeks.
      *
     C                   when      catdtdow = 1  and                            Day of Week = 7
     c                             caacdtsy = svacdtsy
     C                   move      cahpsyr       svhpsyr2                       year 1
     C                   move      cahpswk       svhpswk2                       week 1
      *
     C                   exsr      $update
      *
      * if NOT day 7 or 1, then just read another record....
      *
     c                   other
      *
     c                   endsl
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Main do
      *
     C                   seton                                        lr
      /eject
      *----------------------------------------------------------------
      * read the DM calendar file for BOTH weeks and UPDATE
      *----------------------------------------------------------------
      *
     C     $update       begsr
      *
      * setup key for 1st week and update all records
      *
     C                   move      svhpsyr1      wkhpsyr
     C                   move      svhpswk1      wkhpswk
      *
     C     Key01         setll     calcala
     C                   dou       *in92 = *on                                  Main do
     C     Key01         reade     calcala                                92
     C                   if        *in92 = *off
     C                   move      'Y'           cawkexfl                       exception flag
     C                   update    carecla
     c                   endif
     c                   enddo
      *
      * setup key for 2nd week and update all records
      *
     C                   move      svhpsyr2      wkhpsyr
     C                   move      svhpswk2      wkhpswk
      *
     C     Key01         setll     calcala
     C                   dou       *in92 = *on                                  Main do
     C     Key01         reade     calcala                                92
     C                   if        *in92 = *off
     C                   move      'Y'           cawkexfl                       exception flag
     C                   update    carecla
     c                   endif
     c                   enddo
      *
     C                   endsr
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
     C     key01         klist
     C                   kfld                    wkcono                         Company
     C                   kfld                    wkhpsyr                        HPS Year
     C                   kfld                    wkhpswk                        HPS Week
      *
      * default to 350
     C                   z-add     350           wkcono
      *
     C                   endsr
