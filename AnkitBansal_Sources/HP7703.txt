     *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Hog Production
      * PROGRAM:     HP7703
      * TITLE:       View Farm Links for a Budget Template
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     02/02/04
      *
      *
      * FUNCTION: This is a View that displays the records in the Budget Template/Farm Linking
      *           file.
      *
      *           Restrictors:
      *                Budget template
      *
      *           Positioners:
      *               Farm site
      *
      *           Selectors:
      *               Farm name
      *               Active budgets (yes/no)
      *               Inactive budgets (yes/no)
      *               Closed budgets (yes/no)
      *               Cell
      *               Type of farm
      *               Production phase
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 10/16/13  LeAnne Ramsey (E2831)
      *           Recompile only. Added field 'MTech Reference'.
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fhpd7703   cf   e             workstn
     F                                     sfile(hp7703s1:hdrrn1)
     F                                     infds(iofeed)
      *
      *
     Fhsp018    if   e           k disk
      *    Farm site
      *
      *
     Fhsp187    if   e           k disk
     F                                     infds(dbfeed)
      *    Budget template/farm links
      *
      *
     Fhsl188b   if   e           k disk
      *    Farm budget header
      *
      /EJECT
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
     D set1            c                   'SET1  '
     D scrn1           c                   'SCRN1 '
     D exit            c                   'EXIT  '
      *
     D upper           c                   'ABCDEFGHIJKLMNOPQRSTUVWXYZ'
     D lower           c                   'abcdefghijklmnopqrstuvwxyz'
      *
      ****************************************************************
      * ARRAYS AND TABLES
      ****************************************************************
      *
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Standard fields
      *
     D mode            s              6
     D pgm             s             10
     D msgfl           s             10
     D maxmsg          s              2  0  inz(20)
     D routne          s              6
     D maxrrn          s              4  0
     D reset           s              1    inz('N')
     D norecs          s              1    inz('N')
     D count           s              2  0
     D wkflow          s                   like(idflow)
     D rtncd           s              2
     D svrrn1          s                   like(hdrrn1)
      *
      *
      * Workfields required for subfile paging--see initialization
      * subroutine.
      *
     D wkplin          s              2  0
     D wkrlin          s              2  0
      *
      *
      * Control fields
      *
     D length          s              2  0
     D procfl          s              1
      *
     D svfscd          s                   like(c1fscd)
     D svfsnm          s                   like(c1fsnm)
     D svactfl         s                   like(c1actfl)
     D svinafl         s                   like(c1inafl)
     D svclofl         s                   like(c1clofl)
     D svcell          s                   like(c1cell)
     D svppcd          s                   like(c1ppcd)
     D svtfcd          s                   like(c1tfcd)
      *
      *
      * Workfields
      *
     D wkname          s                   like(c1fsnm)
     D wkfsnm          s                   like(c1fsnm)
      *
      *
      * Parms
      *
     D xxynfl          s              1
     D xxaist          s              1
      *
     D xxtfcd          s                   like(fstfcd)
     D xxtfds          s             25
      *
     D xxppcd          s                   like(c1ppcd)
     D xxppds          s             30
      *
     D xxfscd          s                   like(fsfscd)
     D xxfsnm          s                   like(fsfsnm)
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * standard error message handling data structures
      *---------------------------------------------------------------
      *
      * For error message handling, a packed index (3) is required.
      * For program readability, define a corresponding error count
      * field called 'error'
      *
     D                 ds                  inz
     D  error                         2p 0
     D   e                            2p 0 overlay(error)
      *
      * This data structure supplies the name of the message file to
      * the message handling CL program.  The field name MSGFIL must be
      * constant. The value in quotes is the name of the specific
      * message file containing the user defined messages.
      *
     D                 ds                  inz
     D  msgfil                       10    inz('HSMSGF    ')
      *
      *---------------------------------------------------------------
      *  Standard message data structures
      *---------------------------------------------------------------
      *
      * The following 3 data structures are used to speed message
      * handling since it is faster to clear data structures than
      * arrays.  Each is associated with a standard message array.
      *
     D mgi             ds                  inz
     D  mgid                          7    dim(20)
      *
     D mgd             ds                  inz
     D  mgdt                         50    dim(20)
      *
     D mgwk            ds                  inz
     D  mgw                           1    dim(50)
      *
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *---------------------------------------------------------------
      * Standard workstation information data structure
      *---------------------------------------------------------------
      *    externally defined as UTWSFR (record format: UTIDFRR)
     D iofeed        e ds                  extname(utwsfr)
      *
      *
      *---------------------------------------------------------------
      * Standard database file information data structure
      *---------------------------------------------------------------
      *    externally defined as UTDBGR (record format: FDBCKD)
     D dbfeed        e ds                  extname(utdbfr)
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * MAINLINE
      ****************************************************************
      *
     C                   eval      routne = set1
      *
     C     *inlr         doweq     *off                                         Main do loop
     C     routne        caseq     set1          $set1
     C     routne        caseq     scrn1         $scrn1
     C     routne        caseq     exit          $exit
     C                   endcs
     C                   enddo                                                  Main do loop
      /EJECT
      *----------------------------------------------------------------
      * Set environment for screen 1
      *----------------------------------------------------------------
      *
     C     $set1         begsr
      *
      * Clear and fill the subfile
      *
     C                   exsr      $clr1
     C                   exsr      $frsh1
      *
      * Set routine to display screen
      *
     C                   eval      routne = scrn1
      *
     C                   endsr
      /eject
      *----------------------------------------------------------------
      * Perform operator I/O for screen 1
      *----------------------------------------------------------------
      *
     C     $scrn1        begsr
      *
      * Write the error messages from the error arrays to the
      * error message subfile
      *
     C                   exsr      $wrmsg
      *
      * Write screen 1 to crt
      *
     C                   write     hp7703k1
     C                   write     hp7703ec
     C                   exfmt     hp7703c1
      *
      * Save the value of the lowest subfile rrn on the screen for
      * later repositioning of the subfile
      *
     C                   eval      wkflow = idflow
      *
      * Clear the messages from the message data structures and
      * program message queue
      *
     C                   exsr      $clmsg
      *
      * Get user's entry and set next routine
      *
     C                   select
     C                   when      *in03 = *on                                  F3-EXIT
     C                   eval      routne = exit
      *
     C                   when      *in04 = *on                                  F4-prompt
     C                   exsr      $f4s1
      *
     C                   if        error = 0
     C                   exsr      $chksv
     C                   else
     C                   eval      routne = scrn1
     C                   endif
      *
     C                   when      *in05 = *on                                  F5-refresh
     C                   movel     yes           reset
     C                   eval      routne = set1
      *
      *
      * Reposition the subfile if the user has changed any of the positioners
      * or selectors in the control record.
      *
     C                   when      c1fscd <> svfscd or
     C                             c1fsnm <> svfsnm or
     C                             c1actfl <> svactfl or
     C                             c1inafl <> svinafl or
     C                             c1clofl <> svclofl or
     C                             c1cell <> svcell or
     C                             c1tfcd <> svtfcd or
     C                             c1ppcd <> svppcd
      *
     C                   exsr      $chksv
      *
     C                   when      *in25 = *on                                  F25-ROLLUP
     C                   exsr      $load1
     C                   other
      *
      * User has pressed enter
      *
     C                   eval      routne = set1
     C                   move      yes           reset
     C                   endsl
      *
     C                   endsr
      /EJECT
      *----------------------------------------------------------------
      * Save the control record positioners/selectors
      *----------------------------------------------------------------
      *
     C     $chksv        begsr
      *
     C                   movel     no            reset
     C                   eval      routne = set1
      *
      *
      * Prepare to scan on farm site name
      *  1) trim leading/trailing blanks
      *  2) find first non-blank position from right
      *
     C                   eval      c1fsnm = %trim(c1fsnm)
     C     ' '           checkr    c1fsnm        length
     C                   movel     c1fsnm        svfsnm
      *
     C                   z-add     c1fscd        svfscd
     C                   z-add     c1cell        svcell
     C                   move      c1actfl       svactfl
     C                   move      c1inafl       svinafl
     C                   move      c1clofl       svclofl
     C                   move      c1tfcd        svtfcd
     C                   move      c1ppcd        svppcd
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * F4 prompt on screen 1
      *---------------------------------------------------------------
      *
     C     $f4s1         begsr
      *
      * Farm site
     C                   select
     C                   when      fld = 'C1FSCD'
     C                   call      'HP5040'
     C                   parm      'A'           xxaist
     C                   parm      0             xxfscd
     C                   parm      *blank        xxfsnm
     C                   seton                                        53
     C                   if        xxfscd <> 0
     C                   z-add     xxfscd        c1fscd
     C                   endif
      *
      * Inactive budgets yes/no flag
      *
     C                   when      fld = 'C1INAFL'
     C                   call      'HP522'
     C                   parm      *blank        xxynfl
     C                   seton                                        50
     C                   if        xxynfl <> *blank
     C                   movel     xxynfl        c1inafl
     C                   endif
      *
      * Active budgets yes/no flag
      *
     C                   when      fld = 'C1ACTFL'
     C                   call      'HP522'
     C                   parm      *blank        xxynfl
     C                   seton                                        51
     C                   if        xxynfl <> *blank
     C                   movel     xxynfl        c1actfl
     C                   endif
      *
      * Closed budgets yes/no flag
      *
     C                   when      fld = 'C1CLOFL'
     C                   call      'HP522'
     C                   parm      *blank        xxynfl
     C                   seton                                        52
     C                   if        xxynfl <> *blank
     C                   movel     xxynfl        c1clofl
     C                   endif
      *
      * Type of farm
     C                   when      fld = 'C1TFCD'
     C                   call      'HP5018'
     C                   parm      *blank        xxtfcd
     C                   parm      *blank        xxtfds
     C                   seton                                        54
     C                   if        xxtfcd <> *blank
     C                   movel     xxtfcd        c1tfcd
     C                   endif
      *
      *  Production type
     C                   when      fld = 'C1PPCD'
     C                   call      'HP507'
     C                   parm      *blank        xxppcd
     C                   parm      *blank        xxppds
     C                   seton                                        55
     C                   if        xxppcd <> *blank
     C                   movel     xxppcd        c1ppcd
     C                   endif
     C                   other
      *
      * F4 not valid for this field...issue message
      *
     C                   if        error < maxmsg
     C                   add       1             error                                        013
     C                   movel     'HS09011'     mgid(e)
     C                   endif
      *
     C                   endsl
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Clear subfile for screen 1
      *---------------------------------------------------------------
      *
     C     $clr1         begsr
      *
     C                   z-add     0             svrrn1
      *
     C                   clear                   hp7703s1
     C                   seton                                        83        sflclr
     C                   write     hp7703c1
     C                   setoff                                       818283
      *                                                    81=sfldsp
      *                                                    82=sflend
      *                                                    83=sflclr
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Refresh/fill the subfile for screen 1
      *---------------------------------------------------------------
      *
     C     $frsh1        begsr
      *
      * Set position in file
      *
     C     key02         setll     hsp187
      *
      *
      * The field 'reset' is a flag field to indicate whether you are
      * attempting to reposition the user close to where he was in the
      * subfile.
      * The lowest subfile RRN on the screen from the DSPF feedback
      * area (IDFLOW) was retrieved earlier and put in WKFLOW.
      * Add to WKFLOW the number of lines on your subfile page or the
      * number less 1 to determine a subfile RRN to reposition to.
      *
      *
     C                   if        reset = yes
     C                   eval      maxrrn = wkflow + wkrlin
     C                   else
      *
      * Don't try to reposition the user, just start the subfile over
      *
     C                   eval      maxrrn = wkplin
     C                   endif
      *
      * Load the subfile until you have the required number of records.
      * The number will depend on whether uou are repositioning the
      * subfile or not. Stop if you get to the end of the subfile.
      *
     C                   dou       svrrn1 >= maxrrn or *in82 = *on
     C                   exsr      $load1
     C                   enddo
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Load subfile for screen 1
      *---------------------------------------------------------------
      *
     C     $load1        begsr
      *
     C                   z-add     svrrn1        hdrrn1
      *
      * Set the count of records on the subfile page to zero.
      *
     C                   z-add     0             count
      *
      * Fill the subfile until the end of the database file or
      * you have filled all the lines on the subfile page.
      *
     C                   setoff                                       82
     C                   dou       *in82 = *on or count >= wkplin               Do fill
     C     c1btcd        reade     hsp187                                 82
     C                   if        *in82 = *off                                 If read
      *
     C                   move      yes           procfl
      *
      * Retrieve farm info so that you can make user selection matches.
      * Only include records that match the user's selectors
      *
     C     tlfscd        chain     hsp018                             92
     C                   if        *in92 = *off                                 If farm hit
      *
      *
      * Determine if this farm has active/inactive/closed budgets for this
      * template.
      *
     C                   move      no            d1actfl
     C                   move      no            d1inafl
     C                   move      no            d1clofl
     C     key01         setll     hsl188b
      *
     C                   dou       *in91 = *on                                  Do budgets
     C     key01         reade     hsl188b                                91
     C                   if        *in91 = *off                                 If budgets
     C                   select
     C                   when      jhfbscd = 'A'
     C                   move      yes           d1actfl
     C                   when      jhfbscd = 'I'
     C                   move      yes           d1inafl
     C                   when      jhfbscd = 'C'
     C                   move      yes           d1clofl
     C                   endsl
     C                   endif                                                  If budgets
     C                   enddo                                                  Do budgets
      *
     C                   if        c1fsnm <> *blank
     C     lower:upper   xlate     c1fsnm        wkname
     C     lower:upper   xlate     fsfsnm        wkfsnm
     C     wkname:length scan      wkfsnm                                 92
     C                   if        *in92 = *off
     C                   move      no            procfl
     C                   endif
     C                   endif
      *
      * Active budgets exist yes/no flag
      *
     C                   if        c1actfl <> *blank and
     C                             c1actfl <> d1actfl
     C                   move      no            procfl
     C                   endif
      *
      * Inactive budgets exist yes/no flag
      *
     C                   if        c1inafl <> *blank and
     C                             c1inafl <> d1inafl
     C                   move      no            procfl
     C                   endif
      *
      * Closed budgets exist yes/no flag
      *
     C                   if        c1clofl <> *blank and
     C                             c1clofl <> d1clofl
     C                   move      no            procfl
     C                   endif
      *
      * Cell
     C                   if        c1cell <> 0 and
     C                             c1cell <> fscell
     C                   move      no            procfl
     C                   endif
      *
      * Type of farm
     C                   if        c1tfcd > *blank and
     C                             c1tfcd <> fstfcd
     C                   move      no            procfl
     C                   endif
      *
      * Production phase
     C                   if        c1ppcd > *blank and
     C                             c1ppcd <> fsppcd
     C                   move      no            procfl
     C                   endif
      *
     C                   if        procfl = yes                                 If process
     C                   add       1             count
     C                   add       1             hdrrn1
     C                   z-add     tlfscd        d1fscd
     C                   movel     fsfsnm        d1fsnm
     C                   move      fsppcd        d1ppcd
     C                   z-add     fscell        d1cell
     C                   move      fstfcd        d1tfcd
     C                   write     hp7703s1
     C                   seton                                        81
      *
      * For positioning, place the subfile relative record number into
      * the sflrcdnbr field (hdpag1)
      *
     C                   eval      hdpag1 = hdrrn1
      *
     C                   endif                                                  If process
     C                   endif                                                  If farm hit
     C                   endif                                                  If read
     C                   enddo                                                  Do fill loop
      *
      * If no records were written to the subfile, then issue
      * the standard message 'No data to Display'.
      *
     C                   if        hdrrn1 = 0                                   If no recs
     C                   movel     yes           norecs
      *
     C                   if        error < maxmsg
     C                   add       1             error
     C                   eval      mgid(e) = 'HS09002'
     C                   endif
      *
     C                   else
     C                   movel     no            norecs
     C                   endif                                                  If no recs
      *
     C                   z-add     hdrrn1        svrrn1
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Seton last record and end job
      *---------------------------------------------------------------
      *
     C     $exit         begsr
     C                   seton                                        LR
     C                   endsr
      *
      /EJECT
      *---------------------------------------------------------------
      * Write error messages
      *---------------------------------------------------------------
      *
     C     $wrmsg        begsr
      *
     C                   call      'UT80045J'                           43
     C                   parm                    mgid
     C                   parm                    mgdt
     C                   parm                    error
     C                   parm      sdpgm         pgm
     C                   parm      msgfil        msgfl
      *
      * If call to UT80045J failed, a message hardcoded in the DSPF
      * command line format will be set on. So, redisplay screen.
      *
     C                   if        *in43 = *on
     C                   eval      routne = set1
     C                   endif
      *
     C                   endsr
      *
      *----------------------------------------------------------------
      * Clear messages
      *----------------------------------------------------------------
      *
     C     $clmsg        begsr
      *
     C                   call      'UT80045J'                           43
     C                   parm      *blank        mgi
     C                   parm      *blank        mgd
     C                   parm      *zero         error
     C                   parm      sdpgm         pgm
     C                   parm      msgfil        msgfl
      *
      * If call to UT80045J failed, a message hardcoded in the DSPF
      * command line format will be set on; so, redisplay screen.
      *
     C                   if        *in43 = *on
     C                   eval      routne = set1
     C                   endif
      *
     C                   endsr
      /EJECT
      *----------------------------------------------------------------
      * Initialization subroutine
      *----------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Parm lists
      *
     C     *entry        plist
     C                   parm                    c1btcd
     C                   parm                    c1btds
     C                   parm                    rtncd
      *
      *
      * Key lists
      *
     C     key01         klist
     C                   kfld                    c1btcd
     C                   kfld                    fsfscd
      *
     C     key02         klist
     C                   kfld                    c1btcd
     C                   kfld                    c1fscd
      *
      * To support F5-Refresh, logic requires the number of lines on
      * the subfile page and also the number of lines less 1.  By
      * setting these values up as constant work fields, the processing
      * logic can be standardized between programs.
      *
     C                   z-add     11            wkplin                         sflpag
     C     wkplin        sub       1             wkrlin                         sflpag - 1
      *
      *
      * The following standard code must be included to make the
      * standard error message handling program function properly.
      * This code sets and clears the program message queue.
      *
     C                   movel     '*'           msgpgm
     C                   exsr      $clmsg
      *
     C                   endsr
      /eject
