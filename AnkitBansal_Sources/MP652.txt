      *****************  RPG PROGRAM HEADING  ************************
      *
      * ENVIRONMENT:  PORK DIVISION
      * SYSTEM:       Interface MP2 to JDE
      * PROGRAM:      MP652
      * DESCRIPTION:  MP2: Listing of Invoices Not Posted to JDE
      * PROGRAMMER:   LeAnne Fedor
      * DATE:         09/11/03
      *
      *
      * FUNCTION:    Print a listing of the invoices that:
      *                1) have not yet been posted to JDE
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 03/12/08  LeAnne Ramsey
      *           We gave all functionality to High Plains Biodiesel. So,
      *           we now run over two different library lists. We added a Data Area
      *           holding Company Number. We will now print Company Number/Name at
      *           the top of the Report.
      *           Also replaced QPRINT with PRINT198.
      *
      * 03/06/14  LeAnne Ramsey (E2992)
      *           Added Accounting Company as a required selection.
      *
      * 03/26/19  Brad Baden  (E14631)
      *           Recompile only due to field size changes in database.
      *
      * 06/02/21  Danny Nguyen (S17068)
      *           Recompile only due to MPJ102B file change. MPP104 file was changed.
      *
      /eject
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fcaabrel1  if   e           k disk
      *    Company
      *
      *
     Fmpj102b   if   e           k disk
      *    MP2: Purchase Order Header + Invoice Header
      *    (Logical selects only invoices not yet posted to JDE)
      *
      *
     Fprint198  o    f  198        printer oflind(*inof)
      *
      /eject
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      *
      ****************************************************************
      * ARRAYS AND TABLES
      ****************************************************************
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Standard fields
      *
     d dash            s            154    inz(*all'-')
     d rtime           s              6  0
      *
      *
      * Control fields
      *
     D first           s              1    inz('Y')
      *
      *
      * Work fields for date manipulation
      *
     D wkmdy           s               D   datfmt(*mdy)
      *
      *
      * Print fields
      *
     d h1conm          s                   like(abadtx)
      *
     D l1rmam          s                   like(ihinam)
     d l1mmddyy        s              6  0
      *
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *---------------------------------------------------------------
      * Local data area.
      *---------------------------------------------------------------
      *
     Dlda             uds                  dtaara(*lda)
     D  ldjdvnno               1      8  0
     D  ldjdvnnm               9     48
     D  ldacono               86     88  0
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * MAINLINE
      ****************************************************************
      *
     C                   exsr      $h1hdr
      *
      * Read each record in the join logical file of purchase order header +
      * invoice header (This logical is limited to invoices not-yet-posted to JDE.)
      *
     C     ldacono       setll     mpj102b
      *
     C                   dou       *in90 = *on                                  Main do
     C     ldacono       reade     mpj102b                                90
     C                   if        *in90 = *off and                             If not EOF
     C                             (ldjdvnno = 0 or
     C                             ldjdvnno = phjdvnno)
      *
     C                   if        first = yes
     C                   move      no            first
     C                   endif
      *
      * Calculate remaining amount
      *
     C     ihtaam        sub       ihapam        l1rmam
      *
      *
      * Flip invoice date to MMDDYY
      *
     C     *iso          test(d)                 ihindt                 92
     C                   if        *in92 = *on                                  If bad date
     C                   z-add     0             l1mmddyy
     C                   else
     C     *iso          move      ihindt        wkmdy
     C                   movel     wkmdy         l1mmddyy
     C                   endif                                                  If bad date
      *
      * Print detail line
     C                   if        *inof = *on
     C                   exsr      $h1hdr
     C                   endif
     C                   except    l1dtl
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Main do
      *
      *----------------
      * EOF processing
      *----------------
      *
     C                   if        first = yes
     C                   except    nodata
     C                   endif
      *
     C                   seton                                        lr
      /eject
      *---------------------------------------------------------------
      * Print report headings
      *---------------------------------------------------------------
      *
     C     $h1hdr        begsr
      *
      * If the users have selected a single vendor, they only want his number/name
      * to print on the first line of each page. So, every time you page break, set
      * on indicator 86...set it off after you print the detail line.
      *
     C                   if        ldjdvnno <> 0
     C                   seton                                        86
     C                   endif
      *
     C                   except    h1hdr
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Retrieve time for report heading.
      *
     C                   time                    rtime
      *
      * Retrieve Accounting Company name
      *
     C     ldacono       chain     caabrel1                           92
     C                   if        *in92 = *off
     C                   eval      h1conm = abadtx
     C                   endif
      *
      * All vendors
     C                   if        ldjdvnno = 0
     C                   seton                                        8688
     C                   endif
      *
     C                   endsr
      /eject
      *-------------------------------------------------------------
      * Report heading lines
      *-------------------------------------------------------------
      *
     Oprint198  e            h1hdr          1 01
     O                       sdpgm               10
     O                                           85 'MP2 INTERFACE TO JDE'
     O                                          144 'DATE'
     O                       udate         y    154
      *
     O          e            h1hdr          1
     O                       sdusr               10
     O                                           80 'LISTING OF INVOICES NOT'
     O                                           94 ' POSTED TO JDE'
     O                                          144 'TIME'
     O                       rtime              154 '  :  :  '
      *
     O          e            h1hdr          2
     O                                          144 'PAGE'
     O                       page          z    154
      *
     O          e            h1hdr          1
     O                                           19 'Accounting Company:'
     O                       ldacono       z     28
     O                       h1conm              60
      *
     O          e            h1hdr          2
     O                                           11 'JDE Vendor:'
     O              N88      ldjdvnno      z     28
     O               88                          24 'ALL'
     O              N88      ldjdvnnm            70
      *
      *
      *-------------------------------------------------------------
      * Column headings
      *-------------------------------------------------------------
      *
     O          e            h1hdr          1
     O                                           10 'JDE VENDOR'
     O                                           61 'PURCHASE'
     O                                           94 'INVOICE'
     O                                          108 'INVOICED'
     O                                          123 'TOTAL AMOUNT'
     O                                          138 'APPLIED'
     O                                          153 'REMAINING'
      *
     O          e            h1hdr          1
     O                                            8 'NUMBER'
     O                                           27 'JDE VENDOR NAME'
     O                                           58 'ORDER'
     O                                           78 'INVOICE NUMBER'
     O                                           92 'DATE'
     O                                          108 'AMOUNT'
     O                                          123 'TO APPLY'
     O                                          138 'AMOUNT'
     O                                          153 'AMT TO APPLY'
      *
     O          e            h1hdr          1
     O                       dash               154
      *
      *-------------------------------------------------------------
      * Detail line
      *-------------------------------------------------------------
      *
     O          e            l1dtl       1
     O               86      phjdvnno      z      8
     O               86      phjdvnnm            52
     O                       phpono              61
      *
     O                       ihinno              84
     O                       l1mmddyy       b    94 '  -  -  '
     O                       ihinam        k    109
     O                       ihtaam        k    124
     O                       ihapam        k    139
     O                       l1rmam        kb   154
      *
      *-------------------------------------------------------------
      * No data message line
      *-------------------------------------------------------------
      *
     O          e            nodata      1
     O                                           22 'There were no unposted'
     O                                           32 ' invoices.'
      /eject
