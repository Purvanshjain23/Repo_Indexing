      *****************  RPG PROGRAM HEADING  ***********************
      *
      * SYSTEM:      Hog Production System
      * PROGRAM:     HP1062
      * TITLE:       UT: Edit (Maintain) Uploaded Transfers
      * PROGRAMMER:  LeAnne Ramsey
      * CREATED:     07/08/09
      *
      * FUNCTION: This program uses a subfile to maintain uploaded transfer movements.
      *
      *           This program is called straight from the Menu Option.
      *
      *           It does not have selectors/positioners.
      *           It has F4-Prompt logic.
      *           It uses the standard message handling program.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 07/29/09  LeAnne Ramsey
      *           Added logic to populate new field (Scheduled Ship Date CCYYMMDD) in
      *           the Editing file (HSP2062).
      *
      * 08/19/09  LeAnne Ramsey
      *           Since we are loading the subfile entirely instead of a page at a time,
      *           removed the *in25 logic....it was redundant.
      *
      * 10/15/13  LeAnne Ramsey (E2831)
      *           Recompile only. Added field 'MTech Reference'.
      *
      * 01/18/17  Brad Baden (E7986)
      *           Added select screen for Business Office prior to displaying edit screen.
      *           If the Business Office is 'OKLIV', Guymon records are displayed.  If the
      *           Business Office is 'IFLIV', Iowa records are displayed.
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fhpd1062   cf   e             workstn sfile(hp1062s1:hdrrn1)
     F                                     infds(iofeed)
      *
      *
     Fhsp018    if   e           k disk
      *  Farm site
      *
      *
     Fhsp019    if   e           k disk
      *  Farm buildings
      *
      *
     Fhsp0071   if   e           k disk
      *  Company Control
      *
      *
     Fhsp020    if   e           k disk
      *  Building rooms
      *
      *
     Fhsp034    if   e           k disk
      *  Hog groups
      *
      *
     Fhsl034d   if   e           k disk    rename(hgrec:hgrecd)
      *  Hog groups
      *
      *
     Fhsl034o   if   e           k disk    rename(hgrec:hgreco)
      *  Hog groups  (open and created groups only)
      *
      *
     Fhsl034p   if   e           k disk    rename(hgrec:hgrecp) prefix(p1)
      *  Hog groups (open groups only)
      *
      *
     Fhsp2062   uf a e           k disk
      *  Guymon Editing file
      *
     Fhsp2064   uf a e           k disk
      *  Iowa Editing file
      *
      /EJECT
      ****************************************************************
      * Definition Specifications
      ****************************************************************
      *
      ****************************************************************
      * Named Constants
      ****************************************************************
      *
     D create          c                   'CREATE'
     D yes             c                   'Y'
     D no              c                   'N'
     D set1            c                   'SET1  '
     D edit1           c                   'EDIT1 '
     D scrn1           c                   'SCRN1 '
     D exit            c                   'EXIT  '
      *
      ****************************************************************
      * ARRAYS AND TABLES
      ****************************************************************
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Standard fields
      *
     D mode            s              6
     D pgm             s             10
     D msgfl           s             10
     D maxmsg          s              2  0  inz(20)
     D routne          s              6
     D maxrrn          s              4  0
     D reset           s              1    inz('N')
     D norecs          s              1    inz('N')
     D count           s              2  0
     D wkflow          s                   like(idflow)
     D rtncd           s              2    inz('00')
     D svrrn1          s                   like(hdrrn1)
     D sdbodv          s              5
      *
     D eeditfl         s                        like(e1editfl)
     D emvsn           s                        like(e1mvsn)
     D emdy            s                        like(e1mdy)
     D escdt           s                        like(e1scdt)
     D eticd           s                        like(e1ticd)
     D effscd          s                        like(e1ffscd)
     D efblcd          s                        like(e1fblcd)
     D efrmcd          s                        like(e1frmcd)
     D efhgsn          s                        like(e1fhgsn)
     D efppcd          s                        like(e1fppcd)
     D efptcd          s                        like(e1fptcd)
     D etfscd          s                        like(e1tfscd)
     D etblcd          s                        like(e1tblcd)
     D etrmcd          s                        like(e1trmcd)
     D ethgsn          s                        like(e1thgsn)
     D etppcd          s                        like(e1tppcd)
     D etptcd          s                        like(e1tptcd)
     D ehd             s                        like(e1hd)
     D esplit          s                        like(e1split)
     D etype           s                        like(e1type)
      *
      * Workfields required for subfile paging--see initialization
      * subroutine.
      *
     D wkplin          s              2  0
     D wkrlin          s              2  0
      *
      *
      * Control and save fields
      *
     D svmvsn          s                   like(e1mvsn)
      *
     D svmdy           s                   like(e1mdy)
     D svorfscd        s                   like(hgfscd)
     D svorppcd        s                   like(hgppcd)
     D svorptcd        s                   like(hgptcd)
     D svdnfscd        s                   like(hgfscd)
     D svdnppcd        s                   like(hgppcd)
     D svdnptcd        s                   like(hgptcd)
      *
      *
      * Workfields for date manipulation
      *
     D wkisodate       s               d   datfmt(*iso)
      *
      *
     D xxhgcd          s                   like(hghgcd)
     D xxptcd          s                   like(hgptcd)
     D xxppcd          s                   like(hgppcd)
     D xxfscd          s                   like(hgfscd)
     D xxrmcd          s                   like(hgrmcd)
     D xxgscd          s                   like(hggscd)
     D xxalphfscd      s              5
     D xxfsnm          s                   like(fsfsnm)
     D xxaist          s                   like(fsaist)
     D xxblcd          s                   like(fbblcd)
     D xxblds          s                   like(fbblds)
      *
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard error message handling data structures
      *---------------------------------------------------------------
      *
      * For error message handling, a packed index (3) is required.
      * For program readability, define a corresponding error count
      * field called 'error'
      *
     D                 ds                  inz
     D  error                         2p 0
     D   e                            2p 0 overlay(error)
      *
      * This data structure supplies the name of the message file to
      * the message handling CL program.  The field name MSGFIL must be
      * constant. The value in quotes is the name of the specific
      * message file containing the user defined messages.
      *
     D                 ds                  inz
     D  msgfil                       10    inz('HSMSGF    ')
      *
      *---------------------------------------------------------------
      *  Standard message data structures
      *---------------------------------------------------------------
      *
      * The following 3 data structures are used to speed message
      * handling since it is faster to clear data structures than
      * arrays.  Each is associated with a standard message array.
      *
     D mgi             ds                  inz
     D  mgid                          7    dim(20)
      *
     D mgd             ds                  inz
     D  mgdt                         50    dim(20)
      *
     D mgwk            ds                  inz
     D  mgw                           1    dim(50)
      *
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *---------------------------------------------------------------
      * Standard workstation information data structure
      *---------------------------------------------------------------
      *    externally defined as UTWSFR (record format: UTIDFRR)
     D iofeed        e ds                  extname(utwsfr)
      *
      *
      *---------------------------------------------------------------
      * Standard database file information data structure
      *---------------------------------------------------------------
      *    externally defined as UTDBGR (record format: FDBCKD)
     D dbfeed        e ds                  extname(utdbfr)
      *
      *
      *-----------------------------------------------------------------
      * Definition for external data area 'DAAPER' for accounting period
      *-----------------------------------------------------------------
      *
     D*daaper          ds                  dtaara(daaper)
     D* daccyy                        4s 0
     D* daper                         2s 0
     D* dabpdt                        8s 0
     D* daepdt                        8s 0
     D* dapgfl                        1a
     D* dappfl                        1a
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * Mainline
      ****************************************************************
      *
     C                   eval      routne = set1
      *
     C     *inlr         doweq     *off
     C     routne        caseq     set1          $set1
     C     routne        caseq     scrn1         $scrn1
     C     routne        caseq     edit1         $edit1
     C     routne        caseq     exit          $exit
     C                   endcs
     C                   enddo
      /EJECT
      *----------------------------------------------------------------
      * Set environment for screen 1
      *----------------------------------------------------------------
      *
     C     $set1         begsr
      *
      * Clear and fill the subfile
      *
     C                   exsr      $clr1
     C                   exsr      $frsh1
      *
      * Set routine to display screen
      *
     C                   eval      routne = scrn1
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Perform operator I/O for screen 1
      *---------------------------------------------------------------
      *
     C     $scrn1        begsr
      *
      * Set the page of the subfile to display
      *
     C                   select
     C                   when      wkflow = 0 and error = 0
     C                   z-add     1             hdpag1
      *
     C                   when      reset = yes and wkflow <= hdrrn1
     C                   z-add     wkflow        hdpag1
     C                   endsl
      *
      *
      * Write the error messages from the error arrays to the
      * error message subfile
      *
     C                   exsr      $wrmsg
      *
      * This subfile has Fold/Unfold capabilities.  The DDS uses the keywords
      * (SFLDROP, SFLFOLD) to determine whether the user is folded or unfolded
      * and to redisplay the screen in the same mode as when he last saw it.
      * Indicators 78 and 79 are assigned in the DDS to the keywords.
      *
     C                   setoff                                       7879
     C                   if        fold = '0'
     C                   seton                                        78
     C                   else
     C                   seton                                        79
     C                   endif
      *
      * Write screen 1 to crt
      *
     C                   write     hp1062k1
     C                   write     hp1062ec
     C                   exfmt     hp1062c1
      *
      * Save the value of the lowest subfile rrn on the screen for
      * later repositioning of the subfile
      *
     C                   z-add     idflow        wkflow
     C                   move      no            reset
      *
      * Clear the messages from the message data structures and
      * program message queue
      *
     C                   exsr      $clmsg
      *
      * Get user's entry and set next routine
      *
     C                   select
     C                   when      *in03 = *on                                  F3-exit
     C                   eval      routne = exit
      *
     C                   when      norecs = yes                                 No data
     C                   eval      routne = set1
      *
     C                   when      *in04 = *on                                  F4-prompt
     C                   exsr      $f4s1
     C                   eval      routne = scrn1
      *
     C                   when      *in05 = *on                                  F5-refresh
     C                   eval      reset = yes
     C                   eval      routne = set1
      *
     C                   when      *in09 = *on                                  F9-accept
     C                   exsr      $edit1
     C                   if        error = 0
     C                   exsr      $accept
     C                   endif
     C                   other
      *
      * User pressed ENTER
      *
     C                   exsr      $edit1
     C                   movel     scrn1         routne
     C                   endsl
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Subroutine to edit input fields on screen 1
      *---------------------------------------------------------------
      *
     C     $edit1        begsr
      *
      * Edit the Control Record
      *
     C                   exsr      $edc1
      *
      * Edit the subfile if there are no errors in the control record.
      * If there are errors in the control record, you need to rewrite
      * any subfile recorrds with the error indicators off.
      *
     C                   if        error = 0
     C                   exsr      $eds1
     C                   else
     C                   exsr      $sfloff
     C                   endif
      *
     C                   movel     scrn1         routne
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Edit the control record info on screen 1
      *---------------------------------------------------------------
      *
     C     $edc1         begsr
      *
      * At the present time, we have no fields in the Control record to edit.
      *
     C                   endsr
      /EJECT
      *--------------------------------------------------------------------------------------
      * Edit subfile on screen 1
      *--------------------------------------------------------------------------------------
      *
     C     $eds1         begsr
      *
      * Set/initialize control/save fields.
      *
     C                   z-add     0             svmvsn
     C                   z-add     0             svmdy
     C                   z-add     0             svorfscd
     C                   move      *blank        svorppcd
     C                   move      *blank        svorptcd
     C                   z-add     0             svdnfscd
     C                   move      *blank        svdnppcd
     C                   move      *blank        svdnptcd
      *
      * Read/process all changed subfile records
      *
     C                   dou       *in80 = *on                                  Do process
     C                   readc     hp1062s1                               80
     C                   if        *in80 = *off                                 If rec read
      *
      * Set on MDT
      * Set off error indicators in the subfile
      *
     C                   seton                                        85
     C                   exsr      $offin
      *
      * If there are no user entries for the changed subfile record,
      * clear all fields on the line....except Movement Number.
      *
     C                   if        d1mdy    = 0 and                             If entry
     C                             d1orfscd = 0 and
     C                             d1orblcd = *blank and
     C                             d1orrmcd = *blank and
     C                             d1orhgcd = *blank and
     C                             d1schd   = 0 and
     C                             d1dnfscd = 0 and
     C                             d1dnblcd = *blank and
     C                             d1dnrmcd = *blank and
     C                             d1dnhgcd = *blank
     C                   exsr      $clrln
     C                   else
      *
      * Edit Scheduled Ship Date.
     C                   exsr      $mdy
      *
      * If groups are blank, clear output only group fields
      *
     C                   if        d1orhgcd = *blank
     C                   move      *blank        d1orgtcd
     C                   move      *blank        d1orptcd
     C                   move      *blank        d1orppcd
     C                   move      *blank        hdorfsnm
     C                   move      *blank        hdorgscd
     C                   endif
      *
     C                   if        d1dnhgcd = *blank
     C                   move      *blank        d1dngtcd
     C                   move      *blank        d1dnptcd
     C                   move      *blank        d1dnppcd
     C                   move      *blank        hddnfsnm
     C                   move      *blank        hddngscd
     C                   endif
      *
      * The user must at least enter an origin farm and a building or group.
      * Edit farm site. If farm is valid and neither building or group are
      * entered, issue message; otherwise retrieve defaults for missing values.
      *
     C                   exsr      $orfscd
      *
     C                   select
     C                   when      d1orblcd = *blank and
     C                             d1orhgcd = *blank and
     C                             *in31 = *off
     C                   seton                                        3435
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS09132'     mgid(e)
     C                   exsr      $err1
     C                   endif
     C                   other
      *
      * Perform 'origin' edits
      *
     C                   exsr      $edorigin
     C                   endsl
      *
      * The user must at least enter a destination farm and a building or group.
      * Edit farm site. If farm is valid and neither building or group are
      * entered, issue message; otherwise retrieve defaults for missing values.
      *
     C                   exsr      $dnfscd
      *
     C                   select
     C                   when      d1dnblcd = *blank and
     C                             d1dnhgcd = *blank and
     C                             *in32 = *off
     C                   seton                                        3637
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS09132'     mgid(e)
     C                   exsr      $err1
     C                   endif
     C                   other
      *
      * Perform 'destination' edits
      *
     C                   exsr      $eddest
     C                   endsl
     C                   endif                                                  If entry
      *
      * If there are multiple lines for the same movement and you have valid
      * Origin/Destination groups, perform 1 more check to make sure that the
      * Farm/Group Production Phase/Group Type values are consistent.
      * Then, save the values for checking the next line.
      *
     C                   if        d1mvsn = svmvsn
     C                                      and *in34 = *off and *in36 = *off
     C                                      and d1orhgcd <> *blank
     C                                      and d1dnhgcd <> *blank
     C                   exsr      $multline
     C                   endif
      *
     C                   z-add     d1mvsn        svmvsn
     C                   z-add     d1mdy         svmdy
     C                   z-add     d1orfscd      svorfscd
     C                   move      d1orppcd      svorppcd
     C                   move      d1orptcd      svorptcd
     C                   z-add     d1dnfscd      svdnfscd
     C                   move      d1dnppcd      svdnppcd
     C                   move      d1dnptcd      svdnptcd
      *
     C                   update    hp1062s1
     C                   exsr      $offin
     C                   endif                                                  If rec read
     C                   enddo                                                  Do process
      *
     C                   endsr
      /EJECT
      *----------------------------------------------------------------
      * Scheduled Ship Date
      *----------------------------------------------------------------
      *
     C     $mdy          begsr
      *
      * The line is so full that I didn't have room to display a negative sign if the
      * user screwed up the entry. So, I'll just flip it if the value is negative; this
      * will keep is from getting an "invalid date" error on it.
      *
     C                   if        d1mdy < 0
     C                   mult      -1            d1mdy
     C                   endif
      *
      * Clear the hidden date field
      *
     C                   z-add     0             hdscdt
      *
     C                   select
     C                   when      d1mdy = 0
     C                   seton                                        39
     C                   if        error < maxmsg
     C                   add       1             error
     C                   exsr      $err1
     C                   move      'HS09001'     mgid(e)
     C                   endif
     C                   other
      *
     C     *mdy          test(d)                 d1mdy                  92
     C                   if        *in92 = *on                                  If bad date
     C                   seton                                        39
     C                   if        error < maxmsg
     C                   add       1             error
     C                   exsr      $err1
     C                   eval      mgid(e) = 'HS09004'
     C                   endif
     C                   else
      *
      * Flip the date from MMDDYY to CCYYMMDD.
      *
     C     *mdy          move      d1mdy         wkisodate
     C                   move      wkisodate     hdscdt
     C                   endif                                                  If bad date
      *
      * If the Date is valid,
      *     Check that it is not in a Closed Period
      *     If it is in the Current Period,
      *         Check that the Period Close programs are not running
      *
     C                   if        *in39 = *off                                 If good date
      *
     C*                  in        daaper
     C                   if        sdbobo = 'OKLIV'
     C                   eval      cccocd = '350'
     C                   else
     C                   eval      cccocd = '340'
     C                   endif                                                  if sdbobo = 'OKLIV'

     C     cccocd        chain     hsp0071
      *
     C                   select
     C                   when      not %found(hsp0071)
     C                   seton                                        39
     C                   if        error < maxmsg
     C                   add       1             error
     C                   exsr      $err1
     C                   movel     'HS09246'     mgid(e)
     C                   endif

     C                   when      hdscdt < ccbpdt
     C                   seton                                        39
     C                   if        error < maxmsg
     C                   add       1             error
     C                   exsr      $err1
     C                   movel     'HS09007'     mgid(e)
     C                   endif
      *
     C                   when      hdscdt >= ccbpdt and
     C                             hdscdt <= ccepdt and
     C                             ccppfl <> *blank
     C                   seton                                        39
     C                   if        error < maxmsg
     C                   add       1             error
     C                   exsr      $err1
     C                   movel     'HS09016'     mgid(e)
     C                   endif
     C                   endsl
      *
     C                   endif                                                  If good date
     C                   endsl
      *
      * If you had a valid date and this is a multiline movement,
      * verify that the date is the same on all the movement lines.
      *
     C                   if        d1mvsn = svmvsn and *in39 = *off
     C                             and svmdy <> d1mdy
     C                   seton                                        39
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS01135'     mgid(e)
     C                   clear                   mgw
     C                   movea     mgw           mgdt(e)
     C                   exsr      $err1
     C                   endif
     C                   endif
      *
     C                   endsr
      /EJECT
      *----------------------------------------------------------------
      * Edit Origin Farm Site
      *----------------------------------------------------------------
      *
     C     $orfscd       begsr
      *
     C                   move      *blank        hdorfsnm
      *
     C                   select
     C                   when      d1orfscd = 0
     C                   seton                                        31
     C                   if        error < maxmsg
     C                   add       1             error
     C                   exsr      $err1
     C                   movel     'HS09001'     mgid(e)
     C                   endif
     C                   other
      *
      * Check that the Farm exists in the Farm Site master file
      *
     C     d1orfscd      chain     hsp018                             92
     C                   if        *in92 = *off                                 If good orig
     C                   movel     fsfsnm        hdorfsnm
     C                   movel     fsfsbo        d1orfsbo
     C                   else
      *
     C                   seton                                        31
     C                   if        error < maxmsg
     C                   add       1             error
     C                   exsr      $err1
     C                   movel     'HS00131'     mgid(e)
     C                   clear                   mgw
     C                   move      d1orfscd      xxalphfscd
     C                   movea     xxalphfscd    mgw(1)
     C                   movea     mgw           mgdt(e)
     C                   endif
     C                   endif                                                  If good orig
     C                   endsl
      *
     C                   endsr
      /EJECT
      *--------------------------------------------------------------------------------------
      * Edits for origin entries
      *--------------------------------------------------------------------------------------
      *
     C     $edorigin     begsr
      *
      * Validate:
      *  1) Building
      *  2) Room
      *  3) Hog group
      *
     C                   exsr      $orblcd
      *
      * Edit room if a) it is not blank and b) it was not edited previously
      * in the logic that attempted to default a group for a building.
      * (The check on indicator 40 keeps you from issuing a second invalid
      * room message.)
      *
     C                   if        d1orrmcd <> *blank and *in40 = *off
     C                   exsr      $orrmcd
     C                   endif
      *
     C                   exsr      $orhgcd
      *
      * Validate Scheduled Head
      *
     C                   select
     C                   when      d1schd = 0
     C                   seton                                        38
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS09001'     mgid(e)
     C                   exsr      $err1
     C                   endif
      *
     C                   when      d1schd < 0
     C                   seton                                        38
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS09000'     mgid(e)
     C                   exsr      $err1
     C                   endif
     C                   endsl
      *
     C                   endsr
      /EJECT
      *--------------------------------------------------------------------------------------
      * Validate origin building
      *--------------------------------------------------------------------------------------
      *
     C     $orblcd       begsr
      *
      * If building is blank and hog group is entered, default building and
      * room from hog group.
      *
     C                   if        d1orblcd = *blank and d1orhgcd <> *blank
     C     d1orhgcd      chain     hsp034                             92
     C                   if        *in92 = *off
     C                   move      hgblcd        d1orblcd
     C                   move      hgrmcd        d1orrmcd
     C                   endif
     C                   endif
      *
      * Validations:
      *  1) building is required
      *  2) building must exist on the specified farm site
      *
     C                   select
     C                   when      d1orblcd = *blank
     C                   seton                                        35
     C                   if        error < maxmsg
     C                   add       1             error
     C                   exsr      $err1
     C                   eval      mgid(e) = 'HS09001'
     C                   endif
     C                   other
      *
     C     key01         chain     hsp019                             92
     C                   if        *in92 = *on                                  If not found
     C                   seton                                        35
     C                   if        error < maxmsg
     C                   add       1             error
     C                   exsr      $err1
     C                   eval      mgid(e) = 'HS00116'
     C                   clear                   mgw
     C                   movea     d1orblcd      mgw(1)
     C                   move      d1orfscd      xxalphfscd
     C                   movea     xxalphfscd    mgw(6)
     C                   movea     mgw           mgdt(e)
     C                   endif
     C                   endif                                                  If not found
     C                   endsl
      *
      * Attempt to default in a group when:
      *   1) building is valid and group is blank and
      *   2) room is blank or valid
      *
     C                   if        *in35 = *off and d1orhgcd = *blank           If valid building
      *
     C                   if        d1orrmcd <> *blank
     C                   exsr      $orrmcd
     C                   endif
      *
     C                   if        *in40 = *off                                 If room OK/blank
     C                   exsr      $orgroup
     C                   endif                                                  If room OK/blank
     C                   endif                                                  If valid building
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------------------------------------
      * Validate origin room
      *---------------------------------------------------------------------------------------------
      *
      * Validations:
      *  1) required
      *  2) must exist in the building on the farm site
      *  3) must have an active status
      *
     C     $orrmcd       begsr
      *
     C                   select
     C                   when      d1orrmcd = *blank
     C                   seton                                        40
     C                   if        error < maxmsg
     C                   add       1             error
     C                   exsr      $err1
     C                   eval      mgid(e) = 'HS09001'
     C                   endif
     C                   other
      *
      * Check that the room exists in the building on the farm site
     ‚*
     C     key02         chain     hsp020                             92
     C                   if        *in92 = *on                                  If no hit
     C                   seton                                        40
     C                   if        error < maxmsg
     C                   add       1             error
     C                   exsr      $err1
     C                   eval      mgid(e) = 'HS00117'
     C                   clear                   mgw
     C                   movea     d1orrmcd      mgw(1)
     C                   movea     d1orblcd      mgw(6)
     C                   move      d1orfscd      xxalphfscd
     C                   movea     xxalphfscd    mgw(11)
     C                   movea     mgw           mgdt(e)
     C                   endif
     C                   else
     ‚*
      * Room must have an 'active' status
     ‚*
     C                   if        brrmst <> 'A'                                If not active
     C                   seton                                        40
     C                   if        error < maxmsg
     C                   add       1             error
     C                   exsr      $err1
     C                   eval      mgid(e) = 'HS00726'
     C                   clear                   mgw
     C                   movea     d1orrmcd      mgw(1)
     C                   movea     mgw           mgdt(e)
     C                   endif
     C                   endif                                                  If not active
     C                   endif                                                  If no hit
      *
     C                   endsl
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------------------------------------
      * Attempt to find a default group in the origin building
      *---------------------------------------------------------------------------------------------
      *
      * If only one group is open in that building or building/room, default
      * it in. But, if multiple groups are open, issue a message to the
      * user to select a group.
      *
     C     $orgroup      begsr
      *
      * Key with either a) farm/building if room is blank or b) farm/
      * building/room if room is populated.
      *
     C                   if        d1orrmcd = *blank
     C     key01         chain     hsl034p                            92
     C                   else
     C     key02         chain     hsl034p                            92
     C                   endif
      *
     C                   if        *in92 = *off                                 If open group
      *
      * Check to see if there is another open group.
      * Key with either a) farm/building if room is blank or b) farm/
      * building/room if room is populated.
      *
     C                   if        d1orrmcd = *blank
     C     key01         reade     hsl034p                                92
     C                   else
     C     key02         reade     hsl034p                                92
     C                   endif
      *
     C                   if        *in92 = *off                                 If mult group
     C                   seton                                        34
     C                   if        error < maxmsg
     C                   add       1             error
     C                   clear                   mgw
     C                   movea     d1orblcd      mgw(1)
      *
     C                   select
     C                   when      d1orrmcd = *blank
     C                   movel     'HS00721'     mgid(e)
     C                   other
     C                   movel     'HS00765'     mgid(e)
     C                   movea     d1orrmcd      mgw(6)
     C                   endsl
      *
     C                   movea     mgw           mgdt(e)
     C                   exsr      $err1
     C                   endif
     C                   else
      *
      * Only one group is open; default in its values.
      *
     C                   movel     p1hghgcd      d1orhgcd
     C                   movel     p1hgrmcd      d1orrmcd
     C                   endif                                                  If mult group
     C                   else
      *
      * No groups are open in this building/room
      *
     C                   seton                                        34
     C                   if        error < maxmsg
     C                   add       1             error
     C                   clear                   mgw
     C                   move      d1orfscd      xxalphfscd
     C                   movea     xxalphfscd    mgw(1)
     C                   movea     d1orblcd      mgw(6)
      *
     C                   select
     C                   when      d1orrmcd = *blank
     C                   movel     'HS00722'     mgid(e)
     C                   other
     C                   movel     'HS00766'     mgid(e)
     C                   movea     d1orrmcd      mgw(11)
     C                   endsl
      *
     C                   movea     mgw           mgdt(e)
     C                   exsr      $err1
     C                   endif
     C                   endif                                                  If open group
      *
     C                   endsr
      /EJECT
      *----------------------------------------------------------------
      * Edit Origin Hog Group
      *----------------------------------------------------------------
      *
     C     $orhgcd       begsr
      *
     C                   z-add     0             hdorhgsn
     C                   move      *blank        hdorgscd
     C                   move      *blank        d1orptcd
     C                   move      *blank        d1orppcd
     C                   move      *blank        d1orgtcd
      *
     C                   select
     C                   when      d1orhgcd = *blank
     C                   seton                                        34
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS09001'     mgid(e)
     C                   exsr      $err1
     C                   endif
     C                   other
      *
     C     d1orhgcd      chain     hsp034                             92
     C                   if        *in92 = *on                                  If bad group
     C                   seton                                        34
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS00261'     mgid(e)
     C                   clear                   mgw
     C                   movea     d1orhgcd      mgw(1)
     C                   movea     mgw           mgdt(e)
     C                   exsr      $err1
     C                   endif
     C                   else
      *
     C                   move      hggscd        hdorgscd
     C                   move      hggtcd        d1orgtcd
      *
      * Origin Group must always have a status of Opened
      *
     C                   if        hggscd <> 'OP'                               If not open
     C                   seton                                        34
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS00603'     mgid(e)
     C                   clear                   mgw
     C                   movea     d1orhgcd      mgw(1)
     C                   movea     hggscd        mgw(8)
     C                   movea     mgw           mgdt(e)
     C                   exsr      $err1
     C                   endif
     C                   endif                                                  If not open
      *
      * Group must be on the origin farm
      *
     C                   if        hgfscd <> d1orfscd                           If diff farm
     C                   seton                                        34
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS00468'     mgid(e)
     C                   clear                   mgw
     C                   movea     d1orhgcd      mgw(1)
     C                   move      hgfscd        xxalphfscd
     C                   movea     xxalphfscd    mgw(8)
     C                   move      d1orfscd      xxalphfscd
     C                   movea     xxalphfscd    mgw(13)
     C                   movea     mgw           mgdt(e)
     C                   exsr      $err1
     C                   endif
     C                   endif                                                  If diff farm
      *
      *
      * Group building must match subfile building---if there is a building
      * on the subfile.
     C                   select
     C                   when      d1orblcd = *blank
     C                   movel     hgblcd        d1orblcd
     C                   other
     C                   if        d1orblcd <> hgblcd                           If mismatch building
     C                   seton                                        34
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS00481'     mgid(e)
     C                   clear                   mgw
     C                   movea     d1orhgcd      mgw(1)
     C                   movea     hgblcd        mgw(8)
     C                   movea     d1orblcd      mgw(13)
     C                   movea     mgw           mgdt(e)
     C                   exsr      $err1
     C                   endif
     C                   endif                                                  If mismatch building
     C                   endsl
      *
      *
      * Group room must match subfile room---if there is a room
      * on the subfile.
     C                   select
     C                   when      d1orrmcd = *blank
     C                   movel     hgrmcd        d1orrmcd
     C                   other
     C                   if        d1orrmcd <> hgrmcd                           If mismatch room
     C                   seton                                        34
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS00482'     mgid(e)
     C                   clear                   mgw
     C                   movea     d1orhgcd      mgw(1)
     C                   movea     hgrmcd        mgw(8)
     C                   movea     d1orrmcd      mgw(13)
     C                   movea     mgw           mgdt(e)
     C                   exsr      $err1
     C                   endif
     C                   endif                                                  If mismatch room
     C                   endsl
      *
     C                   endif                                                  If bad group
     C                   endsl
      *
     C                   if        *in34 = *off
     C                   z-add     hghgsn        hdorhgsn
     C                   move      hgptcd        d1orptcd
     C                   move      hgppcd        d1orppcd
     C                   endif
      *
     C                   endsr
      /EJECT
      *--------------------------------------------------------------------------------------------
      * Edit Destination Farm Site
      *--------------------------------------------------------------------------------------------
      *
     C     $dnfscd       begsr
      *
     C                   move      *blank        hddnfsnm
      *
     C                   select
     C                   when      d1dnfscd = 0
     C                   seton                                        32
     C                   if        error < maxmsg
     C                   add       1             error
     C                   exsr      $err1
     C                   movel     'HS09001'     mgid(e)
     C                   endif
     C                   other
      *
      *
      * Check that the Farm exists in the Farm Site master file
      *
     C     d1dnfscd      chain     hsp018                             92
     C                   if        *in92 = *off                                 If good dest
     C                   movel     fsfsnm        hddnfsnm
     C                   movel     fsfsbo        d1dnfsbo
     C                   else
      *
      * Invalid Farm
     C                   seton                                        32
     C                   if        error < maxmsg
     C                   add       1             error
     C                   exsr      $err1
     C                   movel     'HS00131'     mgid(e)
     C                   clear                   mgw
     C                   move      d1dnfscd      xxalphfscd
     C                   movea     xxalphfscd    mgw(1)
     C                   movea     mgw           mgdt(e)
     C                   endif
     C                   endif                                                  If good dest
     C                   endsl
      *
     C                   endsr
      /EJECT
      *--------------------------------------------------------------------------------------------
      * Edits for Destination Entries
      *--------------------------------------------------------------------------------------------
      *
     C     $eddest       begsr
      *
      * Validate destination:
      *  1) Building
      *  2) Room
      *  3) Hog group
      *
     C                   exsr      $dnblcd
      *
      * Edit room if a) it is not blank and b) it was not edited previously
      * in the logic that attempted to default a group for a building.
      * (The check on indicator 48 keeps you from issuing a second invalid
      * room message.)
      *
     C                   if        d1dnrmcd <> *blank and *in48 = *off
     C                   exsr      $dnrmcd
     C                   endif
      *
     C                   exsr      $dnhgcd
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------------------------------------
      * Validate destination building
      *---------------------------------------------------------------------------------------------
      *
     C     $dnblcd       begsr
      *
      * If building is blank and hog group is entered, default building and
      * room from hog group.
      *
     C                   if        d1dnblcd = *blank and d1dnhgcd <> *blank
     C     d1dnhgcd      chain     hsp034                             92
     C                   if        *in92 = *off
     C                   move      hgblcd        d1dnblcd
     C                   move      hgrmcd        d1dnrmcd
     C                   endif
     C                   endif
      *
      * Validations:
      *  1) building is required
      *  2) building must exist on the specified farm site
      *
     C                   select
     C                   when      d1dnblcd = *blank
     C                   seton                                        37
     C                   if        error < maxmsg
     C                   add       1             error
     C                   exsr      $err1
     C                   eval      mgid(e) = 'HS09001'
     C                   endif
     C                   other
      *
     C     key03         chain     hsp019                             92
     C                   if        *in92 = *on                                  If not found
     C                   seton                                        37
     C                   if        error < maxmsg
     C                   add       1             error
     C                   exsr      $err1
     C                   eval      mgid(e) = 'HS00116'
     C                   clear                   mgw
     C                   movea     d1dnblcd      mgw(1)
     C                   move      d1dnfscd      xxalphfscd
     C                   movea     xxalphfscd    mgw(6)
     C                   movea     mgw           mgdt(e)
     C                   endif
     C                   endif                                                  If not found
     C                   endsl
      *
      * Attempt to default in a group when:
      *   1) building is valid and group is blank and
      *   2) room is blank or valid
      *
     C                   if        *in37 = *off and d1dnhgcd = *blank           If valid building
      *
     C                   if        d1dnrmcd <> *blank
     C                   exsr      $dnrmcd
     C                   endif
      *
     C                   if        *in48 = *off                                 If room OK/blank
     C                   exsr      $dngroup
     C                   endif                                                  If room OK/blank
     C                   endif                                                  If valid building
      *
     C                   endsr
      /EJECT
      *----------------------------------------------------------------
      * Validate destination room
      *----------------------------------------------------------------
      *
      * Validations:
      *  1) required
      *  2) must exist in the building on the farm site
      *  3) must have an active status
      *
     C     $dnrmcd       begsr
      *
     C                   select
     C                   when      d1dnrmcd = *blank
     C                   seton                                        48
     C                   if        error < maxmsg
     C                   add       1             error
     C                   exsr      $err1
     C                   eval      mgid(e) = 'HS09001'
     C                   endif
     C                   other
      *
      * Check that the room exists in the building on the farm site
     ‚*
     C     key04         chain     hsp020                             92
     C                   if        *in92 = *on                                  If no hit
     C                   seton                                        48
     C                   if        error < maxmsg
     C                   add       1             error
     C                   exsr      $err1
     C                   eval      mgid(e) = 'HS00117'
     C                   clear                   mgw
     C                   movea     d1dnrmcd      mgw(1)
     C                   movea     d1dnblcd      mgw(6)
     C                   move      d1dnfscd      xxalphfscd
     C                   movea     xxalphfscd    mgw(11)
     C                   movea     mgw           mgdt(e)
     C                   endif
     C                   else
     ‚*
      * Room must have an 'active' status
     ‚*
     C                   if        brrmst <> 'A'                                If not active
     C                   seton                                        48
     C                   if        error < maxmsg
     C                   add       1             error
     C                   exsr      $err1
     C                   eval      mgid(e) = 'HS00726'
     C                   clear                   mgw
     C                   movea     d1dnrmcd      mgw(1)
     C                   movea     mgw           mgdt(e)
     C                   endif
     C                   endif                                                  If not active
     C                   endif                                                  If no hit
     C                   endsl
      *
     C                   endsr
      /eject
      *-----------------------------------------------------------------------
      * Attempt to find a default group in the destination building
      *-----------------------------------------------------------------------
      *
     C     $dngroup      begsr
      *
      * If only one group is open/created in that building or building/room, default
      * it in. But, if multiple groups are open/created, issue a message to the
      * user to select a group.
      *
      * Key with either a) farm/building if room is blank or b) farm/
      * building/room if room is populated.
      *
     C                   if        d1dnrmcd = *blank
     C     key03         chain     hsl034o                            92
     C                   else
     C     key04         chain     hsl034o                            92
     C                   endif
     C                   if        *in92 = *off                                 If open/created
      *
      * Check to see if there is another open/created group.
      * Key with either a) farm/building if room is blank or b) farm/
      * building/room if room is populated.
      *
      *
     C                   if        d1dnrmcd = *blank
     C     key03         reade     hsl034o                                92
     C                   else
     C     key04         reade     hsl034o                                92
     C                   endif
      *
     C                   if        *in92 = *off                                 If mult group
     C                   seton                                        36
     C                   if        error < maxmsg
     C                   add       1             error
     C                   clear                   mgw
     C                   movea     d1dnblcd      mgw(1)
      *
     C                   select
     C                   when      d1dnrmcd = *blank
     C                   movel     'HS00767'     mgid(e)
     C                   other
     C                   movel     'HS00769'     mgid(e)
     C                   movea     d1dnrmcd      mgw(6)
     C                   endsl
      *
     C                   movea     mgw           mgdt(e)
     C                   exsr      $err1
     C                   endif
     C                   else
      *
      * Only one group is open/created; default in its values.
      *
     C                   movel     hghgcd        d1dnhgcd
     C                   movel     hgrmcd        d1dnrmcd
     C                   endif                                                  If mult group
     C                   else
      *
      * No groups are open/created in this building/room
      *
     C                   seton                                        36
     C                   if        error < maxmsg
     C                   add       1             error
     C                   clear                   mgw
     C                   move      d1dnfscd      xxalphfscd
     C                   movea     xxalphfscd    mgw(1)
     C                   movea     d1dnblcd      mgw(6)
      *
     C                   select
     C                   when      d1dnrmcd = *blank
     C                   movel     'HS00768'     mgid(e)
     C                   other
     C                   movel     'HS00770'     mgid(e)
     C                   movea     d1dnrmcd      mgw(11)
     C                   endsl
      *
     C                   movea     mgw           mgdt(e)
     C                   exsr      $err1
     C                   endif
     C                   endif                                                  If open/created
      *
     C                   endsr
      /EJECT
      *----------------------------------------------------------------
      * Edit destination hog group
      *----------------------------------------------------------------
      *
     C     $dnhgcd       begsr
      *
     C                   move      *blank        d1dngtcd
     C                   move      *blank        d1dnptcd
     C                   move      *blank        d1dnppcd
     C                   z-add     0             hddnhgsn
     C                   move      *blank        hddngscd
      *
      * Validations:
      *  1) required
      *  2) must be valid in hog group file
      *  3) must have a status of created or open
      *  4) must be on the destination farm
      *  5) cannot be the same group as the origin group
      *  6) both groups must be wean-to-finish if either group is WTF
      *
     C                   select
     C                   when      d1dnhgcd = *blank
     C                   seton                                        36
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS09001'     mgid(e)
     C                   exsr      $err1
     C                   endif
     C                   other
      *
     C     d1dnhgcd      chain     hsp034                             92
     C                   if        *in92 = *on                                  If bad group
     C                   seton                                        36
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS00261'     mgid(e)
     C                   clear                   mgw
     C                   movea     d1dnhgcd      mgw(1)
     C                   movea     mgw           mgdt(e)
     C                   exsr      $err1
     C                   endif
     C                   else
      *
     C                   move      hggscd        hddngscd
     C                   move      hggtcd        d1dngtcd
      *
      * Group must have a status of Opened or Created
      *
     C                   if        hggscd = 'DS' or                             If status
     C                             hggscd = 'CL' or
     C                             hggscd = 'VD'
     C                   seton                                        36
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS00602'     mgid(e)
     C                   clear                   mgw
     C                   movea     d1dnhgcd      mgw(1)
     C                   movea     hggscd        mgw(8)
     C                   movea     mgw           mgdt(e)
     C                   exsr      $err1
     C                   endif
     C                   endif                                                  If status
      *
      * Group must be on the Destination Farm
      *
     C                   if        hgfscd <> d1dnfscd                           If diff farm
     C                   seton                                        36
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS00468'     mgid(e)
     C                   clear                   mgw
     C                   movea     d1dnhgcd      mgw(1)
     C                   move      hgfscd        xxalphfscd
     C                   movea     xxalphfscd    mgw(8)
     C                   move      d1dnfscd      xxalphfscd
     C                   movea     xxalphfscd    mgw(13)
     C                   movea     mgw           mgdt(e)
     C                   exsr      $err1
     C                   endif
     C                   endif                                                  If diff farm
     C                   endif                                                  If bad group
      *
      * Group building must match subfile building---if there is a building
      * on the subfile.
      *
     C                   select
     C                   when      d1dnblcd = *blank
     C                   movel     hgblcd        d1dnblcd
     C                   other
     C                   if        d1dnblcd <> hgblcd                           If mismatch building
     C                   seton                                        36
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS00481'     mgid(e)
     C                   clear                   mgw
     C                   movea     d1dnhgcd      mgw(1)
     C                   movea     hgblcd        mgw(8)
     C                   movea     d1dnblcd      mgw(13)
     C                   movea     mgw           mgdt(e)
     C                   exsr      $err1
     C                   endif
     C                   endif                                                  If mismatch building
     C                   endsl
      *
      *
      * Group room must match subfile room---if there is a room
      * on the subfile.
     C                   select
     C                   when      d1dnrmcd = *blank
     C                   movel     hgrmcd        d1dnrmcd
     C                   other
     C                   if        d1dnrmcd <> hgrmcd                           If mismatch room
     C                   seton                                        36
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS00482'     mgid(e)
     C                   clear                   mgw
     C                   movea     d1dnhgcd      mgw(1)
     C                   movea     hgrmcd        mgw(8)
     C                   movea     d1dnrmcd      mgw(13)
     C                   movea     mgw           mgdt(e)
     C                   exsr      $err1
     C                   endif
     C                   endif                                                  If mismatch room
     C                   endsl
     C                   endsl
      *
      *
      * Edits against origin/destination groups
      *
     C                   if        *in34 = *off and *in36 = *off                If edit more
      *
      * Origin/destination cannot be the same group
      *
     C                   if        d1orhgcd = d1dnhgcd                          If same group
     C                   seton                                        36
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS00609'     mgid(e)
     C                   exsr      $err1
     C                   endif
     C                   endif                                                  If same group
      *
      *
      * If either group is WTF, then both origin/destination groups must be WTF
      * (unless the orgin phase is BGF....)
      *
     C                   if        d1orppcd <> 'BGF  ' and                      If either WTF
     C                             (d1orgtcd = 'W' or d1dngtcd = 'W') and
     C                             (d1orgtcd <> 'W' or d1dngtcd <> 'W')
     C                   seton                                        3436
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS00883'     mgid(e)
     C                   exsr      $err1
     C                   endif
     C                   endif                                                  If either WTF
     C                   endif                                                  If edit more
      *
      *
     C                   if        *in36 = *off
     C                   z-add     hghgsn        hddnhgsn
     C                   move      hgptcd        d1dnptcd
     C                   move      hgppcd        d1dnppcd
     C                   endif
      *
      * Perform additional edits when the destination group is:
      *   1) a wean-to-finish group
      *
     C                   if        *in36 = *off and d1dngtcd = 'W'
     C                   exsr      $wtf
     C                   endif
      *
     C                   endsr
      /EJECT
      *-------------------------------------------------------------------------
      * Wean-to-finish edits for destination group
      *-------------------------------------------------------------------------
      *
      * You are only in this subroutine when the destination group has a group type
      * of wean-to-finish. Basically, we are just trying to prevent illegal WTF movements
      * here
      *
     C     $wtf          begsr
      *
     C                   select                                                 Big select
      * Out of BGF
     C                   when      d1orppcd = 'BGF  ' and
     C                             (d1dnppcd = 'NUR  ' or d1dnppcd = 'GF   ')
      * Out of Nursery
     C                   when      d1orppcd = 'NUR  ' and
     C                             (d1dnppcd = 'GF   ' or
     C                             (d1dnppcd = 'NUR  ' and
     C                             (d1orfscd <> d1dnfscd or
     C                              d1orblcd <> d1dnblcd or
     C                              d1orrmcd <> d1dnrmcd)))
      * Out of Grow Finish
      *
     C                   when      d1orppcd = 'GF   ' and
     C                             (d1dnppcd = 'GF   ' and
     C                             (d1orfscd <> d1dnfscd or
     C                              d1orblcd <> d1dnblcd or
     C                              d1orrmcd <> d1dnrmcd))
     C                   other
      *
      * All other transfer combinations into a wean-to-finish group are invalid
      *
     C                   seton                                        36
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS01146'     mgid(e)
     C                   exsr      $err1
     C                   endif
     C                   endsl                                                  Big select
      *
     C                   endsr
      /EJECT
      *--------------------------------------------------------------------------------------
      * Check for consistent Farm/Production Phase/Type on Movements with Multiple Lines
      *--------------------------------------------------------------------------------------
      *
     C     $multline     begsr
      *
      * The Origin Farm must be the same for multiple lines on the same Movement.
      *
     C                   if        svorfscd <> d1orfscd
     C                   seton                                        31
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS01137'     mgid(e)
     C                   clear                   mgw
     C                   movea     mgw           mgdt(e)
     C                   exsr      $err1
     C                   endif
     C                   endif
      *
      * All origin groups on a Movement must have the same Production phase
      *
     C                   if        svorppcd <> d1orppcd
     C                   seton                                        34
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS01143'     mgid(e)
     C                   clear                   mgw
     C                   movea     d1orppcd      mgw(1)
     C                   movea     svorppcd      mgw(6)
     C                   movea     mgw           mgdt(e)
     C                   exsr      $err1
     C                   endif
     C                   endif
      *
      * All origin groups on a Movement must have the same production type
      *
     C                   if        svorptcd <> d1orptcd                         If type
     C                   seton                                        34
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS01140'     mgid(e)
     C                   clear                   mgw
     C                   movea     d1orptcd      mgw(1)
     C                   movea     svorptcd      mgw(6)
     C                   movea     mgw           mgdt(e)
     C                   exsr      $err1
     C                   endif
     C                   endif                                                  If type
      *
      * The Destination Farm must be the same for multiple lines on the same Movement.
      *
     C                   if        svdnfscd <> d1dnfscd
     C                   seton                                        32
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS01136'     mgid(e)
     C                   clear                   mgw
     C                   movea     mgw           mgdt(e)
     C                   exsr      $err1
     C                   endif
     C                   endif
      *
      * All destination groups must have the same production phase
      *
     C                   if        svdnppcd <> d1dnppcd                         If phase
     C                   seton                                        36
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS01142'     mgid(e)
     C                   clear                   mgw
     C                   movea     d1dnppcd      mgw(1)
     C                   movea     svdnppcd      mgw(6)
     C                   movea     mgw           mgdt(e)
     C                   exsr      $err1
     C                   endif
     C                   endif                                                  If phase
      *
      *
      * All destination groups must have the same production type
      *
     C                   if        svdnptcd <> d1dnptcd                         If type
     C                   seton                                        36
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS01141'     mgid(e)
     C                   clear                   mgw
     C                   movea     d1dnptcd      mgw(1)
     C                   movea     svdnptcd      mgw(6)
     C                   movea     mgw           mgdt(e)
     C                   exsr      $err1
     C                   endif
     C                   endif                                                  If type
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Clear the subfile on Screen 1
      *---------------------------------------------------------------
      *
     C     $clr1         begsr
      *
     C                   z-add     0             svrrn1
      *
     C                   clear                   hp1062s1
     C                   seton                                        83        SFLCLR
     C                   write     hp1062c1
     C                   setoff                                       818283
      *                                                    81=sfldsp
      *                                                    82=sflend
      *                                                    83=sflclr
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Refresh/fill the subfile for screen 1
      *---------------------------------------------------------------
      *
     C     $frsh1        begsr
      *
      * Always load the entire subfile.
      *
     C                   if        sdbobo = 'OKLIV'
     C     *start        setll     hsp2062
     C                   else
     C     *start        setll     hsp2064
     C                   endif                                                  If sdbobo = 'OKLIV'
     C                   exsr      $load1
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Load the subfile on screen 1
      *---------------------------------------------------------------
      *
      * We fill the entire subfile in this program.
      *
     C     $load1        begsr
      *
     C                   z-add     svrrn1        hdrrn1
      *
     C                   setoff                                       82
     C                   dou       *in82 = *on                                  Do fill
     C                   if        sdbobo = 'OKLIV'
     C                   read      hsp2062                                82
     C                   else
     C                   read      hsp2064                                82
     C                   endif                                                  If sdbobo = 'OKLIV'
      *
     C                   if        *in82 = *off                                 If not EOF
      *
     C                   if        sdbobo = 'OKLIV'
     C                   exsr      $loadguy
     C                   else
     C                   exsr      $loadiowa
     C                   endif                                                  If sdbobo = 'OKLIV'
      *
     C                   move      esplit        hdsplit
     C                   move      etype         hdtype
      *
     C                   z-add     emvsn         d1mvsn
     C                   z-add     ehd           d1schd
     C                   z-add     emdy          d1mdy
      * Origin
     C                   z-add     effscd        d1orfscd
     C     d1orfscd      chain     hsp018                             92
     C                   if        *in92 = *off
     C                   movel     fsfsnm        hdorfsnm
     C                   else
     C                   move      *blank        hdorfsnm
     C                   endif
      *
     C                   move      efblcd        d1orblcd
     C                   move      efrmcd        d1orrmcd
     C                   move      efppcd        d1orppcd
     C                   move      efptcd        d1orptcd
      *
     C                   move      *blank        d1orhgcd
     C                   move      *blank        d1orgtcd
     C                   if        efhgsn <> 0
     C     efhgsn        chain     hsl034d                            92
     C                   if        *in92 = *off
     C                   move      hghgcd        d1orhgcd
     C                   move      hggtcd        d1orgtcd
     C                   endif
     C                   endif
      *
      * Destination
     C                   z-add     etfscd        d1dnfscd
     C     d1dnfscd      chain     hsp018                             92
     C                   if        *in92 = *off
     C                   movel     fsfsnm        hddnfsnm
     C                   else
     C                   move      *blank        hddnfsnm
     C                   endif
      *
     C                   move      etblcd        d1dnblcd
     C                   move      etrmcd        d1dnrmcd
     C                   move      etppcd        d1dnppcd
     C                   move      etptcd        d1dnptcd
      *
     C                   move      *blank        d1dnhgcd
     C                   move      *blank        d1dngtcd
     C                   if        ethgsn <> 0
     C     ethgsn        chain     hsl034d                            92
     C                   if        *in92 = *off
     C                   move      hghgcd        d1dnhgcd
     C                   move      hggtcd        d1dngtcd
     C                   endif
     C                   endif
      *
     C                   add       1             hdrrn1
      *
     C                   seton                                        85
     C                   write     hp1062s1
     C                   exsr      $sfloff
     C                   seton                                        81
      *
      * For positioning, place the subfile relative record number into
      * the sflrcdnbr field (hdpag1)
      *
     C                   z-add     hdrrn1        hdpag1
     C                   endif                                                  If read
     C                   enddo                                                  Do fill loop
      *
      * If no records were written to the subfile, then issue
      * the standard message 'No data to Display'.
      *
     C                   if        hdrrn1 = 0                                   If no recs
     C                   movel     yes           norecs
     C                   if        error < maxmsg
     C                   add       1             error
     C                   eval      mgid(e) = 'HS09002'
     C                   endif
     C                   else
      *
     C                   movel     no            norecs
     C                   endif                                                  If no recs
      *
     C                   z-add     hdrrn1        svrrn1
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Move Guymon fields to universal fields
      *---------------------------------------------------------------
      *
     C     $loadguy      begsr
      *
     C                   eval      eeditfl = e1editfl
     C                   eval      emvsn   = e1mvsn
     C                   eval      emdy    = e1mdy
     C                   eval      escdt   = e1scdt
     C                   eval      eticd   = e1ticd
     C                   eval      effscd  = e1ffscd
     C                   eval      efblcd  = e1fblcd
     C                   eval      efrmcd  = e1frmcd
     C                   eval      efhgsn  = e1fhgsn
     C                   eval      efppcd  = e1fppcd
     C                   eval      efptcd  = e1fptcd
     C                   eval      etfscd  = e1tfscd
     C                   eval      etblcd  = e1tblcd
     C                   eval      etrmcd  = e1trmcd
     C                   eval      ethgsn  = e1thgsn
     C                   eval      etppcd  = e1tppcd
     C                   eval      etptcd  = e1tptcd
     C                   eval      ehd     = e1hd
     C                   eval      esplit  = e1split
     C                   eval      etype   = e1type
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Move Iowa fields to universal fields
      *---------------------------------------------------------------
      *
     C     $loadiowa     begsr
      *
     C                   eval      eeditfl = e2editfl
     C                   eval      emvsn   = e2mvsn
     C                   eval      emdy    = e2mdy
     C                   eval      escdt   = e2scdt
     C                   eval      eticd   = e2ticd
     C                   eval      effscd  = e2ffscd
     C                   eval      efblcd  = e2fblcd
     C                   eval      efrmcd  = e2frmcd
     C                   eval      efhgsn  = e2fhgsn
     C                   eval      efppcd  = e2fppcd
     C                   eval      efptcd  = e2fptcd
     C                   eval      etfscd  = e2tfscd
     C                   eval      etblcd  = e2tblcd
     C                   eval      etrmcd  = e2trmcd
     C                   eval      ethgsn  = e2thgsn
     C                   eval      etppcd  = e2tppcd
     C                   eval      etptcd  = e2tptcd
     C                   eval      ehd     = e2hd
     C                   eval      esplit  = e2split
     C                   eval      etype   = e2type
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Clear subfile line
      *---------------------------------------------------------------
      *
     C     $clrln        begsr
      *
     C                   move      *blank        hdsplit
     C                   move      *blank        hdtype
      *
     C                   z-add     0             d1mdy
     C                   z-add     0             hdscdt
     C                   z-add     0             d1schd
      * Origin
     C                   z-add     0             d1orfscd
     C                   move      *blank        d1orhgcd
     C                   move      *blank        d1orblcd
     C                   move      *blank        d1orrmcd
     C                   move      *blank        d1orgtcd
     C                   move      *blank        d1orptcd
     C                   move      *blank        d1orppcd
     C                   move      *blank        hdorfsnm
     C                   z-add     0             hdorhgsn
     C                   move      *blank        hdorgscd
      *
      * Destination
     C                   z-add     0             d1dnfscd
     C                   move      *blank        d1dnhgcd
     C                   move      *blank        d1dngtcd
     C                   move      *blank        d1dnblcd
     C                   move      *blank        d1dnrmcd
     C                   move      *blank        d1dnptcd
     C                   move      *blank        d1dnppcd
     C                   move      *blank        hddnfsnm
     C                   z-add     0             hddnhgsn
     C                   move      *blank        hddngscd
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Rewrite the Subfile records with the indicators off
      *---------------------------------------------------------------
      *
      * This subroutine keeps the Cursor from being in the subfile when
      * the displayed error message is for the control record.
      *
     C     $sfloff       begsr
      *
      * Read/Process the changed subfile records
      *
     C                   dou       *in80 = *on                                  Do process
     C                   readc     hp1062s1                               80
     C                   if        *in80 = *off                                 If rec read
      *
      * Set off all subfile indicators
      *
     C                   exsr      $offin
      * Seton MDT
     C                   seton                                        85
      *
     C                   update    hp1062s1
     C                   endif                                                  If rec read
     C                   enddo                                                  Do process
      *
     C                   endsr
      /EJECT
      *----------------------------------------------------------------
      * Set off all subfile indicators
      *----------------------------------------------------------------
      *
     C     $offin        begsr
      *
     C                   setoff                                       3132
     C                   setoff                                       343536
     C                   setoff                                       373839
     C                   setoff                                       4048
     C                   setoff                                       505152
     C                   setoff                                       545556
     C                   setoff                                       5758
      *
     C                   endsr
      /EJECT
      *----------------------------------------------------------------
      * F4 Prompt for Screen 1
      *----------------------------------------------------------------
      *
     C     $f4s1         begsr
      *
      * Set off all indicators in all subfile records.
      *
     C                   exsr      $sfloff
      *
      *----------------------------------------------------------------
      * Prompted fields in the subfile
      *----------------------------------------------------------------
      *
      *-------------------
      * ORIGIN FARM SITE
      *-------------------
      *
     C                   select
     C                   when      fld = 'D1ORFSCD'
     C                   exsr      $rrn1
     C                   if        *in97 = *off
     C                   call      'HP5040'
     C                   parm      'A'           xxaist
     C                   parm      0             xxfscd
     C                   parm      *blank        xxfsnm
     C                   if        xxfscd <> 0
     C                   z-add     xxfscd        d1orfscd
     C                   movel     xxfsnm        hdorfsnm
     C                   endif
     C                   seton                                        8551
     C                   update    hp1062s1
      *
      * Reset the subfile relative record number to what it was before the user
      * took F4 (It will be out-of-synch if the user has selected a record causing
      * thus causing the subfile record to be retrieved and updated.) But, set the
      * page of the subfile to be displayed to the page on which the user took F4.
      *
     C                   z-add     count         hdrrn1
     C                   z-add     hdrr1         hdpag1
     C                   endif
      *
      *-------------------
      * ORIGIN BUILDING
      *-------------------
      *
     C                   when      fld = 'D1ORBLCD'
     C                   exsr      $rrn1
     C                   if        *in97 = *off
      *
      * Reset the subfile relative record number to what it was before the user
      * took F4 (It will be out-of-synch if the user has selected a record causing
      * thus causing the subfile record to be retrieved and updated.) But, set the
      * page of the subfile to be displayed to the page on which the user took F4.
      *
     C                   call      'HP523'
     C                   parm      d1orfscd      xxfscd
     C                   parm      *blank        xxblcd
     C                   parm      *blank        XXblds
     C                   if        xxblcd <> *blank
     C                   movel     xxblcd        d1orblcd
     C                   endif
     C                   seton                                        8555
     C                   update    hp1062s1
      *
      * Reset the subfile relative record number to what it was before the user
      * took F4 (It will be out-of-synch if the user has selected a record causing
      * thus causing the subfile record to be retrieved and updated.) But, set the
      * page of the subfile to be displayed to the page on which the user took F4.
      *
     C                   z-add     count         hdrrn1
     C                   z-add     hdrr1         hdpag1
     C                   endif
      *
      *-------------------
      * ORIGIN ROOM
      *-------------------
      *
     C                   when      fld = 'D1ORRMCD'
     C                   exsr      $rrn1
     C                   if        *in97 = *off
     C                   call      'HP524'
     C                   parm                    d1orfscd
     C                   parm                    d1orblcd
     C                   parm      *blank        xxrmcd
     C                   if        xxrmcd <> *blank
     C                   movel     xxrmcd        d1orrmcd
     C                   endif
     C                   seton                                        8550
     C                   update    hp1062s1
      *
      * Reset the subfile relative record number to what it was before the user
      * took F4 (It will be out-of-synch if the user has selected a record causing
      * thus causing the subfile record to be retrieved and updated.) But, set the
      * page of the subfile to be displayed to the page on which the user took F4.
      *
     C                   z-add     count         hdrrn1
     C                   z-add     hdrr1         hdpag1
     C                   endif
      *
      *-----------------
      * ORIGIN HOG GROUP
      *-----------------
      *
     C                   when      fld = 'D1ORHGCD'
     C                   exsr      $rrn1
     C                   if        *in97 = *off
     C                   call      'HP5950'
     C                   parm                    d1orfscd
     C                   parm                    hdorfsnm
     C                   parm      d1orblcd      xxblcd
     C                   parm      d1orrmcd      xxrmcd
     C                   parm      *blank        xxhgcd
     C                   parm      *blank        xxgscd
     C                   parm      *blank        xxptcd
     C                   parm      *blank        xxppcd
     C                   if        xxhgcd <> *blank
     C                   movel     xxhgcd        d1orhgcd
     C                   movel     xxblcd        d1orblcd
     C                   movel     xxrmcd        d1orrmcd
     C                   movel     xxgscd        hdorgscd
     C                   endif
     C                   seton                                        8554
     C                   update    hp1062s1
      *
      * Reset the subfile relative record number to what it was before the user
      * took F4 (It will be out-of-synch if the user has selected a record causing
      * thus causing the subfile record to be retrieved and updated.) But, set the
      * page of the subfile to be displayed to the page on which the user took F4.
      *
     C                   z-add     count         hdrrn1
     C                   z-add     hdrr1         hdpag1
     C                   endif
      *
      *----------------------
      * DESTINATION FARM SITE
      *----------------------
      *
     C                   when      fld = 'D1DNFSCD'
     C                   exsr      $rrn1
     C                   if        *in97 = *off
     C                   call      'HP5040'
     C                   parm      'A'           xxaist
     C                   parm      0             xxfscd
     C                   parm      *blank        xxfsnm
     C                   if        xxfscd <> 0
     C                   z-add     xxfscd        d1dnfscd
     C                   movel     xxfsnm        hddnfsnm
     C                   endif
     C                   seton                                        8552
     C                   update    hp1062s1
      *
      * Reset the subfile relative record number to what it was before the user
      * took F4 (It will be out-of-synch if the user has selected a record causing
      * thus causing the subfile record to be retrieved and updated.) But, set the
      * page of the subfile to be displayed to the page on which the user took F4.
      *
     C                   z-add     count         hdrrn1
     C                   z-add     hdrr1         hdpag1
     C                   endif
      *
      *----------------------
      * DESTINATION BUILDING
      *----------------------
      *
     C                   when      fld = 'D1DNBLCD'
     C                   exsr      $rrn1
     C                   if        *in97 = *off
     C                   call      'HP523'
     C                   parm      d1dnfscd      xxfscd
     C                   parm      *blank        xxblcd
     C                   parm      *blank        xxblds
     C                   if        xxblcd <> *blank
     C                   movel     xxblcd        d1dnblcd
     C                   endif
     C                   seton                                        8557
     C                   update    hp1062s1
      *
      * Reset the subfile relative record number to what it was before the user
      * took F4 (It will be out-of-synch if the user has selected a record causing
      * thus causing the subfile record to be retrieved and updated.) But, set the
      * page of the subfile to be displayed to the page on which the user took F4.
      *
     C                   z-add     count         hdrrn1
     C                   z-add     hdrr1         hdpag1
     C                   endif
      *
      *-----------------------
      * DESTINATION ROOM
      *-----------------------
      *
     C                   when      fld = 'D1DNRMCD'
     C                   exsr      $rrn1
     C                   if        *in97 = *off
     C                   call      'HP524'
     C                   parm                    d1dnfscd
     C                   parm                    d1dnblcd
     C                   parm      *blank        xxrmcd
     C                   if        xxrmcd <> *blank
     C                   movel     xxrmcd        d1dnrmcd
     C                   endif
     C                   seton                                        8558
     C                   update    hp1062s1
      *
      * Reset the subfile relative record number to what it was before the user
      * took F4 (It will be out-of-synch if the user has selected a record causing
      * thus causing the subfile record to be retrieved and updated.) But, set the
      * page of the subfile to be displayed to the page on which the user took F4.
      *
     C                   z-add     count         hdrrn1
     C                   z-add     hdrr1         hdpag1
     C                   endif
      *
      *----------------------
      * DESTINATION HOG GROUP
      *----------------------
      *
     C                   when      fld = 'D1DNHGCD'
     C                   exsr      $rrn1
     C                   if        *in97 = *off
     C                   call      'HP598'
     C                   parm                    d1dnfscd
     C                   parm                    hddnfsnm
     C                   parm      d1dnblcd      xxblcd
     C                   parm      d1dnrmcd      xxrmcd
     C                   parm      *blank        xxhgcd
     C                   parm      *blank        xxgscd
     C                   parm      *blank        xxptcd
     C                   parm      *blank        xxppcd
     C                   if        xxhgcd <> *blank
     C                   movel     xxhgcd        d1dnhgcd
     C                   movel     xxblcd        d1dnblcd
     C                   movel     xxrmcd        d1dnrmcd
     C                   movel     xxgscd        hddngscd
     C                   endif
     C                   seton                                        8556
     C                   update    HP1062s1
      *
      * Reset the subfile relative record number to what it was before the user
      * took F4 (It will be out-of-synch if the user has selected a record causing
      * thus causing the subfile record to be retrieved and updated.) But, set the
      * page of the subfile to be displayed to the page on which the user took F4.
      *
     C                   z-add     count         hdrrn1
     C                   z-add     hdrr1         hdpag1
     C                   endif
     C                   other
      *
      * F4 not valid for this field; so, issue message
      *
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS09011'     mgid(e)
     C                   endif
     C                   endsl
      *
     C                   endsr
      /EJECT
      *----------------------------------------------------------------------------
      * Set the subfile to redisplay at the first screen that has errors
      *----------------------------------------------------------------------------
      *
     C     $err1         begsr
      *
     C                   if        error = 1
     C                   z-add     hdrrn1        hdpag1
     C                   endif
      *
     C                   endsr
      *
      /eject
      *--------------------------------------------------------------------------------------
      * User took F9 to accept his entries
      *--------------------------------------------------------------------------------------
      *
      * When the user takes F9, we will:
      *  1) delete the records in the Editing file
      *  2) write all subfile records to the editing file
      *
     C     $accept       begsr
      *
      * Delete records
      *
     C                   if        sdbobo = 'OKLIV'
      * Delete Guymon records
     C     *start        setll     hsp2062
     C                   dou       *in92 = *on
     C                   read      hsp2062                                92
     C                   if        *in92 = *off
     C                   delete    e1rec
     C                   endif                                                  if *in92 = *off
     C                   enddo                                                  dou *in92 = *on
      *
     C                   else
      * Delete Iowa records
     C     *start        setll     hsp2064
     C                   dou       *in92 = *on
     C                   read      hsp2064                                92
     C                   if        *in92 = *off
     C                   delete    e2rec
     C                   endif                                                  if *in92 = *off
     C                   enddo                                                  dou *in92 = *on
     C                   endif                                                  if sdbobo = 'OKLIV'
      *
      * Write records
     C                   dou       *in80 = *on                                  Do loop
     C                   readc     hp1062s1                               80
     C                   if        *in80 = *off and d1schd <> 0                 If read
      *
     C                   if        sdbobo = 'OKLIV'
      * write to Guymon records
     C                   move      yes           e1editfl
     C                   move      'SBD  '       e1ticd
     C                   move      hdsplit       e1split
     C                   move      hdtype        e1type
     C                   z-add     hdscdt        e1scdt
      *
     C                   z-add     d1mdy         e1mdy
     C                   z-add     d1mvsn        e1mvsn
     C                   z-add     d1schd        e1hd
     C                   z-add     d1orfscd      e1ffscd
     C                   move      d1orblcd      e1fblcd
     C                   move      d1orrmcd      e1frmcd
     C                   z-add     hdorhgsn      e1fhgsn
     C                   move      d1orppcd      e1fppcd
     C                   move      d1orptcd      e1fptcd
      *
     C                   z-add     d1dnfscd      e1tfscd
     C                   move      d1dnblcd      e1tblcd
     C                   move      d1dnrmcd      e1trmcd
     C                   z-add     hddnhgsn      e1thgsn
     C                   move      d1dnppcd      e1tppcd
     C                   move      d1dnptcd      e1tptcd
      *
     C                   write     e1rec

     C                   else
      * write to Iowa records
     C                   move      yes           e2editfl
     C                   move      'UNK  '       e2ticd
     C                   move      hdsplit       e2split
     C                   move      hdtype        e2type
     C                   z-add     hdscdt        e2scdt
      *
     C                   z-add     d1mdy         e2mdy
     C                   z-add     d1mvsn        e2mvsn
     C                   z-add     d1schd        e2hd
     C                   z-add     d1orfscd      e2ffscd
     C                   move      d1orblcd      e2fblcd
     C                   move      d1orrmcd      e2frmcd
     C                   z-add     hdorhgsn      e2fhgsn
     C                   move      d1orppcd      e2fppcd
     C                   move      d1orptcd      e2fptcd
      *
     C                   z-add     d1dnfscd      e2tfscd
     C                   move      d1dnblcd      e2tblcd
     C                   move      d1dnrmcd      e2trmcd
     C                   z-add     hddnhgsn      e2thgsn
     C                   move      d1dnppcd      e2tppcd
     C                   move      d1dnptcd      e2tptcd
      *
     C                   write     e2rec
     C                   endif                                                  if sdbobo = 'OKLIV'
     C                   endif                                                  If read
     C                   enddo                                                  Do loop
      *
      * Set next routine
     C                   move      exit          routne
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Set on LR and end job
      *---------------------------------------------------------------
      *
     C     $exit         begsr
      *
     C                   seton                                        lr
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Write error messages
      *---------------------------------------------------------------
      *
     C     $wrmsg        begsr
      *
     C                   call      'UT80045J'                           43
     C                   parm                    mgid
     C                   parm                    mgdt
     C                   parm                    error
     C                   parm      sdpgm         pgm
     C                   parm      msgfil        msgfl
      *
      * If call to UT80045J failed, a message hardcoded in the DSPF Command Key
      * Record will be set on. So, redisplay screen to user.
      *
     C                   if        *in43 = *on
     C                   eval      routne = set1
     C                   endif
      *
     C                   endsr
      *
      *---------------------------------------------------------------
      * Clear Messages
      *---------------------------------------------------------------
      *
     C     $clmsg        begsr
      *
     C                   call      'UT80045J'                           43
     C                   parm      *blank        mgi
     C                   parm      *blank        mgd
     C                   parm      *zero         error
     C                   parm      sdpgm         pgm
     C                   parm      msgfil        msgfl
      *
      * If call to UT80045J failed, a message hardcoded in the DSPF Command Key
      * Record will be set on. So, redisplay screen to user.
      *
     C                   if        *in43 = *on
     C                   eval      routne = set1
     C                   endif
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------------------------------
      * Find/Retrieve the subfile record to update when returning from a Select Program
      *---------------------------------------------------------------------------------------
      *
      * HDRR1 is the hidden field associated with the Keyword SFLCSRRRN on the Control Record
      * HDRRN1 is the value associated with the Keyword 'KSFILE' in the F-Specs for the Workstation.
      *
     C     $rrn1         begsr
      *
     C                   setoff                                       97
      *
     C                   if        hdrr1 <> 0
     C                   z-add     hdrr1         hdrrn1
     C     hdrrn1        chain     hp1062s1                           97
     C                   endif
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Initialization
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Initally call the Business Office selection screen
     C                   call      'HP503'
     C                   parm      *blanks       sdbobo
     C                   parm      *blanks       sdbods
     C                   parm      'LIVE '       sdbodv
      *
      * If user does not select a business office, exist program
     C                   if        sdbobo = *blanks
     C                   exsr      $exit
     C                   endif
      *
      * Key lists
      *
     C     key01         klist
     C                   kfld                    d1orfscd
     C                   kfld                    d1orblcd
      *
     C     key02         klist
     C                   kfld                    d1orfscd
     C                   kfld                    d1orblcd
     C                   kfld                    d1orrmcd
      *
     C     key03         klist
     C                   kfld                    d1dnfscd
     C                   kfld                    d1dnblcd
      *
     C     key04         klist
     C                   kfld                    d1dnfscd
     C                   kfld                    d1dnblcd
     C                   kfld                    d1dnrmcd
      *
      *
      * You are always in REVISE mode in this porgram.
      *
     C                   movel     'REVISE'      d1MODE
      *
      * To support F5-Refresh, logic requires the number of lines on
      * the subfile page and also the number of lines less 1.  By
      * setting these values up as constant work fields, the processing
      * logic can be standardized between programs.
      *
     C                   z-add     14            wkplin
     C     wkplin        sub       1             wkrlin
      *
      *
      * The following standard code must be included to make the
      * standard error message handling program function properly.
      * This code sets and clears the program message queue.
      *
     C                   movel     '*'           msgpgm
     C                   exsr      $clmsg
      *
     C                   endsr
      /EJECT
