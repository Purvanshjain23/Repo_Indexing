/***************************************************************** */
/*                                                                 */
/*  PROGRAM NUMBER --> HP454CL                                     */
/*  PROGRAM NAME ----> SPECIFY RATION REPORT OPTIONS               */
/*  PROGRAMMER ------> LEANNE FEDOR                                */
/*  DATE ------------> 08/14/01                                    */
/*                                                                 */
/*  PROGRAM PURPOSE -> THIS CL CALLS THE PROGRAMS TO PRINT         */
/*                     FEED TICKET RATION INFO SUMMARIZED TO THE   */
/*                     RATION/MILL LEVEL.                          */
/*                                                                 */
/*                     USER SELECTIONS ARE PASSED THRU THE LDA.    */
/*******************************************************************/
/*  MODIFICATIONS:                                                 */
/*                                                                 */
/* 05/31/07 LEANNE RAMSEY                                          */
/*          REPLACED QPRINT WITH PRINT198. REPLACED OLD CUSTOMIZED */
/*          PRINT OVERRIDE COMMAND WITH NEW STANDARD COMMAND.      */
/*******************************************************************/

             PGM

             DCL        VAR(&QDATA) TYPE(*CHAR) LEN(210)

             DCL        VAR(&LDFMBO) TYPE(*CHAR) LEN(5)
             DCL        VAR(&LDFYMD) TYPE(*DEC) LEN(8)
             DCL        VAR(&LDTYMD) TYPE(*DEC) LEN(8)
             DCL        VAR(&LDPPCD) TYPE(*CHAR) LEN(5)
             DCL        VAR(&LDFSCD) TYPE(*DEC) LEN(5)
             DCL        VAR(&LDRNCD) TYPE(*CHAR) LEN(5)
             DCL        VAR(&LDHGSN) TYPE(*DEC) LEN(7)
             DCL        VAR(&LDTRCD) TYPE(*CHAR) LEN(1)

             DCL        VAR(&HOLD) TYPE(*CHAR) LEN(10)
             DCL        VAR(&COPYS) TYPE(*DEC) LEN(2)
             DCL        VAR(&OUTQ) TYPE(*CHAR) LEN(10)

             CHGVAR     VAR(&LDFMBO) VALUE(%SST(*LDA 1 5))
             CHGVAR     VAR(&LDFYMD) VALUE(%SST(*LDA 6 8))
             CHGVAR     VAR(&LDTYMD) VALUE(%SST(*LDA 14 8))
             CHGVAR     VAR(&LDPPCD) VALUE(%SST(*LDA 27 5))
             CHGVAR     VAR(&LDFSCD) VALUE(%SST(*LDA 32 5))
             CHGVAR     VAR(&LDRNCD) VALUE(%SST(*LDA 37 5))
             CHGVAR     VAR(&LDHGSN) VALUE(%SST(*LDA 42 7))
             CHGVAR     VAR(&LDTRCD) VALUE(%SST(*LDA 49 1))


/*------------------------------------------------------------------*/
/* SET PRINTER OVERRIDES                                            */
/*------------------------------------------------------------------*/

 HOLD:       IF         COND(%SST(*LDA 411 1) = 'Y') THEN(CHGVAR +
                          VAR(&HOLD) VALUE('*YES      '))
             ELSE       CMD(CHGVAR VAR(&HOLD) VALUE('*NO       '))

 COPYS:      CHGVAR     VAR(&COPYS) VALUE(%SST(*LDA 412 1))
             IF         COND(&COPYS = 0) THEN(CHGVAR VAR(&COPYS) +
                          VALUE(1))

 OUTQ:       CHGVAR     VAR(&OUTQ) VALUE(%SST(*LDA 413 10))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(%SST(*LDA 401 10)))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE('*JOB      '))


/*******************************************************************/
/* SET UP THE OPEN QUERY                                            */
/*******************************************************************/

             CHGVAR     VAR(&QDATA) VALUE(' ')

/*  ALWAYS SELECT FEED TICKET DETAIL FOR DATE RANGE SPECIFIED    */

             CHGVAR     VAR(%SST(&QDATA 1 10)) VALUE('FDTKDT *GE')
             CHGVAR     VAR(%SST(&QDATA 12 8)) VALUE(&LDFYMD)
             CHGVAR     VAR(%SST(&QDATA 21 15)) VALUE('*AND FDTKDT +
                          *LE')
             CHGVAR     VAR(%SST(&QDATA 37 8)) VALUE(&LDTYMD)

/*  IF BUSINESS OFFICE IS NOT BLANK                              */

             IF         COND(&LDFMBO *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 46 17)) VALUE('*AND FDFMBO +
                          *EQ "')
             CHGVAR     VAR(%SST(&QDATA 63 5)) VALUE(&LDFMBO)
             CHGVAR     VAR(%SST(&QDATA 68 1)) VALUE('"')
             ENDDO


/*  IF FARM SITE IS NOT A BLANK                                  */

             IF         COND(&LDFSCD *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 118 15)) VALUE('*AND FDFSCD +
                          *EQ')
             CHGVAR     VAR(%SST(&QDATA 134 5)) VALUE(&LDFSCD)
             ENDDO

/*  IF RATION CODE IS NOT A BLANK                                */

             IF         COND(&LDRNCD *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 142 17)) VALUE('*AND FDRNCD +
                          *EQ "')
             CHGVAR     VAR(%SST(&QDATA 159 5)) VALUE(&LDRNCD)
             CHGVAR     VAR(%SST(&QDATA 164 1)) VALUE('"')
             ENDDO

/*  IF HOG GROUP IS NOT A BLANK,                                  */

             IF         COND(&LDHGSN *NE 0000000) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 166 15)) VALUE('*AND FDHGSN +
                          *EQ')
             CHGVAR     VAR(%SST(&QDATA 182 7)) VALUE(&LDHGSN)
             ENDDO

/*  IF TRANSACTION CODE IS NOT BLANK                              */

             IF         COND(&LDTRCD *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 190 17)) VALUE('*AND FDTRCD +
                          *EQ "')
             CHGVAR     VAR(%SST(&QDATA 207 1)) VALUE(&LDTRCD)
             CHGVAR     VAR(%SST(&QDATA 208 1)) VALUE('"')
             ENDDO


/*------------------------------------------------------------------*/
/*  OPEN QUERY FILE                                                 */
/*------------------------------------------------------------------*/

             OVRDBF     FILE(HSP038) SHARE(*YES)
             OPNQRYF    FILE((HSP038)) QRYSLT(&QDATA) +
                          KEYFLD((FDFMBO) (FDRNCD) (FDADCD)) +
                          SEQONLY(*YES)

/*------------------------------------------------------------------*/
/* OVERRIDE PRINT FILE                                              */
/*------------------------------------------------------------------*/

             OVRPRTF    FILE(PRINT132) OUTQ(&OUTQ) COPIES(&COPYS) +
                          HOLD(&HOLD)

/*------------------------------------------------------------------*/
/*  RUN THE REPORT                                                  */
/*------------------------------------------------------------------*/

             CALL       PGM(HP654)

             CLOF       OPNID(HSP038)
             DLTOVR     FILE(*ALL)

             ENDPGM

