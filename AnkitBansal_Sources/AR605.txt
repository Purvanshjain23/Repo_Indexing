      *****************  RPG PROGRAM HEADING  ************************
      * ENVIRONMENT: PORK
      * SYSTEM:      ACCOUNTS RECEIVABLE
      * PROGRAM:     AR605
      * TITLE:       CUSTOMER STATEMENT
      * PROGRAMMER:  LEANNE FEDOR
      * CREATED:     03/12/98
      *
      *  FUNCTION:   THIS PROGRAM PRINTS A CUSTTOMER STATEMENT REPORT.
      *
      *              AN OPEN QUERY STATEMENT IS USED TO SELECT AND
      *              KEY DATA IN AR605CLP.
      *
      *              THIS PROGRAM IS SUBMITTED FROM AN A/R SYNON FUNCTION.
      *********************************************************************
      * MODIFICATIONS:
      *********************************************************************
      * DATE      PROGRAMMER
      *
      * 11/17/98  LEANNE FEDOR
      *           RECOMPILE ONLY. SYNON FILE WAS CHANGED.
      *
      * 02/23/99  LEANNE FEDOR
      *           RECOMPILE ONLY. DATABASE CHANGES TO:
      *                OPBFCPP--PD ORDER HEADER
      *                OPBGWKP--PD ORDER DETAIL
      *                OMHSTPP--OM HISTORY
      *
      * 08/08/05  Barb Gutierrez
      *           Changed hard coded company name from Seaboard Farms to
      *           Seaboard Foods.
      *
      * 08/26/05  Sharon Zurn
      *           Change program to allow printing of all companies for
      *           a customer.
      *           -Print company number on the statement if all co
      *            selected.
      *           -Added customer and print co parms.
      * 01/15/08  Sharon Zurn
      *           Add code to retreive the address to print at the top of
      *           the form instead of hardcoding. Dailys will now be using
      *           the statements too.
      * 05/16/08  Barb Gutierrez
      *           Recompile only due to new HPB environment.
      *
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     FARBDCPL6  IF   E           K DISK
      *  A/R Header
      *  (RECORDS SELECTED IN OPEN QUERY)
      *
     FCABBREL1  IF   E           K DISK
      *  CUSTOMER
      *
     FOMHSTPLP  IF   E           K DISK
      *  ORDER HISTORY
      *
     FCAABREL1  IF   E           K DISK
      *  COMPANY ADDRESS
      *
     FQPRINT    O    F   84        PRINTER OFLIND(*INOF)
      *
      /EJECT
      ****************************************************************
      * INPUT SPECS
      ****************************************************************
      *
      *---------------------------------------------------------------
      * STANDARD PROGRAM STATUS DATA STRUCTURE
      *---------------------------------------------------------------
      *    EXTERNALLY DEFINED AS UTPGFR (RECORD FORMAT: PGMDSR)
     D PGMDS         ESDS                  EXTNAME(UTPGFR)
      *
      *---------------------------------------------------------------
      *  NAMED CONSTANTS
      *---------------------------------------------------------------
      *
     D YES             C                   CONST('Y')
     D NO              C                   CONST('N')
      *
      *
      *---------------------------------------------------------------
      * FORMATS FOR FLIPPING DATES
      *---------------------------------------------------------------
      *
      * DATE IN CCYYMMDD FORMAT
      *
     D                 DS
     D  WKCYMD                 1      8  0
     D  WKCEN                  1      2  0
     D  WKYER                  3      4  0
     D  WKMON                  5      6  0
     D  WKDAY                  7      8  0
      *
      *
      * DATE IN CYYMMDD FORMAT USED IN THE OMS SYSTEM
      *
     D                 DS
     D  WKOMDT                 1      7  0
     D  OMSC                   1      1  0
     D  OMSYY                  2      3  0
     D  OMSMM                  4      5  0
     D  OMSDD                  6      7  0
      *
      *
     D  DUMMYD                 1      1
      *  THE PRECEDING DUMMY DATA STRUCTURE IS ONLY TO KEEP THE I-SPEC
      *  INFO FROM RUNNING OVER INTO THE 'CALC SPECS' ON THE COMPILE
      *  LISTING.  IT SERVES NO OTHER PURPOSE AND CAN BE DELETED
      *  AT ANY TIME!!!
      /EJECT
      ****************************************************************
      * MAINLINE
      ****************************************************************
      *
      * READ/PROCESS THE A/R HEADER FILE. INITIAL RECORD SELECTIONS HAVE
      * BEEN MADE THROUGH AN OPEN QUERY IN THE CL. YOU STILL NEED TO
      * MAKE THE COMPARISON FOR THE USER'S SELECTION/OMISSION OF
      * NEGATIVE BALANCES.
      *
     C     *LOVAL        SETLL     ARBDCPL6
      *
     C     *IN90         DOUEQ     *ON                                          MAIN DO
     C                   READ      ARBDCPL6                               90
     C     *IN90         IFEQ      *OFF                                         IF NOT EOF
      *
     C                   MOVEL     YES           PROCFL
      *
      * FLIP SIGN ON 'CREDIT MEMO' AND 'ON ACCOUNT' RECORDS
      *
     C                   SELECT
     C     BDCEST        WHENEQ    'CM'
     C     BDCEST        OREQ      'OA'
     C     0             SUB       BDBSVA        DTBSVA
     C     0             SUB       BDBGVA        DTBGVA
     C                   OTHER
      *
     C                   Z-ADD     BDBSVA        DTBSVA
     C                   Z-ADD     BDBGVA        DTBGVA
     C                   ENDSL
      *
      * IF THE REMAINING BALANCE DUE IS NEGATIVE AND THE USER HAS
      * ELECTED TO OMIT THE 'NEGATIVE BALANCES', THEN DO NOT PROCESS
      * THIS RECORD.
      *
     C     DTBGVA        IFLT      0
     C     XXCRED        ANDEQ     NO
     C                   MOVEL     NO            PROCFL
     C                   ENDIF
      *
     C     PROCFL        IFEQ      YES                                          IF PROCESS
      *
      * IF FIRST TIME, RETRIEVE CUSTOMER INFO AND PRINT HEADINGS.
      *
     C     FIRST         IFEQ      YES
     C                   EXSR      $CUST
     C                   EXSR      $Comp
     C                   EXCEPT    HDRRPT
     C                   EXCEPT    HDRADD
     C                   EXCEPT    HDRCOL
     C                   MOVEL     NO            FIRST
     C                   ENDIF
      *
      * IF OVERFLOW, PRINT COLUMN HEADINGS
      *
     C     *INOF         IFEQ      *ON                                          IF OVERFLOW
     C                   EXCEPT    HDRRPT
     C                   EXCEPT    HDRCOL
     C                   ENDIF                                                  IF OVERFLOW
      *
      *
      * RETRIEVE CUSTOMER PURCHASE ORDER NUMBER
      *
     C     KEY01         CHAIN     OMHSTPLP                           92
     C     *IN92         IFEQ      *OFF                                         IF HIT
     C                   MOVEL(P)  F4BCNA        DTBCNA
     C                   ELSE
     C                   MOVE      *BLANK        DTBCNA
     C                   ENDIF                                                  IF HIT
      *
      * FLIP DUE DATE TO MMDDYY FORMAT
      *
     C                   Z-ADD     BDATDT        WKOMDT
     C                   EXSR      $ADDCC
     C                   Z-ADD     WKCYMD        PFRM8
     C                   Z-ADD     0             PTO8
     C                   MOVEL     'CYMD'        PFRFMT
     C                   MOVEL     'MDY '        PTOFMT
     C                   MOVE      *BLANK        PCODE
     C                   Z-ADD     0             PDAYS
     C                   EXSR      $DATE
     C     PRTRN         IFEQ      *BLANK
     C                   Z-ADD     PTO8          DTDMDY
     C                   ELSE
     C                   Z-ADD     0             DTDMDY
     C                   ENDIF
      *
      *
      * ACCUMULATE:
      *    1) TRANSACTION AMOUNT
      *    2) REMAINING BALANCE DUE
      *
     C                   ADD       DTBSVA        TOTAMT
     C                   ADD       DTBGVA        TOTDUE
      *
     C                   EXCEPT    DTLINE
      *
     C                   ENDIF                                                  IF PROCESS
     C                   ENDIF                                                  IF NOT EOF
     C                   ENDDO                                                  MAIN DO
      *
      * PRINT TOTALS
      *
     C     FIRST         IFEQ      NO
     C                   EXCEPT    TOTRPT
     C                   ENDIF
      *
      *----------------------------------------------------------------
      * EOF PROCESSING
      *----------------------------------------------------------------
      *  IF NO DETAIL RECORDS MATCHED THE SELECTION CRITERIA, THEN
      *  PRINT THE STANDARD 'NO DATA...' ERROR MESSAGE
      *
     C     FIRST         IFEQ      YES
     C                   EXCEPT    HDRRPT
     C                   EXCEPT    HDRADD
     C                   EXCEPT    NODATA
     C                   ENDIF
      *
     C                   SETON                                        LR
      /EJECT
      *----------------------------------------------------------------
      * $DATE -  MANIPULATE DATES WITH DATE UTILITY
      *----------------------------------------------------------------
      *
     C     $DATE         BEGSR
      *
     C                   MOVE      *BLANK        PRTRN
     C                   CALL      'UT80060R'
     C                   PARM                    PFRM8             8 0
     C                   PARM                    PTO8              8 0
     C                   PARM                    PFRFMT            4
     C                   PARM                    PTOFMT            4
     C                   PARM                    PDAYS             4 0
     C                   PARM                    PCODE             1
     C                   PARM                    PRTRN             1
      *
     C                   ENDSR
      /EJECT
      *------------------------------------------------------------------
      * $ADDCC - ADD CENTURY TO A SYNON DATE
      *------------------------------------------------------------------
      *
     C     $ADDCC        BEGSR
      *
      * SYNON HOLDS DATES AS A 7,0 NUMERIC FIELD WHERE THE
      * LEADING DIGIT IS 0 OR 1.
      *             0 = 19TH CENTURY
      *             1 = 20TH CENTURY
      *
      * I WANT TO USE THE STANDARD DATE ROUTINE WHICH USES AN 8,0
      * FORMAT. SO, TURN THE 7,0 DATE FIELD INTO AN 8,0 DATE.
      *
     C                   Z-ADD     0             WKCYMD
      *
     C                   SELECT
     C     OMSC          WHENEQ    0
     C                   Z-ADD     19            WKCEN
     C     OMSC          WHENEQ    1
     C                   Z-ADD     20            WKCEN
     C                   ENDSL
      *
     C                   Z-ADD     OMSYY         WKYER
     C                   Z-ADD     OMSMM         WKMON
     C                   Z-ADD     OMSDD         WKDAY
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * $CUST - RETRIEVE CUSTOMER ADDRESS, ETC. FOR PRINTING
      *---------------------------------------------------------------
      *
     C     $CUST         BEGSR
      *
     C                   MOVE      *BLANK        WKADD1
     C                   MOVE      *BLANK        WKADD2
     C                   MOVE      *BLANK        WKADD3
     C                   MOVE      *BLANK        WKADD4
     C                   MOVE      *BLANK        WKCITY
     C                   MOVE      *BLANK        WKZIP
      *
     C     BDANC7        CHAIN     CABBREL1                           92
     C     *IN92         IFEQ      *OFF                                         IF CUST HIT
      * ADDRESS 1
     C                   MOVEL(P)  BBACNA        WKADD1
      * ADDRESS 2
     C                   MOVEL(P)  BBADNA        WKADD2
      * ADDRESS 3
     C                   MOVEL(P)  BBAENA        WKADD3
      * CITY
     C                   MOVEL     BBAJNA        WKAJNA
      * STATE
     C                   MOVEL     BBBGCD        WKBGCD
      * ZIP
     C                   MOVEL     BBBTTX        WKBTTX
      * COUNTRY
     C                   MOVEL     BBKXCD        WKKXCD
      *
      *
      * CONCATENATE CITY/STATE
      *
     C                   EXSR      $CITY
      *
      *
      * CONCATENATE CITY/STATE AND ZIP CODE
      * (CHECK ZIP CODE FOR A PERIOD BEFORE CONCATENATING)
      *
     C                   MOVEL     BBBTTX        WKFLD
     C                   SELECT
     C     WKFLD         WHENEQ    '.'
     C                   MOVEL     WKCITY        WKZIP
     C                   OTHER
     C     WKCITY        CAT       BBBTTX:1      WKZIP
     C                   ENDSL
      *
      * CONCATENATE CITY/STATE/ZIP AND COUNTRY
      *
     C     WKZIP         CAT       BBKXCD:1      WKADD4
     C                   ENDIF                                                  IF HIT
      *
      * FINALIZE THE FOUR ADDRESS LINES
      *
     C                   EXSR      $ADDR
      *
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * $COMP - RETRIEVE COMPANY ADDRESS, ETC. FOR PRINTING
      *---------------------------------------------------------------
      *
     C     $COMP         BEGSR
      *
     C                   MOVE      *BLANK        wkcadd
     C                   MOVE      *BLANK        wkccity
      *If the company passed in is zero, then retreive the corporate
      *address.
     C                   if        xxaic3 = *zeros
     C                   z-add     300           kyaic3
     C                   else
     C                   z-add     xxaic3        kyaic3
     C                   endif
      *
     C     kyaic3        CHAIN     CAABREL1                           92
     C     *IN92         IFEQ      *OFF                                         IF CUST HIT
      * ADDRESS 1
     C                   MOVEL(P)  ABACNA        WKCADD
      * CITY
     C                   MOVEL     ABAJNA        WKAJNA
      * STATE
     C                   MOVEL     ABBGCD        WKBGCD
      * ZIP
     C                   MOVEL     ABBTTX        WKBTTX
      *
      * CONCATENATE CITY/STATE
      *
     C     ABAJNA        CAT(P)    ',':0         wkccity
     C                   CAT       ABBGCD:1      wkccity
      *
      * CONCATENATE CITY/STATE AND ZIP CODE
      *
     C     wkccity       CAT       ABBTTX:1      wkcadd
      *
     C                   ENDIF                                                  IF HIT
      *
     C                   ENDSR
      /EJECT
      *-----------------------------------------------------------------
      * $CITY - CONCATENATE CITY/STATE TOGETHER
      *-----------------------------------------------------------------
      *
     C     $CITY         BEGSR
      *
     C                   MOVE      *BLANK        WKCITY
      * NOTES:
      *   1) FOREIGN CUSTOMERS DO NOT HAVE STATE; THE STATE
      *      FIELD WILL HOLD A '.'  SO, DO NOT CONCATENATE
      *      WHEN STATE IS '.')
      *   2) SOMETIMES CITY IS ALSO A '.'  SO, DO NOT CONCATENATE
      *      WHEN CITY IS '.')
      *
     C                   MOVEL     BBAJNA        WKAJNA
     C                   MOVEL     BBBGCD        WKBGCD
      *
      * CHECK FOR STATE
      *
     C     BBBGCD        IFEQ      '. '
     C     BBBGCD        OREQ      ' .'
     C     BBBGCD        OREQ      *BLANK
     C                   MOVEL     NO            STFL
     C                   ELSE
     C                   MOVEL     YES           STFL
     C                   ENDIF
      *
      * CHECK FOR CITY
      *
     C                   MOVEL     BBAJNA        WKFLD
     C     BBAJNA        IFEQ      *BLANK
     C     WKFLD         OREQ      '.'
     C                   MOVEL     NO            CITYFL
     C                   ELSE
     C                   MOVEL     YES           CITYFL
     C                   ENDIF
      *
      *
     C                   SELECT
      *
      * NO CITY, NO STATE
      *
     C     CITYFL        WHENEQ    NO
     C     STFL          ANDEQ     NO
      *
      *
      * CITY AND STATE
      *
     C     CITYFL        WHENEQ    YES
     C     STFL          ANDEQ     YES
     C     BBAJNA        CAT(P)    ',':0         WKCITY
     C                   CAT       BBBGCD:1      WKCITY
      *
      *
      * CITY WITHOUT STATE
      *
     C     CITYFL        WHENEQ    YES
     C     STFL          ANDEQ     NO
     C                   MOVEL     BBAJNA        WKCITY
      *
      *
      * STATE WITHOUT CITY
      *
     C     CITYFL        WHENEQ    NO
     C     STFL          ANDEQ     YES
     C                   MOVEL     BBBGCD        WKCITY
     C                   ENDSL
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $ADDR - FINALIZE 4 LINES OF ADDRESS
      *----------------------------------------------------------------
      *
     C     $ADDR         BEGSR
      *
     C     WKADD1        IFNE      *BLANK
     C                   MOVEL(P)  WKADD1        HDADD1
     C                   ENDIF
      *
      *
     C     WKADD2        IFNE      *BLANK
     C                   SELECT
     C     WKADD1        WHENEQ    *BLANK
     C                   MOVEL(P)  WKADD2        HDADD1
     C                   OTHER
     C                   MOVEL(P)  WKADD2        HDADD2
     C                   ENDSL
     C                   ENDIF
      *
      *
     C     WKADD3        IFNE      *BLANK
     C                   SELECT
     C     WKADD1        WHENEQ    *BLANK
     C                   MOVEL(P)  WKADD3        HDADD1
     C     WKADD2        WHENEQ    *BLANK
     C                   MOVEL(P)  WKADD3        HDADD2
     C                   OTHER
     C                   MOVEL(P)  WKADD3        HDADD3
     C                   ENDSL
     C                   ENDIF
      *
     C     WKADD4        IFNE      *BLANK
     C                   SELECT
     C     HDADD1        WHENEQ    *BLANK
     C                   MOVEL(P)  WKADD4        HDADD1
     C     HDADD2        WHENEQ    *BLANK
     C                   MOVEL(P)  WKADD4        HDADD2
     C     HDADD3        WHENEQ    *BLANK
     C                   MOVEL(P)  WKADD4        HDADD3
     C                   OTHER
     C                   MOVEL(P)  WKADD4        HDADD4
     C                   ENDSL
     C                   ENDIF
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * *INZSR - INITIALIZATION SUBROUTINE
      *---------------------------------------------------------------
      *
     C     *INZSR        BEGSR
      *
      * PARMS
     C     *ENTRY        PLIST
     C                   PARM                    XXANC7
     C                   PARM                    XXAYTX
     C                   PARM                    XXCRED            1
      *
      *b:sz080526
     C                   PARM                    XXinprt           1
     C                   PARM                    XXaic3            3 0
      *e:sz080526
      *
     C     *LIKE         DEFINE    BDANC7        XXANC7
     C     *LIKE         DEFINE    BBAYTX        XXAYTX
      *
      * KEY LISTS
      *
     C     KEY01         KLIST
     C                   KFLD                    BDANC7
     C                   KFLD                    BDAIC3
     C                   KFLD                    BDCONB
     C                   KFLD                    BDCEST
      *
      * DEFINE PRINT FIELDS FOR DETAIL LINE
      *
     C                   Z-ADD     0             DTDMDY            6 0
     C     *LIKE         DEFINE    F4BCNA        DTBCNA
     C     *LIKE         DEFINE    BDBSVA        DTBSVA
     C     *LIKE         DEFINE    BDBGVA        DTBGVA
     C     *like         define    xxaic3        kyaic3
      *
      * DEFINE PRINT FIELDS FOR TOTAL LINE
      *
     C     *LIKE         DEFINE    BDBSVA        TOTAMT
     C     *LIKE         DEFINE    BDBGVA        TOTDUE
      *
      * DEFINE PRINT FIELDS FOR HEADINGS
      *
     C                   MOVE      *BLANK        HDADD1           45
     C                   MOVE      *BLANK        HDADD2           45
     C                   MOVE      *BLANK        HDADD3           45
     C                   MOVE      *BLANK        HDADD4           45
      *
      *
      * CONTROL FIELDS
      *
     C                   MOVE      *BLANK        PROCFL            1
     C                   MOVE      *BLANK        WKFLD             1
     C                   MOVE      *BLANK        CITYFL            1
     C                   MOVE      *BLANK        STFL              1
      * WORKFIELDS
      *
     C                   MOVE      *BLANK        WKCCITY          24
     C                   MOVE      *BLANK        WKCADD           45
     C                   MOVE      *BLANK        WKCITY           24
     C                   MOVE      *BLANK        WKZIP            36
     C                   MOVE      *BLANK        WKADD1           45
     C                   MOVE      *BLANK        WKADD2           45
     C                   MOVE      *BLANK        WKADD3           45
     C                   MOVE      *BLANK        WKADD4           45
     C     *LIKE         DEFINE    BBAJNA        WKAJNA
     C     *LIKE         DEFINE    BBBGCD        WKBGCD
     C     *LIKE         DEFINE    BBBTTX        WKBTTX
     C     *LIKE         DEFINE    BBKXCD        WKKXCD
      *
      *b:sz080526
     C                   if        xxinprt  = yes
     C                   seton                                        70
     C                   endif
      *e:sz080526
      *
     C                   dump
      *
      * SET UP A FIRST TIME FLAG
      *
     C                   TIME                    RTIME             6 0
     C                   MOVEL     YES           FIRST             1
     C                   MOVEL     *ALL'-'       DASH             90
      *
     C                   ENDSR
      /EJECT
      *-------------------------------------------------------------
      * REPORT HEADING
      *-------------------------------------------------------------
      *
     OQPRINT    E            HDRRPT         1 03
     O                       SDPGM               10
     O                       abadtx              58
     O                                           74 'DATE:'
     O                       UDATE         Y     84
      *
     O          E            HDRRPT         1
     O                       abacna              62
     O                                           74 'TIME:'
     O                       RTIME               84 '  :  :  '
      *
     O          E            HDRRPT         2
     O                       wkcadd              74
      *
     O          E            HDRRPT         2
     O                                           50 'CUSTOMER STATEMENT'
      *
      *
     O          E            HDRRPT      6  2
     O                                           20 'CUSTOMER NUMBER:'
     O                       XXANC7        Z     28
      *
      *-------------------------------------------------------------
      * CUSTOMER ADDRESS
      *-------------------------------------------------------------
      *
     O          E            HDRADD      4
     O                       XXAYTX              34
      *
     O          E            HDRADD      1
     O                       HDADD1              49
      *
     O          E            HDRADD      1
     O                       HDADD2              49
      *
     O          E            HDRADD      1
     O                       HDADD3              49
      *
     O          E            HDRADD      1  2
     O                       HDADD4              49
      *
      *-------------------------------------------------------------
      * COLUMN HEADINGS
      *-------------------------------------------------------------
      *
     O          E            HDRCOL      1  0
      *b:sz050826
     O               70                           3 'CO#'
      *e:sz050826
     O                                           13 'INVOICE'
     O                                           24 'ORDER'
     O                                           33 'DUE'
     O                                           41 'P.O.'
     O                                           67 'TRANSACTION'
     O                                           83 'BALANCE DUE'
      *
     O          E            HDRCOL      1  1
     O                                           12 'NUMBER'
     O                                           25 'NUMBER'
     O                                           34 'DATE'
     O                                           43 'NUMBER'
     O                                           67 'AMOUNT'
     O                                           83 'AMOUNT'
      *
      *-------------------------------------------------------------------------
      * DETAIL LINE
      *-------------------------------------------------------------------------
      *
     O          E            DTLINE      1
      *
      *b:sz050826
     O               70      BDAIC3        Z      3
      *e:sz050826
     O                       BDCONB        Z     11
     O                       BDCEST              14
     O                       BDCAC7        Z     24
     O                       DTDMDY              35 '  /  /  '
     O                       DTBCNA              52
     O                       DTBSVA        K     68
     O                       DTBGVA        K     84
      *
      *
      *-------------------------------------------------------------
      * REPORT TOTALS
      *-------------------------------------------------------------
      *
     O          E            TOTRPT      1  0
     O                                           68 '---------------'
     O                                           84 '---------------'
     O          E            TOTRPT      1  0
     O                                           51 'TOTAL:'
     O                       TOTAMT        K     68
     O                       TOTDUE        K     84
      *
      *-------------------------------------------------------------
      * NO DATA MESSAGE LINE
      *-------------------------------------------------------------
      *
     O          E            NODATA         1
     O                                           19 'NO DATA MEETS YOUR'
     O                                           38 'SELECTION CRITERIA'
