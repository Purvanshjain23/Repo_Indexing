     A*%%TS  SD  20090429  134304  ISLRAMS     REL-V5R4M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD404
     A*  TITLE:         SPECIFY HPS UPLOADS TO JDE
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  CREATED:       10/02/01
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A N99                                  CF08(08 'SUBMIT')
     A N99                                  CF04(04 'PROMPT')
     A N99                                  CF10(10 'VIEW')
     A N99                                  CF20(20 'WRKSPLF')
     A*
     A*---------------------------------------------------------------
     A* RECORD FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP404R1
     A*%%TS  SD  20090429  134304  ISLRAMS     REL-V5R4M0  5722-WDS
     A                                      SETOF(31)
     A                                      SETOF(51)
     A                                      OVERLAY
     A                                      RTNCSRLOC(&RCD &FLD)
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 28'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USR)
     A                                      DSPATR(HI)
     A                                  2 26'SPECIFY HPS UPLOAD TO JDE'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@JOB)
     A                                      DSPATR(HI)
     A                                  4  4'Year:'
     A            D1ACYR    R        O  4 10REFFLD(HSRREF/@@ACYR *LIBL/HSPREF)
     A                                      EDTCDE(M)
     A                                  5  2'Period:'
     A            D1ACPE    R        O  5 12REFFLD(@@ACPE)
     A                                      EDTCDE(M)
     A            D1BPMDY   R     Y  O  5 17REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A                                  5 27'TO'
     A            D1EPMDY   R     Y  O  5 30REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
      * S13682-Added 'Company code'.
     A                                  7  2'Company code .'
     A            D1COCD    R        B  7 19REFFLD(HSRREF/@@COCD *LIBL/HSPREF)
     A N41                                  DSPATR(HI)
     A  41                                  DSPATR(PC)
     A  41                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
      * S13682-Moved 'Upload code' down 1 line.
     A                                  9  2'Upload code ..'
     A                                  9 17'?'
     A            D1HUCD    R        B  9 19REFFLD(HSRREF/@@HUCD *LIBL/HSPREF)
     A N31                                  DSPATR(HI)
     A  31
     AO 51                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A            D1HUDS    R        O  9 23REFFLD(HSRREF/@@HUDS *LIBL/HSPREF)
     A  87                             11  2'ALERT! IMPORTANT MESSAGE:'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                      COLOR(RED)
     A*
     A  87        D1MSG1        60A  O 12  9DSPATR(HI)
     A*
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R HP404K1
     A*%%TS  SD  20081111  144815  ISLRAMS     REL-V5R4M0  5722-WDS
     A  43                             21  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A N99                             23 11'F4=Prompt'
     A N99                             23 22'F8=Submit'
     A                                 23 33'F10=View Upload Status'
     A N99                             23 64'F20=Spool Files'
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *---------------------------------------------------------------
     A          R HP404ES                   SFL SFLMSGRCD(24)
      *
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
     A*---------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*---------------------------------------------------------------
     A*
     A          R HP404EC                   SFLCTL(HP404ES)
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
