     A*%%TS  SD  20091030  101444  ISLRAMS     REL-V5R4M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD130
     A*  TITLE:         MAINTAIN UPLOADED FEED DELIVERY TICKET
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  CREATED:       04/30/97
     A*
     A*****************************************************************
     A* P405   DN  04/03/20 - Bin Code (BNCD) was increased from 5A to
     A*                       6A. Shifted subfile info to the right 1
     A*                       space.
     A*****************************************************************
     A*  INDICATORS:
     A*  90  THE TICKET THE USER HAS SELECTED FROM THE WORK WITH HAS
     A*      BEEN DELETED BY ANOTHER USER SINCE SELECTED
     A*****************************************************************
     A* SDN736 RECOMPILED FOR TICKET NBR INCREASE (TKNO & RTNO 7.0 TO 9.0)
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A N90                                  CF04(04 'PROMPT')
     A N90                                  CF05(05 'REFRESH')
     A N90                                  CF09(09 'MAINTAIN')
     A N90                                  CF11(11 'DELETE')
     A                                      CA12(12 'PREVIOUS')
     A*
     A*---------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP130S1                   SFL
     A*%%TS  SD  19980309  150530  ISLRAMS     REL-V3R7M0  5716-PW1
     A  85                                  SFLNXTCHG
     A            HDRRN1         4Y 0H
     A            HDHGSN    R        H      REFFLD(@@HGSN)
     A            HDPPCD    R        H      REFFLD(@@PPCD)
     A            HDBLCD    R        H      REFFLD(@@BLCD)
     A            HDRMCD    R        H      REFFLD(@@RMCD)
     A            HDDIR     R        H      REFFLD(@@FLAG)
     A            DFBNCD    R        B 16  2REFFLD(@@BNCD)
     A  90                                  DSPATR(PR)
     A  31
     AO 51                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A N31N90                               DSPATR(HI)
     A N31N90                               DSPATR(UL)
P405 A**          DFHGCD    R        B 16  8REFFLD(@@HGCD)
P405 A            DFHGCD    R        B 16  9REFFLD(@@HGCD)
     A  90                                  DSPATR(PR)
     A  32
     AO 52                                  DSPATR(PC)
     A  32                                  DSPATR(RI)
     A N32N90                               DSPATR(HI)
     A N32N90                               DSPATR(UL)
P405 A**          DFRNCD    R        B 16 16REFFLD(@@RNCD)
P405 A            DFRNCD    R        B 16 17REFFLD(@@RNCD)
     A  90                                  DSPATR(PR)
     A  33
     AO 53                                  DSPATR(PC)
     A  33                                  DSPATR(RI)
     A N33N90                               DSPATR(HI)
     A N33N90                               DSPATR(UL)
P405 A**          DFADCD    R        B 16 22REFFLD(@@ADCD)
P405 A            DFADCD    R        B 16 23REFFLD(@@ADCD)
     A  90                                  DSPATR(PR)
     A  47
     AO 57                                  DSPATR(PC)
     A  47                                  DSPATR(RI)
     A N47N90                               DSPATR(HI)
     A N47N90                               DSPATR(UL)
P405 A**          DFFTCD    R        B 16 28REFFLD(@@FTCD)
P405 A            DFFTCD    R        B 16 29REFFLD(@@FTCD)
     A  90                                  DSPATR(PR)
     A  48
     AO 58                                  DSPATR(PC)
     A  48                                  DSPATR(RI)
     A N48N90                               DSPATR(HI)
     A N48N90                               DSPATR(UL)
     A            DFFDLB    R        B 16 31REFFLD(FDFDLB)
     A                                      CHECK(RB)
     A                                      EDTCDE(K)
     A  90                                  DSPATR(PR)
     A  34                                  DSPATR(RI)
     A  34                                  DSPATR(PC)
     A N90N34                               DSPATR(HI)
     A N90N34                               DSPATR(UL)
     A            DFINAM    R        B 16 43REFFLD(FDINAM)
     A                                      EDTCDE(K)
     A  90                                  DSPATR(PR)
     A  35                                  DSPATR(RI)
     A  35                                  DSPATR(PC)
     A N35N90                               DSPATR(HI)
     A N35N90                               DSPATR(UL)
     A            DFMDAM    R        B 16 57REFFLD(FDMDAM)
     A                                      EDTCDE(M)
     A  90                                  DSPATR(PR)
     A  40                                  DSPATR(RI)
     A  40                                  DSPATR(PC)
     A N40N90                               DSPATR(HI)
     A N40N90                               DSPATR(UL)
     A            DFMFAM    R        B 16 69REFFLD(FDMFAM)
     A                                      EDTCDE(M)
     A  90                                  DSPATR(PR)
     A  41                                  DSPATR(RI)
     A  41                                  DSPATR(PC)
     A N41N90                               DSPATR(HI)
     A N41N90                               DSPATR(UL)
     A*
     A*---------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP130C1                   SFLCTL(HP130S1)
     A*%%TS  SD  20091030  101444  ISLRAMS     REL-V5R4M0  5722-WDS
     A                                      SFLSIZ(0100)
     A                                      SFLPAG(0006)
     A N82N90                               ROLLUP(25 'PAGE FORWARD')
     A                                      SFLCSRRRN(&HDRR1)
     A                                      OVERLAY
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A                                      RTNCSRLOC(&RCD &FLD)
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            HDRR1          5S 0H
     A            C1TKDT    R        H      REFFLD(UHREC/UHTKDT *LIBL/HSP237)
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 25'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A            DFMODE    R        O  2  2REFFLD(@@MODE)
     A                                      DSPATR(HI)
     A                                  2 17'MAINTAIN UPLOADED FEED DELIVERY TI-
     A                                      CKET'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  4  2'Mapping mill number:'
     A            C1MMNO    R        O  4 25REFFLD(UHREC/UHMMNO *LIBL/HSP237)
     A                                      EDTCDE(M)
     A                                  5  2'Business office ....'
     A N90                              5 23'?'
     A            C1FMBO    R        B  5 25REFFLD(UHREC/UHFMBO *LIBL/HSP237)
     A  30
     AO 50                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A N30N90                               DSPATR(HI)
     A N30N90                               DSPATR(UL)
     A  90                                  DSPATR(PR)
     A                                  6  2'Farm site ..........'
     A N90                              6 23'?'
     A            C1FSCD    R        B  6 25REFFLD(@@FSCD)
     A                                      EDTCDE(M)
     A  36
     AO 56                                  DSPATR(PC)
     A  36                                  DSPATR(RI)
     A N36N90                               DSPATR(HI)
     A N36N90                               DSPATR(UL)
     A  90                                  DSPATR(PR)
     A            C1FSNM    R        O  6 35REFFLD(FSFSNM)
     A                                  7  2'Ticket number ......'
     A            C1TKNO    R        B  7 25REFFLD(@@TKNO)
     A  37                                  DSPATR(PC)
     A  37                                  DSPATR(RI)
     A N37N90                               DSPATR(HI)
     A N37N90                               DSPATR(UL)
     A  90                                  DSPATR(PR)
     A                                  8  2'Ticket date ........'
     A            C1MDY     R        B  8 25REFFLD(@@DT06 *LIBL/HSPREF)
     A                                      EDTCDE(Y)
     A  38                                  DSPATR(PC)
     A  38                                  DSPATR(RI)
     A N38N90                               DSPATR(HI)
     A N38N90                               DSPATR(UL)
     A  90                                  DSPATR(PR)
     A                                  9  2'Produced flag ......'
     A N90                              9 23'?'
     A            C1PRFL    R        B  9 25REFFLD(@@PRFL)
     A  39
     AO 59                                  DSPATR(PC)
     A  39                                  DSPATR(RI)
     A N39N90                               DSPATR(HI)
     A N39N90                               DSPATR(UL)
     A  90                                  DSPATR(PR)
     A                                 10  2'Cost thru system ...'
     A N90                             10 23'?'
     A            C1CSFL    R        B 10 25REFFLD(@@CSFL)
     A  45
     AO 55                                  DSPATR(PC)
     A  45                                  DSPATR(RI)
     A N45N90                               DSPATR(HI)
     A N45N90                               DSPATR(UL)
     A  90                                  DSPATR(PR)
     A                                 11  2'Vendor invoice # ...'
     A            C1VINO    R        B 11 25REFFLD(@@IJD)
     A N90                                  DSPATR(HI)
     A N90                                  DSPATR(UL)
     A  90                                  DSPATR(PR)
     A                                 11 36'------------------- TOTALS --------
     A                                      ----------'
     A                                      DSPATR(HI)
     A            C1FDLB         7Y 0O 12 31EDTCDE(K)
     A            C1INAM    R        O 12 43REFFLD(FDREC/FDINAM *LIBL/HSP038)
     A                                      EDTCDE(K)
     A            C1MDAM    R        O 12 57REFFLD(FDREC/FDMDAM *LIBL/HSP038)
     A                                      EDTCDE(M)
     A            C1MFAM    R        O 12 69REFFLD(FDREC/FDMFAM *LIBL/HSP038)
     A                                      EDTCDE(M)
     A N90                             13  3'?'
     A                                      DSPATR(HI)
P405 A*N90                             13 10'?'
P405 A N90                             13 11'?'
     A                                      DSPATR(HI)
P405 A*N90                             13 18'?'
P405 A N90                             13 19'?'
     A                                      DSPATR(HI)
P405 A*N90                             13 24'?'
P405 A N90                             13 25'?'
     A                                      DSPATR(HI)
P405 A*N90                             13 28'?'
P405 A N90                             13 29'?'
     A                                      DSPATR(HI)
P405 A**                               14  8'Hog'
P405 A                                 14  9'Hog'
     A                                      DSPATR(HI)
P405 A**                               14 28'F'
P405 A                                 14 29'F'
     A                                      DSPATR(HI)
     A                                 14 45'Ingredient'
     A                                      DSPATR(HI)
     A                                 14 57'Medication'
     A                                      DSPATR(HI)
     A                                 14 71'Manufact'
     A                                      DSPATR(HI)
     A                                 15  2'Bin'
     A                                      DSPATR(HI)
P405 A**                               15  8'Group'
P405 A                                 15  9'Group'
     A                                      DSPATR(HI)
P405 A**                               15 16'Ration/Add'
P405 A                                 15 17'Ration/Add'
     A                                      DSPATR(HI)
P405 A**                               15 28'T'
P405 A                                 15 29'T'
     A                                      DSPATR(HI)
     A                                 15 34'Pounds'
     A                                      DSPATR(HI)
     A                                 15 51'Cost'
     A                                      DSPATR(HI)
     A                                 15 63'Cost'
     A                                      DSPATR(HI)
     A                                 15 75'Cost'
     A                                      DSPATR(HI)
     A*
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R HP130K1
     A*%%TS  SD  19940426  131054  ISLRAMS     REL-V2R2M0  5738-PW1
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A                                      DSPATR(HI)
     A                                 23  2'F3=Exit'
     A N90                             23 11'F4=Prompt'
     A N90                             23 22'F5=Refresh'
     A N90                             23 34'F9=Revise'
     A N90                             23 45'F11=Delete'
     A                                 23 57'F12=Previous'
     A*
     A*---------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE FORMAT
     A*---------------------------------------------------------------
     A          R HP130ES                   SFL SFLMSGRCD(24)
     A*
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
     A*---------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*---------------------------------------------------------------
     A          R HP130EC                   SFLCTL(HP130ES)
     A*
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
