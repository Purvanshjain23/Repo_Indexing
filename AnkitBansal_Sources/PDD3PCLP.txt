/********************************************************************/
/*                                                                  */
/*  SYNOPSIS - PRINT PROGRAM WITH SELECTIONS FOR SALES TONNAGE RPT. */
/*             COMPANY, CUSTOMER NUMBERS, SALESPERSON, AND          */
/*             TERRITORY.                                           */
/*                                                                  */
/*  SYSTEM    - ORDER MANAGEMENT SYSTEM                             */
/*  PROGRAM   - PRINT SALES TONNAGE REPORT                          */
/*  DATE      - 11/25/97                                            */
/*                                                                  */
/*      PARAMETERS RECEIVED:                                        */
/*                                                                  */
/*          &COMPNYN    - COMPANY NUMBER - BLANK FOR ALL            */
/*          &PARCUST    - PARENT CUSTOMER NUMBER                    */
/*          &ARCUST     - A/R CUSTOMER NUMBER                       */
/*          &SALEPER    - SALESPERSON CODE                          */
/*          &TERR       - TERRITORY CODE                            */
/*          &STATE      - STATE CODE                                */
/*          &OUTQ       - OUTPUT QUEUE                              */
/*          &HOLD       - HOLD PRINT                                */
/*          &SAVE       - SAVE PRINT                                */
/*          &COPYN      - NUMBER OF COPIES                          */
/*          &NIGHTQ     - NIGHT OUTPUT QUEUE                        */
/********************************************************************/

 PDD3PCLP:   PGM        PARM(&OUTQ &HOLD &SAVE &NIGHTQ &NQSTAT +
                          &COPYN &COMPNYN &PARCUST &ARCUST &SHIPTO +
                          &SALERTE &TERR &CMPDT &LOWDT &PRNTSTS +
                          &SALTP &RPTTP)
/* PARMS                                                                      */
        DCL    &COMPNYN    *DEC                  /* Company Number (Passed)   */
        DCL    &PARCUST    *DEC                  /* PARENT CUSTOMER(Passed)   */
        DCL    &ARCUST     *DEC                  /* A/R CUSTOMER   (PASSED)   */
        DCL    &SHIPTO     *DEC                  /* SHIP TO CUSTMR (PASSED)   */
        DCL    &SALERTE    *CHAR   (3)           /* SALESPERSON    (PASSED)   */
        DCL    &TERR       *CHAR   (6)           /* TERRITORY                 */
        DCL    &CMPDT      *CHAR   (6)           /* CURRENT YEAR COMPA RE DT  */
        DCL    &LOWDT      *CHAR   (6)           /* PREVIOUS YEAR COMPARE DT  */
        DCL    &CURDATE    *CHAR   (7)           /* CURRENT YEAR COMPA RE DT  */
        DCL    &PRVDATE    *CHAR   (7)           /* PREVIOUS YEAR COMPARE DT  */
        DCL    &DESC       *CHAR   (25)          /* DESCRIPTION               */
        DCL    &COPYN      *DEC                  /* No. of Copies             */
        DCL    &OUTQ       *CHAR   (10)          /* Output Queue              */
        DCL    &HOLD       *CHAR   (4)           /* Hold Status               */
        DCL    &SAVE       *CHAR   (4)           /* Save Status               */
        DCL    &NIGHTQ     *CHAR   (10)          /* Night Job Queue           */
        DCL    &NQSTAT     *CHAR   (1)
        DCL    &JOBQ       *CHAR   (10)
        DCL    &PRNTSTS    *CHAR   (1)
        DCL    &SALTP      *CHAR   (1)
        DCL    &RPTTP      *CHAR   (1)
        DCL    &ALPZER     *CHAR   (3)
        DCL    &ALPZER2    *CHAR   (3)
        DCL    &COMMAND    *CHAR   (512)

/* INTERNAL FIELDS                                                            */
        DCL    &COPY       *DEC    (2 0)         /* Number of copies          */
        DCL    &COMPANY    *DEC    (3 0)         /* Company Number            */
        DCL    &PARENT     *DEC    (7 0)         /* PARENT NUMBER             */
        DCL    &ARCUSNM    *DEC    (7 0)         /* AR CUSTOMER NUMBER        */
        DCL    &SHPTONM    *DEC    (7 0)
        DCL    &COMPDT     *DEC    (7 0)         /* HIGH DATE                 */

/* OPENQRYF                                                                   */
             DCL        VAR(&QRYCOMP) TYPE(*CHAR) LEN(3)
             DCL        VAR(&QRYPAR) TYPE(*CHAR) LEN(7)
             DCL        VAR(&QRYAR) TYPE(*CHAR) LEN(7)
             DCL        VAR(&QRYSHP) TYPE(*CHAR) LEN(7)
             DCL        VAR(&QRYSLRT) TYPE(*CHAR) LEN(4)
             DCL        VAR(&A) TYPE(*CHAR) LEN(30)
             DCL        VAR(&B) TYPE(*CHAR) LEN(30)
             DCL        VAR(&C) TYPE(*CHAR) LEN(30)
             DCL        VAR(&D) TYPE(*CHAR) LEN(30)
             DCL        VAR(&E) TYPE(*CHAR) LEN(30)
             DCL        VAR(&F) TYPE(*CHAR) LEN(30)
             DCL        VAR(&H) TYPE(*CHAR) LEN(30)
             DCL        VAR(&G) TYPE(*CHAR) LEN(280)

             DCL        VAR(&DATA)  TYPE(*CHAR) LEN(10)
             DCL        VAR(&DATAH) TYPE(*CHAR) LEN(10)

/********************************************************************/
/* CHANGE VARIABLES FOR USE IN PROGRAM                              */
/********************************************************************/

             CHGVAR     VAR(&COPY) VALUE(&COPYN)
             CHGVAR     VAR(&COMPANY) VALUE(&COMPNYN)
             CHGVAR     VAR(&PARENT) VALUE(&PARCUST)
             CHGVAR     VAR(&ARCUSNM) VALUE(&ARCUST)
             CHGVAR     VAR(&SHPTONM) VALUE(&SHIPTO)
             CHGVAR     VAR(&QRYCOMP) VALUE(&COMPANY)
             CHGVAR     VAR(&QRYPAR) VALUE(&PARENT)
             CHGVAR     VAR(&QRYAR) VALUE(&ARCUSNM)
             CHGVAR     VAR(&QRYSHP) VALUE(&SHPTONM)
             CHGVAR     VAR(&QRYSLRT) VALUE(&SALERTE)

/*********************************************************************/
/*   CREATE OPEN QUERY FILE TO BE USED FOR INVOICING REPORTS         */
/*********************************************************************/
/*SELECT COMPANY                                                     */
             CHGVAR     VAR(&A) VALUE('F4AIC3 *EQ ' *CAT &QRYCOMP)

/*SELECT PARENT                                                      */
           IF COND(&QRYPAR *NE '0000000') THEN(DO)
             CHGVAR     VAR(&B) VALUE('*AND F4BTC7 *EQ ' *CAT &QRYPAR)
             ENDDO

/*SELECT A/R CUSTOMER                                                */
             IF COND(&QRYAR *NE '0000000') THEN(DO)
             CHGVAR     VAR(&C) VALUE('*AND F4ANC7 *EQ ' *CAT &QRYAR)
             ENDDO

/*SELECT SHIP TO CUSTOMER                                            */
             IF COND(&QRYSHP *NE '0000000') THEN(DO)
             CHGVAR     VAR(&H) VALUE('*AND F4BKC7 *EQ ' *CAT &QRYSHP)
             ENDDO

/*SALES ROUTE CODE                                                  */
/*           IF COND(&QRYSLRT *NE 'ALL') THEN(DO)                   */
/*           CHGVAR     VAR(&D) VALUE('*AND F4R4CD *EQ "' *CAT +    */
/*                       &QRYSLRT *CAT '"')                         */
/*           ENDDO                                                  */

/*CUT-OFF DATE COMPARE                                              */
             CVTDAT     DATE(&CMPDT) TOVAR(&CURDATE) FROMFMT(*YMD) +
                          TOFMT(*CYMD) TOSEP(*NONE)
             CHGVAR     VAR(&E) VALUE('*AND F4GNDT *LE ' *CAT &CURDATE)
             CHGVAR     VAR(&COMPDT) VALUE(&CURDATE)

/*CUT-OFF LOW DATE COMPARE                                          */
             CVTDAT     DATE(&LOWDT) TOVAR(&PRVDATE) FROMFMT(*YMD) +
                          TOFMT(*CYMD) TOSEP(*NONE)
             CHGVAR     VAR(&F) VALUE('*AND F4GNDT *GE ' *CAT &PRVDATE)

/*CONCATENATE ALL SELECTS                                           */
             CHGVAR     VAR(&G) VALUE(&A *BCAT &B *BCAT &C *BCAT &D +
                          *BCAT &E *BCAT &F *BCAT &H)

/*RUN QUERY AGAINST OM HISTORY FILE                                 */
             OVRDBF     FILE(OMHSTPLT) SHARE(*YES)
             OPNQRYF    FILE(OMHSTPLT) +
                        QRYSLT(&G) +
                        KEYFLD(*FILE)

/*SET UP TEMP FILES IF DETAIL REPORT SELECTED                       */
             IF         COND(&RPTTP *EQ 'S') THEN(DO)
             CALL       PGM(CAQXXFR) PARM(' ' &DATA &DATAH)

             RMVLIBLE   LIB(QTEMP)
             MONMSG     MSGID(CPF0000)
             ADDLIBLE   LIB(QTEMP)
             MONMSG     MSGID(CPF0000)

             CLRPFM     FILE(PDMTCPP)
             CRTDUPOBJ  OBJ(PDMTCPP) FROMLIB(&DATA) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)
             CRTDUPOBJ  OBJ(PDMTCPL0) FROMLIB(&DATA) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)
             CRTDUPOBJ  OBJ(PDMTCPL1) FROMLIB(&DATA) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)
             ENDDO
/********************************************************************/
/* OVERRIDE PRINTER FILE WITH USER SPECIFIED SELECTION VALUES       */
/********************************************************************/

             SBDPRTOVR  CMDVAR(&COMMAND) FILE(PDFIPFR$) OUTQ(&OUTQ) +
                          COPIES(&COPY) HOLD(&HOLD) SAVE(&SAVE)

             CALL       PGM(QCMDEXC) PARM(&COMMAND 512)


/*********************************************************************/
/*   CALL PRINT PROGRAM -
/*********************************************************************/

             CALL       PGM(PDFIPFR) PARM(' ' &COMPANY &PARENT +
                          &ARCUSNM &SHPTONM &SALERTE &TERR &COMPDT +
                          &PRNTSTS &SALTP &RPTTP &ALPZER &ALPZER2)
             CLOF       OPNID(OMHSTPLT)
             DLTOVR     FILE(OMHSTPLT)

/*    CALLING DETAIL REPORT AND DELETING TEMP FILES.                 */
             IF COND(&RPTTP *EQ 'S') THEN(DO)
             SBDPRTOVR  CMDVAR(&COMMAND) FILE(PDODPFR$) OUTQ(&OUTQ) +
                          COPIES(&COPY) HOLD(&HOLD) SAVE(&SAVE)

             CALL       PGM(QCMDEXC) PARM(&COMMAND 512)
             CALL       PGM(PDODPFR) PARM(' ' &COMPANY &PARENT +
                          &ARCUSNM &SHPTONM &SALERTE &TERR &COMPDT +
                          &PRNTSTS &SALTP &RPTTP &ALPZER &ALPZER2)
             DLTF       FILE(QTEMP/PDMTCPL1)
             DLTF       FILE(QTEMP/PDMTCPL0)
             DLTF       FILE(QTEMP/PDMTCPP)
             ENDDO

 END:        ENDPGM
