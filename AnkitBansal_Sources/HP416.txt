      *****************  RPG PROGRAM HEADING  ************************
      * SYSTEM:      HOG PRODUCTION
      * PROGRAM:     HP416
      * TITLE:       SPECIFY DELETED MOVEMENT LISTING OPTIONS
      * PROGRAMMER:  LEANNE FEDOR
      * CREATED:     03/28/96
      *
      * FUNCTION: ALLOWS USER TO ENTER LISTING SELECTIONS.
      *           SUBMITS HP416CL AND CARRIES OPTIONS
      *           SELECTED IN LDA.
      *
      *           THIS PROGRAM IS CALLED FROM A MENU.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 05/13/96  LEANNE FEDOR
      *           MADE CHANGES TO FACILITATE NEW JDE RELEASE CONVERSION
      *           CHANGED SEARCH TYPE TO 3A; CALL NEW GENERIC
      *           PROGRAM HPJDEV TO VALIDATE INSTEAD OF HPJDEA SINCE
      *           NO VENDOR ADDRESS IS REQUIRED.
      *
      * 02/03/00  LeAnne Fedor
      *           Removed print options (hold, copies, outque, etc.) from screen
      *           and replaced them with a function key that calls the pop-up
      *           window of print options.
      *
      * 02/14/01  LeAnne Fedor
      *           In preparation for allowing groups on a single farm to have different
      *           production types, we changed the edit for a farm.  The production type
      *           of a selected farm no longer has to "match" the production type that
      *           the user may have keyed on the screen.
      *
      * 07/18/02  LeAnne Fedor
      *           In preparation for allowing groups on a single farm to have different
      *           production phases, we changed the edit for a farm.  The production phase
      *           of a selected farm no longer has to "match" the production phase that
      *           the user may have keyed on the screen.
      *
      * 10/16/13  LeAnne Ramsey (E2831)
      *           Recompile only. Added field 'MTech Reference'.
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     FHPD416    CF   E             WORKSTN
     F                                     INFDS(IOFEED)
      *
     FHSP001    IF   E           K DISK
      *    AUTHORZIED USERS          (KEY: AUUSCD)
      *
     FHSP002    IF   E           K DISK
      *    FUNCTIONS                 (KEY: FNFNCD)
      *
     FHSP009    IF   E           K DISK
      * BUSINESS OFFICE               (KEY: BOBOCD)
      *
     FHSP010    IF   E           K DISK
      *    PRODUCTION TYPE           (KEY: PTPTCD)
      *
     FHSP011    IF   E           K DISK
      *    PRODUCTION PHASE          (KEY: PPPPCD)
      *
     FHSP018    IF   E           K DISK
      *    FARM SITE                 (KEY: FSFSCD)
      *
      /EJECT
      ****************************************************************
      * TABLE AND ARRAY SPECIFICATIONS
      ****************************************************************
      *
      *---------------------------------------------------------------
      *  STANDARD MESSAGE ARRAYS
      *---------------------------------------------------------------
      *
      *---------------------------------------------------------------
      *  COMPILE TIME ARRAYS FOR SUBMIT JOB COMMAND USED IN QCMDEXC
      *---------------------------------------------------------------
     D CMD             S              1    DIM(80) CTDATA PERRCD(80)
      /EJECT
      ****************************************************************
      * INPUT SPECIFICATIONS
      ****************************************************************
      *
      *---------------------------------------------------------------
      * STANDARD ERROR MESSAGE HANDLING DATA STRUCTURES
      *---------------------------------------------------------------
      *
      * FOR ERROR MESSAGE HANDLING, A PACKED INDEX (E) IS REQUIRED.
      * FOR PROGRAM READABILITY, DEFINE A CORRESPONDING ERROR COUNT
      * FIELD CALLED 'ERROR'
      *
     D                 DS                  INZ
     D  ERROR                  1      2P 0
     D  E                      1      2P 0
      *
      * THIS DATA STRUCTURE SUPPLIES THE NAME OF THE MESSAGE FILE TO
      * THE MESSAGE HANDLING CL PROGRAM.  THE FIELD NAME MSGFIL MUST BE
      * CONSTANT. THE VALUE IN QUOTES IS THE NAME OF THE SPECIFIC
      * MESSAGE FILE CONTAINING THE USER DEFINED MESSAGES.
      *
     D                 DS                  INZ
     D  MSGFIL                 1     10    INZ('HSMSGF    ')
      *
      * THE FOLLOWING 3 DATA STRUCTURES ARE USED TO SPEED MESSAGE
      * HANDLING SINCE IT IS FASTER TO CLEAR DATA STRUCTURES THAN
      * ARRAYS.  EACH IS ASSOCIATED WITH A STANDARD MESSAGE ARRAY.
      *
     D MGI             DS           140    INZ
     D  MGID                   1    140
     D                                     DIM(20)                              MSG ID ARRAY
      *
     D MGD             DS          1000    INZ
     D  MGDT                   1   1000
     D                                     DIM(20)                              MSG PARMS
      *
     D MGWK            DS            50    INZ
     D  MGW                    1     50
     D                                     DIM(50)                              MSG WORK PARMS
      *
      *---------------------------------------------------------------
      * STANDARD PROGRAM STATUS DATA STRUCTURE
      *---------------------------------------------------------------
      *    EXTERNALLY DEFINED AS UTPGFR (RECORD FORMAT: PGMDSR)
     D PGMDS         ESDS                  EXTNAME(UTPGFR)
      *
      *---------------------------------------------------------------
      * STANDARD WORKSTATION INFORMATION DATA STRUCTURE
      *---------------------------------------------------------------
      *    EXTERNALLY DEFINED AS UTWSFR (RECORD FORMAT: UTIDFRR)
     D IOFEED        E DS                  EXTNAME(UTWSFR)
      *
      *---------------------------------------------------------------
      * STANDARD DATABASE FILE INFORMATION DATA STRUCTURE
      *---------------------------------------------------------------
      *    EXTERNALLY DEFINED AS UTDBFR (RECORD FORMAT: FDBCKD)
     D DBFEED        E DS                  EXTNAME(UTDBFR)
      *
      *---------------------------------------------------------------
      *  LDA -  WITH THE REQUESTED PRINT SELECTIONS
      *---------------------------------------------------------------
      *
     D LDA            UDS
     D  LDUSCD                 1     10
      *
     D  LDFDYM                11     18  0
     D  LDTDYM                19     26  0
     D  LDFSYM                27     34  0
     D  LDTSYM                35     42  0
      *
     D  LDMTCD                43     43
      *
     D  LDORBO                44     48
     D  LDORPT                49     53
     D  LDORPP                54     58
     D  LDORFS                59     63  0
      *
     D  LDDNBO                64     68
     D  LDDNPT                69     73
     D  LDDNPP                74     78
     D  LDDNFS                79     83  0
      *
     D  LDCVNO                84     91  0
      *
     D  LDFDMD                92     97  0
     D  LDTDMD                98    103  0
     D  LDFSMD               104    109  0
     D  LDTSMD               110    115  0
      *
     D  LDMTDS               116    135
      *
     D  LDORBD               136    165
     D  LDORTD               166    175
     D  LDORPD               176    205
     D  LDORFN               206    230
      *
     D  LDDNBD               231    260
     D  LDDNTD               261    270
     D  LDDNPD               271    300
     D  LDDNFN               301    325
      *
     D  LDCNAM               326    355
     D  LDUSNM               356    385
      *
     D  LDOUTU               401    410
     D  LDHOLD               411    411
     D  LDCOPY               412    412  0
     D  LDOUTQ               413    422
      *
      *---------------------------------------------------------------
      *  NAMED CONSTANTS
      *---------------------------------------------------------------
      *
     D YES             C                   CONST('Y')
     D NO              C                   CONST('N')
     D SET1            C                   CONST('SET1  ')
     D EDIT1           C                   CONST('EDIT1 ')
     D SCRN1           C                   CONST('SCRN1 ')
     D EXIT            C                   CONST('EXIT  ')
     D ALL             C                   CONST('A')
      *
      *---------------------------------------------------------------
      *  DATE FORMATS
      *---------------------------------------------------------------
      *
      * DATE IN MMDDYY FORMAT
      *
     D                 DS
     D  WKMDY                  1      6  0
     D  WKMM                   1      2  0
     D  WKDD                   3      4  0
     D  WKYY                   5      6  0
      * DATE IN CCYYMMDD FORMAT
      *
     D                 DS
     D  WKCYMD                 1      8  0
     D  WKCEN                  1      2  0
     D  WKYER                  3      4  0
     D  WKMON                  5      6  0
     D  WKDAY                  7      8  0
      *
      *---------------------------------------------------------------
      *  DEFINITION FOR EXTERNAL DATA AREA 'DAJOBQ'
      *---------------------------------------------------------------
     D                 DS
     D  DAJOBQ                 1     10
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * MAIN LINE
      ****************************************************************
      *
     C                   MOVEL     SET1          ROUTNE
      *
     C     *INLR         DOWEQ     *OFF                                         MAIN DO LOOP
      *
     C     ROUTNE        CASEQ     SET1          $SET1
     C     ROUTNE        CASEQ     SCRN1         $SCRN1
     C     ROUTNE        CASEQ     EDIT1         $EDIT1
      *
     C     ROUTNE        CASEQ     EXIT          $EXIT
     C                   ENDCS
     C                   ENDDO                                                  MAIN DO LOOP
      /EJECT
      *----------------------------------------------------------------
      * $SET1 - SUBROUTINE TO SET ENVIRONMENT FOR SCREEN 1
      *----------------------------------------------------------------
      *
     C     $SET1         BEGSR
      *
      *  SET ROUTINE TO DISPLAY SCREEN
      *
     C                   MOVEL     SCRN1         ROUTNE
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $SCRN1 - SUBROUTINE TO PERFORM OPERATOR I/O FOR SCREEN 1
      *----------------------------------------------------------------
      *
     C     $SCRN1        BEGSR
      *
      * IF THE USER FAILED THE INITIAL SECURITY CHECKS, SETON 99
      * TO PROTECT INPUT FIELDS AND INVALIDATE FUNCTION KEYS.
      *
     C     TERMFL        IFEQ      YES
     C                   SETON                                        99
     C                   ENDIF
      *
      * WRITE THE ERROR MESSAGES FROM THE ERROR ARRAYS TO THE
      * ERROR MESSAGE SUBFILE
      *
     C                   EXSR      $WRMSG
      *
      * WRITE SCREEN 1 TO CRT
      *
     C                   WRITE     HP416K1
     C                   WRITE     HP416EC
     C                   EXFMT     HP416R1
      *
      * CLEAR THE MESSAGES FROM THE MESSAGE DATA STRUCTURES AND PROGRAM
      * MESSAGE QUEUE.
      *
     C                   EXSR      $CLMSG
      *
      * GET USER'S ENTRY AND SET THE NEXT ROUTINE
      *
     C                   SELECT
     C     *IN03         WHENEQ    *ON                                          F3-EXIT
     C     *IN12         OREQ      *ON                                          F12-PREVIOUS
     C     TERMFL        OREQ      YES                                          BAD USER
     C                   MOVEL     EXIT          ROUTNE
      *
     C     *IN17         WHENEQ    *ON                                          F17-PRINT OPT
     C                   MOVEL     SCRN1         ROUTNE
     C                   CALL      'HP451'
     C                   PARM                    SDPGM
     C     LDCOPY        PARM      LDCOPY        XXCOPY
     C                   PARM                    LDHOLD
     C                   PARM                    LDOUTQ
     C                   PARM                    XXNITE
     C                   PARM                    XXJOBQ
      *
     C     *IN20         WHENEQ    *ON                                          F20-WRKSPLF
     C                   CALL      'QEZOUTPT'
     C                   MOVEL     SCRN1         ROUTNE
      *
     C     *IN04         WHENEQ    *ON                                          WH PROMPT
     C                   EXSR      $F4S1
     C                   MOVEL     SCRN1         ROUTNE
     C                   OTHER
      *
     C                   MOVEL     EDIT1         ROUTNE
     C                   ENDSL
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $EDIT1 - SUBROUTINE TO EDIT INPUT FIELDS FOR SCREEN 1
      *----------------------------------------------------------------
      *
     C     $EDIT1        BEGSR
      *
      * VALIDATE USER IF ENTERED
      *
     C                   MOVE      *BLANK        DFUSNM
     C     DFUSCD        IFNE      *BLANK                                       IF USER
     C     DFUSCD        CHAIN     HSP001                             92
     C     *IN92         IFEQ      *OFF                                         IF USER OK
     C                   MOVEL     AUUSNM        DFUSNM
     C                   ELSE
     C                   SETON                                        50
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS09102'     MGID(E)
     C                   CLEAR                   MGW
     C                   MOVEA     DFUSCD        MGW(1)
     C                   MOVEA     MGW           MGDT(E)
     C                   ENDIF
     C                   ENDIF                                                  IF USER OK
     C                   ENDIF                                                  IF USER
      *
      * VALIDATE FROM/TO DELETE DATES IF EITHER ARE ENTERED
      *
     C                   Z-ADD     0             LDFDYM
     C                   Z-ADD     0             LDTDYM
     C                   Z-ADD     0             LDFDMD
     C                   Z-ADD     0             LDTDMD
      *
     C     DFFDMD        IFNE      0
     C     DFTDMD        ORNE      0
     C                   EXSR      $DLTDT
     C                   ENDIF
      *
      * VALIDATE FROM/TO SCHEDULED DATES IF EITHER ARE ENTERED
      *
     C                   Z-ADD     0             LDFSYM
     C                   Z-ADD     0             LDTSYM
     C                   Z-ADD     0             LDFSMD
     C                   Z-ADD     0             LDTSMD
      *
     C     DFFSMD        IFNE      0
     C     DFTSMD        ORNE      0
     C                   EXSR      $SCHDT
     C                   ENDIF
      *
      * VALIDATE MOVEMENT TYPE IF ENTERED
      *
     C                   MOVE      *BLANK        DFMTDS
     C                   SELECT
     C     DFMTCD        WHENEQ    *BLANK                                       WH BLANK
      *
     C     DFMTCD        WHENNE    'T'                                          WH BAD
     C     DFMTCD        ANDNE     'S'
     C                   SETON                                        31
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS09087'     MGID(E)
     C                   ENDIF
      *
     C     DFMTCD        WHENEQ    'T'                                          WH TRANSFER
     C                   MOVEL(P)  'TRANSFER'    DFMTDS
      *
     C     DFMTCD        WHENEQ    'S'                                          WH SALE
     C                   MOVEL(P)  'SALE'        DFMTDS
     C                   ENDSL
      *
      * ORIGIN BUSINESS OFFICE
      *
     C                   MOVE      *BLANK        DFORBD
     C     DFORBO        IFNE      *BLANK                                       IF OR BO
     C                   MOVEL     'O'           WKODFL
     C                   MOVEL     DFORBO        WKFSBO
     C                   EXSR      $CHKBO
     C                   ENDIF                                                  IF OR BO
      *
      * ORIGIN PRODUCTION TYPE
      *
     C                   MOVE      *BLANK        DFORTD
     C     DFORPT        IFNE      *BLANK                                       IF OR TYPE
     C     DFORPT        CHAIN     HSP010                             92
     C     *IN92         IFEQ      *OFF                                         IF GOOD TYPE
     C                   MOVEL     PTPTDS        DFORTD
     C                   ELSE
     C                   SETON                                        33
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS00180'     MGID(E)
     C                   CLEAR                   MGW
     C                   MOVEA     DFORPT        MGW(1)
     C                   MOVEA     MGW           MGDT(E)
     C                   ENDIF
     C                   ENDIF                                                  IF GOOD TYPE
     C                   ENDIF                                                  IF OR TYPE
      *
      * ORIGIN PRODUCTION PHASE
      *
     C                   MOVE      *BLANK        DFORPD
     C     DFORPP        IFNE      *BLANK                                       IF OR PHASE
     C     DFORPP        CHAIN     HSP011                             92
     C     *IN92         IFEQ      *OFF                                         IF GOOD PHAS
     C                   MOVEL     PPPPDS        DFORPD
     C                   ELSE
     C                   SETON                                        34
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS00170'     MGID(E)
     C                   CLEAR                   MGW
     C                   MOVEA     DFORPP        MGW(1)
     C                   MOVEA     MGW           MGDT(E)
     C                   ENDIF
     C                   ENDIF                                                  IF GOOD PHAS
     C                   ENDIF                                                  IF OR PHASE
      *
      * ORIGIN FARM SITE
      *
     C                   MOVE      *BLANK        DFORFN
     C     DFORFS        IFNE      0                                            IF FARM
     C                   EXSR      $ORFS
     C                   ENDIF                                                  IF FARM
      *
      *
      * DESTINATION BUSINESS OFFICE
      *
     C                   MOVE      *BLANK        DFDNBD
     C                   SELECT
     C     DFMTCD        WHENEQ    'S'
     C     DFDNBO        ANDNE     *BLANK
     C                   SETON                                        36
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS09119'     MGID(E)
     C                   ENDIF
      *
     C     DFDNBO        WHENNE    *BLANK
     C                   MOVEL     'D'           WKODFL
     C                   MOVEL     DFDNBO        WKFSBO
     C                   EXSR      $CHKBO
     C                   ENDSL
      *
      * DESTINATION PRODUCTION TYPE
      *
     C                   MOVE      *BLANK        DFDNTD
     C                   SELECT
     C     DFMTCD        WHENEQ    'S'
     C     DFDNPT        ANDNE     *BLANK
     C                   SETON                                        37
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS09121'     MGID(E)
     C                   ENDIF
      *
     C     DFDNPT        WHENNE    *BLANK
     C     DFDNPT        CHAIN     HSP010                             92
     C     *IN92         IFEQ      *OFF                                         IF GOOD TYPE
     C                   MOVEL     PTPTDS        DFDNTD
     C                   ELSE
     C                   SETON                                        37
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS00180'     MGID(E)
     C                   CLEAR                   MGW
     C                   MOVEA     DFDNPT        MGW(1)
     C                   MOVEA     MGW           MGDT(E)
     C                   ENDIF
     C                   ENDIF                                                  IF GOOD TYPE
     C                   ENDSL
      *
      * DESTINATION PRODUCTION PHASE
      *
     C                   MOVE      *BLANK        DFDNPD
     C                   SELECT
     C     DFMTCD        WHENEQ    'S'
     C     DFDNPP        ANDNE     *BLANK
     C                   SETON                                        38
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS09120'     MGID(E)
     C                   ENDIF
      *
     C     DFDNPP        WHENNE    *BLANK
     C     DFDNPP        CHAIN     HSP011                             92
     C     *IN92         IFEQ      *OFF                                         IF GOOD PHAS
     C                   MOVEL     PPPPDS        DFDNPD
     C                   ELSE
     C                   SETON                                        38
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS00170'     MGID(E)
     C                   CLEAR                   MGW
     C                   MOVEA     DFDNPP        MGW(1)
     C                   MOVEA     MGW           MGDT(E)
     C                   ENDIF
     C                   ENDIF                                                  IF GOOD PHAS
     C                   ENDSL                                                  IF DN PHASE
      *
      * DESTINATION FARM SITE
      *
     C                   MOVE      *BLANK        DFDNFN
     C                   SELECT
     C     DFMTCD        WHENEQ    'S'
     C     DFDNFS        ANDNE     0
     C                   SETON                                        39
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS09122'     MGID(E)
     C                   ENDIF
      *
     C     DFDNFS        WHENNE    0
     C                   EXSR      $DNFS
     C                   ENDSL
      *
      * VALIDATE THE CUSTOMER A/R NUMBER
      *
     C                   MOVE      *BLANK        DFCNAM
     C                   MOVE      *BLANK        LDCNAM
      *
     C                   SELECT
     C     DFMTCD        WHENEQ    'T'
     C     DFCVNO        ANDNE     0
     C                   SETON                                        40
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS09123'     MGID(E)
     C                   ENDIF
      *
     C     DFCVNO        WHENNE    0
     C                   Z-ADD     DFCVNO        INAJD
     C                   MOVEL(P)  'C'           INAT1
     C                   EXSR      $JDEV
      * IF ERROR
     C     JDEFL         IFEQ      YES
     C                   SETON                                        40
     C                   ELSE
     C                   MOVEL     OUALPH        DFCNAM
     C                   MOVEL     OUALPH        LDCNAM
     C                   ENDIF
     C                   ENDSL
      *
      * IF THERE ARE NO ERRORS, EXECUTE THE SUBROUTINE TO PROCESS THE
      * USERS REQUEST
      *
     C     ERROR         IFEQ      0
     C     *IN44         IFEQ      *OFF
     C                   SETON                                        44
     C                   ELSE
     C                   EXSR      $PROC1
     C                   ENDIF
     C                   ELSE
     C                   SETOFF                                       44
     C                   ENDIF
      *
     C                   MOVEL     SCRN1         ROUTNE
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $ORFS - VALIDATE ORIGIN FARM SITE
      *----------------------------------------------------------------
      *
      *     VALIDATE AND GET FARM SITE NAME
      *     FARM B.O. AND SCREEN B.O. MATCH
      *
     C     $ORFS         BEGSR
      *
     C     DFORFS        CHAIN     HSP018                             92
     C     *IN92         IFEQ      *ON                                          IF BAD FARM
     C                   SETON                                        35
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS00131'     MGID(E)
     C                   CLEAR                   MGW
     C                   MOVE      DFORFS        XXFSCD
     C                   MOVEA     XXFSCD        MGW(1)
     C                   MOVEA     MGW           MGDT(E)
     C                   ENDIF
     C                   ELSE
      *
     C                   MOVEL     FSFSNM        DFORFN
      *
     C     FSFSBO        IFNE      DFORBO                                       IF BUS OFF
     C     DFORBO        ANDNE     *BLANK
     C                   SETON                                        35
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS00158'     MGID(E)
     C                   CLEAR                   MGW
     C                   MOVE      DFORFS        XXFSCD
     C                   MOVEA     XXFSCD        MGW(1)
     C                   MOVEA     FSFSBO        MGW(6)
     C                   MOVEA     DFORBO        MGW(11)
     C                   MOVEA     MGW           MGDT(E)
     C                   ENDIF
     C                   ENDIF                                                  IF BUS OFF
      *
     C                   ENDIF                                                  IF BAD FARM
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $DNFS - VALIDATE DESTINATION FARM SITE
      *----------------------------------------------------------------
      *
      *     VALIDATE AND GET FARM SITE NAME
      *     FARM B.O. AND SCREEN B.O. MATCH
      *
     C     $DNFS         BEGSR
      *
     C     DFDNFS        CHAIN     HSP018                             92
     C     *IN92         IFEQ      *ON                                          IF BAD FARM
     C                   SETON                                        39
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS00131'     MGID(E)
     C                   CLEAR                   MGW
     C                   MOVE      DFDNFS        XXFSCD
     C                   MOVEA     XXFSCD        MGW(1)
     C                   MOVEA     MGW           MGDT(E)
     C                   ENDIF
     C                   ELSE
      *
     C                   MOVEL     FSFSNM        DFDNFN
      *
     C     FSFSBO        IFNE      DFDNBO                                       IF BUS OFF
     C     DFDNBO        ANDNE     *BLANK
     C                   SETON                                        39
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS00158'     MGID(E)
     C                   CLEAR                   MGW
     C                   MOVE      DFDNFS        XXFSCD
     C                   MOVEA     XXFSCD        MGW(1)
     C                   MOVEA     FSFSBO        MGW(6)
     C                   MOVEA     DFDNBO        MGW(11)
     C                   MOVEA     MGW           MGDT(E)
     C                   ENDIF
     C                   ENDIF                                                  IF BUS OFF
      *
     C                   ENDIF                                                  IF BAD FARM
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $CHKBO - CHECK THE BUSINESS OFFICE ENTERED
      *----------------------------------------------------------------
      *
      * THE BUSINESS OFFICE ENTERED MUST:
      *    BE A VALID OFFICE IN THE BUSINESS OFFICE MASTER
      *    HAVE A SECURITY LOCATION TO WHICH THE USER IS AUTHORIZED
      *    IS NOT REQUIRED
      *
     C     $CHKBO        BEGSR
      *
      * SET THE 'VALIDATE B.O.' FLAG DEPENDING ON WHETHER THE
      * USER LEFT B.O. BLANK TO GET ALL BUSINESS OFFICES OR ENTERED
      * A SPECIFIC OFFICE.
      *
     C     WKFSBO        IFEQ      *BLANK
     C                   MOVEL     ALL           INBOFL
     C                   ELSE
     C                   MOVEL     YES           INBOFL
     C                   ENDIF
      *
      * CHECK THE USER'S SECURITY TO THIS BUSINESS OFFICE.
      * SET INDVFL TO YES TO TELL SECURITY PROGRAM TO COMPARE THE
      * FUNCTION DIVISION CODE TO THE B.O. DIVISION CODE.
      *
     C                   MOVEL     YES           INDVFL
     C                   EXSR      $SEC
     C                   MOVEL     NO            INDVFL
      *
     C                   SELECT
     C     WKODFL        WHENEQ    'O'
     C                   MOVEL     OUBODS        DFORBD
     C     WKODFL        WHENEQ    'D'
     C                   MOVEL     OUBODS        DFDNBD
     C                   ENDSL
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * $SEC   - CALL SECURITY PROGRAM
      *---------------------------------------------------------------
      *
     C     $SEC          BEGSR
      *
      * SETUP THE FIELDS TO SEND INTO THE SECURITY PROGRAM
      *
     C                   MOVEL     WKFNCD        INFNCD                         PGM NAME
      *
     C                   SELECT
     C     WKODFL        WHENEQ    'O'
     C                   MOVEL     DFORBO        INBOBO
     C     WKODFL        WHENEQ    'D'
     C                   MOVEL     DFDNBO        INBOBO
     C                   ENDSL
      *
     C                   MOVE      *BLANK        OUBOBO                         BO CODE
     C                   MOVE      *BLANK        OUBODS                         BO DESC
     C                   MOVE      *BLANK        OUDVCD                         FUNCTION DIV
     C                   MOVEL     NO            TERMFL            1            TERMINAL FLG
      *
     C                   CALL      'HPSEC  '
      **** IN
     C                   PARM                    MGID                           MSGID ARRAY
     C                   PARM                    MGDT                           MSGDTA DATA
     C                   PARM                    ERROR                          # OF ERRORS
     C                   PARM                    INFNCD           10            FUNC CODE
     C                   PARM                    INBOBO            5            BO CODE
     C                   PARM                    INDVFL            1            CHECK DIV?
     C                   PARM                    INBOFL            1            CHECK B.O.?
      **** OUT
     C                   PARM                    OUBOBO            5            BO CODE
     C                   PARM                    OUBODS           30            BO DESC.
     C                   PARM                    OUDVCD            5            DIV CODE
     C                   PARM                    SECFL             1            SEC FLAG
     C                   PARM                    OUBOFL            1            BO ERRFLG
     C                   PARM                    OURMFL            1
      *
     C     OUBOFL        IFEQ      YES
     C                   SELECT
     C     WKODFL        WHENEQ    'O'
     C                   SETON                                        32
     C     WKODFL        WHENEQ    'D'
     C                   SETON                                        36
     C                   ENDSL
     C                   ENDIF
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * $DLTDT - VALIDATE FROM/TO DELETED DATES
      *---------------------------------------------------------------
      *
     C     $DLTDT        BEGSR
      *
     C                   SELECT
     C     DFFDMD        WHENEQ    0                                            WH NO FROM
     C                   SETON                                        47
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS09088'     MGID(E)
     C                   ENDIF
      *
     C     DFFDMD        WHENLT    0                                            WH NEG FROM
     C                   SETON                                        47
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS09000'     MGID(E)
     C                   ENDIF
     C                   OTHER
      *
     C                   Z-ADD     DFFDMD        PFRM8
     C                   Z-ADD     0             PTO8
     C                   MOVEL     'MDY '        PFRFMT
     C                   MOVEL     'CYMD'        PTOFMT
     C                   Z-ADD     0             PDAYS
     C                   MOVE      *BLANK        PCODE
     C                   MOVE      *BLANK        PRTRN
     C                   EXSR      $DATE
      *
     C     PRTRN         IFNE      *BLANK                                       IF BAD FROM
     C                   SETON                                        47
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS09004'     MGID(E)
     C                   ENDIF
     C                   ELSE
     C                   Z-ADD     PTO8          LDFDYM
     C                   Z-ADD     DFFDMD        LDFDMD
     C                   ENDIF                                                  IF BAD FROM
     C                   ENDSL
      *
      * VERIFY THAT A VALID TO DATE IS ENTERED
      *
     C                   SELECT
     C     DFTDMD        WHENEQ    0                                            WH NO TO
     C                   SETON                                        48
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS09089'     MGID(E)
     C                   ENDIF
      *
     C     DFTDMD        WHENLT    0                                            WH NEG TO
     C                   SETON                                        48
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS09000'     MGID(E)
     C                   ENDIF
     C                   OTHER
      *
     C                   Z-ADD     DFTDMD        PFRM8
     C                   Z-ADD     0             PTO8
     C                   MOVEL     'MDY '        PFRFMT
     C                   MOVEL     'CYMD'        PTOFMT
     C                   Z-ADD     0             PDAYS
     C                   MOVE      *BLANK        PCODE
     C                   MOVE      *BLANK        PRTRN
     C                   EXSR      $DATE
      *
     C     PRTRN         IFNE      *BLANK                                       IF BAD TO
     C                   SETON                                        48
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS09004'     MGID(E)
     C                   ENDIF
     C                   ELSE
     C                   Z-ADD     PTO8          LDTDYM
     C                   Z-ADD     DFTDMD        LDTDMD
     C                   ENDIF                                                  IF BAD TO
     C                   ENDSL
      *
      * VERIFY THAT THE "TO" DATE IS NOT EARLIER THAN THE "FROM" DATE
      *
     C     *IN47         IFEQ      *OFF                                         IF EARLIER
     C     *IN48         ANDEQ     *OFF
     C     LDTDYM        ANDLT     LDFDYM
     C                   SETON                                        48
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS09009'     MGID(E)
     C                   ENDIF
     C                   ENDIF                                                  IF EARLIER
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * $SCHDT - VALIDATE FROM/TO SCHEDULED DATES
      *---------------------------------------------------------------
      *
     C     $SCHDT        BEGSR
      *
     C                   SELECT
     C     DFFSMD        WHENEQ    0                                            WH NO FROM
     C                   SETON                                        49
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS09088'     MGID(E)
     C                   ENDIF
      *
     C     DFFSMD        WHENLT    0                                            WH NEG FROM
     C                   SETON                                        49
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS09000'     MGID(E)
     C                   ENDIF
     C                   OTHER
      *
     C                   Z-ADD     DFFSMD        PFRM8
     C                   Z-ADD     0             PTO8
     C                   MOVEL     'MDY '        PFRFMT
     C                   MOVEL     'CYMD'        PTOFMT
     C                   Z-ADD     0             PDAYS
     C                   MOVE      *BLANK        PCODE
     C                   MOVE      *BLANK        PRTRN
     C                   EXSR      $DATE
      *
     C     PRTRN         IFNE      *BLANK                                       IF BAD FROM
     C                   SETON                                        49
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS09004'     MGID(E)
     C                   ENDIF
     C                   ELSE
     C                   Z-ADD     PTO8          LDFSYM
     C                   Z-ADD     DFFSMD        LDFSMD
     C                   ENDIF                                                  IF BAD FROM
     C                   ENDSL
      *
      * VERIFY THAT A VALID TO DATE IS ENTERED
      *
     C                   SELECT
     C     DFTSMD        WHENEQ    0                                            WH NO TO
     C                   SETON                                        66
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS09089'     MGID(E)
     C                   ENDIF
      *
     C     DFTSMD        WHENLT    0                                            WH NEG TO
     C                   SETON                                        66
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS09000'     MGID(E)
     C                   ENDIF
     C                   OTHER
      *
     C                   Z-ADD     DFTSMD        PFRM8
     C                   Z-ADD     0             PTO8
     C                   MOVEL     'MDY '        PFRFMT
     C                   MOVEL     'CYMD'        PTOFMT
     C                   Z-ADD     0             PDAYS
     C                   MOVE      *BLANK        PCODE
     C                   MOVE      *BLANK        PRTRN
     C                   EXSR      $DATE
      *
     C     PRTRN         IFNE      *BLANK                                       IF BAD TO
     C                   SETON                                        66
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS09004'     MGID(E)
     C                   ENDIF
     C                   ELSE
     C                   Z-ADD     PTO8          LDTSYM
     C                   Z-ADD     DFTSMD        LDTSMD
     C                   ENDIF                                                  IF BAD TO
     C                   ENDSL
      *
      * VERIFY THAT THE "TO" DATE IS NOT EARLIER THAN THE "FROM" DATE
      *
     C     *IN49         IFEQ      *OFF                                         IF EARLIER
     C     *IN66         ANDEQ     *OFF
     C     LDTSYM        ANDLT     LDFSYM
     C                   SETON                                        66
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS09009'     MGID(E)
     C                   ENDIF
     C                   ENDIF                                                  IF EARLIER
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * $PROC1 - SUBROUTINE TO PROCESS THE USER'S REQUEST
      *---------------------------------------------------------------
      *
     C     $PROC1        BEGSR
      *
      *  IF USER TOOK F8=SUBMIT EXCEPTION REPORT
      *
     C     *IN08         IFEQ      *ON                                          IF F8=SBMJOB
      *
      * MOVE DISPLAY FILE FIELDS INTO LDA
      *
     C                   MOVEL     DFUSCD        LDUSCD
     C                   MOVEL     DFUSNM        LDUSNM
      *
     C                   MOVEL     DFMTCD        LDMTCD
     C                   MOVEL     DFMTDS        LDMTDS
      *
     C                   Z-ADD     DFCVNO        LDCVNO
     C                   MOVEL     DFCNAM        LDCNAM
      *
     C                   MOVEL     DFORBO        LDORBO
     C                   MOVEL     DFORBD        LDORBD
     C                   MOVEL     DFORPT        LDORPT
     C                   MOVEL     DFORTD        LDORTD
     C                   MOVEL     DFORPP        LDORPP
     C                   MOVEL     DFORPD        LDORPD
     C                   Z-ADD     DFORFS        LDORFS
     C                   MOVEL     DFORFN        LDORFN
      *
     C                   MOVEL     DFDNBO        LDDNBO
     C                   MOVEL     DFDNBD        LDDNBD
     C                   MOVEL     DFDNPT        LDDNPT
     C                   MOVEL     DFDNTD        LDDNTD
     C                   MOVEL     DFDNPP        LDDNPP
     C                   MOVEL     DFDNPD        LDDNPD
     C                   Z-ADD     DFDNFS        LDDNFS
     C                   MOVEL     DFDNFN        LDDNFN
      *
     C                   OUT       LDA
      *
      *
      * MOVE THE 80-CHARACTER COMPILE TIME ARRAY 'CMD' TO A
      * 80-CHARACTER FIELD FOR SUBMITTING HP416CL TO QCMDEXC.
      *
      *
     C                   MOVEA     XXJOBQ        CMD(50)
     C                   MOVEA     CMD           CMDSBM
      *
     C                   CALL      'QCMDEXC'
     C                   PARM                    CMDSBM
     C                   PARM                    LENGTH
      *
      * ISSUE STANDARD REQUEST SUBMISSION MESSAGE
      *
     C                   ADD       1             ERROR
     C                   MOVEL     'HS09003'     MGID(E)
     C                   ENDIF                                                  IF F8=SBMJOB
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $F4S1 - F4 PROMPT ON SCREEN 1
      *----------------------------------------------------------------
      *
     C     $F4S1         BEGSR
      *
     C                   SELECT
      *
      * IF USER CODE
      *
     C     FLD           WHENEQ    'DFUSCD'
     C                   CALL      'HP589'
     C                   PARM      *BLANK        XXUSCD
     C                   PARM      *BLANK        XXUSNM
     C                   SETON                                        50
     C     XXUSCD        IFNE      *BLANK                                       IF SELECTED
     C                   MOVEL     XXUSCD        DFUSCD
     C                   MOVEL     XXUSNM        DFUSNM
     C                   ENDIF                                                  IF SELECTED
      *
      * MOVEMENT TYPE
      *
     C     FLD           WHENEQ    'DFMTCD'
     C                   CALL      'HP5001'
     C                   PARM      *BLANK        XXMTCD
     C                   PARM      *BLANK        XXMTDS
     C                   SETON                                        51
     C     XXMTCD        IFNE      *BLANK                                       IF SELECTED
     C                   MOVEL     XXMTCD        DFMTCD
     C                   MOVEL     XXMTDS        DFMTDS
     C                   ENDIF                                                  IF SELECTED
      *
      *
      * ORIGIN BUSINESS OFFICE
      *
     C     FLD           WHENEQ    'DFORBO'
     C                   CALL      'HP503'
     C                   PARM      *BLANK        XXFSBO
     C                   PARM      *BLANK        XXBODS
     C                   PARM                    SVDVCD
     C                   SETON                                        52
     C     XXFSBO        IFNE      *BLANK                                       IF SELECTED
     C                   MOVEL     XXFSBO        DFORBO
     C                   MOVEL     XXBODS        DFORBD
     C                   ENDIF                                                  IF SELECTED
      *
      * ORIGIN PRODUCTION TYPE
      *
     C     FLD           WHENEQ    'DFORPT'
     C                   CALL      'HP504'
     C                   PARM      *BLANK        XXPTCD
     C                   PARM      *BLANK        XXPTDS
     C                   SETON                                        53
     C     XXPTCD        IFNE      *BLANK                                       IF SELECTED
     C                   MOVEL     XXPTCD        DFORPT
     C                   MOVEL     XXPTDS        DFORTD
     C                   ENDIF                                                  IF SELECTED
      *
      * ORIGIN PRODUCTION PHASE
      *
     C     FLD           WHENEQ    'DFORPP'
     C                   CALL      'HP507'
     C                   PARM                    XXPPCD
     C                   PARM                    XXPPDS
     C                   SETON                                        54
     C     XXPPCD        IFNE      *BLANK                                       IF SELECTED
     C                   MOVEL     XXPPCD        DFORPP
     C                   MOVEL     XXPPDS        DFORPD
     C                   ENDIF                                                  IF SELECTED
      *
      * ORIGIN FARM SITE
      *
     C     FLD           WHENEQ    'DFORFS'
     C                   CALL      'HP5040'
     C                   PARM      *blank        xxaist
     C                   PARM      0             WKFSCD
     C                   PARM      *BLANK        XXFSNM
     C                   SETON                                        55
     C     WKFSCD        IFNE      0                                            IF SELECTED
     C                   Z-ADD     WKFSCD        DFORFS
     C                   MOVEL     XXFSNM        DFORFN
     C                   ENDIF                                                  IF SELECTED
      *
      *
      * DESTINATION BUSINESS OFFICE
      *
     C     FLD           WHENEQ    'DFDNBO'
     C                   CALL      'HP503'
     C                   PARM      *BLANK        XXFSBO
     C                   PARM      *BLANK        XXBODS
     C                   PARM                    SVDVCD
     C                   SETON                                        56
     C     XXFSBO        IFNE      *BLANK                                       IF SELECTED
     C                   MOVEL     XXFSBO        DFDNBO
     C                   MOVEL     XXBODS        DFDNBD
     C                   ENDIF                                                  IF SELECTED
      *
      * DESTINATION PRODUCTION TYPE
      *
     C     FLD           WHENEQ    'DFDNPT'
     C                   CALL      'HP504'
     C                   PARM      *BLANK        XXPTCD
     C                   PARM      *BLANK        XXPTDS
     C                   SETON                                        57
     C     XXPTCD        IFNE      *BLANK                                       IF SELECTED
     C                   MOVEL     XXPTCD        DFDNPT
     C                   MOVEL     XXPTDS        DFDNTD
     C                   ENDIF                                                  IF SELECTED
      *
      * DESTINATION PRODUCTION PHASE
      *
     C     FLD           WHENEQ    'DFDNPP'
     C                   CALL      'HP507'
     C                   PARM                    XXPPCD
     C                   PARM                    XXPPDS
     C                   SETON                                        58
     C     XXPPCD        IFNE      *BLANK                                       IF SELECTED
     C                   MOVEL     XXPPCD        DFDNPP
     C                   MOVEL     XXPPDS        DFDNPD
     C                   ENDIF                                                  IF SELECTED
      *
      * DESTINATION FARM SITE
      *
     C     FLD           WHENEQ    'DFDNFS'
     C                   CALL      'HP5040'
     C                   PARM      *blank        xxaist
     C                   PARM      0             WKFSCD
     C                   PARM      *BLANK        XXFSNM
     C                   SETON                                        59
     C     WKFSCD        IFNE      0                                            IF SELECTED
     C                   Z-ADD     WKFSCD        DFDNFS
     C                   MOVEL     XXFSNM        DFDNFN
     C                   ENDIF                                                  IF SELECTED
      *
      * CUSTOMER
      *
     C     FLD           WHENEQ    'DFCVNO'
     C                   MOVEL(P)  'C'           INAT1                          SEARCH TYPE
     C                   CALL      'HP513'
     C                   PARM                    BOCOCD                         COMPANY
     C                   PARM                    INAT1                          SEARCH TYPE
     C                   PARM      *ZERO         OUAJD                          JDE NUMBER
     C                   PARM      *BLANK        OUALPH                         JDE NAME
     C                   SETON                                        60
     C     OUAJD         IFNE      0                                            IF SELECTED
     C                   Z-ADD     OUAJD         DFCVNO
     C                   MOVEL     OUALPH        DFCNAM
     C                   ENDIF                                                  IF SELECTED
     C                   OTHER
      *
      * F4 NOT VALID FOR THIS FIELD SO ISSUE MESSAGE
      *
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS09011'     MGID(E)
     C                   ENDIF
     C                   ENDSL
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $DATE -  MANIPULATE DATES WITH DATE UTILITY
      *----------------------------------------------------------------
      *
     C     $DATE         BEGSR
      *
     C                   CALL      'UT80060R'
     C                   PARM                    PFRM8             8 0
     C                   PARM                    PTO8              8 0
     C                   PARM                    PFRFMT            4
     C                   PARM                    PTOFMT            4
     C                   PARM                    PDAYS             4 0
     C                   PARM                    PCODE             1
     C                   PARM                    PRTRN             1
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * $WRMSG - SUBROUTINE TO WRITE ERROR MESSAGES
      *---------------------------------------------------------------
      *
     C     $WRMSG        BEGSR
      *
     C                   CALL      'UT80045J'                           43
     C                   PARM                    MGID
     C                   PARM                    MGDT
     C                   PARM                    ERROR
     C                   PARM      SDPGM         PGM
     C                   PARM      MSGFIL        MSGFL
      *
      * IF CALL TO UT80045J FAILED, A MESSAGE HARDCODED IN THE DSPF
      * COMMAND KEY RECORD WILL BE SET ON. SO, REDISPLAY SCREEN TO USER
      *
     C     *IN43         IFEQ      *ON
     C                   MOVEL     SET1          ROUTNE
     C                   ENDIF
      *
     C                   ENDSR
      *
      *----------------------------------------------------------------
      * $CLMSG - SUBROUTINE TO CLEAR MESSAGES
      *----------------------------------------------------------------
      *
     C     $CLMSG        BEGSR
      *
     C                   CALL      'UT80045J'                           43
     C                   PARM      *BLANK        MGI
     C                   PARM      *BLANK        MGD
     C                   PARM      *ZEROS        ERROR
     C                   PARM      SDPGM         PGM
     C                   PARM      MSGFIL        MSGFL
      *
      * IF CALL TO UT80045J FAILED, A MESSAGE HARDCODED IN THE DSPF
      * COMMAND KEY RECORD WILL BE SET ON. SO, REDISPLAY SCREEN TO USER
      *
     C     *IN43         IFEQ      *ON
     C                   MOVEL     SET1          ROUTNE
     C                   ENDIF
      *
     C                   ENDSR
      *
      *----------------------------------------------------------------
      * $EXIT - SUBROUTINE TO SET ON LR AND END JOB
      *----------------------------------------------------------------
      *
     C     $EXIT         BEGSR
     C                   SETON                                        LR
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $JDEV - SUBROUTINE TO CALL THE EXTERNAL PROGRAM TO VALIDATE JDE
      *         ADDRESS NUMBERS.
      *----------------------------------------------------------------
      *
     C     $JDEV         BEGSR
      *
     C                   MOVEL     NO            JDEFL                          JDE ERRORS
      *
     C                   CALL      'HPJDEV'
     C                   PARM                    MGI                            MSG ID'S
     C                   PARM                    MGD                            MSG DATA FLD
     C                   PARM                    ERROR                          # ERR MSGS
     C                   PARM                    INAJD                          JDE ADDR #
     C                   PARM                    INAT1                          JDE ADDR TYP
     C                   PARM                    JDEFL                          ERROR FLAG
     C                   PARM      *BLANK        OUALPH                         JDE NAME
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * *INZSR - INITIALIZATION SUBROUTINE
      *---------------------------------------------------------------
      *
     C     *INZSR        BEGSR
      *
      * KEY LISTS
      *
      * PARM LISTS
      *
      * DEFINE LDA
     C     *DTAARA       DEFINE    *LDA          LDA
      *
      * Initialize LDA fields for printing
      *
     C                   MOVE      *BLANK        LDHOLD
     C                   Z-ADD     0             LDCOPY
     C                   MOVEL     LDOUTU        LDOUTQ
     C                   MOVEL     '*JOBD'       XXJOBQ           10
     C                   MOVEL     NO            XXNITE            1
     C                   Z-ADD     0             XXCOPY            1 0
      *
      * GET THIS FUNCTION'S NUMBER OF COPIES, HOLD YES/NO
      *
     C     SDPGM         CHAIN     HSP002                             92
     C     *IN92         IFEQ      *OFF
     C                   Z-ADD     FNCOPY        LDCOPY
     C                   MOVEL     FNHOLD        LDHOLD
     C                   ENDIF
      *
     C     LDCOPY        IFEQ      0
     C                   Z-ADD     1             LDCOPY
     C                   ENDIF
      *
     C     LDHOLD        IFEQ      *BLANK
     C                   MOVEL     NO            LDHOLD
     C                   ENDIF
      *
      * GLOBAL DEFINES
      *
     C                   MOVE      *BLANK        PGM              10            USED IN MSG
     C                   MOVE      *BLANK        MSGFL            10            USED IN MSG
     C                   MOVE      *BLANK        ROUTNE            6
     C                   MOVEL     '00'          RTNCD             2            RETURN CODE
      *
      *
      * JDE FIELDS
     C                   MOVE      *BLANK        JDEFL             1            JDE ERR FLAG
     C                   MOVE      *BLANK        INAT1             3            JDE ADDR TYP
     C                   MOVE      *BLANK        OUALPH           40            JDE NAME
     C     *LIKE         DEFINE    DFCVNO        INAJD                          JDE ADDR IN
     C     *LIKE         DEFINE    DFCVNO        OUAJD                          JDR ADDR OUT
      *
      * WORKFIELDS
      *
     C     *LIKE         DEFINE    DFORBO        WKFSBO
     C                   MOVE      *BLANK        WKODFL            1
      *
      * NUMERIC PARM FIELDS
      *
     C     *LIKE         DEFINE    DFORFS        WKFSCD
      *
      * ALPHA PARM FIELDS
      *
     C                   MOVE      *BLANK        XXAIST            1
     C                   MOVE      *BLANK        XXFSCD            5
     C     *LIKE         DEFINE    DFORFN        XXFSNM
     C     *LIKE         DEFINE    DFORBO        XXFSBO
     C     *LIKE         DEFINE    DFORBD        XXBODS
     C     *LIKE         DEFINE    DFORPP        XXPPCD
     C     *LIKE         DEFINE    DFORPD        XXPPDS
     C     *LIKE         DEFINE    DFORPT        XXPTCD
     C     *LIKE         DEFINE    DFORPD        XXPTDS
     C     *LIKE         DEFINE    DFUSCD        XXUSCD
     C     *LIKE         DEFINE    DFUSNM        XXUSNM
     C     *LIKE         DEFINE    DFMTCD        XXMTCD
     C     *LIKE         DEFINE    DFMTDS        XXMTDS
      *
      *
      * INITIALIZE NUMERIC LDA FIELDS
      *
     C                   Z-ADD     0             LDFDYM
     C                   Z-ADD     0             LDFDMD
     C                   Z-ADD     0             LDTDYM
     C                   Z-ADD     0             LDTDMD
      *
     C                   Z-ADD     0             LDFSYM
     C                   Z-ADD     0             LDFSMD
     C                   Z-ADD     0             LDTSYM
     C                   Z-ADD     0             LDTSMD
      *
     C                   Z-ADD     0             LDCVNO
     C                   Z-ADD     0             LDORFS
     C                   Z-ADD     0             LDDNFS
      *
      *
      *  PARMS FOR USE WITH QCMDEXC.  'LENGTH' IS THE LENGTH OF THE
      *  COMMAND THAT WILL BE ISSUED WITH QCMDEXC.  QCMDEXC REQUIRES
      *  THE FIELD CONTAINING LENGTH TO BE DEFINED AS 15 WITH 5 DECIMAL
      *  POSITIONS.  THE FIELD 'CMDSBM' WILL HOLD THE VALUES OF THE
      *  THE SUBMIT JOB COMMAND WHEN PASSED AS A PARM ON THE CALL TO
      *  QCMDEXC.
      *
     C                   Z-ADD     80            LENGTH           15 5          QCMDEXC PARM
     C                   MOVE      *BLANK        CMDSBM           80            QCMDEXC PARM
      *
      * THE FOLLOWING STANDARD CODE MUST BE INCLUDED TO MAKE THE
      * STANDARD ERROR MESSAGE HANDLING PROGRAM FUNCTION PROPERLY.
      * THIS CODE SETS AND CLEARS THE PROGRAM MESSAGE QUEUE.
      *
     C                   Z-ADD     20            MAXMSG            2 0
     C                   MOVEL     '*'           MSGPGM                         SET QUEUE
     C                   EXSR      $CLMSG                                       CLEAR MSG
      *
      * RETRIEVE COMPANY CODE FOR J.D. EDWARDS VERIFICATION
      *
     C     *LOVAL        SETLL     HSP009
     C                   READ      HSP009                                 92
      *
      * EXECUTE THE SECURITY SUBROUTINE TO DETERMINE USER
      * AUTHORIZATIONS.
      *
     C                   MOVEL(P)  SDPGM         WKFNCD           10
     C                   MOVEL     NO            INBOFL
     C                   EXSR      $SEC
     C                   MOVEL     YES           INBOFL
      *
      * IF ERRORS ON INITIAL CALL, SET TERMINAL FLAG TO YES.
      *
     C     SECFL         IFEQ      YES
     C                   MOVEL     YES           TERMFL
     C                   ENDIF
      *
      * SAVE THIS FUNCTION'S DIVISION CODE
      *
     C     *LIKE         DEFINE    OUDVCD        SVDVCD
     C                   MOVEL     OUDVCD        SVDVCD
      *
     C                   ENDSR
      /EJECT
      *****************************************************************
      * COMPILE TIME ARRAY NAMED 'CMD' USED IN QCMDEXC
      *****************************************************************
** CMD TABLE
SBMJOB CMD(CALL PGM(HP416CL)) JOB(DLTMOVES) JOBQ(__________)
