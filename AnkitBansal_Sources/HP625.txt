      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      HOG PRODUCTION
      * PROGRAM:     HP625
      * TITLE:       LISTING OF FUNCTIONS
      * PROGRAMMER:  LEANNE FEDOR
      * CREATED:     02/09/95
      *
      * FUNCTION: BATCH PROGRAM TO LIST THE RECORDS IN THE
      *           FUNCTIONS FILE. CALLED FROM HP425-WORK WITH
      *           FUNCTIONS.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      * 99/99/99  NAME
      *
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     FHSP002    IF   E           K DISK
      *    FUNCTIONS                 (KEY: FNFNCD)
      *
     FQPRINT    O    F  132        PRINTER OFLIND(*INOF)
      *
      /EJECT
      ****************************************************************
      * INPUT SPECS
      ****************************************************************
      *
      *---------------------------------------------------------------
      * STANDARD PROGRAM STATUS DATA STRUCTURE
      *---------------------------------------------------------------
      *    EXTERNALLY DEFINED AS UTPGFR (RECORD FORMAT: PGMDSR)
     D PGMDS         ESDS                  EXTNAME(UTPGFR)
      *
      *---------------------------------------------------------------
      *  NAMED CONSTANTS
      *---------------------------------------------------------------
      *
     D YES             C                   CONST('Y')
     D NO              C                   CONST('N')
      *
     D                 DS
     D  DUMMYD                 1      1
      *  THE PRECEDING DUMMY DATA STRUCTURE IS ONLY TO KEEP THE I-SPEC
      *  INFO FROM RUNNING OVER INTO THE 'CALC SPECS' ON THE COMPILE
      *  LISTING.  IT SERVES NO OTHER PURPOSE AND CAN BE DELETED
      *  AT ANY TIME!!!
      /EJECT
      ****************************************************************
      * MAIN LINE
      ****************************************************************
      *
     C                   EXCEPT    HRHDR
     C     *LOVAL        SETLL     HSP002
     C                   SETOFF                                       92
      *
     C     *IN92         DOUEQ     *ON                                          MAIN DO
     C                   READ      HSP002                                 92
     C     *IN92         IFEQ      *OFF                                         IF NOT EOF
      *
     C     *INOF         IFEQ      *ON
     C                   EXCEPT    HRHDR
     C                   ENDIF
     C                   EXCEPT    DTLINE
      *
     C                   MOVEL     YES           PRTFLG
      *
     C                   ENDIF                                                  IF NOT EOF
     C                   ENDDO                                                  MAIN DO
      *
      *---------------------------------------------------------------
      *  EOF PROCESSING
      *---------------------------------------------------------------
      *  IF THE FILE WAS EMPTY AND NO RECORDS WERE PRINTED,
      *  PRINT THE STANDARD 'NO DATA...' ERROR MESSAGE
      *
     C     PRTFLG        IFEQ      NO
     C                   EXCEPT    NODATA
     C                   ENDIF
      *
     C                   SETON                                        LR
      /EJECT
      *---------------------------------------------------------------
      * *INZSR - INITIALIZATION SUBROUTINE
      *---------------------------------------------------------------
      *
     C     *INZSR        BEGSR
      *
      * SET PRINT FLAG TO DETERMINE IF ANY RECORDS WERE PRINTED
      * RETRIEVE TIME FOR REPORT HEADING
      *
     C                   MOVEL     NO            PRTFLG            1
     C                   MOVEL     *ALL'-'       DASH            100
     C                   TIME                    RTIME             6 0
      *
     C                   ENDSR
      /EJECT
      *-------------------------------------------------------------
      * REPORT HEADING LINES
      *-------------------------------------------------------------
      *
     OQPRINT    E            HRHDR          1 01
     O                                            5 'HP625'
     O                                           58 'HOG PRODUCTION SYSTEM'
     O                                           90 'DATE'
     O                       UDATE         Y    100
      *
     O          E            HRHDR          1
     O                       SDUSR               10
     O                                           57 'LISTING OF FUNCTIONS'
     O                                           90 'TIME'
     O                       RTIME              100 '  :  :  '
      *
     O          E            HRHDR          2
     O                                           90 'PAGE'
     O                       PAGE          Z    100
      *
     O          E            HRHDR          1
     O                                           49 'SECURED'
     O                                           60 'FUNCTION'
     O                                           68 'B/O'
     O                                           80 'DIVISION'
     O                                           91 'NUMBER OF'
     O                                           99 'HOLD'
      *
     O          E            HRHDR          1
     O                                            8 'FUNCTION'
     O                                           22 'DESCRIPTION'
     O                                           50 'FUNCTION'
     O                                           58 'TYPE'
     O                                           70 'REQUIRED'
     O                                           78 'CODE'
     O                                           89 'COPIES'
     O                                          100 'OUTPUT'
      *
     O          E            HRHDR          1
     O                       DASH               100
      *
      *-------------------------------------------------------------
      *  DETAIL LINE
      *-------------------------------------------------------------
      *
     O          E            DTLINE      1
     O                       FNFNCD              10
     O                       FNFNDS              41
     O                       FNSFFL              46
     O                       FNFNTY              57
     O                       FNBOFL              66
     O                       FNDVCD              79
     O                       FNCOPY        K     87
     O                       FNHOLD              97
      *
      *-------------------------------------------------------------
      * NO DATA MESSAGE LINE
      *-------------------------------------------------------------
      *
     O          E            NODATA      1
     O                                           19 'NO DATA MEETS YOUR'
     O                                           38 'SELECTION CRITERIA'
