/***************************************************************** */
/*                                                                 */
/*  SYSTEM:          TRIUMPH FOODS                                 */
/*  PROGRAM NUMBER:  TF433CL                                       */
/*  PROGRAM NAME:    WORK WITH INVOICES                            */
/*  PROGRAMMER:      LEANNE FEDOR                                  */
/*  CREATE DATE:     03/15/05                                      */
/*                                                                 */
/*  PRINTS LISTING(S) FROM WORK WITH.                              */
/*                                                                 */
/*******************************************************************/
/* MODIFIED:                                                       */
/*                                                                 */
/* 05/07/09  LEANNE RAMSEY                                         */
/*           CHANGED PRINT LOGIC TO MATCH MEAT COSTING.            */
/*******************************************************************/

             PGM

             DCL        VAR(&QDATA) TYPE(*CHAR) LEN(201)

             DCL        VAR(&OUTQ) TYPE(*CHAR) LEN(10)
             DCL        VAR(&HOLD) TYPE(*CHAR) LEN(4)
             DCL        VAR(&SAVE) TYPE(*CHAR) LEN(4)
             DCL        VAR(&COPY) TYPE(*DEC)  LEN(1)

             DCL        VAR(&LDFCYMD) TYPE(*DEC) LEN(8)
             DCL        VAR(&LDTCYMD) TYPE(*DEC) LEN(8)
             DCL        VAR(&LDDCYMD) TYPE(*DEC) LEN(8)
             DCL        VAR(&LDPCYMD) TYPE(*DEC) LEN(8)
             DCL        VAR(&LDINSN)  TYPE(*DEC) LEN(7)

             DCL        VAR(&LDTASK)   TYPE(*CHAR) LEN(1)
             DCL        VAR(&LDYNFL)   TYPE(*CHAR) LEN(1)
             DCL        VAR(&LDINSTCD) TYPE(*CHAR) LEN(1)
             DCL        VAR(&LDCFQCD)  TYPE(*CHAR) LEN(1)

             CHGVAR     VAR(&LDTASK)   VALUE(%SST(*LDA 1 1))
             CHGVAR     VAR(&LDINSN)  VALUE(%SST(*LDA 2 7))
             CHGVAR     VAR(&LDYNFL)   VALUE(%SST(*LDA 9 1))
             CHGVAR     VAR(&LDFCYMD) VALUE(%SST(*LDA 16 8))
             CHGVAR     VAR(&LDTCYMD) VALUE(%SST(*LDA 30 8))
             CHGVAR     VAR(&LDINSTCD) VALUE(%SST(*LDA 38 1))
             CHGVAR     VAR(&LDCFQCD)  VALUE(%SST(*LDA 39 1))
             CHGVAR     VAR(&LDPCYMD) VALUE(%SST(*LDA 46 8))
             CHGVAR     VAR(&LDDCYMD) VALUE(%SST(*LDA 60 8))

/******************************************************************/
/* OVERRIDE PRINT FILE                                            */
/******************************************************************/

 OUTQ:       CHGVAR     VAR(&OUTQ) VALUE(%SST(*LDA 401 10))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(*JOB))

 HOLD:       CHGVAR     VAR(&HOLD) VALUE(%SST(*LDA 411 4))
             IF         COND(&HOLD *NE '*YES' *AND &HOLD *NE '*NO ') +
                          THEN(CHGVAR VAR(&HOLD) VALUE(*NO))

 COPY:       CHGVAR     VAR(&COPY) VALUE(%SST(*LDA 419 1))
             IF         COND(&COPY = 0) THEN(CHGVAR VAR(&COPY) +
                          VALUE(1))

 SAVE:       CHGVAR     VAR(&SAVE) VALUE(%SST(*LDA 415 4))
             IF         COND(&SAVE *NE '*YES' *AND &SAVE *NE '*NO ') +
                          THEN(CHGVAR VAR(&SAVE) VALUE(*NO))

/******************************************************************/
/* PRINT THE INVOICE                                             */
/******************************************************************/

/* P = PRINT PREVIEW OF UNPOSTED INVOICE                          */
/* F = POST/PRINT 'FINAL' INVOICE                                 */
/* R = REPRINT POSTED INVOICE                                     */

             IF         COND(&LDTASK *EQ 'P' *OR &LDTASK *EQ 'F' *OR +
                          &LDTASK *EQ 'R') THEN(DO)

             OVRPRTF    FILE(PRINT132) OUTQ(&OUTQ) COPIES(&COPY) +
                          HOLD(&HOLD) SAVE(&SAVE)

             CALL       PGM(TF634)     /* Print Invoice */
             DLTOVR     FILE(PRINT132)
             ENDDO

/******************************************************************/
/* IF PRINTING A "LISTING"                                        */
/******************************************************************/

             IF         COND(&LDTASK *EQ 'L') THEN(DO)

/* BUILD QUERY                                                         */

             CHGVAR     VAR(&QDATA) VALUE(' ')
             CHGVAR     VAR(%SST(&QDATA 1 12)) VALUE('IHINSN *NE 0')

/* IF 'INCLUDE POSTED INVOICES' IS NO                                */

             IF         COND(&LDYNFL *EQ 'N') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 14 21)) VALUE('*AND IHINSTCD +
                          *NE "P"')
             ENDDO

/* IF FROM INVOICE DATE                                             */

             IF         COND(&LDFCYMD *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 36 15)) VALUE('*AND IHINDT *GE')
             CHGVAR     VAR(%SST(&QDATA 52 8)) VALUE(&LDFCYMD)
             ENDDO

/* IF TO INVOICE DATE                                               */

             IF         COND(&LDTCYMD *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 61 15)) VALUE('*AND IHINDT *LE')
             CHGVAR     VAR(%SST(&QDATA 77 8)) VALUE(&LDTCYMD)
             ENDDO

/* IF INVOICE STATUS CODE                                              */

             IF         COND(&LDINSTCD *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 86 19)) VALUE('*AND IHINSTCD *EQ "')
             CHGVAR     VAR(%SST(&QDATA 105 1)) VALUE(&LDINSTCD)
             CHGVAR     VAR(%SST(&QDATA 106 1)) VALUE('"')
             ENDDO

/* IF CHARGE FREQUENCY                                                 */

             IF         COND(&LDCFQCD *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 108 18)) VALUE('*AND IHCFQCD *EQ "')
             CHGVAR     VAR(%SST(&QDATA 126 1)) VALUE(&LDCFQCD)
             CHGVAR     VAR(%SST(&QDATA 127 1)) VALUE('"')
             ENDDO

/* IF DUE DATE                                                      */

             IF         COND(&LDDCYMD *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 129 15)) VALUE('*AND IHDUDT *EQ')
             CHGVAR     VAR(%SST(&QDATA 145 8)) VALUE(&LDDCYMD)
             ENDDO

/* IF POSTED DATE                                                   */

             IF         COND(&LDPCYMD *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 154 15)) VALUE('*AND IHPODT *EQ')
             CHGVAR     VAR(%SST(&QDATA 170 8)) VALUE(&LDPCYMD)
             ENDDO

/* IF INVOICE NUMBER                                                */

             IF         COND(&LDINSN *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 179 15)) VALUE('*AND IHINSN *EQ')
             CHGVAR     VAR(%SST(&QDATA 195 7)) VALUE(&LDINSN)
             ENDDO

/* OVERRIDE INVOICE FILE                                      */

             OVRDBF     FILE(TFP020) SHARE(*YES)
             OPNQRYF    FILE((TFP020)) OPTION(*INP) QRYSLT(&QDATA) +
                          KEYFLD(*FILE) SEQONLY(*YES)

 /* CALL PRINT PROGRAM                                                    */

             OVRPRTF    FILE(PRINT132) OUTQ(&OUTQ) COPIES(&COPY) +
                          HOLD(&HOLD) SAVE(&SAVE)

             CALL       PGM(TF633)

             CLOF       OPNID(TFP020)
             DLTOVR     FILE(TFP020)
             ENDDO

/*---------------------------------------------------------------------*/
/* CLEAN-UP, GET OUT                                                   */
/*---------------------------------------------------------------------*/

             DLTOVR     FILE(*ALL)

             ENDPGM

