     A*%%TS  SD  20080909  133802  ISLRAMS     REL-V5R4M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD4045
     A*  TITLE:         WORK WITH CHECK LOCKS
     A*  PROGRAMMER:    LEANNE RAMSEY
     A*  DATE:          03/12/07
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A                                      CF05(05 'REFRESH')
     A                                      CF20(20 'WRKSPLF')
     A*
     A*
     A*---------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP4045S1                  SFL
     A*%%TS  SD  20070312  130156  ISLRAMS     REL-V5R3M0  5722-WDS
     A  85                                  SFLNXTCHG
     A            HDRRN1         4Y 0H
     A            HDCHDT    R        H      REFFLD(CHCHDT)
     A            HDLDT     R        H      REFFLD(@@LDT)
     A            D1OPT          2A  B 11  2CHGINPDFT(UL)
     A  30                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A N30                                  DSPATR(HI)
     A            D1CHNO    R        O 11  7REFFLD(CHCHNO)
     A                                      EDTCDE(M)
     A            D1CHDTMDY R        O 11 17REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  ')
     A            D1CVNO    R        O 11 27REFFLD(CHCVNO)
     A                                      EDTCDE(M)
     A            D1LUSCD   R        O 11 38REFFLD(@@USCD)
     A            D1LDTMDY  R        O 11 50REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  ')
     A            D1LTM     R        O 11 60REFFLD(@@UPTM)
     A                                      EDTWRD('  :  :  ')
     A*
     A*---------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP4045C1                  SFLCTL(HP4045S1)
     A*%%TS  SD  20080909  133802  ISLRAMS     REL-V5R4M0  5722-WDS
     A                                      SFLSIZ(0050)
     A                                      SFLPAG(0010)
     A N82                                  ROLLUP(25 'PAGE FORWARD')
     A                                      SETOF(30)
     A                                      OVERLAY
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 27'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  2 27'WORK WITH CHECK LOCKS'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  4  2'This function is to be used when y-
     A                                      our session dropped during the HPS'
     A                                      COLOR(BLU)
     A                                  5  2'Check function and you cannot get -
     A                                      back into your check.'
     A                                      COLOR(BLU)
     A                                  7  2'4=Unlock'
     A                                  9  9'Check'
     A                                      DSPATR(HI)
     A                                  9 19'Check'
     A                                      DSPATR(HI)
     A                                  9 27'Customer'
     A                                      DSPATR(HI)
     A                                  9 51'Locked'
     A                                      DSPATR(HI)
     A                                  9 61'Locked'
     A                                      DSPATR(HI)
     A                                 10  2'Opt'
     A                                      DSPATR(HI)
     A                                 10  8'Number'
     A                                      DSPATR(HI)
     A                                 10 19'Date'
     A                                      DSPATR(HI)
     A                                 10 28'Number'
     A                                      DSPATR(HI)
     A                                 10 38'Locked By'
     A                                      DSPATR(HI)
     A                                 10 52'Date'
     A                                      DSPATR(HI)
     A                                 10 62'Time'
     A                                      DSPATR(HI)
     A*
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R HP4045K1
     A*%%TS  SD  20011024  165925  ISLRAMS     REL-V4R4M0  5769-PW1
     A  43                             21  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A                                 23 11'F5=Refresh'
     A                                 23 65'F20=Spool Files'
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *---------------------------------------------------------------
     A          R HP4045ES                  SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE CONTROL FORMAT
      *---------------------------------------------------------------
     A          R HP4045EC                  SFLCTL(HP4045ES)
      *
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
