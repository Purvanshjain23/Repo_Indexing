     A*%%TS  SD  20020429  105714  ISLRAMS     REL-V5R1M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD418
     A*  TITLE:         WORK WITH EXPENSE TICKETS
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  DATE:          04/04/94
     A*  MODIFIED BY:   BARB GUTIERREZ         3/07/2016
     A*                 ADDED BUSINESS OFFICE SELECTION
     A*                 E005190
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A N99                                  CF04(04 'PROMPT')
     A N99                                  CF05(05 'REFRESH')
     A N99                                  CF06(06 'CREATE')
     A N99                                  CF20(20 'WRKSPLF')
     A*
     A*
     A*--------------------------------------------------------------*
     A* SUBFILE FORMAT ONE
     A*--------------------------------------------------------------*
     A          R HP418S1                   SFL
     A*%%TS  SD  20020429  101135  ISLRAMS     REL-V5R1M0  5722-WDS
     A  85                                  SFLNXTCHG
     A            HDRRN1         4Y 0H
     A            HDPOFL    R        H      REFFLD(XSPOFL)
     A            DFOPT          2A  B 12  2CHGINPDFT(UL)
     A  30                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A N30                                  DSPATR(HI)
     A  99                                  DSPATR(PR)
     A            DFXTSN    R        O 12  7REFFLD(XTXTSN)
     A                                      EDTCDE(M)
     A            DFBMDY    R        O 12 18REFFLD(@@DT06)
     A                                      EDTCDE(Y)
     A            DFEXCD    R        O 12 29REFFLD(EXEXCD)
     A            DFEXDS    R        O 12 37REFFLD(EXEXDS)
     A            DFXSCD    R        O 12 60REFFLD(XTXSCD)
     A            DFALFL    R        O 12 72REFFLD(XTALFL)
     A*
     A*--------------------------------------------------------------*
     A* SUBFILE CONTROL FORMAT ONE
     A*--------------------------------------------------------------*
     A          R HP418C1                   SFLCTL(HP418S1)
     A*%%TS  SD  20020429  105714  ISLRAMS     REL-V5R1M0  5722-WDS
     A                                      SFLSIZ(0050)
     A                                      SFLPAG(0010)
     A N82N99                               ROLLUP(25 'PAGE FORWARD')
     A                                      SETOF(33)
     A                                      SETOF(53)
     A                                      SETOF(55)
     A                                      SETOF(57)
     A                                      RTNCSRLOC(&RCD &FLD)
     A                                      OVERLAY
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 26'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  2 24'WORK WITH EXPENSE TICKETS'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  4  2'Business Office .............. ?'
     A            C1FSBO    R        B  4 35REFFLD(HGFSBO)
     A  31
     AO 51                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A N31                                  DSPATR(HI)
     A  99                                  DSPATR(PR)
     A            C1BODS    R        O  4 41REFFLD(BOBODS)
     A                                  5  2'Show current tickets only ..'
     A                                  5 31'?'
     A                                      DSPATR(HI)
     A            C1CURFL   R        B  5 33REFFLD(HSRREF/@@FLAG *LIBL/HSPREF)
     A  53
     AO 33                                  DSPATR(PC)
     A  33                                  DSPATR(RI)
     A N33                                  DSPATR(HI)
     A  99                                  DSPATR(PR)
     A                                  7  2'2=Revise'
     A                                  7 12'4=Delete'
     A                                  7 22'5=View'
     A                                  8 31'?'
     A                                      DSPATR(HI)
     A                                  8 60'?'
     A                                      DSPATR(HI)
     A                                  8 72'?'
     A                                      DSPATR(HI)
     A                                  9  8'Ticket'
     A                                      DSPATR(HI)
     A                                  9 20'Begin'
     A                                      DSPATR(HI)
     A                                  9 28'Expense'
     A                                      DSPATR(HI)
     A                                  9 67'Allocation'
     A                                      DSPATR(HI)
     A                                 10  2'Opt'
     A                                      DSPATR(HI)
     A                                 10  8'Number'
     A                                      DSPATR(HI)
     A                                 10 20'Date'
     A                                      DSPATR(HI)
     A                                 10 29'Code'
     A                                      DSPATR(HI)
     A                                 10 37'Description'
     A                                      DSPATR(HI)
     A                                 10 58'Status'
     A                                      DSPATR(HI)
     A                                 10 70'Flag'
     A                                      DSPATR(HI)
     A            C1XTSN    R        B 11  7REFFLD(XTXTSN)
     A                                      DSPATR(HI)
     A  99                                  DSPATR(PR)
     A            C1EXCD    R        B 11 29REFFLD(XTEXCD)
     A                                      DSPATR(HI)
     A  53                                  DSPATR(PC)
     A  99                                  DSPATR(PR)
     A            C1XSCD    R        B 11 60REFFLD(XTXSCD)
     A                                      DSPATR(HI)
     A  55                                  DSPATR(PC)
     A  99                                  DSPATR(PR)
     A                                      TEXT('TICKET STATUS')
     A            C1ALFL    R        B 11 72REFFLD(XTALFL)
     A                                      DSPATR(HI)
     A  57                                  DSPATR(PC)
     A  99                                  DSPATR(PR)
     A*
     A*--------------------------------------------------------------*
     A* COMMAND KEY RECORD 1
     A*--------------------------------------------------------------*
     A          R HP418K1
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A                                 23 11'F4=Prompt'
     A                                 23 22'F5=Refresh'
     A                                 23 34'F6=Create'
     A                                 23 45'F20=Spool Files'
      *--------------------------------------------------------------*
      * ERROR MESSAGE SUBFILE FORMAT
      *--------------------------------------------------------------*
     A          R HP418ES                   SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *                                                                *********
      *--------------------------------------------------------------*
      * ERROR MESSAGE SUBFILE CONTROL FORMAT
      *--------------------------------------------------------------*
     A          R HP418EC                   SFLCTL(HP418ES)
      *
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
