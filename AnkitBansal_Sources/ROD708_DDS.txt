     A*%%TS  SD  20090302  133301  ISLRAMS     REL-V5R4M0  5722-WDS
     A*****************************************************************
     A*
     A*  ENVIRONMENT:   PORK DIVISION
     A*  SYSTEM:        RESOURCE OPTIMIZATION
     A*  DISPLAY FILE:  ROD708
     A*  TITLE:         VIEW COMPONENT ITEMS
     A*  PROGRAMMER:    LEANNE RAMSEY
     A*  CREATED:       02/13/07
     A*
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/ROPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A                                      CF05(05 'REFRESH')
     A                                      CF07(07 'LISTING')
     A                                      CF13(13 'FOLD/DROP')
     A                                      CF17(17 'PRINT OPTIONS')
     A                                      CF15(15 'VIEW YEILD')
     A                                      CF20(20 'WRKSPLF')
     A*
     A*
     A*---------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*---------------------------------------------------------------
     A          R RO708S1                   SFL
     A*%%TS  SD  20070305  103038  ISLRAMS     REL-V5R3M0  5722-WDS
     A  85                                  SFLNXTCHG
     A            HDRRN1         4Y 0H
     A            D1BOMTY   R        O 15  4REFFLD(ROREF/@@BOMTY *LIBL/ROPREF)
     A            D1CIITCD  R        O 15  9REFFLD(ROREF/@@CIITCD *LIBL/ROPREF)
     A                                      EDTCDE(M)
     A            D1CIBSPR  R        O 15 19REFFLD(ROREF/@@BSPR *LIBL/ROPREF)
     A                                      EDTCDE(K)
     A            D1CICRVL  R        O 15 31REFFLD(ROREF/@@CRVL *LIBL/ROPREF)
     A                                      EDTCDE(K)
     A            D1CGCD    R        O 15 46REFFLD(ROREF/@@CGCD *LIBL/ROPREF)
     A            D1SYPC    R        O 15 55REFFLD(ROREF/@@SYPC *LIBL/ROPREF)
     A                                      EDTCDE(K)
     A            D1CSPC    R        O 15 66REFFLD(ROREF/@@CSPC *LIBL/ROPREF)
     A                                      EDTCDE(K)
     A            D1CIITDS  R        O 16 11REFFLD(ROREF/@@ITDS *LIBL/ROPREF)
     A                                      COLOR(PNK)
     A*
     A*---------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*---------------------------------------------------------------
     A          R RO708C1                   SFLCTL(RO708S1)
     A*%%TS  SD  20090302  133301  ISLRAMS     REL-V5R4M0  5722-WDS
     A                                      SFLSIZ(0050)
     A                                      SFLPAG(0003)
     A N82                                  ROLLUP(25 'PAGE FORWARD')
     A                                      OVERLAY
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A  78                                  SFLFOLD(CF13)
     A  79                                  SFLDROP(CF13)
     A                                      SFLMODE(&FOLD)
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            FOLD           1A  H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 25'RESOURCE OPTIMIZATION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USR)
     A                                      DSPATR(HI)
     A                                  2 29'VIEW COMPONENT ITEMS'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@JOB)
     A                                      DSPATR(HI)
     A                                  4  2'Company ..:'
     A            C1CONO    R        O  4 14REFFLD(ROREF/@@CONO *LIBL/ROPREF)
     A                                      DSPATR(HI)
     A                                      EDTCDE(K)
     A                                  4 31'Item code:'
     A            C1ITCD    R        O  4 42REFFLD(ROREF/@@ITCD *LIBL/ROPREF)
     A                                      DSPATR(HI)
     A                                      EDTCDE(Z)
     A            C1ITDS    R        O  4 50REFFLD(ROREF/@@ITDS *LIBL/ROPREF)
     A                                      DSPATR(HI)
     A                                  5  2'W/E date .:'
     A            C1WEMDY   R        O  5 14REFFLD(@@DT06)
     A                                      DSPATR(HI)
     A                                      EDTWRD('  /  /  -')
     A                                  5 28'Primal group:'
     A            C1PGCD    R        O  5 42REFFLD(ROREF/@@PGCD *LIBL/ROPREF)
     A                                      DSPATR(HI)
     A                                  5 50'Prod dept:'
     A            C1PDCD    R        O  5 61REFFLD(ROREF/@@PDCD *LIBL/ROPREF)
     A                                      DSPATR(HI)
     A                                  5 68'Line:'
     A            C1PLCD    R        O  5 74REFFLD(ROREF/@@PLCD *LIBL/ROPREF)
     A                                      DSPATR(HI)
     A                                  6  2'Sales yld%:'
     A            C1SYPC    R        O  6 14REFFLD(ROREF/@@SYPC *LIBL/ROPREF)
     A                                      EDTCDE(K)
     A                                      DSPATR(HI)
     A                                  6 26'Base item desc:'
     A            C1BIDS    R        O  6 42REFFLD(ROREF/@@BIDS *LIBL/ROPREF)
     A                                      DSPATR(HI)
     A                                  7  2'Base price:'
     A            C1BSPR    R        O  7 14REFFLD(ROREF/@@BSPR *LIBL/ROPREF)
     A                                      DSPATR(HI)
     A                                      EDTCDE(K)
     A                                  7 34'Meat cost group code:'
     A            C1CGCD    R        O  7 56REFFLD(ROREF/@@CGCD *LIBL/ROPREF)
     A                                      DSPATR(HI)
     A                                  8  2'Credit val:'
     A            C1CRVL    R        O  8 14REFFLD(ROREF/@@CRVL *LIBL/ROPREF)
     A                                      EDTCDE(K)
     A                                      DSPATR(HI)
     A                                  8 33'Produced value amount:'
     A            C1PUAM    R        O  8 59REFFLD(ROREF/@@PUAM *LIBL/ROPREF)
     A                                      EDTCDE(K)
     A                                      DSPATR(HI)
     A                                  9  2'Yld meat .:'
     A            C1YMCO    R        O  9 14REFFLD(ROREF/@@YMCO *LIBL/ROPREF)
     A                                      EDTCDE(K)
     A                                      DSPATR(HI)
     A                                  9 37'Produced price/lb:'
     A            C1PUPR    R        O  9 67REFFLD(ROREF/@@PUPR *LIBL/ROPREF)
     A                                      EDTCDE(K)
     A                                      DSPATR(HI)
     A                                 10  2'Std meat .:'
     A            C1MCO     R        O 10 14REFFLD(ROREF/@@YMCO *LIBL/ROPREF)
     A                                      DSPATR(HI)
     A                                      EDTCDE(K)
     A                                 10 39'Produced pounds:'
     A            C1PULB    R        O 10 56REFFLD(ROREF/@@PULB *LIBL/ROPREF)
     A                                      EDTCDE(K)
     A                                      DSPATR(HI)
     A                                 12  3'BOM'
     A                                      DSPATR(HI)
     A                                 12  8'Component'
     A                                      DSPATR(HI)
     A                                 12 35'Credit'
     A                                      DSPATR(HI)
     A                                 12 44'Meat Cost'
     A                                      DSPATR(HI)
     A                                 12 56'Sales'
     A                                      DSPATR(HI)
     A                                 12 66'Consumed'
     A                                      DSPATR(HI)
     A                                 13  2'Type'
     A                                      DSPATR(HI)
     A                                 13 11'Item'
     A                                      DSPATR(HI)
     A                                 13 19'Base Price'
     A                                      DSPATR(HI)
     A                                 13 36'Value'
     A                                      DSPATR(HI)
     A                                 13 46'Group'
     A                                      DSPATR(HI)
     A                                 13 56'Yield %'
     A                                      DSPATR(HI)
     A                                 13 69'%'
     A                                      DSPATR(HI)
     A            C1BOMTY   R        B 14  4REFFLD(ROREF/@@BOMTY *LIBL/ROPREF)
     A                                      DSPATR(HI)
     A            C1CIITCD  R        B 14  9REFFLD(ROREF/@@CIITCD *LIBL/ROPREF)
     A                                      EDTCDE(M)
     A                                      DSPATR(HI)
     A*
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R RO708K1
     A*%%TS  SD  20080103  163008  ISLRAMS     REL-V5R4M0  5722-WDS
     A  43                             21  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 22  2'F3=Exit'
     A                                 22 12'F5=Refresh'
     A                                 22 26'F7=Reports'
     A                                 22 62'F17=Print Options'
     A                                 23  2'F13=Fold/Unfold'
     A                                 23 26'F15=View Ham Group Yield Breakdown'
     A                                 23 62'F20=Spool Files'
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *---------------------------------------------------------------
     A          R RO708ES                   SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE CONTROL FORMAT
      *---------------------------------------------------------------
     A          R RO708EC                   SFLCTL(RO708ES)
      *
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
