     A*%%TS  SD  20180226  153304  ISJBADE     REL-V7R1M0  5770-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD4084
     A*  TITLE:         UT: CLEAR MOVEMENT INTERFACE FILES TO ALLOW A REDO
     A*  PROGRAMMER:    BRAD BADEN
     A*  PROJECT:       E010812
     A*  DATE:          09/13/2017
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A                                      CF08(08 'SUBMIT')
     A                                      CF20(20 'WRKSPLF')
     A*
     A*--------------------------------------------------------------*
     A* RECORD FORMAT ONE
     A*--------------------------------------------------------------*
     A          R HP4084R1
     A*%%TS  SD  20170913  085409  ISJBADE     REL-V5R4M0  5722-WDS
     A                                      OVERLAY
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 27'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  2 12'CLEAR SCHEDULED MOVEMENT FILES TO -
     A                                      ALLOW AN UPLOAD REDO'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  5  2'This function will clear the HPS s-
     A                                      cheduled movement files allowing yo-
     A                                      u'
     A                                  6  2'to start over/redo an upload.'
     A                                  8  2'The following files are cleared:'
     A                                  9  8'1) Receiving File'
     A                                 10  8'2) Editing File'
     A                                 12  2'Please note that this function has-
     A                                       no effect on data that has already'
     A                                 13  2'been interfaced completely into HP-
     A                                      S.  So, if you have already taken t-
     A                                      he'
     A                                 14  2'option to move the Transfers into -
     A                                      HPS, then it is too late to start o-
     A                                      ver'
     A                                 15  2'with this upload.'
     A                                 17  2'Business Office.....'
     A                                      COLOR(BLU)
     A            SDBOBO         5A    17 23
     A                                      DSPATR(HI)
     A            SDBODS        30A    17 30
     A                                      COLOR(BLU)
     A                                 19  2'Take F8-Submit to submit the job t-
     A                                      o clear the files.'
     A                                      DSPATR(HI)
      *
     A  48                             20  2'Unable to allocate Receiving -
     A                                      file HSP2081. Contact I/T'
     A  48                                  COLOR(RED)
     A  48                                  DSPATR(HI)
      *
     A  49                             21  2'Unable to allocate Editing -
     A                                      file HSP2082. Contact I/T'
     A  49                                  COLOR(RED)
     A  49                                  DSPATR(HI)
     A*--------------------------------------------------------------*
     A* COMMAND KEY RECORD 1
     A*--------------------------------------------------------------*
     A          R HP4084K1
     A*%%TS  SD  20170913  083140  ISJBADE     REL-V5R2M0  5722-WDS
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A                                 23 11'F8=Submit'
     A                                 23 22'F20=Spool Files'
      *
      *--------------------------------------------------------------*
      * ERROR MESSAGE SUBFILE FORMAT
      *--------------------------------------------------------------*
     A          R HP4084ES                  SFL SFLMSGRCD(24)
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
     A*
     A*--------------------------------------------------------------*
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*--------------------------------------------------------------*
     A*
     A          R HP4084EC                  SFLCTL(HP4084ES)
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
