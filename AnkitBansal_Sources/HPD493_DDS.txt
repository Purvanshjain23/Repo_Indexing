     A*%%TS  SD  20040915  074258  ISLRAMS     REL-V5R2M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD493
     A*  TITLE:         SPECIFY FORM TO PRINT-SCHEDULED TRANSFER ONLY
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  DATE:          09/13/04
     A*
     A*****************************************************************
     A* DN  07/17/2021 W80211 Added line 'Enter Email to send as PDF:'
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A N99                                  CF04(04 'PROMPT')
     A N99                                  CF08(08 'SUBMIT')
     A N99                                  CA12(12 'PREVIOUS')
     A N99                                  CF17(17 'PRINT OPTIONS')
     A N99                                  CF20(20 'WRKSPLF')
     A*
     A*----------------------------------------------------------------
     A* RECORD FORMAT ONE
     A*----------------------------------------------------------------
     A          R HP493R1
     A*%%TS  SD  20040915  074258  ISLRAMS     REL-V5R2M0  5722-WDS
     A                                      SETOF(30)
     A                                      SETOF(31)
     A                                      SETOF(32)
     A                                      SETOF(33)
     A                                      SETOF(34)
     A                                      SETOF(35)
     A                                      SETOF(36)
     A                                      SETOF(37)
     A                                      SETOF(38)
     A                                      SETOF(39)
     A                                      SETOF(45)
     A                                      SETOF(46)
     A                                      SETOF(50)
     A                                      SETOF(52)
     A                                      SETOF(53)
     A                                      SETOF(54)
     A                                      SETOF(55)
     A                                      SETOF(57)
     A                                      SETOF(58)
     A                                      SETOF(59)
     A                                      OVERLAY
     A                                      RTNCSRLOC(&RCD &FLD)
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 27'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  2  8'SPECIFY BILL OF LADINGS TO PRINT---
     A                                      SCHEDULED TRANSFERS ONLY'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A  98                              3 33'(REPRINT)'
     A                                      DSPATR(HI)
     A                                      COLOR(PNK)
     A                                      DSPATR(RI)
     A                                  4  2'ENTER:'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  5  2'Movement number ...............'
     A                                  5 34'?'
     A            D1MVSN    R        B  5 36REFFLD(@@MVSN)
     A                                      EDTCDE(M)
     A N32                                  DSPATR(HI)
     A  32                                  DSPATR(RI)
     A  32
     AO 52                                  DSPATR(PC)
     A  99                                  DSPATR(PR)
     A                                  7  2'OR:'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  8  2'Origin transportation office ..'
     A                                  8 34'?'
     A            D1ORTOCD  R        B  8 36REFFLD(@@TOCD)
     A N30                                  DSPATR(HI)
     A  30
     AO 50                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A            D1ORTONM  R        O  8 46REFFLD(@@TONM)
     A                                  9  2'From scheduled date ...........'
     A            D1FMDY    R     Y  B  9 36REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A N45                                  DSPATR(HI)
     A  45                                  DSPATR(PC)
     A  45                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A                                 10  2'To scheduled date .............'
     A            D1TMDY    R     Y  B 10 36REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A N46                                  DSPATR(HI)
     A  46                                  DSPATR(PC)
     A  46                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A                                 12  2'Origin production type ........'
     A                                 12 34'?'
     A            D1ORPTCD  R        B 12 36REFFLD(@@PTCD)
     A N33                                  DSPATR(HI)
     A  33                                  DSPATR(RI)
     A  33
     AO 53                                  DSPATR(PC)
     A  99                                  DSPATR(PR)
     A            D1ORPTDS  R        O 12 46REFFLD(PTPTDS)
     A                                 13  2'Origin production phase .......'
     A                                 13 34'?'
     A            D1ORPPCD  R        B 13 36REFFLD(@@PPCD)
     A N34                                  DSPATR(HI)
     A  34
     AO 54                                  DSPATR(PC)
     A  34                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A            D1ORPPDS  R        O 13 46REFFLD(PPPPDS)
     A                                 14  2'Origin cell ...................'
     A            D1ORCELL  R        B 14 36REFFLD(HSRREF/@@CELL *LIBL/HSPREF)
     A                                      EDTCDE(M)
     A N31                                  DSPATR(HI)
     A  31                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A                                 15  2'Origin farm site ..............'
     A                                 15 34'?'
     A            D1ORFSCD  R        B 15 36REFFLD(@@FSCD)
     A                                      EDTCDE(M)
     A N35                                  DSPATR(HI)
     A  35
     AO 55                                  DSPATR(PC)
     A  35                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A            D1ORFSNM  R        O 15 46REFFLD(FSFSNM)
     A                                 17  2'Destination production type ...'
     A                                 17 34'?'
     A            D1DNPTCD  R        B 17 36REFFLD(@@PTCD)
     A N37                                  DSPATR(HI)
     A  37
     AO 57                                  DSPATR(PC)
     A  37                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A            D1DNPTDS  R        O 17 46REFFLD(PTPTDS)
     A                                 18  2'Destination production phase ..'
     A                                 18 34'?'
     A            D1DNPPCD  R        B 18 36REFFLD(@@PPCD)
     A N38                                  DSPATR(HI)
     A  38
     AO 58                                  DSPATR(PC)
     A  38                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A            D1DNPPDS  R        O 18 46REFFLD(PPPPDS)
     A                                 19  2'Destination cell ..............'
     A            D1DNCELL  R        B 19 36REFFLD(HSRREF/@@CELL *LIBL/HSPREF)
     A                                      EDTCDE(M)
     A N36                                  DSPATR(HI)
     A  36                                  DSPATR(PC)
     A  36                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A                                 20  2'Destination farm site .........'
     A                                 20 34'?'
     A            D1DNFSCD  R        B 20 36REFFLD(@@FSCD)
     A                                      EDTCDE(M)
     A N39                                  DSPATR(HI)
     A  39
     AO 59                                  DSPATR(PC)
     A  39                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A            D1DNFSNM  R        O 20 46REFFLD(FSFSNM)
80211A                                 21  2'Enter Email to send as PDF ....'
  |  A            D1EMAIL       44A  B 21 36
80211A                                      DSPATR(HI)
     A*
     A*----------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*----------------------------------------------------------------
     A          R HP493K1
     A*%%TS  SD  19960213  100746  ISLRAMS     REL-V3R1M0  5763-PW1
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A N99                             23 11'F4=Prompt'
     A                                 23 22'F8=Submit'
     A N99                             23 33'F12-Previous'
     A N99                             23 47'F17=Print Options'
     A N99                             23 65'F20=Spool Files'
      *
      *----------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *----------------------------------------------------------------
     A          R HP493ES                   SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
     A*
     A*----------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*----------------------------------------------------------------
     A*
     A          R HP493EC                   SFLCTL(HP493ES)
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
