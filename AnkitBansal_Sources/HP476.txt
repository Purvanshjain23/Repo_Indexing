      *****************  RPG PROGRAM HEADING  ************************
      * SYSTEM:      HOG PRODUCTION
      * PROGRAM:     HP476
      * TITLE:       SPECIFY ACTUAL MOVEMENT LOG OPTIONS
      * PROGRAMMER:  SHARON ZURN
      * CREATED:     06/08/94
      *
      * FUNCTION: ALLOWS USER TO ENTER REPORT SELECTIONS.
      *           SUBMITS HP476CL AND CARRIES OPTIONS
      *           SELECTED IN LDA.
      *
      *           THIS PROGRAM IS CALLED FROM A MENU.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 04/25/95  LEANNE FEDOR
      *           CHANGED FARM SITE CODE FROM
      *
      * 05/13/96  LEANNE FEDOR
      *           MADE CHANGES TO FACILITATE NEW JDE RELEASE CONVERSION
      *           CHANGED SEARCH TYPE TO 3A; CALL NEW GENERIC
      *           PROGRAM HPJDEV TO VALIDATE INSTEAD OF HPJDEA SINCE
      *           NO VENDOR ADDRESS IS REQUIRED.
      *
      * 03/19/97  LEANNE FEDOR
      *           LOGIC WAS ADDED TO ALLOW USER TO SELECTE THE REPORT
      *           BASED ON 1) A FROM/TO SCHEDULED DATE RANGE OR 2) A
      *           SPECIFIC UPDATE DATE. THE UPDATE DATE CORRESPONDS
      *           TO THE UPDATE DATE/TIME STAMP THAT WAS ADDED TO THE
      *           MOVEMENT EVENT FILE.
      *
      * 06/08/00  LeAnne Fedor
      *           As requested by Jodi Radel at Julesburg, added 'cell' as
      *
      * 07/22/02  LeAnne Fedor
      *           In preparation for allowing groups on a single farm to have different
      *           production phases, we changed the edit for a farm.  The production phase
      *           of a selected farm no longer has to "match" the production phase that
      *           the user may have keyed on the screen.
      *
      * 06/24/09  LeAnne Ramsey
      *           The user can now get the data downloaded as an Excel Spreadsheet---instead
      *           of getting the printed report.
      *
      * 07/06/09  LeAnne Ramsey
      *           Recompile only. Added new field 'Continuous Flow Flag' to Hog Group file.
      *
      * 10/16/13  LeAnne Ramsey (E2831)
      *           Recompile only. Added field 'MTech Reference'.
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     FHPD476    CF   E             WORKSTN INFDS(IOFEED)
      *
      *
     FHSP002    IF   E           K DISK
      *    FUNCTIONS
      *
      *
     FHSP009    IF   E           K DISK
      *    BUSINESS OFFICE
      *
      *
     FHSP011    IF   E           K DISK
      *    PRODUCTION PHASE
      *
      *
     FHSP018    IF   E           K DISK
      *    FARM SITE
      *
      *
     FHSP034    IF   E           K DISK
      *    HOG GROUP
      *
      *
     FHSP054    IF   E           K DISK
      *    MOVEMENT REPORT CODES
      *
      /EJECT
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Workfields for date manipulation
      *
     D wkisodate       s               d   datfmt(*iso)
      *
      ****************************************************************
      * ARRAYS AND TABLES
      ****************************************************************
      *
      *---------------------------------------------------------------
      *  COMPILE TIME ARRAYS FOR SUBMIT JOB COMMAND USED IN QCMDEXC
      *---------------------------------------------------------------
     D CMD             S              1    DIM(80) CTDATA PERRCD(80)
      *
      *---------------------------------------------------------------
      *  DEFINITION FOR EXTERNAL DATA AREA 'DAAPER'
      *---------------------------------------------------------------
     D                 DS
     D  DAAPER                 1     24
     D  DACCYY                 1      4
     D  DAPER                  5      6
     D  DABPDT                 7     14  0
     D  DAEPDT                15     22  0
     D  DAPGFL                23     23
     D  DAPPFL                24     24
      *
      *---------------------------------------------------------------
      * STANDARD ERROR MESSAGE HANDLING DATA STRUCTURES
      *---------------------------------------------------------------
      *
      * FOR ERROR MESSAGE HANDLING, A PACKED INDEX (E) IS REQUIRED.
      * FOR PROGRAM READABILITY, DEFINE A CORRESPONDING ERROR COUNT
      * FIELD CALLED 'ERROR'
      *
     D                 DS                  INZ
     D  ERROR                  1      2P 0
     D  E                      1      2P 0
      *
      * THIS DATA STRUCTURE SUPPLIES THE NAME OF THE MESSAGE FILE TO
      * THE MESSAGE HANDLING CL PROGRAM.  THE FIELD NAME MSGFIL MUST BE
      * CONSTANT. THE VALUE IN QUOTES IS THE NAME OF THE SPECIFIC
      * MESSAGE FILE CONTAINING THE USER DEFINED MESSAGES.
      *
     D                 DS                  INZ
     D  MSGFIL                 1     10    INZ('HSMSGF    ')
      *
      * THE FOLLOWING 3 DATA STRUCTURES ARE USED TO SPEED MESSAGE
      * HANDLING SINCE IT IS FASTER TO CLEAR DATA STRUCTURES THAN
      * ARRAYS.  EACH IS ASSOCIATED WITH A STANDARD MESSAGE ARRAY.
      *
     D MGI             DS           140    INZ
     D  MGID                   1    140
     D                                     DIM(20)                              MSG ID ARRAY
      *
     D MGD             DS          1000    INZ
     D  MGDT                   1   1000
     D                                     DIM(20)                              MSG PARMS
      *
     D MGWK            DS            50    INZ
     D  MGW                    1     50
     D                                     DIM(50)                              MSG WORK PARMS
      *
      *---------------------------------------------------------------
      * STANDARD PROGRAM STATUS DATA STRUCTURE
      *---------------------------------------------------------------
      *    EXTERNALLY DEFINED AS UTPGFR (RECORD FORMAT: PGMDSR)
     D PGMDS         ESDS                  EXTNAME(UTPGFR)
      *
      *---------------------------------------------------------------
      * STANDARD WORKSTATION INFORMATION DATA STRUCTURE
      *---------------------------------------------------------------
      *    EXTERNALLY DEFINED AS UTWSFR (RECORD FORMAT: UTIDFRR)
     D IOFEED        E DS                  EXTNAME(UTWSFR)
      *
      *---------------------------------------------------------------
      * STANDARD DATABASE FILE INFORMATION DATA STRUCTURE
      *---------------------------------------------------------------
      *    EXTERNALLY DEFINED AS UTDBFR (RECORD FORMAT: FDBCKD)
     D DBFEED        E DS                  EXTNAME(UTDBFR)
      *
      *---------------------------------------------------------------
      *  LDA -  WITH THE REQUESTED PRINT SELECTIONS
      *---------------------------------------------------------------
      *
     D LDA             DS
     D  LDFSBO                 1      5
     D  LDFYMD                 6     13  0
     D  LDTYMD                14     21  0
     D  LDPPCD                22     26
     D  LDFSCD                27     31  0
     D  LDMRCD                39     40
     D  LDHGSN                41     47  0
     D  LDCVNO                48     55  0
      *
     D  LDBODS                56     85
     D  LDFSNM                86    110
     D  LDHGCD               111    117
     D  LDCNAM               118    147
     D  LDMRDS               148    177
     D  LDPPDS               178    207
     D  LDFMDY               208    213  0
     D  LDTMDY               214    219  0
      *
     D  LDUMDY               220    225  0
     D  LDUYMD               226    233  0
      *
     D  LDCELL               234    235  0
     D  ldemail              236    285
      *
     D  LDOUTU               401    410
     D  LDHOLD               411    411
     D  LDCOPY               412    412  0
     D  LDOUTQ               413    422
      *
     D  ldsel                451    580
      *
      *---------------------------------------------------------------
      *  NAMED CONSTANTS
      *---------------------------------------------------------------
      *
     D YES             C                   CONST('Y')
     D NO              C                   CONST('N')
     D SET1            C                   CONST('SET1  ')
     D EDIT1           C                   CONST('EDIT1 ')
     D SCRN1           C                   CONST('SCRN1 ')
     D EXIT            C                   CONST('EXIT  ')
     D ALL             C                   CONST('A')
      *
      * Workfields
     d alphfmdy        s              6
     d alphtmdy        s              6
     d alphcell        s              2
     d alphfscd        s              5
     d alphcvno        s              8
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * MAIN LINE
      ****************************************************************
      *
     C                   MOVEL     SET1          ROUTNE
      *
     C     *INLR         DOWEQ     *OFF
     C     ROUTNE        CASEQ     SET1          $SET1
     C     ROUTNE        CASEQ     SCRN1         $SCRN1
     C     ROUTNE        CASEQ     EDIT1         $EDIT1
     C     ROUTNE        CASEQ     EXIT          $EXIT
     C                   ENDCS
     C                   ENDDO
      /EJECT
      *----------------------------------------------------------------
      * SET ENVIRONMENT FOR SCREEN 1
      *----------------------------------------------------------------
      *
     C     $SET1         BEGSR
      *
     C                   MOVEL     SCRN1         ROUTNE
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * PERFORM OPERATOR I/O FOR SCREEN 1
      *----------------------------------------------------------------
      *
     C     $SCRN1        BEGSR
      *
      * IF THE USER FAILED THE INITIAL SECURITY CHECKS, SETON 99
      * TO PROTECT INPUT FIELDS AND INVALIDATE FUNCTION KEYS.
      *
     C     TERMFL        IFEQ      YES
     C                   SETON                                        99
     C                   ENDIF
      *
      * WRITE THE ERROR MESSAGES FROM THE ERROR ARRAYS TO THE
      * ERROR MESSAGE SUBFILE
      *
     C                   EXSR      $WRMSG
      *
      * WRITE SCREEN 1 TO CRT
      *
     C                   WRITE     HP476K1
     C                   WRITE     HP476EC
     C                   EXFMT     HP476R1
      *
      * CLEAR THE MESSAGES FROM THE MESSAGE DATA STRUCTURES AND PROGRAM
      * MESSAGE QUEUE.
      *
     C                   EXSR      $CLMSG
      *
      * GET USER'S ENTRY AND SET THE NEXT ROUTINE
      *
     C                   SELECT
     C     *IN03         WHENEQ    *ON                                          F3-EXIT
     C     TERMFL        OREQ      YES                                          BAD USER
     C                   MOVEL     EXIT          ROUTNE
      *
     C     *IN17         WHENEQ    *ON                                          F17-PRINT OPT
     C                   MOVEL     SCRN1         ROUTNE
     C                   CALL      'HP451'
     C                   PARM                    SDPGM
     C     LDCOPY        PARM      LDCOPY        XXCOPY
     C                   PARM                    LDHOLD
     C                   PARM                    LDOUTQ
     C                   PARM                    XXNITE
     C                   PARM                    XXJOBQ
      *
     C     *IN20         WHENEQ    *ON                                          F20-WRKSPLF
     C                   CALL      'QEZOUTPT'
     C                   MOVEL     SCRN1         ROUTNE
      *
      *
      * CHECK FOR A USER PROMPT FIRST. THE USER MAY WANT
      * TO SELECT A NEW BUSINESS OFFICE.
      *
     C     *IN04         WHENEQ    *ON                                          WH PROMPT
     C                   EXSR      $F4S1
     C                   MOVEL     SCRN1         ROUTNE
     C                   OTHER
      *
      * ALWAYS CHECK SECURITY FOR BUSINESS OFFICE
      *
     C                   MOVEL(P)  'HP476'       WKFNCD
     C                   EXSR      $CHKBO
      *
     C                   MOVEL     EDIT1         ROUTNE
     C                   ENDSL
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * EDIT INPUT FIELDS FOR SCREEN 1
      *----------------------------------------------------------------
      *
     C     $EDIT1        BEGSR
      *
      * THE USER MUST ENTER THE REPORT HE WISHES TO HAVE
      * PRINTED.
      *
     C                   MOVE      *BLANK        DFMRDS
     C     DFMRCD        IFEQ      *BLANK                                         IF NO RPT
     C     ERROR         IFLT      MAXMSG
     C                   SETON                                        34
     C                   ADD       1             ERROR
     C                   MOVEL     'HS09001'     MGID(E)
     C                   CLEAR                   MGW
     C                   MOVEA     DFMRCD        MGW(1)
     C                   MOVEA     MGW           MGDT(E)
     C                   ENDIF
     C                   ELSE
      *
     C     DFMRCD        CHAIN     HSP054                             93
     C     *IN93         IFEQ      *ON                                          IF BAD RPT
     C                   SETON                                        34
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS09006'     MGID(E)
     C                   CLEAR                   MGW
     C                   MOVEA     DFMRCD        MGW(1)
     C                   MOVEA     MGW           MGDT(E)
     C                   ENDIF
     C                   ELSE
      *
     C                   MOVEL     MRMRDS        DFMRDS
      *
      * IT IS AN ERROR IF THE USER SELECTED A REPORT THAT IS
      * ONLY AVAILABLE FOR 'SCHEDULED' MOVEMENT REPORTS.
      *
     C     MRMRFL        IFEQ      'S'
     C                   SETON                                        34
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS00703'     MGID(E)
     C                   CLEAR                   MGW
     C                   MOVEA     DFMRCD        MGW(1)
     C                   MOVEA     MGW           MGDT(E)
     C                   ENDIF
     C                   ENDIF
      *
     C                   ENDIF                                                  IF BAD RPT
     C                   ENDIF                                                  IF NO RPT
      *
      *
      * EDIT DATES
      * THE USERS MUST ENTER ONE OF THE FOLLOWING (BUT NOT BOTH):
      *    1) A FROM/TO SCHEDULED DATE RANGE, OR
      *    2) AN UPDATE DATE
      *
     C                   Z-ADD     0             LDFYMD
     C                   Z-ADD     0             LDTYMD
     C                   Z-ADD     0             LDUYMD
     C                   Z-ADD     0             LDFMDY
     C                   Z-ADD     0             LDTMDY
     C                   Z-ADD     0             LDUMDY
      *
     C                   SELECT
     C     DFFMDY        WHENEQ    0
     C     DFTMDY        ANDEQ     0
     C     DFUMDY        ANDEQ     0
     C                   SETON                                        303233
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS09124'     MGID(E)
     C                   ENDIF
      *
     C     DFUMDY        WHENNE    0
     C     DFFMDY        ANDNE     0
     C                   SETON                                        303233
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS09124'     MGID(E)
     C                   ENDIF
      *
     C     DFUMDY        WHENNE    0
     C     DFTMDY        ANDNE     0
     C                   SETON                                        303233
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS09124'     MGID(E)
     C                   ENDIF
      *
     C     DFFMDY        WHENNE    0
     C     DFTMDY        ORNE      0
     C                   EXSR      $FRTO
      *
     C     DFUMDY        WHENNE    0
     C     *mdy          test(d)                 dfumdy                 92
     C                   if        *in92 = *on                                  If bad date
     C                   seton                                        30
     C                   if        error < maxmsg
     C                   add       1             error
     C                   eval      mgid(e) = 'HS09004'
     C                   endif
     C                   else
      *
     C                   z-add     dfumdy        ldumdy
     C     *mdy          move      dfumdy        wkisodate
     C                   move      wkisodate     lduymd
     C                   endif                                                  If bad date
     C                   ENDSL
      *
      *
      * IF PRODUCTION PHASE KEYED, CHECK TO SEE IF IT IS VALID
      *
     C                   MOVE      *BLANK        DFPPDS
     C     DFPPCD        IFNE      *BLANK                                       IF PHASE
     C     DFPPCD        CHAIN     HSP011                             91
     C     *IN91         IFEQ      *ON                                          IF BAD PHASE
     C                   SETON                                        35
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS00170'     MGID(E)
     C                   CLEAR                   MGW
     C                   MOVEA     DFPPCD        MGW(1)
     C                   MOVEA     MGW           MGDT(E)
     C                   ENDIF
     C                   ELSE
     C                   MOVEL     PPPPDS        DFPPDS
     C                   ENDIF                                                  IF BAD PHASE
     C                   ENDIF                                                  IF PHASE
      *
      * If cell is keyed, validate.
      *
     C                   exsr      $cell
      *
      * IF FARM SITE KEYED, VALIDATE
      *
     C                   MOVE      *BLANK        DFFSNM
     C     DFFSCD        IFNE      0                                            IF FARM
     C                   exsr      $fscd
     C                   ENDIF                                                  IF FARM
      *
      *
      * IF THE HOG GROUP IS KEYED, VALIDATE GROUP IS NOT VOIDED OR
      *    CREATED; CHECK FOR MATCHES ON BUSINESS OFFICE, FARM SITE,
      *    AND PRODUCTION PHASE.
      *
     C                   Z-ADD     0             WKHGSN
     C     DFHGCD        IFNE      *BLANK                                       IF GROUP
     C     DFHGCD        CHAIN     HSP034                             93
     C     *IN93         IFEQ      *ON                                          IF BAD GROUP
     C                   SETON                                        37
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS00261'     MGID(E)
     C                   CLEAR                   MGW
     C                   MOVEA     DFHGCD        MGW(1)
     C                   MOVEA     MGW           MGDT(E)
     C                   ENDIF
     C                   ELSE
      *
     C                   Z-ADD     HGHGSN        WKHGSN
      *
      * VOIDED GROUPS CANNOT BE SELECTED FOR THIS REPORT
      *
     C                   SELECT
     C     HGGSCD        WHENEQ    'VD'                                         WH VOIDED
     C                   SETON                                        37
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS00477'     MGID(E)
     C                   CLEAR                   MGW
     C                   MOVEA     DFHGCD        MGW(1)
     C                   MOVEA     MGW           MGDT(E)
     C                   ENDIF
      *
      * 'CREATED' GROUPS CANNOT BE SELECTED FOR THIS REPORT
      *
     C     HGGSCD        WHENEQ    'CR'                                         WH CREATED
     C                   SETON                                        37
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS00478'     MGID(E)
     C                   CLEAR                   MGW
     C                   MOVEA     DFHGCD        MGW(1)
     C                   MOVEA     MGW           MGDT(E)
     C                   ENDIF
     C                   ENDSL
      *
      * THE GROUP'S BUSINESS OFFICE MUST MATCH THE ONE KEYED
      * BY THE USER.
      *
     C     HGFSBO        IFNE      DFFSBO                                       IF DIFF B/O
     C     DFFSBO        ANDNE     *BLANK
     C                   SETON                                        37
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS00459'     MGID(E)
     C                   CLEAR                   MGW
     C                   MOVEA     DFHGCD        MGW(1)
     C                   MOVEA     HGFSBO        MGW(8)
     C                   MOVEA     DFFSBO        MGW(13)
     C                   MOVEA     MGW           MGDT(E)
     C                   ENDIF
     C                   ENDIF                                                  IF DIFF B/O
      *
      * THE GROUP'S PRODUCTION PHASE MUST MATCH THE ONE KEYED
      * BY THE USER.
      *
     C     HGPPCD        IFNE      DFPPCD                                       IF DIFF PP
     C     DFPPCD        ANDNE     *BLANK
     C                   SETON                                        37
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS00462'     MGID(E)
     C                   CLEAR                   MGW
     C                   MOVEA     DFHGCD        MGW(1)
     C                   MOVEA     HGPPCD        MGW(8)
     C                   MOVEA     DFPPCD        MGW(13)
     C                   MOVEA     MGW           MGDT(E)
     C                   ENDIF
     C                   ENDIF                                                  IF DIFF PP
      *
      * THE GROUP'S FARM SITE MUST MATCH THE ONE KEYED
      * BY THE USER.
      *
     C     HGFSCD        IFNE      DFFSCD                                       IF DIFF FARM
     C     DFFSCD        ANDNE     0
     C                   SETON                                        37
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS00468'     MGID(E)
     C                   CLEAR                   MGW
     C                   MOVEA     DFHGCD        MGW(1)
     C                   MOVE      HGFSCD        XXFSCD
     C                   MOVEA     XXFSCD        MGW(8)
     C                   MOVE      DFFSCD        XXFSCD
     C                   MOVEA     XXFSCD        MGW(13)
     C                   MOVEA     MGW           MGDT(E)
     C                   ENDIF
     C                   ENDIF                                                  IF DIFF FARM
     C                   ENDIF                                                  IF GOOD GRP
     C                   ENDIF                                                  IF GROUP
      *
      * VALIDATE THE CUSTOMER A/R NUMBER
      *
     C                   MOVE      *BLANK        DFCNAM
     C                   MOVE      *BLANK        LDCNAM
     C     DFCVNO        IFNE      0                                            IF CUST
      *
      * CUSTOMER SELECTION IS ONLY VALID WHEN THE USER HAS
      * ELECTED TO PRINT THE 'CUSTOMER' REPORT OR THE
      * 'NOT FULLY PAID SALES REPORT'
      *
     C     DFMRCD        IFNE      'CU'                                         IF NOT SALE
     C     DFMRCD        ANDNE     'NS'
     C                   SETON                                        42
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS00207'     MGID(E)
     C                   CLEAR                   MGW
     C                   MOVEA     DFMRCD        MGW(1)
     C                   MOVEA     MGW           MGDT(E)
     C                   ENDIF
     C                   ELSE
      *
     C                   Z-ADD     DFCVNO        INAJD
     C                   MOVEL(P)  'C'           INAT1
     C                   EXSR      $JDEV
      *
      * IF THERE IS AN ERROR WITH THE CUST A/R NUMBER
      *
     C     JDEFL         IFEQ      YES                                          IF ERROR
     C                   SETON                                        42
     C                   ELSE
     C                   MOVEL     OUALPH        DFCNAM
     C                   MOVEL     OUALPH        LDCNAM
     C                   ENDIF                                                  IF ERROR
     C                   ENDIF                                                  IF NOT SALE
     C                   ENDIF                                                  IF CUST
      *
      * IF THERE ARE NO ERRORS, EXECUTE THE SUBROUTINE TO PROCESS THE
      * USERS REQUEST
      *
     C     ERROR         IFEQ      0                                            IF OK
     C                   EXSR      $PROC1
     C                   ENDIF                                                  IF OK
      *
     C                   MOVEL     SCRN1         ROUTNE
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * Validate cell
      *----------------------------------------------------------------
      *
     C     $cell         begsr
      *
      *
      * CELL CANNOT BE NEGATIVE.
      *
     C                   SELECT
     C     DFCELL        WHENLT    0                                            IF NEG CELL
     C                   SETON                                        38
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS09000'     MGID(E)
     C                   ENDIF
      *
     C     DFFSCD        WHENNE    0
     C     DFCELL        ANDGT     0
     C                   SETON                                        38
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS09032'     MGID(E)
     C                   ENDIF
     C                   ENDSL
      *
      *
     C                   endsr
      /EJECT
      *----------------------------------------------------------------
      * Edit farm site
      *----------------------------------------------------------------
      *
      * CHECK FOR MATCHES BETWEEN:
      *     FARM B.O. AND SCREEN B.O.
      *     FARM CELL AND SCREEN CELL
      *
     C     $fscd         BEGSR
      *
     C     DFFSCD        CHAIN     HSP018                             92
     C     *IN92         IFEQ      *ON                                          IF BAD FARM
     C                   SETON                                        36
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS00131'     MGID(E)
     C                   CLEAR                   MGW
     C                   MOVE      DFFSCD        XXFSCD
     C                   MOVEA     XXFSCD        MGW(1)
     C                   MOVEA     MGW           MGDT(E)
     C                   ENDIF
     C                   ELSE
     C                   MOVEL     FSFSNM        DFFSNM
      *
      * CHECK FOR BUSINESS OFFICE MATCH
      *
     C     FSFSBO        IFNE      DFFSBO                                       IF MISMATCH
     C     DFFSBO        ANDNE     *BLANK
     C                   SETON                                        36
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS00158'     MGID(E)
     C                   CLEAR                   MGW
     C                   MOVE      DFFSCD        XXFSCD
     C                   MOVEA     XXFSCD        MGW(1)
     C                   MOVEA     FSFSBO        MGW(6)
     C                   MOVEA     DFFSBO        MGW(11)
     C                   MOVEA     MGW           MGDT(E)
     C                   ENDIF
     C                   ENDIF                                                  IF MISMATCH
      *
     C                   ENDIF                                                  IF BAD FARM
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * EDIT FROM/TO DATE RANGE
      *----------------------------------------------------------------
      *
     C     $FRTO         BEGSR
      *
      * VERIFY THAT A FROM DATE IS ENTERED
      *
     C     DFFMDY        IFNE      0                                            IF DATE ENTERED
     C     *mdy          test(d)                 dffmdy                 92
     C                   if        *in92 = *on                                  If bad date
     C                   seton                                        32
     C                   if        error < maxmsg
     C                   add       1             error
     C                   eval      mgid(e) = 'HS09004'
     C                   endif
     C                   else
      *
     C     *mdy          move      dffmdy        wkisodate
     C                   move      wkisodate     ldfymd
     C                   endif                                                  If bad date
     C                   ELSE
      *
      * IF FROM DATE IS ZEROS, DISPLAY AN ERROR MSG.
      *
     C                   SETON                                        32
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS09001'     MGID(E)
     C                   ENDIF
     C                   ENDIF
      *
      * VERIFY THAT A VALID 'TO' DATE IS ENTERED
      *
     C     DFTMDY        IFNE      0                                            IF TO DATE
     C     *mdy          test(d)                 dftmdy                 92
     C                   if        *in92 = *on                                  If bad date
     C                   seton                                        33
     C                   if        error < maxmsg
     C                   add       1             error
     C                   eval      mgid(e) = 'HS09004'
     C                   endif
     C                   else
      *
     C     *mdy          move      dftmdy        wkisodate
     C                   move      wkisodate     ldtymd
     C                   endif                                                  If bad date
     C                   ELSE
      *
     C                   SETON                                        33
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS09001'     MGID(E)
     C                   ENDIF
     C                   ENDIF                                                  IF TO DATE
      *
      * VERIFY THAT THE "TO" DATE IS NOT EARLIER THAN THE "FROM" DATE
      *
     C     *IN32         IFEQ      *OFF                                         IF EARLIER
     C     *IN33         ANDEQ     *OFF
     C     LDTYMD        ANDLT     LDFYMD
     C                   SETON                                        33
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS09009'     MGID(E)
     C                   ENDIF
     C                   ENDIF                                                  IF EARLIER
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * CHECK THE BUSINESS OFFICE ENTERED AS A RESTRICTOR
      *----------------------------------------------------------------
      *
      * THE BUSINESS OFFICE ENTERED MUST:
      *    BE A VALID OFFICE IN THE BUSINESS OFFICE MASTER
      *    HAVE A SECURITY LOCATION TO WHICH THE USER IS AUTHORIZED
      *    IS NOT REQUIRED
      *
     C     $CHKBO        BEGSR
      *
      * SET THE 'VALIDATE B.O.' FLAG DEPENDING ON WHETHER THE
      * USER LEFT B.O. BLANK TO GET ALL BUSINESS OFFICES OR ENTERED
      * A SPECIFIC OFFICE.
      *
     C     DFFSBO        IFEQ      *BLANK
     C                   MOVEL     ALL           INBOFL
     C                   ELSE
     C                   MOVEL     YES           INBOFL
     C                   ENDIF
      *
      * CHECK THE USER'S SECURITY TO THIS BUSINESS OFFICE.
      * SET INDVFL TO YES TO TELL SECURITY PROGRAM TO COMPARE THE
      * FUNCTION DIVISION CODE TO THE B.O. DIVISION CODE.
      *
     C                   MOVEL     YES           INDVFL
     C                   EXSR      $SEC
     C                   MOVEL     NO            INDVFL
      *
     C                   MOVEL     OUBODS        DFBODS
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * CALL SECURITY PROGRAM
      *---------------------------------------------------------------
      *
     C     $SEC          BEGSR
      *
      * SETUP THE FIELDS TO SEND INTO THE SECURITY PROGRAM
      *
     C                   MOVEL     WKFNCD        INFNCD                         PGM NAME
     C                   MOVEL     DFFSBO        INBOBO                         SCREEN BO
      *
     C                   MOVE      *BLANK        OUBOBO                         BO CODE
     C                   MOVE      *BLANK        OUBODS                         BO DESC
     C                   MOVE      *BLANK        OUDVCD                         FUNCTION DIV
     C                   MOVEL     NO            TERMFL            1            TERMINAL FLG
      *
     C                   CALL      'HPSEC  '
      **** IN
     C                   PARM                    MGID                           MSGID ARRAY
     C                   PARM                    MGDT                           MSGDTA DATA
     C                   PARM                    ERROR                          # OF ERRORS
     C                   PARM                    INFNCD           10            FUNC CODE
     C                   PARM                    INBOBO            5            BO CODE
     C                   PARM                    INDVFL            1            CHECK DIV?
     C                   PARM                    INBOFL            1            CHECK B.O.?
      **** OUT
     C                   PARM                    OUBOBO            5            BO CODE
     C                   PARM                    OUBODS           30            BO DESC.
     C                   PARM                    OUDVCD            5            DIV CODE
     C                   PARM                    SECFL             1            SEC FLAG
     C                   PARM                    OUBOFL            1            BO ERRFLG
     C                   PARM                    OURMFL            1
      *
     C     OUBOFL        IFEQ      YES
     C                   SETON                                        31
     C                   ENDIF
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * PROCESS THE USER'S REQUEST
      *---------------------------------------------------------------
      *
     C     $PROC1        BEGSR
      *
      *  IF USER TOOK F8=SUBMIT EXCEPTION REPORT
      *
     C     *IN08         IFEQ      *ON                                          IF F8=SBMJOB
      *
      * MOVE DISPLAY FILE FIELDS INTO LDA
      *
     C                   Z-ADD     DFFMDY        LDFMDY
     C                   Z-ADD     DFTMDY        LDTMDY
     C                   MOVEL     DFFSBO        LDFSBO
     C                   MOVEL     DFBODS        LDBODS
     C                   Z-ADD     DFFSCD        LDFSCD
     C                   Z-ADD     DFCELL        LDCELL
     C                   MOVEL     DFFSNM        LDFSNM
     C                   MOVEL     DFPPCD        LDPPCD
     C                   MOVEL     DFPPDS        LDPPDS
     C                   MOVEL     DFMRCD        LDMRCD
     C                   MOVEL     DFMRDS        LDMRDS
     C                   Z-ADD     WKHGSN        LDHGSN
     C                   MOVEL     DFHGCD        LDHGCD
     C                   Z-ADD     DFCVNO        LDCVNO
      *
     C                   move      dfemail       ldemail
     C                   if        dfemail = *blank
     C                   move      *blank        ldsel
     C                   else
     C                   exsr      $bldsel
     C                   endif
      *
     C                   OUT       LDA
      *
      * MOVE THE 80-CHARACTER COMPILE TIME ARRAY 'CMD' TO A
      * 80-CHARACTER FIELD FOR SUBMITTING HP676CL TO QCMDEXC.
      *
      *
     C                   MOVEA     XXJOBQ        CMD(49)
     C                   MOVEA     CMD           CMDSBM
      *
     C                   CALL      'QCMDEXC'
     C                   PARM                    CMDSBM
     C                   PARM                    LENGTH
      *
      * ISSUE STANDARD REQUEST SUBMISSION MESSAGE
      *
     C                   ADD       1             ERROR
     C                   MOVEL     'HS09003'     MGID(E)
     C                   ENDIF                                                  IF F8=SBMJOB
      *
     C                   ENDSR
      /EJECT
      *--------------------------------------------------------------------------
      * If the user is emailing a XLS, build his selections into one long
      * field to use in the email "message".
      *--------------------------------------------------------------------------
      *
     C     $bldsel       begsr
      *
     C                   move(p)   dffmdy        alphfmdy
     C                   move(p)   dftmdy        alphtmdy
      *
     C                   if        dfcell = 0
     C                   move      *blank        alphcell
     C                   else
     C                   move(p)   dfcell        alphcell
     C                   endif
      *
     C                   if        dffscd = 0
     C                   move      *blank        alphfscd
     C                   else
     C                   move(p)   dffscd        alphfscd
     C                   endif
      *
     C                   if        dfcvno = 0
     C                   move      *blank        alphcvno
     C                   else
     C                   move(p)   dfcvno        alphcvno
     C                   endif
      *
     C                   eval      ldsel = ldmrds  + '     ' +
     C                             ' Date Range: ' + alphfmdy + ' to '
     C                                             + alphtmdy +
     C                             ' Phase: '      + dfppcd   +
     C                             ' Cell: '       + alphcell +
     C                             ' Farm: '       + alphfscd +
     C                             ' Group: '      + dfhgcd   +
     C                             ' Cust: '       + alphcvno
      *
     C                   endsr
      /EJECT
      *----------------------------------------------------------------
      * CALL THE EXTERNAL PROGRAM TO VALIDATE JDE ADDRESS NUMBERS
      *----------------------------------------------------------------
      *
     C     $JDEV         BEGSR
      *
     C                   MOVEL     NO            JDEFL                          JDE ERRORS
      *
     C                   CALL      'HPJDEV'
     C                   PARM                    MGI                            MSG ID'S
     C                   PARM                    MGD                            MSG DATA FLD
     C                   PARM                    ERROR                          # ERR MSGS
     C                   PARM                    INAJD                          JDE ADDR #
     C                   PARM                    INAT1                          JDE ADDR TYP
     C                   PARM                    JDEFL                          ERROR FLAG
     C                   PARM      *BLANK        OUALPH                         JDE NAME
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * F4 PROMPT ON SCREEN 1
      *---------------------------------------------------------------
      *
     C     $F4S1         BEGSR
      *
     C                   SELECT
      *
      * IF BUSINESS OFFICE
      *
     C     FLD           WHENEQ    'DFFSBO'
     C                   MOVE      *BLANK        XXFSBO
     C                   MOVE      *BLANK        XXBODS
     C                   CALL      'HP503'
     C                   PARM                    XXFSBO
     C                   PARM                    XXBODS
     C                   PARM                    SVDVCD
     C                   SETON                                        51
     C     XXFSBO        IFNE      *BLANK                                       IF SELECTED
     C                   MOVEL     XXFSBO        DFFSBO
     C                   MOVEL     XXBODS        DFBODS
     C                   ENDIF                                                  IF SELECTED
      *
      * PRODUCTION PHASE
      *
     C     FLD           WHENEQ    'DFPPCD'
     C                   MOVE      *BLANK        XXPPCD
     C                   MOVE      *BLANK        XXPPDS
     C                   CALL      'HP507'
     C                   PARM                    XXPPCD
     C                   PARM                    XXPPDS
     C                   SETON                                        53
     C     XXPPCD        IFNE      *BLANK                                       IF SELECTED
     C                   MOVEL     XXPPCD        DFPPCD
     C                   MOVEL     XXPPDS        DFPPDS
     C                   ENDIF                                                  IF SELECTED
      *
      * FARM SITE
      *
     C     FLD           WHENEQ    'DFFSCD'
     C                   CALL      'HP5040'
     C                   PARM      *blank        xxaist
     C                   PARM      0             WKFSCD
     C                   PARM      *blank        XXFSNM
     C                   SETON                                        54
     C     WKFSCD        IFNE      0                                            IF SELECTED
     C                   Z-ADD     WKFSCD        DFFSCD
     C                   MOVEL     XXFSNM        DFFSNM
     C                   ENDIF                                                  IF SELECTED
      *
      *  HOG GROUP
      *
     C     FLD           WHENEQ    'DFHGCD'
     C                   MOVEL     DFFSBO        XXFSBO
     C                   Z-ADD     DFFSCD        WKFSCD
     C                   MOVE      *BLANK        XXHGCD
     C                   CALL      'HP541'
     C                   PARM                    XXFSBO
     C                   PARM                    WKFSCD
     C                   PARM                    XXHGCD
     C                   SETON                                        55
      *
     C     XXHGCD        IFNE      *BLANK                                       IF SELECTED
     C                   MOVEL     XXHGCD        DFHGCD
     C                   ENDIF                                                  IF SELECTED
      *
      * CUSTOMER NUMBER
      *
     C     FLD           WHENEQ    'DFCVNO'
      *
     C     DFFSBO        IFEQ      *BLANK
     C     *LOVAL        SETLL     HSP009
     C                   READ      HSP009                                 92
     C                   ELSE
     C     DFFSBO        CHAIN     HSP009                             92
     C                   ENDIF
      *
     C     *IN92         IFEQ      *OFF
     C                   MOVEL(P)  'C'           INAT1                          SEARCH TYPE
     C                   CALL      'HP513'
     C                   PARM                    BOCOCD                         COMPANY
     C                   PARM                    INAT1                          SEARCH TYPE
     C                   PARM      *ZERO         OUAJD                          SELECTED NO.
     C                   PARM      *BLANK        OUALPH                         JDE NAME
     C                   ENDIF
      *
     C                   SETON                                        59
     C     OUAJD         IFNE      0                                            IF SELECTED
     C                   Z-ADD     OUAJD         DFCVNO
     C                   MOVEL     OUALPH        DFCNAM
     C                   ENDIF                                                  IF SELECTED
      *
      * MOVEMENT REPORT
      *
     C     FLD           WHENEQ    'DFMRCD'
     C                   MOVEL     'A'           XXMRFL
     C                   MOVE      *BLANK        XXMRCD
     C                   MOVE      *BLANK        XXMRDS
     C                   CALL      'HP562'
     C                   PARM                    XXMRFL
     C                   PARM                    XXMRCD
     C                   PARM                    XXMRDS
     C                   SETON                                        55
     C     XXMRCD        IFNE      *BLANK                                       IF SELECTED
     C                   MOVEL     XXMRCD        DFMRCD
     C                   MOVEL     XXMRDS        DFMRDS
     C                   ENDIF                                                  IF SELECTED
     C                   OTHER
      *
      * F4 NOT VALID FOR THIS FIELD SO ISSUE MESSAGE
      *
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS09011'     MGID(E)
     C                   ENDIF
     C                   ENDSL
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * WRITE ERROR MESSAGES
      *---------------------------------------------------------------
      *
     C     $WRMSG        BEGSR
      *
     C                   CALL      'UT80045J'                           43
     C                   PARM                    MGID
     C                   PARM                    MGDT
     C                   PARM                    ERROR
     C                   PARM      SDPGM         PGM
     C                   PARM      MSGFIL        MSGFL
      *
      * IF CALL TO UT80045J FAILED, A MESSAGE HARDCODED IN THE DSPF
      * COMMAND KEY RECORD WILL BE SET ON. SO, REDISPLAY SCREEN TO USER
      *
     C     *IN43         IFEQ      *ON
     C                   MOVEL     SET1          ROUTNE
     C                   ENDIF
      *
     C                   ENDSR
      *
      *----------------------------------------------------------------
      * CLEAR MESSAGES
      *----------------------------------------------------------------
      *
     C     $CLMSG        BEGSR
      *
     C                   CALL      'UT80045J'                           43
     C                   PARM      *BLANK        MGI
     C                   PARM      *BLANK        MGD
     C                   PARM      *ZEROS        ERROR
     C                   PARM      SDPGM         PGM
     C                   PARM      MSGFIL        MSGFL
      *
      * IF CALL TO UT80045J FAILED, A MESSAGE HARDCODED IN THE DSPF
      * COMMAND KEY RECORD WILL BE SET ON. SO, REDISPLAY SCREEN TO USER
      *
     C     *IN43         IFEQ      *ON
     C                   MOVEL     SET1          ROUTNE
     C                   ENDIF
      *
     C                   ENDSR
      *
      *----------------------------------------------------------------
      * SET ON LR AND END JOB
      *----------------------------------------------------------------
      *
     C     $EXIT         BEGSR
     C                   SETON                                        LR
     C                   ENDSR
      *
      /EJECT
      *---------------------------------------------------------------
      * INITIALIZATION SUBROUTINE
      *---------------------------------------------------------------
      *
     C     *INZSR        BEGSR
      *
      * PARM LISTS
      *     NONE
      *
      * DEFINE LDA
      *
     C     *DTAARA       DEFINE    *LDA          LDA
     C                   IN        LDA
      *
      * INITIALIZE LDA FIELDS
      *
     C                   MOVE      *BLANK        LDFSBO
     C                   Z-ADD     0             LDFYMD
     C                   Z-ADD     0             LDTYMD
     C                   Z-ADD     0             LDUYMD
     C                   Z-ADD     0             LDFMDY
     C                   Z-ADD     0             LDTMDY
     C                   Z-ADD     0             LDUMDY
     C                   MOVE      *BLANK        LDPPCD
     C                   Z-ADD     0             LDFSCD
     C                   Z-ADD     0             LDCELL
     C                   MOVE      *BLANK        LDMRCD
     C                   Z-ADD     0             LDHGSN
     C                   Z-ADD     0             LDCVNO
     C                   MOVE      *BLANK        LDBODS
     C                   MOVE      *BLANK        LDFSNM
     C                   MOVE      *BLANK        LDHGCD
     C                   MOVE      *BLANK        LDCNAM
     C                   MOVE      *BLANK        LDMRDS
     C                   MOVE      *BLANK        LDPPDS
      *
      * Initialize LDA fields for printing
      *
     C                   MOVE      *BLANK        LDHOLD
     C                   Z-ADD     0             LDCOPY
     C                   MOVEL     LDOUTU        LDOUTQ
     C                   MOVEL     '*JOBD'       XXJOBQ           10
     C                   MOVEL     NO            XXNITE            1
     C                   Z-ADD     0             XXCOPY            1 0
      *
      * GET THIS FUNCTION'S NUMBER OF COPIES, HOLD YES/NO
      *
     C     SDPGM         CHAIN     HSP002                             92
     C     *IN92         IFEQ      *OFF
     C                   Z-ADD     FNCOPY        LDCOPY
     C                   MOVEL     FNHOLD        LDHOLD
     C                   ENDIF
      *
     C     LDCOPY        IFEQ      0
     C                   Z-ADD     1             LDCOPY
     C                   ENDIF
      *
     C     LDHOLD        IFEQ      *BLANK
     C                   MOVEL     NO            LDHOLD
     C                   ENDIF
      *
      * GLOBAL DEFINES
      *
     C                   MOVE      *BLANK        PGM              10            USED IN MSG
     C                   MOVE      *BLANK        MSGFL            10            USED IN MSG
     C                   MOVE      *BLANK        ROUTNE            6
     C                   MOVEL     '00'          RTNCD             2            RETURN CODE
      *
      * NUMERIC PARM FIELDS
      *
     C     *LIKE         DEFINE    DFFSCD        WKFSCD
      *
      * ALPHA PARM FIELDS
      *
     C                   MOVE      *BLANK        XXAIST            1
     C                   MOVE      *BLANK        XXFSCD            5
     C     *LIKE         DEFINE    DFFSNM        XXFSNM
     C     *LIKE         DEFINE    DFFSBO        XXFSBO
     C     *LIKE         DEFINE    DFBODS        XXBODS
     C     *LIKE         DEFINE    DFPPCD        XXPPCD
     C     *LIKE         DEFINE    DFPPDS        XXPPDS
     C     *LIKE         DEFINE    DFHGCD        XXHGCD
     C     *LIKE         DEFINE    MRMRFL        XXMRFL
     C     *LIKE         DEFINE    DFMRCD        XXMRCD
     C     *LIKE         DEFINE    DFMRDS        XXMRDS
     C     *LIKE         DEFINE    HGHGSN        WKHGSN
      *
      * JDE FIELDS
     C                   MOVE      *BLANK        JDEFL             1            JDE ERR FLAG
     C                   MOVE      *BLANK        INAT1             3            JDE ADDR TYP
     C                   MOVE      *BLANK        OUALPH           40            JDE NAME
     C     *LIKE         DEFINE    DFCVNO        INAJD                          JDE ADDR IN
     C     *LIKE         DEFINE    DFCVNO        OUAJD                          JDR ADDR OUT
      *
      *  PARMS FOR USE WITH QCMDEXC.  'LENGTH' IS THE LENGTH OF THE
      *  COMMAND THAT WILL BE ISSUED WITH QCMDEXC.  QCMDEXC REQUIRES
      *  THE FIELD CONTAINING LENGTH TO BE DEFINED AS 15 WITH 5 DECIMAL
      *  POSITIONS.  THE FIELD 'CMDSBM' WILL HOLD THE VALUES OF THE
      *  THE SUBMIT JOB COMMAND WHEN PASSED AS A PARM ON THE CALL TO
      *  QCMDEXC.
      *
     C                   Z-ADD     80            LENGTH           15 5          QCMDEXC PARM
     C                   MOVE      *BLANK        CMDSBM           80            QCMDEXC PARM
      *
      * THE FOLLOWING STANDARD CODE MUST BE INCLUDED TO MAKE THE
      * STANDARD ERROR MESSAGE HANDLING PROGRAM FUNCTION PROPERLY.
      * THIS CODE SETS AND CLEARS THE PROGRAM MESSAGE QUEUE.
      *
     C                   Z-ADD     20            MAXMSG            2 0
     C                   MOVEL     '*'           MSGPGM                         SET QUEUE
     C                   EXSR      $CLMSG                                       CLEAR MSG
      *
      * EXECUTE THE SECURITY SUBROUTINE TO DETERMINE USER
      * AUTHORIZATIONS.
      *
     C                   MOVEL(P)  SDPGM         WKFNCD           10
     C                   MOVEL     NO            INBOFL
     C                   EXSR      $SEC
     C                   MOVEL     YES           INBOFL
      *
      * IF ERRORS ON INITIAL CALL, SET TERMINAL FLAG TO YES.
      *
     C     SECFL         IFEQ      YES
     C                   MOVEL     YES           TERMFL
     C                   ENDIF
      *
      * MOVE RETURNED FIELDS TO SCREEN
      *
     C                   MOVEL     OUBOBO        DFFSBO
     C                   MOVEL     OUBODS        DFBODS
      *
      * SAVE THIS FUNCTION'S DIVISION CODE
      *
     C     *LIKE         DEFINE    OUDVCD        SVDVCD
     C                   MOVEL     OUDVCD        SVDVCD
      *
      * DEFAULT 'FROM DATE' TO LAST DAY OF PREVIOUS PERIOD BY SUBTRACT-
      * ING ONE DAY FROM THE BEGINNING DATE OF THE CURRENT PERIOD.
      *
      * BRING IN THE DATA AREA THAT CONTAINS THE CURRENT ACCOUNTING
      * PERIOD
     C     *DTAARA       DEFINE                  DAAPER
     C                   IN        DAAPER
      *
     C     *iso          test(d)                 dabpdt                 92
     C                   if        *in92 = *off                                 If OK
     C     *iso          move      dabpdt        wkisodate
     C     *mdy          move      wkisodate     dffmdy
     C                   endif                                                  If OK
      *
      * DEFAULT TODAY'S DATE INTO THE 'TO DATE'.
      *
     C                   Z-ADD     UDATE         DFTMDY
      *
     C                   ENDSR
      /EJECT
      *****************************************************************
      * COMPILE TIME ARRAY NAMED 'CMD' USED IN QCMDEXC
      *****************************************************************
** CMD TABLE
SBMJOB CMD(CALL PGM(HP476CL)) JOB(MVMTRPT) JOBQ(__________)
