     A*%%TS  SD  20140312  131141  ISLRAMS     REL-V6R1M0  5761-WDS
     A*****************************************************************
     A*  ENVIRONMENT:   PORK DIVISION
     A*  SYSTEM:        AS/400
     A*  DISPLAY FILE:  MPD460
     A*  TITLE:         SPECIFY BATCH EDIT LISTING OF INVOICES
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  CREATED:       04/30/02
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A                                      CF04(04 'PROMPT')
     A                                      CF08(08 'SUBMIT')
     A                                      CF17(17 'PRINT OPTIONS')
     A                                      CF20(20 'WRKSPLF')
     A*
     A*---------------------------------------------------------------
     A* RECORD FORMAT ONE
     A*---------------------------------------------------------------
     A          R MP460R1
     A*%%TS  SD  20140312  131141  ISLRAMS     REL-V6R1M0  5761-WDS
     A                                      OVERLAY
     A                                      SETOF(31)
     A                                      SETOF(32)
     A                                      SETOF(33)
     A                                      SETOF(34)
     A                                      SETOF(50)
     A                                      RTNCSRLOC(&RCD &FLD)
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM         10A  O  1  2DSPATR(HI)
     A                                  1 26'MP2 INTERFACE TO JDE'
     A                                      DSPATR(HI)
     A            SDUSR         10A  O  1 70DSPATR(HI)
     A                                  2 18'SPECIFY EDIT/POST OF INVOICES TO J-
     A                                      DE'
     A                                      DSPATR(HI)
     A            SDJOB         10A  O  2 70DSPATR(HI)
     A                                  4  2'Edit/post flag ..'
     A            D1EPFL         1A  B  4 20
     A N31                                  DSPATR(HI)
     A  31                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A                                  4 23'(E=Edit only; no posting to JDE)'
     A                                  5 23'(P=Edit and Post to JDE)'
     A                                  7  2'Include all invoices:'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  8  2'With an invoice date on or before -
     A                                      ..'
     A            D1MDY     R        B  8 41REFFLD(MPREC/@@DT06 PRKPLIB/MP2REF)
     A                                      EDTWRD('  /  /  -')
     A N32                                  DSPATR(HI)
     A  32                                  DSPATR(PC)
     A  32                                  DSPATR(RI)
     A                                  8 52'(MMDDYY)'
     A                                  9  2'Created by AS400 User ID .........-
     A                                      ..'
     A            D1USER    R        B  9 41REFFLD(MPREC/@@USER PRKPLIB/MP2REF)
     A N33                                  DSPATR(HI)
     A  33                                  DSPATR(PC)
     A  33                                  DSPATR(RI)
     A                                 10  2'For Accounting Company ...........-
     A                                      ..'
     A                                 10 39'?'
     A            D1ACONO   R        B 10 41REFFLD(MPREC/@@ACONO PRKPLIB/MP2REF)
     A                                      EDTCDE(K)
     A N34                                  DSPATR(HI)
     A  34
     AO 50                                  DSPATR(PC)
     A  34                                  DSPATR(RI)
     A            D1ACONM       30   O 10 50
     A*
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R MP460K1
     A*%%TS  SD  20140312  131141  ISLRAMS     REL-V6R1M0  5761-WDS
     A  43                             21  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A                                 23 11'F4=Prompt'
     A                                 23 22'F8=Submit'
     A                                 23 33'F17=Print Options'
     A                                 23 52'F20=Spool Files'
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *---------------------------------------------------------------
     A          R MP460ES                   SFL SFLMSGRCD(24)
      *
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
     A*---------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*---------------------------------------------------------------
     A*
     A          R MP460EC                   SFLCTL(MP460ES)
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
