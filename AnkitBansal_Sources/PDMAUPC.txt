/********************************************************************/
/*                                                                  */
/*  SYNOPSIS -  JOB CONTROL PROMPT FOR PRINTER OVERRIDE INFORMATION */
/*              GENERIC PROMPTING FUNCTION. CAUSE PRINTER OVERRIDE  */
/*              PROMPTING AND PASSES COMPANY, PRODUCTION DATE,      */
/*              EXPORT/DOMESTIC INDICATOR, AND WAREHOUSE SELECTION  */
/*              CRITERIA.                                           */
/*                                                                  */
/*  SYSTEM    - ORDER MANAGEMENT SYSTEM                             */
/*  PROGRAM   - PRINT REPORTS                                       */
/*  DATE      - 05/01/98                                            */
/*                                                                  */
/*      PARAMETERS:                                                 */
/*                                                                  */
/*     O    &RETURN     - RETURN CODE                               */
/*     I    &CLP        - CL PROGRAM TO SUBMIT                      */
/*     I    &COMPNYN    - COMPANY NUMBER                            */
/*     I    &PRODDATE   - USER SPECIFIED WEEK BEGINNING DATE        */
/*     I    &EXPDEM     - USER SPECIFIED EXPORT/DOMESTIC SELECTION  */
/*     I    &WHSE1-10   - USER SPECIFIED WAREHOUSE SELECTIONS       */
/*     I    &SELOMT     - USER SPECIFIED SELECT/OMIT WAREHOUSE      */
/*     I    &DESC       - PRINT PROGRAM DESCRIPTION (FOR PROMPT     */
/*                         SCREEN)                                  */
/*     I    &OUTQ       - OUTPUT QUEUE                              */
/*     I    &HOLD       - HOLD STATUS                               */
/*     I    &SAVE       - SAVE STATUS                               */
/*     I    &NQSTAT     - INDICATOR WHETHER SUBMITTED JOB MUST RUN  */
/*                         IN NIGHT JOB QUEUE                       */
/*     I    &COPYN      - NUMBER OF COPIES                          */
/*     I    &PAPER      - SIZE OF PAPER TO USE                      */
/*                                                                  */
/********************************************************************/

 PDMAUPC:    PGM        PARM(&RETURN &CLP &COMPNYN &PRODDATE &EXPDOM +
                          &REAS1 &REAS2 &REAS3 &REAS4 &TRNTP &WHSE1 +
                          &WHSE2 &WHSE3 &WHSE4 &WHSE5 &WHSE6 &WHSE7 +
                          &WHSE8 &WHSE9 &WHSE10 &SELOMT &DESC &OUTQ +
                          &HOLD &SAVE &NQSTAT &COPYN &PAPER)
/* PARMS                                                                      */
        DCL    &RETURN     *CHAR   (7)           /* Return Code               */
        DCL    &APPLIC     *CHAR   (06)          /* Application Code          */
        DCL    &CLP        *CHAR   (10)          /* CL to Submit              */
        DCL    &PGM        *CHAR   (10)          /* RPG TO SUBMIT             */
        DCL    &DESC       *CHAR   (25)          /* PRT Pgm Description       */
        DCL    &OUTQ       *CHAR   (10)          /* Output Queue              */
        DCL    &HOLD       *CHAR   (4)           /* Hold Status               */
        DCL    &SAVE       *CHAR   (4)           /* Save Status               */
        DCL    &NQSTAT     *CHAR   (01)          /* If MUST run in Night Q    */
        DCL    &NIGHTQ     *CHAR   (10)          /* Night Job Queue           */
        DCL    &COMPNYN    *DEC                  /* Company Number (Passed)   */
        DCL    &COPYN      *DEC                  /* No. of Copies             */
        DCL    &PAPER      *CHAR   (1)           /* SIZE OF PAPER TO USE      */
        DCL    &PRODDATE   *DEC                  /* WEEK BEGIN DATE           */
        DCL    &EXPDOM     *CHAR   (1)           /* EXPORT/DOMESTIC           */
        DCL    &REAS1      *CHAR   (3)
        DCL    &REAS2      *CHAR   (3)
        DCL    &REAS3      *CHAR   (3)
        DCL    &REAS4      *CHAR   (4)
        DCL    &TRNTP      *CHAR   (2)
        DCL    &WHSE1      *CHAR   (3)           /* WAREHOUSE 1               */
        DCL    &WHSE2      *CHAR   (3)           /* WAREHOUSE 2               */
        DCL    &WHSE3      *CHAR   (3)           /* WAREHOUSE 3               */
        DCL    &WHSE4      *CHAR   (3)           /* WAREHOUSE 4               */
        DCL    &WHSE5      *CHAR   (3)           /* WAREHOUSE 5               */
        DCL    &WHSE6      *CHAR   (3)           /* WAREHOUSE 6               */
        DCL    &WHSE7      *CHAR   (3)           /* WAREHOUSE 7               */
        DCL    &WHSE8      *CHAR   (3)           /* WAREHOSUE 8               */
        DCL    &WHSE9      *CHAR   (3)           /* WAREHOUSE 9               */
        DCL    &WHSE10     *CHAR   (3)           /* WAREHOUSE 10              */
        DCL    &SELOMT     *CHAR   (1)           /* SELECT/OMIT               */
/* INTERNAL FIELDS                                                            */
        DCL    &COPY       *DEC    (2 0)         /* Number of copies          */

/* ERROR MESSAGE FIELDS                                                       */
        DCL    &MSG        *CHAR   (512)         /* message                   */
        DCL    &MSGID      *CHAR   (7)           /* message id                */
        DCL    &MSGL       *DEC    (5 0)

/********************************************************************/
/*   INITIALIZE VARIABLES                                           */
/********************************************************************/

             CHGVAR     VAR(&RETURN) VALUE('       ')
             CHGVAR     VAR(&COPY) VALUE(&COPYN)
/********************************************************************/
/*   MONITOR FOR ALL MESSAGES AT PROGRAM LEVEL                      */
/********************************************************************/

             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(MSGTRAP))

/********************************************************************/
/*   CALL PROMPT PROGRAM                                            */
/********************************************************************/

             CALL       PGM(PDV8PVR) PARM(&RETURN &DESC &OUTQ &HOLD +
                          &SAVE &COPY &NQSTAT &NIGHTQ &APPLIC &PAPER)
             CHGVAR     VAR(&COPYN) VALUE(&COPY)
             IF         COND(&RETURN *NE ' ') THEN(GOTO +
                          CMDLBL(USERQUIT))

/********************************************************************/
/*   SUBMIT PRINT CL PROGRAM                                        */
/********************************************************************/

/*           CALL       PGM(&CLP) PARM(&OUTQ &HOLD &SAVE &NIGHTQ +   */
/*                        &NQSTAT &COPYN &COMPNYN &PRODDATE &EXPDOM +*/
/*                        &REAS1 &REAS2 &REAS3 &REAS4 &TRNTP &WHSE1 +*/
/*                        &WHSE2 &WHSE3 &WHSE4 &WHSE5 &WHSE6 &WHSE7 +*/
/*                        &WHSE8 &WHSE9 &WHSE10 &SELOMT)             */
 SUBMIT:     SBMJOB     CMD(CALL PGM(&CLP) PARM(&OUTQ &HOLD &SAVE +
                          &NIGHTQ &NQSTAT &COPYN &COMPNYN &PRODDATE +
                          &EXPDOM &REAS1 &REAS2 &REAS3 &REAS4 +
                          &TRNTP &WHSE1 &WHSE2 &WHSE3 &WHSE4 &WHSE5 +
                          &WHSE6 &WHSE7 &WHSE8 &WHSE9 &WHSE10 +
                          &SELOMT)) JOB(&CLP) JOBQ(&NIGHTQ)
             GOTO       CMDLBL(END)

/*-------------------------USER QUIT--------------------------------*/
 USERQUIT:   CHGVAR     VAR(&RETURN) VALUE('Y2U0005')
             GOTO       CMDLBL(END)

/*----------------------RECEIVE MESSAGES----------------------------*/

 MSGTRAP:    RCVMSG     MSGTYPE(*EXCP) MSGDTA(&MSG) MSGDTALEN(&MSGL) +
                          MSGID(&MSGID)
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(END))

             SNDPGMMSG  MSGID(&MSGID) MSGF(QCPFMSG) MSGDTA(%SST(&MSG +
                          1 &MSGL)) MSGTYPE(*ESCAPE)
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(END))

 END:        ENDPGM

