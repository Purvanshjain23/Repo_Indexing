      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Hog Production System
      * PROGRAM:     HP6078
      * TITLE:       Room Availability Report
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     09/03/02
      *
      * FUNCTION:  Batch program to list room availablility.
      *
      *            This listing is submitted from HP4078-Specify Options for
      *            Room Availablity Report--which is called from a menu.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
      *
     Fhsp3078   if   e           k disk
      *   Workfile: Room Availability
      *
      *
     Fqprint    o    f  130        printer oflind(*inof)
      *
      /EJECT
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Standard fields
      *
     D prtfl           s              1    inz('N')
     D dash            s            130
     d rtime           s              6  0
      *
      *
      * Print fields
      *
     D h1ppcd          s                   like(ldppcd)
      *
      *
      *
      * Control fields
      *
     D first           s              1    inz('Y')
     D svfscd          s                   like(wrfscd)
      *
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *---------------------------------------------------------------
      * Local data area.
      *---------------------------------------------------------------
      *
     Dlda             uds                  dtaara(*lda)
     D  ldppcd                 1      5
     D  ldcell                 6      7  0
     D  ldfscd                 8     12  0
     D  ldmdy                 13     18  0
     D  ldcymd                19     26  0
      *
     D  ldppds                35     64
     D  ldfsnm                65     89
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * MAINLINE
      ****************************************************************
      *
      * Prepare to read the workfile and print headings the first time through.
      *
     C     *hival        setll     hsp3078
     C                   exsr      $h1hdr
      *
     C                   dou       *in90 = *on                                  Main do
     C                   read      hsp3078                                90
     C                   if        *in90 = *off                                 If not EOF
      *
      * Insert a blank line before each farm..except when you are already
      * going to a new page.
      *
     C                   select
     C                   when      first = yes
     C                   z-add     wrfscd        svfscd
     C                   move      no            first
      *
     C                   when      wrfscd <> svfscd
     C                   z-add     wrfscd        svfscd
     C                   seton                                        87
     C                   if        *inof = *on
     C                   exsr      $h1hdr
     C                   else
     C                   except    blank
     C                   endif
      *
     C                   when      *inof = *on
     C                   exsr      $h1hdr
     C                   endsl
      *
      * Print detail line
     C                   except    l1dtl
     C                   setoff                                       87
     C                   move      yes           prtfl
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Main do
      *
      *---------------------------------------------------------------
      * EOF processing
      *---------------------------------------------------------------
      * If the file was empty, print the standard 'no data...'
      * error message
      *
     C                   if        prtfl = no
     C                   except    nodata
     C                   endif
      *
     C                   seton                                        lr
      /EJECT
      *---------------------------------------------------------------
      * Print headings
      *---------------------------------------------------------------
      *
     C     $h1hdr        begsr
      *
     C                   seton                                        87
     C                   except    h1hdr
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Set up a line of underscores and retrieve time for report heading.
      *
     C                   movel     *all'-'       dash
     C                   time                    rtime
      *
      * Set up headings
      *
     C                   if        ldppcd = *blank
     C                   movel     'All'         h1ppcd
     C                   else
     C                   move      ldppcd        h1ppcd
     C                   endif
      *
     C                   if        ldcell = 0
     C                   seton                                        88
     C                   endif
      *
     C                   if        ldfscd = 0
     C                   seton                                        89
     C                   endif
      *
     C                   endsr
      /EJECT
      *-------------------------------------------------------------
      * Report heading lines
      *-------------------------------------------------------------
      *
     Oqprint    e            h1hdr          1 01
     O                       sdpgm               10
     O                                           72 'HOG PRODUCTION SYSTEM'
     O                                          120 'DATE'
     O                       udate         y    130
      *
     O          e            h1hdr          1
     O                       sdusr               10
     O                                           74 'ROOM AVAILABILITY REPORT'
     O                                          120 'TIME'
     O                       rtime              130 '  :  :  '
      *
     O          e            h1hdr          2
     O                                          120 'PAGE'
     O                       page          z    130
      *
      *
     O          e            h1hdr          1
     O                                           20 'As-of date ........:'
     O                       ldmdy               30 '  /  /  '
      *
      *
     O          e            h1hdr          1
     O                                           20 'Production phase ..:'
     O                       h1ppcd              27
     O                       ldppds              62
      *
      *
     O          e            h1hdr          1
     O                                           20 'Cell ..............:'
     O              N88      ldcell        z     24
     O               88                          25 'All'
      *
      *
     O          e            h1hdr          2
     O                                           20 'Farm site .........:'
     O              N89      ldfscd        z     27
     O              N89      ldfsnm              57
     O               89                          25 'All'
      *
      *
      *-------------------------------------------------------------
      * Column headings
      *-------------------------------------------------------------
      *
     O          e            h1hdr          1
     O                                            4 'AVG'
     O                                           33 'ROOM'
     O                                           44 'CURRENT'
     O                                           63 '--- SCHEDULED ---'
     O                                           73 'PLANNED'
     O                                           93 'SPACE'
     O          e            h1hdr          1
     O                                            4 'DAYS'
     O                                           11 'FARM'
     O                                           33 'CAPACITY'
     O                                           44 'INVENTORY'
     O                                           53 'IN'
     O                                           63 'OUT'
     O                                           73 'IN'
     O                                           82 'TOTAL'
     O                                           93 'AVAILABLE'
     O          e            h1hdr          1
     O                                            4 'OLD'
     O                                           11 'SITE'
     O                                           17 'BLDG'
     O                                           24 'ROOM'
     O                                           33 'HEAD'
     O                                           44 'HEAD'
     O                                           53 'HEAD'
     O                                           63 'HEAD'
     O                                           73 'HEAD'
     O                                           82 'HEAD'
     O                                           93 'HEAD'
     O                                          100 'CELL'
     O                                          116 'FARM SITE NAME'
      *
     O          e            h1hdr          1
     O                       dash               130
      *
      *-------------------------------------------------------------
      * Detail line
      *-------------------------------------------------------------
      *
     O          e            l1dtl       1
     O               87      wravgage      k      4
     O               87      wrfscd        z     11
     O                       wrblcd              18
     O                       wrrmcd              25
     O                       wrcphd        k     34
     O                       wrinhd        k     45
     O                       wrscinhd      k     54
     O                       wrscouthd     k     64
     O                       wrplanhd      k     74
     O                       wrtothd       k     83
     O                       wravailhd     k     94
     O               87      wrcell        z    100
     O               87      wrfsnm             127
      *
      *-------------------------------------------------------------
      * Blank line
      *-------------------------------------------------------------
      *
     O          e            blank       1
      *
      *-------------------------------------------------------------
      * No data message line
      *-------------------------------------------------------------
      *
     O          e            nodata      1
     O                                           19 'NO DATA MEETS YOUR'
     O                                           38 'SELECTION CRITERIA'
