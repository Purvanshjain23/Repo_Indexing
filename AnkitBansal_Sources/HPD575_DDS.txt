     A*%%TS  SD  19941206  114001  ISLRAMS     REL-V3R0M5  5763-PW1
     A*****************************************************************
     A*
     A*  SYSTEM:        HOG PRODUCTION
     A*  DISPLAY FILE:  HPD575 - SELECT RATION ADDITIVE COST EFFECTIVE
     A*                          DATE
     A*  CREATED BY:    MARY PALMER
     A*  DATE:          08/14/94
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA12(12 'PREVIOUS')
     A N82                                  ROLLUP(25 'PAGE FORWARD')
     A*--------------------------------------------------------------*
     A* REFERENCE WINDOW RECORD FORMAT 1
     A*--------------------------------------------------------------*
     A          R HP575R1
     A                                      TEXT('Reference window')
     A                                      WINDOW(*DFT 16 62)
     A                                      WDWBORDER((*DSPATR RI) (*CHAR '    -
     A                                          '))
     A*--------------------------------------------------------------*
     A* RECORD FORMAT ONE
     A*--------------------------------------------------------------*
     A          R HP575R2
     A                                      TEXT('Written in RPG pgm to prevent-
     A                                       screen from clearing')
     A                                      CLRL(*NO)
     A                                      OVERLAY
     A*--------------------------------------------------------------*
     A* RECORD FORMAT TWO
     A*--------------------------------------------------------------*
     A* THIS FORMAT IS NOT WRITTEN IN THE RPG PGM.  IT PREVENTS THE
     A* CALLING_PROGRAM_SCREEN FROM BEING CLEARED.
     A*--------------------------------------------------------------*
     A          R HP575R3
     A                                      TEXT('Dummy format not written in R-
     A                                      PG pgm.')
     A                                      ASSUME
     A                                 11  1' '
     A*--------------------------------------------------------------*
     A* SUBFILE FORMAT ONE
     A*--------------------------------------------------------------
     A          R HP575S1                   SFL
     A*%%TS  SD  19941205  102350  ISLRAMS     REL-V3R0M5  5763-PW1
     A                                      TEXT('subfile record format one')
     A            HDRRN1         4S 0H
     A            DFMDY     R        O  6  1REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A            DFRNCD    R        O  6 12REFFLD(RCRNCD)
     A            DFADCD    R        O  6 21REFFLD(RCADCD)
     A            DFIBRT    R        O  6 29REFFLD(RCIBRT)
     A                                      EDTCDE(K)
     A            DFMBRT    R        O  6 47REFFLD(RCMBRT)
     A                                      EDTCDE(K)
     A*--------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*--------------------------------------------------------------
     A          R HP575C1                   SFLCTL(HP575S1)
     A*%%TS  SD  19941205  103150  ISLRAMS     REL-V3R0M5  5763-PW1
     A                                      SFLSIZ(0050)
     A                                      SFLPAG(0008)
     A                                      WINDOW(HP575R1)
     A                                      TEXT('control format one')
     A                                      OVERLAY
     A                                      SFLCSRRRN(&HDRRN)
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND(*MORE)
     A                                      RMVWDW
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            HDRRN          5S 0H
     A                                  1 10'SELECT RATION ADDITIVE COST EFFECT-
     A                                      IVE DATE'
     A                                      DSPATR(HI)
     A                                  2  1'Position Cursor, Press Enter'
     A                                  3 28'Ingredient Cost'
     A                                      DSPATR(HI)
     A                                  3 46'Medication Cost'
     A                                      DSPATR(HI)
     A                                  4  1'Date'
     A                                      DSPATR(HI)
     A                                  4 12'Ration'
     A                                      DSPATR(HI)
     A                                  4 20'Additive'
     A                                      DSPATR(HI)
     A                                  4 32'Per Batch'
     A                                      DSPATR(HI)
     A                                  4 50'Per Batch'
     A                                      DSPATR(HI)
     A            C1MDY     R        B  5  1REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A                                      DSPATR(HI)
     A            C1RNCD    R        B  5 12REFFLD(RCRNCD)
     A                                      DSPATR(HI)
     A            C1ADCD    R        B  5 21REFFLD(RCADCD)
     A                                      DSPATR(HI)
     A          R HP575K1
     A                                      WINDOW(HP575R1)
     A                                      OVERLAY
     A                                 15  1'F12=Previous'
