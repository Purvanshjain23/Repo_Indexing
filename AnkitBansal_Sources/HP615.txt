      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      HOG PRODUCTION
      * PROGRAM:     HP615
      * TITLE:       LISTING OF AUTHORIZED USERS FOR A FUNCTION
      * PROGRAMMER:  LEANNE FEDOR
      * CREATED:     02/10/95
      *
      * FUNCTION: SUBMITTED FROM HP425-WORK WITH FUNCTIONS.
      *           A SEPARATE REPORT WILL PRINT FOR EACH FUNCTION
      *           SELECTED.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 03/27/96  LEANNE FEDOR
      *           RECOMPILE ONLY. A NEW FIELD 'DELETE MOVEMENTS FLAG'
      *           WAS ADDED TO THE AUTHORIZED USERS FILE.
      *
      * 04/30/96  LEANNE FEDOR
      *           RECOMPILE ONLY. A NEW FIELD 'MOVEMENT STATUS'
      *           WAS ADDED TO THE AUTHORIZED USERS FILE TO SUPPORT
      *           THE LOGIC FOR DELETING MOVEMENTS.
      *
      * 05/14/97  LEANNE FEDOR
      *           RECOMPILE ONLY.  OUTQUEUE WAS ADDED TO THE
      *           AUTHORIZED USERS FILE. THIS FIELD WILL NOW BE THE
      *           USER'S DEFAULT PRINTER IN HPS INSTEAD OF THE OUTQ
      *           ON THE USER'S PROFILE.
      *
      * 06/17/97  LEANNE FEDOR
      *           REPLACED ENTRY PARM LOGIC WITH LDA VALUES.
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     FHSP001    IF   E           K DISK
      *    AUTHORIZED USERS          (KEY: AUUSCD)
      *
     FHSP002    IF   E           K DISK
      *    FUNCTIONS                 (KEY: FNFNCD)
      *
     FHSL003A   IF   E           K DISK
      *    FUNCTION AUTHORIZATION    (KEY: FAFNCD FAUSCD)
      *
     FQPRINT    O    F  132        PRINTER OFLIND(*INOF)
      *
      /EJECT
      ****************************************************************
      * INPUT SPECS
      ****************************************************************
      *
      *---------------------------------------------------------------
      * STANDARD PROGRAM STATUS DATA STRUCTURE
      *---------------------------------------------------------------
      *    EXTERNALLY DEFINED AS UTPGFR (RECORD FORMAT: PGMDSR)
     D PGMDS         ESDS                  EXTNAME(UTPGFR)
      *
      *---------------------------------------------------------------
      *  NAMED CONSTANTS
      *---------------------------------------------------------------
      *
     D YES             C                   CONST('Y')
     D NO              C                   CONST('N')
      *
      *
      *----------------------------------------------------------------
      * LDA - REPORT SELECTIONS
      *----------------------------------------------------------------
      *
     D                UDS
     D  LDFNCD                 2     11
      *
     D                 DS
     D  DUMMYD                 1      1
      *  THE PRECEDING DUMMY DATA STRUCTURE IS ONLY TO KEEP THE I-SPEC
      *  INFO FROM RUNNING OVER INTO THE 'CALC SPECS' ON THE COMPILE
      *  LISTING.  IT SERVES NO OTHER PURPOSE AND CAN BE DELETED
      *  AT ANY TIME!!!
      /EJECT
      ****************************************************************
      * MAIN LINE
      ****************************************************************
      *
     C                   EXCEPT    HRHDR
     C     LDFNCD        SETLL     HSL003A
     C                   SETOFF                                       92
      *
      * READ AND PRINT USERS OF THE SELECTED FUNCTION
      *
     C     *IN92         DOUEQ     *ON                                          MAIN DO
     C     LDFNCD        READE     HSL003A                                92
     C     *IN92         IFEQ      *OFF                                         IF NOT EOF
      *
      * RETRIEVE USER INFORMATION
      *
     C     FAUSCD        CHAIN     HSP001                             93
     C     *IN93         IFEQ      *ON
     C                   MOVEL     'UNKNOWN'     DTUSNM
     C                   ELSE
     C                   MOVEL     AUUSNM        DTUSNM
     C                   MOVEL     AUSLCD        DTSLCD
     C                   MOVEL     AURUFL        DTRUFL
     C                   MOVEL     AUMDFL        DTMDFL
     C                   MOVEL     AURMFL        DTRMFL
     C                   MOVEL     AURVFL        DTRVFL
     C                   MOVEL     AUAFFL        DTAFFL
     C                   ENDIF
      *
     C     *INOF         IFEQ      *ON
     C                   EXCEPT    HRHDR
     C                   ENDIF
     C                   EXCEPT    DTLINE
      *
     C                   MOVEL     YES           PRTFLG
     C                   ENDIF                                                  IF NOT EOF
     C                   ENDDO                                                  MAIN DO
      *
      *---------------------------------------------------------------
      * EOF PROCESSING
      *---------------------------------------------------------------
      * IF NO DATA WAS PRINTED, PRINT THE STANDARD 'NO DATA...' ERROR
      * MESSAGE
      *
     C     PRTFLG        IFEQ      NO
     C                   EXCEPT    NODATA
     C                   ENDIF
      *
     C                   SETON                                        LR
      /EJECT
      *---------------------------------------------------------------
      * *INZSR - INITIALIZATION SUBROUTINE
      *---------------------------------------------------------------
      *
     C     *INZSR        BEGSR
      *
      *
      * DEFINE PRINT FIELDS FOR HEADING
      *
     C     *LIKE         DEFINE    FNFNDS        HRFNDS
      *
      * DEFINE PRINT FIELDS FOR DETAIL LINE
      *
     C     *LIKE         DEFINE    AUUSNM        DTUSNM
     C     *LIKE         DEFINE    AUSLCD        DTSLCD
     C     *LIKE         DEFINE    AURUFL        DTRUFL
     C     *LIKE         DEFINE    AUMDFL        DTMDFL
     C     *LIKE         DEFINE    AURMFL        DTRMFL
     C     *LIKE         DEFINE    AURVFL        DTRVFL
     C     *LIKE         DEFINE    AUAFFL        DTAFFL
      *
      * RETRIEVE FUNCTION DESCRIPTION FOR HEADING
      *
     C     LDFNCD        CHAIN     HSP002                             95
     C     *IN95         IFEQ      *OFF
     C                   MOVEL     FNFNDS        HRFNDS
     C                   ELSE
     C                   MOVEL     'UNKNOWN'     HRFNDS
     C                   ENDIF
      *
      * SET PRINT FLAG TO DETERMINE IF ANY RECORDS WERE PRINTED
      * SET UP HEADING UNDERLINE FIELD
      * RETRIEVE TIME FOR HEADING
      *
     C                   MOVEL     NO            PRTFLG            1
     C                   MOVE      *ALL'-'       DASH            132
     C                   TIME                    RTIME             6 0
      *
     C                   ENDSR
      /EJECT
      *-------------------------------------------------------------
      * REPORT HEADING
      *-------------------------------------------------------------
      *
     OQPRINT    E            HRHDR          1 01
     O                       SDPGM               10
     O                                           63 'HOG PRODUCTION SYSTEM'
     O                                          122 'DATE'
     O                       UDATE         Y    132
      *
     O          E            HRHDR          1
     O                       SDUSR               10
     O                                           53 'LISTING OF AUTHORIZED'
     O                                           74 'USERS FOR A FUNCTION'
     O                                          122 'TIME'
     O                       RTIME              132 '  :  :  '
      *
     O          E            HRHDR          1
     O                                          122 'PAGE'
     O                       PAGE          Z    132
      *
     O          E            HRHDR          2
     O                                            9 'FUNCTION:'
     O                       LDFNCD              21
     O                       HRFNDS              53
      *
     O          E            HRHDR          1
     O                                           20 'SECURITY'
     O                                           32 'RESTRICTED'
     O                                           41 'MODIFY'
     O                                           54 'RESTRICTED'
     O                                           68 'RESTRICTED'
     O                                           79 'REQUIRES'
      *
     O          E            HRHDR          1
     O                                            9 'USER CODE'
     O                                           20 'LOCATION'
     O                                           29 'USER'
     O                                           40 'DATA'
     O                                           55 'MAINTENANCE'
     O                                           65 'VIEW'
     O                                           80 'FUNC AUTH'
     O                                           91 'USER NAME'
      *
     O          E            HRHDR          1
     O                       DASH               132
      *
      *-------------------------------------------------------------
      * DETAIL
      *-------------------------------------------------------------
      *
     O          E            DTLINE      1
     O                       FAUSCD         B    10
     O                       DTSLCD         B    20
     O                       DTRUFL         B    27
     O                       DTMDFL         B    39
     O                       DTRMFL         B    50
     O                       DTRVFL         B    64
     O                       DTAFFL         B    76
     O                       DTUSNM         B   132
      *
      *-------------------------------------------------------------
      * NO DATA MESSAGE LINE
      *-------------------------------------------------------------
      *
     O          E            NODATA      1
     O                                           19 'NO DATA MEETS YOUR'
     O                                           38 'SELECTION CRITERIA'
