      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Hog Production System
      * PROGRAM:     HP3700-Delete Template Items
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     02/02/04
      *
      * Function:    Depending on the incoming parm, this function:
      *                1) deletes all records from the Budget Template Detail file, or
      *                2) deletes only the records that have zero quantities
      *
      *              This program is called from HP4700-Work With Budget Templates.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      /eject
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fhsp186    uf   e           k disk
      *    Budget template detail
      *
      /eject
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      *----------------------------------------------------------------
      * CONSTANTS
      *----------------------------------------------------------------
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      *----------------------------------------------------------------
      * STANDALONE FIELDS
      *----------------------------------------------------------------
      *
      * Standard fields
      *
     D rtncd           s              2
      *
      *
      * Parms
      *
     D xxbtcd          s                   like(tdbtcd)
     D xxonly0         s              1
      *
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * Mainline
      ****************************************************************
      *
      * Process each Budget template detail record for the incoming Budget
      * Template code.
      *
     C     xxbtcd        setll     hsp186
      *
     C                   dou       *in90 = *on                                  Main do
     C     xxbtcd        reade     hsp186                                 90
     C                   if        *in90 = *off                                 If not EOF
      *
     C                   if        (xxonly0 = yes and tdbtqt = 0) or
     C                             xxonly0 = no
     C                   delete    tdrec
     C                   endif
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Main do
      *
     C                   seton                                        lr
      /eject
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
     C     *entry        plist
     C                   parm                    xxbtcd
     C                   parm                    xxonly0
     C                   parm                    rtncd
      *
     C                   endsr
      /eject
