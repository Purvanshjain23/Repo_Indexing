      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Hog Production System
      * PROGRAM:     HP6014
      * TITLE:       Listing of Transportation Offices
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     12/23/02
      *
      * FUNCTION:  Batch program to list the records in the file.
      *            This listing is submitted from HP4014-Work With Transportation Offices.
      *
      *            The listing 'selections' will be the positioner/selector values
      *            in the control record at the time the user submitted the listing.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fhsp109    if   e           k disk
      *    Transportation offices
      *
      *
     Fqprint    o    f   95        printer oflind(*inof)
      *
      /EJECT
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Standard fields
      *
     D prtfl           s              1    inz('N')
     D dash            s             95
     d rtime           s              6  0
      *
      * Print fields
      *
     D h1status        s             75
     D h1office        s             75
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *---------------------------------------------------------------
      * Local data area.
      *---------------------------------------------------------------
      *
     Dlda             uds                  dtaara(*lda)
     D  ldtocd                 1      5
     D  ldaist                 6      6
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * MAINLINE
      ****************************************************************
      *
      * Prepare to read the file and print headings the first time through.
      *
     C     ldtocd        setll     hsp109
     C                   except    h1hdr
      *
      * Read all records in the file. Check the user's selected status with
      * the status in the record before printing.
      *
     C                   dou       *in90 = *on                                  Main do
     C                   read      hsp109                                 90
     C                   if        *in90 = *off and                             If not EOF
     C                             (ldaist = ' ' or ldaist = toaist)
      *
      * Print detail line
      *
     C                   if        *inof = *on
     C                   except    h1hdr
     C                   endif
      *
     C                   except    l1dtl
     C                   movel     yes           prtfl
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Main do
      *
      *---------------------------------------------------------------
      * EOF processing
      *---------------------------------------------------------------
      * If the file was empty, print the standard 'no data...' message.
      *
     C                   if        prtfl = no
     C                   except    nodata
     C                   endif
      *
     C                   seton                                        lr
      /EJECT
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Set up a line of underscores and retrieve time for report heading.
      *
     C                   movel     *all'-'       dash
     C                   time                    rtime
      *
      * Set up headings for user selections:
      *
     C                   if        ldtocd = *blank
     C                   eval      h1office = 'Listing begins with the ' +
     C                                        'first transportation office'
     C                   else
     C                   eval      h1office = 'Listing begins with ' +
     C                                        'transportation office ' +
     C                                        ldtocd
     C                   endif
      *
      *
      * Active/inactive status
      *
     C                   select
     C                   when      ldaist = *blank
     C                   eval      h1status = 'Listing includes both Active ' +
     C                                        'and Inactive offices'
     C                   when      ldaist = 'A'
     C                   eval      h1status = 'Listing includes only Active ' +
     C                                        'offices'
     C                   when      ldaist = 'I'
     C                   eval      h1status = 'Listing includes only ' +
     C                                        'Inactive offices'
     C                   endsl
      *
     C                   endsr
      /EJECT
      *-------------------------------------------------------------
      * Report heading lines
      *-------------------------------------------------------------
      *
     Oqprint    e            h1hdr          1 01
     O                       sdpgm               10
     O                                           49 'HOG PRODUCTION SYSTEM'
     O                                           85 'DATE'
     O                       udate         y     95
      *
     O          e            h1hdr          1
     O                       sdusr               10
     O                                           48 'LISTING OF TRANSPORTATION '
     O                                           55 'OFFICES'
     O                                           85 'TIME'
     O                       rtime               95 '  :  :  '
      *
     O          e            h1hdr          2
     O                                           85 'PAGE'
     O                       page          z     95
      *
      *
     O          e            h1hdr          1
     O                       h1office            75
      *
      *
     O          e            h1hdr          2
     O                       h1status            75
      *
     O          e            h1hdr          1
     O                                            4 'TRAN'
     O                                           42 'DEFAULT'
     O                                           63 'DEFAULT'
     O                                           83 'DEFAULT'
     O                                           95 'ACTIVE/'
      *
     O          e            h1hdr          1
     O                                            6 'OFFICE'
     O                                           41 'OUTPUT'
     O                                           54 'DEFAULT'
     O                                           61 'LOAD'
     O                                           72 'DEFAULT'
     O                                           85 'ROUTE START'
     O                                           95 'INACTIVE'
     O          e            h1hdr          1
     O                                            4 'CODE'
     O                                           12 'NAME'
     O                                           40 'QUEUE'
     O                                           54 'TRUCKER'
     O                                           61 'CREW'
     O                                           71 'DRIVER'
     O                                           82 'POINT'
     O                                           95 'STATUS'
      *
     O          e            h1hdr          1
     O                       dash                95
      *
      *-------------------------------------------------------------
      * Detail line
      *-------------------------------------------------------------
      *
     O          e            l1dtl       1
     O                       totocd               5
     O                       totonm              33
     O                       tooutq              45
     O                       toticd              53
     O                       tolccd              62
     O                       todicd              71
     O                       torspcd             80
     O                       toaist              93
      *
      *-------------------------------------------------------------
      * No data message line
      *-------------------------------------------------------------
      *
     O          e            nodata      1
     O                                           19 'NO DATA MEETS YOUR'
     O                                           38 'SELECTION CRITERIA'
