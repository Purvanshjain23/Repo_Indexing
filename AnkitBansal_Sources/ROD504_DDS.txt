     A*%%TS  SD  20070122  104325  ISLRAMS     REL-V5R3M0  5722-WDS
     A*****************************************************************
     A*
     A*  SYSTEM:        RESOURCE OPTIMIZATION
     A*  DISPLAY FILE:  ROD504- SELECT ITEM
     A*  CREATED BY:    LEANNE RAMSEY
     A*  DATE:          01/17/07
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/ROPREF)
     A                                      PRINT
     A                                      CA12(12 'PREVIOUS')
     A                                      CF04(04 'PROMPT')
     A N82                                  ROLLUP(25 'PAGE FORWARD')
     A*---------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*---------------------------------------------------------------
     A          R RO504S1                   SFL
     A*%%TS  SD  20070122  104325  ISLRAMS     REL-V5R3M0  5722-WDS
     A            HDRRN1    R        H      REFFLD(@@RRN)
     A            D1ITCD    R        O  8  2REFFLD(@@ITCD)
     A                                      EDTCDE(Z)
     A            D1ITDS    R        O  8 12REFFLD(@@ITDS)
     A            D1ISCLCD  R        O  8 44REFFLD(@@ISCLCD)
     A                                      EDTCDE(Z)
     A            D1ISGRCD  R        O  8 51REFFLD(@@ISGRCD)
     A                                      EDTCDE(Z)
     A            D1ISTYCD  R        O  8 58REFFLD(@@ISTYCD)
     A                                      EDTCDE(Z)
     A            D1PGCD    R        O  8 64REFFLD(ROREF/@@PGCD *LIBL/ROPREF)
     A*
     A*---------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*---------------------------------------------------------------
     A          R RO504C1                   SFLCTL(RO504S1)
     A*%%TS  SD  20070122  104325  ISLRAMS     REL-V5R3M0  5722-WDS
     A                                      SFLSIZ(0100)
     A                                      SFLPAG(0009)
     A                                      WINDOW(RO504EC)
     A                                      SETOF(51)
     A                                      SETOF(52)
     A                                      SETOF(53)
     A                                      SETOF(54)
     A                                      RTNCSRLOC(&RCD &FLD)
     A                                      OVERLAY
     A                                      SFLCSRRRN(&HDRRN)
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A            HDPAG1    R        H      REFFLD(@@PAG)
     A                                      SFLRCDNBR
     A            RCD           10A  H
     A            FLD           10A  H
     A            HDRRN     R        H      REFFLD(@@RRN)
     A            SDPGM     R        O  1  1REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 31'SELECT ITEM'
     A                                      DSPATR(HI)
     A                                  3  2'Position Cursor, Press Enter'
     A                                  3 46'?'
     A                                      DSPATR(HI)
     A                                  3 53'?'
     A                                      DSPATR(HI)
     A                                  3 59'?'
     A                                      DSPATR(HI)
     A                                  3 66'?'
     A                                      DSPATR(HI)
     A                                  4 44'--Item Structure--'
     A                                      DSPATR(HI)
     A                                  4 64'Primal'
     A                                      DSPATR(HI)
     A                                  5  2'Item'
     A                                      DSPATR(HI)
     A                                  5 44'Class'
     A                                      DSPATR(HI)
     A                                  5 51'Group'
     A                                      DSPATR(HI)
     A                                  5 58'Type'
     A                                      DSPATR(HI)
     A                                  5 64'Group'
     A                                      DSPATR(HI)
     A                                  6  2'Code'
     A                                      DSPATR(HI)
     A                                  6 12'Description'
     A                                      DSPATR(HI)
     A                                  6 44'Code'
     A                                      DSPATR(HI)
     A                                  6 51'Code'
     A                                      DSPATR(HI)
     A                                  6 58'Code'
     A                                      DSPATR(HI)
     A                                  6 65'Code'
     A                                      DSPATR(HI)
     A            C1ITCD    R        B  7  2REFFLD(@@ITCD)
     A                                      DSPATR(HI)
     A            C1ITDS    R        B  7 12REFFLD(@@ITDS)
     A                                      CHGINPDFT(UL LC FE)
     A                                      DSPATR(HI)
     A            C1ISCLCD  R        B  7 44REFFLD(@@ISCLCD)
     A                                      EDTCDE(M)
     A                                      DSPATR(HI)
     A  51                                  DSPATR(PC)
     A            C1ISGRCD  R        B  7 51REFFLD(@@ISGRCD)
     A                                      EDTCDE(M)
     A                                      DSPATR(HI)
     A  52                                  DSPATR(PC)
     A            C1ISTYCD  R        B  7 58REFFLD(@@ISTYCD)
     A                                      EDTCDE(M)
     A                                      DSPATR(HI)
     A  53                                  DSPATR(PC)
     A            C1PGCD    R        B  7 64REFFLD(ROREF/@@PGCD *LIBL/ROPREF)
     A                                      DSPATR(HI)
     A  54                                  DSPATR(PC)
     A*---------------------------------------------------------------
     A* COMMAND LINE LINE
     A*---------------------------------------------------------------
     A          R RO504K1
     A                                      WINDOW(RO504EC)
     A                                      OVERLAY
     A  43                             17  1'Message program failed. Call IS.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 18  1'F4=Prompt'
     A                                 18 12'F12=Previous'
      *
     A*-------------------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
     A*-------------------------------------------------------------------------
     A          R RO504ES                   SFL SFLMSGRCD(19)
      *
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
     A*
     A*-------------------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*-------------------------------------------------------------------------
     A*
     A          R RO504EC                   SFLCTL(RO504ES)
     A*%%TS  SD  19980327  105628  ISLRAMS     REL-V3R7M0  5716-PW1
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A                                      WINDOW(*DFT 20 75)
     A                                      WDWBORDER((*DSPATR RI) (*CHAR '    -
     A                                          '))
     A            MSGPGM                    SFLPGMQ(10)
     A*
     A*---------------------------------------------------------------
     A* ASSUME FORMAT -- REQUIRED FOR WINDOWS
     A*---------------------------------------------------------------
     A          R ASSUME
     A*%%TS  SD  20011025  160559  ISLRAMS     REL-V4R4M0  5769-PW1
     A                                      ASSUME
     A                                      OVERLAY
     A                                 24 79' '
