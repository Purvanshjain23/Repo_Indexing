/*********************************************************************/
/* PROGRAM: PBDIUPC                                                  */
/* TITLE: DLTSPLF PARM( ... )                                        */
/* DATE: 05/04/2012                                                  */
/* DESCRIPTION: DELETE SPOOLED FILE WITH ADDITIONAL PARAMETERS       */
/*                                                                   */
/*  PARAMETERS:                                                      */
/*  &FILE       SPOOL FILE NAME                                      */
/*  &JOB        JOB NAME                                             */
/*  &JOBUSER    JOB USER                                             */
/*  &JOBNBR     JOB NUMBER                                           */
/*  &SELECT     *ALL, *CURRENT, OR USER ID                           */
/*  &PRTDEV     *OUTQ, *ALL, NAME                                    */
/*  &FORMTYPE   FOR TYPE, *ALL, *STD                                 */
/*  &USERDATA   USER DATA, *ALL                                      */
/*********************************************************************/
/* Modifications:                                                    */
/*                                                                   */
/*********************************************************************/
             PGM        PARM(&FILE &JOB &JOBUSER &JOBNBR &SPLFNBR +
                          &SELECT  &PRTDEV &FORMTYPE &USERDATA)

/* VARIABLES                                                         */

             DCL  VAR(&FILE)     TYPE(*CHAR) LEN(10)
             DCL  VAR(&JOB)      TYPE(*CHAR) LEN(10)
             DCL  VAR(&JOBUSER)  TYPE(*CHAR) LEN(10)
             DCL  VAR(&JOBNBR)   TYPE(*CHAR) LEN(6)
             DCL  VAR(&SPLFNBR)  TYPE(*CHAR) LEN(6)
             DCL  VAR(&SELECT)   TYPE(*CHAR) LEN(10)
             DCL  VAR(&PRTDEV)   TYPE(*CHAR) LEN(10)
             DCL  VAR(&FORMTYPE) TYPE(*CHAR) LEN(10)
             DCL  VAR(&USERDATA) TYPE(*CHAR) LEN(10)

/* Delete SPLF, MONITOR FOR FILE NOT FOUND, NO ACTIVE FILES,         */
/* BLANK SPOOLED FILE NAME, AND ETC.                                 */
             MONMSG     MSGID(CPF3309 CPF3303 CPF0000 CPF9999)

/* SETUP DEFAULT VALUES IF VARIABLES ARE BLANK                       */
             IF         COND(&SPLFNBR *EQ '     ') THEN(DO)
             CHGVAR     VAR(&SPLFNBR) VALUE('*LAST')
             ENDDO
             IF         COND((&SELECT *EQ '     ') *OR (&SELECT *EQ +
                          '*CURRENT')) THEN(DO)
             CHGVAR     VAR(&SELECT) VALUE('*CURRENT')
             CHGVAR     VAR(&PRTDEV) VALUE('*ALL')
             CHGVAR     VAR(&FORMTYPE) VALUE('*ALL')
             CHGVAR     VAR(&USERDATA) VALUE('*ALL')
             ENDDO

/* DELETE SPOOLED FILE                                               */
             IF         COND(&JOB *EQ '     ') THEN(DO)
             DLTSPLF    FILE(&FILE) SPLNBR(&SPLFNBR) SELECT(&SELECT +
                          &PRTDEV &FORMTYPE &USERDATA)
             ENDDO

             IF         COND(&JOB *NE '     ') THEN(DO)
             DLTSPLF    FILE(&FILE) JOB(&JOBNBR/&JOBUSER/&JOB) +
                          SPLNBR(&SPLFNBR) SELECT(&SELECT &PRTDEV +
                          &FORMTYPE &USERDATA)
             ENDDO

 END:        ENDPGM
