     A*****************************************************************
     A*
     A*  ENVIRONMENT:   J.D.EDWARDS
     A*  DISPLAY FILE:  SELDSUBW - Select JDE Subsidiary for Cost Center
     A*                 and Object that was passed in for JDE World.
     A*  CREATED BY:    Brad baden   SDN465   TFS# 16169
     A*  DATE:           3/13/2020
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(*DS3 *DS4)
     A                                      REF(*LIBL/F0901)
     A                                      PRINT
     A                                      CA12(12 'PREVIOUS')
     A N82                                  ROLLUP(25 'PAGE FORWARD')
     A*-------------------------------------------------------------------------
     A* WINDOW DEFINITION
     A*-------------------------------------------------------------------------
     A*
     A          R SUBSELW1
     A                                      WINDOW(*DFT 16 65)
     A                                      WDWBORDER((*DSPATR RI) (*CHAR '    -
     A                                          '))
     A*
     A*---------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*---------------------------------------------------------------
     A          R SUBSELS1                  SFL
     A            HDRRN1         5S 0H
     A            HDAID     R        H      REFFLD(GMAID)
     A            D1MCU     R        O  6  1REFFLD(GMMCU)
     A            D1OBJ     R        O  6 15REFFLD(GMOBJ)
     A            D1SUB     R        O  6 23REFFLD(GMSUB)
     A            D1DL01    R        O  6 36REFFLD(GMDL01)
     A*
     A*---------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*---------------------------------------------------------------
     A          R SUBSELC1                  SFLCTL(SUBSELS1)
     A                                      SFLSIZ(0025)
     A                                      SFLPAG(0008)
     A                                      WINDOW(SUBSELW1)
     A                                      OVERLAY
     A                                      SFLCSRRRN(&HDRRN)
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND(*MORE)
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            HDRRN          5S 0H
     A            SDPGM         10A  O  1  1DSPATR(HI)
     A                                  1  2'SELSUBW'
     A                                  1 15'SELECT J.D. EDWARDS SUBSIDIARY'
     A                                      DSPATR(HI)
     A                                  2  1'Posting Company:'
     A            C1CO           3A  O  2 18
     A            C1CONM        30A  O  2 23
     A                                  4  2'COST CENTER'
     A                                      DSPATR(HI)
     A                                  4 15'OBJECT'
     A                                      DSPATR(HI)
     A                                  4 23'SUBSIDIARY'
     A                                      DSPATR(HI)
     A                                  4 36'DESCRIPTION'
     A                                      DSPATR(HI)
     A            C1MCU     R        O  5  1REFFLD(GMMCU)
     A                                      DSPATR(HI)
     A            C1OBJ     R        O  5 15REFFLD(GMOBJ)
     A                                      DSPATR(HI)
     A            C1SUB     R        B  5 23REFFLD(GMSUB)
     A                                      DSPATR(HI)
     A            C1DL01    R        B  5 36REFFLD(GMDL01)
     A                                      DSPATR(HI)
     A*---------------------------------------------------------------
     A* COMMAND LINE LINE
     A*---------------------------------------------------------------
     A          R SUBSELK1
     A                                      WINDOW(SUBSELW1)
     A                                      OVERLAY
     A                                 15  1'F12=Previous'
     A*
      *
     A*---------------------------------------------------------------
     A* ASSUME FORMAT -- REQUIRED FOR WINDOWS
     A*---------------------------------------------------------------
     A          R ASSUME
     A                                      ASSUME
     A                                      OVERLAY
     A                                 24 79' '
