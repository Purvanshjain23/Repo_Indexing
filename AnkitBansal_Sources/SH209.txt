      *****************  RPG PROGRAM HEADING  ************************
     h option(*SRCSTMT:*NODEBUGIO)
      *
      * ENVIRONMENT: Pork Division
      * SYSTEM:      OMS---Datamart
      * PROGRAM:     SH209 - Datamart Sales: Sales History Costs (Seaboard)
      * PROGRAMMER:  LeAnne Ramsey
      * CREATED:     03/19/08
      *
      * FUNCTION: This program populates the "cost" fields in the Sales History file for
      *           the Seaboard/Triumph records--no Dailys data is processed in this
      *           program.
      *
      *           No records will be deleted/added thru this program. We are only
      *           clearing/populating the Cost fields.
      *
      *           Initially the cost logic was in SH204.
      *
      *           The cost info is being pulled from files in the TFS Fees & Payments System
      *           that are updated during the TFS Margin Adjustment Close function. What
      *           we found was:
      *           1) TFS users might skip several weeks before "closing" a week in TFS
      *           2) TFS users might reopen/reclose a week (I.S. has to reopen for them)
      *
      *           So, our sales history records were getting built with 1) no cost data or
      *           2) data that was good at the time but became "bad" when a TFS week was
      *           reopened/reclosed.
      *
      *           So, we pulled the original "cost" logic out of SH204 and put it in
      *           this program adding some logic to allow us to repopulate records
      *           where necessary.
      *
      ********************************************************************************************
      * MODIFICATIONS:
      ********************************************************************************************
      * Date      Programmer
      *
      * 03/31/08  LeAnne Ramsey
      *           Recompile only. Signal Demand Status Flag was removed from the
      *           Datamart Sales History file.
      *
      * 09/11/08  LeAnne Ramsey
      *           Recompile only. Export/Domestic Flag was added to TFP010-
      *           Weekly Product Revenue Detail file.
      *
      * 09/30/08  LeAnne Ramsey
      *           Recompile only. Invoice Suffix was added to SHP204-Datamart: Sales History.
      *
      * 11/05/08  LeAnne Ramsey
      *           Recompile only. Three fields were added to SHP204-Datamart: Sales History:
      *             Current Market Code
      *             Current Market Description
      *             Current Commissionable Item Flag
      *
      * 07/22/09  LeAnne Ramsey
      *           Recompile only. Three fields were added to SHP204-Datamart: Sales History:
      *             Palletize Flag
      *             Slip Sheet Flag
      *             Floor Loaded Flag
      *
      * 09/09/09  LeAnne Ramsey
      *           Recompile only. Two fields were added to SHP204-Datamart: Sales History:
      *             Inteneded for Export Flag
      *             Intended for Export Country
      *
      * 02/22/10  LeAnne Ramsey
      *           Recompile only. New field was added to SHP204-Datamart Sales History:
      *             1) price type
      *
      * 05/24/10  LeAnne Ramsey
      *           Recompile only.
      *           Added a field to SHP204-Datamart Sales History:
      *           1) SHWEDTMDCY-Week Ending Date in date format mm/dd/ccyy
      *
      * 11/29/11  LeAnne Ramsey  (E1818)
      *           Recompile only.
      *           Added 5 fields to SHP204-Datamart Sales History.
      *
      * 11/30/11  LeAnne Ramsey  (E1818)
      *           Recompile only.
      *           Changed our 3 new SalesRoute fields to say "Order"...like
      *           our SalesPerson fields.
      *
      * 12/12/12  LeAnne Ramsey  (E2360)
      *           Fix bug. While working with Adam Gross, Doug Engleman discovered that
      *           there was a bug with the CoOwned Flag in SHP204. I tracked it to here.
      *           I changed most of the data access logic; I believe our design was
      *           faulty from day one.
      *
      * 02/14/13  LeAnne Ramsey  (E2450)
      *           Recompile only.
      *           Adam Gross had Rose C. add 'sales channel' to the order entry screen. Now,
      *           the users will have to enter a valid Sales Channel (aka: Customer Type) on
      *           each Order!!  They were not happy with having it at the Customer level!!
      *           Rose will hold/store this new value in PMD0CPP-Sales History Extension.
      *           I have added 3 fields to our Datamart Sales History file:
      *             SHORCHCD-Order Sales Channel Code
      *             SHORCHDS-Order Sales Channel Description
      *             SHORCHCDDS-Order Sales Channel Code/Description
      *
      * 07/16/13  LeAnne Ramsey  (E2682)
      *           Recompile only.
      *           Adam Gross had us add Gate/Final Price and Gate/Final Price Adjustment to
      *           Sales History.  These will come from the Sales History Extension file (PMD0CPP).
      *
      * 07/08/14  LeAnne Ramsey  (E3174)
      *           Recompile only.
      *           Added a new field to SHP204-Sales History:
      *               Spot/Buy Flag    (SHSPBYFL)
      *
      * 08/14/14  LeAnne Ramsey  (E3320)
      *           Recompile only.
      *           Added 3 new fields to SHP204-Sales History:
      *               Last Quantity Change Date  (SHLQCDT)
      *               Last Quantity Change Date  (SH80LQCDT)
      *               Last Quantity Change Time  (SHLQCTM)
      *
      * 09/23/14  Rose Centonze  (E3329)
      *           Recompile only.
      *           Added 7 new fields to SHP204-Sales History:
      *               DP Location      (SHDPLOC)
      *               DP Region        (SHDPREGION)
      *               DP Channel Type  (SHDPCHTYP)
      *               Req Shp Date     (SHRQSHDT)
      *               Req Shp w/end    (SHRQWEDT)
      *               Req Dlv Date     (SHRQDLDT)
      *               Req Dlv w/end    (SHDLWEDT)
      *
      * 10/07/14  LeAnne Ramsey  (E3499)
      *           Recompile only.
      *           Added 1 new field to SHP204-Sales History:
      *               Stop             (SHSTOP)
      *
      * 11/24/14  LeAnne Ramsey  (E3640)
      *           Recompile only.
      *           Added 3 new fields to SHP204-Sales History:
      *               DP Salesperson Code        (SHDPSPCD)
      *               DP Salesperson Name        (SHDPSPNM)
      *               DP Salesperson Code/Name   (SHDPSPCDNM)
      *
      * 10/28/16  Danny Nguyen   (E7720)
      *           Recompile only.
      *           Added 2 new fields to SHP204-Sales History:
      *               Requested Delivery Time      (SHRQDLTM)
      *               Requested Delivery Timestamp (SHRQDLSTMP)
      *
      * 11/14/17  Danny Nguyen  (R12011A-Weekly Product Revenue)
      *           Recompile only. Added 25 STF fields to TFP010 file.
      *
      * 05/23/18  Danny Nguyen  (R12926 - Absorbed Freight Rate Override)
      *           Recompile only.
      *           Added 3 new fields to SHP204-Sales History:
      *               ABSORBED FREIGHT RATE        (SHABFRRT)
      *               ORIG ORD ABS FREIGHT RATE    (SHORABFRRT)
      *               INVOICED MODE OF TRANSPTN    (SHINMOTCD)
      *
      * 05/17/22  Danny Nguyen  (DO2484 - WI 479 STF Variance Reporting)
      *           Recompile only. Added 2 STF fields to TFP010 file.
      *           PRXYPC   - STF STD YIELD %
      *           PRXPMPPC - STF PUMP %
      *
      * W105621 RMC 10/10/2022 INCREASE FIELD SIZES TO HANDLE PRICES > 999.99
      *         CHANGE @@ANPRPR TO SIZE 10.6
      *           CHANGES PRANPRPR IN TFP010
      *
      *
      /eject
      ********************************************************************************************
      * FILE SPECIFICATIONS
      ********************************************************************************************
      *
     Ftfp007    if   e           k disk
      *    Exempt codes
      *
      *
     Ftfl010a   if   e           k disk
      *    Weekly product revenue detail
      *
      *
     Ftfp099    if   e           k disk
      *    Function control
      *
      *
     Fshl204d   uf   e           k disk
      *    Datamart: Sales history  (logical selects only Seaboard/Triumph; no Dailys)
      *
      /EJECT
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      *---------------------------------------------------------------
      *  Named Constants
      *---------------------------------------------------------------
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      *---------------------------------------------------------------
      *  Standalone fields
      *---------------------------------------------------------------
      *
      * Workfields
      *
     D wkrcexfl        s                   like(exrcexfl)
     D wkledt          s                   like(fnledt)
     D wkdt            s                   like(fnledt)
      *
      ****************************************************************
      * Data Structures
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *---------------------------------------------------------------
      * Definition for external data area DATFSALE
      *---------------------------------------------------------------
     D
     Ddatfsale        uds
     D  da099dt               30     37  0
     D  dadmdt                80     87  0
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ***************************************************************************************
      * Mainline
      ***************************************************************************************
      *
      * Retrieve the TFS Function Control record to get the date of the most
      * recent Margin Close.
      *
     C     'MARGIN    '  chain     tfp099                             92
     C                   if        *in92 = *off
     C                   z-add     fnledt        wkledt
     C                   endif
      *
     C                   select
     C                   when      dadmdt = da099dt and
     C                             dadmdt = wkledt
      *
     C                   when      dadmdt < wkledt
     C                   z-add     da099dt       wkdt
     C                   exsr      $process
      *
     C                   when      wkledt < dadmdt
     C                   z-add     wkledt        wkdt
     C                   exsr      $process
     C                   endsl
      *
      * Update the Data Area with the Last End Date from the TFP099 Margin Close
      * record.
     C                   z-add     wkledt        da099dt
     C                   z-add     wkledt        dadmdt
      *
     C                   seton                                        lr
      /EJECT
      *----------------------------------------------------------------------------------------
      * Process the Sales History records
      *----------------------------------------------------------------------------------------
      *
     C     $process      begsr
      *
     C     wkdt          setgt     shl204d
      *
     C                   dou       *in91 = *on                                  Do repop
     C                   read      shl204d                                91
     C                   if        *in91 = *off                                 If not EOF
      *
      * Get the TF Exempt flag to use for later keying to TFP010
      *
     C     shtfexcd      chain     tfp007
     C                   if        *in92 = *off
     C                   move      exrcexfl      wkrcexfl
     C                   else
     C                   move      'N'           wkrcexfl
     C                   endif
      *
      * Either repopulate or clear cost fields.
      *
     C                   select
     C                   when      sh80wedt <= wkledt
     C                   exsr      $repop
     C                   when      sh80wedt > wkledt
     C                   move      no            shfcfl
     C                   exsr      $clear
     C                   endsl
      *
     C                   update    shrec
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Do repop
      *
     C                   endsr
      /EJECT
      *----------------------------------------------------------------------------------------
      * Clear the Cost fields
      *----------------------------------------------------------------------------------------
      *
     C     $clear        begsr
      *
      * Retrieve the CoOwned Flag/Percent from the Weekly Product Revenue
      * Detail file for this: Week Ending Date
      *                       Item Code
      *                       Record Exempt Flag
      *
     C     key01         chain     tfl010a                            92
     C                   if        *in92 = *off                                 If hit
     C                   move      prcoownfl     shtownfl
     C                   z-add     prtownpc      shtownpc
     C                   else
     C                   move      no            shtownfl
     C                   z-add     0             shtownpc
     C                   endif
      *
     C                   z-add     0             shslmam
     C                   z-add     0             shsllam
     C                   z-add     0             shslkam
     C                   z-add     0             shsliam
     C                   z-add     0             shsloam
     C                   z-add     0             shslpam
     C                   z-add     0             shnpram
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------------------------------
      * Repop
      *---------------------------------------------------------------------------------------
      *
     C     $repop        begsr
      *
      * Set Final Cost Flag
     C                   move      yes           shfcfl
      *
      * Retrieve weekly values for an Item from the Weekly Product Revenue
      * Detail file for this: Week Ending Date
      *                       Item Code
      *                       Record Exempt Flag
      *
     C     key01         chain     tfl010a                            92
     C                   if        *in92 = *on                                  If no hit
     C                   exsr      $clear
     C                   else
      *
      * CoOwned Flag and %
     C                   move      prcoownfl     shtownfl
     C                   z-add     prtownpc      shtownpc
      *
      * Sold Cost Amounts:
      *
      *           Meat
     C                   eval(h)   shslmam = prmco * shblwt
      *          Labor
     C                   eval(h)   shsllam = prlco * shblwt
      *        Packaging
     C                   eval(h)   shslkam = prkco * shblwt
      *       Ingredient
     C                   eval(h)   shsliam = prico * shblwt
      *          Other
     C                   eval(h)   shsloam = proco * shblwt
      *       Product
     C                   eval      shslpam = shslmam + shsllam + shslkam +
     C                                       shsliam + shsloam
      *
      * Net Product Revenue Amount
      *
     C                   eval      shnpram = pranprpr * shblwt
     C                   endif                                                  If no hit
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Key lists
      *
     C     key01         klist
     C                   kfld                    sh80wedt
     C                   kfld                    shshitcd
     C                   kfld                    wkrcexfl
      *
     C                   endsr
      /EJECT
