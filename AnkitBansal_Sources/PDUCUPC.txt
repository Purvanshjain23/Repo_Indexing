/********************************************************************/
/*                                                                  */
/*  SYNOPSIS -  JOB CONTROL PROMPT FOR PRINTER OVERRIDE INFORMATION */
/*              PROMPTING FUNCTION. CAUSE PRINTER OVERRIDE          */
/*              PROMPTING                                           */
/*                                                                  */
/*  SYSTEM    - ORDER MANAGEMENT SYSTEM                             */
/*  PROGRAM   - PRINT LATE DELIVERY REPORT                          */
/*  DATE      - 12/18/96                                            */
/*                                                                  */
/*      PARAMETERS:                                                 */
/*                                                                  */
/*     O    &RETURN     - RETURN CODE                               */
/*     I    &APPLIC     - APPLICATION CODE                          */
/*     I    &PGM        - CL PROGRAM TO SUBMIT                      */
/*     I    &DESC       - PRINT PROGRAM DESCRIPTION (FOR PROMPT     */
/*                         SCREEN)                                  */
/*     B    &OUTQ       -  OUTPUT QUEUE                             */
/*                         IF THIS IS BLANK AT START, PROMPT        */
/*                         PGM IS CALLED; OTHERWISE, THE PRINT CL   */
/*                         IS SUBMITTED WITHOUT PROMPTING           */
/*     B    &HOLD       -  HOLD STATUS                              */
/*     B    &SAVE       -  SAVE STATUS                              */
/*     B    &NQSTAT     -  INDICATOR WHETHER MUST RUN IN NIGHT Q    */
/*     B    &NIGHTQ     -  NIGHT JOB QUEUE                          */
/*     B    &COMPNYN    - COMPANY NUMBER (PASSED)                   */
/*     B    &FRMDATN    - FROM DATE      (PASSED)                   */
/*     B    &THRDATN    - THRU DATE      (PASSED)                   */
/*     B    &LOADN      - LOAD NUMBER    (PASSED)                   */
/*     B    &CARRIER    - CARRIER CODE   (PASSED)                   */
/*     B    &LOADT      - LOAD TYPE      (PASSED)                   */
/*     B    &COPYN      - NO. OF COPIES  (PASSED)                   */
/*     B    &RPTSLT     - SELECTED REPORT TYPE                      */
/*     B    &UOM        - UNIT OF MEASURE                           */
/*     B    &EXPORTYN   - EXCLUDE EXPORTS Y OR N                    */
/*     B    &OTRGROUP   - ON TIME REASON GROUP CODE                 */
/********************************************************************/
/*  RMC 1/02/2001 ADD SALESPERSON AS INPUT PARM AND PASS TO CALLED  */
/*                PROGRAMS                                          */
/*  RMC 6/27/2003 ADDED REASON PARM                                 */
/*  LJB 6/14/2006 ADDED EXCLUDE EXPORT PARM, REMOVED CUSTOMER#      */
/*                IT IS NOT USED AND SYNON PARMS ARE MAX'D OUT      */
/*  LJB 6/21/2006 ADDITIONAL PARMS WERE ADDED. CHANGED INPUT PARMS  */
/*                TO MATCH SYNON PARM LIST                          */
/*  LJB 6/30/2006 REPORTS WERE CHANGED TO REPLICATE THE CARRIER     */
/*                PERFORMANCE REPORTS. CARRIER PERFORMANCE AND      */
/*                LATE DELIVERY REPORTS ALL CALL THIS CL PROGRAM.   */
/*  RMC 7/19/2006 ADDED SHIP TO CUST SELECTION                      */
/*  LJB 8/25/2006 ADDED ON TIME REASON AND ON TIME DEPARTMENT       */
/*                CODES TO PARMS AND PASS TO CALLED PROGRAMS        */
/*  LJB 04/16/2015 E004076  PASS IN OPTIONAL EMAIL ADDRESS          */
/*  RMC 02/13/2020 W57687  PASS IN OPTIONAL EMAIL XLS PARM         */
/********************************************************************/
 PDUCUPC:    PGM        PARM(&RETURN &PGM &APPLIC &CARRIER &DESC +
                          &OUTQ &HOLD &SAVE &NQSTAT &NIGHTQ +
                          &COMPNYN &YEARN &FRMDATN &THRDATN &LOADN +
                          &COPYN &SALESP &CUST &EXPORTYN &UOM +
                          &LOADT &QTR &EMXLS &RPTSLT &OTREAS &OTRDEPT +
                          &OTRGROUP &EMAIL)
/* PARMS                                                             */
        DCL    &RETURN     *CHAR   (7)     /* RETURN CODE            */
        DCL    &APPLIC     *CHAR   (06)    /* APPLICATION CODE       */
        DCL    &PGM        *CHAR   (10)    /* CL TO SUBMIT           */
        DCL    &DESC       *CHAR   (25)    /* PRT PGM DESCRIPTION    */
        DCL    &COPYN      *DEC            /* NO. OF COPIES          */
        DCL    &OUTQ       *CHAR   (10)    /* OUTPUT QUEUE           */
        DCL    &HOLD       *CHAR   (4)     /* HOLD STATUS            */
        DCL    &SAVE       *CHAR   (4)     /* SAVE STATUS            */
        DCL    &NQSTAT     *CHAR   (1)     /* IF MUST RUN IN NIGHT Q */
        DCL    &NIGHTQ     *CHAR   (10)    /* NIGHT JOB QUEUE        */
        DCL    &COMPNYN    *DEC            /* COMPANY NUMBER (PASSED)*/
        DCL    &QTR        *CHAR   (1)     /* QUARTER                */
        DCL    &YEARN      *DEC            /* YEAR           (PASSED)*/
        DCL    &FRMDATN    *DEC            /* FROM DATE      (PASSED)*/
        DCL    &THRDATN    *DEC            /* THRU DATE      (PASSED)*/
        DCL    &LOADN      *DEC            /* LOAD NUMBER    (PASSED)*/
        DCL    &CUST       *DEC            /* SHIP TO CUST   (PASSED)*/
        DCL    &SALESP     *CHAR   (3)     /* SALESPERSON CODE       */
        DCL    &CARRIER    *CHAR   (3)     /* CARRIER CODE           */
        DCL    &LOADT      *CHAR   (1)     /* LOAD TYPE              */
        DCL    &OTRGROUP   *CHAR   (1)     /* ON TIME REASON GROUP   */
        DCL    &COPYN      *DEC            /* NO. OF COPIES          */
        DCL    &DESC       *CHAR   (25)    /* PRT PGM DESCRIPTION    */
        DCL    &OUTQ       *CHAR   (10)    /* OUTPUT QUEUE           */
        DCL    &HOLD       *CHAR   (4)     /* HOLD STATUS            */
        DCL    &SAVE       *CHAR   (4)     /* SAVE STATUS            */
        DCL    &NQSTAT     *CHAR   (1)     /* IF MUST RUN IN NIGHT Q */
        DCL    &NIGHTQ     *CHAR   (10)    /* NIGHT JOB QUEUE        */
        DCL    &RPTSLT     *CHAR   (1)     /* REPORT SELECT          */
        DCL    &UOM        *CHAR   (2)     /* UNIT OF MEASURE        */
        DCL    &EXPORTYN   *CHAR   (1)     /* EXCLUDE EXPORTS Y/N    */
        DCL    &OTREAS     *CHAR   (3)     /* ON TIME REASON         */
        DCL    &OTRDEPT    *CHAR   (2)     /* ON TIME REASON DEPT    */
        DCL    &EMXLS      *CHAR   (1 )    /* EMAIL XLS              */
        DCL    &EMAIL      *CHAR   (50)    /* EMAIL ADDRESS          */
        DCL    &EMAIL1     *CHAR   (25)    /* EMAIL ADDRESS          */
        DCL    &EMAIL2     *CHAR   (25)    /* EMAIL ADDRESS          */

/* INTERNAL FIELDS                                                   */
        DCL    &COPY       *DEC    (2 0)   /* NUMBER OF COPIES       */

/* ERROR MESSAGE FIELDS                                              */
        DCL    &MSG        *CHAR   (512)   /* MESSAGE                */
        DCL    &MSGID      *CHAR   (7)     /* MESSAGE ID             */
        DCL    &MSGL       *DEC    (5 0)

/********************************************************************/
/*   MONITOR FOR ALL MESSAGES AT PROGRAM LEVEL                      */
/********************************************************************/
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(MSGTRAP))

/********************************************************************/
/*   INITIALIZE VARIABLES                                           */
/********************************************************************/
             CHGVAR     VAR(&RETURN) VALUE('       ')
             CHGVAR     VAR(&COPY) VALUE(&COPYN)
             CHGVAR     VAR(&EMAIL1) VALUE(%SST(&EMAIL 1 25))
             CHGVAR     VAR(&EMAIL2) VALUE(%SST(&EMAIL 26 25))

/********************************************************************/
/*   CALL PROMPT PROGRAM                                            */
/********************************************************************/
             CALL       PGM(PDG0PVR) PARM(&RETURN &DESC &OUTQ &HOLD +
                          &SAVE &COPY &NQSTAT &NIGHTQ &APPLIC)
             CHGVAR     VAR(&COPYN) VALUE(&COPY)
             IF         COND(&RETURN *NE ' ') THEN(GOTO +
                          CMDLBL(USERQUIT))

    /* FOR TESTING ONLY! COMMENT OUT LINE BELOW WHEN FINISH TESTING */
         /*  GOTO       CMDLBL(TEST) */

/********************************************************************/
/*   SUBMIT PRINT CL PROGRAM                                        */
/*    &PGM WILL CONTAIN PPY4UPC OR PPYQUPC                         */
/********************************************************************/
 SUBMIT:     SBMJOB     CMD(CALL PGM(&PGM) PARM(&COMPNYN &FRMDATN +
                          &THRDATN &LOADN &QTR &YEARN &SALESP &CUST +
                          &CARRIER &LOADT &OUTQ &HOLD &SAVE &COPYN +
                          &NIGHTQ &RPTSLT &UOM &OTRGROUP &EXPORTYN +
                          &OTREAS &OTRDEPT &EMXLS &EMAIL1 &EMAIL2)) +
                          JOB(LATEDELRPT)
             GOTO       CMDLBL(END)

 TEST:       CALL       PGM(&PGM) PARM(&COMPNYN &FRMDATN &THRDATN +
                          &LOADN &QTR &YEARN &SALESP &CUST &CARRIER +
                          &LOADT &OUTQ &HOLD &SAVE &COPYN &NIGHTQ +
                          &RPTSLT &UOM &OTRGROUP &EXPORTYN &OTREAS +
                          &OTRDEPT &EMXLS &EMAIL1 &EMAIL2)
             GOTO       CMDLBL(END)

/*-------------------------USER QUIT--------------------------------*/
 USERQUIT:   CHGVAR     VAR(&RETURN) VALUE('Y2U0005')
             GOTO       CMDLBL(END)

/*----------------------RECEIVE MESSAGES----------------------------*/

 MSGTRAP:    RCVMSG     MSGTYPE(*EXCP) MSGDTA(&MSG) MSGDTALEN(&MSGL) +
                          MSGID(&MSGID)
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(END))

             SNDPGMMSG  MSGID(&MSGID) MSGF(QCPFMSG) MSGDTA(%SST(&MSG +
                          1 &MSGL)) MSGTYPE(*ESCAPE)
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(END))

 END:        ENDPGM
