     A*%%TS  SD  20030904  140850  ISLRAMS     REL-V5R2M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD424
     A*  TITLE:         INTERFACE: CLEAR FILES TO ALLOW UPLOAD REDO
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  DATE:          07/12/01
     A*
     A*****************************************************************
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A                                      CF08(08 'SUBMIT')
     A                                      CF20(20 'WRKSPLF')
     A*
     A*----------------------------------------------------------------
     A* RECORD FORMAT ONE
     A*----------------------------------------------------------------
     A          R HP424R1
     A*%%TS  SD  20030904  140850  ISLRAMS     REL-V5R2M0  5722-WDS
     A                                      OVERLAY
     A                                      RTNCSRLOC(&RCD &FLD)
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 27'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  2 27'FEED TICKET INTERFACE'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  3 21'CLEAR FILES TO ALLOW UPLOAD REDO'
     A                                      DSPATR(HI)
     A                                  5  3'This function will clear the HPS i-
     A                                      nterface files allowing you to star-
     A                                      t'
     A                                  6  3'over or redo a upload.'
     A                                  8  3'Please note that this function has-
     A                                       no effect on data that has already'
     A                                  9  3'been interfaced completely into HP-
     A                                      S.  So, if you have already taken t-
     A                                      he'
     A                                 10  3'option Interface Tickets to HPS, t-
     A                                      hen it is too late to start over wi-
     A                                      th'
     A                                 11  3'this upload.'
     A                                 14  3'Take F8-Submit to submit the job t-
     A                                      o clear the files.'
     A                                      DSPATR(HI)
     A*
     A*--------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*--------------------------------------------------------------
     A          R HP424K1
     A*%%TS  SD  20010712  133659  ISLRAMS     REL-V4R4M0  5769-PW1
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A                                 23 11'F8=Submit'
     A                                 23 22'F20=Spool Files'
      *
      *----------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *----------------------------------------------------------------
     A          R HP424ES                   SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
     A*
     A*----------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*----------------------------------------------------------------
     A*
     A          R HP424EC                   SFLCTL(HP424ES)
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
