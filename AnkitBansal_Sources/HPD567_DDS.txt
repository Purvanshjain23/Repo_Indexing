     A*%%TS  SD  20021217  161135  ISLRAMS     REL-V5R1M0  5722-WDS
     A*****************************************************************
     A*
     A*  SYSTEM:        HOG PRODUCTION
     A*  DISPLAY FILE:  HPD567 - SELECT SALES MOVEMENT
     A*                 (OMITS SCHEULDED SALES)
     A*  CREATED BY:    LEANNE FEDOR
     A*  DATE:          11/14/02
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA12(12 'PREVIOUS')
     A N82                                  ROLLUP(25 'PAGE FORWARD')
     A*---------------------------------------------------------------
     A* REFERENCE WINDOW RECORD FORMAT 1
     A*---------------------------------------------------------------
     A          R HP567R1
     A*%%TS  SD  19950619  151645  ISLRAMS     REL-V3R0M5  5763-PW1
     A                                      TEXT('Reference window')
     A                                      WINDOW(*DFT 17 74)
     A                                      WDWBORDER((*DSPATR RI) (*CHAR '    -
     A                                          '))
     A*---------------------------------------------------------------
     A* RECORD FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP567R2
     A                                      TEXT('Written in RPG pgm to prevent-
     A                                       screen from clearing')
     A                                      CLRL(*NO)
     A                                      OVERLAY
     A*---------------------------------------------------------------
     A* RECORD FORMAT TWO
     A*---------------------------------------------------------------
     A* THIS FORMAT IS NOT WRITTEN IN THE RPG PGM.  IT PREVENTS THE
     A* CALLING_PROGRAM_SCREEN FROM BEING CLEARED.
     A*---------------------------------------------------------------
     A          R HP567R3
     A                                      TEXT('Dummy format not written in R-
     A                                      PG pgm.')
     A                                      ASSUME
     A                                 11  1' '
     A*----------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*----------------------------------------------------------------
     A          R HP567S1                   SFL
     A*%%TS  SD  20001017  141757  ISLRAMS     REL-V4R4M0  5769-PW1
     A                                      TEXT('subfile record format one')
     A            HDRRN1         4S 0H
     A            DFRMDY    R        O  7  1REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  ')
     A            DFMVSN    R        O  7 11REFFLD(SHMVSN)
     A                                      EDTCDE(M)
     A            DFCVNO    R        O  7 21REFFLD(SHCVNO)
     A                                      EDTCDE(M)
     A            DFCNAM    R        O  7 31REFFLD(@@DS30)
     A            DFMSCD    R        O  7 62REFFLD(SHMSCD)
     A            DFSTCD    R        O  7 65REFFLD(SHSTCD)
     A            DFLTCD    R        O  7 72REFFLD(HSRREF/@@LTCD *LIBL/HSPREF)
     A*--------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*--------------------------------------------------------------
     A          R HP567C1                   SFLCTL(HP567S1)
     A*%%TS  SD  20021217  161135  ISLRAMS     REL-V5R1M0  5722-WDS
     A                                      SFLSIZ(0050)
     A                                      SFLPAG(0008)
     A                                      WINDOW(HP567R1)
     A                                      TEXT('control format one')
     A                                      OVERLAY
     A                                      SFLCSRRRN(&HDRRN)
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND(*MORE)
     A                                      RMVWDW
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            HDRRN          5S 0H
     A                                  1  1'SELECT SALES MOVEMENT  (omits sche-
     A                                      duled sales)'
     A                                      DSPATR(HI)
     A                                  2  1'Position Cursor, Press Enter'
     A                                  4  1'Received'
     A                                      DSPATR(HI)
     A                                  4 21'Customer'
     A                                      DSPATR(HI)
     A                                  4 65'Sales'
     A                                      DSPATR(HI)
     A                                  4 71'Load'
     A                                      DSPATR(HI)
     A                                  5  3'Date'
     A                                      DSPATR(HI)
     A                                  5 11'Movement'
     A                                      DSPATR(HI)
     A                                  5 21'Number'
     A                                      DSPATR(HI)
     A                                  5 31'Customer Name'
     A                                      DSPATR(HI)
     A                                  5 62'St'
     A                                      DSPATR(HI)
     A                                  5 66'Type'
     A                                      DSPATR(HI)
     A                                  5 71'Type'
     A                                      DSPATR(HI)
     A            C1RMDY    R        B  6  1REFFLD(@@DT06)
     A                                      DSPATR(HI)
     A                                      EDTWRD('  /  /  -')
     A            C1CVNO    R        B  6 21REFFLD(SHCVNO)
     A                                      DSPATR(HI)
     A                                      EDTCDE(M)
     A            C1MSCD    R        B  6 62REFFLD(MSMSCD PRKPLIB/HSPREF)
     A                                      DSPATR(HI)
     A            C1STCD    R        B  6 65REFFLD(FSPTCD)
     A                                      DSPATR(HI)
     A            C1LTCD    R        B  6 72REFFLD(HSRREF/@@LTCD *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A*----------------------------------------------------------------
     A          R HP567K1
     A*----------------------------------------------------------------
     A                                      OVERLAY
     A                                      WINDOW(HP567R1)
     A                                 16  1'F12=Previous'
