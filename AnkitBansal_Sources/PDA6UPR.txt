/********************************************************************/
/*                                                                  */
/*  SYNOPSIS -  JOB CONTROL PROMPT FOR PRINTER OVERRIDE INFORMATION */
/*              GENERIC PROMPTING FUNCTION USED BY ANY PROGRAM      */
/*              PASSING COMPANY, DATE, SEQ. NO, LTL FLAG, LOAD STS, */
/*              TRUCK SIZE                                          */
/*                                                                  */
/*  SYSTEM    - ORDER MANAGEMENT SYSTEM                             */
/*  PROGRAM   - PRT PASS FOR LOAD PLAN  UP     (CL)      PDA6UPR    */
/*  DATE      - 10/19/95                                            */
/*                                                                  */
/*      PARAMETERS:                                                 */
/*                                                                  */
/*     O    &RETURN     - RETURN CODE                               */
/*     I    &APPLIC     - APPLICATION CODE                          */
/*     I    &PGM        - CL PROGRAM TO SUBMIT                      */
/*     B    &COMPNYN    - COMPANY NUMBER  (PASSED)                  */
/*     B    &DATEN      - SCHEDULED SHIP DATE (PASSED)              */
/*     B    &COPYN      - NO. OF COPIES (PASSED)                    */
/*     B    &SEQN       - SCHEDULED SEQUENCE  (PASSED)              */
/*     B    &STATUS     - LOAD STATUS                               */
/*     B    &CARRIER    - CARRIER CODE                              */
/*     B    &TYPE       - LOAD TYPE (REGULAR, BULK)                 */
/*     B    &VEHINIT    - CAR/VEHICLE INITIALS*** REMOVED 2/7/96*** */
/*     B    &STATE      - STATE CODE          *** ADDED   2/7/96*** */
/*     B    &INCEXC     - INCLUDE/EXCLUDE OPTION                    */
/*     I    &DESC       - PRINT PROGRAM DESCRIPTION (FOR PROMPT     */
/*                         SCREEN)                                  */
/*     B    &OUTQ       - OUTPUT QUEUE                              */
/*                         IF THIS IS BLANK AT START, PROMPT        */
/*                         PGM IS CALLED; OTHERWISE, THE PRINT CL   */
/*                         IS SUBMITTED WITHOUT PROMPTING           */
/*     B    &HOLD       - HOLD STATUS                               */
/*     B    &SAVE       - SAVE STATUS                               */
/*     B    &NQSTAT     - INDICATOR WHETHER MUST RUN IN NIGHT Q     */
/*     B    &NIGHTQ     - NIGHT JOB QUEUE                           */
/*                                                                  */
/********************************************************************/

 PDA6UPR:    PGM        PARM(&RETURN &APPLIC &PGM &COMPNYN &DATEN +
                          &SEQN &STATUS &CARRIER &TYPE &STATE &INCEXC +
                          &DESC &OUTQ &HOLD &SAVE &NQSTAT &NIGHTQ +
                          &COPYN)
/* PARMS                                                                      */
        DCL    &RETURN     *CHAR   (7)           /* Return Code               */
        DCL    &APPLIC     *CHAR   (06)          /* Application Code          */
        DCL    &PGM        *CHAR   (10)          /* CL to Submit              */
        DCL    &COMPNYN    *DEC                  /* Company Number  (Passed)  */
        DCL    &DATEN      *DEC                  /* Sched Ship Date (Passed)  */
        DCL    &SEQN       *DEC                  /* Sched Sequence  (Passed)  */
        DCL    &STATUS     *CHAR   (01)          /* Load Status               */
        DCL    &CARRIER    *CHAR   (03)          /* Carrier for load          */
        DCL    &TYPE       *CHAR   (01)          /* Load Type                 */
/*      DCL    &VEHINIT    *CHAR   (12)          /* Car/Vehicle Inits         */
        DCL    &STATE      *CHAR   (02)          /* STATE CODE                */
        DCL    &INCEXC     *CHAR   (03)          /* Include/Exclude Sts       */
        DCL    &DESC       *CHAR   (25)          /* PRT Pgm Description       */
        DCL    &OUTQ       *CHAR   (10)          /* Output Queue              */
        DCL    &HOLD       *CHAR   (4)           /* Hold Status               */
        DCL    &SAVE       *CHAR   (4)           /* Save Status               */
        DCL    &NQSTAT     *CHAR   (01)          /* If MUST run in Night Q    */
        DCL    &NIGHTQ     *CHAR   (10)          /* Night Job Queue           */
        DCL    &COPYN      *DEC                  /* No. of Copies             */

/* INTERNAL FIELDS                                                            */
        DCL    &COPY       *DEC    (2 0)         /* Number of copies          */

/* ERROR MESSAGE FIELDS                                                       */
        DCL    &MSG        *CHAR   (512)         /* message                   */
        DCL    &MSGID      *CHAR   (7)           /* message id                */
        DCL    &MSGL       *DEC    (5 0)

/********************************************************************/
/*   INITIALIZE VARIABLES                                           */
/********************************************************************/

             CHGVAR     VAR(&RETURN) VALUE('       ')
             CHGVAR     VAR(&COPY)   VALUE(&COPYN)

/********************************************************************/
/*   MONITOR FOR ALL MESSAGES AT PROGRAM LEVEL                      */
/********************************************************************/

             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(MSGTRAP))

/********************************************************************/
/*   CALL PROMPT PROGRAM                                            */
/********************************************************************/

             CALL       PGM(PDG0PVR) PARM(&RETURN &DESC &OUTQ &HOLD +
                          &SAVE &COPY &NQSTAT &NIGHTQ &APPLIC)
             CHGVAR     VAR(&COPYN) VALUE(&COPY)
             IF         COND(&RETURN *NE ' ') THEN(GOTO +
                          CMDLBL(USERQUIT))

/********************************************************************/
/*   SUBMIT PRINT CL PROGRAM                                        */
/********************************************************************/

 SUBMIT:     SBMJOB     CMD(CALL PGM(&PGM) PARM(&COMPNYN &DATEN +
                          &SEQN &STATUS &CARRIER &TYPE &STATE &INCEXC +
                          &APPLIC &OUTQ &HOLD &SAVE &COPYN +
                          &NIGHTQ)) JOB(&PGM)
             GOTO       CMDLBL(END)

/*-------------------------USER QUIT--------------------------------*/
 USERQUIT:   CHGVAR     VAR(&RETURN) VALUE('Y2U0005')
             GOTO       CMDLBL(END)

/*----------------------RECEIVE MESSAGES----------------------------*/

 MSGTRAP:    RCVMSG     MSGTYPE(*EXCP) MSGDTA(&MSG) MSGDTALEN(&MSGL) +
                          MSGID(&MSGID)
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(END))

             SNDPGMMSG  MSGID(&MSGID) MSGF(QCPFMSG) MSGDTA(%SST(&MSG +
                          1 &MSGL)) MSGTYPE(*ESCAPE)
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(END))

 END:        ENDPGM

