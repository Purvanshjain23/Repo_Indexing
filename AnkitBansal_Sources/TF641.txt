      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Hog Production
      * PROGRAM:     TF641 - Weekly Base Marketing Report
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     03/28/05
      *
      * FUNCTION:    Prints the Base Marketing Fee Transaction file data.
      *              One line is printed per day with a total line for the week.
      *
      *            This 'report' is generated:
      *                 1) on-demand
      *                 2) from the Weekly Base Marketing Close process
      *
      **********************************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 12/12/08  LeAnne Ramsey
      *           For our new TFS Yield Reports, we added new "head" fields
      *           to the Reference File.  These new "head" fields will hold "weekly"
      *           values. At this time, weekly values are exceeding 100,000 head. So, we made
      *           the new head fields 7,0. To be consistent, I changed the "daily" head
      *           field from 5,0 to 7,0. So, file TFP016 was recompiled to pick up the
      *           7,0 field size.
      *           I changed this layout to accommodate the new 7,0 field size.
      /eject
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Ftfl016a   if   e           k disk
      *    Base marketing fee transactions
      *
      *
     Fprint132  o    f  132        printer oflind(*inof)
      *
      /eject
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      ****************************************************************
      * ARRAYS AND TABLES
      ****************************************************************
      *
     D arcam           s             11  2 dim(2)
     D ardam           s             11  2 dim(2)
     D arhd            s              9  0 dim(2)
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Standard fields
      *
     D dash            s            132    inz(*all'-')
     d rtime           s              6  0
      *
      *
      * Control fields
      *
     D first           s              1    inz('Y')
     D svwedt          s                   like(btwedt)
      *
      *
      * Workfields for date manipulation
      *
     D wkisodate       s               D   datfmt(*iso)
      *
      *
      * Print fields
      *
     d h1demand        s             32
      *
     d l1wbmdy         s              6  0
     d l1wemdy         s              6  0
     d l1klmdy         s              6  0
      *
      *
      * Array index
      *
     D x               s              3  0
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *---------------------------------------------------------------
      * Local data area.
      *---------------------------------------------------------------
      *
     Dlda             uds                  dtaara(*lda)
      *
      * Do not use the first 38 LDA positions. We have to generate this from the
      * Close function as well as on-demand from this program. So, the LDAs have to
      * match up (The Close function is already using the first 38 positions.)
      * EXCEPT, put blanks in position 37 (prelimary/final code) to match TF440.
      *
     D  ldpfcd                37     37
      *
     D  ldfmdy                39     44  0
     D  ldtmdy                45     50  0
     D  ldfcymd               52     59  0
     D  ldtcymd               60     67  0
      *
     D  lddemand             300    300
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /eject
      ****************************************************************************************
      * Mainline
      ****************************************************************************************
      *
      * Read all Base Marketing Fee Transaction records for the selected date range.
      * Print each record and accumulate values for the total for the week.
      *
     C                   exsr      $h1hdr
      *
     C     ldfcymd       setll     tfl016a                                90
      *
     C                   dou       *in90 = *on or btwedt > ldtcymd              Main do
     C                   read      tfl016a                                90
     C                   if        *in90 = *off and                             If not EOF
     C                             btwedt <= ldtcymd
      *
      * Control break logic
      *
     C                   select
     C                   when      first = yes
     C                   z-add     btwedt        svwedt
     C                   move      no            first
      *
     C                   when      btwedt <> svwedt
     C                   exsr      $t1tot
     C                   endsl
      *
      * Detail processing:
      *   1) accumulate totals into arrays
      *
     C                   add       btklhd        arhd
     C                   add       btbmcam       arcam
     C                   add       btbmdam       ardam
      *
      * Print detail line
     C                   exsr      $l1dtl
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Main do
      *
      *----------------------------------------------------------------
      * EOF PROCESSING
      *----------------------------------------------------------------
      *
     C                   select
     C                   when      first = yes
     C                   except    nodata
     C                   other
      *
      * Print total lines:
      *  1) For last week
      *  2) For report
      *
     C                   exsr      $t1tot
     C                   exsr      $rpttot
     C                   endsl
      *
     C                   seton                                        lr
      /eject
      *---------------------------------------------------------------
      * Print detail line
      *---------------------------------------------------------------
      *
     C     $l1dtl        begsr
      *
      * Flip Week Beginning Date from CCYYMMDD to MMDDYY format
      *
     C                   if        btwbdt <> 0
     C     *iso          test(d)                 btwbdt                 92
     C                   if        *in92 = *off                                 If OK date
     C                   move      btwbdt        wkisodate
     C     *mdy          move      wkisodate     l1wbmdy
     C                   endif                                                  If OK date
     C                   endif
      *
      * Flip Week Ending Date from CCYYMMDD to MMDDYY format
      *
     C                   if        btwedt <> 0
     C     *iso          test(d)                 btwedt                 92
     C                   if        *in92 = *off                                 If OK date
     C                   move      btwedt        wkisodate
     C     *mdy          move      wkisodate     l1wemdy
     C                   endif                                                  If OK date
     C                   endif
      *
      * Flip Kill Date from CCYYMMDD to MMDDYY format
      *
     C                   if        btkldt <> 0
     C     *iso          test(d)                 btkldt                 92
     C                   if        *in92 = *off                                 If OK date
     C                   move      btkldt        wkisodate
     C     *mdy          move      wkisodate     l1klmdy
     C                   endif                                                  If OK date
     C                   endif
      *
      *
      * Print headings if necessary
      *
     C                   if        *inof = *on
     C                   exsr      $h1hdr
     C                   endif
      *
     C                   except    l1dtl
      *
      * Set off the indicator that controls printing of the Week info on
      * the detail line.  We will only print it on the first record for each
      * week.
     C                   setoff                                       87
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Print Weekly Total line
      *---------------------------------------------------------------
      *
     C     $t1tot        begsr
      *
     C                   if        *inof = *on
     C                   exsr      $h1hdr
     C                   endif
      *
     C                   z-add     1             x
     C                   except    t1tot
      *
     C                   z-add     btwedt        svwedt
      *
      * Set on the indicator that controls printing of the Week info on the
      * the detail line.  We want to print it on the first record for each
      * week.
     C                   seton                                        87
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Print Report Total line
      *---------------------------------------------------------------
      *
     C     $rpttot       begsr
      *
     C                   if        *inof = *on
     C                   exsr      $h1hdr
     C                   endif
      *
     C                   seton                                        88
     C                   z-add     2             x
     C                   except    t1tot
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Print report headings
      *---------------------------------------------------------------
      *
     C     $h1hdr        begsr
      *
     C                   except    h1hdr
      *
      * Set on the indicator that controls printing of the Week info on the first
      * detail line for the week.
      *
     C                   seton                                        87
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Set up report headings
      *
     C                   time                    rtime
      *
      *
      * This report is generated on-demand and also from the submitted
      * Weekly Base Marketing Close function.
      *
     C                   select
     C                   when      lddemand = yes
     C                   eval      h1demand = '           On-Demand            '
      *
     C                   when      ldpfcd = 'F'
     C                   eval      h1demand = '   Final Base Marketing Close   '
      *
     C                   when      ldpfcd = 'P'
     C                   eval      h1demand = 'Preliminary Base Marketing Close'
     C                   endsl
      *
     C                   endsr
      /eject
      *-------------------------------------------------------------
      * Report heading lines
      *-------------------------------------------------------------
      *
     Oprint132  e            h1hdr          1 01
     O                       sdpgm               10
     O                                           66 'WEEKLY BASE MARKETING'
     O                                           73 ' REPORT'
     O                                          122 'DATE'
     O                       udate         y    132
      *
     O          e            h1hdr          1
     O                       sdusr               10
     O                                          122 'TIME'
     O                       h1demand            75
     O                       rtime              132 '  :  :  '
      *
     O          e            h1hdr          1
     O                                          122 'PAGE'
     O                       page          Z    132
      *
     O          e            h1hdr       1  1
     O                                           22 'From week ending date:'
     O                       ldfmdy        y     32
      *
     O          e            h1hdr          2
     O                                           22 'To week ending date .:'
     O                       ldtmdy        y     32
      *
      *
      *-------------------------------------------------------------
      * Column headings
      *-------------------------------------------------------------
      *
     O          e            h1hdr          1
     O                                            8 'Week'
     O                                           21 'Week'
      *
     O          e            h1hdr          1
     O                                           10 'Beginning'
     O                                           21 'Ending'
     O                                           31 'Kill'
     O                                           43 'Killed'
     O                                           54 'Current'
     O                                           70 'Current'
     O                                           81 'Deferred'
     O                                           97 'Deferred'
      *
     O          e            h1hdr          1
     O                                            8 'Date'
     O                                           21 'Date'
     O                                           31 'Date'
     O                                           43 'Head'
     O                                           54 'Fee'
     O                                           70 'Amount'
     O                                           81 'Fee'
     O                                           97 'Amount'
     O                                          104 'Year'
     O                                          110 'Week'
      *
     O          e            h1hdr          1
     O                       dash               132
      *
      *-------------------------------------------------------------
      * Detail line
      *-------------------------------------------------------------
      *
     O          e            l1dtl          1
     O               87      l1wbmdy             10 '  /  /  '
     O               87      l1wemdy             22 '  /  /  '
     O                       l1klmdy             32 '  /  /  '
     O                       btklhd        k     44
     O                       btbmcfee      k     55
     O                       btbmcam       k     71
     O                       btbmdfee      k     82
     O                       btbmdam       k     98
     O               87      btyr          z    104
     O               87      btwk          z    109
      *
      *
      *-------------------------------------------------------------
      * Total line
      *-------------------------------------------------------------
      *
     O          e            t1tot          1
     O              n88                          43 '----------'
     O              n88                          70 '--------------'
     O              n88                          97 '--------------'
      *
     O          e            t1tot          2
     O               88                          31 'Report Total:'
     O                       arhd(x)       kb    44
     O                       arcam(x)      kb    71
     O                       ardam(x)      kb    98
      *
      *
      *-------------------------------------------------------------
      * No data message line
      *-------------------------------------------------------------
      *
     O          e            nodata         1
     O                                           19 'No data meets your'
     O                                           39 'selection criteria.'
