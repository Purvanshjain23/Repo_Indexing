      *****************  RPG PROGRAM HEADING  ************************
      *
      * ENVIRONMENT: Pork Division
      * SYSTEM:      TMS---Datamart
      * PROGRAM:     SH250LOAD - Datamart Transportation Management
      * PROGRAMMER:  Brad Baden
      * CREATED:      8/22/2019
      *
      * FUNCTION: This program will read all of the SHP250 records and
      *           retrieve the associated Load Header file OMFJCPMA by
      *           Load ID.  The Unused Date 3 and Unused Time 3 values
      *           from the Load Header file will be updated in the new
      *           Load Plan Start Date and Load Plan Start Time values
      *           in the SHP250 Datamart: Load Information file.
      *
      *           This is a 1 time run program to load the date and time
      *           values into the SHP250 file.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * Date       Programmer           TFS#
      *
      * xx/xx/xxxx xxxxxxxxxxxxxxxxxxxx Exxxxx
      *            xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
      *            xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
      *
      /eject
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fshp250    uf   e           k disk
      *  Datamart TMS: Load Information
      *
      *
     Fomfjcpl1  if   e           k disk
      *  OMS Load header
      *  (logical contains only Invoiced records)
      *
      /eject
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      *---------------------------------------------------------------
      *  Named Constants
      *---------------------------------------------------------------
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
     D upper           c                   'ABCDEFGHIJKLMNOPQRSTUVWXYZ'
     D lower           c                   'abcdefghijklmnopqrstuvwxyz'
      *
      ****************************************************************
      * ARRAYS AND TABLES
      ****************************************************************
      *
      *
      ****************************************************************
      *  Standalone fields
      ****************************************************************
      *
      * Workfields
      *
      *
      ****************************************************************
      * Data Structures
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /eject
      **********************************************************************************************
      * Mainline
      **********************************************************************************************
      *
      * Read all SHP250 records.  For each record:
      *   1) Retrieve the associated OMFJCPMA (Load Header) record
      *   2) If OMFJCPMA record found
      *      a) Convert the Unused Date 3 to the Load Plan Start Date fields
      *      b) Convert the Unused Time 3 to the Load Plan Start Time field
      *      c) Update SHP250 record with the date and time fields
      *   3) Read the next SHP250 record until end-of-file
      * Note: The logical file selects only Invoiced records.
      *
      *
     C     *loval        setll     shp250
     C                   if        %found                                       if found(shp250)
     C                   dou       %eof(shp250)                                 dou %eof
     C                   read      shp250
     C                   if        not %eof(shp250)                             if not%eof(shp250)
      *
     C     tmloadno      chain     omfjcpl1
     C                   if        %found(omfjcpl1)                             if %found(omfjcpma)
      *
      ** Only process records for Load Status = "I" (invoice)
     C                   if        fjpkst = 'I'                                 if fjpkst = 'I'
      *
      * Convert Unused Date 3 field to Load Plan Start Dates
     C     *cymd         test(de)                fjp0dt
     C                   if        not %error                                   if not %error
     C     *cymd         move      fjp0dt        tmpsdt
     C                   move      tmpsdt        tmpsdtmdcy
     C     *iso          move      tmpsdt        tm80psdt
     C                   else
     C                   move      *loval        tmpsdt
     C                   move      *loval        tmpsdtmdcy
     C                   endif                                                  fi not %error
      *
      * Convert Unused Time 3 field to Load Plan Start Time
     C     *jis          test(te)                fjcmtm
     C                   if        not %error                                   if not %error
     C     *jis          move      fjcmtm        tmpstm
     C                   else
     C                   move      *loval        tmpstm
     C                   endif                                                  fi not %error
      *
      * Update SHP250 record
     C                   update    tmrec
      *
     C                   endif                                                  fi fjpkst = 'I'
     C                   endif                                                  fi %found(omfjcpma)
     C                   endif                                                  fi not%eof(shp250)
     C                   enddo                                                  uod %eof
     C                   endif                                                  fi found(shp250)
      *
      * End program
     C                   eval      *inlr = *on
