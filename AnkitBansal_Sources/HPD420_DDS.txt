     A*%%TS  SD  20090513  102423  ISLRAMS     REL-V5R4M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD420
     A*  TITLE:         WORK WITH FEED TICKETS
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  DATE:          04/04/94
     A*
     A*****************************************************************
     A* SDN736 RECOMPILED FOR TICKET NBR INCREASE (TKNO & RTNO 7.0 TO 9.0)
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A N99                                  CF04(04 'PROMPT')
     A N99                                  CF05(05 'REFRESH')
     A N99                                  CF06(06 'CREATE')
     A N99                                  CF20(20 'WRKSPLF')
     A*
     A*
     A*--------------------------------------------------------------*
     A* SUBFILE FORMAT ONE
     A*--------------------------------------------------------------*
     A          R HP420S1                   SFL
     A*%%TS  SD  20090513  102423  ISLRAMS     REL-V5R4M0  5722-WDS
     A  85                                  SFLNXTCHG
     A            HDRRN1         4Y 0H
     A            HDTKDT    R        H      REFFLD(FHTKDT)
     A            HDPOFL    R        H      REFFLD(TSPOFL)
     A            HDRTDT    R        H      REFFLD(FHRTDT)
     A            HDPRFL    R        H      REFFLD(FHPRFL)
     A            HDCSFL    R        H      REFFLD(FHCSFL)
     A            DFOPT          2A  B 12  2CHGINPDFT(UL)
     A  30                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A N30                                  DSPATR(HI)
     A  99                                  DSPATR(PR)
     A            DFRTNO    R        O 12  5REFFLD(FHRTNO)
     A                                      EDTCDE(M)
     A            DFFMBO    R        O 12 16REFFLD(@@BOCD)
     A            DFTKNO    R        O 12 23REFFLD(FHTKNO)
     A                                      EDTCDE(M)
     A            DFTRCD    R        O 12 35REFFLD(FHTRCD)
     A            DFTOFL    R        O 12 37REFFLD(@@FLAG)
     A            DFPUTYCD  R        O 12 40REFFLD(HSRREF/@@PUTYCD *LIBL/HSPREF)
     A            DFFSCD    R        O 12 44REFFLD(FHFSCD)
     A                                      EDTCDE(M)
     A            DFCSFL    R        O 12 53REFFLD(FHREC/FHCSFL *LIBL/HSP037)
     A            DFMDY     R        O 12 57REFFLD(@@DT06)
     A                                      EDTCDE(Y)
     A            DFTSCD    R        O 12 67REFFLD(FHTSCD)
     A            DFVINO    R        O 12 71REFFLD(FHVINO)
     A*
     A                                 13 25'Costed Date/Time/User:'
     A                                      COLOR(BLU)
     A            DFCOMDY   R        O 13 48REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  ')
     A                                      COLOR(BLU)
     A            DFCOTM    R        O 13 57REFFLD(HSRREF/@@COTM *LIBL/HSPREF)
     A                                      EDTWRD('  :  :  ')
     A                                      COLOR(BLU)
     A            DFCOUSR   R        O 13 66REFFLD(HSRREF/@@COUSR *LIBL/HSPREF)
     A                                      COLOR(BLU)
     A*--------------------------------------------------------------*
     A* SUBFILE CONTROL FORMAT ONE
     A*--------------------------------------------------------------*
     A          R HP420C1                   SFLCTL(HP420S1)
     A*%%TS  SD  20090512  161825  ISLRAMS     REL-V5R4M0  5722-WDS
     A                                      SFLSIZ(0150)
     A                                      SFLPAG(0005)
     A N82N99                               ROLLUP(25 'PAGE FORWARD')
     A                                      SETOF(32)
     A                                      SETOF(33)
     A                                      SETOF(51)
     A                                      SETOF(52)
     A                                      SETOF(53)
     A                                      SETOF(54)
     A                                      SETOF(55)
     A                                      SETOF(56)
     A                                      SETOF(58)
     A                                      RTNCSRLOC(&RCD &FLD)
     A                                      OVERLAY
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A  78                                  SFLFOLD(CF13)
     A  79                                  SFLDROP(CF13)
     A                                      SFLMODE(&FOLD)
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            FOLD           1A  H
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 26'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  2 26'WORK WITH FEED TICKETS'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  4  2'Show current tickets only ..'
     A                                  4 31'?'
     A                                      DSPATR(HI)
     A            C1CURFL   R        B  4 34REFFLD(HSRREF/@@FLAG *LIBL/HSPREF)
     A  53
     AO 33                                  DSPATR(PC)
     A  33                                  DSPATR(RI)
     A N33                                  DSPATR(HI)
     A  99                                  DSPATR(PR)
     A                                  6  2'2=Revise'
     A                                  6 12'5=View Reference Ticket'
     A                                  6 37'9=Recost'
     A                                  7 18'?'
     A                                      DSPATR(HI)
     A                                  7 33'?'
     A                                      DSPATR(HI)
     A                                  7 39'?'
     A                                      DSPATR(HI)
     A                                  7 45'?'
     A                                      DSPATR(HI)
     A                                  7 52'?'
     A                                      DSPATR(HI)
     A                                  7 66'?'
     A                                      DSPATR(HI)
     A                                  8  5'Reference'
     A                                      DSPATR(HI)
     A                                  8 17'Mill'
     A                                      DSPATR(HI)
     A                                  8 24'Ticket'
     A                                      DSPATR(HI)
     A                                  8 33'Tran'
     A                                      DSPATR(HI)
     A                                  8 39'PU'
     A                                      DSPATR(HI)
     A                                  8 44'Farm'
     A                                      DSPATR(HI)
     A                                  8 51'Cost'
     A                                      DSPATR(HI)
     A                                  8 57'Ticket'
     A                                      DSPATR(HI)
     A                                  8 70'Vendor'
     A                                      DSPATR(HI)
     A                                  9  2'Op'
     A                                      DSPATR(HI)
     A                                  9  7'Ticket'
     A                                      DSPATR(HI)
     A                                  9 16'Office'
     A                                      DSPATR(HI)
     A                                  9 24'Number'
     A                                      DSPATR(HI)
     A                                  9 33'Code'
     A                                      DSPATR(HI)
     A                                  9 38'Type'
     A                                      DSPATR(HI)
     A                                  9 44'Site'
     A                                      DSPATR(HI)
     A                                  9 49'ThruSys'
     A                                      DSPATR(HI)
     A                                  9 58'Date'
     A                                      DSPATR(HI)
     A                                  9 65'Stat'
     A                                      DSPATR(HI)
     A                                  9 70'Invoice'
     A                                      DSPATR(HI)
     A            C1RTNO    R        B 10  5REFFLD(FHRTNO)
     A                                      DSPATR(HI)
     A N33N53                               DSPATR(PC)
     A  99                                  DSPATR(PR)
     A            C1FMBO    R        B 10 16REFFLD(FHFMBO)
     A  51                                  DSPATR(PC)
     A                                      DSPATR(HI)
     A            C1TKNO    R        B 10 23REFFLD(FHTKNO)
     A                                      DSPATR(HI)
     A  99                                  DSPATR(PR)
     A            C1TRCD    R        B 10 35REFFLD(FHTRCD)
     A  58
     AO 32                                  DSPATR(PC)
     A  32                                  DSPATR(RI)
     A N32                                  DSPATR(HI)
     A  99                                  DSPATR(PR)
     A            C1PUTYCD  R        B 10 39REFFLD(HSRREF/@@PUTYCD *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A  56                                  DSPATR(PC)
     A  99                                  DSPATR(PR)
     A            C1FSCD    R        B 10 43REFFLD(FHFSCD)
     A                                      EDTCDE(M)
     A                                      DSPATR(HI)
     A  52                                  DSPATR(PC)
     A  99                                  DSPATR(PR)
     A            C1CSFL    R        B 10 52REFFLD(FHREC/FHCSFL *LIBL/HSP037)
     A                                      DSPATR(HI)
     A  54                                  DSPATR(PC)
     A  99                                  DSPATR(PR)
     A            C1TSCD    R        B 10 66REFFLD(FHTSCD)
     A                                      DSPATR(HI)
     A  55                                  DSPATR(PC)
     A  99                                  DSPATR(PR)
     A                                      TEXT('TICKET STATUS')
     A            C1VINO    R        B 10 70REFFLD(FHVINO)
     A                                      DSPATR(HI)
     A  99                                  DSPATR(PR)
     A                                 11 25'Costed Date/Time/User:'
     A                                      DSPATR(HI)
     A                                      COLOR(BLU)
     A            C1COMDY   R        B 11 48REFFLD(HSRREF/@@DT06 *LIBL/HSPREF)
     A                                      EDTWRD('  /  /  ')
     A                                      DSPATR(HI)
     A            C1COTM    R        B 11 57REFFLD(HSRREF/@@TM06 *LIBL/HSPREF)
     A                                      EDTWRD('  :  :  ')
     A                                      DSPATR(HI)
     A            C1COUSR   R        B 11 66REFFLD(HSRREF/@@COUSR *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A*
     A*--------------------------------------------------------------*
     A* COMMAND KEY RECORD 1
     A*--------------------------------------------------------------*
     A          R HP420K1
     A*%%TS  SD  20090427  085605  ISLRAMS     REL-V5R4M0  5722-WDS
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A                                 23 11'F4=Prompt'
     A                                 23 22'F5=Refresh'
     A                                 23 34'F6=Create'
     A                                 23 45'F13=Fold/Drop'
     A                                 23 60'F20=Spool Files'
      *--------------------------------------------------------------*
      * ERROR MESSAGE SUBFILE FORMAT
      *--------------------------------------------------------------*
     A          R HP420ES                   SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
      *--------------------------------------------------------------*
      * ERROR MESSAGE SUBFILE CONTROL FORMAT
      *--------------------------------------------------------------*
     A          R HP420EC                   SFLCTL(HP420ES)
      *
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
