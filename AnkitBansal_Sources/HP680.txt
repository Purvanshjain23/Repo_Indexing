      *****************  RPG PROGRAM HEADING  ************************
      * SYSTEM:      HOG PRODUCTION
      * PROGRAM:     HP680
      * TITLE:       HOG SALES ACTIVITY REPORT
      * PROGRAMMER:  ROSE CENTONZE
      * CREATED:     6/21/94
      *
      * FUNCTION: THIS PROGRAM PRINTS A REPORT OF HOG SALES ACTIVITY
      *           FOR THE USER'S SELECTION CRITERIA. A WORKFILE
      *           (HSP380) IS BUILT IN HP380 AND PRINTED HERE.
      *
      *           THIS PROGRAM IS SUBMITTED BY HP480CL WHICH IS CALLED
      *           FROM HP480 THROUGH QCMDEXC.
      *
      *           THIS PROGRAM WILL PRINT EITHER A DETAILED OR A
      *           SUMMARIZED REPORT.  ONLY THE 'TOTAL' LINES ARE
      *           PRINTED ON THE 'SUMMARY' REPORT.
      *
      *           PLEASE NOTE THAT ALL VALUES ON TOTAL LINES ARE
      *           CALCULATED AS WEIGHTED AVERAGES.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      * 07/29/94  R. CENTONZE
      *           USE HSL064D A LIEU HSP064 - REMOVE B/O
      *
      * 08/05/94  R. CENTONZE
      *           USE HSL065C A LIEU HSP065 - REMOVE B/O;
      *           PRINT MORTALITY & SALES HD COUNT TOTALS.
      *
      * 09/12/94  LEANNE FEDOR
      *           RECOMPILE ONLY. REMOVED NEXT MOVEMENT NUMBER FROM
      *           BUSINESS OFFICE FILE.
      *
      * 09/26/94  MARY PALMER
      *           CALCULATE AND PRINT IOWA TOP BULK AND PREMIUM OVER/
      *           UNDER TOTAL FOR "LIVE" SALES TYPE.
      *
      * 09/27/94  LEANNE FEDOR
      *           RECOMPILE ONLY. REMOVED THE FOLLOWING UNUSED
      *           DATABASE FIELDS FROM THE CHECK FILES:
      *             HSP063-CHECK HEADER        CHCHBO CHHRDT
      *             HSP064-CHECK DETAIL        CDCHBO CDCSAM
      *             HSP065-CHECK ADJUSTMENTS   CJCHBO
      *
      * 10/07/94  LEANNE FEDOR
      *           MODIFIED LOGIC FOR 'DEAD' HOGS--THOSE THAT HAVE NO
      *           GROSS AMOUNT IN A CHECK DETAIL RECORD. AFTER THIS
      *           CHANGE ONLY THE NUMBER OF DEAD HEAD WILL PRINT ON
      *           A DETAIL LINE. NO DEAD VALUES, IF THERE ARE ANY,
      *           WILL PRINT UNDER THE MARKET, ACTUAL, PREMIUM OR
      *           PERFORMANCE COLUMNS OF THE REPORT.  DEAD VALUES
      *           WILL NOT BE ACCUMULATED FOR TOTALS OR CALCULATIONS.
      *
      * 10/07/94  LEANNE FEDOR
      *           ADDED 'HALF-ADJUST' TO ALL DIVIDE OPERATIONS AND
      *           ALL MULTIPLY OPERATIONS WHERE APPLICABLE.
      *
      * 11/16/94  MARY PALMER
      *           ADDED NOTATION OF "YES" OR "NO" TO RELATE TO NO
      *           FREIGHT AND NO NPPC ON A CHECK.
      *
      * 12/12/94  MARY PALMER
      *           CHANGED RECEIVED DATE THAT PRINTS ON LEVEL BREAK TO
      *           DATE FROM PREVIOUS PRINTED RECORDS, NOT CURRENT REC.
      *           CHANGE CHAIN TO IOWA MARKET PRICE, TO SETLL AND A
      *           READ PRIOR EQUAL ON EFFECTIVE DATE.
      *
      * 01/13/95  LEANNE FEDOR
      *           RECOMPILE ONLY. SEX ADDED TO HOG GROUP FILE.
      *
      * 03/13/95  LEANNE FEDOR
      *           RECOMPILE ONLY. ORIGIN BGF FARM SITE AND ORIGIN
      *           NURSERY FARM SITE ADDED TO HOG GROUP FILE.
      *
      * 04/24/95  LEANNE FEDOR
      *           CHANGED FARM SITE CODE FROM ALPHA TO NUMERIC.
      *
      * 05/05/95  LEANNE FEDOR
      *           AS REQUESTED BY TROY ZINN, ADDED AGE IN WEEK/DAYS TO
      *           THE REPORT.
      *
      * 08/21/95  LEANNE FEDOR
      *           REWRITTEN FOR MOVEMENT REWRITE. DURING THE REWRITE
      *           THE ORIGINAL PROGRAMS (HP680 FOR SUMMARY AND HP681
      *           FOR THE DETAIL REPORT) WERE COMBINED INTO THIS SINGLE
      *           PROGRAM (HP680) WHICH NOW PRINTS EITHER REPORT.
      *
      * 08/30/95  LEANNE FEDOR
      *           BUSINESS OFFICE IS NOW AN OPTIONAL SELECTION.
      *
      * 11/29/95  LEANNE FEDOR
      *           FIXED BUG. LEAN AMOUNT WAS BEING PRINTED ON THE
      *           DETAIL LINE IN THE LEAN PERCENT COLUMN. CHANGED
      *           REPORT TO PRINT LEAN PERCENT.
      *
      * 03/14/96  LEANNE FEDOR
      *           CHANGED THE COLUMN HEADING 'HEAD' TO BE 'LIVE HEAD'
      *
      * 04/29/96  LEANNE FEDOR
      *           PER DAVE O'SULLIVAN, REPLACED GENETIC LINE COLUMN
      *           WITH BREED STOCK COLUMN.
      *
      * 05/07/96  LEANNE FEDOR
      *           DAVE O'SULLIVAN REQUESTED THAT 'BREED STOCK' BE
      *           A SELECTION FOR THE REPORT. LOGIC WAS ADDED TO PLACE
      *           THIS SELECTION IN THE REPORT HEADING.
      *
      * 05/13/96  LEANNE FEDOR
      *           MADE CHANGES TO FACILITATE NEW JDE RELEASE CONVERSION
      *           CHANGED SEARCH TYPE TO 3A; CALL NEW GENERIC
      *           PROGRAM HPJDEV TO VALIDATE INSTEAD OF HPJDEA SINCE
      *           NO VENDOR ADDRESS IS REQUIRED.
      *
      * 05/21/97  LEANNE FEDOR
      *           THE ARRAY FIELDS FOR DOLLAR AMOUNTS WERE NOT LARGE
      *           ENOUGH.
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     FHSP011    IF   E           K DISK
      * PRODUCTION PHASE             (KEY: PPPPCD)
      *
     FHSP045    IF   E           K DISK
      *    PREMIUM/DISCOUNT CODES    (KEY: PDPDCD)
      *
     FHSL065C   IF   E           K DISK
      *    CHECK ADJUSTMENTS         (KEY: CJCHDT CJCVNO CJCHNO CJLNNO)
      *
     FHSP066    IF   E           K DISK
      *    SALES TYPE CODES          (KEY: STSTCD)
      *
     FHSP380    IF   E           K DISK
      *  SALES ACTIVITY WORKFILE
      *  (KEY: WFPPCD WFSTCD WFTPCD WFFSCD WFRCDT WFCVNO)
      *
     FQPRINT    O    F  198        PRINTER OFLIND(*INOF)
      *
      /EJECT
      *---------------------------------------------------------------
      *  STANDARD MESSAGE ARRAYS
      *---------------------------------------------------------------
      *
      *---------------------------------------------------------------
      *  ARRAYS FOR TOTALS
      *---------------------------------------------------------------
      *
     D SLHD            S              7  0 DIM(3)                               SOLD HEAD
     D LVHD            S              7  0 DIM(3)                               LIVE HEAD
     D DOHD            S              7  0 DIM(3)                               DEAD HEAD
      *
     D LVLB            S             15  0 DIM(3)                               LIVE POUNDS
     D CRCW            S             15  2 DIM(3)                               CARCASS CWT
      *
     D ITAM            S             15  2 DIM(3)                               IOWA MARKET $
     D BAAM            S             15  2 DIM(3)                               BASE MEAT $
     D GPAM            S             15  2 DIM(3)                               GRADE PREMIUM
     D SLAM            S             15  2 DIM(3)                               SORT LOSS $
     D GRAM            S             15  2 DIM(3)                               GROSS $
      *
     D NCAM            S             15  2 DIM(3)                               NET CARCASS $
     D NLAM            S             15  2 DIM(3)                               NET LIVE $
     D PYAM            S             15  4 DIM(3)                               PERF YIELD $
      *
     D LNAM            S             15  2 DIM(3)                               LEAN
     D LNCW            S             15  2 DIM(3)                               LEAN CWT
      *
     D BFAM            S             15  2 DIM(3)                               BACKFAT
     D BFCW            S             15  2 DIM(3)                               BACKFAT CWT
      *
      /EJECT
      ****************************************************************
      * INPUT SPECS
      ****************************************************************
      *---------------------------------------------------------------
      * STANDARD ERROR MESSAGE HANDLING DATA STRUCTURES
      *---------------------------------------------------------------
      *
      * FOR ERROR MESSAGE HANDLING, A PACKED INDEX (E) IS REQUIRED.
      * FOR PROGRAM READABILITY, DEFINE A CORRESPONDING ERROR COUNT
      * FIELD CALLED 'ERROR'
      *
     D                 DS                  INZ
     D  ERROR                  1      2P 0
     D  E                      1      2P 0
      *
      * THIS DATA STRUCTURE SUPPLIES THE NAME OF THE MESSAGE FILE TO
      * THE MESSAGE HANDLING CL PROGRAM.  THE FIELD NAME MSGFIL MUST BE
      * CONSTANT. THE VALUE IN QUOTES IS THE NAME OF THE SPECIFIC
      * MESSAGE FILE CONTAINING THE USER DEFINED MESSAGES.
      *
     D                 DS                  INZ
     D  MSGFIL                 1     10    INZ('HSMSGF    ')
      *
      * THE FOLLOWING 3 DATA STRUCTURES ARE USED TO SPEED MESSAGE
      * HANDLING SINCE IT IS FASTER TO CLEAR DATA STRUCTURES THAN
      * ARRAYS.  EACH IS ASSOCIATED WITH A STANDARD MESSAGE ARRAY.
      *
     D MGI             DS           140    INZ
     D  MGID                   1    140
     D                                     DIM(20)                              MSG ID ARRAY
      *
     D MGD             DS          1000    INZ
     D  MGDT                   1   1000
     D                                     DIM(20)                              MSG PARMS
      *
     D MGWK            DS            50    INZ
     D  MGW                    1     50
     D                                     DIM(50)                              MSG WORK PARMS
      *
      *---------------------------------------------------------------
      * STANDARD PROGRAM STATUS DATA STRUCTURE
      *---------------------------------------------------------------
      *    EXTERNALLY DEFINED AS UTPGFR (RECORD FORMAT: PGMDSR)
     D PGMDS         ESDS                  EXTNAME(UTPGFR)
      *
      *----------------------------------------------------------------
      * NAMED CONSTANTS
      *----------------------------------------------------------------
      *
     D YES             C                   CONST('Y')
     D NO              C                   CONST('N')
     D CARC            C                   CONST('CARCASS PAYMENT')
     D LIVE            C                   CONST('LIVE PAYMENT')
      *
      *---------------------------------------------------------------
      *  DATE FORMATS
      *---------------------------------------------------------------
      *
      * DATE IN MMDDYY FORMAT
      *
     D                 DS
     D  WKMDY                  1      6  0
     D  WKMM                   1      2  0
     D  WKDD                   3      4  0
     D  WKYY                   5      6  0
      *
      * DATE IN CCYYMMDD FORMAT
      *
     D                 DS
     D  WKCYMD                 1      8  0
     D  WKCEN                  1      2  0
     D  WKYER                  3      4  0
     D  WKMON                  5      6  0
     D  WKDAY                  7      8  0
      *
      *----------------------------------------------------------------
      * LDA - REPORT SELECTIONS
      *----------------------------------------------------------------
      *
     D                UDS
     D  LDFSBO                 1      5
     D  LDFYMD                 6     13  0
     D  LDTYMD                14     21  0
     D  LDPTCD                22     26
     D  LDPPCD                27     31
     D  LDSTCD                32     36
     D  LDFSCD                37     41  0
     D  LDCVNO                42     49  0
     D  LDHGSN                50     56  0
     D  LDRPFL                57     57
     D  LDTPCD                58     58
      *
     D  LDBODS                59     94
     D  LDFSNM                95    119
     D  LDHGCD               120    126
     D  LDCNAM               127    156
     D  LDSTDS               157    176
     D  LDPPDS               177    206
     D  LDFMDY               207    212  0
     D  LDTMDY               213    218  0
     D  LDPTDS               219    228
     D  LDTPDS               229    248
     D  LDBLCD               249    253
     D  LDRMCD               254    258
     D  LDRPDS               259    265
     D  LDBSCD               266    270
     D  LDBSDS               271    290
      /EJECT
      ****************************************************************
      * MAIN LINE
      ****************************************************************
      *
      * PROCESS EACH WORKFILE RECORD
      *
     C     *IN90         DOUEQ     *ON                                          MAIN DO
     C                   READ      HSP380                                 90
     C     *IN90         IFEQ      *OFF                                         IF NOT EOF
      *
     C     FIRST         IFEQ      YES                                          IF FIRST
     C                   EXSR      $FIRST
     C                   ENDIF                                                  IF FIRST
      *
      * ------------------------------------------------------------
      * BREAK IN PRODUCTION PHASE, SALE TYPE OR TYPE OF PAYMENT
      * ------------------------------------------------------------
      *
     C                   SELECT
     C     WFPPCD        WHENNE    SVPPCD
     C     WFSTCD        ORNE      SVSTCD
     C     WFTPCD        ORNE      SVTPCD
     C                   EXSR      $TDTOT
     C                   EXSR      $TFTOT
     C                   EXSR      $TTTOT
      *
     C     WFPPCD        IFNE      SVPPCD
     C                   EXSR      $PPDS
     C                   EXCEPT    HRHDR
     C                   EXCEPT    HPHDR
     C                   ENDIF
      *
      * RETRIEVE AND PRINT HEADING FOR NEXT SALE TYPE/PAYMENT TYPE
      *
     C                   EXSR      $STDS
     C     *INOF         IFEQ      *ON
     C                   EXCEPT    HRHDR
     C                   EXCEPT    HPHDR
     C                   ENDIF
     C                   EXCEPT    HSHDR
      *
      *-------------------------------------------------------------
      * BREAK IN FARM SITE
      *-------------------------------------------------------------
      *
     C     WFFSCD        WHENNE    SVFSCD
     C                   EXSR      $TDTOT
     C                   EXSR      $TFTOT
      *
      *-------------------------------------------------------------
      * BREAK IN RECEIVED DATE OR CUSTOMER
      *-------------------------------------------------------------
      *
     C     WFRCDT        WHENNE    SVRCDT
     C     WFCVNO        ORNE      SVCVNO
     C                   EXSR      $TDTOT
     C                   ENDSL
      *
      *-------------------------------------------------------------
      * DETAIL PROCESSING
      *-------------------------------------------------------------
      *
     C                   EXSR      $DTL
      *
     C                   ENDIF                                                  IF NOT EOF
     C                   ENDDO                                                  MAIN DO
      *
      *----------------------------------------------------------------
      * EOF PROCESSING
      *----------------------------------------------------------------
      *
      *  IF NO DETAIL RECORDS MATCHED THE SELECTION CRITERIA, THEN
      *  PRINT THE STANDARD 'NO DATA...' ERROR MESSAGE
      *
     C     FIRST         IFEQ      YES
     C                   EXCEPT    HRHDR
     C                   EXCEPT    NODATA
     C                   ELSE
     C                   EXSR      $TDTOT
     C                   EXSR      $TFTOT
     C                   EXSR      $TTTOT
     C                   ENDIF
      *
     C                   SETON                                        LR
      /EJECT
      *----------------------------------------------------------------
      * $DTL - DETAIL PROCESSING
      *----------------------------------------------------------------
      *
     C     $DTL          BEGSR
      *
      * RETRIEVE DEDUCTIONS/ADD-ONS AND PREMIUMS/DISCOUNTS
      * FOR THE CHECK DETAIL LINE
      *
     C                   EXSR      $TOTDA
      *
      * ACCUMULATE VALUES IN TABLES FOR TOTALS
      *
     C                   ADD       WFLVHD        SLHD
     C                   ADD       WFDOHD        DOHD
     C                   ADD       WFLVHD        LVHD
      *
     C                   ADD       WFCRCW        CRCW
     C                   ADD       WFLVLB        LVLB
      *
      *----------------------------------------------------------
      * PERFORM CALCULATIONS THAT ARE COMMON FOR LIVE AND CARCASS
      *----------------------------------------------------------
      *
      * CALCULATE GRADE PREMIUM
      *
     C     WFCRCW        IFNE      0                                            IF CARC CWT
      *
     C     WKGDAM        IFNE      0                                            IF GRADE
     C     WKGDAM        DIV(H)    WFCRCW        WKF92
     C                   Z-ADD     WKF92         DTGRPR
      *
      *  ACCUMULATE FOR WEIGHTED AVERAGES ON TOTAL LINES
      *
     C     WKF92         MULT      WFLVHD        WKF154
     C                   ADD       WKF154        GPAM
     C                   ENDIF                                                  IF GRADE
      *
      * ACCUMULATE SORT LOSS AMOUNT FOR TOTALS AND
      * CALCULATE SORT LOSS FOR DETAIL LINE
      *
     C                   ADD       WKSLAM        SLAM
     C     WKSLAM        DIV(H)    WFCRCW        DTSRLS
     C                   ENDIF                                                  IF CARC CWT
      *
      * CALCULATE NET LIVE PRICE
      *
     C     WFLVCW        IFNE      0
     C     WFGRAM        ADD       WKDAAM        WKF92
     C     WKF92         DIV(H)    WFLVCW        WKF92
     C                   Z-ADD     WKF92         DTNLRT
      *
      *  ACCUMULATE FOR WEIGHTED AVERAGES ON TOTAL LINES
      *
     C     WKF92         MULT      WFLVHD        WKF154
     C                   ADD       WKF154        NLAM
     C                   ENDIF
      *
      * CALCULATE PERFORMANCE YIELD
      *
     C     WFLVCW        IFNE      0
     C     WFCRCW        DIV(H)    WFLVCW        WKF94
     C     WKF94         MULT(H)   100           DTPYLD
      *
      *  ACCUMULATE FOR WEIGHTED AVERAGES ON TOTAL LINES
      *
     C     WKF94         MULT      WFLVHD        WKF154
     C                   ADD       WKF154        PYAM
     C                   ENDIF
      *
      * ACCUMULATE BACKFAT FOR WEIGHTED AVERAGES ON TOTAL LINES
      *
     C     WFBFAM        MULT(H)   WFCRCW        WKF92
     C     WKF92         IFNE      0
     C                   ADD       WKF92         BFAM
     C                   ADD       WFCRCW        BFCW
     C                   ENDIF
      *
      * ACCUMULATE LEAN PERCENT FOR WEIGHTED AVERAGES ON TOTAL LINES
      *
     C     WFLNPR        MULT(H)   WFCRCW        WKF92
     C     WKF92         IFNE      0
     C                   ADD       WKF92         LNAM
     C                   ADD       WFCRCW        LNCW
     C                   ENDIF
      *
      * CALCULATE AVERAGE LIVE WEIGHT FOR THE DETAIL LINE
      *
     C     WFLVHD        IFNE      0
     C     WFLVLB        DIV(H)    WFLVHD        DTLVWT
     C                   ENDIF
      *
      *----------------------------------------------------------
      * PERFORM CALCULATIONS THAT ONLY PERTAIN TO RECORDS
      * THAT HAVE A PAYMENT TYPE OF 'CARCASS'
      *----------------------------------------------------------
      *
     C                   SELECT
     C     WFTPCD        WHENEQ    'C'                                          WH CARCASS
      *
      * CALCULATE BASE MEAT PRICE FOR CARCASS CHECKS
      *
     C     WFCRCW        IFNE      0                                            IF CARC LBS
     C     WFBAAM        DIV(H)    WFCRCW        WKF92
     C                   Z-ADD     WKF92         DTBMRT
      *
      * ACCUMULATE BASE MEAT PRICE FOR WEIGHTED AVERAGES ON
      * TOTAL LINES.
      *
     C     WKF92         MULT      WFLVHD        WKF154
     C                   ADD       WKF154        BAAM
      *
      * CALCULATE GROSS MEAT PRICE FOR CARCASS CHECKS
      *
     C     WFGRAM        DIV(H)    WFCRCW        WKF92
     C                   Z-ADD     WKF92         DTGMRT
      *
      * ACCUMULATE GROSS MEAT PRICE FOR WEIGHTED AVERAGES ON
      * TOTAL LINES.
      *
     C     WKF92         MULT      WFLVHD        WKF154
     C                   ADD       WKF154        GRAM
      *
      * CALCULATE NET CARCASS PRICE
      *
     C     WFGRAM        IFNE      0                                            IF GROSS $
     C     WFGRAM        ADD       WKDAAM        WKF92
     C     WKF92         DIV(H)    WFCRCW        WKF92
     C                   Z-ADD     WKF92         DTNCRT
      *
      * ACCUMULATE NET CARCASS PRICE FOR WEIGHTED AVERAGES ON
      * TOTAL LINES.
      *
     C     WKF92         MULT      WFLVHD        WKF154
     C                   ADD       WKF154        NCAM
     C                   ENDIF                                                  IF GROSS $
     C                   ENDIF                                                  IF CARC LBS
      *
      * CALCULATE MARKET MEAT PRICE
      *
     C     WFITRT        DIV(H)    .74           DTMMRT
      *
      * ACCUMULATE MARKET MEAT PRICE FOR WEIGHTED AVERAGES ON
      * TOTAL LINES.
      *
     C     WFITRT        MULT      WFLVHD        WKF154
     C                   ADD       WKF154        ITAM
      *
      * CALCULATE PREMIUM OVER/UNDER MARKET BASE
      *
     C     DTBMRT        SUB       DTMMRT        DTPBRT
      *
      * CALCULATE PREMIUM OVER/UNDER MARKET TOTAL
      *
     C     DTNCRT        SUB       DTMMRT        DTPTRT
     C                   OTHER
      *
      *----------------------------------------------------------
      * PERFORM CALCULATIONS FOR RECORDS THAT WERE PAID LIVE
      *----------------------------------------------------------
      *
      * CALCULATE GROSS MEAT PRICE ON PAID LIVE RECORDS
      *
     C     WFLVCW        IFNE      0                                            IF LIVE LBS
     C     WFGRAM        DIV(H)    WFLVCW        WKF92
     C                   Z-ADD     WKF92         DTGMRT
      *
      * ACCUMULATE GROSS MEAT PRICE FOR WEIGHTED AVERAGES ON
      * TOTAL LINES.
      *
     C     WKF92         MULT      WFLVHD        WKF154
     C                   ADD       WKF154        GRAM
     C                   ENDIF                                                  IF LIVE LBS
      *
      * CALCULATE PREMIUM OVER/UNDER MARKET TOTAL FOR MARKET
      * HOG SALES THAT WERE PAID 'LIVE' (OMIT FEEDER PIG AND
      * CULL SALES).
      *
     C     WFSTCD        IFEQ      'MRKTS'
      *
     C     DTNLRT        SUB       WFITRT        DTPTRT
      *
      * ACCUMULATE IOWA TOP MARKET FOR TOTALS
      *
     C     WFITRT        MULT      WFLVHD        WKF154
     C                   ADD       WKF154        ITAM
     C                   ENDIF
     C                   ENDSL
      *
      * PRINT DETAIL LINE IF THE USER SELECTED THE 'DETAIL' VERSION
      * OF THE REPORT.
      *
     C     LDRPFL        IFEQ      'D'
     C     *INOF         IFEQ      *ON
     C                   EXCEPT    HRHDR
     C                   EXCEPT    HPHDR
     C                   EXCEPT    HSHDR
     C                   ENDIF
      *
     C                   EXCEPT    DTLINE
     C                   ENDIF
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $TDTOT - PRINT TOTAL LINE FOR BREAK ON RECEIVED DATE/CUSTOMER
      *----------------------------------------------------------------
      *
     C     $TDTOT        BEGSR
      *
      * CONVERT RECEIVED DATE TO MMDDYY
      *
     C                   Z-ADD     SVRCDT        WKCYMD
     C                   EXSR      $TOMDY
     C                   Z-ADD     WKMDY         TDRMDY
      *
      * RETRIEVE CUSTOMER NAME
      *
     C                   Z-ADD     SVCVNO        INAJD
     C                   MOVEL(P)  'C'           INAT1
     C                   EXSR      $JDEV
     C                   MOVEL     OUALPH        TDCNAM
      *
      *--------------------------------------------------------------
      * FOR ALL SALE TYPES AND PAYMENT TYPES
      *--------------------------------------------------------------
      *
      * CALCULATE ACTUAL GROSS MEAT PRICE
      *
     C     LVHD(1)       IFNE      0
     C     GRAM(1)       DIV(H)    LVHD(1)       TDGMRT
     C                   ENDIF
      *
      * CALCULATE ACTUAL NET CARCASS PRICE
      *
     C     LVHD(1)       IFNE      0
     C     NCAM(1)       DIV(H)    LVHD(1)       TDNCRT
     C                   ENDIF
      *
      * CALCULATE ACTUAL NET LIVE PRICE
      *
     C     LVHD(1)       IFNE      0
     C     NLAM(1)       DIV(H)    LVHD(1)       TDNLRT
     C                   ENDIF
      *
      * CALCULATE PERFORMANCE YIELD
      *
     C     LVHD(1)       IFNE      0
     C     PYAM(1)       DIV(H)    LVHD(1)       WKF94
     C     WKF94         MULT(H)   100           TDPYLD
     C                   ENDIF
      *
      * CALCULATE AVERAGE LIVE WEIGHT
      *
     C     LVHD(1)       IFNE      0
     C     LVLB(1)       DIV(H)    LVHD(1)       TDLVWT
     C                   ENDIF
      *
      * CALCULATE BACKFAT
      *
     C     BFCW(1)       IFNE      0
     C     BFAM(1)       DIV(H)    BFCW(1)       TDBFAM
     C                   ENDIF
      *
      * CALCULATE LEAN PERCENT
      *
     C     LNCW(1)       IFNE      0
     C     LNAM(1)       DIV(H)    LNCW(1)       TDLNAM
     C                   ENDIF
      *
      *--------------------------------------------------------------
      * FOR MARKET HOG SALES ONLY
      *--------------------------------------------------------------
      *
     C     SVSTCD        IFEQ      'MRKTS'                                      IF MARKET
     C     LVHD(1)       ANDNE     0
      *
      * IOWA TOP RATE
      *
     C     ITAM(1)       DIV(H)    LVHD(1)       TDITRT
      *
      *--------------------------------------------------------
      * FOR MARKET HOG SALES---PAYMENT TYPE OF 'CARCASS'
      *--------------------------------------------------------
      *
     C                   SELECT
     C     SVTPCD        WHENEQ    'C'                                          WH CARC
      *
      *  CALCULATE MARKET MEAT PRICE
      *
     C     TDITRT        DIV(H)    .74           TDMMRT
      *
      * CALCULATE ACTUAL BASE MEAT PRICE
      *
     C     LVHD(1)       IFNE      0
     C     BAAM(1)       DIV(H)    LVHD(1)       TDBMRT
     C                   ENDIF
      *
      * CALCULATE GRADE PREMIUM
      *
     C     LVHD(1)       IFNE      0
     C     GPAM(1)       DIV(H)    LVHD(1)       TDGRPR
     C                   ENDIF
      *
      * CALCULATE SORT LOSS
      *
     C     CRCW(1)       IFNE      0
     C     SLAM(1)       DIV(H)    CRCW(1)       TDSRLS
     C                   ENDIF
      *
      *  CALCULATE PREMIUM OVER/UNDER MARKET BASE
      *
     C     TDBMRT        SUB       TDMMRT        TDPBRT
      *
      *  CALCULATE PREMIUM OVER/UNDER MARKET TOTAL
      *
     C     TDNCRT        SUB       TDMMRT        TDPTRT
      *
      *--------------------------------------------------------
      * FOR MARKET HOG SALES PAID LIVE
      *--------------------------------------------------------
      *
     C     SVTPCD        WHENEQ    'L'
     C     TDNLRT        SUB       TDITRT        TDPTRT
     C                   ENDSL
     C                   ENDIF                                                  IF MARKET
      *
      * PRINT TOTAL LINE FOR RECEIVED DATE/CUSTOMER BREAK
      *
     C     *INOF         IFEQ      *ON
     C                   EXCEPT    HRHDR
     C                   EXCEPT    HPHDR
     C                   EXCEPT    HSHDR
     C                   ENDIF
      *
     C                   EXCEPT    TDTOT
      *
      * IF YOU ARE PRINTING THE 'DETAIL' VERSION OF THE REPORT,
      * PRINT A BLANK LINE AFTER THE TOTAL LINE.
      *
     C     LDRPFL        IFEQ      'D'
     C                   EXCEPT    BLANK
     C                   ENDIF
      *
      * SAVE CONTROL BREAK FIELDS AND SET OFF PRINT INDICATORS
      *
     C                   Z-ADD     WFRCDT        SVRCDT
     C                   Z-ADD     WFCVNO        SVCVNO
     C                   SETOFF                                       7172
      *
      * CLEAR DATE/CUSTOMER ACCUMULATIONS FROM TABLES
      *
     C                   Z-ADD     1             X
     C                   EXSR      $CLEAR
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $TOTDA - ACCUMULATE DEDUCTION/ADD-ONS, PREMIUMS/DISCOUNTS
      *----------------------------------------------------------------
      *
     C     $TOTDA        BEGSR
      *
     C                   Z-ADD     0             WKDAAM
     C                   Z-ADD     0             WKGDAM
     C                   Z-ADD     0             WKSLAM
      *
     C     K1L065        SETLL     HSL065C
      *
     C     *IN70         DOUEQ     *ON                                          DO LOOP
     C     K1L065        READE     HSL065C                                70
     C     *IN70         IFEQ      *OFF                                         IF NOT EOF
      *
     C                   SELECT
     C     CJCJFL        WHENEQ    'D'                                          WH DED/ADD
     C                   ADD       CJCJAM        WKDAAM
      *
      *  SETON INDICATOR 71 & 72 IF FREIGHT OR NPPC EXIST
      *
     C                   SELECT
     C     CJCJCD        WHENEQ    'NPPC '
     C                   SETON                                        71
      *
     C     CJCJCD        WHENEQ    'TRUCK'
     C                   SETON                                        72
     C                   ENDSL
     C                   OTHER
      *
      * RETRIEVE PREMIUM/DISCOUNT RECORD
      *
     C     CJCJCD        CHAIN     HSP045                             92
     C     *IN92         IFEQ      *OFF                                         IF PREM/DIS
     C                   SELECT
     C     PDPDFL        WHENEQ    'G'                                          WH GRADE
     C                   ADD       CJCJAM        WKGDAM
     C     PDPDFL        WHENEQ    'S'                                          WH SORT
     C                   ADD       CJCJAM        WKSLAM
     C                   ENDSL
     C                   ENDIF                                                  IF PREM/DIS
      *
     C                   ENDSL
     C                   ENDIF                                                  IF NOT EOF
     C                   ENDDO                                                  DO LOOP
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * $STDS - SET UP SALES TYPE AND PAYMENT TYPE DESCRIPTION
      *---------------------------------------------------------------
      *
     C     $STDS         BEGSR
      *
      *  SALES TYPE
     C                   MOVE      *BLANK        HSSTDS
     C     WFSTCD        CHAIN     HSP066                             92
     C     *IN92         IFEQ      *OFF
     C                   MOVEL     STSTDS        HSSTDS
     C                   ELSE
     C                   MOVEL     'UNKNOWN'     HSSTDS
     C                   ENDIF
      *
      * TYPE OF PAYMENT
     C                   SELECT
     C     WFSTCD        WHENEQ    'CULLS'
     C     WFSTCD        OREQ      'FPS  '
     C                   MOVE      *BLANK        HSTPDS
      *
     C     WFTPCD        WHENEQ    'C'
     C                   MOVEL(P)  CARC          HSTPDS
      *
     C     WFTPCD        WHENEQ    'L'
     C                   MOVEL(P)  LIVE          HSTPDS
     C                   ENDSL
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * $PPDS - RETRIEVE PRODUCTION PHASE DESCRIPTION
      *---------------------------------------------------------------
      *
     C     $PPDS         BEGSR
      *
     C                   MOVE      *BLANK        HPPPDS
     C                   MOVEL     WFPPCD        SVPPCD
      *
     C     WFPPCD        CHAIN     HSP011                             92
     C     *IN92         IFEQ      *OFF                                         IF FOUND
     C                   MOVEL     PPPPDS        HPPPDS
     C                   ELSE
     C                   MOVEL     'UNKNOWN'     HPPPDS
     C                   ENDIF                                                  IF FOUND
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * $TFTOT -  PRINT FARM SITE TOTALS
      *---------------------------------------------------------------
      *
     C     $TFTOT        BEGSR
      *
      *--------------------------------------------------------------
      * FOR ALL SALE TYPES AND PAYMENT TYPES
      *--------------------------------------------------------------
      *
      * CALCULATE ACTUAL GROSS MEAT PRICE
      *
     C     LVHD(2)       IFNE      0
     C     GRAM(2)       DIV(H)    LVHD(2)       TFGMRT
     C                   ENDIF
      *
      * CALCULATE ACTUAL NET CARCASS PRICE
      *
     C     LVHD(2)       IFNE      0
     C     NCAM(2)       DIV(H)    LVHD(2)       TFNCRT
     C                   ENDIF
      *
      * CALCULATE ACTUAL NET LIVE PRICE
      *
     C     LVHD(2)       IFNE      0
     C     NLAM(2)       DIV(H)    LVHD(2)       TFNLRT
     C                   ENDIF
      *
      * CALCULATE PERFORMANCE YIELD
      *
     C     LVHD(2)       IFNE      0
     C     PYAM(2)       DIV(H)    LVHD(2)       WKF94
     C     WKF94         MULT(H)   100           TFPYLD
     C                   ENDIF
      *
      * CALCULATE AVERAGE LIVE WEIGHT
      *
     C     LVHD(2)       IFNE      0
     C     LVLB(2)       DIV(H)    LVHD(2)       TFLVWT
     C                   ENDIF
      *
      * CALCULATE BACKFAT
      *
     C     BFCW(2)       IFNE      0
     C     BFAM(2)       DIV(H)    BFCW(2)       TFBFAM
     C                   ENDIF
      *
      * CALCULATE LEAN PERCENT
      *
     C     LNCW(2)       IFNE      0
     C     LNAM(2)       DIV(H)    LNCW(2)       TFLNAM
     C                   ENDIF
      *
      *--------------------------------------------------------------
      * FOR MARKET HOG SALES ONLY
      *--------------------------------------------------------------
      *
     C     SVSTCD        IFEQ      'MRKTS'                                      IF MARKET
     C     LVHD(2)       ANDNE     0
      *
      * IOWA TOP RATE
      *
     C     ITAM(2)       DIV(H)    LVHD(2)       TFITRT
      *
      *--------------------------------------------------------
      * FOR MARKET HOG SALES---PAYMENT TYPE OF 'CARCASS'
      *--------------------------------------------------------
      *
     C                   SELECT
     C     SVTPCD        WHENEQ    'C'                                          WH CARC
      *
      *  CALCULATE MARKET MEAT PRICE
      *
     C     TFITRT        DIV(H)    .74           TFMMRT
      *
      * CALCULATE ACTUAL BASE MEAT PRICE
      *
     C     LVHD(2)       IFNE      0
     C     BAAM(2)       DIV(H)    LVHD(2)       TFBMRT
     C                   ENDIF
      *
      * CALCULATE GRADE PREMIUM
      *
     C     LVHD(2)       IFNE      0
     C     GPAM(2)       DIV(H)    LVHD(2)       TFGRPR
     C                   ENDIF
      *
      * CALCULATE SORT LOSS
      *
     C     CRCW(2)       IFNE      0
     C     SLAM(2)       DIV(H)    CRCW(2)       TFSRLS
     C                   ENDIF
      *
      *  CALCULATE PREMIUM OVER/UNDER MARKET BASE
      *
     C     TFBMRT        SUB       TFMMRT        TFPBRT
      *
      *  CALCULATE PREMIUM OVER/UNDER MARKET TOTAL
      *
     C     TFNCRT        SUB       TFMMRT        TFPTRT
      *
      *--------------------------------------------------------
      * FOR MARKET HOG SALES PAID LIVE
      *--------------------------------------------------------
      *
     C     SVTPCD        WHENEQ    'L'
     C     TFNLRT        SUB       TFITRT        TFPTRT
     C                   ENDSL
     C                   ENDIF                                                  IF MARKET
      *
     C     *INOF         IFEQ      *ON
     C                   EXCEPT    HRHDR
     C                   EXCEPT    HPHDR
     C                   EXCEPT    HSHDR
     C                   ENDIF
     C                   EXCEPT    TFTOT
      *
      * SAVE CONTROL BREAK FIELDS
      * CLEAR FARM SITE TOTAL ACCUMULATIONS FROM TABLES
      *
     C                   Z-ADD     WFFSCD        SVFSCD
     C                   Z-ADD     2             X
     C                   EXSR      $CLEAR
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * $TTTOT - TOTALS FOR BREAK ON PHASE/SALES TYPE/TYPE OF PAYMENT
      *---------------------------------------------------------------
      *
     C     $TTTOT        BEGSR
      *
      *--------------------------------------------------------------
      * FOR ALL SALE TYPES AND PAYMENT TYPES
      *--------------------------------------------------------------
      *
      * CALCULATE ACTUAL GROSS MEAT PRICE
      *
     C     LVHD(3)       IFNE      0
     C     GRAM(3)       DIV(H)    LVHD(3)       TTGMRT
     C                   ENDIF
      *
      * CALCULATE ACTUAL NET CARCASS PRICE
      *
     C     LVHD(3)       IFNE      0
     C     NCAM(3)       DIV(H)    LVHD(3)       TTNCRT
     C                   ENDIF
      *
      * CALCULATE ACTUAL NET LIVE PRICE
      *
     C     LVHD(3)       IFNE      0
     C     NLAM(3)       DIV(H)    LVHD(3)       TTNLRT
     C                   ENDIF
      *
      * CALCULATE PERFORMANCE YIELD
      *
     C     LVHD(3)       IFNE      0
     C     PYAM(3)       DIV(H)    LVHD(3)       WKF94
     C     WKF94         MULT(H)   100           TTPYLD
     C                   ENDIF
      *
      * CALCULATE AVERAGE LIVE WEIGHT
      *
     C     LVHD(3)       IFNE      0
     C     LVLB(3)       DIV(H)    LVHD(3)       TTLVWT
     C                   ENDIF
      *
      * CALCULATE BACKFAT
      *
     C     BFCW(3)       IFNE      0
     C     BFAM(3)       DIV(H)    BFCW(3)       TTBFAM
     C                   ENDIF
      *
      * CALCULATE LEAN PERCENT
      *
     C     LNCW(3)       IFNE      0
     C     LNAM(3)       DIV(H)    LNCW(3)       TTLNAM
     C                   ENDIF
      *
      *--------------------------------------------------------------
      * FOR MARKET HOG SALES ONLY
      *--------------------------------------------------------------
      *
     C     SVSTCD        IFEQ      'MRKTS'                                      IF MARKET
     C     LVHD(3)       ANDNE     0
      *
      * IOWA TOP MARKET RATE
      *
     C     ITAM(3)       DIV(H)    LVHD(3)       TTITRT
      *
      *--------------------------------------------------------
      * FOR MARKET HOG SALES---PAYMENT TYPE OF 'CARCASS'
      *--------------------------------------------------------
      *
     C                   SELECT
     C     SVTPCD        WHENEQ    'C'                                          WH CARC
      *
      *  CALCULATE MARKET MEAT PRICE
      *
     C     TTITRT        DIV(H)    .74           TTMMRT
      *
      * CALCULATE ACTUAL BASE MEAT PRICE
      *
     C     LVHD(3)       IFNE      0
     C     BAAM(3)       DIV(H)    LVHD(3)       TTBMRT
     C                   ENDIF
      *
      * CALCULATE GRADE PREMIUM
      *
     C     LVHD(3)       IFNE      0
     C     GPAM(3)       DIV(H)    LVHD(3)       TTGRPR
     C                   ENDIF
      *
      * CALCULATE SORT LOSS
      *
     C     CRCW(3)       IFNE      0
     C     SLAM(3)       DIV(H)    CRCW(3)       TTSRLS
     C                   ENDIF
      *
      *  CALCULATE PREMIUM OVER/UNDER MARKET BASE
      *
     C     TTBMRT        SUB       TTMMRT        TTPBRT
      *
      *  CALCULATE PREMIUM OVER/UNDER MARKET TOTAL
      *
     C     TTNCRT        SUB       TTMMRT        TTPTRT
      *
      *--------------------------------------------------------
      * FOR MARKET HOG SALES PAID LIVE
      *--------------------------------------------------------
      *
     C     SVTPCD        WHENEQ    'L'
     C     TTNLRT        SUB       TTITRT        TTPTRT
     C                   ENDSL
     C                   ENDIF                                                  IF MARKET
     C                   EXCEPT    TTTOT
      *
      * SAVE CONTROL BREAK FIELDS
      * CLEAR SALES TYPE TOTAL ACCUMULATIONS FROM TABLES
      *
     C                   MOVEL     WFSTCD        SVSTCD
     C                   MOVEL     WFTPCD        SVTPCD
      *
     C                   Z-ADD     3             X
     C                   EXSR      $CLEAR
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * $CLEAR - CLEAR ACCUMULATIONS FROM TABLE ELEMENTS
      *---------------------------------------------------------------
      *
     C     $CLEAR        BEGSR
      *
     C                   Z-ADD     0             LVLB(X)
     C                   Z-ADD     0             CRCW(X)
      *
     C                   Z-ADD     0             SLHD(X)
     C                   Z-ADD     0             LVHD(X)
     C                   Z-ADD     0             DOHD(X)
      *
     C                   Z-ADD     0             ITAM(X)
     C                   Z-ADD     0             BAAM(X)
     C                   Z-ADD     0             GPAM(X)
     C                   Z-ADD     0             SLAM(X)
     C                   Z-ADD     0             GRAM(X)
     C                   Z-ADD     0             NCAM(X)
     C                   Z-ADD     0             NLAM(X)
     C                   Z-ADD     0             PYAM(X)
      *
     C                   Z-ADD     0             LNAM(X)
     C                   Z-ADD     0             LNCW(X)
      *
     C                   Z-ADD     0             BFAM(X)
     C                   Z-ADD     0             BFCW(X)
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * $FIRST - FIRST TIME THROUGH
      *---------------------------------------------------------------
      *
     C     $FIRST        BEGSR
      *
     C                   MOVEL     NO            FIRST
      *
     C                   MOVEL     WFPPCD        SVPPCD
     C                   MOVEL     WFSTCD        SVSTCD
     C                   MOVEL     WFTPCD        SVTPCD
     C                   Z-ADD     WFFSCD        SVFSCD
     C                   Z-ADD     WFRCDT        SVRCDT
     C                   Z-ADD     WFCVNO        SVCVNO
      *
      * GET DESCRIPTIONS FOR PRODUCTION PHASE/SALE TYPE/TYPE OF PAYMENT
      *
     C                   EXSR      $PPDS
     C                   EXSR      $STDS
      *
      * PRINT HEADINGS FOR FIRST TIME THROUGH
      *
     C                   EXCEPT    HRHDR
     C                   EXCEPT    HPHDR
     C                   EXCEPT    HSHDR
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * $TOMDY - FLIP DATE FROM YYMMDD TO MMDDYY
      *---------------------------------------------------------------
      *
     C     $TOMDY        BEGSR
      *
     C                   Z-ADD     WKMON         WKMM
     C                   Z-ADD     WKDAY         WKDD
     C                   Z-ADD     WKYER         WKYY
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $JDEV - SUBROUTINE TO CALL THE EXTERNAL PROGRAM TO VALIDATE JDE
      *         ADDRESS NUMBERS.
      *----------------------------------------------------------------
      *
     C     $JDEV         BEGSR
      *
     C                   MOVEL     NO            JDEFL                          JDE ERRORS
      *
     C                   CALL      'HPJDEV'
     C                   PARM                    MGI                            MSG ID'S
     C                   PARM                    MGD                            MSG DATA FLD
     C                   PARM                    ERROR                          # ERR MSGS
     C                   PARM                    INAJD                          JDE ADDR #
     C                   PARM                    INAT1                          JDE ADDR TYP
     C                   PARM                    JDEFL                          ERROR FLAG
     C                   PARM      *BLANK        OUALPH                         JDE NAME
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * *INZSR - INITIALIZATION SUBROUTINE
      *---------------------------------------------------------------
      *
     C     *INZSR        BEGSR
      *
      * KEY FOR FILE HSL065C -CHECK ADJUSTMENTS
      *
     C     K1L065        KLIST
     C                   KFLD                    WFCHDT
     C                   KFLD                    WFCVNO
     C                   KFLD                    WFCHNO
     C                   KFLD                    WFLNNO
      *
      * INDEX FOR TABLES
     C                   Z-ADD     0             X                 1 0
      *
      *----------------------------------------------------------------
      * J.D. EDWARDS PARMS
      *----------------------------------------------------------------
      *
     C                   MOVE      *BLANK        JDEFL             1            JDE ERR FLAG
     C                   MOVE      *BLANK        INAT1             3            JDE ADDR TYP
     C     *LIKE         DEFINE    CJCVNO        INAJD                          JDE NUMBER
     C                   MOVE      *BLANK        OUALPH           40            JDE NAME
      *
      *----------------------------------------------------------------
      * WORKFIELDS FOR CALCULATIONS AND ACCUMULATIONS
      *----------------------------------------------------------------
      *
     C                   Z-ADD     0             WKF94             9 4
     C                   Z-ADD     0             WKF92             9 2
     C                   Z-ADD     0             WKF154           15 4
     C     *LIKE         DEFINE    WKF92         WKDAAM
     C     *LIKE         DEFINE    WKF92         WKGDAM
     C     *LIKE         DEFINE    WKF92         WKSLAM
      *
      *----------------------------------------------------------------
      * SAVE FIELDS FOR LEVEL BREAK COMPARISONS
      *----------------------------------------------------------------
      *
     C     *LIKE         DEFINE    WFRCDT        SVRCDT
     C     *LIKE         DEFINE    WFFSCD        SVFSCD
     C     *LIKE         DEFINE    WFCVNO        SVCVNO
     C     *LIKE         DEFINE    WFSTCD        SVSTCD
     C     *LIKE         DEFINE    WFTPCD        SVTPCD
     C     *LIKE         DEFINE    WFPPCD        SVPPCD
      *
      *----------------------------------------------------------------
      * WORKFIELDS FOR PRINTING
      *----------------------------------------------------------------
      *
      * PRINT FIELDS FOR HEADINGS
      *
     C     *LIKE         DEFINE    LDPPDS        HPPPDS
     C     *LIKE         DEFINE    LDSTDS        HSSTDS
     C                   MOVE      *BLANK        HSTPDS           15
      *
      * PRINT FIELDS FOR THE DETAIL LINE
      *
     C     *LIKE         DEFINE    WFITRT        DTGRPR
     C     *LIKE         DEFINE    WFITRT        DTSRLS
     C     *LIKE         DEFINE    WFITRT        DTMMRT
     C     *LIKE         DEFINE    WFITRT        DTBMRT
     C     *LIKE         DEFINE    WFITRT        DTGMRT
     C     *LIKE         DEFINE    WFITRT        DTNCRT
     C     *LIKE         DEFINE    WFITRT        DTNLRT
     C     *LIKE         DEFINE    WFITRT        DTPBRT
     C     *LIKE         DEFINE    WFITRT        DTPTRT
     C     *LIKE         DEFINE    WFITRT        DTPYLD
     C                   Z-ADD     0             DTLVWT            4 1
      *
      * PRINT FIELDS FOR TOTAL LINE BY RECEIVED DATE/CUSTOMER
      *
     C                   MOVE      *BLANK        TDCNAM           15
     C     *LIKE         DEFINE    WFITRT        TDBMRT
     C     *LIKE         DEFINE    WFITRT        TDGMRT
     C     *LIKE         DEFINE    WFITRT        TDGRPR
     C     *LIKE         DEFINE    WFITRT        TDBFAM
     C     *LIKE         DEFINE    WFITRT        TDLNAM
     C     *LIKE         DEFINE    WFITRT        TDMMRT
     C     *LIKE         DEFINE    WFITRT        TDNCRT
     C     *LIKE         DEFINE    WFITRT        TDNLRT
     C     *LIKE         DEFINE    WFITRT        TDPBRT
     C     *LIKE         DEFINE    WFITRT        TDPTRT
     C     *LIKE         DEFINE    WFITRT        TDPYLD
     C     *LIKE         DEFINE    WFITRT        TDSRLS
     C     *LIKE         DEFINE    WFITRT        TDITRT
     C                   Z-ADD     0             TDRMDY            6 0
     C                   Z-ADD     0             TDLVWT            4 1
      *
      * PRINT FIELDS FOR FARM SITE TOTAL LINE
      *
     C     *LIKE         DEFINE    WFITRT        TFBMRT
     C     *LIKE         DEFINE    WFITRT        TFGMRT
     C     *LIKE         DEFINE    WFITRT        TFGRPR
     C     *LIKE         DEFINE    WFITRT        TFBFAM
     C     *LIKE         DEFINE    WFITRT        TFLNAM
     C     *LIKE         DEFINE    WFITRT        TFMMRT
     C     *LIKE         DEFINE    WFITRT        TFNCRT
     C     *LIKE         DEFINE    WFITRT        TFNLRT
     C     *LIKE         DEFINE    WFITRT        TFPBRT
     C     *LIKE         DEFINE    WFITRT        TFPTRT
     C     *LIKE         DEFINE    WFITRT        TFPYLD
     C     *LIKE         DEFINE    WFITRT        TFSRLS
     C     *LIKE         DEFINE    WFITRT        TFITRT
     C                   Z-ADD     0             TFLVWT            4 1
      *
      * PRINT FIELDS FOR SALES TYPE/PAYMENT TYPE TOTAL LINE
      *
     C     *LIKE         DEFINE    WFITRT        TTBMRT
     C     *LIKE         DEFINE    WFITRT        TTGMRT
     C     *LIKE         DEFINE    WFITRT        TTGRPR
     C     *LIKE         DEFINE    WFITRT        TTBFAM
     C     *LIKE         DEFINE    WFITRT        TTLNAM
     C     *LIKE         DEFINE    WFITRT        TTMMRT
     C     *LIKE         DEFINE    WFITRT        TTNCRT
     C     *LIKE         DEFINE    WFITRT        TTNLRT
     C     *LIKE         DEFINE    WFITRT        TTPBRT
     C     *LIKE         DEFINE    WFITRT        TTPTRT
     C     *LIKE         DEFINE    WFITRT        TTPYLD
     C     *LIKE         DEFINE    WFITRT        TTSRLS
     C     *LIKE         DEFINE    WFITRT        TTITRT
     C                   Z-ADD     0             TTLVWT            4 1
      *
      *----------------------------------------------------------------
      * SET UP REPORT HEADINGS
      *----------------------------------------------------------------
      *
      * IF USER SELECTED THE 'DETAIL' REPORT SET ON INDICATOR TO
      * CONTROL PRINTING OF ADDITIONAL COLUMN HEADING VALUES.
      *
     C     LDRPFL        IFEQ      'D'
     C                   SETON                                        89
     C                   ENDIF
      *
      * BUSINESS OFFICE
      *
     C     LDFSBO        IFEQ      *BLANK
     C                   MOVEL     'ALL'         LDFSBO
     C                   ENDIF
      *
      * PRODUCTION TYPE
      *
     C     LDPTCD        IFEQ      *BLANK
     C                   MOVEL     'ALL'         LDPTCD
     C                   ENDIF
      *
      * PRODUCTION PHASE
      *
     C     LDPPCD        IFEQ      *BLANK
     C                   MOVEL     'ALL'         LDPPCD
     C                   ENDIF
      *
      * SALES TYPE
     C     LDSTCD        IFEQ      *BLANK
     C                   MOVEL     'ALL'         LDSTCD
     C                   ENDIF
      *
      * ALL PAYMENT TYPES
      *
     C     LDTPCD        IFEQ      *BLANK
     C                   SETON                                        96
     C                   ENDIF
      *
      * ALL FARMS
     C     LDFSCD        IFEQ      0                                                         B001
     C                   SETON                                        81
     C                   ENDIF
      *
      * ALL CUSTOMERS
      *
     C     LDCVNO        IFEQ      0
     C                   SETON                                        95
     C                   ENDIF
      *
      * BREED STOCK
      *
     C     LDBSCD        IFEQ      *BLANK
     C                   MOVEL     'ALL'         LDBSCD
     C                   ENDIF
      *
      * ALL HOG GROUPS OR ONE GROUP
      *
     C     LDHGSN        IFEQ      0
     C                   SETON                                        87
     C                   ENDIF
      *
      * SET UP A FIRST TIME FLAG
      * SET UP A DASHED LINE FOR UNDERLINING THE HEADING
      * RETRIEVE TIME FOR REPORT HEADING
      *
     C                   MOVEL     YES           FIRST             1
     C                   MOVEL     *ALL'-'       DASH            198
     C                   TIME                    RTIME             6 0
      *
     C                   ENDSR
      /EJECT
      *-------------------------------------------------------------
      * REPORT HEADINGS
      *-------------------------------------------------------------
      *
     OQPRINT    E            HRHDR          1 01
     O                       SDPGM               10
     O                                          109 'HOG PRODUCTION SYSTEM'
     O                                          185 'DATE'
     O                       UDATE         Y    195
      *
     O          E            HRHDR          1
     O                       SDUSR               10
     O                                          104 'HOG SALES ACTIVITY'
     O                                          111 'REPORT'
     O                                          185 'TIME'
     O                       RTIME              195 '  :  :  '
      *
     O          E            HRHDR          1
     O                       LDRPDS             102
     O                                          185 'PAGE'
     O                       PAGE          Z    195
      *
      *
     O          E            HRHDR          1
     O                                           22 'BUSINESS OFFICE ......'
     O                       LDFSBO              29
     O                       LDBODS              70
      *
     O          E            HRHDR          1
     O                                           22 'FROM RECEIVE DATE ....'
     O                       LDFMDY        Y     32
      *
     O          E            HRHDR          1
     O                                           22 'TO RECEIVE DATE ......'
     O                       LDTMDY        Y     32
      *
     O          E            HRHDR          1
     O                                           22 'PRODUCTION TYPE ......'
     O                       LDPTCD              29
     O                       LDPTDS              44
      *
     O          E            HRHDR          1
     O                                           22 'PRODUCTION PHASE .....'
     O                       LDPPCD              29
     O                       LDPPDS              64
      *
     O          E            HRHDR          1
     O                                           22 'SALE TYPE ............'
     O                       LDSTCD              29
     O                       LDSTDS              54
      *
     O          E            HRHDR          1
     O                                           22 'TYPE OF PAYMENT ......'
     O               96                          27 'ALL'
     O              N96      LDTPCD              25
     O              N96      LDTPDS              54
      *
     O          E            HRHDR          1
     O                                           22 'FARM SITE ............'
     O               81                          27 'ALL'
     O              N81      LDFSCD        M     30
     O                       LDFSNM              59
      *
      *
     O          E            HRHDR          1
     O                                           22 'BREED STOCK ..........'
     O                       LDBSCD              29
     O                       LDBSDS              54
      *
     O          E            HRHDR          1
     O                                           22 'HOG GROUP/BLDG/ROOM ..'
     O               87                          27 'ALL'
     O              N87      LDHGCD              31
     O              N87      LDBLCD              39
     O              N87                          41 '/'
     O              N87      LDRMCD              47
      *
     O          E            HRHDR          1
     O                                           22 'CUSTOMER .............'
     O               95                          27 'ALL'
     O              N95      LDCVNO        Z     32
     O              N95      LDCNAM              64
      *
      *-------------------------------------------------------------
      * COLUMN HEADINGS
      *-------------------------------------------------------------
      *  *NOTE: THE VALUES CONDITIONED WITH '89' ONLY APPEAR ON THE
      *         DETAIL REPORT.
      *
     O          E            HRHDR          1
     O                                           85 '-----MARKET-----'
     O                                          110 '-----------------------'
     O                                          116 'ACTUAL'
     O                                          139 '-----------------------'
     O                                          155 '-----PREMIUM-----'
     O                                          180 '-------------PERFORMANCE'
     O                                          190 '----------'
     O                                          198 '--AGE--'
     O          E            HRHDR          1
     O                                           76 'IOWA'
     O                                           93 'BASE'
     O                                          120 'GROSS'
     O                                          128 'NET'
     O                                          137 'NET'
     O                                          153 'OVER / UNDER'
     O                                          171 'BACK'
     O                                          190 'AVERAGE'
     O                                          194 'WK'
     O                                          198 'DAY'
     O          E            HRHDR          1
     O                                            5 'FARM'
     O                                           14 'RECEIVE'
     O               89                          23 'CHECK#'
     O               89                          33 'MOVEMENT'
     O               89                          41 'BREED'
     O                                           58 'LIVE'
     O                                           67 'DOA'
     O                                           76 'BULK'
     O                                           84 'MEAT'
     O                                           93 'MEAT'
     O                                          103 'GRADE'
     O                                          112 'SORT'
     O                                          120 'MEAT'
     O                                          129 'CARC'
     O                                          138 'LIVE'
     O                                          151 'MARKET'
     O                                          164 'DRESSING'
     O                                          170 'FAT'
     O                                          180 'PERCENT'
     O                                          188 'LIVE'
     O          E            HRHDR          1
     O                                            5 'SITE'
     O                                           14 'DATE'
     O                                           29 'CUSTOMER'
     O               89                          41 'STOCK'
     O               89                          48 'GROUP'
     O                                           58 'HEAD'
     O                                           67 'HEAD'
     O                                           75 'TOP'
     O                                           85 'PRICE'
     O                                           94 'PRICE'
     O                                          103 'PREMIUM'
     O                                          112 'LOSS'
     O                                          121 'PRICE'
     O                                          130 'PRICE'
     O                                          139 'PRICE'
     O                                          145 'BASE'
     O                                          154 'TOTAL'
     O                                          163 'YIELD'
     O                                          172 'INCHES'
     O                                          180 'LEAN'
     O                                          189 'WEIGHT'
     O                                          198 'FRT NPC'
     O          E            HRHDR          1
     O                       DASH               198
      *
      *-------------------------------------------------------------
      * PHASE HEADING
      *-------------------------------------------------------------
     O          E            HPHDR       1  1
     O                       HPPPDS             120
      *
      *-------------------------------------------------------------
      * SALES TYPE HEADING
      *-------------------------------------------------------------
     O          E            HSHDR       1  2
     O                       HSSTDS             110
     O                       HSTPDS             127
      *
      *--------------------------------------------------------------
      * DETAIL LINE
      *--------------------------------------------------------------
      * **NOTE: THIS LINE ONLY PRINTS ON THE 'DETAIL' VERSON OF THE
      *         REPORT--NOT THE SUMMARY VERSION.
      *
     O          E            DTLINE         1
     O                       WFCHNO        Z     24
     O                       WFMVSN        Z     33
     O                       WFBSCD              41
     O                       WFHGCD              50
     O                       WFLVHD        K     58
     O                       WFDOHD        K     67
     O                       WFITRT        K     76
     O                       DTMMRT        KB    85
     O                       DTBMRT        KB    94
     O                       DTGRPR        KB   103
     O                       DTSRLS        KB   112
     O                       DTGMRT        KB   121
     O                       DTNCRT        KB   130
     O                       DTNLRT        KB   138
     O                       DTPBRT        KB   146
     O                       DTPTRT        KB   155
     O                       DTPYLD        KB   164
     O                       WFBFAM        KB   172
     O                       WFLNPR        KB   181
     O                       DTLVWT        KB   190
     O                       WFAGWK        M    195
     O                       WFAGDY        M    198
      *
      *-------------------------------------------------------------
      * TOTAL LINE FOR THE BREAK ON RECEIVED DATE/CUSTOMER
      *-------------------------------------------------------------
      *
     O          E            TDTOT          1
     O                       SVFSCD        M      6
     O                       TDRMDY        YB    15
     O                       TDCNAM         B    32
     O                       SLHD(1)       K     58
     O                       DOHD(1)       K     67
     O                       TDITRT        KB    76
     O                       TDMMRT        KB    85
     O                       TDBMRT        KB    94
     O                       TDGRPR        KB   103
     O                       TDSRLS        KB   112
     O                       TDGMRT        KB   121
     O                       TDNCRT        KB   130
     O                       TDNLRT        KB   138
     O                       TDPBRT        KB   146
     O                       TDPTRT        KB   155
     O                       TDPYLD        KB   164
     O                       TDBFAM        KB   172
     O                       TDLNAM        KB   181
     O                       TDLVWT        KB   190
     O               72                         194 'YES'
     O              N72                         194 'NO'
     O               71                         198 'YES'
     O              N71                         197 'NO'
      *
      *-------------------------------------------------------------
      * TOTAL LINE FOR FARM SITE
      *-------------------------------------------------------------
      *
     O          E            TFTOT          2
     O                                           10 'TOTAL'
     O                       SVFSCD        M     17
     O                                           21 'SITE'
     O                       SLHD(2)       K     58
     O                       DOHD(2)       K     67
     O                       TFITRT        KB    76
     O                       TFMMRT        KB    85
     O                       TFBMRT        KB    94
     O                       TFGRPR        KB   103
     O                       TFSRLS        KB   112
     O                       TFGMRT        KB   121
     O                       TFNCRT        KB   130
     O                       TFNLRT        KB   138
     O                       TFPBRT        KB   146
     O                       TFPTRT        KB   155
     O                       TFPYLD        KB   164
     O                       TFBFAM        KB   172
     O                       TFLNAM        KB   181
     O                       TFLVWT        KB   190
      *
      *-------------------------------------------------------------
      * TOTAL LINE FOR SALE TYPE/TYPE OF PAYMENT
      *-------------------------------------------------------------
      *
     O          E            TTTOT       1  1
     O                                           12 'TOTAL'
     O                       HSTPDS              28
     O                       HSSTDS              49
     O                       SLHD(3)       K     58
     O                       DOHD(3)       K     67
     O                       TTITRT        KB    76
     O                       TTMMRT        KB    85
     O                       TTBMRT        KB    94
     O                       TTGRPR        KB   103
     O                       TTSRLS        KB   112
     O                       TTGMRT        KB   121
     O                       TTNCRT        KB   130
     O                       TTNLRT        KB   138
     O                       TTPBRT        KB   146
     O                       TTPTRT        KB   155
     O                       TTPYLD        KB   164
     O                       TTBFAM        KB   172
     O                       TTLNAM        KB   181
     O                       TTLVWT        KB   190
      *
      *-------------------------------------------------------------
      * A BLANK LINE USED TO PROPERLY SPACE WHEN PRINTING THE
      * DETAIL VERSION VERSUS THE SUMMARY VERSION OF THE REPORT
      *-------------------------------------------------------------
      *
     O          E            BLANK       1
      *
      *-------------------------------------------------------------
      * NO DATA MESSAGE LINE
      *-------------------------------------------------------------
      *
     O          E            NODATA      2
     O                                           19 'NO DATA MEETS YOUR'
     O                                           38 'SELECTION CRITERIA'
