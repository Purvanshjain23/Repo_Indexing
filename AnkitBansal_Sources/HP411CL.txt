/***************************************************************** */
/*  PROGRAM NUMBER --> HP411CL                                     */
/*  PROGRAM NAME ----> GROUP SOURCE REPORT                         */
/*  PROGRAMMER ------> LEANNE FEDOR                                */
/*  DATE ------------> 12/16/99                                    */
/*                                                                 */
/*  FUNCTION --------> THIS CL CALLS THE PROGRAMS TO CREATE/PRINT  */
/*                     THE REPORT.                                 */
/*                                                                 */
/***************************************************************** */

             PGM

             DCL        VAR(&QDATA) TYPE(*CHAR) LEN(298)

             DCL        VAR(&LDFSBO) TYPE(*CHAR) LEN(5)
             DCL        VAR(&LDOFCYMD) TYPE(*DEC) LEN(8)
             DCL        VAR(&LDOTCYMD) TYPE(*DEC) LEN(8)
             DCL        VAR(&LDCFCYMD) TYPE(*DEC) LEN(8)
             DCL        VAR(&LDCTCYMD) TYPE(*DEC) LEN(8)
             DCL        VAR(&LDPPCD) TYPE(*CHAR) LEN(5)
             DCL        VAR(&LDFSCD) TYPE(*DEC) LEN(5)
             DCL        VAR(&LDPTCD) TYPE(*CHAR) LEN(5)
             DCL        VAR(&LDHGSN) TYPE(*DEC) LEN(7)
             DCL        VAR(&LDRPFL) TYPE(*CHAR) LEN(1)
             DCL        VAR(&LDGTCD) TYPE(*CHAR) LEN(1)

             DCL        VAR(&HOLD) TYPE(*CHAR) LEN(10)
             DCL        VAR(&COPYS) TYPE(*DEC) LEN(2)
             DCL        VAR(&OUTQ) TYPE(*CHAR) LEN(10)

             CHGVAR     VAR(&LDOFCYMD) VALUE(%SST(*LDA 6 8))
             CHGVAR     VAR(&LDOTCYMD) VALUE(%SST(*LDA 14 8))
             CHGVAR     VAR(&LDCFCYMD) VALUE(%SST(*LDA 32 8))
             CHGVAR     VAR(&LDCTCYMD) VALUE(%SST(*LDA 40 8))

             CHGVAR     VAR(&LDFSBO) VALUE(%SST(*LDA 1 5))
             CHGVAR     VAR(&LDPPCD) VALUE(%SST(*LDA 22 5))
             CHGVAR     VAR(&LDFSCD) VALUE(%SST(*LDA 27 5))
             CHGVAR     VAR(&LDPTCD) VALUE(%SST(*LDA 48 5))
             CHGVAR     VAR(&LDHGSN) VALUE(%SST(*LDA 53 7))
             CHGVAR     VAR(&LDRPFL) VALUE(%SST(*LDA 174 1))
             CHGVAR     VAR(&LDGTCD) VALUE(%SST(*LDA 224 1))

/*********************************************************************/
/* SETUP QUERY FOR HOG GROUPS                                        */
/*********************************************************************/

/* ALWAYS OMIT VOIDED GROUPS                                         */

             CHGVAR     VAR(%SST(&QDATA 1 15)) VALUE('HGGSCD *NE "VD"')

/* ALWAYS OMIT BGF GROUPS                                            */

             CHGVAR     VAR(%SST(&QDATA 17 23)) VALUE('*AND HGPPCD +
                          *NE "BGF  "')
/*  IF HOG GROUP                                                    */

             IF         COND(&LDHGSN *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 41 15)) VALUE('*AND HGHGSN +
                          *EQ')
             CHGVAR     VAR(%SST(&QDATA 57 7)) VALUE(&LDHGSN)
             ENDDO

/*  IF BUSINESS OFFICE                                               */

             IF         COND(&LDFSBO *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 65 17)) VALUE('*AND HGFSBO +
                          *EQ "')
             CHGVAR     VAR(%SST(&QDATA 82 5)) VALUE(&LDFSBO)
             CHGVAR     VAR(%SST(&QDATA 87 1)) VALUE('"')
             ENDDO

/*  IF PRODUCTION TYPE                                               */

             IF         COND(&LDPTCD *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 89 17)) VALUE('*AND HGPTCD +
                          *EQ "')
             CHGVAR     VAR(%SST(&QDATA 106 5)) VALUE(&LDPTCD)
             CHGVAR     VAR(%SST(&QDATA 111 1)) VALUE('"')
             ENDDO

/*  IF PRODUCTION PHASE                                              */

             IF         COND(&LDPPCD *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 113 17)) VALUE('*AND HGPPCD +
                          *EQ "')
             CHGVAR     VAR(%SST(&QDATA 130 5)) VALUE(&LDPPCD)
             CHGVAR     VAR(%SST(&QDATA 135 1)) VALUE('"')
             ENDDO

/*  IF FARM SITE                                                     */

             IF         COND(&LDFSCD *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 137 16)) VALUE('*AND HGFSCD +
                          *EQ ')
             CHGVAR     VAR(%SST(&QDATA 153 5)) VALUE(&LDFSCD)
             ENDDO


/*  IF GROUP TYPE                                                    */

             IF         COND(&LDGTCD *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 159 17)) VALUE('*AND HGGTCD +
                          *EQ "')
             CHGVAR     VAR(%SST(&QDATA 176 1)) VALUE(&LDGTCD)
             CHGVAR     VAR(%SST(&QDATA 177 1)) VALUE('"')
             ENDDO


/* CLOSED DATE RANGE                                                      */
/* IF THE USER DID NOT SELECT ONE SPECIFIC GROUP AND HAS A CLOSED DATE:   */
/*     A) SELECT ONLY GROUPS WITH A CLOSED DATE IN THE SELECTED RANGE     */


             IF         COND(&LDHGSN *EQ 0 *AND &LDCFCYMD *NE 0) +
                          THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 179 15)) VALUE('*AND HGCLDT +
                          *GE')
             CHGVAR     VAR(%SST(&QDATA 195 8)) VALUE(&LDCFCYMD)
             CHGVAR     VAR(%SST(&QDATA 204 15)) VALUE('*AND HGCLDT +
                          *LE')
             CHGVAR     VAR(%SST(&QDATA 220 8)) VALUE(&LDCTCYMD)
             ENDDO


/* OPEN DATE RANGE   */
/* IF THE USER DID NOT SELECT ONE SPECIFIC GROUP AND HAS NO CLOSED DATE:  */
/*     A) SELECT ONLY GROUPS WITH AN OPEN DATE IN THE SELECTED RANGE, OR  */
/*     B) SELECT ONLY CURRENTLY OPEN GROUPS IF NO OPEN DATE RANGE         */

             IF         COND(&LDHGSN *EQ 0 *AND &LDCFCYMD *EQ 0) +
                          THEN(DO)

/* A) CONDITIONALLY SELECT GROUPS WITH AN OPEN DATE IN THE SELECTED RANGE */

             IF         COND(&LDOFCYMD *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 229 15)) VALUE('*AND HGOPDT +
                          *GE')
             CHGVAR     VAR(%SST(&QDATA 245 8)) VALUE(&LDOFCYMD)
             CHGVAR     VAR(%SST(&QDATA 254 15)) VALUE('*AND HGOPDT +
                          *LE')
             CHGVAR     VAR(%SST(&QDATA 270 8)) VALUE(&LDOTCYMD)
             ENDDO


/* B) CONDITIONALLY SELECT ALL CURRENTLY OPEN GROUPS */

             IF         COND(&LDOFCYMD *EQ 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 279 17)) VALUE('*AND HGGSCD +
                          *EQ "')
             CHGVAR     VAR(%SST(&QDATA 296 2)) VALUE('OP')
             CHGVAR     VAR(%SST(&QDATA 298 1)) VALUE('"')
             ENDDO
             ENDDO



/*-------------------------------------------------------------------*/
/* RUN QUERY OVER THE HOG GROUP FILE                                 */
/*-------------------------------------------------------------------*/

             OVRDBF     FILE(HSP034) SHARE(*YES)
             OPNQRYF    FILE((HSP034)) QRYSLT(&QDATA) KEYFLD(*FILE) +
                          SEQONLY(*YES)

/*-------------------------------------------------------------------*/
/* CREATE THE WORKFILES                                              */
/*-------------------------------------------------------------------*/

             CRTDUPOBJL OBJ(HSP315) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)

             CRTDUPOBJL OBJ(HSP316) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)

             CRTDUPOBJL OBJ(HSP317) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)

             CRTDUPOBJL OBJ(HSP318) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)
             CRTDUPOBJL OBJ(HSL318A) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)

             CRTDUPOBJL OBJ(HSP319) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)
             CRTDUPOBJL OBJ(HSL319A) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)


/*-------------------------------------------------------------------*/
/* CALL THE PROGRAM TO BUILD THE WORKFILES                           */
/*-------------------------------------------------------------------*/

             CALL       PGM(HP311)

/*******************************************************************/
/* SET UP PRINT PARMS                                              */
/*******************************************************************/

 HOLD:       IF         COND(%SST(*LDA 411 1) = 'Y') THEN(CHGVAR +
                          VAR(&HOLD) VALUE(*YES))
             ELSE       CMD(CHGVAR VAR(&HOLD) VALUE(*NO))

 COPYS:      CHGVAR     VAR(&COPYS) VALUE(%SST(*LDA 412 1))
             IF         COND(&COPYS = 0) THEN(CHGVAR VAR(&COPYS) +
                          VALUE(1))

 OUTQ:       CHGVAR     VAR(&OUTQ) VALUE(%SST(*LDA 413 10))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(%SST(*LDA 401 10)))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(*JOB))

/*------------------------------------------------------------------*/
/* OVERRIDE PRINT FILE                                              */
/*------------------------------------------------------------------*/


             OVRPRTF    FILE(PRINT198) OUTQ(&OUTQ) COPIES(&COPYS) +
                          HOLD(&HOLD)


/*-------------------------------------------------------------------*/
/* CALL PRINT PROGRAM                                                */
/*-------------------------------------------------------------------*/

             CALL       PGM(HP611)

             CLOF       OPNID(HSP034)
             DLTOVR     FILE(*ALL)

             ENDPGM

