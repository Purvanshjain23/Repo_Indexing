/*********************************************************************/
/* PROGRAM    : PUQPUPC                                             */
/* TITLE      : EXECUTE THE ESNDMAIL CMD TO EMAIL 1 LD/EMAIL BOLS-SBD ONLY  */
/* DATE       : 04/15/2020                                           */
/* Description: EXECUTE THE ESNDNMAIL COMMAND, ADD AND REMOVE THE    */
/*                ESEND LIBRARY, MONITOR FOR ERRORS                  */
/*                                                                   */
/*  Parameters:                                                      */
/*  Usage   Field       Field Description                            */
/*  -----   ---------   ------------------------------------------   */
/*    I     &EADDRESS   EMAIL ADDRESS TO SEND TO                     */
/*    I     &ESUBJECT   EMAIL SUBJECT                                */
/*    I     &EMESSAGE   MESSAGE TO SEND                              */
/*********************************************************************/
/* Modifications:                                                    */
/*-------------------------------------------------------------------*/
/*   Date     Pgmr     Description                                   */
/* -------- ---------- --------------------------------------------  */
/* RMC 4/15/20 R16455- COPIED FROM HPB PGM TO WORK FOR SBD FOR CUSTOMERS  */
/*                     THAT HAVE THE CUST DIST ENTRY SH-BOL           */
/*                     USES A FRONT OVL FOR THE BOL, BACK OVERLAY   */
/*                     FOR TERMS AND ALSO SEND THE MANIFEST DTL.     */
/*           FOR NOW- PRINT TERMS PAGE FROM QTXTSRC.BOLTERM SOURCEFILE. */
/*                     WILL PRINT ONLY STOPS THAT HAVE THE CUSTS ORDERS */
/*                     PUQRPFR REPLACES PDC2PFR                     */
/* OVERRIDE FROM EMAIL TO NOREPLY#SEABOARDFOODS.COM                 */
/*********************************************************************/
 PUQPUPC:    PGM        PARM(&RETURN &EADDRESS &ESUBJECT &EMESSAGE +
                    &JOBNAME &BLOVER &BKOVER &LOADA &CUSTA)
/*-------------------------------------------------------------------*/
/* Declare Variables                                                 */
/*-------------------------------------------------------------------*/
             DCL        VAR(&RETURN)   TYPE(*CHAR) LEN(7)
             DCL        VAR(&EADDRESS) TYPE(*CHAR) LEN(50)
             DCL        VAR(&ESUBJECT) TYPE(*CHAR) LEN(60)
             DCL        VAR(&EMESSAGE) TYPE(*CHAR) LEN(256)
             DCL        VAR(&JOBNAME) TYPE(*CHAR) LEN(10)
             DCL        VAR(&BLOVER) TYPE(*CHAR) LEN(8)  /* FX OVERLAY NME */
             DCL        VAR(&BKOVER) TYPE(*CHAR) LEN(8)  /* BACKOVERLAY NME */
             DCL        VAR(&LOADA)    TYPE(*CHAR) LEN(7)
             DCL        VAR(&LOAD) TYPE(*DEC) LEN(7 0)
             DCL        VAR(&CUSTA)    TYPE(*CHAR) LEN(7)
             DCL        VAR(&CUST) TYPE(*DEC) LEN(7 0)


/* ERROR MESSAGE FIELDS                                                       */
             DCL        VAR(&MSG)   TYPE(*CHAR) LEN(512)
             DCL        VAR(&MSGID) TYPE(*CHAR) LEN(7)
             DCL        VAR(&MSGL)  TYPE(*DEC) LEN(5 0)
             DCL        VAR(&MSG2)  TYPE(*CHAR) LEN(512)

             CHGVAR     VAR(&LOAD) VALUE(&LOADA)
             CHGVAR     VAR(&CUST) VALUE(&CUSTA)

/*-------------------------------------------------------------------*/
/* EXECUTE OVERRIDE PRINTFILES                                       */
/*-------------------------------------------------------------------*/
/*           OVRPRTF    FILE(PUQRPFR$) PAGESIZE(88 136) LPI(8) +
                          CPI(18) OVRFLW(88) DUPLEX(*YES) +
                          FRONTOVL(&BLOVER .3 .3) BACKOVL(&BKOVER 0 +
                          0 *CONSTANT) HOLD(*YES) USRDTA(&JOBNAME)   */
             OVRPRTF    FILE(PUQRPFR$) PAGESIZE(88 85) LPI(8) +
          FRONTOVL(&BLOVER)  CPI(10) OVRFLW(51) HOLD(*YES) +
                          USRDTA(&JOBNAME)
 /* SD*/     OVRPRTF    FILE(OMD0PFR$) PAGESIZE(88 136) LPI(8) +
                          CPI(18) OVRFLW(88)                       +
                          HOLD(*YES) USRDTA(&JOBNAME)
             OVRPRTF    FILE(PRINT132) HOLD(*YES)
/*-------------------------------------------------------------------*/
/* PRINT THE BOLS AND MANIFEST SALES DETAIL SD  AND TERMS        */
/*-------------------------------------------------------------------*/
             OVRDBF     FILE(QTXTSRC) TOFILE(QTXTSRC) MBR(BOLTERM)
             CALL       PGM(PUQQXFR) PARM('' &LOAD  &CUST &JOBNAME)
/*-------------------------------------------------------------------*/
/* EXECUTE THE COMMAND AND MONITOR FOR ERRORS                        */
/*-------------------------------------------------------------------*/
             ADDLIBLE   LIB(ESEND)
             MONMSG     MSGID(CPF2103 CPF9999)
/*                   */
             CRTDTAARA  DTAARA(QTEMP/ESNDADTA) TYPE(*CHAR) LEN(256)
             MONMSG     MSGID(CPF1023 CPF9999)
  CHGDTAARA  DTAARA(QTEMP/ESNDADTA) +
               VALUE(NOREPLY@SEABOARDFOODS.COM)

 FGPROD:     ESNDMAIL   RECIPIENT(&EADDRESS) SUBJECT(&ESUBJECT) +
                          ATTLIST((* *PDF *N PUQRPFR$ * *ALL *ALL) +
                          (MANIFEST *PDF *N OMD0PFR$ * *ALL *ALL) +
                          (TERMS *PDF *N PRINT132 * *LAST *ALL))


 DELETE:     DLTSPLF    FILE(*SELECT) SELECT(*CURRENT *ALL *ALL +
                          &JOBNAME)
 DELET2:     DLTSPLF    FILE(PRINT132) SELECT(*CURRENT *ALL *ALL)
             MONMSG     MSGID(CPF3309)

             GOTO       CMDLBL(ENDPGM)

/*----------------------RECEIVE MESSAGES----------------------------*/

 MSGTRAP:    RCVMSG     MSGTYPE(*EXCP) MSGDTA(&MSG) MSGDTALEN(&MSGL) +
                          MSGID(&MSGID)
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(ENDPGM))

             SNDPGMMSG  MSGID(&MSGID) MSGF(QCPFMSG) MSGDTA(%SST(&MSG +
                          1 &MSGL)) MSGTYPE(*ESCAPE)
             MONMSG     MSGID(CPF0000)

 ENDPGM:     RMVLIBLE   LIB(ESEND)
             MONMSG     MSGID(CPF0000)

             ENDPGM
