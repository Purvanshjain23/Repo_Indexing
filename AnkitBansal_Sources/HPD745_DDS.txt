     A*%%TS  SD  20030106  161942  ISLRAMS     REL-V5R1M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION
     A*  DISPLAY FILE:  HPD745
     A*  TITLE:         VIEW SCHEDULED SALE MOVEMENT
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  DATE:          09/21/95
     A*
     A*----------------------------------------------------------------
     A*  INDICATORS:
     A*  75  SCHEDULED KILL DATE IS POPULATED, SO DISPLAY TEXT
     A*  90  THE SALE THE USER HAS SELECTED FROM THE WORK WITH HAS
     A*      BEEN DELETED BY ANOTHER USER SINCE SELECTED
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A N90                                  CF05(05 'REFRESH')
     A                                      CA12(12 'PREVIOUS')
     A*---------------------------------------------------------------
     A* SUBFILE FORMAT 1
     A*---------------------------------------------------------------
     A*
     A          R HP745S1                   SFL
     A*%%TS  SD  19991109  145522  ISLRAMS     REL-V4R2M0  5769-PW1
     A  85                                  SFLNXTCHG
     A            HDRRN1         4Y 0H
     A            DFBLCD    R        O 17  2REFFLD(@@BLCD)
     A            DFRMCD    R        O 17  9REFFLD(@@RMCD)
     A            DFHGCD    R        O 17 16REFFLD(HGHGCD)
     A            DFSCHD    R        O 17 27REFFLD(SGSCHD)
     A                                      EDTCDE(K)
     A            DFGSCD    R        O 17 39REFFLD(GSGSCD)
     A*
     A*----------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT  1
     A*----------------------------------------------------------------
     A*
     A          R HP745C1                   SFLCTL(HP745S1)
     A*%%TS  SD  20030106  161942  ISLRAMS     REL-V5R1M0  5722-WDS
     A                                      SFLSIZ(0100)
     A                                      SFLPAG(0005)
     A                                      SFLCSRRRN(&HDRR1)
     A                                      OVERLAY
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A                                      SETOF(75)
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            HDRR1          5S 0H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 26'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  2 23'VIEW SCHEDULED SALE MOVEMENT'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  4  2'Movement number ......:'
     A            C1MVSN    R        O  4 27REFFLD(@@MVSN)
     A                                      EDTCDE(Z)
     A                                  5  2'Sale type ............:'
     A            C1STCD    R        O  5 27REFFLD(STSTCD)
     A                                  6  2'Load type ............:'
     A            C1LTCD    R        O  6 27REFFLD(HSRREF/@@LTCD *LIBL/HSPREF)
     A            C1LTDS    R        O  6 37REFFLD(HSRREF/@@LTDS *LIBL/HSPREF)
     A                                  7  2'Scheduled ship date ..:'
     A            C1MDY     R     S  O  7 27REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  ')
     A  75                              8  2'Scheduled kill date ..:'
     A            C1SCKLMDY R     S  O  8 27REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  ')
     A                                  9  2'Customer number ......:'
     A            C1CVNO    R        O  9 27REFFLD(SHCVNO)
     A                                      EDTCDE(M)
     A            C1CNAM    R        O  9 37REFFLD(@@DS30)
     A                                 10  2'Farm .................:'
     A            C1FSCD    R        O 10 27REFFLD(FSFSCD)
     A                                      EDTCDE(M)
     A            C1FSNM    R        O 10 37REFFLD(FSFSNM)
     A                                 11  2'Trucker ..............:'
     A            C1TICD    R        O 11 27REFFLD(SHTICD)
     A            C1TINM    R        O 11 37REFFLD(TITINM HSPREF)
     A                                 12  2'Total scheduled head .:'
     A            C1SCHD    R        O 12 27REFFLD(SGSCHD)
     A                                      EDTCDE(K)
     A                                 14 25'Scheduled'
     A                                      DSPATR(HI)
     A                                 14 37'Group'
     A                                      DSPATR(HI)
     A                                 15  2'Bldg'
     A                                      DSPATR(HI)
     A                                 15  9'Room'
     A                                      DSPATR(HI)
     A                                 15 16'Group'
     A                                      DSPATR(HI)
     A                                 15 28'Head'
     A                                      DSPATR(HI)
     A                                 15 37'Status'
     A                                      DSPATR(HI)
     A*----------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*----------------------------------------------------------------
     A          R HP745K1
     A*%%TS  SD  19950622  110238  ISLRAMS     REL-V3R0M5  5763-PW1
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A N90                             23 11'F5=Refresh'
     A                                 23 23'F12=Previous'
      *----------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *----------------------------------------------------------------
     A          R HP745ES                   SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
     A*----------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*----------------------------------------------------------------
     A*
     A          R HP745EC                   SFLCTL(HP745ES)
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
