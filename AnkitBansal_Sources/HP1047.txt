      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Hog Production System
      * PROGRAM:     HP1047
      * TITLE:       Revise Expense Code
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     06/17/03
      *
      *
      * FUNCTION: Uses an edit record format to add/change/delete expense code records from the
      *           master file.
      *
      *           This program is called from HP4047 - Work with Expense Codes.
      *
      *           The field 'mode' contains the value passed into
      *           this program from the calling program.  The field
      *           'd1mode' is the screen field that displays the
      *           value of 'mode'.  Mode will be CREATE, REVISE or
      *           DELETE.           Once in the program, mode and
      *           d1mode will never change.  However, the action
      *           being performed can change depending on the function
      *           keys the user takes.
      *
      *           The user may delete an existing record by taking
      *           F11=DELETE from this program or option 4=DELETE
      *           from the WORK WITH. If the user is deleting with
      *           4=DELETE, no panel will be displayed from this program
      *           unless there is an error on the attempted deletion.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 12/30/03  LeAnne Fedor
      *           Added another parm to the call to HP559-Select Report Code.
      *           The new parm is the 'user-controlled flag' field.
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fhpd1047   cf   e             workstn
     F                                     infds(iofeed)
      *
      *
     Fhsp006    if   e           k disk
      *    Report code
      *
      *
     Fhsl048j   if   e           k disk
      *    Expense ticket
      *
      *
     Fhsp050    if   e           k disk
      *    Expense class
      *
      *
     Fhsl051a   if   e           k disk
      *    Expense subclass
      *
      *
     Fhsp052    uf a e           k disk
      *    Expense codes
      *
      /eject
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D create          c                   'CREATE'
     D revise          c                   'REVISE'
     D delete          c                   'DELETE'
     D view            c                   'VIEW  '
     D select          c                   'SELECT'
     D yes             c                   'Y'
     D no              c                   'N'
     D set1            c                   'SET1  '
     D edit1           c                   'EDIT1 '
     D scrn1           c                   'SCRN1 '
     D exit            c                   'EXIT  '
     D update          c                   'UPDATE'
      *
      *
      ****************************************************************
      * ARRAYS AND TABLES
      ****************************************************************
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Standard fields
      *
     D mode            s              6
     D pgm             s             10
     D msgfl           s             10
     D routne          s              6
     D rtncd           s              2
     D action          s                   like(mode)
     D maxmsg          s              2  0  inz(20)
      *
      *
      * Parms
      *
     D xxrpcd          s                   like(rprpcd)
     D xxcacd          s                   like(rpcacd)
     D xxrsds          s                   like(rprsds)
     D xxucfl          s                   like(rpucfl)
      *
     D xxeccd          s                   like(ececcd)
     D xxecds          s                   like(ececds)
      *
     D xxescd          s                   like(esescd)
     D xxesds          s                   like(esesds)
      *
      *
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * standard error message handling data structures
      *---------------------------------------------------------------
      *
      * For error message handling, a packed index (3) is required.
      * For program readability, define a corresponding error count
      * field called 'error'
      *
     D                 ds                  inz
     D  error                         2p 0
     D   e                            2p 0 overlay(error)
      *
      * This data structure supplies the name of the message file to
      * the message handling CL program.  The field name MSGFIL must be
      * constant. The value in quotes is the name of the specific
      * message file containing the user defined messages.
      *
     D                 ds                  inz
     D  msgfil                       10    inz('HSMSGF    ')
      *
      *---------------------------------------------------------------
      *  Standard message data structures
      *---------------------------------------------------------------
      *
      * The following 3 data structures are used to speed message
      * handling since it is faster to clear data structures than
      * arrays.  Each is associated with a standard message array.
      *
     D mgi             ds                  inz
     D  mgid                          7    dim(20)
      *
     D mgd             ds                  inz
     D  mgdt                         50    dim(20)
      *
     D mgwk            ds                  inz
     D  mgw                           1    dim(50)
      *
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *---------------------------------------------------------------
      * Standard workstation information data structure
      *---------------------------------------------------------------
      *    externally defined as UTWSFR (record format: UTIDFRR)
     D iofeed        e ds                  extname(utwsfr)
      *
      *
      *---------------------------------------------------------------
      * Standard database file information data structure
      *---------------------------------------------------------------
      *    externally defined as UTDBGR (record format: FDBCKD)
     D dbfeed        e ds                  extname(utdbfr)
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * MAINLINE
      ****************************************************************
      *
     C                   eval      routne = set1
      *
     C     *inlr         doweq     *off                                         Main do loop
     C     routne        caseq     set1          $set1
     C     routne        caseq     scrn1         $scrn1
     C     routne        caseq     edit1         $edit1
     C     routne        caseq     update        $upd1
     C     routne        caseq     exit          $exit
     C                   endcs
     C                   enddo                                                  Main do loop
      /EJECT
      *----------------------------------------------------------------
      * Set environment for screen 1
      *----------------------------------------------------------------
      *
     C     $set1         begsr
      *
      * Clear fields on display panel if creating
      *
     C                   select
     C                   when      mode = create                                WH create
     C                   move      *blank        d1excd
     C                   move      *blank        d1exds
     C                   z-add     0             d1exseq
     C                   move      *blank        d1rpcd
     C                   move      *blank        d1rsds
     C                   move      *blank        d1eccd
     C                   move      *blank        d1ecds
     C                   move      *blank        d1escd
     C                   move      *blank        d1esds
      *
      * If incoming mode is REVISE or DELETE, then retrieve the database
      * record.  Do not lock the record.  Typically, in DELETE mode, these
      * values will not be displayed. However, if there is an error on the
      * delete action, the panel will be displayed with messages.
      *
     C                   when      mode = revise or mode = delete               WH revise/delete
     C     d1excd        chain(n)  hsp052                             92
     C                   if        *in92 = *off                                 If found
     C                   move      exexds        d1exds
     C                   z-add     exexseq       d1exseq
     C                   move      exrpcd        d1rpcd
     C                   move      execcd        d1eccd
     C                   move      exescd        d1escd
      *
      * Retrieve the Report Code Short Description
      *
     C     d1rpcd        chain     hsp006                             92
     C                   if        *in92 = *off
     C                   move      rprsds        d1rsds
     C                   endif
      *
      * Retrieve the Expense Class Description
      *
     C     d1eccd        chain     hsp050                             92
     C                   if        *in92 = *off
     C                   move      ececds        d1ecds
     C                   endif
      *
      * Retrieve the Expense Subclass Description
      *
     C     key01         chain     hsl051a                            92
     C                   if        *in92 = *off
     C                   move      esesds        d1esds
     C                   endif
     C                   else
      *
      * Expense Code record was not found
      *
     C                   seton                                        62
     C                   if        error < maxmsg
     C                   add       1             error
     C                   eval      mgid(e) = 'HS09008'
     C                   endif
      *
     C                   endif                                                  If found
     C                   endsl
      *
      * If the user is deleting and there were no errors when retrieving the
      * record, go on to the update routine without displaying the screen.
      * Otherwise, display the screen.
      *
     C                   if        error = 0 and action = delete                If no error
     C                   eval      routne = update
     C                   else
     C                   eval      routne = scrn1
     C                   endif                                                  If no error
      *
     C                   endsr
      /EJECT
      *----------------------------------------------------------------
      * Perform operator I/O for screen 1
      *----------------------------------------------------------------
      *
     C     $scrn1        begsr
      *
      * Write the error messages from the error arrays to the
      * error message subfile
      *
     C                   exsr      $wrmsg
      *
      *
      * Set indicators to control input/output fields and available
      * function keys.
      *
     c                   select
     C                   when      mode = revise
     C                   seton                                        60
      *
     C                   when      mode = create
     C                   seton                                        61
      *
     C                   when      mode = delete
     C                   seton                                        62
     c                   endsl
      *
      * Write screen 1 to CRT
      *
     C                   write     hp1047k1
     C                   write     hp1047ec
     C                   exfmt     hp1047r1
      *
      * Clear messages
      *
     C                   exsr      $clmsg
      *
      * Reset action to be equal to the incoming mode.  This is required
      * in case the user took F11-Delete and got an error on the
      * deletion and now needs to be able to change the record and take
      * an action other than delete
      *
     C                   eval      action = mode
      *
      * Get user's input and set routine
      *
     C                   select
      *
     C                   when      *in03 = *on                                  F3-exit
     C                   eval      rtncd = '03'
     C                   eval      routne = exit
      *
     C                   when      *in04 = *on                                  F4=prompt
     C                   exsr      $f4s1
      *
     C                   when      *in05 = *on                                  F5-refresh
     C                   eval      routne = set1
      *
     C                   when      *in11 = *on or mode = delete                 F11-delete
     C                   eval      action = delete
     C                   eval      routne = update
     C                   other
      *
      * User has pressed enter
      *
     C                   eval      routne = edit1
     C                   endsl
      *
     C                   endsr
      /EJECT
      *----------------------------------------------------------------
      * Edit input fields for screen 1
      *----------------------------------------------------------------
      *
     C     $edit1        begsr
      *
      * *IN62 will be on if the record the user is trying to access has
      * been deleted by another user.  So, only perform the edits for the
      * fields on the screen if the indicator is off
      *
     C                   if        *in62 = *off                                 If here
      *
      * If creating a new record, validate:
      *   1) expense code
      *
     C                   if        action = create                              If create
     C                   exsr      $excd
     C                   endif                                                  If create
      *
      * Always validate:
      *   1) expense code description
      *   2) sequence
      *   3) expense class
      *   4) expense subclass
      *   5) report code
      *
     C                   exsr      $exds
     C                   exsr      $exseq
     C                   exsr      $eccd
     C                   exsr      $escd
     C                   exsr      $rpcd
     C                   endif                                                  If here
      *
      * If there are no errors, attempt the write/update; otherwise,
      * redisplay the screen.  If a record has been deleted out from
      * under the user (*IN62 is ON), then the error message will be
      * issued in the update subroutine.  So, always perform this code.
      *
     C                   if        error = 0
     C                   exsr      $upd1
     C                   else
     C                   eval      routne = scrn1
     C                   endif
      *
     C                   endsr
      /EJECT
      *----------------------------------------------------------------
      * Validate expense code
      *----------------------------------------------------------------
      *
      * Validations:
      *  1) required
      *  2) cannot already exist in the master file
      *
      *
     C     $excd         begsr
      *
      * Eliminate leading spaces
      *
     C                   eval      d1excd = %triml(d1excd)
      *
     C                   select
     C                   when      d1excd = *blank
     C                   seton                                        35
     C                   if        error < maxmsg
     C                   add       1             error
     C                   eval      mgid(e) = 'HS09001'
     C                   endif
     C                   other
      *
     C     d1excd        chain(n)  hsp052                             92
     C                   if        *in92 = *off                                 If found
     C                   seton                                        35
     C                   if        error < maxmsg
     C                   add       1             error
     C                   eval      mgid(e) = 'HS00300'
     C                   clear                   mgw
     C                   movea     d1excd        mgw(1)
     C                   movea     mgw           mgdt(e)
     C                   endif
     C                   endif                                                  If found
     C                   endsl
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Validate Expense Code Description
      *---------------------------------------------------------------
      *
     C     $exds         begsr
      *
     C                   if        d1exds = *blank
     C                   seton                                        34
     C                   if        error < maxmsg
     C                   add       1             error
     C                   eval      mgid(e) = 'HS09001'
     C                   endif
     C                   endif
     C
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Validate Report Code
      *---------------------------------------------------------------
      *
      * Validations:
      *  1) required
      *  2) must be a valid report code
      *
     C     $rpcd         begsr
      *
     C                   move      *blank        d1rsds
      *
     C                   select
     C                   when      d1rpcd = *blank
     C                   seton                                        37
     C                   if        error < maxmsg
     C                   add       1             error
     C                   eval      mgid(e) = 'HS09001'
     C                   endif
     C                   other
      *
     C     d1rpcd        chain     hsp006                             92
     C                   if        *in92 = *off                                 If hit
     C                   move      rprsds        d1rsds
     C                   else
     C                   seton                                        37
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS00450'     mgid(e)
     C                   clear                   mgw
     C                   movea     d1rpcd        mgw(1)
     C                   movea     mgw           mgdt(e)
     C                   endif
     C                   endif                                                  If hit
      *
     C                   endsl
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Validate Expense Class Code
      *---------------------------------------------------------------
      *
      * Validations:
      *  1) required
      *  2) must be a valid code
      *
     C     $eccd         begsr
      *
     C                   move      *blank        d1ecds
      *
     C                   select
     C                   when      d1eccd = *blank
     C                   seton                                        36
     C                   if        error < maxmsg
     C                   add       1             error
     C                   eval      mgid(e) = 'HS09001'
     C                   endif
     C                   other
      *
     C     d1eccd        chain     hsp050                             92
     C                   if        *in92 = *off                                 If hit
     C                   move      ececds        d1ecds
     C                   else
     C                   seton                                        36
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS00281'     mgid(e)
     C                   clear                   mgw
     C                   movea     d1eccd        mgw(1)
     C                   movea     mgw           mgdt(e)
     C                   endif
     C                   endif                                                  If hit
      *
     C                   endsl
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Validate Expense Subclass Code
      *---------------------------------------------------------------
      *
      * Validations:
      *  1) required
      *  2) must be a valid expense subclass in the Subclass file
      *  2) must be associated with an Expense Class in the Subclass file
      *
     C     $escd         begsr
      *
     C                   move      *blank        d1esds
      *
     C                   select
     C                   when      d1escd = *blank
     C                   seton                                        38
     C                   if        error < maxmsg
     C                   add       1             error
     C                   eval      mgid(e) = 'HS09001'
     C                   endif
     C                   other
      *
     C     d1escd        chain     hsl051a                            92
     C                   if        *in92 = *off                                 If hit
     C                   move      esesds        d1esds
     C                   else
     C                   seton                                        38
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS00289'     mgid(e)
     C                   clear                   mgw
     C                   movea     d1escd        mgw(1)
     C                   movea     mgw           mgdt(e)
     C                   endif
     C                   endif                                                  If hit
      *
     C     key01         chain     hsl051a                            92
     C                   if        *in92 = *off                                 If has class
     C                   move      esesds        d1esds
     C                   else
     C                   seton                                        38
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS00296'     mgid(e)
     C                   clear                   mgw
     C                   movea     d1escd        mgw(1)
     C                   movea     d1eccd        mgw(6)
     C                   movea     mgw           mgdt(e)
     C                   endif
     C                   endif                                                  If has class
      *
     C                   endsl
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Validate Sequence
      *---------------------------------------------------------------
      *
     C     $exseq        begsr
      *
     C                   if        d1exseq < 0
     C                   seton                                        33
     C                   if        error < maxmsg
     C                   add       1             error
     C                   eval      mgid(e) = 'HS09000'
     C                   endif
     C                   endif
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      *  F4 prompt on screen 1
      *---------------------------------------------------------------
      *
     C     $f4s1         begsr
      *
      * Report code
     C                   select
     C                   when      fld = 'D1RPCD'
     C                   call      'HP559'
     C                   parm      *blank        xxcacd
     C                   parm      *blank        xxrpcd
     C                   parm      *blank        xxrsds
     C                   parm      *blank        xxucfl
     C                   seton                                        57
     C                   if        xxrpcd <> *blank
     C                   movel     xxrpcd        d1rpcd
     C                   movel     xxrsds        d1rsds
     C                   endif
      *
      * Expense class
     C                   when      fld = 'D1ECCD'
     C                   call      'HP556'
     C                   parm      *blank        xxeccd
     C                   parm      *blank        xxecds
     C                   seton                                        56
     C                   if        xxeccd <> *blank
     C                   movel     xxeccd        d1eccd
     C                   movel     xxecds        d1ecds
     C                   endif
      *
      * Expense subclass
      *
     C                   when      fld = 'D1ESCD'
     C                   call      'HP5029'
     C                   parm      *blank        xxescd
     C                   parm      *blank        xxesds
     C                   seton                                        58
     C                   if        xxescd <> *blank
     C                   movel     xxescd        d1escd
     C                   movel     xxesds        d1esds
     C                   endif
     C                   other
      *
      * F4 not valid for this field...issue message
      *
     C                   if        error < maxmsg
     C                   add       1             error                                        013
     C                   movel     'HS09011'     mgid(e)
     C                   endif
      *
     C                   endsl
      *
     C                   endsr
      /EJECT
      *----------------------------------------------------------------
      * Write/update/delete record in database file
      *----------------------------------------------------------------
      *
     C     $upd1         begsr
      *
      * If the user is deleting,
      *   perform deletion integrity checks
      *
     C                   if        action = delete                              If delete
     C                   exsr      $dltck
     C                   endif                                                  If delete
      *
      * If user action is revising or deleting, determine that the
      * record he is accessing still exists prior to attempting to
      * update.  Lock the record for update.
      *
     C                   if        error = 0 and                                If OK
     C                             (action = delete or action = revise)
      *
     C     d1excd        chain     hsp052                             92
     C                   if        *in92 = *on                                  If not found
     C                   seton                                        62
     C                   if        error < maxmsg
     C                   add       1             error
     C                   eval      mgid(e) = 'HS09008'
     C                   endif
      *
     C                   endif                                                  If not found
     C                   endif                                                  If OK
      *
      * If there are no errors, delete, revise or create the record.
      *
     C                   if        error = 0                                    If no error
      *
     c                   select
     C                   when      action = delete
     C                   delete    exrec
     C                   other
      *
      * Move DSPF fields to database file fields
      *
     C                   move      d1excd        exexcd
     C                   move      d1exds        exexds
     C                   z-add     d1exseq       exexseq
     C                   move      d1rpcd        exrpcd
     C                   move      d1eccd        execcd
     C                   move      d1escd        exescd
      *
     C                   if        action = create                              If create
     C                   write     exrec
     C                   else
     C                   update    exrec
     C                   endif                                                  If create
     C                   endsl
     C                   endif                                                  If no error
      *
      * Set next routine
      *
     C                   select
     C                   when      mode = create                                Wh creating
     C                   if        error = 0
     C                   add       1             error
     C                   eval      mgid(e) = 'HS09040'
     C                   eval      routne = set1
     c                   else
     C                   eval      routne = scrn1
     C                   endif
      *
     C                   when      error <> 0                                   Wh errors
     C                   eval      routne = scrn1
     C                   other
      *
     C                   eval      routne = exit
     C                   endsl
      *
     C                   endsr
      /EJECT
      *----------------------------------------------------------------
      * Deletion integrity checks
      *----------------------------------------------------------------
      *
      * The Expense Code cannot be deleted if:
      *  1) it has been used on any expense ticket
      *
      *
     C     $dltck        begsr
      *
     C     d1excd        chain     hsl048j                            92
     C                   if        *in92 = *off and error < maxmsg
     C                   add       1             error
     C                   eval      mgid(e) = 'HS00298'
     C                   clear                   mgw
     C                   movea     d1excd        mgw(1)
     C                   movea     mgw           mgdt(e)
     C                   endif
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Write error messages
      *---------------------------------------------------------------
      *
     C     $wrmsg        begsr
      *
     C                   call      'UT80045J'                           43
     C                   parm                    mgid
     C                   parm                    mgdt
     C                   parm                    error
     C                   parm      sdpgm         pgm
     C                   parm      msgfil        msgfl
      *
      * If call to UT80045J failed, a message hardcoded in the DSPF
      * command line format will be set on. So, redisplay screen.
      *
     C                   if        *in43 = *on
     C                   eval      routne = set1
     C                   endif
      *
     C                   endsr
      *
      *----------------------------------------------------------------
      * Clear messages
      *----------------------------------------------------------------
      *
     C     $clmsg        begsr
      *
     C                   call      'UT80045J'                           43
     C                   parm      *blank        mgi
     C                   parm      *blank        mgd
     C                   parm      *zero         error
     C                   parm      sdpgm         pgm
     C                   parm      msgfil        msgfl
      *
      * If call to UT80045J failed, a message hardcoded in the DSPF
      * command line format will be set on; so, redisplay screen.
      *
     C                   if        *in43 = *on
     C                   eval      routne = set1
     C                   endif
      *
     C                   endsr
      /EJECT
      *----------------------------------------------------------------
      * Set on last record indicator and end job
      *----------------------------------------------------------------
      *
     C     $exit         begsr
     C                   seton                                        lr
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Key lists
      *
     C     key01         klist
     C                   kfld                    d1escd
     C                   kfld                    d1eccd
      *
      * Parm Lists
      *
     C     *entry        plist
     C     d1mode        parm                    mode
     C                   parm                    d1excd
     C                   parm                    rtncd
      *
     C                   eval      action = mode
      *
      *
      * The following standard code must be included to make the
      * standard error message handling program function properly.
      * This code sets and clears the program message queue.
      *
     C                   movel     '*'           msgpgm
     C                   exsr      $clmsg
      *
     C                   endsr
      /EJECT
