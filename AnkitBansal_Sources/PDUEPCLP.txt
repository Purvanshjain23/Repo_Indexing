/********************************************************************/
/*                                                                  */
/*  SYNOPSIS - PRINT PROGRAM WITH SELECTIONS FOR LOADS LOADED NOT   */
/*             PICKED UP - BY COMPANY                               */
/*                                                                  */
/*  SYSTEM    - ORDER MANAGEMENT SYSTEM                             */
/*  PROGRAM   - PRINT LOADS LOADED NOT PICKED UP                    */
/*  DATE      - 12/23/96                                            */
/*                                                                  */
/*      PARAMETERS RECEIVED:                                        */
/*                                                                  */
/*          &COMPNYN    - COMPANY NUMBER - BLANK FOR ALL            */
/*          &OUTQ       - OUTPUT QUEUE                              */
/*          &HOLD       - HOLD PRINT                                */
/*          &SAVE       - SAVE PRINT                                */
/*          &COPYN      - NUMBER OF COPIES                          */
/*          &NIGHTQ     - NIGHT OUTPUT QUEUE                        */
/*          &CARRIER    - CARRIER CODE                              */
/*          &EMAIL      - EMAIL ADDRESS                             */
/*          &EMLFMT     - EMAIL FORMAT       92536                  */
/********************************************************************/
/* 04/24/2015 LJB E004076 ADD SEND Y/N FLAG. NEEDED FOR REPORT.     */
/*                THIS IS FOR WHEN THE REPORT IS CALLED FROM THE    */
/*                JOB SCHEDULER OR FOR EMAIL                        */
/*                ADD OPTIONAL EMAIL ADDRESS IF CALLED FROM PROMPT  */
/* 03/05/2015 LJB E003914 ADD CARRIER CODE PARM                     */
/* 08/25/2022 EL  O92536  Loads Loaded Not Picked Up (EXCEL)        */
/********************************************************************/
 PDUEPCLP:   PGM        PARM(&COMPNYN &OUTQ &HOLD &SAVE &COPYN +
                          &NIGHTQ &CARRIER &EMAIL1 &EMAIL2 &EMLFMT)

/* PARMS                                                                      */
        DCL    &COMPNYN    *DEC                  /* Company Number (Passed)   */
        DCL    &COMPANY    *DEC    (3 0)         /* Company Number            */
        DCL    &COMPA      *CHAR   (3)           /* COMPANY ALPHA             */
        DCL    &DESC       *CHAR   (25)          /* DESCRIPTION               */
        DCL    &COPYN      *DEC                  /* No. of Copies             */
        DCL    &COPY       *DEC    (2 0)         /* Number of copies          */
        DCL    &OUTQ       *CHAR   (10)          /* Output Queue              */
        DCL    &HOLD       *CHAR   (4)           /* Hold Status               */
        DCL    &SAVE       *CHAR   (4)           /* Save Status               */
        DCL    &NIGHTQ     *CHAR   (10)          /* Night Job Queue           */
        DCL    &COMMAND    *CHAR   (512)
        DCL    &EMAIL      *CHAR   (50)          /* EMAIL ADDRESS             */
        DCL    &EMAIL1     *CHAR   (25)          /* EMAIL ADDRESS             */
        DCL    &EMAIL2     *CHAR   (25)          /* EMAIL ADDRESS             */
        DCL    &CARRIER    *CHAR   (3)           /* CARRIER CODE              */
        DCL    &SENDYN     *CHAR   (1)           /* SEND Y/N FLAG             */
        DCL    &SUBJECT    *CHAR   (100)         /* SUBJECT FOR EMAIL         */
        DCL    &TEXT       *CHAR   (80)          /*  EMAIL TEXT               */
        DCL    &EMLFMT     *CHAR   (5)           /*  EMAIL FORMAT             */

/********************************************************************/
/* CHANGE VARIABLES FOR USE IN PROGRAM                              */
/********************************************************************/
             CHGVAR     VAR(&COMPANY) VALUE(&COMPNYN)
             CHGVAR     VAR(&COMPA) VALUE(&COMPANY)
             CHGVAR     VAR(&COPY) VALUE(&COPYN)

/********************************************************************/
/* OVERRIDE PRINTER FILE WITH USER SPECIFIED SELECTION VALUES       */
/********************************************************************/
             SBDPRTOVR  CMDVAR(&COMMAND) FILE(PDUEPFR$) OUTQ(&OUTQ) +
                          COPIES(&COPY) HOLD(&HOLD) SAVE(&SAVE)

             CALL       PGM(QCMDEXC) PARM(&COMMAND 512)

             IF         COND(&EMAIL1 *NE '           ') THEN(DO)
             CHGVAR     VAR(&HOLD) VALUE(*YES)
             CHGVAR     VAR(&SAVE) VALUE(*YES)
             ENDDO

/*********************************************************************/
/*   CALL PRINT PROGRAM    ADDED SENDYN FOR JOB SCHEDULER JOB E004076*/
/*********************************************************************/
             DLTF       FILE(QTEMP/PUCLCPL0)
             MONMSG CPF0000
             DLTF       FILE(QTEMP/PUCLCPL1)
             MONMSG CPF0000
             DLTF       FILE(QTEMP/PUCLCPP)
             MONMSG CPF0000
             CRTDUPOBJL OBJ(PUCLCPP)  OBJTYPE(*FILE) TOLIB(QTEMP)
             CRTDUPOBJL OBJ(PUCLCPL0) OBJTYPE(*FILE) TOLIB(QTEMP)
             CRTDUPOBJL OBJ(PUCLCPL1) OBJTYPE(*FILE) TOLIB(QTEMP)
             CALL       PGM(PDUEPFR) PARM(' ' &COMPANY &CARRIER +
                          &SENDYN)

/* ONLY EMAIL IF AN EMAIL ADDRESS WAS PROVIDED                       */
/* *PDF FORMAT */
             IF         COND(&EMLFMT *EQ '*PDF') THEN(DO)
             IF         COND(&EMAIL1 *NE '               ') THEN(DO)
             IF         COND(&SENDYN *EQ 'Y') THEN(DO)
             IF         COND(&CARRIER *NE 'BUY') THEN(DO)
             CHGVAR     VAR(&EMAIL) VALUE(&EMAIL1 *TCAT &EMAIL2)
             CHGVAR     VAR(&COMPA) VALUE(&COMPANY)
             CHGVAR     VAR(&SUBJECT) VALUE('Loads Not Picked Up' +
                          *BCAT 'FOR' *BCAT &CARRIER *CAT '/' *CAT +
                          &COMPA)
             ESEND/ESNDMAIL RECIPIENT(&EMAIL) SUBJECT(&SUBJECT) +
                          MSG(&SUBJECT) ATTLIST((* *DFT *N PDUEPFR$))
             MONMSG     MSGID(EML0023 EML0004)
             ENDDO
             ENDDO
             ENDDO
             ENDDO
/* ONLY EMAIL IF AN EMAIL ADDRESS WAS PROVIDED                       */
/* *XLS FORMAT */
             IF         COND(&EMLFMT *EQ '*XLS') THEN(DO)
             IF         COND(&EMAIL1 *NE '               ') THEN(DO)
             CHGVAR     VAR(&TEXT) VALUE('Load Loaded Not Picked Up')
             CHGVAR     VAR(&EMAIL) VALUE(&EMAIL1 *TCAT &EMAIL2)
             EXECUTE    VIEW(LOADSNOTPU) PCFMT(*XLS) +
                          RECIPIENT(&EMAIL) EMLMSG(&TEXT)
             ENDDO
             ENDDO

 END:        ENDPGM
