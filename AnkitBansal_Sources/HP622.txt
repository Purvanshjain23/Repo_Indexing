      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      HOG PRODUCTION
      * PROGRAM:     HP622
      * TITLE:       LISTING OF TRUCKERS
      * PROGRAMMER:  ROSE CENTONZE
      * CREATED:     5/03/94
      *
      * FUNCTION:  Batch program to list the records in the file.
      *            This listing is submitted from HP414-Work with Truckers.
      *
      *            The listing 'selections' will be the positioner/selector values
      *            in the control record at the time the user submitted the listing.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 06/18/97  LEANNE FEDOR
      *           ADDED USER ID TO HEADINGS.
      *
      * 01/07/02  LEANNE FEDOR
      *           We added additional fields to the Trucker Master. And, we now take the
      *           'selections' directly from the Work With...and pass them in the LDA.
      *
      * 10/15/13  LeAnne Ramsey (E2831)
      *           Recompile only. Added field 'MTech Reference'.
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     FHSP046    IF   E           K DISK
      *    TRUCKERS
      *
      *
     Fhsp110    if   e           k disk
      *    Trucker type
      *
      *
     FQPRINT    O    F  132        PRINTER OFLIND(*INOF)
      *
      /EJECT
      ****************************************************************
      * INPUT SPECS
      ****************************************************************
      *
      *---------------------------------------------------------------
      * STANDARD PROGRAM STATUS DATA STRUCTURE
      *---------------------------------------------------------------
      *    EXTERNALLY DEFINED AS UTPGFR (RECORD FORMAT: PGMDSR)
     D PGMDS         ESDS                  EXTNAME(UTPGFR)
      *
      *---------------------------------------------------------------
      * Local data area.
      *---------------------------------------------------------------
      *
     Dlda             uds                  dtaara(*lda)
     D  ldticd                 1      5
     D  ldaist                 6      6
     D  ldttcd                 7      7
      *
      *---------------------------------------------------------------
      *  NAMED CONSTANTS
      *---------------------------------------------------------------
      *
     D YES             C                   CONST('Y')
     D NO              C                   CONST('N')
      *
      *---------------------------------------------------------------
      * STANDALONE FIELDS
      *---------------------------------------------------------------
      *
      * Print fields
      *
     D dash            s             80    inz(*all'-')
      *
     D h1trucker       s             75
     D h1status        s             75
     D h1type          s             75
     D h1ttds          s             25
      *
      /EJECT
      ****************************************************************
      * MAIN LINE
      ****************************************************************
      *
      * PREPARE TO READ THE TRUCKER FILE AND PRINT HEADINGS THE FIRST TIME THROUGH.
      *
     C     ldticd        setll     hsp046
     C                   EXCEPT    h1hdr
      *
      * READ AND PRINT ALL RECORDS IN THE FILE THAT MATCH THE USERS SELECTIONS.
      *
     C                   dou       *in90 = *on                                  Main do
     C                   read      hsp046                                 90
      *
     C                   if        *in90 = *off and                             If not EOF
     C                             (ldaist = ' ' or ldaist = tiaist) and
     C                             (ldttcd = ' ' or ldttcd = tittcd)
      *
     C     *INOF         IFEQ      *ON
     C                   EXCEPT    h1hdr
     C                   ENDIF
      *
      * Print detail line
     C                   EXCEPT    d1dtl
     C                   MOVEL     YES           PRTFLG
      *
     C                   ENDIF                                                  IF NOT EOF
     C                   ENDDO                                                  MAIN DO
      *
      * End of File
      *
     C     PRTFLG        IFEQ      NO
     C                   EXCEPT    NODATA
     C                   ENDIF
      *
     C                   SETON                                        LR
      /EJECT
      *---------------------------------------------------------------
      * INITIALIZATION SUBROUTINE
      *---------------------------------------------------------------
      *
     C     *INZSR        BEGSR
      *
      * SET PRINT FLAG TO DETERMINE IF ANY RECORDS WERE PRINTED
      *
     C                   MOVEL     NO            PRTFLG            1
      *
      * RETRIEVE TIME FOR REPORT HEADING
      *
     C                   TIME                    RTIME             6 0
      *
      *
      * Set up headings for user selections:
      *
     C                   if        ldticd = *blank
     C                   eval      h1trucker = 'Listing begins with the ' +
     C                                        'first trucker'
     C                   else
     C                   eval      h1trucker = 'Listing begins with trucker ' +
     C                                        ldticd
     C                   endif
      *
      * Active/inactive status
      *
     C                   select
     C                   when      ldaist = *blank
     C                   eval      h1status = 'Listing includes both Active ' +
     C                                        'and Inactive truckers'
     C                   when      ldaist = 'A'
     C                   eval      h1status = 'Listing includes only Active ' +
     C                                        'truckers'
     C                   when      ldaist = 'I'
     C                   eval      h1status = 'Listing includes only ' +
     C                                        'Inactive truckers'
     C                   endsl
      *
      *
      * Truck type
     C                   select
     C                   when      ldttcd = *blank
     C                   eval      h1type = 'Listing includes truckers with' +
     C                                        ' all Trucker Types'
     C                   other

     C     ldttcd        chain     hsp110                             92
     C                   if        *in92 = *off
     C                   move      ttttds        h1ttds
     C                   endif
     C                   eval      h1type = 'Listing only includes truckers' +
     C                                        ' with Trucker Type: ' +
     C                                        h1ttds
     C                   endsl
      *
     C                   endsr
      *
      /EJECT
      *-------------------------------------------------------------
      * REPORT HEADING LINES
      *-------------------------------------------------------------
      *
     OQPRINT    E            h1hdr          1 01
     O                       SDPGM               10
     O                                           48 'HOG PRODUCTION SYSTEM'
     O                                           66 'DATE'
     O                       UDATE         Y     76
      *
     O          E            h1hdr          1
     O                       SDUSR               10
     O                                           47 ' LISTING OF TRUCKERS'
     O                                           66 'TIME'
     O                       RTIME               76 '  :  :  '
      *
     O          E            h1hdr          2
     O                                           66 'PAGE'
     O                       PAGE          Z     71
      *
      *
     O          e            h1hdr          1
     O                       h1trucker           75
      *
     O          e            h1hdr          1
     O                       h1status            75
      *
     O          e            h1hdr          2
     O                       h1type              75
      *
     O          E            h1hdr          1
     O                                            7 'TRUCKER'
     O                                           80 'ACTIVE/INACTIVE'
     O                                           52 'TRUCKER'
      *
     O          E            h1hdr          1
     O                                            5 'CODE'
     O                                           15 'NAME'
     O                                           50 'TYPE'
     O                                           76 'STATUS'
      *
     O          E            h1hdr          1
     O                       dash                80
      *
      *-------------------------------------------------------------
      *  DETAIL LINE
      *-------------------------------------------------------------
      *
     O          E            d1dtl       1
     O                       titicd               6
     O                       titinm              36
     O                       tittcd              49
     O                       tiaist              73
      *
      *-------------------------------------------------------------
      * NO DATA MESSAGE LINE
      *-------------------------------------------------------------
      *
     O          E            NODATA      1
     O                                           19 'NO DATA MEETS YOUR'
     O                                           38 'SELECTION CRITERIA'
