/********************************************************************/
/*  PROGRAM  - PDIPSPCL                                             */
/*  SYNOPSIS - POST INVOICES                                        */
/*                                                                  */
/*  SYSTEM    - ORDER MANAGEMENT SYSTEM                             */
/*  PROGRAM   - POST INVOICES                                       */
/*  DATE      -  8/01/95                                            */
/*                                                                  */
/*      PARAMETERS RECEIVED:                                        */
/*                                                                  */
/*          &COMPNYN    - COMPANY NUMBER - BLANK FOR ALL            */
/*          &INVOICN    - INVOICE NUMBER - BLANK FOR ALL            */
/*                        *** INVOICE NUMBER ONLY PASSED FOR MEMOS  */
/*          &OUTQ       - OUTPUT QUEUE                              */
/*          &HOLD       - HOLD PRINT                                */
/*          &SAVE       - SAVE PRINT                                */
/*          &COPYN      - NUMBER OF COPIES                          */
/*          &NIGHTQ     - NIGHT OUTPUT QUEUE                        */
/*------------------------------------------------------------------*/
/* MODIFICATIONS:                                                   */
/*------------------------------------------------------------------*/
/* 12/96 OMS206 PLL   ADD INVOICE # AS PARM TO INVOICE POST ROUTINE */
/*------------------------------------------------------------------*/
/* 06/09/2006 LJB OMS047 REMOVE THE SBDPRTOVR COMMAND AND CHANGE TO */
/*                       OVRPRTF INSTEAD. MOVE TO LOCATION OF       */
/*                       THE CALL TO THE PROGRAM.                   */
/* 06/23/2006 PKD WORKIN SEND MESSAGE TO DAILY'S ONCE THE INTER     */
/*                       COMPANY INVOICES HAVE BEEN POSTED.         */
/********************************************************************/
 PDIPSPCL:   PGM        PARM(&COMPNYN &INVOICN &OUTQ &HOLD &SAVE +
                          &COPYN &NQSTAT &NIGHTQ &PRCUSTT)

/* PARMS                                                                      */
        DCL    &COMPNYN    *DEC                  /* Company Number (Passed)   */
        DCL    &INVOICN    *DEC                  /* Invoice Number (Passed)   */
        DCL    &OUTQ       *CHAR   (10)          /* Output Queue              */
        DCL    &HOLD       *CHAR   (4)           /* Hold Status               */
        DCL    &SAVE       *CHAR   (4)           /* Save Status               */
        DCL    &COPYN      *DEC                  /* No. of Copies  (Passed)   */
        DCL    &NQSTAT     *CHAR   (1)           /* If MUST run in night q.   */
        DCL    &NIGHTQ     *CHAR   (10)          /* Night Job Queue           */
        DCL    &PRCUSTT    *CHAR   (02)          /* PROCESS CUST TYPE (PASSED)*/

/* INTERNAL FIELDS                                                            */
        DCL    &COMPANY    *DEC    (3 0)         /* Company Number            */
        DCL    &INVOICE    *DEC    (7 0)         /* Invoice Number            */
        DCL    &COPY       *DEC    (2 0)         /* Number of copies          */
        DCL    &COMMAND    *CHAR   (512)         /* For Trans. Reg. override  */
        DCL    &PGMO       *CHAR   (10)          /* Printer File              */
             DCL        VAR(&ZERO) TYPE(*DEC) VALUE(0.0)
             DCL        VAR(&USER) TYPE(*CHAR) LEN(10)
             DCL        VAR(&GRPPRF) TYPE(*CHAR) LEN(10)

/* PAGER MESSAGE */
             DCL        VAR(&DSTLST) TYPE(*CHAR) LEN(10)
             DCL        VAR(&MSG) TYPE(*CHAR) LEN(132)

/********************************************************************/
/* CHANGE VARIABLES FOR USE IN PROGRAM                              */
/********************************************************************/
             CHGVAR     VAR(&COMPANY) VALUE(&COMPNYN)
             CHGVAR     VAR(&INVOICE) VALUE(&INVOICN)
             CHGVAR     VAR(&COPY) VALUE(&COPYN)
/*** OMS047  CHGVAR     VAR(&PGMO) VALUE('PDVHPFR$')                */

             RTVJOBA    USER(&USER)
             RTVUSRPRF  USRPRF(&USER) GRPPRF(&GRPPRF)
/********************************************************************/
/* OMS047 REMOVED THE SBDPRTOVR - OVRPRTF IS USED INSTEAD           */
/********************************************************************/
/*** OMS047  IF         COND(&INVOICN *EQ &ZERO) THEN(DO)        +
             SBDPRTOVR  CMDVAR(&COMMAND) FILE(&PGMO) OUTQ(&OUTQ) +
                          COPIES(&COPY) HOLD(&HOLD) SAVE(&SAVE)  +
             CALL       PGM(QCMDEXC) PARM(&COMMAND 512)          +
             ENDDO                                                   */
/*********************************************************************/
/*   CALL PROCESSING PROGRAM                                         */
/*********************************************************************/
/*           CALL       PGM(PDT1XFR) PARM(' ' &COMPANY &INVOICE +
                          &PRCUSTT) */

/*           IF         COND(&GRPPRF *EQ SBDPGMR) THEN(DO) */
/* FOR TEST  CPYF       FROMFILE(OMHSTPP) +
                          TOFILE(OMSDEVGEN/OMHSTPPS1) +
                          MBROPT(*REPLACE) CRTFILE(*YES)             */
/*           ENDDO */

/*********************************************************************/
/*   CALL COPY TO HISTORY PROGRAM                                    */
/*********************************************************************/
             CALL       PGM(PDRGXFR) PARM(' ' &COMPANY &INVOICE +
                          &PRCUSTT)

/*********************************************************************/
/*   CALL WAREHOUSE TRANSFER COMPLETION PROGRAM                      */
/*********************************************************************/
/* DIDN'T ADD INVOICE TO THIS PROCESS, SINCE THIS ISN'T FOR MEMOS    */

             IF         COND(&INVOICN *EQ &ZERO) THEN(DO)
             CALL       PGM(PDHBXFR) PARM(' ' &COMPANY &PRCUSTT)
             ENDDO

/*********************************************************************/
/* CALL INVOICE POSTING REGISTER                                     */
/* OVERRIDE PRINTER FILE WITH USER SPECIFIED SELECTION VALUES        */
/* DO NOT PRINT THE POSTING REGISTER IF PROCESSING ONE INVOICE       */
/*********************************************************************/
             IF         COND(&INVOICN *EQ &ZERO) THEN(DO)
             IF         COND(&COPY *EQ 0) THEN(CHGVAR VAR(&COPY) +
                          VALUE(1))
             OVRPRTF    FILE(PDVHPFR$) OUTQ(&OUTQ) COPIES(&COPY) +
                          HOLD(&HOLD) SAVE(&SAVE)
             CALL       PGM(PDVHPFR) PARM(' ' &COMPANY &PRCUSTT)
             ENDDO

/*  UPDATE ORDER TYPE PRINT STATUS TO Y                            */
             ELSE       CMD(DO)
             CALL       PGM(PDUDXFR) PARM(' ' &COMPANY &INVOICE 'Y')
             ENDDO

/*********************************************************************/
/*   CALL JD EDWARDS POSTING PROGRAM                                 */
/*********************************************************************/
             OVRPRTF    FILE(PRINT198) OUTQ(&OUTQ) HOLD(&HOLD) +
                          SAVE(&SAVE)

             CALL       PGM(OMINVCL) PARM(' ' &COMPANY)

/* ------------------------------------------------------------------*/
/* SEND EMAIL TO DAILY'S                                             */
/* ------------------------------------------------------------------*/

             IF         COND(&GRPPRF *NE SBDPGMR) THEN(DO)
             IF         COND(&PRCUSTT *EQ 'IC') THEN(DO)
             CHGVAR     VAR(&DSTLST) VALUE('PRKICINV')
             CHGVAR     VAR(&MSG) VALUE('Inter-Company Invoices have +
                          been posted.')
             CALL       PGM(POMSUPC) PARM(&DSTLST &MSG)
             MONMSG     MSGID(CPF0000)
             ENDDO
             ENDDO

 END:        ENDPGM
