/***************************************************************** */
/*                                                                 */
/*  SYSTEM:          TRIUMPH FOODS                                 */
/*  PROGRAM NUMBER:  TF411CL                                       */
/*  PROGRAM NAME:    WEEKLY PRODUCT REVENUE REPORTS/DOWNLOAD       */
/*  PROGRAMMER:      LEANNE FEDOR                                  */
/*  CREATE DATE:     09/29/04                                      */
/*                                                                 */
/*  FUNCTION:        WEEKLY PRODUCT REPORTS/DOWNLOAD               */
/*                                                                 */
/*******************************************************************/
/* MODIFICATIONS                                                   */
/*                                                                 */
/* 05/11/09  LeAnne Ramsey                                         */
/*           Make Print logic match Meat Costing.                  */
/*                                                                 */
/* 05/14/09  LeAnne Ramsey                                         */
/*           Added the name of the Distribution List to the email  */
/*           for TF608.                                            */
/*                                                                 */
/* 05/10/17 ROSE CENTONZE   SCAN: R9083                            */
/*           GET DATA LIB - IF NOT PRODUCTION PRKFLIB, SND EMAILS TO TEST */
/*                                                                 */
/* 11/13/17  Danny Nguyen  R12011A-Weekly Product Revenue          */
/*           File TFP010 was changed.                              */
/*           Add call to print TF6110 (Revenue: Weekly Product     */
/*           Revenue Report - ALL) which includes STF data.        */
/*           Add call to print TF6080 (Revenue: Weekly Product     */
/*           Floor Production Rpt - ALL) which includes STF data.  */
/*                                                                 */
/*******************************************************************/

             PGM

             DCL        VAR(&QDATA) TYPE(*CHAR) LEN(290)

             DCL        VAR(&OUTQ) TYPE(*CHAR) LEN(10)
             DCL        VAR(&HOLD) TYPE(*CHAR) LEN(4)
             DCL        VAR(&SAVE) TYPE(*CHAR) LEN(4)
             DCL        VAR(&COPY) TYPE(*DEC)  LEN(1)

             DCL        VAR(&FLAG)     TYPE(*CHAR) LEN(1)
             DCL        VAR(&LDEMAIL)  TYPE(*CHAR) LEN(50)
             DCL        VAR(&LDDEMAND) TYPE(*CHAR) LEN(1)

             DCL        VAR(&LDFCYMD) TYPE(*DEC) LEN(8)
             DCL        VAR(&LDTCYMD) TYPE(*DEC) LEN(8)
             DCL        VAR(&LDVER)   TYPE(*CHAR) LEN(1)
             DCL        VAR(&LDRPFL)  TYPE(*CHAR) LEN(1)
             DCL        VAR(&LDIECD)  TYPE(*CHAR) LEN(1)
             DCL        VAR(&LDCOFL)  TYPE(*CHAR) LEN(1)
             DCL        VAR(&LDEDFL)  TYPE(*CHAR) LEN(1)

             DCL        VAR(&LDISTYCD) TYPE(*DEC) LEN(3)
             DCL        VAR(&LDISGRCD) TYPE(*DEC) LEN(3)
             DCL        VAR(&LDISCLCD) TYPE(*DEC) LEN(3)

             DCL        VAR(&LDTFCGCD) TYPE(*CHAR) LEN(2)
             DCL        VAR(&LDTFCLCD) TYPE(*CHAR) LEN(3)

             DCL        VAR(&LDPRCD) TYPE(*DEC) LEN(7)

             DCL        VAR(&DATLIB) TYPE(*CHAR) LEN(10)  /* DATA LIB  */
       DCL        VAR(&HISLIB) TYPE(*CHAR) LEN(10) /* HIST LIB-NOT USED HERE */
             DCL        VAR(&TESTMAIL) TYPE(*CHAR) LEN(20)  /* TEST EMAIL */

             CHGVAR     VAR(&LDFCYMD)   VALUE(%SST(*LDA 52 8))
             CHGVAR     VAR(&LDTCYMD)   VALUE(%SST(*LDA 60 8))
             CHGVAR     VAR(&LDVER)     VALUE(%SST(*LDA 269 1))
             CHGVAR     VAR(&LDIECD)    VALUE(%SST(*LDA 301 1))
             CHGVAR     VAR(&LDRPFL)    VALUE(%SST(*LDA 328 1))
             CHGVAR     VAR(&LDCOFL)    VALUE(%SST(*LDA 329 1))
             CHGVAR     VAR(&LDEDFL)    VALUE(%SST(*LDA 349 1))

             CHGVAR     VAR(&LDISTYCD)  VALUE(%SST(*LDA 83 3))
             CHGVAR     VAR(&LDISGRCD)  VALUE(%SST(*LDA 116 3))
             CHGVAR     VAR(&LDISCLCD)  VALUE(%SST(*LDA 149 3))

             CHGVAR     VAR(&LDTFCGCD)  VALUE(%SST(*LDA 219 2))
             CHGVAR     VAR(&LDTFCLCD)  VALUE(%SST(*LDA 236 3))

             CHGVAR     VAR(&LDPRCD)    VALUE(%SST(*LDA 182 7))

             CHGVAR     VAR(&LDDEMAND)  VALUE(%SST(*LDA 300 1))

/*---------------------------------------------------------------------*/
/* SET UP QUERY FOR WEEKLY PRODUCT REVENUE DETAIL RECORDS              */
/*---------------------------------------------------------------------*/

             CHGVAR     VAR(&QDATA) VALUE(' ')

/* FROM/TO DATE                                                           */

             CHGVAR     VAR(%SST(&QDATA 1 10)) VALUE('PRWEDT *GE')
             CHGVAR     VAR(%SST(&QDATA 12 8)) VALUE(&LDFCYMD)
             CHGVAR     VAR(%SST(&QDATA 21 15)) VALUE('*AND PRWEDT +
                          *LE')
             CHGVAR     VAR(%SST(&QDATA 37 8)) VALUE(&LDTCYMD)

/* ONLY INCLUDE RECORDS BUILT IN A "FINAL" RUN OF THE WEEKLY REVENUE      */
/* CLOSE. ALICE DOES NOT WANT ANY "PRELIMINARY" DATA ON THIS REPORT.      */

             CHGVAR     VAR(%SST(&QDATA 46 19)) VALUE('*AND PRPFCD +
                          *EQ "F"')


/*  ITEM STRUCTURE TYPE                                           */

             IF         COND(&LDISTYCD *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 66 17)) VALUE('*AND PRISTYCD +
                          *EQ')
             CHGVAR     VAR(%SST(&QDATA 84 3)) VALUE(&LDISTYCD)
             ENDDO

/* ITEM STRUCTURE GROUP                                      */

             IF         COND(&LDISGRCD *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 88 17)) VALUE('*AND PRISGRCD +
                          *EQ')
             CHGVAR     VAR(%SST(&QDATA 106 3)) VALUE(&LDISGRCD)
             ENDDO

/* ITEM STRUCTURE CLASS                                      */

             IF         COND(&LDISCLCD *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 110 17)) VALUE('*AND PRISCLCD +
                          *EQ')
             CHGVAR     VAR(%SST(&QDATA 128 3)) VALUE(&LDISCLCD)
             ENDDO

/* PRODUCT                                                   */

             IF         COND(&LDPRCD *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 132 15)) VALUE('*AND PRPRCD +
                          *EQ')
             CHGVAR     VAR(%SST(&QDATA 148 7)) VALUE(&LDPRCD)
             ENDDO

/*  TF CLASS GROUP                                            */

             IF         COND(&LDTFCGCD *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 156 19)) VALUE('*AND PRTFCGCD +
                          *EQ "')
             CHGVAR     VAR(%SST(&QDATA 175 2)) VALUE(&LDTFCGCD)
             CHGVAR     VAR(%SST(&QDATA 177 1)) VALUE('"')
             ENDDO

/* TF CLASSIFICATION                                         */

             IF         COND(&LDTFCLCD *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 179 19)) VALUE('*AND PRTFCLCD +
                          *EQ "')
             CHGVAR     VAR(%SST(&QDATA 198 3)) VALUE(&LDTFCLCD)
             CHGVAR     VAR(%SST(&QDATA 201 1)) VALUE('"')
             ENDDO


/* IF ONLY THE 'EXEMPT' RECORDS                       */

             IF         COND(&LDIECD *EQ 'E') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 203 21)) VALUE('*AND PRRCEXFL +
                          *EQ "Y"')
             ENDDO


/* IF ONLY THE 'NON-EXEMPT' RECORDS                   */

             IF         COND(&LDIECD *EQ 'N') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 225 21)) VALUE('*AND PRRCEXFL +
                          *EQ "N"')
             ENDDO

/* IF ONLY THE 'CO-OWNED' PRODUCTS                    */

             IF         COND(&LDCOFL *EQ 'C') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 247 22)) VALUE('*AND PRCOOWNFL +
                          *EQ "Y"')
             ENDDO


/* IF ONLY THE 'NOT CO-OWNED' PRODUCTS                */

             IF         COND(&LDCOFL *EQ 'N') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 247 22)) VALUE('*AND PRCOOWNFL +
                          *EQ "N"')
             ENDDO


/* EXPORT OR DOMESTIC PRODUCTS ONLY                   */

             IF         COND(&LDEDFL *EQ 'E' *OR &LDEDFL *EQ 'D') +
                          THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 270 17)) VALUE('*AND PREDFL +
                          *EQ "')
             CHGVAR     VAR(%SST(&QDATA 287 1)) VALUE(&LDEDFL)
             CHGVAR     VAR(%SST(&QDATA 288 1)) VALUE('"')
             ENDDO
/*---------------------------------------------------------------------*/
/* R9083 GET DATA LIBRARY -- IF NOT PRODUCTION PRKFLIB, THEN EMAIL TEST */
/*---------------------------------------------------------------------*/
      CALL CAQXXFR  ('' &DATLIB &HISLIB)
      IF COND(&DATLIB *NE 'PRKFLIB   ') THEN(DO)
             CALL       PGM(POMTXFR) PARM('' X'360F' 'ISCEMAIL ' +
                          &TESTMAIL)
      ENDDO

/*---------------------------------------------------------------------*/
/* OVERRIDE FILE KEYS AND EXTRACT DATA                                 */
/*---------------------------------------------------------------------*/

/* IF USER SELECTED THE DOWNLOAD                                   */

             IF         COND(&LDRPFL *EQ 'D') THEN(DO)
             OVRDBF     FILE(TFL010B) SHARE(*YES)
             OPNQRYF    FILE((TFL010B)) QRYSLT(&QDATA) +
                          KEYFLD((PRRCEXFL) (PRPRCD) (PRWEDT)) +
                          SEQONLY(*YES)
             ENDDO

/* IF USER SELECTED VERSION 1=SORT/PRINT BY ITEM STRUCTURE           */
/* AND THE USER DID NOT SELECT THE DOWNLOAD                          */

             IF         COND(&LDVER *EQ '1' *AND &LDRPFL *NE 'D') +
                          THEN(DO)
             OVRDBF     FILE(TFL010B) SHARE(*YES)
             OPNQRYF    FILE((TFL010B)) QRYSLT(&QDATA) +
                          KEYFLD((PRRCEXFL) (PRPREXFL) (PRISTYCD) +
                          (PRISGRCD) (PRISCLCD) (PRPRCD) (PRWEDT)) +
                          SEQONLY(*YES)
             ENDDO

/* IF USER SELECTED VERSION 2=SORT/PRINT BY TF STRUCTURE           */
/* AND THE USER DID NOT SELECT THE DOWNLOAD                          */

             IF         COND(&LDVER *EQ '2' *AND &LDRPFL *NE 'D') +
                          THEN(DO)
             OVRDBF     FILE(TFL010B) SHARE(*YES)
             OPNQRYF    FILE((TFL010B)) QRYSLT(&QDATA) KEYFLD(*FILE) +
                          SEQONLY(*YES)
             ENDDO


/*----------------------------------------------------------------*/
/* WHEN THE USER SELECTED THE "DOWNLOAD",                         */
/*    EMAIL A SPREADSHEET                                         */
/*----------------------------------------------------------------*/

             IF         COND(&LDRPFL *EQ 'D') THEN(DO)         /* If Download */

             CHGVAR     VAR(&LDEMAIL) VALUE(%SST(*LDA 350 50))

             CRTDUPOBJ  OBJ(TFP311) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)
             CALL       PGM(TF311) PARM(&FLAG)

             IF         COND(&FLAG = 'Z')     THEN(DO)
             IF COND(&DATLIB = 'PRKFLIB   ') THEN(DO)
           ESNDMAIL   RECIPIENT(&LDEMAIL) SUBJECT('Weekly Product +
                          Revenue Download') MSG('Your selections +
                          on TF411-Specify Options for Weekly +
                          Product Reports/Downloads did not extract +
                          any data for the spreadsheet download. +
                          Please change your selections and submit +
                          the Download again.')
             MONMSG     MSGID(CPF0000)
             ENDDO
             ELSE DO
             ESNDMAIL   RECIPIENT(&TESTMAIL) SUBJECT('Weekly Product +
                          Revenue Download-TEST') MSG('Your selections +
                          on TF411-Specify Options for Weekly +
                          Product Reports/Downloads did not extract +
                          any data for the spreadsheet download. +
                          Please change your selections and submit +
                          the Download again.')
             MONMSG     MSGID(CPF0000)
             ENDDO
             ENDDO

             IF         COND(&FLAG = 'T')     THEN(DO)
             IF COND(&DATLIB = 'PRKFLIB   ') THEN(DO)
           ESNDMAIL   RECIPIENT(&LDEMAIL) SUBJECT('Weekly Product +
                          Revenue Download') MSG('Your selections +
                          on TF411-Specify Options for Weekly +
                          Product Reports/Downloads extracted over +
                          66,000 records--which is too many records +
                          for the spreadsheet to handle. Change +
                          your selections to extract fewer records +
                          and submit your Download request again.')
             MONMSG     MSGID(CPF0000)
             ENDDO
             ELSE  DO
             ESNDMAIL   RECIPIENT(&TESTMAIL) SUBJECT('Weekly Product +
                          Revenue Download-TEST') MSG('Your selections +
                          on TF411-Specify Options for Weekly +
                          Product Reports/Downloads extracted over +
                          66,000 records--which is too many records +
                          for the spreadsheet to handle. Change +
                          your selections to extract fewer records +
                          and submit your Download request again.')
             MONMSG     MSGID(CPF0000)
             ENDDO
             ENDDO

             IF         COND(&FLAG = ' ')     THEN(DO)
             IF COND(&DATLIB = 'PRKFLIB   ') THEN(DO)
            EXECUTE    VIEW(*LIBL/TF311V) PCFMT(*XLS) REPLACE(*YES) +
                          RECIPIENT(&LDEMAIL) EMLMSG('Weekly +
                          Product Revenue Download') TEXT('Weekly +
                          Product Revenue Download')
             MONMSG     MSGID(CPF0000)
             ENDDO                                             /* If Download */
             ELSE   DO
             EXECUTE    VIEW(*LIBL/TF311V) PCFMT(*XLS) REPLACE(*YES) +
                          RECIPIENT(&TESTMAIL) EMLMSG('Weekly +
                          Product Revenue Download-TEST') TEXT('Weekly +
                          Product Revenue Download')
             MONMSG     MSGID(CPF0000)
             ENDDO
             ENDDO

             ENDDO                                             /* If Download */


/*----------------------------------------------------------------*/
/* SET PARAMETERS FOR LISTINGS                                    */
/*----------------------------------------------------------------*/

 OUTQ:       CHGVAR     VAR(&OUTQ) VALUE(%SST(*LDA 401 10))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(*JOB))

 HOLD:       CHGVAR     VAR(&HOLD) VALUE(%SST(*LDA 411 4))
             IF         COND(&HOLD *NE '*YES' *AND &HOLD *NE '*NO ') +
                          THEN(CHGVAR VAR(&HOLD) VALUE(*NO))

 SAVE:       CHGVAR     VAR(&SAVE) VALUE(%SST(*LDA 415 4))
             IF         COND(&LDRPFL *EQ 'F' *AND &LDDEMAND = 'N') +
                          THEN(CHGVAR VAR(&SAVE) VALUE('*YES'))
             IF         COND(&SAVE *NE '*YES' *AND &SAVE *NE '*NO ') +
                          THEN(CHGVAR VAR(&SAVE) VALUE(*NO))

 COPY:       CHGVAR     VAR(&COPY) VALUE(%SST(*LDA 419 1))
             IF         COND(&COPY = 0) THEN(CHGVAR VAR(&COPY) +
                          VALUE(1))

/*---------------------------------------------------------------------*/
/* CALL THE PRINT PROGRAM                                              */
/*---------------------------------------------------------------------*/

/* WE HAVE TO ULTIMATELY EMAIL THE "FLOOR" REPORT GENERATED FROM THE   */
/* SYNON FUNCTION. SO, CONDITIONALLY SET THE "SAVE" PARM ON THE        */
/* PRINT FILE BEFORE OVERRIDING THE PRINT FILE FOR THE REPORTS.        */

             CHGVAR     VAR(&SAVE)      VALUE('*NO ')
             IF         COND(&LDRPFL *EQ 'F' *AND &LDDEMAND = 'N') +
                          THEN(DO)
             CHGVAR     VAR(&SAVE) VALUE('*YES')
             ENDDO

             OVRPRTF    FILE(PRINT198) OUTQ(&OUTQ) COPIES(&COPY) +
                          HOLD(&HOLD) SAVE(&SAVE)

/* REVENUE REPORT                                                      */

             IF         COND(&LDRPFL *EQ 'R') THEN(DO)
             CALL       PGM(TF611)

      /*------------------------------------------------------------------*/
      /* R12011 - Print Report Revenue: Weekly Product Revenue Report -   */
      /*     ALL; which includes STF data on Legal Size Paper.            */
      /*------------------------------------------------------------------*/
             OVRPRTF    FILE(PRINT198) PAGESIZE(8.5 14 *UOM) LPI(12) +
                          CPI(20) OVRFLW(81) DRAWER(2) PAGRTT(90) +
                          UOM(*INCH) OUTQ(&OUTQ) COPIES(&COPY) +
                          SCHEDULE(*FILEEND) HOLD(&HOLD) SAVE(&SAVE)

             CALL       PGM(TF6110)

             ENDDO

/* FLOOR PRODUCTION REPORT                                             */
/*    THE "FLOOR" REPORT IS ON-DEMAND FROM THIS FUNCTION AND IS ALSO   */
/*    GENERATED AUTOMATICALLY FROM A SYNON "POST INVENTORY VALUES"     */
/*    FUNCTION. WHEN IT IS FROM THE SYNON FUNCTION, WE E-MAIL IT.      */

             IF         COND(&LDRPFL *EQ 'F') THEN(DO)
             CALL       PGM(TF608)

             IF         COND(&LDDEMAND *EQ 'N') THEN(DO)
             CHGVAR     VAR(&LDEMAIL) VALUE('TF Revenue Close-TF')
             IF COND(&DATLIB = 'PRKFLIB   ') THEN(DO)
         ESNDMAIL   RECIPIENT(&LDEMAIL) SUBJECT('TF608-Weekly +
                          Product Floor Production Report') +
                          MSG('Distribution List: TF Revenue +
                          Close-TF') ATTLIST((* *PDF *N PRINT198 *))
             MONMSG     MSGID(CPF0000)
             ENDDO
             ELSE DO
             ESNDMAIL   RECIPIENT(&TESTMAIL) SUBJECT('TF608-Weekly +
                          Product Floor Production Report-TEST') +
                          MSG('Distribution List: TF Revenue +
                          Close-TF') ATTLIST((* *PDF *N PRINT198 *))
             MONMSG     MSGID(CPF0000)
             ENDDO
             ENDDO

      /*------------------------------------------------------------------*/
      /* R12011 - Print Report Revenue: Weekly Product Floor Production - */
      /*     ALL; which includes STF data on Legal Size Paper.            */
      /*------------------------------------------------------------------*/
             OVRPRTF    FILE(PRINT198) PAGESIZE(8.5 14 *UOM) LPI(12) +
                          CPI(20) DRAWER(2) PAGRTT(90) +
                          OUTQ(&OUTQ) COPIES(&COPY) +
                          SCHEDULE(*FILEEND) HOLD(&HOLD) SAVE(&SAVE)

             CALL       PGM(TF6080)

             IF         COND(&LDDEMAND *EQ 'N') THEN(DO)
             CHGVAR     VAR(&LDEMAIL) VALUE('TF Revenue Close-TF')
             IF         COND(&DATLIB = 'PRKFLIB   ') THEN(DO)
             ESNDMAIL   RECIPIENT(&LDEMAIL) SUBJECT('TF6080-Weekly +
                          Product Floor Production Report - ALL') +
                          MSG('Distribution List: TF Revenue +
                          Close-TF') ATTLIST((* *PDF *N PRINT198 *))
             MONMSG     MSGID(CPF0000)
             ENDDO
             ELSE DO
             ESNDMAIL   RECIPIENT(&TESTMAIL) SUBJECT('TF6080-Weekly +
                          Product Floor Production Report - ALL - +
                          TEST') MSG('Distribution List: TF Revenue +
                          Close-TF') ATTLIST((* *PDF *N PRINT198 *))
             MONMSG     MSGID(CPF0000)
             ENDDO
/*R12011*/   ENDDO

             ENDDO      /* &LDRPFL *EQ 'F' */

/* SALES PRODUCTION REPORT                                             */

             IF         COND(&LDRPFL *EQ 'S') THEN(DO)
             CALL       PGM(TF625)
             ENDDO


             CLOF       OPNID(TFL010B)
             DLTOVR     FILE(*ALL)

             ENDPGM

