     A*%%TS  SD  20080912  110154  ISLRAMS     REL-V5R4M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        TRIUMPH FOODS
     A*  DISPLAY FILE:  TFD411
     A*  TITLE:         SPECIFY WEEKLY PRODUCT REPORTS/DOWNLOAD
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  CREATED:       09/28/04
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/TFPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A                                      CF04(04 'PROMPT')
     A                                      CF08(08 'SUBMIT')
     A                                      CF17(17 'PRINT OPTIONS')
     A                                      CF20(20 'WRKSPLF')
     A*
     A*---------------------------------------------------------------
     A* RECORD FORMAT ONE
     A*---------------------------------------------------------------
     A          R TF411R1
     A*%%TS  SD  20080912  110154  ISLRAMS     REL-V5R4M0  5722-WDS
     A                                      SETOF(29)
     A                                      SETOF(30)
     A                                      SETOF(31)
     A                                      SETOF(32)
     A                                      SETOF(33)
     A                                      SETOF(34)
     A                                      SETOF(35)
     A                                      SETOF(36)
     A                                      SETOF(37)
     A                                      SETOF(38)
     A                                      SETOF(39)
     A                                      SETOF(40)
     A                                      SETOF(45)
     A                                      SETOF(46)
     A                                      SETOF(47)
     A                                      SETOF(49)
     A                                      SETOF(50)
     A                                      SETOF(51)
     A                                      SETOF(52)
     A                                      SETOF(53)
     A                                      SETOF(54)
     A                                      SETOF(55)
     A                                      SETOF(56)
     A                                      SETOF(57)
     A                                      SETOF(58)
     A                                      SETOF(59)
     A                                      SETOF(75)
     A                                      OVERLAY
     A                                      RTNCSRLOC(&RCD &FLD)
     A            RCD       R        H      REFFLD(@@RCD)
     A            FLD       R        H      REFFLD(@@FLD)
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 14'SPECIFY OPTIONS FOR WEEKLY PRODUCT-
     A                                       REPORTS/DOWNLOAD'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USR)
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@JOB)
     A                                      DSPATR(HI)
     A                                  3  2'From date ..'
     A            D1FMDY    R     Y  B  3 16REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A N37                                  DSPATR(HI)
     A  37                                  DSPATR(PC)
     A  37                                  DSPATR(RI)
     A                                  3 27'Week-ending:'
     A            D1FWEMDY  R     Y  O  3 40REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A                                  4  2'To date ....'
     A            D1TMDY    R     Y  B  4 16REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A N36                                  DSPATR(HI)
     A  36                                  DSPATR(PC)
     A  36                                  DSPATR(RI)
     A                                  4 27'Week-ending:'
     A            D1TWEMDY  R     Y  O  4 40REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A                                  5  2'Report/download ..................-
     A                                      .'
     A                                  5 38'?'
     A            D1RPFL    R        B  5 40REFFLD(@@FLAG)
     A N46                                  DSPATR(HI)
     A  46
     AO 56                                  DSPATR(PC)
     A  46                                  DSPATR(RI)
     A            D1RPDS        38A  O  5 42
     A                                  6  2'Detail or summary ................-
     A                                      .'
     A                                  6 38'?'
     A            D1DSFL    R        B  6 40REFFLD(@@FLAG)
     A N35                                  DSPATR(HI)
     A  35
     AO 55                                  DSPATR(PC)
     A  35                                  DSPATR(RI)
     A            D1DSDS        10A  O  6 42
     A                                  7  2'Sort/print sequence ..............-
     A                                      .'
     A                                  7 38'?'
     A            D1VER     R        B  7 40REFFLD(@@FLAG)
     A N30                                  DSPATR(HI)
     A  30
     AO 50                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A            D1VERDS       30A  O  7 42
     A                                  8  2'Exempt Only/Non-Exempt Only/Both .-
     A                                      .'
     A                                  8 38'?'
     A            D1IECD    R        B  8 40REFFLD(TFREF/@@IECD *LIBL/TFPREF)
     A N47                                  DSPATR(HI)
     A  47
     AO 57                                  DSPATR(PC)
     A  47                                  DSPATR(RI)
     A            D1IEDS    R        O  8 42REFFLD(TFREF/@@IEDS *LIBL/TFPREF)
     A                                  9  2'CoOwned Only/Not CoOwned Only/Both-
     A                                      .'
     A                                  9 38'?'
     A            D1COFL    R        B  9 40REFFLD(TFREF/@@IECD *LIBL/TFPREF)
     A N29                                  DSPATR(HI)
     A  29
     AO 49                                  DSPATR(PC)
     A  29                                  DSPATR(RI)
     A            D1CODS        30A  O  9 42
     A                                 10  2'Export Only/Domestic Only/Both ...-
     A                                      .'
     A                                 10 38'?'
     A            D1EDFL    R        B 10 40REFFLD(TFREF/@@EDFL *LIBL/TFPREF)
     A N45                                  DSPATR(HI)
     A  45
     AO 75                                  DSPATR(PC)
     A  45                                  DSPATR(RI)
     A            D1EDDS        25A  O 10 42
     A                                 12  2'Email address ....'
     A            D1EMAIL   R        B 12 23REFFLD(TFREF/@@EMAIL *LIBL/TFPREF)
     A                                      CHGINPDFT(UL LC FE)
     A N40                                  DSPATR(HI)
     A  40                                  DSPATR(PC)
     A  40                                  DSPATR(RI)
     A                                 14  2'Item Structure Codes:'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 15  7'Type ............'
     A                                 15 25'?'
     A            D1ISTYCD  R        B 15 27REFFLD(TFREF/@@ISTYCD *LIBL/TFPREF)
     A                                      EDTCDE(M)
     A N31                                  DSPATR(HI)
     A  31
     AO 51                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A            D1ISTYDS  R        O 15 36REFFLD(TFREF/@@ISTYDS *LIBL/TFPREF)
     A                                 15 67'(Blank = All)'
     A                                 16  7'Group ...........'
     A                                 16 25'?'
     A            D1ISGRCD  R        B 16 27REFFLD(TFREF/@@ISGRCD *LIBL/TFPREF)
     A                                      EDTCDE(M)
     A N32                                  DSPATR(HI)
     A  32
     AO 52                                  DSPATR(PC)
     A  32                                  DSPATR(RI)
     A            D1ISGRDS  R        O 16 36REFFLD(TFREF/@@ISGRDS *LIBL/TFPREF)
     A                                 16 67'(Blank = All)'
     A                                 17  7'Class ...........'
     A                                 17 25'?'
     A            D1ISCLCD  R        B 17 27REFFLD(TFREF/@@ISCLCD *LIBL/TFPREF)
     A                                      EDTCDE(M)
     A N33                                  DSPATR(HI)
     A  33
     AO 53                                  DSPATR(PC)
     A  33                                  DSPATR(RI)
     A            D1ISCLDS  R        O 17 36REFFLD(TFREF/@@ISCLDS *LIBL/TFPREF)
     A                                 17 67'(Blank = All)'
     A                                 18  2'TF Structure Codes:'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 19  7'Class group .....'
     A                                 19 25'?'
     A            D1TFCGCD  R        B 19 27REFFLD(TFREF/@@TFCGCD *LIBL/TFPREF)
     A N38                                  DSPATR(HI)
     A  38
     AO 58                                  DSPATR(PC)
     A  38                                  DSPATR(RI)
     A            D1TFCGDS  R        O 19 36REFFLD(TFREF/@@TFCGDS *LIBL/TFPREF)
     A                                 19 67'(Blank = All)'
     A                                 20  7'Classification ..'
     A                                 20 25'?'
     A            D1TFCLCD  R        B 20 27REFFLD(TFREF/@@TFCLCD *LIBL/TFPREF)
     A N39                                  DSPATR(HI)
     A  39
     AO 59                                  DSPATR(PC)
     A  39                                  DSPATR(RI)
     A            D1TFCLDS  R        O 20 36REFFLD(TFREF/@@TFCLDS *LIBL/TFPREF)
     A                                 20 67'(Blank = All)'
     A                                 21  2'Product code .........'
     A                                 21 25'?'
     A            D1PRCD    R        B 21 27REFFLD(TFREF/@@PRCD *LIBL/TFPREF)
     A N34                                  DSPATR(HI)
     A  34
     AO 54                                  DSPATR(PC)
     A  34                                  DSPATR(RI)
     A            D1PRDS    R        O 21 36REFFLD(TFREF/@@PRDS *LIBL/TFPREF)
     A                                 21 67'(Blank = All)'
     A*
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R TF411K1
     A*%%TS  SD  20060418  143047  ISLRAMS     REL-V5R3M0  5722-WDS
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A                                 23 11'F4=Prompt'
     A                                 23 22'F8=Submit'
     A                                 23 33'F17=Print Options'
     A                                 23 52'F20=Spool Files'
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *---------------------------------------------------------------
     A          R TF411ES                   SFL SFLMSGRCD(24)
      *
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
     A*---------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*---------------------------------------------------------------
     A*
     A          R TF411EC                   SFLCTL(TF411ES)
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
