/*********************************************************************/
/* PROGRAM : PVISUPC                                                 */
/* DESC    : CRT BACKUP AFT EDI RESTCL EXECUTE USER PROGRAM          */
/* DATE    : 11/25/2024                                              */
/* PROJECT : WI568 - WAREHOUSE INVENTORY MANAGEMENT (WIM)            */
/* BY      : DANNY NGUYEN                                            */
/* PURPOSE : Process will make a backup copies of the Item Balance   */
/*           and Item Balance Detail files for Legacy after the Job  */
/*           Schedule completes.                                     */
/*                                                                   */
/* HOW USE : The Legacy JS 'IB846AS400' runs at 3:30am. When job     */
/*           completes, call this program to create backup.          */
/*                                                                   */
/*********************************************************************/
/* MAINTENANCE LOG:                                                  */
/*-------------------------------------------------------------------*/
/* DATE     BY  PROJ      CHANGE DESCRIPTION                         */
/* -------- --- --------- ------------------------------------------ */
/*                                                                   */
/*********************************************************************/

             PGM

             DCL        VAR(&DATA) TYPE(*CHAR) LEN(10)
             DCL        VAR(&DATAH) TYPE(*CHAR) LEN(10)
             DCL        VAR(&CPYLGYIB) TYPE(*CHAR) LEN(10) +
                          VALUE('CAB0REP') /* Copy To Legacy +
                          Item Balance File */
             DCL        VAR(&CPYLGYIBD) TYPE(*CHAR) LEN(10) +
                          VALUE('CAB1REP') /* Copy To Legacy +
                          Item Balance Detail File */
             DCL        VAR(&MSG) TYPE(*CHAR) LEN(512)
             DCL        VAR(&MSGID) TYPE(*CHAR) LEN(7)
             DCL        VAR(&MSGL) TYPE(*DEC) LEN(5 0)


/*------------------------------------------------------------------*/
/* GET DATA LIBRARY.                                                */
/*------------------------------------------------------------------*/
             CALL       PGM(CAQXXFR) PARM(' ' &DATA &DATAH)


    /*Backup Legacy Files */
       /* Concat 'EDI' Suffix to Filename. */
             CHGVAR     VAR(&CPYLGYIB) VALUE(&CPYLGYIB *TCAT 'EDI')
             CHGVAR     VAR(&CPYLGYIBD) VALUE(&CPYLGYIBD *TCAT 'EDI')

       /* Validate Copy to Library/Filename Exist. */
          /* Legacy Item Balance */
             CHKOBJ     OBJ(&DATA/&CPYLGYIB) OBJTYPE(*FILE)
             MONMSG     MSGID(CPF9801) EXEC(DO)
             SNDPGMMSG  MSG('Copy To File ' *CAT &DATA *TCAT '/' +
                          *CAT &CPYLGYIB *TCAT ' not found.')
             GOTO       CMDLBL(MSGTRAP)
             ENDDO
         /* Legacy Item Balance Detail */
             CHKOBJ     OBJ(&DATA/&CPYLGYIBD) OBJTYPE(*FILE)
             MONMSG     MSGID(CPF9801) EXEC(DO)
             SNDPGMMSG  MSG('Copy To File ' *CAT &DATA *TCAT '/' +
                          *CAT &CPYLGYIBD *TCAT ' not found.')
             GOTO       CMDLBL(MSGTRAP)
             ENDDO

    /* No Errors then Copy to Backup Files */
       /* Legacy Item Balance */
             CPYF       FROMFILE(&DATA/CAB0REP) +
                          TOFILE(&DATA/&CPYLGYIB) MBROPT(*REPLACE) +
                          FROMRCD(1)
       /* Legacy Item Balance Detail */
             CPYF       FROMFILE(&DATA/CAB1REP) +
                          TOFILE(&DATA/&CPYLGYIBD) MBROPT(*REPLACE) +
                          FROMRCD(1)

             GOTO       CMDLBL(END)


/*----------------------RECEIVE MESSAGES----------------------------*/
 MSGTRAP:    RCVMSG     MSGTYPE(*EXCP) MSGDTA(&MSG) MSGDTALEN(&MSGL) +
                          MSGID(&MSGID)
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(END))

             SNDPGMMSG  MSGID(&MSGID) MSGF(QCPFMSG) MSGDTA(%SST(&MSG +
                          1 &MSGL)) MSGTYPE(*ESCAPE)
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(END))

 END:
             ENDPGM
