      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Hog Production System
      * PROGRAM:     HP3019
      * TITLE:       Build Workfile: Weeks in Date Range for Missing Sire Line Doses Report
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     05/04/04
      *
      * FUNCTION:  Batch program to build workfile of weeks in a date range.
      *
      *            This file was created for the Missing Sire Line Doses Report
      *            which is submitted from HP4011-Work with Weekly Sire Line Doses.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fhsp3019   o    e           k disk
      *  Workfile: Weeks in date range
      *
      /EJECT
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Control fields
      *
     D procfl          s              1    inz('Y')
     D first           s              1    inz('Y')
      *
      *
      * Parms
      *
     D xxccyymmdd      s              8  0
     D xxcdyr          s              4  0
     D xxcdwk          s              2  0
     D xxwbdt          s              8  0
     D xxwedt          s              8  0
      *
      *
      * Work fields for date manipulation
      *
     D wkisodate       s               D   datfmt(*iso)
     D wkccyymmdd      s              8  0
     D wkfcymd         s              8  0
     D wktcymd         s              8  0
      *
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *---------------------------------------------------------------
      * Data areas
      *---------------------------------------------------------------
      *
     Dlda             uds                  dtaara(*lda)
     D  ldfcdyr               15     18  0
     D  ldfcdwk               19     20  0
     D  ldtcdyr               21     24  0
     D  ldtcdwk               25     26  0
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * MAINLINE
      ****************************************************************
      *
      * Perform a loop from the 'FROM' date thru the TO date.
      *
     C                   dou       procfl = no                                  Main do
      *
      * First time
     C                   select                                                 Select 1
     C                   when      first = yes
     C                   move      no            first
     C     *iso          move      wkfcymd       wkisodate
     C                   move      wkisodate     wkccyymmdd
     C                   exsr      $wrt3019
     C                   other
      *
      * Increment last date processed by 7 days.
      *
     C                   adddur    7:*days       wkisodate
     C                   move      wkisodate     wkccyymmdd
      *
      * Continue processing with new date.
      *
     C                   select                                                 Select 2
      *
      * Logic for 'final' week when user selected multiple weeks.
      *
     C                   when      wkccyymmdd = wktcymd
     C                   exsr      $wrt3019
     C                   move      no            procfl
      *
      * You will hit this code when the user selected a single week.
      *
     C                   when      wkccyymmdd > wktcymd
     C                   move      no            procfl
     C                   other
      *
      * Normal week processing
      *
     C                   exsr      $wrt3019
     C                   endsl                                                  Select 2
      *
     C                   endsl                                                  Select 1
     C                   enddo                                                  Main do
      *
      * EOF processing
     C                   seton                                        lr
      /EJECT
      *--------------------------------------------------------------------------
      * Write workfile record
      *--------------------------------------------------------------------------
      *
     C     $wrt3019      begsr
      *
      * Call a program that will return the calendar year/week for this
      * date.
     C                   call      'HP8010'
     C                   parm      wkccyymmdd    xxccyymmdd
     C     w1cdyr        parm      0             xxcdyr
     C     w1cdwk        parm      0             xxcdwk
      *
      *
     C                   z-add     wkccyymmdd    w1wedt
      *
      * Flip week-ending date to MMDDYY format
      *
     C     *iso          test(d)                 w1wedt                 92
     C                   if        *in92 = *off                                 If good date
     C                   move      w1wedt        wkisodate
     C     *mdy          move      wkisodate     w1wemdy
     C                   write     w1rec
     C                   clear                   w1rec
     C                   endif                                                  If good date
      *
     C                   endsr
      /EJECT
      *-----------------------------------------------------------------------
      * Retrieve beginning/ending dates associated with calendar year/week
      *-----------------------------------------------------------------------
      *
      * Call a generic program to retrieve beginning/ending dates associated with
      * a calendar year/week. Dates are retrieved from the OMS Calendar file using Synon.
      *
     C     $dates        begsr
      *
     C                   call      'HP8016'
     C                   parm                    xxcdyr
     C                   parm                    xxcdwk
     C                   parm      0             xxwbdt
     C                   parm      0             xxwedt
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Retrieve the week-ending date of the FROM calendar year/week.
      *
     C                   z-add     ldfcdyr       xxcdyr
     C                   z-add     ldfcdwk       xxcdwk
     C                   exsr      $dates
     C                   if        xxwedt <> 0
     C                   z-add     xxwedt        wkfcymd
     C                   endif
      *
      * Retrieve the week-ending date of the TO calendar year/week.
      *
     C                   z-add     ldtcdyr       xxcdyr
     C                   z-add     ldtcdwk       xxcdwk
     C                   exsr      $dates
     C                   if        xxwedt <> 0
     C                   z-add     xxwedt        wktcymd
     C                   endif
      *
     C                   endsr
      /EJECT
