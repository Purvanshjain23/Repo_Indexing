     ******************************************************************
     ** DAILY'S PROGRAM WITH CHAINS TO OMS FILES
     ******************************************************************
     ** APPLICATION:    JRD ACCRUALS
     ** PROGRAM:        JRACCR
     ** DATE:           10/00
     ** PROGRAMMER:     TAE
     **
     ** DESCRIPTION:    ACCRUALS INQUIRY
     **
     ******************************************************************
     ** MODIFICATIONS:
     ** 11/20/2007 LJB DLY029  IF NO REC FOUND IN JRCUSTS, USE
     **            1. IF NO REC FOUND IN JRCUSTS, USE THE OMS CUSTOMER
     **               FILE TO GET THE CUSTOMER NAME
     **            2. IF THE ITEM# IS NOT ZEROS (REC 4) THEN GET THE
     **               ITEM DESCRIPTION FROM OMS ITEM DEFAULT FILE,
     **               IF NO REC FOUND, USE THE DESC FROM ACCRUALS FILE
     ******************************************************************
     H            Y
     FACCRUALSIP  E           K        DISK
     FJRCUSTS IF  E           K        DISK
     FCABBREL1IF  E           K        DISK
     FOMBYREL1IF  E           K        DISK
     FLIST80  O   F      80     OF     PRINTER                            HYD3
     F**************************************************
     IACCRUAL     01
     I                                              CUST  L1
     ISYSDA1      DS
     I                                        1   7 $NUMB
     I                                        8  55 $NAME
     I                                       56  70 $APPL
     I                                       71 100 $BLANK
     C*****************************************************************
     C*
     C           *NAMVAR   DEFN           SYSDA1
     C                     IN   SYSDA1
     C*
     C           *IN20     IFEQ *OFF
     C                     MOVE *DATE     NEWDTE  80
     C                     TIME           CLOCK  140
     C                     MOVELCLOCK     NEWTIM  40
     C                     SETON                     20
     C                     Z-ADD0         RRN1    40
     C                     EXCPTHEADER
     C                     ENDIF
     C*
     C* DLY029 IF ITEM# IS NOT 0, GET DESC FROM OMS ITEM DEFAULT FILE.
     C*   IF NO OMBYREP REC, JUST USE THE DESC FROM ACCRUALS RECORD
     C*
     C           PROD      IFNE 0
     C                     Z-ADDPROD      BYHGCD
     C           BYHGCD    CHAINOMBYREL1             91
     C           *IN91     IFEQ *OFF
     C                     MOVELBYAVNA    DESC
     C                     ENDIF
     C                     ENDIF
     C*
     C   L1      CUST      CHAINJRCUSTS              99
     C*
     C* DLY029 IF NO REC IN JRCUSTS, GET FROM CABBREP
     C*
     C           *IN99     IFEQ *ON
     C                     Z-ADDCUST      BBBKC7
     C           BBBKC7    CHAINCABBREL1             98
     C           *IN98     IFEQ *ON
     C                     MOVE *BLANKS   NAME
     C                     MOVEL'*INVALID'NAME
     C                     ELSE
     C                     MOVE *BLANKS   NAME
     C                     MOVELBBAYTX    NAME
     C                     ENDIF
     C                     ENDIF
     C*
     C   L1                EXCPTL1HEAD
     C*
     C   01                GOTO CALCS
     C                     GOTO END
     C           CALCS     TAG
     C           ACCAMT    ADD  TOTDUE    TOTDUE  82
     C           ACCAMT    ADD  ALLDUE    ALLDUE  82
     C                     EXCPTDETAIL
     C                     GOTO END
     C           END       TAG
     CL1                   EXCPTL1TOTL
     CLR                   EXCPTFOOTER
     C***************************************************************
     OLIST80  E  204           HEADER                                     HYD3
     O       OR        OF
     O                                    8 'JRACCPD1'
     O                         $NAME     58                               WHL003
     O                         $APPL     75
     O        E  2             HEADER                                     HYD3
     O       OR        OF
     O                                   25 'ACCRUALS REPORT'             HYD3
     O                                   31 'AS OF'                       HYD3
     O                         NEWTIM    36                               HYD3
     O                         NEWDTEY   48                               HYD3
     O                                   70 'PAGE'
     O                         PAGE  Z   75
     O        E  2             HEADER                                     HYD3
     O       OR        OF
     O                                    5 'ITEM#'
     O                                   13 'DESC'
     O                                   49 'DATE'
     O                                   59 'INVOICE'
     O                                   72 'AMOUNT'
     O                                   79 'VENDOR'
     O        E  2             L1HEAD                                     HYD3
     O       OR        OF
     O                         CUST       8
     O                         NAME      40
     O        EF 1             DETAIL                                     HYD3
     O                         PROD  Z    5
     O                         DESC      36
     O                         DATE      50 '0   /  /  '
     O                         INVOIC    59
     O                         ACCAMTL   73
     O                         VENDOR    79
     O        EF12             L1TOTL                                     HYD3
     O                                   58 'BALANCE'
     O                         TOTDUEJB  73
     O        E 2              FOOTER
     O                                   58 'GRAND TOTAL'
     O                         ALLDUEJB  73
     O                                   15 'END OF REPORT'
