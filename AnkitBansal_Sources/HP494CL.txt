/***************************************************************** */
/*  PROGRAM NUMBER --> HP494CL                                     */
/*  PROGRAM NAME ----> PRODUCTION ANALYSIS REPORT                  */
/*  PROGRAMMER ------> LEANNE FEDOR                                */
/*  DATE ------------> 01/30/95                                    */
/*                                                                 */
/*  PROGRAM PURPOSE -> THIS CL USES OPEN QUERY AND WORKFILES       */
/*                     TO PRODUCE THE REPORT.                      */
/*                     VALUES ARE PASSED THRU THE LDA FROM         */
/*                     SPECIFY HP494.                              */
/*                                                                 */
/***************************************************************** */
/* MODIFIED  PROGRAMMER                                            */
/*                                                                 */
/*  03/13/95  LEANNE FEDOR                                         */
/*            RECOMPILE ONLY.  ORIGIN BGF FARM SITE AND ORIGIN     */
/*            NURSERY FARM SITE ADDED TO HOG GROUP FILE            */
/*                                                                 */
/*  04/26/95  LEANNE FEDOR                                         */
/*            CHANGED FARM SITE FROM ALPHA TO NUMERIC.             */
/*                                                                 */
/*  11/01/95  LEANNE FEDOR                                         */
/*            RECOMPILE ONLY. NEW FIELD 'GROUP TYPE' ADDED TO      */
/*            HOG GROUP FILE.                                      */
/*                                                                 */
/* 04/25/96  LEANNE FEDOR                                          */
/*           ADDED CUSTOMIZED PRINTER OVERRIDE COMMAND TO FORCE    */
/*           REPORT TO PRINT ON LASER PRINTER.                     */
/*                                                                 */
/* 05/21/96  LEANNE FEDOR                                          */
/*           IN PREPARATION FOR HAVING TWO BOXES (CISC AND RISC),  */
/*           REPLACED STANDARD CREATE DUPLICATE OBJECT COMMAND     */
/*           WITH A COMMAND CUSTOMIZED BY JOHN ERICSON. THIS       */
/*           COMMAND WILL RETRIEVE/USE THE FILE FROM THE APPRO-    */
/*           PRIATE LIBRARY INSTEAD OF USING HARDCODED 'PRKFLIB'.  */
/*           THE CUSTOMIZED COMMAND IS CRTDUPOBJL FOR 'CREATE      */
/*           DUPLICATE OBJECT FROM LIBRARY'.                       */
/*                                                                 */
/* 06/03/97 LEANNE FEDOR                                           */
/*          RECOMPILE ONLY.  NEW FIELDS 'PURGE FLAG' AND 'DATE OF  */
/*          FIRST PURGE' ADDED TO HOG GROUP FILE.                  */
/*                                                                 */
/* 06/19/97 LEANNE FEDOR                                           */
/*          CHANGED OUT QUEUE LOGIC.                               */
/*                                                                 */
/* 10/01/01 LEANNE FEDOR                                           */
/*          ADDED 'GROUP TYPE' AS NEW REPORT SELECTION.            */
/*******************************************************************/

             PGM

/* DEFINE THE VARIABLE REQUIRED IN THE COMMAND TO OVERRIDE THE     */
/* PRINT FILE TO A LASER PRINTER                                   */

             DCL        VAR(&CMD) TYPE(*CHAR) LEN(512)

             DCL        VAR(&QDATA)  TYPE(*CHAR) LEN(157)
             DCL        VAR(&LDFSBO) TYPE(*CHAR) LEN(5)
             DCL        VAR(&LDFYMD) TYPE(*DEC)  LEN(8)
             DCL        VAR(&LDTYMD) TYPE(*DEC)  LEN(8)
             DCL        VAR(&LDPTCD) TYPE(*CHAR) LEN(5)
             DCL        VAR(&LDPPCD) TYPE(*CHAR) LEN(5)
             DCL        VAR(&LDGTCD) TYPE(*CHAR) LEN(1)

             DCL        VAR(&HOLD) TYPE(*CHAR) LEN(10)
             DCL        VAR(&COPYS) TYPE(*DEC) LEN(2)
             DCL        VAR(&OUTQ) TYPE(*CHAR) LEN(10)

             CHGVAR     VAR(&LDFSBO) VALUE(%SST(*LDA 1 5))
             CHGVAR     VAR(&LDFYMD) VALUE(%SST(*LDA 6 8))
             CHGVAR     VAR(&LDTYMD) VALUE(%SST(*LDA 14 8))
             CHGVAR     VAR(&LDPTCD) VALUE(%SST(*LDA 22 5))
             CHGVAR     VAR(&LDPPCD) VALUE(%SST(*LDA 27 5))
             CHGVAR     VAR(&LDGTCD) VALUE(%SST(*LDA 101 1))


/* ALWAYS SELECT ONLY CLOSED HOG GROUPS                           */

             CHGVAR     VAR(%SST(&QDATA 1 12)) VALUE('HGGSCD *EQ "')
             CHGVAR     VAR(%SST(&QDATA 13 2)) VALUE('CL')
             CHGVAR     VAR(%SST(&QDATA 15 1)) VALUE('"')

/* ALWAYS SELECT ONLY GROUPS WITH A CLOSE DATE IN THE SELECTED RANGE */

             CHGVAR     VAR(%SST(&QDATA 17 15)) VALUE('*AND HGCLDT +
                          *GE')
             CHGVAR     VAR(%SST(&QDATA 33 8)) VALUE(&LDFYMD)
             CHGVAR     VAR(%SST(&QDATA 42 15)) VALUE('*AND HGCLDT +
                          *LE')
             CHGVAR     VAR(%SST(&QDATA 58 8)) VALUE(&LDTYMD)

/*  ALWAYS SELECT A PRODUCTION PHASE                             */

             IF         COND(&LDPPCD *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 67 17)) VALUE('*AND HGPPCD +
                          *EQ "')
             CHGVAR     VAR(%SST(&QDATA 84 5)) VALUE(&LDPPCD)
             CHGVAR     VAR(%SST(&QDATA 89 1)) VALUE('"')
             ENDDO


/*  IF USER SELECTED A BUSINESS OFFICE, ADD SELECTION            */

             IF         COND(&LDFSBO *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 91 17)) VALUE('*AND HGFSBO +
                          *EQ "')
             CHGVAR     VAR(%SST(&QDATA 108 5)) VALUE(&LDFSBO)
             CHGVAR     VAR(%SST(&QDATA 113 1)) VALUE('"')
             ENDDO

/*  IF PRODUCTION TYPE IS NOT BLANK, THEN ADD A TYPE SELECTION */

             IF         COND(&LDPTCD *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 115 17)) VALUE('*AND HGPTCD +
                          *EQ "')
             CHGVAR     VAR(%SST(&QDATA 132 5)) VALUE(&LDPTCD)
             CHGVAR     VAR(%SST(&QDATA 137 1)) VALUE('"')
             ENDDO

/*  IF GROUP TYPE                                               */

             IF         COND(&LDGTCD *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 139 17)) VALUE('*AND HGGTCD +
                          *EQ "')
             CHGVAR     VAR(%SST(&QDATA 156 1)) VALUE(&LDGTCD)
             CHGVAR     VAR(%SST(&QDATA 157 1)) VALUE('"')
             ENDDO

/*------------------------------------------------------------------*/
/* RUN QUERY OVER THE HOG GROUP FILE                                 */
/*------------------------------------------------------------------*/

             OVRDBF     FILE(HSP034) SHARE(*YES)
             OPNQRYF    FILE((HSP034)) QRYSLT(&QDATA) KEYFLD(*FILE) +
                          SEQONLY(*YES)

/*------------------------------------------------------------------*/
/*  CREATE TWO WORKFILES AND BUILD WORKFILE DATE                    */
/*------------------------------------------------------------------*/

             CRTDUPOBJL OBJ(HSP393) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)

             CRTDUPOBJL OBJ(HSP394) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)

             CALL       PGM(HP394)

/*------------------------------------------------------------------*/
/* SETUP THE REPORT PRINT PARAMETERS                                */
/*------------------------------------------------------------------*/

 HOLD:       IF         COND(%SST(*LDA 411 1) = 'Y') THEN(CHGVAR +
                          VAR(&HOLD) VALUE(*YES))
             ELSE       CMD(CHGVAR VAR(&HOLD) VALUE(*NO))

 COPYS:      CHGVAR     VAR(&COPYS) VALUE(%SST(*LDA 412 1))
             IF         COND(&COPYS = 0) THEN(CHGVAR VAR(&COPYS) +
                          VALUE(1))

 OUTQ:       CHGVAR     VAR(&OUTQ) VALUE(%SST(*LDA 413 10))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(%SST(*LDA 401 10)))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(*JOB))

/*------------------------------------------------------------------*/
/* OVERRIDE PRINT FILE                                              */
/*------------------------------------------------------------------*/

/* USE THE CUSTOMIZED SEABOARD COMMAND TO OVERRIDE THE PRINT FILE.  */
/* THIS COMMAND ALLOWS THE REPORT TO BE GENERATED ON A STANDARD LINE*/
/* PRINTER OR A LASER PRINTER. TO USE THIS OVERRIDE YOU MUST DECLARE*/
/* A COMMAND VARIABLE AND FOLLOW THE OVERRIDE WITH A CALL TO        */
/* QCMDEXC.                                                         */

/* THIS REPORT IS CONDENSED PRINT OF 15CPI ON 198 COLUMNS OF PRINT  */
/* FORCE IT TO PRINT OUT OF DRAWER 2 ON LANDSCAPE                   */

             SBDPRTOVR  CMDVAR(&CMD) FILE(QPRINT) OUTQ(&OUTQ) +
                          WIDTH(198) LANDSCAPE(*YES) DRAWER(2) +
                          COPIES(&COPYS) HOLD(&HOLD)

             CALL       PGM(QCMDEXC) PARM(&CMD 512)


/*------------------------------------------------------------------*/
/*  RUN REPORT                                                      */
/*------------------------------------------------------------------*/

             CALL PGM(HP694)


EXIT:        CLOF       OPNID(HSP034)
             DLTOVR     FILE(*ALL)
             ENDPGM

