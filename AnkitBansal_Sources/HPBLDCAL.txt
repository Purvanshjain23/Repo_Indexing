/***************************************************************** */
/*                                                                 */
/*  PROGRAM NUMBER:  HPBLDCAL                                      */
/*  PROGRAM NAME:    BUILD CALENDAR FILE                           */
/*  PROGRAMMER:      BARB GUTIERREZ                                */
/*  CREATE DATE:     02/16/04                                      */
/*                                                                 */
/*  FUNCTION:        THIS CL IS SUBMITTED ANNUALLY FROM THE JOB    */
/*                   SCHEDULER ON BIGBYTE (BIBLDCAL)               */
/*                                                                 */
/*                   IT PERFORMS THE FOLLOWING FUNCTIONS:          */
/*                      CALLS THE PROGRAM THAT READS THE OMS       */
/*                      CALENDAR FILE AND WRITES TO THE            */
/*                      CALENDAR FILE.                             */
/*                                                                 */
/*                   AS OF 2007, THIS "CALENDAR" FILE IS ALSO      */
/*                   BEING USED FOR NON-DATAMART FUNCTIONS.        */
/*******************************************************************/
/* MODIFICATIONS:                                                  */
/*                                                                 */
/* 12/06/06  LEANNE RAMSEY                                         */
/*           WE ARE NOW USING THIS CALENDAR FILE IN NON-DATAMART   */
/*           FUNCTIONS. SO, WE CHANGED THE FILE NAME/TEXT FROM     */
/*           DMPCAL-DATAMART CALENDAR TO CAPCAL-CALENDAR.          */
/*                                                                 */
/* 06/20/07  SHARON ZURN                                           */
/*           COMMENTED OUT CLEAR OF CAPCAL. IF WE CLEAR IT WE LOSE */
/*           ALL THE DBU CHANGES THAT WE HAVE DONE FOR PREVIOUS YRS*/
/*                                                                 */
/* 04/29/10  LeAnne Ramsey  (C719)                                 */
/*           Changed logic to use ESEND instead of MPLUS/MPLUS.    */
/*******************************************************************/

             PGM

             DCL        VAR(&ERR)    TYPE(*CHAR) LEN(1) VALUE('N')

             DCL        VAR(&EMAIL)    TYPE(*CHAR) LEN(38)
             DCL        VAR(&TSTPRDFL) TYPE(*CHAR) LEN(1)
             DCL        VAR(&SUBJECT)  TYPE(*CHAR) LEN(60) +
                          VALUE('HPBLDCAL-Build Calendar File +
                          ..........DIST LIST: prk cognos')

/*------------------------------------------------------------------*/
/* Set up the Email Recipient (either Test or Production)           */
/*------------------------------------------------------------------*/

/* Retrieve the Data Area that lets us know whether we are in TEST  */
/* or PRODUCTION. (Always default to the 'TEST' Distribution List.) */

             CHGVAR     VAR(&EMAIL) VALUE('prk cognos test')
             RTVDTAARA  DTAARA(DATSTPRD (1 1)) RTNVAR(&TSTPRDFL)
             IF         COND(&TSTPRDFL *EQ 'P') THEN(CHGVAR +
                          VAR(&EMAIL) VALUE('prk cognos'))


/*********************************************************************/
/* CREATE THE DATA IN THE CAPCAL CALENDAR FILE IN PRKFLIB            */
/*********************************************************************/

             CALL       PGM(DM200) /* Build records for the new year  */
             CALL       PGM(DM201) /* Update the records just written */

/*********************************************************************/
/* COPY THE DATA TO THE FILE in PRKDMFLIB                            */
/*********************************************************************/

             CPYF       FROMFILE(PRKFLIB/CAPCAL) +
                          TOFILE(PRKDMFLIB/CAPCAL) MBROPT(*REPLACE) +
                          CRTFILE(*NO) FMTOPT(*MAP *DROP)
             MONMSG     MSGID(CPF2817) EXEC(CHGVAR VAR(&ERR) +
                          VALUE('Y'))

/*---------------------------------------------------------------------*/
/* SEND COMPLETION MESSAGE TO I.S. FOLKS                               */
/*---------------------------------------------------------------------*/

             IF         COND(&ERR = 'Y') THEN(ESNDMAIL +
                          RECIPIENT(&EMAIL) SUBJECT(&SUBJECT) +
                          MSG('The Calendar File (CAPCAL) did NOT +
                          get copied from PRKFLIB to PRKDMFLIB; +
                          please investigate.') IMPORTANCE(*HIGH))

             IF         COND(&ERR = 'N') THEN(ESNDMAIL +
                          RECIPIENT(&EMAIL) SUBJECT(&SUBJECT) +
                          MSG('The Calendar File (CAPCAL) was +
                          successfully copied from PRKFLIB to +
                          PRKDMFLIB; the build completed +
                          normally.') IMPORTANCE(*HIGH))

             ENDPGM

