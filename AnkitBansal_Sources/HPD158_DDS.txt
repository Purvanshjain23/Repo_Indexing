     A*%%TS  SD  19991111  114206  ISLRAMS     REL-V4R2M0  5769-PW1
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD158
     A*  TITLE:         MAINTAIN INVENTORY ADJUSTMENTS
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  CREATED:       03/29/95
     A*
     A*  INDICATOR 60 IS ON WHEN IN REVISE MODE
     A*  INDICATOR 61 IS ON WHEN IN CREATE MODE
     A*  INDICATOR 62 IS ON WHEN USER HAS CREATED A RECORD
     A*  INDICATOR 63 IS ON WHEN USER HAS DELETED A RECORD
     A*  INDICATOR 64 IS ON WHEN AN ADJUSTMENT IS POSTED AND MUST BE
     A*               PROTECTED
     A*  INDICATOR 89 THE USER CAME FROM THE DISPOSE OPEN HOG GROUPS FUNCTION.
     A*               DISPLAY THE DISPOSE DATE AND REMAINING HEAD VALUES THAT
     A*               WERE PASSED IN AS PARMS.
     A*  INDICATOR 64 IS ON WHEN AN ADJUSTMENT IS POSTED AND MUST BE
     A*               PROTECTED
     A*               FROM THE WORK WITH HAS BEEN DELETED BY ANOTHER
     A*               USER
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A N90N61                               CF04(04 'PROMPT')
     A N90                                  CF05(05 'REFRESH')
     A N90                                  CF10(10 'CHANGE MODES')
     A                                      CF12(12 'PREVIOUS')
     A*
     A*---------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP158S1                   SFL
     A*%%TS  SD  19950510  110445  ISLRAMS     REL-V3R0M5  5763-PW1
     A  85                                  SFLNXTCHG
     A            HDRRN1         4Y 0H
     A            HDIADT    R        H      REFFLD(IAIADT)
     A            DFOPT          2A  B 13  2CHGINPDFT(UL)
     A  61
     AO 62
     AO 63
     AO 64
     AO 90                                  DSPATR(PR ND)
     A  30                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A N30N61N90                            DSPATR(HI)
     A  60N30N90                            DSPATR(UL)
     A            DFMDY     R        B 13  9REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A  62
     AO 63
     AO 64
     AO 90                                  DSPATR(PR)
     A  63                                  DSPATR(ND)
     A  31                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A N31N62N64
     A N90                                  DSPATR(HI)
     A N31N62N64
     A N90                                  DSPATR(UL)
     A            DFIAHD    R        B 13 22REFFLD(IAIAHD)
     A  62
     AO 63
     AO 64
     AO 90                                  DSPATR(PR)
     A  63                                  DSPATR(ND)
     A                                      EDTCDE(K)
     A  34                                  DSPATR(PC)
     A  34                                  DSPATR(RI)
     A N34N62N64
     A N90                                  DSPATR(HI)
     A N34N62N64
     A N90                                  DSPATR(UL)
     A            DFMSCD    R        O 13 33REFFLD(IAMSCD)
     A  63                                  DSPATR(ND)
     A            DFMVSN    R     Y  O 13 41REFFLD(IAMVSN)
     A                                      EDTCDE(M)
     A  63                                  DSPATR(ND)
     A*
     A*---------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP158C1                   SFLCTL(HP158S1)
     A*%%TS  SD  19991028  092056  ISLRAMS     REL-V4R2M0  5769-PW1
     A                                      SFLSIZ(0100)
     A                                      SFLPAG(0009)
     A                                      SETOF(30)
     A                                      SETOF(31)
     A                                      SETOF(34)
     A                                      SETOF(56)
     A                                      SETOF(62)
     A N82N90                               ROLLUP(25 'PAGE FORWARD')
     A                                      SFLCSRRRN(&HDRR1)
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A                                      OVERLAY
     A                                      RTNCSRLOC(&RCD &FLD)
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            HDRR1          5S 0H
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 28'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A            DFMODE    R        O  2  2REFFLD(@@MODE)
     A                                      DSPATR(HI)
     A                                  2 23'MAINTAIN INVENTORY ADJUSTMENTS'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  4  2'Farm site ..:'
     A            C1FSCD    R        O  4 17REFFLD(FSFSCD)
     A                                      EDTCDE(M)
     A            C1FSNM    R        O  4 25REFFLD(FSFSNM)
     A                                  5  2'Hog group ..:'
     A            C1HGCD    R        O  5 17REFFLD(HGHGCD)
     A  89N90                           6 39'The values from the Dispose Screen-
     A                                       are:'
     A                                      DSPATR(HI)
     A N61                              7  2'4=Delete'
     A  89N90                           7 44'Dispose date ....:'
     A                                      DSPATR(HI)
     A  89N90     C1MDYDSDT R        O  7 64REFFLD(HSRREF/@@DT06 *LIBL/HSPREF)
     A                                      EDTWRD('  /  /  ')
     A  89N90                           8 44'Remaining head ..:'
     A                                      DSPATR(HI)
     A  89N90     C1HD      R        O  8 66REFFLD(IAREC/IAIAHD *LIBL/HSP069)
     A                                      EDTCDE(K)
     A                                  9  8'Adjustment'
     A N61                              9 33'?'
     A N61                             10  2'Opt'
     A                                 10 11'Date'
     A                                 10 24'Head'
     A                                 10 31'Status'
     A                                 10 41'Movement'
     A N90        C1MDY     R        B 11  9REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A  61                                  DSPATR(ND)
     A  61                                  DSPATR(PR)
     A N90        C1MSCD    R        B 11 33REFFLD(IAMSCD)
     A  56N61                               DSPATR(PC)
     A                                      DSPATR(HI)
     A  61                                  DSPATR(ND)
     A  61                                  DSPATR(PR)
     A N90        C1MVSN    R        B 11 41REFFLD(IAMVSN)
     A                                      DSPATR(HI)
     A  61                                  DSPATR(ND)
     A  61                                  DSPATR(PR)
     A*
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R HP158K1
     A*%%TS  SD  19991111  114206  ISLRAMS     REL-V4R2M0  5769-PW1
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A                                      DSPATR(HI)
     A                                 23  2'F3=Exit'
     A N61N90                          23 11'F4=Prompt'
     A N90                             23 22'F5=Refresh'
     A  60N90                          23 34'F10=Go to Create'
     A  61N90                          23 34'F10=Go to Revise'
     A                                 23 52'F12=Previous'
     A*
     A*--------------------------------------------------------------*
     A* ERROR MESSAGE SUBFILE FORMAT
     A*---------------------------------------------------------------
     A          R HP158ES                   SFL SFLMSGRCD(24)
     A*
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
     A*---------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*---------------------------------------------------------------
     A          R HP158EC                   SFLCTL(HP158ES)
     A*
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
