/*                                                                 */
/*  PROGRAM NUMBER:  HP375CL                                       */
/*  PROGRAM NAME:    COST MOVEMENTS                                */
/*  PROGRAMMER:      LEANNE FEDOR                                  */
/*  CREATE DATE:     04/24/03                                      */
/*                                                                 */
/*  FUNCTION:        THIS CL IS SUBMITTED FROM HP477CL-SPECIFY     */
/*                   PERIOD CLOSE.                                 */
/*                                                                 */
/*******************************************************************/
/*                                                                 */
/* 12/13/04  LEANNE FEDOR                                          */
/*           SEVERAL EXISTING FILES HAVE BEEN RENAMED AS WORKFILES:*/
/*             HSP113 ... HSP352                                   */
/*             HSP114 ... HSP357                                   */
/*             HSP115 ... HSP358                                   */
/*             HSP116 ... HSP359                                   */
/*             HSP117 ... HSP360                                   */
/*             HSP119 ... HSP361                                   */
/*           WE ADDED CLEARS FOR THESE FILES.                      */
/*           WE REMOVED ALL 'REORGS'.                              */
/*                                                                 */
/* 02/29/16    Rose Centonze    E5290                              */
/*             Added company code parm and add to opnqry select    */
/*             JOIN FILES TO HSP009 WITH BUSINESS OFFICE, SELECT COMPANY  */
/*******************************************************************/

             PGM        PARM(&ERROR &PPCOCD)

             DCL        VAR(&PPCOCD) TYPE(*CHAR) LEN(3)            /* E5290 */

             DCL        VAR(&ERROR) TYPE(*CHAR) LEN(1)
             DCL        VAR(&CMD) TYPE(*CHAR) LEN(512)
             DCL        VAR(&QDATA) TYPE(*CHAR) LEN(110)  /*E5290- WAS 90  */

             DCL        VAR(&LDACYR) TYPE(*DEC) LEN(4)
             DCL        VAR(&LDACPE) TYPE(*DEC) LEN(2)
             DCL        VAR(&LDBPDT) TYPE(*DEC) LEN(8)
             DCL        VAR(&LDEPDT) TYPE(*DEC) LEN(8)

             DCL        VAR(&HOLD) TYPE(*CHAR) LEN(4)
             DCL        VAR(&SAVE) TYPE(*CHAR) LEN(4)
             DCL        VAR(&COPYS) TYPE(*DEC) LEN(2)
             DCL        VAR(&OUTQ) TYPE(*CHAR) LEN(10)

             CHGVAR     VAR(&LDACYR) VALUE(%SST(*LDA 20 4))
             CHGVAR     VAR(&LDACPE) VALUE(%SST(*LDA 17 2))
             CHGVAR     VAR(&LDBPDT) VALUE(%SST(*LDA 1 8))
             CHGVAR     VAR(&LDEPDT) VALUE(%SST(*LDA 9 8))


/*********************************************************************/
/* CLEAR WORKFILES                                                   */
/*********************************************************************/

             CLRPFM     FILE(HSP352) /* Origin Groups               */
             CLRPFM     FILE(HSP357) /* Origin Group Feed           */
             CLRPFM     FILE(HSP358) /* Origin Group Expenses       */
             CLRPFM     FILE(HSP359) /* Origin Group Cull Sales     */
             CLRPFM     FILE(HSP360) /* Origin Group Inventory      */
             CLRPFM     FILE(HSP361) /* Origin Group Summarized Exp */
             CLRPFM     FILE(HSP362) /* Movement Detail Driver      */

/*********************************************************************/
/*  DELETE COSTED MOVEMENT DATA FOR THE PERIOD  AND COMPANY E5290  */
/*         DO COMPANY SELECTION IN THE PGM                         */
/*********************************************************************/

             CALL       PGM(HP8013) PARM(&LDACYR &LDACPE &PPCOCD)


/*********************************************************************/
/* SETUP OPNQRY STATEMENTS AND BUILD DRIVER FILE OF MOVEMENTS        */
/*********************************************************************/

/*  TRANSFER MOVEMENTS                                            */

             CHGVAR     VAR(&QDATA) VALUE(' ')
             CHGVAR     VAR(%SST(&QDATA 1 10)) VALUE('MHRCDT *GE')
             CHGVAR     VAR(%SST(&QDATA 12 8)) VALUE(&LDBPDT)
             CHGVAR     VAR(%SST(&QDATA 21 15)) VALUE('*AND MHRCDT +
                          *LE')
             CHGVAR     VAR(%SST(&QDATA 37 8)) VALUE(&LDEPDT)
             CHGVAR     VAR(%SST(&QDATA 46 20)) VALUE('*AND MHMSCD +
                          *EQ "SH"')

/*   SELECT COMPANY IN PARM          E5290                     */

             CHGVAR     VAR(%SST(&QDATA 67 15)) VALUE('*AND BOCOCD +
                          *EQ')
             CHGVAR     VAR(%SST(&QDATA 83 3)) VALUE(&PPCOCD)

             OVRDBF     FILE(HSP074) SHARE(*YES)
 /*          OPNQRYF    FILE((HSP074))  QRYSLT(&QDATA) +     ORIGINAL  +
                          KEYFLD(*FILE) SEQONLY(*YES)                  */
             OPNQRYF    FILE((HSP074) (HSP009)) FORMAT(HSP074) +
                          QRYSLT(&QDATA) KEYFLD(*FILE) +
                          JFLD((HSP074/MHORBO HSP009/BOBOBO)) +
                          SEQONLY(*YES)

/*  SALES MOVEMENTS                                               */

             CHGVAR     VAR(&QDATA) VALUE(' ')
             CHGVAR     VAR(%SST(&QDATA 1 10)) VALUE('SHRCDT *GE')
             CHGVAR     VAR(%SST(&QDATA 12 8)) VALUE(&LDBPDT)
             CHGVAR     VAR(%SST(&QDATA 21 15)) VALUE('*AND SHRCDT +
                          *LE')
             CHGVAR     VAR(%SST(&QDATA 37 8)) VALUE(&LDEPDT)
             CHGVAR     VAR(%SST(&QDATA 46 23)) VALUE('*AND SHSTCD +
                          *NE "CULLS"')
             CHGVAR     VAR(%SST(&QDATA 70 20)) VALUE('*AND SHMSCD +
                          *EQ "FP"')

/*   SELECT COMPANY IN PARM          E5290                     */

             CHGVAR     VAR(%SST(&QDATA 91 15)) VALUE('*AND BOCOCD +
                          *EQ')
             CHGVAR     VAR(%SST(&QDATA 107 3)) VALUE(&PPCOCD)


             OVRDBF     FILE(HSP084) SHARE(*YES)
/*           OPNQRYF    FILE((HSP084))  QRYSLT(&QDATA)        ORIGINAL      +
                          KEYFLD(*FILE) SEQONLY(*YES)        */
             OPNQRYF    FILE((HSP084) (HSP009)) FORMAT(HSP084) +
                          QRYSLT(&QDATA) KEYFLD(*FILE) +
                          JFLD((HSP084/SHFSBO HSP009/BOBOBO)) +
                          SEQONLY(*YES)

             CALL       PGM(HP317)

             CLOF       OPNID(HSP074)
             CLOF       OPNID(HSP084)
             DLTOVR     FILE(*ALL)

/*********************************************************************/
/*  SET UP PRINT FILE OVERRIDES                                      */
/*********************************************************************/

/* AS OF MARCH 2004, USERS ASKED US TO ALWAYS PUT OUTPUT ON SAVE/HOLD */

             CHGVAR     VAR(&SAVE) VALUE(*YES)
             CHGVAR     VAR(&HOLD) VALUE(*YES)

/* SET PARAMETERS FROM LDA                                           */

COPYS:       CHGVAR     VAR(&COPYS) VALUE(%SST(*LDA 412 1))
             IF         COND(&COPYS = 0) THEN(CHGVAR VAR(&COPYS) +
                          VALUE(1))

OUTQ:        CHGVAR     VAR(&OUTQ) VALUE(%SST(*LDA 413 10))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(%SST(*LDA 401 10)))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(*JOB))


             SBDPRTOVR  CMDVAR(&CMD) FILE(QPRINT) OUTQ(&OUTQ) +
                          WIDTH(85) LANDSCAPE(*PRTF) DRAWER(*PRTF) +
                          COPIES(&COPYS) HOLD(&HOLD) SAVE(&SAVE)

             CALL       PGM(QCMDEXC) PARM(&CMD 512)

/*********************************************************************/
/* BUILD ORIGIN GROUP DATA                                           */
/*********************************************************************/

             CALL       PGM(HP318) PARM(&ERROR)
             DLTOVR     FILE(QPRINT)

/*********************************************************************/
/* IF THERE WERE NO ERRORS BUILDING GROUP DATA, COST MOVEMENTS       */
/*********************************************************************/

             IF         COND(&ERROR *EQ 'N') THEN(DO)
             CALL       PGM(HP319)
             ENDDO

             ENDPGM

