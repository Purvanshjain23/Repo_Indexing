/***************************************************************** */
/*  PROGRAM NUMBER --> HP455CL                                     */
/*  PROGRAM NAME ----> DEATH REPORTS                               */
/*  PROGRAMMER ------> LEANNE FEDOR                                */
/*  DATE ------------> 05/11/95                                    */
/*                                                                 */
/*  FUNCTION --------> THIS CL CALLS THE PROGRAMS TO PRINT         */
/*                     DEATH REPORTS. ONE SET OF QUERIES IS        */
/*                     RUN FOR THE DAILY DEAD REPORTS AND A        */
/*                     DIFFERENT SET IS RUN FOR THE REPORT OF      */
/*                     DAILY DEAD/DOA/REJECTS.                     */
/*                                                                 */
/***************************************************************** */
/*  MODIFIED:                                                      */
/*  10/11/95  LEANNE FEDOR                                         */
/*            ADDED SUMMARY/DETAIL VERSION OF DEATH REPORT BY FARM */
/*                                                                 */
/*  10/19/95  LEANNE FEDOR                                         */
/*            ADDED NEW DOA REPORT BY FARM--SUMMARY VERSION ONLY   */
/*                                                                 */
/*  11/01/95  LEANNE FEDOR                                         */
/*            RECOMPILE ONLY. NEW FIELD 'GROUP TYPE' ADDED TO      */
/*            HOG GROUP FILE.                                      */
/*                                                                 */
/*  02/06/96  LEANNE FEDOR                                         */
/*            RECOMPILE ONLY. NEW FIELD 'BOL PRINTED COUNT' ADDED  */
/*            TO SALES MOVEMENT HEADER FILE.                       */
/*                                                                 */
/*  02/13/96  LEANNE FEDOR                                         */
/*            RECOMPILE ONLY. NEW FIELD 'BOL PRINTED COUNT' ADDED  */
/*            TO TRANSFER MOVEMENT HEADER FILE.                    */
/*                                                                 */
/* 04/23/96  LEANNE FEDOR                                          */
/*           ADDED CUSTOMIZED PRINTER OVERRIDE COMMAND TO FORCE    */
/*           REPORT TO PRINT ON LASER PRINTER.                     */
/*                                                                 */
/* 05/21/96  LEANNE FEDOR                                          */
/*           IN PREPARATION FOR HAVING TWO BOXES (CISC AND RISC),  */
/*           REPLACED STANDARD CREATE DUPLICATE OBJECT COMMAND     */
/*           WITH A COMMAND CUSTOMIZED BY JOHN ERICSON. THIS       */
/*           COMMAND WILL RETRIEVE/USE THE FILE FROM THE APPRO-    */
/*           PRIATE LIBRARY INSTEAD OF USING HARDCODED 'PRKFLIB'.  */
/*           THE CUSTOMIZED COMMAND IS CRTDUPOBJL FOR 'CREATE      */
/*           DUPLICATE OBJECT FROM LIBRARY'.                       */
/*                                                                 */
/* 06/03/97 LEANNE FEDOR                                           */
/*          RECOMPILE ONLY.  NEW FIELDS 'PURGE FLAG' AND 'DATE OF  */
/*          FIRST PURGE' ADDED TO HOG GROUP FILE.                  */
/*                                                                 */
/* 06/18/97 LEANNE FEDOR                                           */
/*          CHANGED OUT QUEUE LOGIC.                               */
/*                                                                 */
/* 01/10/01 LEANNE FEDOR                                           */
/*          ADDED NEW REPORT: DAILY DEATHS BY CELL-SUMMARY.        */
/*                                                                 */
/* 03/23/01 LEANNE FEDOR                                           */
/*          ADDED 1 NEW REPORT:                                   */
/*            A=ALL FARMS-DAILY DEATHS/REASONS                     */
/*          CHANGED EXISTING REPORT TO RUN OVER FARM REASONS       */
/*            R=FARM DEATHS WITH REASON BY CELL                    */
/*                                                                 */
/* 09/06/01 LEANNE FEDOR                                           */
/*          ADDED 'GROUP TYPE' AS A SELECTOR.                      */
/*                                                                 */
/* 12/02/02 LEANNE FEDOR                                           */
/*          'PRODUCTION TYPE' AND 'PRODUCTION PHASE' WERE REMOVED  */
/*          FROM THE KILLED/DEAD FILE. SO, THE LOGIC WAS CHANGED   */
/*          HERE.                                                  */
/*                                                                 */
/* 12/18/02 LEANNE FEDOR                                           */
/*          'PRODUCTION TYPE' AND 'PRODUCTION PHASE' WERE REMOVED  */
/*          FROM THE SALES MOVEMENT HEADER FILE. SO, ALL LOGIC     */
/*          WAS CHANGED HERE.                                      */
/*                                                                 */
/* 04/07/09 LEANNE RAMSEY                                          */
/*          REMOVED REPORTS OVER HSP088-FARM DEATHS WITH REASONS:  */
/*            1) HP645-ALL FARMS DAILY DEATHS/REASONS              */
/*            2) HP652-FARM DEATHS WITH REASONS BY CELL            */
/*                                                                 */
/***************************************************************** */

             PGM

/* DEFINE THE VARIABLE REQUIRED IN THE COMMAND TO OVERRIDE THE     */
/* PRINT FILE TO A LASER PRINTER                                   */

             DCL        VAR(&CMD) TYPE(*CHAR) LEN(512)

             DCL        VAR(&QDATA) TYPE(*CHAR) LEN(187)

             DCL        VAR(&LDFSBO) TYPE(*CHAR) LEN(5)
             DCL        VAR(&LDFYMD) TYPE(*DEC) LEN(8)
             DCL        VAR(&LDTYMD) TYPE(*DEC) LEN(8)
             DCL        VAR(&LDPPCD) TYPE(*CHAR) LEN(5)
             DCL        VAR(&LDGTCD) TYPE(*CHAR) LEN(1)
             DCL        VAR(&LDFSCD) TYPE(*DEC) LEN(5)
             DCL        VAR(&LDPTCD) TYPE(*CHAR) LEN(5)
             DCL        VAR(&LDHGSN) TYPE(*DEC) LEN(7)
             DCL        VAR(&LDRPFL) TYPE(*CHAR) LEN(1)

             DCL        VAR(&HOLD) TYPE(*CHAR) LEN(10)
             DCL        VAR(&COPYS) TYPE(*DEC) LEN(2)
             DCL        VAR(&OUTQ) TYPE(*CHAR) LEN(10)

             CHGVAR     VAR(&LDFSBO) VALUE(%SST(*LDA 1 5))
             CHGVAR     VAR(&LDFYMD) VALUE(%SST(*LDA 6 8))
             CHGVAR     VAR(&LDTYMD) VALUE(%SST(*LDA 14 8))
             CHGVAR     VAR(&LDPPCD) VALUE(%SST(*LDA 22 5))
             CHGVAR     VAR(&LDGTCD) VALUE(%SST(*LDA 47 1))
             CHGVAR     VAR(&LDFSCD) VALUE(%SST(*LDA 27 5))
             CHGVAR     VAR(&LDPTCD) VALUE(%SST(*LDA 48 5))
             CHGVAR     VAR(&LDHGSN) VALUE(%SST(*LDA 53 7))
             CHGVAR     VAR(&LDRPFL) VALUE(%SST(*LDA 174 1))

             CHGVAR     VAR(&QDATA) VALUE(' ')

/*******************************************************************/
/* SET UP PRINT PARMS                                              */
/*******************************************************************/

 HOLD:       IF         COND(%SST(*LDA 411 1) = 'Y') THEN(CHGVAR +
                          VAR(&HOLD) VALUE(*YES))
             ELSE       CMD(CHGVAR VAR(&HOLD) VALUE(*NO))

 COPYS:      CHGVAR     VAR(&COPYS) VALUE(%SST(*LDA 412 1))
             IF         COND(&COPYS = 0) THEN(CHGVAR VAR(&COPYS) +
                          VALUE(1))

 OUTQ:       CHGVAR     VAR(&OUTQ) VALUE(%SST(*LDA 413 10))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(%SST(*LDA 401 10)))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(*JOB))

/*------------------------------------------------------------------*/
/* OVERRIDE PRINT FILE                                              */
/*------------------------------------------------------------------*/

/* USE THE CUSTOMIZED SEABOARD COMMAND TO OVERRIDE THE PRINT FILE.  */
/* THIS COMMAND ALLOWS THE REPORT TO BE GENERATED ON A STANDARD LINE*/
/* PRINTER OR A LASER PRINTER. TO USE THIS OVERRIDE YOU MUST DECLARE*/
/* A COMMAND VARIABLE AND FOLLOW THE OVERRIDE WITH A CALL TO        */
/* QCMDEXC.                                                         */

/* THIS REPORT IS STANDARD 10 CPI ON STANDARD 132 CHARACTER PAPER   */

             SBDPRTOVR  CMDVAR(&CMD) FILE(QPRINT) OUTQ(&OUTQ) +
                          WIDTH(132) COPIES(&COPYS) HOLD(&HOLD)

             CALL       PGM(QCMDEXC) PARM(&CMD 512)


/*******************************************************************/
/* LOGIC FOR THE REPORTS OVER THE 'KILLED/DEAD' FILE               */
/*******************************************************************/

             IF         COND(&LDRPFL *EQ 'D' *OR &LDRPFL *EQ 'F' *OR +
                          &LDRPFL *EQ 'S' *OR &LDRPFL *EQ 'C') THEN(DO)

/*  SELECT ALL KILLED/DEAD RECORDS WITHIN THE DATE RANGE             */

             CHGVAR     VAR(%SST(&QDATA 1 10)) VALUE('KDKDDT *GE')
             CHGVAR     VAR(%SST(&QDATA 12 8)) VALUE(&LDFYMD)
             CHGVAR     VAR(%SST(&QDATA 21 15)) VALUE('*AND KDKDDT +
                          *LE')
             CHGVAR     VAR(%SST(&QDATA 37 8)) VALUE(&LDTYMD)


/*  IF BUSINESS OFFICE         (KILLED/DEAD FILE)            */

             IF         COND(&LDFSBO *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 46 17)) VALUE('*AND KDFSBO +
                          *EQ "')
             CHGVAR     VAR(%SST(&QDATA 63 5)) VALUE(&LDFSBO)
             CHGVAR     VAR(%SST(&QDATA 68 1)) VALUE('"')
             ENDDO

/*  IF PRODUCTION TYPE                                          */

             IF         COND(&LDPTCD *NE ' ') THEN(DO)

             CHGVAR     VAR(%SST(&QDATA 70 17)) VALUE('*AND HGPTCD +
                          *EQ "')
             CHGVAR     VAR(%SST(&QDATA 87 5)) VALUE(&LDPTCD)
             CHGVAR     VAR(%SST(&QDATA 92 1)) VALUE('"')
             ENDDO

/*  IF PRODUCTION PHASE                                          */

             IF         COND(&LDPPCD *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 94 17)) VALUE('*AND HGPPCD +
                          *EQ "')
             CHGVAR     VAR(%SST(&QDATA 111 5)) VALUE(&LDPPCD)
             CHGVAR     VAR(%SST(&QDATA 116 1)) VALUE('"')
             ENDDO

/*  IF FARM SITE                                                 */

             IF         COND(&LDFSCD *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 118 15)) VALUE('*AND KDFSCD +
                          *EQ')
             CHGVAR     VAR(%SST(&QDATA 135 5)) VALUE(&LDFSCD)
             ENDDO

/*  IF HOG GROUP                                                */

             IF         COND(&LDHGSN *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 143 17)) VALUE('*AND KDHGSN +
                          *EQ ')
             CHGVAR     VAR(%SST(&QDATA 160 7)) VALUE(&LDHGSN)
             CHGVAR     VAR(%SST(&QDATA 167 1)) VALUE(' ')
             ENDDO

/*  IF GROUP TYPE                                               */

             IF         COND(&LDGTCD *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 169 17)) VALUE('*AND HGGTCD +
                          *EQ "')
             CHGVAR     VAR(%SST(&QDATA 186 1)) VALUE(&LDGTCD)
             CHGVAR     VAR(%SST(&QDATA 187 1)) VALUE('"')
             ENDDO

/*-----------------------------------------------------------------*/
/* RUN REPORTS OVER THE KILLED/DEAD FILE                           */
/*-----------------------------------------------------------------*/
/* DAILY DEATHS BY DATE REPORT */

             IF         COND(&LDRPFL *EQ 'D') THEN(DO)
             OVRDBF     FILE(HSJ068A) SHARE(*YES)
             OPNQRYF    FILE((HSJ068A)) QRYSLT(&QDATA) +
                          KEYFLD((HGPTCD) (HGPPCD) (KDKDDT) +
                          (KDFSCD) (HGHGCD)) SEQONLY(*YES)
             CALL PGM(HP657)
             CLOF       OPNID(HSJ068A)
             ENDDO

/* DAILY DEATHS BY FARM REPORT -- DETAIL OR SUMMARY */

             IF         COND(&LDRPFL *EQ 'F' *OR &LDRPFL *EQ 'S') +
                          THEN(DO)
             OVRDBF     FILE(HSJ068A) SHARE(*YES)
             OPNQRYF    FILE((HSJ068A)) QRYSLT(&QDATA) +
                          KEYFLD((HGPTCD) (HGPPCD) (KDFSCD) +
                          (HGHGCD) (KDKDDT *DESCEND)) SEQONLY(*YES)
             CALL PGM(HP655)
             CLOF       OPNID(HSJ068A)
             ENDDO

/* DAILY DEATHS BY CELL-SUMMARY                     */

             IF         COND(&LDRPFL *EQ 'C') THEN(DO)
             OVRDBF     FILE(HSJ068F) SHARE(*YES)
             OPNQRYF    FILE((HSJ068F)) QRYSLT(&QDATA) KEYFLD(*FILE) +
                          SEQONLY(*YES)
             CALL PGM(HP653)
             CLOF       OPNID(HSJ068F)
             ENDDO

             ENDDO      /* DAILY DEAD REPORTS */


/*********************************************************************/
/* LOGIC FOR THE REPORT OF DAILY DEATHS, DOAS, AND REJECTS           */
/*********************************************************************/

/* SIX QUERIES ARE SET UP AND RUN FOR THIS REPORT:                   */
/*    1) OVER THE KILLED/DEAD FILE FOR DAILY DEADS BY REASON CODE    */
/*    2) OVER A JOIN FILE OF TRANSFER HEADER/DETAIL/REJECT REASON TO */
/*       GET THE REJECTS BY REASON CODE.                             */
/*    3) OVER THE SALES MOVEMENT HEADER FOR DOA HEAD                 */
/*    4) OVER THE SALES MOVEMENT DETAIL FOR DOA HEAD                 */
/*    5) OVER THE TRANSFER MOVEMENT HEADER FOR DOA HEAD              */
/*    6) OVER THE TRANSFER MOVEMENT DETAIL FOR DOA HEAD              */

             IF         COND(&LDRPFL *EQ 'O') THEN(DO)

/*-------------------------------------------------------------------*/
/* SET UP QUERY OVER THE KILLED/DEAD FILE TO GET THE DAILY DEAD HEAD */
/*-------------------------------------------------------------------*/

             CHGVAR     VAR(&QDATA) VALUE(' ')

/*  SELECT ALL KILLED/DEAD RECORDS FOR THE DATE RANGE                */

             CHGVAR     VAR(%SST(&QDATA 1 10)) VALUE('KDKDDT *GE')
             CHGVAR     VAR(%SST(&QDATA 12 8)) VALUE(&LDFYMD)
             CHGVAR     VAR(%SST(&QDATA 21 15)) VALUE('*AND KDKDDT +
                          *LE')
             CHGVAR     VAR(%SST(&QDATA 37 8)) VALUE(&LDTYMD)


/*  IF BUSINESS OFFICE                (KILLED/DEAD FILE)             */

             IF         COND(&LDFSBO *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 46 17)) VALUE('*AND KDFSBO +
                          *EQ "')
             CHGVAR     VAR(%SST(&QDATA 63 5)) VALUE(&LDFSBO)
             CHGVAR     VAR(%SST(&QDATA 68 1)) VALUE('"')
             ENDDO


/*  IF FARM SITE                      (KILLED/DEAD FILE)         */

             IF         COND(&LDFSCD *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 118 16)) VALUE('*AND KDFSCD +
                          *EQ ')
             CHGVAR     VAR(%SST(&QDATA 134 5)) VALUE(&LDFSCD)
             ENDDO

/*  IF HOG GROUP                      (KILLED/DEAD FILE)        */

             IF         COND(&LDHGSN *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 140 16)) VALUE('*AND KDHGSN +
                          *EQ ')
             CHGVAR     VAR(%SST(&QDATA 156 7)) VALUE(&LDHGSN)
             ENDDO

/* RUN QUERY OVER THE KILLED/DEAD FILE TO GET THE DAILY DEAD HEAD */

             OVRDBF     FILE(HSP068) SHARE(*YES)
             OPNQRYF    FILE((HSP068)) QRYSLT(&QDATA) KEYFLD(*FILE) +
                          SEQONLY(*YES)


/*-------------------------------------------------------------------*/
/* SET UP QUERY OVER THE TRANSFER FILES TO GET THE REJECTED HEAD     */
/*-------------------------------------------------------------------*/

             CHGVAR     VAR(&QDATA) VALUE(' ')

/* SELECT ALL TRANSFER RECORDS WITH A RECEIVED DATE IN RANGE */

             CHGVAR     VAR(%SST(&QDATA 1 10)) VALUE('MHRCDT *GE')
             CHGVAR     VAR(%SST(&QDATA 12 8)) VALUE(&LDFYMD)
             CHGVAR     VAR(%SST(&QDATA 21 15)) VALUE('*AND MHRCDT +
                          *LE')
             CHGVAR     VAR(%SST(&QDATA 37 8)) VALUE(&LDTYMD)


/*  IF BUSINESS OFFICE USING THE ORIGIN SIDE OF TRANSFERS           */

             IF         COND(&LDFSBO *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 46 17)) VALUE('*AND MHORBO +
                          *EQ "')
             CHGVAR     VAR(%SST(&QDATA 63 5)) VALUE(&LDFSBO)
             CHGVAR     VAR(%SST(&QDATA 68 1)) VALUE('"')
             ENDDO


/*  IF PRODUCTION TYPE          (TRANSFERS FOR REJECTED HEAD)        */

             IF         COND(&LDPTCD *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 70 17)) VALUE('*AND MHORPT +
                          *EQ "')
             CHGVAR     VAR(%SST(&QDATA 87 5)) VALUE(&LDPTCD)
             CHGVAR     VAR(%SST(&QDATA 92 1)) VALUE('"')
             ENDDO

/*  IF PRODUCTION PHASE         (TRANSFERS FOR REJECTED HEAD)    */

             IF         COND(&LDPPCD *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 94 17)) VALUE('*AND MHORPP +
                          *EQ "')
             CHGVAR     VAR(%SST(&QDATA 111 5)) VALUE(&LDPPCD)
             CHGVAR     VAR(%SST(&QDATA 116 1)) VALUE('"')
             ENDDO

/*  IF FARM SITE                (TRANSFERS FOR REJECTED HEAD)    */

             IF         COND(&LDFSCD *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 118 16)) VALUE('*AND MHORFS +
                          *EQ ')
             CHGVAR     VAR(%SST(&QDATA 134 5)) VALUE(&LDFSCD)
             ENDDO

/*  IF HOG GROUP                (TRANSFERS FOR REJECTED HEAD)   */

             IF         COND(&LDHGSN *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 140 16)) VALUE('*AND MDORSN +
                          *EQ ')
             CHGVAR     VAR(%SST(&QDATA 156 7)) VALUE(&LDHGSN)
             ENDDO

/* RUN QUERY OVER THE TRANSFERS TO GET THE REJECTED HEAD            */

             OVRDBF     FILE(HSJ074B) SHARE(*YES)
             OPNQRYF    FILE((HSJ074B)) QRYSLT(&QDATA) KEYFLD(*FILE) +
                          SEQONLY(*YES)

/*-------------------------------------------------------------------*/
/* SET UP QUERY OVER THE SALES HEADER TO GET THE DOA HEAD            */
/*-------------------------------------------------------------------*/

             CHGVAR     VAR(&QDATA) VALUE(' ')

/*  SELECT ALL SALES RECORDS IN THE DATE RANGE USING RECEIVED DATE   */

             CHGVAR     VAR(%SST(&QDATA 1 10)) VALUE('SHRCDT *GE')
             CHGVAR     VAR(%SST(&QDATA 12 8)) VALUE(&LDFYMD)
             CHGVAR     VAR(%SST(&QDATA 21 15)) VALUE('*AND SHRCDT +
                          *LE')
             CHGVAR     VAR(%SST(&QDATA 37 8)) VALUE(&LDTYMD)


/*  IF BUSINESS OFFICE                (SALES HEADER FOR DOA)         */

             IF         COND(&LDFSBO *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 46 17)) VALUE('*AND SHFSBO +
                          *EQ "')
             CHGVAR     VAR(%SST(&QDATA 63 5)) VALUE(&LDFSBO)
             CHGVAR     VAR(%SST(&QDATA 68 1)) VALUE('"')
             ENDDO

/*  IF FARM SITE                      (SALES HEADER FOR DOA)     */

             IF         COND(&LDFSCD *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 118 15)) VALUE('*AND SHFSCD +
                          *EQ')
             CHGVAR     VAR(%SST(&QDATA 134 5)) VALUE(&LDFSCD)
             ENDDO

/* RUN QUERY OVER THE SALES HEADER TO GET DOA FROM SALES        */

             OVRDBF     FILE(HSP084) SHARE(*YES)
             OPNQRYF    FILE((HSP084)) QRYSLT(&QDATA) KEYFLD(*FILE) +
                          SEQONLY(*YES)

/*-------------------------------------------------------------------*/
/* SET UP QUERY OVER THE SALES DETAIL TO GET THE DOA HEAD            */
/*-------------------------------------------------------------------*/

             CHGVAR     VAR(&QDATA) VALUE(' ')

/* ALWAYS SELECT ONLY SALES DETAIL RECORDS WITH DOA HEAD             */

             CHGVAR     VAR(%SST(&QDATA 1 12)) VALUE('SGDOHD +
                          *NE 0')

/*  IF HOG GROUP          (SALES MOVEMENT DETAIL FOR DOA)            */

             IF         COND(&LDHGSN *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 14 17)) VALUE('*AND SGHGSN +
                          *EQ ')
             CHGVAR     VAR(%SST(&QDATA 31 7)) VALUE(&LDHGSN)
             ENDDO

/* RUN QUERY OVER THE SALES DETAIL TO GET THE DOA FROM SALES      */

             OVRDBF     FILE(HSP085) SHARE(*YES)
             OPNQRYF    FILE((HSP085)) QRYSLT(&QDATA) KEYFLD(*FILE) +
                          SEQONLY(*YES)

/*-------------------------------------------------------------------*/
/* SET UP QUERY OVER THE TRANSFER HEADER TO GET THE DOA HEAD         */
/*-------------------------------------------------------------------*/

             CHGVAR     VAR(&QDATA) VALUE(' ')

/* SELECT ALL TRANSFER HEADER RECORDS WITH A RECEIVED DATE IN RANGE */

             CHGVAR     VAR(%SST(&QDATA 1 10)) VALUE('MHRCDT *GE')
             CHGVAR     VAR(%SST(&QDATA 12 8)) VALUE(&LDFYMD)
             CHGVAR     VAR(%SST(&QDATA 21 15)) VALUE('*AND MHRCDT +
                          *LE')
             CHGVAR     VAR(%SST(&QDATA 37 8)) VALUE(&LDTYMD)


/*  IF BUSINESS OFFICE USING THE ORIGIN SIDE (TRANSFER)              */

             IF         COND(&LDFSBO *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 46 17)) VALUE('*AND MHORBO +
                          *EQ "')
             CHGVAR     VAR(%SST(&QDATA 63 5)) VALUE(&LDFSBO)
             CHGVAR     VAR(%SST(&QDATA 68 1)) VALUE('"')
             ENDDO


/*  IF PRODUCTION TYPE   (TRANSFER HEADER FOR DOA)                   */

             IF         COND(&LDPTCD *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 70 17)) VALUE('*AND MHORPT +
                          *EQ "')
             CHGVAR     VAR(%SST(&QDATA 87 5)) VALUE(&LDPTCD)
             CHGVAR     VAR(%SST(&QDATA 92 1)) VALUE('"')
             ENDDO

/*  IF PRODUCTION PHASE        (TRANSFER HEADER FOR DOA)         */

             IF         COND(&LDPPCD *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 94 17)) VALUE('*AND MHORPP +
                          *EQ "')
             CHGVAR     VAR(%SST(&QDATA 111 5)) VALUE(&LDPPCD)
             CHGVAR     VAR(%SST(&QDATA 116 1)) VALUE('"')
             ENDDO

/*  IF FARM SITE               (TRANSFER HEADER FOR DOA)         */

             IF         COND(&LDFSCD *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 118 15)) VALUE('*AND MHORFS +
                          *EQ')
             CHGVAR     VAR(%SST(&QDATA 134 5)) VALUE(&LDFSCD)
             ENDDO

/* RUN QUERY OVER THE TRANSFER HEADER TO GET THE DOA HEAD       */

             OVRDBF     FILE(HSP074) SHARE(*YES)
             OPNQRYF    FILE((HSP074)) QRYSLT(&QDATA) KEYFLD(*FILE) +
                          SEQONLY(*YES)

/*-------------------------------------------------------------------*/
/* SET UP QUERY OVER THE TRANSFER DETAIL TO GET THE DOA HEAD         */
/*-------------------------------------------------------------------*/

             CHGVAR     VAR(&QDATA) VALUE(' ')

/* SELECT ONLY RECORDS WITH DOA HEAD                                 */

             CHGVAR     VAR(%SST(&QDATA 1 12)) VALUE('MDDOHD +
                          *NE 0')

/*  IF HOG GROUP        (TRANSFER DETAIL FOR DOA)               */

             IF         COND(&LDHGSN *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 14 16)) VALUE('*AND MDORSN +
                          *EQ ')
             CHGVAR     VAR(%SST(&QDATA 30 7)) VALUE(&LDHGSN)
             ENDDO

/* RUN QUERY OVER THE TRANSFER DETAIL TO GET THE DOA HEAD       */

             OVRDBF     FILE(HSP075) SHARE(*YES)
             OPNQRYF    FILE((HSP075)) QRYSLT(&QDATA) KEYFLD(*FILE) +
                          SEQONLY(*YES)

/*-------------------------------------------------------------------*/
/* CREATE THE WORKFILE                                               */
/*-------------------------------------------------------------------*/

             CRTDUPOBJL OBJ(HSP385) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)

/* CALL THE PROGRAM TO BUILD THE WORKFILE                          */

             CALL       PGM(HP385)

/* CALL THE PROGRAM TO PRINT THE REPORT                            */

             CALL       PGM(HP685)

             CLOF       OPNID(HSP068)
             CLOF       OPNID(HSJ074B)
             CLOF       OPNID(HSP074)
             CLOF       OPNID(HSP075)
             CLOF       OPNID(HSP084)
             CLOF       OPNID(HSP085)

             ENDDO      /* Daily deads, DOAs and Rejects */

/*********************************************************************/
/* LOGIC FOR THE DOA ONLY REPORT--NO REJECTS OR DAILY DEADS INCLUDED */
/*********************************************************************/

/* FOUR QUERIES ARE SET UP AND RUN FOR THE DOA ONLY REPORT:          */
/*    1) OVER THE SALES MOVEMENT HEADER FOR DOA HEAD                 */
/*    2) OVER THE SALES MOVEMENT DETAIL FOR DOA HEAD                 */
/*    3) OVER THE TRANSFER MOVEMENT HEADER FOR DOA HEAD              */
/*    4) OVER THE TRANSFER MOVEMENT DETAIL FOR DOA HEAD              */

             IF         COND(&LDRPFL *EQ 'A') THEN(DO)

/*-------------------------------------------------------------------*/
/* SET UP QUERY OVER THE SALES HEADER TO GET THE DOA HEAD            */
/*-------------------------------------------------------------------*/

             CHGVAR     VAR(&QDATA) VALUE(' ')

/*  SELECT ALL SALES RECORDS IN THE DATE RANGE USING RECEIVED DATE   */

             CHGVAR     VAR(%SST(&QDATA 1 10)) VALUE('SHRCDT *GE')
             CHGVAR     VAR(%SST(&QDATA 12 8)) VALUE(&LDFYMD)
             CHGVAR     VAR(%SST(&QDATA 21 15)) VALUE('*AND SHRCDT +
                          *LE')
             CHGVAR     VAR(%SST(&QDATA 37 8)) VALUE(&LDTYMD)


/*  IF BUSINESS OFFICE                (SALES HEADER FOR DOA)         */

             IF         COND(&LDFSBO *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 46 17)) VALUE('*AND SHFSBO +
                          *EQ "')
             CHGVAR     VAR(%SST(&QDATA 63 5)) VALUE(&LDFSBO)
             CHGVAR     VAR(%SST(&QDATA 68 1)) VALUE('"')
             ENDDO


/*  IF FARM SITE                      (SALES HEADER FOR DOA)     */

             IF         COND(&LDFSCD *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 118 15)) VALUE('*AND SHFSCD +
                          *EQ')
             CHGVAR     VAR(%SST(&QDATA 134 5)) VALUE(&LDFSCD)
             ENDDO

/* RUN QUERY OVER THE SALES HEADER TO GET DOA FROM SALES        */

             OVRDBF     FILE(HSP084) SHARE(*YES)
             OPNQRYF    FILE((HSP084)) QRYSLT(&QDATA) KEYFLD(*FILE) +
                          SEQONLY(*YES)

/*-------------------------------------------------------------------*/
/* SET UP QUERY OVER THE SALES DETAIL TO GET THE DOA HEAD            */
/*-------------------------------------------------------------------*/

             CHGVAR     VAR(&QDATA) VALUE(' ')

/* ALWAYS SELECT ONLY SALES DETAIL RECORDS WITH DOA HEAD             */

             CHGVAR     VAR(%SST(&QDATA 1 12)) VALUE('SGDOHD +
                          *NE 0')

/*  IF HOG GROUP          (SALES MOVEMENT DETAIL FOR DOA)            */

             IF         COND(&LDHGSN *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 14 17)) VALUE('*AND SGHGSN +
                          *EQ ')
             CHGVAR     VAR(%SST(&QDATA 31 7)) VALUE(&LDHGSN)
             ENDDO

/* RUN QUERY OVER THE SALES DETAIL TO GET THE DOA FROM SALES      */

             OVRDBF     FILE(HSP085) SHARE(*YES)
             OPNQRYF    FILE((HSP085)) QRYSLT(&QDATA) KEYFLD(*FILE) +
                          SEQONLY(*YES)

/*-------------------------------------------------------------------*/
/* SET UP QUERY OVER THE TRANSFER HEADER TO GET THE DOA HEAD         */
/*-------------------------------------------------------------------*/

             CHGVAR     VAR(&QDATA) VALUE(' ')

/* SELECT ALL TRANSFER HEADER RECORDS WITH A RECEIVED DATE IN RANGE */

             CHGVAR     VAR(%SST(&QDATA 1 10)) VALUE('MHRCDT *GE')
             CHGVAR     VAR(%SST(&QDATA 12 8)) VALUE(&LDFYMD)
             CHGVAR     VAR(%SST(&QDATA 21 15)) VALUE('*AND MHRCDT +
                          *LE')
             CHGVAR     VAR(%SST(&QDATA 37 8)) VALUE(&LDTYMD)


/*  IF BUSINESS OFFICE USING THE ORIGIN SIDE (TRANSFER)              */

             IF         COND(&LDFSBO *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 46 17)) VALUE('*AND MHORBO +
                          *EQ "')
             CHGVAR     VAR(%SST(&QDATA 63 5)) VALUE(&LDFSBO)
             CHGVAR     VAR(%SST(&QDATA 68 1)) VALUE('"')
             ENDDO


/*  IF PRODUCTION TYPE   (TRANSFER HEADER FOR DOA)                   */

             IF         COND(&LDPTCD *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 70 17)) VALUE('*AND MHORPT +
                          *EQ "')
             CHGVAR     VAR(%SST(&QDATA 87 5)) VALUE(&LDPTCD)
             CHGVAR     VAR(%SST(&QDATA 92 1)) VALUE('"')
             ENDDO

/*  IF PRODUCTION PHASE        (TRANSFER HEADER FOR DOA)         */

             IF         COND(&LDPPCD *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 94 17)) VALUE('*AND MHORPP +
                          *EQ "')
             CHGVAR     VAR(%SST(&QDATA 111 5)) VALUE(&LDPPCD)
             CHGVAR     VAR(%SST(&QDATA 116 1)) VALUE('"')
             ENDDO

/*  IF FARM SITE               (TRANSFER HEADER FOR DOA)         */

             IF         COND(&LDFSCD *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 118 15)) VALUE('*AND MHORFS +
                          *EQ')
             CHGVAR     VAR(%SST(&QDATA 134 5)) VALUE(&LDFSCD)
             ENDDO

/* RUN QUERY OVER THE TRANSFER HEADER TO GET THE DOA HEAD       */

             OVRDBF     FILE(HSP074) SHARE(*YES)
             OPNQRYF    FILE((HSP074)) QRYSLT(&QDATA) KEYFLD(*FILE) +
                          SEQONLY(*YES)

/*-------------------------------------------------------------------*/
/* SET UP QUERY OVER THE TRANSFER DETAIL TO GET THE DOA HEAD         */
/*-------------------------------------------------------------------*/

             CHGVAR     VAR(&QDATA) VALUE(' ')

/* SELECT ONLY RECORDS WITH DOA HEAD                                 */

             CHGVAR     VAR(%SST(&QDATA 1 12)) VALUE('MDDOHD +
                          *NE 0')

/*  IF HOG GROUP        (TRANSFER DETAIL FOR DOA)               */

             IF         COND(&LDHGSN *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 14 16)) VALUE('*AND MDORSN +
                          *EQ ')
             CHGVAR     VAR(%SST(&QDATA 30 7)) VALUE(&LDHGSN)
             ENDDO

/* RUN QUERY OVER THE TRANSFER DETAIL TO GET THE DOA HEAD       */

             OVRDBF     FILE(HSP075) SHARE(*YES)
             OPNQRYF    FILE((HSP075)) QRYSLT(&QDATA) KEYFLD(*FILE) +
                          SEQONLY(*YES)

/*-------------------------------------------------------------------*/
/* CREATE THE WORKFILE                                               */
/*-------------------------------------------------------------------*/

             CRTDUPOBJL OBJ(HSP349) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)


/* CALL THE PROGRAM TO BUILD THE WORKFILE                          */

             CALL       PGM(HP349)

/* CALL THE PROGRAM TO PRINT THE REPORT                            */

             CALL       PGM(HP649)

             CLOF       OPNID(HSP074)
             CLOF       OPNID(HSP075)
             CLOF       OPNID(HSP084)
             CLOF       OPNID(HSP085)

             ENDDO      /* DOA REPORTS--DETAIL AND SUMMARY VERSIONS */

             DLTOVR     FILE(*ALL)

             ENDPGM

