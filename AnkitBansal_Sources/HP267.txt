      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Hog Production System
      * PROGRAM:     Safedata: Write BGF Weekly Production Receiving File
      *                        to Editing File
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     01/17/02
      *
      *        !!!!! NO WEB DATA IS PROCESSED IN THIS PROGRAM !!!!!!!
      *
      *              This program reads records from the Receiving file and writes
      *              them to the Editing file. Thus, this program is only processing
      *              Safedata data...not WEB data.
      *
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      * 11/18/02  LeAnne Fedor
      *           Replaced 'de-pop flag' with 'population code'.
      *
      * 10/22/03  LeAnne Fedor
      *           Increased record length from 121 to 126 characters to accommodate 1 new
      *           field (which was added at the end of the record prior to SDI reference
      *           number):
      *               total p2 litters farrowed
      *
      * 06/28/04  LeAnne Fedor
      *           Added 1 new field: Number of Gilts Bred with Passed Dates
      *           Removed 10 fields: number of good piglets farrowed
      *                              number of dome heads
      *                              number of splays
      *                              number of weak and lethargic
      *                              number of scours
      *                              number of good piglets weaning next
      *                              number of lame
      *                              number of under 7 pounds
      *                              number of rough hair coat
      *                              number of swollen joints
      *
      * 02/07/05  LeAnne Fedor
      *           We are no longer collecting/Safedata is no longer sending:
      *                  Average Lactation Pounds
      *           We have left the field in the data structure---just named it as
      *           "unused". We thought this would help keep problems to a minimum since
      *           removing it would force us to 1) shorten the file record length and
      *           2) reposition all the other fields.
      *
      * 08/04/08  LeAnne Ramsey
      *           The receiving record length decreased from 81 to 58 positions.
      *           Removed 5 fields:  XXADGB   - Avg gilt age at breeding      (position 14)
      *                              XXPSGLSV - Nbr gilts bred w/passed dates (position 17)
      *                              XXUNUSED - unused                        (position 26)
      *                              XXWSLB   - Wkly ending lbs supple used   (position 49)
      *                              XXP2LT   - Total P2 Litters Farrowed     (position 71)
      *
      *  2/25/20  Brad Baden   E15763
      *           Use System Date instead of UDATE to calculate the Week Ending Date
      *           and Create Date.
      *
      *  7/05/21  Danny Nguyen P2156
      *           Move existing 'Get Saturday' date logic to Mainline to use the Week
      *           Ending Date from HSP267 record to get the previous Saturday date.
      *
      /eject
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fhsp267    if   e           k disk
      *  Safedata: BGF weekly production receiving file
      *
      *
     Fhsp268    o    e           k disk
      *  Safedata/WEB: BGF weekly production editing file
      *
      /eject
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      *
      ****************************************************************
      * ARRAYS AND TABLES
      ****************************************************************
      *
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * System Date
     d sysdate         s               d   inz(*sys)
      *
      * Parm fields
      *
     D xxname          s              9
     D xxabbrev        s              4
     D xxsatdt         s                   like(bpcrdt)
      *
      *
      * Workfields
      *
     D wknum40         s              4  0
      *
      *
      * Workfields for date manipulation
      *
     D wkcymdiso       s               D   datfmt(*iso)
     D wkcrdt          s                   like(bpcrdt)
     D wkcrtm          s                   like(bpcrtm)
P2156D wkdate          s              8  0                                      MMDDYYYY
P2156D wkwedate        s              8  0                                      YYYYMMDD
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *---------------------------------------------------------------
      * Break out single-field input record into individual fields
      *---------------------------------------------------------------
      *
     D inputrec        ds
     D   allfields                   58
      *
     D   xxfscd                       5    overlay(allfields:1)
     D   xxdt                         8    overlay(allfields:6)
      *
     D   xxbredpr                     4    overlay(allfields:14)
      *
     D   xxglprhd                     5    overlay(allfields:18)
     D   xxprhd                       5    overlay(allfields:23)
      *
     D   xxglfahd                     5    overlay(allfields:28)
     D   xxfahd                       5    overlay(allfields:33)
      *
     D   xxenplhd                     5    overlay(allfields:38)
     D   xxglsohd                     5    overlay(allfields:43)
     D   xxbohd                       5    overlay(allfields:48)
      *
     D   xxsdino                      6    overlay(allfields:53)
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * MAINLINE
      ****************************************************************
      *
      * Read each record in the Receiving file and extract fields for the
      * record you will write to the editing file.
      *
     C                   dou       *in90 = *on                                  Main do
     C                   read      hsp267                                 90
     C                   if        *in90 = *off                                 If not EOF
      *
     C                   move(p)   u1field       inputrec
      *
      * P2156 - Convert MMDDYYYY Date into YYYYMMDD Date.
P2156C                   move      xxdt          wkdate
  |   /free
  |      wkwedate = %dec(%char(%date(wkdate:*usa):*iso0):8:0);
P2156 /end-free
      *
      * P2156 - Moved Existing Get Saturday Date Routine Here.
P2156C                   call      'HP8005'
  |  C                   parm                    wkwedate
  |  C                   parm      *blank        xxname
  |  C                   parm      *blank        xxabbrev
  |   *
  |   * If the system date is a Saturday, use it as the 'week-ending' date. If it
  |   * is not a Saturday, find the previous Saturday and use it as the week-ending
  |   * date.
  |  C                   if        xxabbrev = 'Sat '                            If Saturday
  |  C     *iso          movel     wkwedate      wkcymdiso
  |  C                   else
  |   *
  |   * Find the previous Saturday
  |   *
  |  C                   call      'HPSAT'
  |  C                   parm                    wkwedate
  |  C                   parm                    xxsatdt
  |   *
  |  C     *iso          movel     xxsatdt       wkcymdiso
P2156C                   endif                                                  If Saturday
      *
      * Test all fields for numerics; set non-numeric fields to 0.
      *
     C                   testn                   xxfscd               92
     C                   if        *in92 = *on
     C                   move      xxfscd        bpfscd
     C                   else
     C                   z-add     0             bpfscd
     C                   endif
      *
      *
      * Safedata is sending us 'breed percent' with an implied decimal and 1 decimal position.
      * Thus, we will get 87.9, for example, as 879. Or, 100.0 as 1000.
      * We will take these 4 digits and put them into our 5,2 field.
      *
     C                   testn                   xxbredpr             92
     C                   if        *in92 = *on
     C                   move      xxbredpr      wknum40
     C     wknum40       mult      .1            bpbredpr
     C                   else
     C                   z-add     0             bpbredpr
     C                   endif
      *
      *
     C                   testn                   xxglprhd             92
     C                   if        *in92 = *on
     C                   move      xxglprhd      bpglprhd
     C                   else
     C                   z-add     0             bpglprhd
     C                   endif
      *
     C                   testn                   xxprhd               92
     C                   if        *in92 = *on
     C                   move      xxprhd        bpprhd
     C                   else
     C                   z-add     0             bpprhd
     C                   endif
      *
     C                   testn                   xxglfahd             92
     C                   if        *in92 = *on
     C                   move      xxglfahd      bpglfahd
     C                   else
     C                   z-add     0             bpglfahd
     C                   endif
      *
     C                   testn                   xxfahd               92
     C                   if        *in92 = *on
     C                   move      xxfahd        bpfahd
     C                   else
     C                   z-add     0             bpfahd
     C                   endif
      *
     C                   testn                   xxenplhd             92
     C                   if        *in92 = *on
     C                   move      xxenplhd      bpenplhd
     C                   else
     C                   z-add     0             bpenplhd
     C                   endif
      *
     C                   testn                   xxglsohd             92
     C                   if        *in92 = *on
     C                   move      xxglsohd      bpglsohd
     C                   else
     C                   z-add     0             bpglsohd
     C                   endif
      *
     C                   testn                   xxbohd               92
     C                   if        *in92 = *on
     C                   move      xxbohd        bpbohd
     C                   else
     C                   z-add     0             bpbohd
     C                   endif
      *
      * Default Fully Populated to Population Code
      *
     C                   move      'F'           bppocd
      *
      * Populate 'weekly production' date with the calculated date that is 1 day
      * less than the system date.
      *
     C                   movel     wkcymdiso     bpwedt
      *
      * Set the 'data source' field to say 'Safedata'.
      *
     C                   move      'S'           bpdscd
      *
      * Put 'system date/time' into the 'create date/time' fields
      *
     C                   z-add     wkcrdt        bpcrdt
     C                   z-add     wkcrtm        bpcrtm
      *
      * Write a record to the editing file.
      *
     C                   write     bprec
     C                   clear                   bprec
     C
     C                   endif                                                  If not EOF
     C                   enddo                                                  Main do
      *
     C                   seton                                        lr
      /eject
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Put system date/time into work fields to use as 'create date/time'.
      *
      **  2/26/2020 JBB E15763 - Use System Date instead of UDATE
     C*    *mdy          movel     udate         wkcymdiso
     c                   movel     sysdate       wkcymdiso
     C                   movel     wkcymdiso     wkcrdt
     C                   time                    wkcrtm
      *
      * Determine if the system date is a Saturday.
      * P2156 - Commented Out & Moved Get Saturday Date Routine to Mainline.
      *
P2156C**                 call      'HP8005'
  |  C**                 parm                    wkcrdt
  |  C**                 parm      *blank        xxname
  |  C**                 parm      *blank        xxabbrev
  |   *
  |   * If the system date is a Saturday, use it as the 'week-ending' date. If it
  |   * is not a Saturday, find the previous Saturday and use it as the week-ending
  |   * date.
  |  C**                 if        xxabbrev = 'Sat '                            If Saturday
  |  C**   *iso          movel     wkcrdt        wkcymdiso
  |  C**                 else
  |   *
  |   * Find the previous Saturday
  |   *
  |  C**                 call      'HPSAT'
  |  C**                 parm                    wkcrdt
  |  C**                 parm                    xxsatdt
  |   *
  |  C**   *iso          movel     xxsatdt       wkcymdiso
P2156C**                 endif                                                  If Saturday
      *
     C                   endsr
      /eject
