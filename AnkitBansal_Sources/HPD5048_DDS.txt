     A*%%TS  SD  20040830  101130  ISLRAMS     REL-V5R2M0  5722-WDS
     A*****************************************************************
     A*
     A*  SYSTEM:        HOG PRODUCTION
     A*  DISPLAY FILE:  HPD5048 - SELECT NURSERY OR GROW FINISH GROUP
     A*  CREATED BY:    LEANNE FEDOR
     A*  DATE:          08/30/04
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      PRINT
     A                                      CF04(04 'PROMPT')
     A                                      CA12(12 'PREVIOUS')
     A N82                                  ROLLUP(25 'PAGE FORWARD')
     A*---------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP5048S1                  SFL
     A*%%TS  SD  20040830  101130  ISLRAMS     REL-V5R2M0  5722-WDS
     A            HDRRN1    R        H      REFFLD(@@RRN)
     A            D1CLMDY   R        O  8  1REFFLD(HSRREF/@@DT06 *LIBL/HSPREF)
     A                                      EDTWRD('  /  /  ')
     A            D1HGCD    R        O  8 11REFFLD(HSRREF/@@HGCD *LIBL/HSPREF)
     A            D1GSCD    R        O  8 22REFFLD(HSRREF/@@GSCD *LIBL/HSPREF)
     A            D1PPCD    R        O  8 28REFFLD(HSRREF/@@PPCD *LIBL/HSPREF)
     A            D1PTCD    R        O  8 35REFFLD(HSRREF/@@PTCD *LIBL/HSPREF)
     A            D1FSCD    R        O  8 43REFFLD(@@FSCD)
     A                                      EDTCDE(M)
     A            D1BLCD    R        O  8 51REFFLD(HSRREF/@@BLCD *LIBL/HSPREF)
     A            D1RMCD    R        O  8 58REFFLD(HSRREF/@@RMCD *LIBL/HSPREF)
     A*
     A*---------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP5048C1                  SFLCTL(HP5048S1)
     A*%%TS  SD  20040830  101130  ISLRAMS     REL-V5R2M0  5722-WDS
     A                                      SFLSIZ(0100)
     A                                      SFLPAG(0009)
     A                                      WINDOW(HP5048EC)
     A                                      SETOF(51)
     A                                      SETOF(52)
     A                                      SETOF(53)
     A                                      SETOF(54)
     A                                      RTNCSRLOC(&RCD &FLD)
     A                                      OVERLAY
     A                                      SFLCSRRRN(&HDRRN)
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A            HDPAG1    R        H      REFFLD(@@PAG)
     A                                      SFLRCDNBR
     A            RCD           10A  H
     A            FLD           10A  H
     A            HDRRN     R        H      REFFLD(@@RRN)
     A            SDPGM     R        O  1  1REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 17'SELECT NURSERY OR GROW FINISH GROU-
     A                                      P'
     A                                      DSPATR(HI)
     A                                  3  1'Position Cursor, Press Enter'
     A                                  4 22'?'
     A                                      DSPATR(HI)
     A                                  4 30'?'
     A                                      DSPATR(HI)
     A                                  4 36'?'
     A                                      DSPATR(HI)
     A                                  4 45'?'
     A                                      DSPATR(HI)
     A                                  5  1'Closed'
     A                                      DSPATR(HI)
     A                                  5 11'Hog'
     A                                      DSPATR(HI)
     A                                  5 20'Group'
     A                                      DSPATR(HI)
     A                                  5 28'-Production-'
     A                                      DSPATR(HI)
     A                                  5 44'Farm'
     A                                      DSPATR(HI)
     A                                  6  2'Date'
     A                                      DSPATR(HI)
     A                                  6 11'Group'
     A                                      DSPATR(HI)
     A                                  6 20'Status'
     A                                      DSPATR(HI)
     A                                  6 28'Phase'
     A                                      DSPATR(HI)
     A                                  6 35'Type'
     A                                      DSPATR(HI)
     A                                  6 44'Site'
     A                                      DSPATR(HI)
     A                                  6 51'Bldg'
     A                                      DSPATR(HI)
     A                                  6 58'Room'
     A                                      DSPATR(HI)
     A            C1CLMDY   R        B  7  1REFFLD(HSRREF/@@DT06 *LIBL/HSPREF)
     A                                      EDTWRD('  /  /  ')
     A                                      DSPATR(HI)
     A            C1GSCD    R        B  7 22REFFLD(HSRREF/@@GSCD *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A  54                                  DSPATR(PC)
     A            C1PPCD    R        B  7 28REFFLD(HSRREF/@@PPCD *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A  51                                  DSPATR(PC)
     A            C1PTCD    R        B  7 35REFFLD(HSRREF/@@PTCD *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A  52                                  DSPATR(PC)
     A            C1FSCD    R        B  7 43REFFLD(@@FSCD)
     A                                      DSPATR(HI)
     A  53                                  DSPATR(PC)
     A*
     A*---------------------------------------------------------------
     A* COMMAND LINE LINE
     A*---------------------------------------------------------------
     A          R HP5048K1
     A                                      WINDOW(HP5048EC)
     A                                      OVERLAY
     A  43                             17  1'Message program failed. Call IS.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 18  1'F4=Prompt'
     A                                 18 12'F12=Previous'
      *
     A*-------------------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
     A*-------------------------------------------------------------------------
     A          R HP5048ES                  SFL SFLMSGRCD(19)
      *
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
     A*
     A*-------------------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*-------------------------------------------------------------------------
     A*
     A          R HP5048EC                  SFLCTL(HP5048ES)
     A*%%TS  SD  19980327  105628  ISLRAMS     REL-V3R7M0  5716-PW1
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A                                      WINDOW(*DFT 20 65)
     A                                      WDWBORDER((*DSPATR RI) (*CHAR '    -
     A                                          '))
     A            MSGPGM                    SFLPGMQ(10)
     A*
     A*---------------------------------------------------------------
     A* ASSUME FORMAT -- REQUIRED FOR WINDOWS
     A*---------------------------------------------------------------
     A          R ASSUME
     A*%%TS  SD  20011025  160559  ISLRAMS     REL-V4R4M0  5769-PW1
     A                                      ASSUME
     A                                      OVERLAY
     A                                 24 79' '
