      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Hog Production System
      * PROGRAM:     HP338 - Build Workfile--Feed Ticket Detail Reports
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     09/13/01
      *
      * Function:    Build a workfile that can be used in for the Feed Ticket
      *              Detail Reports.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 12/09/02  LeAnne Fedor
      *           Removed 'production phase' from the Feed Ticket Detail file.
      *           So, changed logic in this program to use the 'production phase' from
      *           the Hog Group file or Farm Site file...whichever is appropriate.
      *
      * 04/20/05  LeAnne Fedor
      *           Added logic for new user selections:
      *               Company relationship
      *               Feed delivery zone
      *               Produced flag
      *
      * 04/23/09  LeAnne Ramsey
      *           Recompile only. Costed Time/User added to Feed Ticket Header.
      *
      * 05/12/09  LeAnne Ramsey
      *           Recompile only. PickUp Ticket Type added to Feed Ticket Header.
      *
      * 07/06/09  LeAnne Ramsey
      *           Recompile only. Added new field 'Continuous Flow Flag' to Hog Group file.
      *
      * 09/21/09  LeAnne Ramsey
      *           Recompile only. Added new field 'Upload Date' to Feed Ticket Header.
      *
      * 10/15/13  LeAnne Ramsey (E2831)
      *           Recompile only. Added field 'MTech Reference'.
      *
      * 03/26/20  Danny Nguyen - P405 - Farm Number Increase
      *           Recompile only. Increased Bin Code (@@BNCD) in HSPREF file from 5A to 6A.
      * 05/10/22 Eric L SDN736 Recompiled ticket nbr increase (TKNO & RTNO 7.0 TO 9.0)
      *
      /eject
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fhsp011    if   e           k disk
      *    Production phase
      *
      *
     Fhsp016    if   e           k disk
      *    Manager
      *
      *
     Fhsp018    if   e           k disk
      *    Farm site
      *
      *
     Fhsl034d   if   e           k disk
      *    Hog group
      *
      *
     Fhsp037    if   e           k disk
      *   Feed ticket header
      *
      *
     Fhsp038    if   e           k disk
      *    Feed ticket detail (records selected by open query)
      *
      *
     Fhsp338    o    e           k disk
      *    Workfile: Feed ticket detail
      *
      /eject
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      *------------------------------------------------------------------
      * CONSTANTS
      *------------------------------------------------------------------
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Control fields
      *
     D procfl          s              1    inz('Y')
      *
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *---------------------------------------------------------------
      *  LDA
      *---------------------------------------------------------------
      *
     Dlda             uds                  dtaara(*lda)
     D  ldpmgcd               22     26
     D  ldppcd                27     31
     D  ldcell               179    180  0
      *
     D  ldprfl               237    237
     D  ldcrcd               238    242
     D  lddzcd               253    257
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * Mainline
      ****************************************************************
      *
      * Read all feed ticket detail records selected by the open query
      * and conditionally write corresponding records to the workfile.
      *
      * If the user selected 1) a specific cell or 2) a specific production
      * manager, or 3) a specific production phase, only write the record if it
      * matches the selections.
      *
     C                   dou       *in90 = *on                                  Main do loop
     C                   read      hsp038                                 90
     C                   if        *in90 = *off                                 If not EOF
      *
     C                   move      yes           procfl
     C                   clear                   wdrec
      *
      * Retrieve hog group code and phase
      *
     C                   if        fdhgsn <> 0
     C     fdhgsn        chain     hsl034d                            92
     C                   if        *in92 = *on
     C                   move      no            procfl
     C                   else
     C                   move      hghgcd        wdhgcd
     C                   move      hgppcd        wdppcd
     C                   endif
     C                   endif
      *
      * If still OK, check for matches on User Selections.
      *
     C                   if        procfl = yes
     C                   exsr      $matches
     C                   endif
      *
      *
      * Process this record
      *
     C                   if        procfl = yes                                 If process
      *
     C                   move      fspmgcd       wdpmgcd
     C                   z-add     fscell        wdcell
      *
      *
      * Retrieve production manager name
      *
     C     fspmgcd       chain     hsp016                             92
     C                   if        *in92 = *off
     C                   move      mgmgnm        wdpmgnm
     C                   endif
      *
      * Retrieve production phase description
      *
     C     wdppcd        chain     hsp011                             92
     C                   if        *in92 = *off
     C                   move      ppppds        wdppds
     C                   endif
      *
      * Move feed ticket detail fields into workfile fields
      *
     C                   move      fdrgcd        wdrgcd
     C                   move      fdfmbo        wdfmbo
     C                   z-add     fdfscd        wdfscd
      *
     C                   z-add     fdtkno        wdtkno
     C                   move      fdtrcd        wdtrcd
     C                   z-add     fdlnno        wdlnno
     C                   z-add     fdtkdt        wdtkdt
     C                   z-add     fdrtno        wdrtno
     C                   z-add     fdrtdt        wdrtdt
      *
     C                   move      fdblcd        wdblcd
     C                   move      fdrmcd        wdrmcd
     C                   move      fdbncd        wdbncd
      *
     C                   z-add     fdhgsn        wdhgsn
      *
     C                   move      fdalfl        wdalfl
     C                   z-add     fdaldt        wdaldt
      *
     C                   move      fdrncd        wdrncd
     C                   move      fdadcd        wdadcd
     C                   move      fdftcd        wdftcd
     C                   z-add     fdfdlb        wdfdlb
      *
     C                   z-add     fdinam        wdinam
     C                   z-add     fdrcdt        wdrcdt
     C                   z-add     fdmfam        wdmfam
     C                   z-add     fdmcdt        wdmcdt
     C                   z-add     fddlam        wddlam
     C                   z-add     fdmdam        wdmdam
      *
     C                   write     wdrec
      *
      *
     C                   endif                                                  If process
     C                   endif                                                  If not EOF
     C                   enddo                                                  Main do loop
      *
     C                   seton                                        lr
      /eject
      *--------------------------------------------------------------------------------------
      * Check for matches on User Selections
      *--------------------------------------------------------------------------------------
      *
     C     $matches      begsr
      *
      * Retrieve farm info and check for matches on:
      *   Cell, Production Manager, Company Relationship, Delivery Zone
      *
     C     fdfscd        chain     hsp018                             92
     C                   if        *in92 = *on or
     C                             (ldcell <> 0 and fscell <> ldcell) or
     C                             (ldpmgcd <> *blank and ldpmgcd <> fspmgcd) or
     C                             (ldcrcd <> *blank and fscrcd <> ldcrcd) or
     C                             (lddzcd <> *blank and fsdzcd <> lddzcd)
     C                   move      no            procfl
     C                   else
     C                   if        wdppcd = *blank
     C                   move      fsppcd        wdppcd
     C                   endif
     C                   endif
      *
      * Check Production Phase match
      *
     C                   if        ldppcd <> *blank and ldppcd <> wdppcd
     C                   move      no            procfl
     C                   endif
      *
      *
      * If still OK, check for "Produced Flag" value in the Ticket Header.
      *
     C                   if        procfl = yes and ldprfl <> *blank
      *
     C     key01         chain     hsp037                             92
     C                   if        *in92 = *on or fhprfl <> ldprfl
     C                   move      no            procfl
     C                   endif
      *
     C                   endif
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Key lists
      *
     C     key01         klist
     C                   kfld                    fdfmbo
     C                   kfld                    fdtkno
     C                   kfld                    fdtrcd
      *
     C                   endsr
      /eject
