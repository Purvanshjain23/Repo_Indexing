/********************************************************************/
/*                                                                  */
/*  SYNOPSIS - PRINT MULTIPLE INVOICES OR REPRINT 1 INVOICE IN      */
/*             1 SPOOL FILE.                                        */
/*             NOTE: PDR4PFR DOES NOT SET ON 'LR'.                  */
/*                                                                  */
/*  SYSTEM    - ORDER MANAGEMENT SYSTEM                             */
/*  PROGRAM   - PRINT INVOICES/REPRINT 1 INVOICE                    */
/*  DATE      -  7/20/95                                            */
/*                                                                  */
/*      PARAMETERS RECEIVED:                                        */
/*                                                                  */
/*          &COMPNYN    - COMPANY NUMBER - BLANK FOR ALL            */
/*          &LOADN      - NOT USED - PROCESS ORDERS ON LOAD         */
/*          &THRDATN    - NOT USED - PROCESS ORDERS THRU ACTUAL SHIP DATE  */
/*          &ORDRN      - NOT USED - ORDER NUMBER                   */
/*          &BILTYP     - NOT USED - BILLING TYPE (INVOICE,MEMO)    */
/*                        NOT USED HERE; USED IN TO PRE-INV REG.    */
/*          &OUTQ       - OUTPUT QUEUE                              */
/*          &HOLD       - HOLD PRINT                                */
/*          &SAVE       - SAVE PRINT                                */
/*          &COPYN      - NUMBER OF COPIES                          */
/*          &NQSTAT     - NOT USED - NIGHT OUTPUT QUEUE RUN STS     */
/*          &NIGHTQ     - NOT USED - NIGHT OUTPUT QUEUE             */
/*          &REPRINT    - REPRINT FLAG                              */
/*          &PRTREG     - NOT USED -  USED FOR PRE-INVOICE REG      */
/*          &PRTEXC     - NOT USED -  USED FOR PRE-INVOICE REG      */
/*          &JOBNME     - JOB NAME FOR FAXING                       */
/*          &JOBNUM     - JOB NUMBER                                */
/*          &ARCUSTP    - NOT USED - A/R CUSTOMER NUMBER            */
/*          &PRCUSTT    - PROCESS CUSTOMER TYPE                     */
/********************************************************************/
/*  RMC 12/14/00  PRINT INVOICE TRANSACTION REPORT ON KC PRINTER    */
/*  SCAN:RC1214   PRKPRT04 -- PDUWPFR CALLED FROM PDTOXFR. SET      */
/*                PROCESS STATUS TO 'Y' SO THE WORK RCD WILL BE     */
/*                DELETED FOR NOT_FAXED INVOICES IN PDUWPFR.        */
/*  RMC 04/20/01  CHANGE FORM LENGTH TO 88 AND PORTRAIT             */
/*  RMC 01/29/04  PRINT ON PRKPRT08 INSTEAD OF PRKPRT02             */
/*  RMC 04/25/05  PRINT INVOICES FROM DRAWER(2); DONT PRINT         */
/*                ON PRKPRT08 IF ISRCENT, ISSMASO, ISSPATE          */
/*  RMC 04/26/05  PRINT INVOICES FROM DRAWER(3)!                    */
/*  RMC 02/03/06  ADD ISLBUSE TO EXCLUSIONS ABOVE                   */
/*  RMC 02/24/06  DUP PDUWPFR$ TO TFPRTDUP SO HOLD(*YES), THEN      */
/*                RLSSPLF USER(PDUWPFR08)   SCAN:  RC0224           */
/*  RMC 03/07/06  DUP WONT WORK. SO PRINT TO TFPRTDUP IF 960        */
/*                BEFORE PRINTING TO PRKPRT08. SET PRCSTS TO 'N'    */
/*                FOR 960 PRINT, AND TO 'Y' FOR PRKPRT08 PRINT      */
/*  SLM 12/07/06  Changes due to Combine Invoice processing         */
/*  SLM 06/25/07  Add logic for Standard/Combine/Custom Invoices    */
/*                add logic for generic company document file       */
/*                for the custom invoice program                    */
/*                and for the custom invoice overlay                */
/*                Add logic If record found of INCST1               */
/*                THEN perform the custom invoice routine           */
/*                we also have to perform this custom invoice       */
/*                prgram 3 times: FOR CUSTOMER COPY                 */
/*                orginal copy, and office copy  (FOR DAILY'S)      */
/*  SLM 07/03/07  Add logic for CUSTOM FAXING PROGRAM.              */
/*                REMOVE 'GOTO END' AND ALL AFTER.                  */
/*                WW REPRINT USES THIS CL AND DOES THE FOLLOWING    */
/*                                                                  */
/*                8=REPRINT  CALLS PDTLUPR WHICH SUBMITS THIS       */
/*                PROGRAM.                                          */
/*                DAILYS - USES DAILYS CUSTOM PRINT PROGRAM         */
/*                FOR INV AND FAXING IN THIS CL BECAUSE IT PRINTS   */
/*                ON PAPER. INVOICE AND FAX ARE SEPARATE PROGRAMS   */
/*                FOR BOTH SEABOARD AND DAILYS.                     */
/*                THE FONTS ARE DIFFERENT WHEN YOU PRINT A FAX      */
/*                AND WHEN YOU PRINT AN INVOICE.                    */
/*                                                                  */
/*                9=Print Form/Seaboard will print on plain paper   */
/*                                                                  */
/*                INCUST01 IS THE CUSTOM INVOICE OVERLAY FOR        */
/*                THE DAILYS INVOICE PGM AND HPB                    */
/*                                                                  */
/*                THE COVER SHEETS FOR ALL FAXES WILL BE HANDLED    */
/*                IN PDZ3XFR & POD2UPC                              */
/*  PKD 10/26/07  ADDED DUPSPLF FOR DAILYS                          */
/*  RMC 01/02/08  Change to print HPB invoices which use an overlay */
/*  RMC 07/22/08  FP1110 - HPB ONLY-PRINT BOL WHEN PRINTING INVOICE */
/*                PGM PMRCPFR BY CALLING BOL PGM PMWZXFR            */
/*  LJB 03/30/10 P000198 - CHANGE TO FAX OR EMAIL, ALSO TO SEND TO  */
/*                MULTIPLE RECIPIENTS (INDENTED "DO"'S)             */
/*                GROUPED ALL THE OVERRIDES TOGETHER. CHANGED SOME  */
/*                OF THE "ELSE DO"S TO IF'S TO RELIEVE SOME OF      */
/*                THE NESTING AND TO MAKE IT MORE READABLE. ALSO    */
/*                TO MAKE IT EASIER TO FOLLOW THE LOGIC.            */
/*                   I ADDED A NEW PARM TO PDTOXFR. I DON'T KNOW    */
/*                WHAT IT IS FOR OR WHEN IT WAS ADDED BUT THIS      */
/*                PROGRAM NEEDS TO PASS IT TO KEEP IT FROM FAILING. */
/*                ULTIMATELY, THE PRINT PGM NEEDS IT TO BE "N".     */
/*  LJB 04/03/12 E002016 - CALLED PROGRAMS PDTOXFR AND PMCRXFR      */
/*                WERE CHANGED TO PASS THE &REPRINT FLAG TO THEIR   */
/*                PRINT INVOICE ROUTINES SO THE CORRECT FORM WILL   */
/*                BE USED (PREPRINTED OR NOT). ADD PARM &JOBNUMCDE  */
/*                BECAUSE &JOBNUM NEEDS TO BE *CHAR 10.             */
/*  JBB 04/03/17  E9064 440 LOGIC ON DUP SPOOLFILES          */
/*  RMC 04/19/19  R14851 ADD PARM &DLTWRK - PASS TO PDVGPFR AS NO +
/*                 SO THE WF INVOICE LIST RCD WILL BE DELETED    */
/*                 ALSO HOLD THIS NEW ONE EML/INV SPLF - PUMFPFR$  */
/*  DN  07/15/21  W80535 - PUT PDUWPFR REPORT ON *HOLD=*YES FOR     */
/*                COMPANIES 960, 440, 363 & 364.                    */
/*  DN  08/04/21  W81963 - PUT PDUWPFR REPORT ON HOLD WHEN CALLING  */
/*                PDTOXFR.                                          */
/*  DN  11/18/21  W68695 - PUT PDR4PFR REPORT ON HOLD.              */
/********************************************************************/

 PDR4PCLP:   PGM        PARM(&COMPNYN &LOADN &THRDATN &ORDRN &BILTYP +
                          &OUTQ &HOLD &SAVE &COPYN &NQSTAT &NIGHTQ +
                          &REPRINT &PRTREG &PRTEXC &JOBNME +
                          &JOBNUMCDE &ARCUSTP &PRCUSTT)

/* NOTE: If REPRINT FLAG is 'Y' we are reprinting an invoice ON FORM*/
/*       OTHERWISE, WE ARE PROCESSING REGULAR INVOICE PRINT.        */

/* PARMS                                                            */
             DCL        VAR(&COMPNYN) TYPE(*DEC) /* Company Number  */
             DCL        VAR(&LOADN) TYPE(*DEC)   /* Load  Number    */
             DCL        VAR(&THRDATN) TYPE(*DEC) /* Thru Ship Date  */
             DCL        VAR(&ORDRN) TYPE(*DEC)   /* Order Number    */
             DCL        VAR(&BILTYP) TYPE(*CHAR) LEN(1)
                            /*           Bill type (Invoice,Memo)    */
             DCL        VAR(&OUTQ) TYPE(*CHAR) LEN(10) /*Output Queue*/
             DCL        VAR(&HOLD) TYPE(*CHAR) LEN(4) /* Hold Status */
             DCL        VAR(&SAVE) TYPE(*CHAR) LEN(4) /* Save Status */
             DCL        VAR(&COPYN) TYPE(*DEC)   /* No. of Copies    */
             DCL        VAR(&NQSTAT) TYPE(*CHAR) LEN(1)
                            /* If must run in night q.               */
             DCL        VAR(&NIGHTQ) TYPE(*CHAR) LEN(10)
                            /* Night Job Queue                       */
             DCL        VAR(&REPRINT) TYPE(*CHAR) LEN(1)
                            /* If Y then REPRINT----                 */
             DCL        VAR(&PRTREG) TYPE(*CHAR) LEN(1)
                            /* Prt Pre -Invc Reg                     */
             DCL        VAR(&PRTEXC) TYPE(*CHAR) LEN(1)
                            /* Prt Pree-Invc Exc                     */
             DCL        VAR(&JOBNME) TYPE(*CHAR) LEN(10) /* Job Name */
             DCL        VAR(&JOBNUMCDE) TYPE(*CHAR) LEN(6) /*JOB# ALPHA */
             DCL        VAR(&JOBNUM) TYPE(*CHAR) LEN(10) /*JOB# ALPHA */
             DCL        VAR(&ARCUSTP) TYPE(*DEC) /* A R CUSTOMER    */
             DCL        VAR(&PRCUSTT) TYPE(*CHAR) LEN(02)
             DCL        VAR(&DLTWRK) TYPE(*CHAR) LEN(1)

/* INTERNAL FIELDS                                                   */
             DCL        VAR(&CUSTNON) TYPE(*DEC) /* Customer Number */
             DCL        VAR(&RETURN) TYPE(*CHAR) LEN(7) /* RETURN +
                          CODE*/
             DCL        &CRDFLAG    *CHAR   (1)  VALUE(N)
             DCL        &PRCSTS     *CHAR   (1) /* PROCESS CUST TYPE */
             DCL        VAR(&USER) TYPE(*CHAR) LEN(10) /* USER */
             DCL        VAR(&USERPRC) TYPE(*CHAR) LEN(1) VALUE('N')
                            /* User Special Prcessing                */
             DCL        VAR(&USRCLS)   TYPE(*CHAR) LEN(10)
             DCL        VAR(&COMPANY) TYPE(*DEC) LEN(3 0) /*Company #*/
             DCL        VAR(&LOAD) TYPE(*DEC) LEN(7 0)    /* Load #  */
             DCL        VAR(&THRDAT) TYPE(*DEC) LEN(7 0)
                            /* Thru Actual Ship Date                 */
             DCL        VAR(&ORDR) TYPE(*DEC) LEN(7 0)    /*Order #  */
             DCL        VAR(&ARCUST) TYPE(*DEC) LEN(7 0)  /*Order #  */
             DCL        VAR(&COPY) TYPE(*DEC) LEN(2 0)    /*# of +
                          copies*/
             DCL        VAR(&COMMAND) TYPE(*CHAR) LEN(512)
                            /* For Trans. Reg. override              */
             DCL        VAR(&PGMO) TYPE(*CHAR) LEN(10) /*Printer File*/
             DCL        VAR(&CVRNME) TYPE(*CHAR) LEN(10) /*CS NAME*/
             DCL        VAR(&PRTSTS) TYPE(*CHAR) LEN(1)
                            /* Print Status                          */
             DCL        VAR(&COPY1) TYPE(*DEC) LEN(2 0) VALUE(1)
             DCL        VAR(&ONEPO) TYPE(*CHAR) LEN(1)
                            /* One Per Purhase Order                 */
             DCL        VAR(&JOBNO) TYPE(*CHAR) LEN(10) /*JOB# ALPHA*/

/*----------------------------------------------------------------- */
/* PARMS FOR THE RETRIEVE OVERLAY PARAMETERS PROGRAM                */
/*----------------------------------------------------------------- */
             DCL        VAR(&TYPE) TYPE(*CHAR) LEN(2)
             DCL        VAR(&DEPT) TYPE(*CHAR) LEN(6)
             DCL        VAR(&OVERLAY) TYPE(*CHAR) LEN(8)
             DCL        VAR(&CDDPGM) TYPE(*CHAR) LEN(10)
             DCL        VAR(&CDDFILE) TYPE(*CHAR) LEN(10)
             DCL        VAR(&ADDLOBJ) TYPE(*CHAR) LEN(10)
             DCL        VAR(&SENDPHONE) TYPE(*CHAR) LEN(10)
             DCL        VAR(&SENDFAX) TYPE(*CHAR) LEN(10)
             DCL        VAR(&OVOUTQ) TYPE(*CHAR) LEN(10)
             DCL        VAR(&OVHOLD) TYPE(*CHAR) LEN(4)
             DCL        VAR(&OVSAVE) TYPE(*CHAR) LEN(4)
             DCL        VAR(&DRAWER) TYPE(*DEC) LEN(3 0)
             DCL        VAR(&PRTDEVTYPE) TYPE(*CHAR) LEN(10)
             DCL        VAR(&LENGTH) TYPE(*DEC) LEN(3 0)
             DCL        VAR(&WIDTH) TYPE(*DEC) LEN(3 0)
             DCL        VAR(&CPI) TYPE(*DEC) LEN(3 1)
             DCL        VAR(&LPI) TYPE(*DEC) LEN(2 0)
             DCL        VAR(&OVERFLOW) TYPE(*DEC) LEN(3 0)
             DCL        VAR(&ROTATE) TYPE(*CHAR) LEN(5)
             DCL        VAR(&COMMENT1) TYPE(*CHAR) LEN(50)
             DCL        VAR(&COMMENT2) TYPE(*CHAR) LEN(50)
             DCL        VAR(&COMMENT3) TYPE(*CHAR) LEN(50)
             DCL        VAR(&COMMENT4) TYPE(*CHAR) LEN(50)
             DCL        VAR(&COMMENT5) TYPE(*CHAR) LEN(50)
             DCL        VAR(&RECSTS) TYPE(*CHAR) LEN(1)

             DCL        VAR(&STATUS) TYPE(*CHAR) LEN(1)

/********************************************************************/
/* CHANGE VARIABLES FOR USE IN PROGRAM                              */
/********************************************************************/
             CHGVAR     VAR(&COMPANY) VALUE(&COMPNYN)
             CHGVAR     VAR(&LOAD) VALUE(&LOADN)
             CHGVAR     VAR(&THRDAT) VALUE(&THRDATN)
             CHGVAR     VAR(&ORDR) VALUE(&ORDRN)
             CHGVAR     VAR(&ARCUST) VALUE(&ARCUSTP)
             CHGVAR     VAR(&COPY) VALUE(&COPYN)
             CHGVAR     VAR(&PGMO) VALUE('PDUWPFR$')
             CHGVAR     VAR(&CVRNME) VALUE('SBDFINV1  ')
             CHGVAR     VAR(&PRCSTS) VALUE('Y')
             CHGVAR     VAR(&ONEPO) VALUE('N')
             CHGVAR     VAR(&STATUS) VALUE('N')
             CHGVAR     VAR(&JOBNUM) VALUE(&JOBNUMCDE)
             CHGVAR     VAR(&DLTWRK) VALUE('N')

             RTVJOBA    USER(&USER) NBR(&JOBNO)
             RTVUSRPRF  USRCLS(&USRCLS)

/********************************************************************/
/* Determine if we have a USER that is a Programmer                 */
/********************************************************************/
             IF         COND(&USRCLS *EQ '*PGMR') THEN(DO)
               CHGVAR     VAR(&USERPRC) VALUE('Y')
             ENDDO

/********************************************************************/
/* Determine if we have a Standard Invoice or a Custom Invoice      */
/* The Custom Invoice will have an overlay                          */
/* At this time the Standard Invoice does not have a overlay        */
/*----------------------------------------------------------------- */
             CHGVAR     VAR(&TYPE) VALUE('IN') /* INVOICE */
             CHGVAR     VAR(&DEPT) VALUE('INCST1') /* CUSTOM INVOICE */

/* GET CUSTOM INVOICE DOCUMENT OVERLAY INFORMATION                  */
             CALL       PGM(PMG9XFR) PARM(' ' &COMPANY &TYPE &DEPT +
                          &OVERLAY &CDDPGM &CDDFILE &ADDLOBJ +
                          &SENDPHONE &SENDFAX &OVOUTQ &OVHOLD +
                          &OVSAVE &DRAWER &PRTDEVTYPE &LENGTH &WIDTH +
                          &LPI &CPI &OVERFLOW &ROTATE &COMMENT1 +
                          &COMMENT2 &COMMENT3 &COMMENT4 &COMMENT5 +
                          &RECSTS)

             IF         COND(&OUTQ *EQ '     ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(&OVOUTQ))

             IF         COND(&HOLD *EQ '     ') THEN(CHGVAR +
                          VAR(&HOLD) VALUE(&OVHOLD))

             IF         COND(&SAVE *EQ '     ') THEN(CHGVAR +
                          VAR(&SAVE) VALUE(&OVSAVE))

/********************************************************************/
/* VERIFY IF THIS SELECTION IS FOR INVOICE COMBINED                 */
/* IF ONE INVOICE PER PO IS Y THEN THE INVOICE COMBINED WILL BE     */
/* SELECTED. CUSTOM INVOICES WILL NOT HAVE ONE INVOICE PER PO       */
/********************************************************************/
             IF         COND(&CDDPGM *EQ '       ') THEN(CALL +
                          PGM(PMCPXFR) PARM(' ' &JOBNME &JOBNUM +
                          &ORDR &COMPANY &ONEPO))

/********************************************************************/
/* PRINTER OVERRIDES                                                */
/********************************************************************/
             IF         COND(&CDDPGM *EQ '       ') THEN(DO)
 combo:        OVRPRTF    FILE(PMCJPFR$) PAGESIZE(88 125) LPI(8) +
                            CPI(15) OVRFLW(82) DRAWER(3) OUTQ(&OUTQ) +
                            FORMTYPE(*INV) COPIES(&COPY) HOLD(&HOLD) +
                            SAVE(&SAVE)

/* W68695 - FORCE HOLD=*YES ON PDR4PFR REPORT */
/*  reginv:       OVRPRTF    FILE(PDR4PFR$) PAGESIZE(88 125) LPI(8) +
                            CPI(15) OVRFLW(82) DRAWER(3) OUTQ(&OUTQ) +
                            FORMTYPE(*INV) COPIES(&COPY) HOLD(&HOLD) +
                            SAVE(&SAVE)                              */
 reginv:       OVRPRTF    FILE(PDR4PFR$) PAGESIZE(88 125) LPI(8) +
                            CPI(15) OVRFLW(82) DRAWER(3) OUTQ(&OUTQ) +
                            FORMTYPE(*INV) COPIES(&COPY) HOLD(*YES) +
                            SAVE(&SAVE)

               SBDPRTOVR  CMDVAR(&COMMAND) FILE(PDVGPFR$) +
                            OUTQ(&OUTQ) COPIES(&COPY) HOLD(&HOLD) +
                            SAVE(&SAVE)
               CALL       PGM(QCMDEXC) PARM(&COMMAND 512)

               SBDPRTOVR  CMDVAR(&COMMAND) FILE(PUMFPFR$) +
                            OUTQ(&OUTQ) COPIES(&COPY) HOLD(*YES)  +
                            SAVE(*YES)
               CALL       PGM(QCMDEXC) PARM(&COMMAND 512)
             ENDDO      /* &CDDPGM BLANKS */

 custom:     IF         COND(&CDDPGM *NE '       ') THEN(DO)
               OVRPRTF    FILE(&CDDFILE) DEVTYPE(&PRTDEVTYPE) +
                            PAGESIZE(&LENGTH &WIDTH) LPI(&LPI) +
                            CPI(&CPI) OVRFLW(&OVERFLOW) +
                            DRAWER(&DRAWER) PAGRTT(&ROTATE) +
                            FRONTOVL(&OVERLAY) OUTQ(&OUTQ) +
                            HOLD(&HOLD) SAVE(&SAVE)
             ENDDO      /* &CDDPGM NOT BLANKS */

 ONEPO:      IF         COND(&ONEPO *EQ 'Y') THEN(DO)
               SBDPRTOVR  CMDVAR(&COMMAND) FILE(PMCKPFR$) +
                            OUTQ(&OUTQ) COPIES(&COPY) HOLD(&HOLD) +
                            SAVE(&SAVE)
               CALL       PGM(QCMDEXC) PARM(&COMMAND 512)
             ENDDO      /* END &ONEPO */

/* W80535 - ALWAYS PUT ON HOLD *YES FOR PDUWPFR REPORT */
/* W80535    SBDPRTOVR  CMDVAR(&COMMAND) FILE(PDUWPFR$) OUTQ(&OUTQ) +
                          COPIES(&COPY) HOLD(&HOLD) SAVE(&SAVE)      */
             SBDPRTOVR  CMDVAR(&COMMAND) FILE(PDUWPFR$) OUTQ(&OUTQ) +
                          COPIES(&COPY) HOLD(*YES) SAVE(&SAVE)  /* W80535 */
             CALL       PGM(QCMDEXC) PARM(&COMMAND 512)

/*********************************************************************/
/* CALL PRINT PROGRAM -                                              */
/*********************************************************************/
             IF         COND(&REPRINT *EQ 'R') THEN(DO)   /* REPRINT */

/* COMBO INVOICE                              SLM - 12/07/06         */
               IF         COND(&ONEPO *EQ 'Y') THEN(DO)
                 CALL       PGM(PMCRXFR) PARM(' ' &JOBNME &JOBNUM +
                              &ORDR &COMPANY &REPRINT)
               ENDDO      /* END &ONEPO */

/* NOT A COMBO INVOICE                                               */
               IF         COND(&ONEPO *NE 'Y') THEN(DO) /* NOT ONEPO */
                  IF         COND(&CDDPGM *EQ '       ') THEN(CALL +
                               PGM(PDVGPFR) PARM(' ' &COMPANY &ORDR +
                               &ARCUST &PRTSTS &DLTWRK &JOBNME))

/* Print Customer copy only for reprint FOR DAILYS                   */
                 IF         COND(&CDDPGM *EQ 'PMIVPFR') THEN(CALL +
                              PGM(PMIVPFR) PARM(' ' &COMPANY &ORDR +
                              &REPRINT 'CS' &JOBNME)) /* daily cust +
                              copy */
/*********************************************************************/
/* PRINT HPB BOL AND INVOICE                                         */
             IF         COND(&CDDPGM *EQ 'PMRCPFR') THEN(DO)
 HPBINV:     CALL       PGM(PMRCPFR) PARM(' ' &COMPANY &ORDR +
                          &REPRINT) /* hpb inv */

 HPBBOL:     CALL       PGM(PMWZXFR) PARM(' ' &COMPANY &ORDR &OUTQ +
                          &HOLD &SAVE &COPY1) /* hpb BOL */
             ENDDO                       /* END HPB INVOINCE AND BOL */
/* ------------------------------------------------------------------*/
               ENDDO                                 /* IF NOT ONEPO */
             ENDDO                                   /* REPRINT = R  */
/*********************************************************************/
/*  NOT REPRINT (R=REPRINT INVOICE & Y=PRINT INVOICE)                */
/*  COMBO INVOICE                   SLM - 12/07/06                   */
/*********************************************************************/
 PRINT:     IF         COND(&REPRINT *EQ 'Y') THEN(DO)
             IF         COND(&ONEPO *EQ 'Y') THEN(CALL PGM(PMCRXFR) +
                          PARM(' ' &JOBNME &JOBNUM &ORDR &COMPANY +
                          &REPRINT))

              IF         COND(&ONEPO *NE 'Y') THEN(DO)
 SBDINV:        IF         COND(&CDDPGM *EQ '       ') THEN(CALL +
                             PGM(PDR4PFR) PARM(' ' &COMPANY &ORDR +
                             &REPRINT))

/* HPB - PRINT BOTH THE INVOICE AND THE BOL                         */
 hpbinv2:       IF         COND(&CDDPGM *EQ 'PMRCPFR') THEN(DO)
                  CALL       PGM(PMRCPFR) PARM(' ' &COMPANY &ORDR +
                               &REPRINT)
 HPBBOL2:         CALL       PGM(PMWZXFR) PARM(' ' &COMPANY &ORDR +
                               &OUTQ &HOLD &SAVE &COPY1) /* hpb +
                               BOL */
                ENDDO                           /* END HPB INVOICES */

/* Dailys -- CUSTOM INVOICE PROGRAM-3 COPIES                        */
                IF         COND(&CDDPGM *EQ 'PMIVPFR') THEN(DO)
/* Customer copy                                                    */
                  CALL       PGM(PMIVPFR) PARM(' ' &COMPANY &ORDR +
                               &REPRINT 'CS' &JOBNME)
/* Office copy                                                      */
                  CALL       PGM(PMIVPFR) PARM(' ' &COMPANY &ORDR +
                               &REPRINT 'OF' &JOBNME)
/* Orginal copy                                                     */
                  CALL       PGM(PMIVPFR) PARM(' ' &COMPANY &ORDR +
                               &REPRINT 'OR' &JOBNME)
                ENDDO                         /* END DAILYS INVOICES */
              ENDDO                                /* END ONEPO NE Y */
            ENDDO                          /* END IF &REPRINT *EQ 'Y'*/

/* ------------------------------------------------------------------*/
/*  REPRINT: (R=REPRINT INVOICE & Y=PRINT INVOICE)                   */
               IF         COND(&REPRINT *EQ 'Y') THEN(DO)

    /* W81963 - ADDED OVRPRTF TO HOLD PDUWPFR REPORT WHEN CALLING PDTOXFR */
                 OVRPRTF    FILE(PDUWPFR$) OUTQ(&OUTQ) COPIES(1) +
                              HOLD(*YES) SAVE(*YES) /* W80535 */

/* Call SEL Invoices to Print  XF                                    */
                 CALL       PGM(PDTOXFR) PARM(' ' &COMPANY &ORDR +
                              &THRDAT &LOAD &BILTYP &PRCUSTT &STATUS)

 /* -----------------------------------------------------------------*/
/*  NOT A PROGRAMMER:                                                */
 RC0224:         IF         COND(&USERPRC *EQ 'N') THEN(DO)
 /* -----------------------------------------------------------------*/
 /*   IF 960, PRINT ON TFPRTDUP AND DONT DELETE SO SET PRCSTS=N      */
 /* -----------------------------------------------------------------*/
 CO960:            IF         COND(&COMPANY *EQ 960) THEN(DO)
                     CHGVAR     VAR(&PRCSTS) VALUE('N')
/* W80535 - ALWAYS PUT ON HOLD *YES FOR PDUWPFR REPORT */
/* W80535            OVRPRTF    FILE(PDUWPFR$) OUTQ(TFPRTDUP) +
                                  COPIES(1) HOLD(*NO) SAVE(*NO)      */
                     OVRPRTF    FILE(PDUWPFR$) OUTQ(TFPRTDUP) +
                                  COPIES(1) HOLD(*YES) SAVE(*NO) /* W80535 */
                     CALL       PGM(PDUWPFR) PARM(' ' &JOBNME +
                                  &COMPANY &ORDR &THRDAT &LOAD +
                                  &BILTYP &PRCSTS &PRCUSTT)
                   ENDDO                                  /* END 960 */
 CO363:
                   IF         COND(&COMPANY *EQ 363) THEN(DO)
                     CHGVAR     VAR(&PRCSTS) VALUE('N')
/* W80535 - ALWAYS PUT ON HOLD *YES FOR PDUWPFR REPORT */
/* W80535            OVRPRTF    FILE(PDUWPFR$) OUTQ(DSPRTDUP) +
                                  COPIES(1) HOLD(*NO) SAVE(*NO)      */
                     OVRPRTF    FILE(PDUWPFR$) OUTQ(DSPRTDUP) +
                                  COPIES(1) HOLD(*YES) SAVE(*NO) /* W80535 */
                     CALL       PGM(PDUWPFR) PARM(' ' &JOBNME +
                                  &COMPANY &ORDR &THRDAT &LOAD +
                                  &BILTYP &PRCSTS &PRCUSTT)
                   ENDDO                                 /* END 363 */
 CO364:
                   IF         COND(&COMPANY *EQ 364) THEN(DO)
                     CHGVAR     VAR(&PRCSTS) VALUE('N')
/* W80535 - ALWAYS PUT ON HOLD *YES FOR PDUWPFR REPORT */
/* W80535            OVRPRTF    FILE(PDUWPFR$) OUTQ(DMPRTDUP) +
                                  COPIES(1) HOLD(*NO) SAVE(*NO)      */
                     OVRPRTF    FILE(PDUWPFR$) OUTQ(DMPRTDUP) +
                                  COPIES(1) HOLD(*YES) SAVE(*NO) /* W80535 */
                     CALL       PGM(PDUWPFR) PARM(' ' &JOBNME +
                                  &COMPANY &ORDR &THRDAT &LOAD +
                                  &BILTYP &PRCSTS &PRCUSTT)
                   ENDDO                                  /* END 364 */
 CO440:
                   IF         COND(&COMPANY *EQ 440) THEN(DO)
                     CHGVAR     VAR(&PRCSTS) VALUE('N')
/* W80535 - ALWAYS PUT ON HOLD *YES FOR PDUWPFR REPORT */
/* W80535            OVRPRTF    FILE(PDUWPFR$) OUTQ(SPPRTDUP) +
                                  COPIES(1) HOLD(*NO) SAVE(*NO)      */
                     OVRPRTF    FILE(PDUWPFR$) OUTQ(SPPRTDUP) +
                                  COPIES(1) HOLD(*YES) SAVE(*NO) /* W80535 */
                     CALL       PGM(PDUWPFR) PARM(' ' &JOBNME +
                                  &COMPANY &ORDR &THRDAT &LOAD +
                                  &BILTYP &PRCSTS &PRCUSTT)
                   ENDDO                                  /* END 440 */
                 ENDDO                     /* END "NOT A PROGRAMMER" */
/*-------------------------------------------------------------------*/
/* PROGRAMMERS: PRINT THE TRANSACTION REGISTER - DON'T DELETE THE    */
/* WF INVOICE LIST WORK FILE RECORDS - PRINT TO ISPRT01              */

 PGMMRS:         IF         COND(&USERPRC  *EQ 'Y') THEN(DO)
 CO960_2:          IF         COND(&COMPANY *EQ 960) THEN(DO)
                     CHGVAR     VAR(&PRCSTS) VALUE('N')
                     OVRPRTF    FILE(PDUWPFR$) OUTQ(ISPRT01) +
                                  COPIES(&COPY) HOLD(*YES) SAVE(*YES)
                     CALL       PGM(PDUWPFR) PARM(' ' &JOBNME +
                                  &COMPANY &ORDR &THRDAT &LOAD +
                                  &BILTYP &PRCSTS &PRCUSTT)
                   ENDDO                                 /* END 960 */

/* PRINT THE TRANSACTION REGISTER AGAIN, BUT THIS TIME DELETE THE   */
/* WF INVOICE LIST WORK FILE RECORDS                                */
                   CHGVAR     VAR(&PRCSTS) VALUE('Y')
                   CALL       PGM(PDUWPFR) PARM(' ' &JOBNME &COMPANY +
                                &ORDR &THRDAT &LOAD &BILTYP &PRCSTS +
                                &PRCUSTT)
                   CALL       PGM(PPUPXFR) PARM(' ' &JOBNME)

 ENDPGMMRS:      ENDDO                       /* END FOR PROGRAMMERS */
               ENDDO                /* END &REPRINT *EQ Y (REPRINT) */

 END:        ENDPGM
