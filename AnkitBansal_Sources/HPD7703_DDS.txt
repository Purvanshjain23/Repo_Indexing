     A*%%TS  SD  20040414  080811  ISLRAMS     REL-V5R2M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD7703
     A*  TITLE:         VIEW FARM LINKS FOR A TEMPLATE
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  DATE:          02/02/04
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A                                      CF04(04 'PROMPT')
     A                                      CF05(05 'REFRESH')
     A*
     A*
     A*---------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP7703S1                  SFL
     A*%%TS  SD  20040212  145324  ISLRAMS     REL-V5R2M0  5722-WDS
     A  85                                  SFLNXTCHG
     A            HDRRN1         4Y 0H
     A            D1FSCD    R        O 11  3REFFLD(@@FSCD)
     A                                      EDTCDE(M)
     A            D1FSNM    R        O 11 11REFFLD(HSRREF/FSFSNM *LIBL/HSPREF)
     A            D1INAFL   R        O 11 40REFFLD(HSRREF/@@FLAG *LIBL/HSPREF)
     A            D1ACTFL   R        O 11 46REFFLD(HSRREF/@@FLAG *LIBL/HSPREF)
     A            D1CLOFL   R        O 11 51REFFLD(HSRREF/@@FLAG *LIBL/HSPREF)
     A            D1CELL    R        O 11 58REFFLD(FSREC/FSCELL *LIBL/HSP018)
     A                                      EDTCDE(M)
     A            D1TFCD    R        O 11 64REFFLD(@@TFCD)
     A            D1PPCD    R        O 11 73REFFLD(@@PPCD)
     A*
     A*
     A*---------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP7703C1                  SFLCTL(HP7703S1)
     A*%%TS  SD  20040414  080811  ISLRAMS     REL-V5R2M0  5722-WDS
     A                                      SFLSIZ(0050)
     A                                      SFLPAG(0011)
     A N82                                  ROLLUP(25 'PAGE FORWARD')
     A                                      SETOF(50)
     A                                      SETOF(51)
     A                                      SETOF(52)
     A                                      SETOF(53)
     A                                      SETOF(54)
     A                                      SETOF(55)
     A                                      RTNCSRLOC(&RCD &FLD)
     A                                      OVERLAY
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 26'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  2 18'VIEW FARM LINKS FOR A BUDGET TEMPL-
     A                                      ATE'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  4  2'Budget template:'
     A            C1BTCD    R        O  4 20REFFLD(@@BTCD)
     A                                      DSPATR(HI)
     A            C1BTDS    R        O  4 31REFFLD(HSRREF/@@BTDS *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A                                  6  6'?'
     A                                      DSPATR(HI)
     A                                  6 40'?'
     A                                      DSPATR(HI)
     A                                  6 46'?'
     A                                      DSPATR(HI)
     A                                  6 51'?'
     A                                      DSPATR(HI)
     A                                  6 66'?'
     A                                      DSPATR(HI)
     A                                  6 75'?'
     A                                      DSPATR(HI)
     A                                  7 38'- Budgets Exist -'
     A                                      DSPATR(HI)
     A                                  7 57'----- Farm Site -----'
     A                                      DSPATR(HI)
     A                                  8  4'Farm'
     A                                      DSPATR(HI)
     A                                  8 38'for Farm/Template'
     A                                      DSPATR(HI)
     A                                  8 63'Type of'
     A                                      DSPATR(HI)
     A                                  8 73'Prod'
     A                                      DSPATR(HI)
     A                                  9  4'Site'
     A                                      DSPATR(HI)
     A                                  9 11'Name'
     A                                      DSPATR(HI)
     A                                  9 38'Inact'
     A                                      DSPATR(HI)
     A                                  9 45'Act'
     A                                      DSPATR(HI)
     A                                  9 49'Closed'
     A                                      DSPATR(HI)
     A                                  9 57'Cell'
     A                                      DSPATR(HI)
     A                                  9 64'Farm'
     A                                      DSPATR(HI)
     A                                  9 73'Phase'
     A                                      DSPATR(HI)
     A            C1FSCD    R        B 10  3REFFLD(@@FSCD)
     A                                      EDTCDE(M)
     A                                      DSPATR(HI)
     A  53                                  DSPATR(PC)
     A            C1FSNM    R        B 10 11REFFLD(HSRREF/FSFSNM *LIBL/HSPREF)
     A                                      CHGINPDFT(UL LC FE)
     A                                      DSPATR(HI)
     A            C1INAFL   R        B 10 40REFFLD(HSRREF/@@FLAG *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A  50                                  DSPATR(PC)
     A            C1ACTFL   R        B 10 46REFFLD(HSRREF/@@FLAG *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A  51                                  DSPATR(PC)
     A            C1CLOFL   R        B 10 51REFFLD(HSRREF/@@FLAG *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A  52                                  DSPATR(PC)
     A            C1CELL    R        B 10 58REFFLD(HSRREF/@@CELL *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A            C1TFCD    R        B 10 64REFFLD(@@TFCD)
     A                                      DSPATR(HI)
     A  54                                  DSPATR(PC)
     A            C1PPCD    R        B 10 73REFFLD(@@PPCD)
     A                                      DSPATR(HI)
     A  55                                  DSPATR(PC)
     A*
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R HP7703K1
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A                                 23 11'F4=Prompt'
     A                                 23 22'F5=Refresh'
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *---------------------------------------------------------------
     A          R HP7703ES                  SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE CONTROL FORMAT
      *---------------------------------------------------------------
     A          R HP7703EC                  SFLCTL(HP7703ES)
      *
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
