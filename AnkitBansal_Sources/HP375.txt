      *****************  RPG PROGRAM HEADING  ************************
      * SYSTEM:      HOG PRODUCTION
      * PROGRAM:     HP375
      * TITLE:       BUILD WORKFILE FOR SCHEDULED MOVEMENT REPORT
      * PROGRAMMER:  LEANNE FEDOR
      * CREATED:     08/16/95
      *
      * *NOTE:       INCLUDE ONLY SCHEDULED MOVEMENTS (TRANSFERS AND
      *              SALES MOVEMENTS)
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 11/01/95  LEANNE FEDOR
      *           RECOMPILE ONLY. NEW FIELD 'GROUP TYPE' ADDED TO
      *           HOG GROUP FILE.
      *
      * 02/06/96  LEANNE FEDOR
      *           RECOMPILE ONLY. NEW FIELD 'BOL PRINTED COUNT' ADDED
      *           TO SALES MOVEMENT HEADER FILE.
      *
      * 02/13/96  LEANNE FEDOR
      *           RECOMPILE ONLY. NEW FIELD 'BOL PRINTED COUNT' ADDED
      *           TO TRANSFER MOVEMENT HEADER FILE.
      *
      * 05/13/96  LEANNE FEDOR
      *           MADE CHANGES TO FACILITATE NEW JDE RELEASE CONVERSION
      *           CHANGED SEARCH TYPE TO 3A; CALL NEW GENERIC
      *           PROGRAM HPJDEV TO VALIDATE INSTEAD OF HPJDEA SINCE
      *           NO VENDOR ADDRESS IS REQUIRED.
      *
      * 03/18/97  LEANNE FEDOR
      *           RECOMPILE ONLY. UPDATE TIME STAMP ADDED TO MOVEMENT
      *           EVENT FILE.
      *
      * 06/03/97  LEANNE FEDOR
      *           RECOMPILE ONLY. NEW FIELDS 'PURGE FLAG' AND 'DATE OF
      *           FIRST PURGE' ADDED TO HOG GROUP FILE.
      *
      * 02/16/98  LeAnne Fedor
      *           Recompile only. Added the following fields to the
      *           database for sales movement detail and check detail:
      *                     condemned head and pounds
      *                     yard dead head and pounds
      *
      * 04/13/98  LeAnne Fedor
      *           Recompile only. New field 'state' added to farm site file.
      *
      * 01/11/99  LeAnne Fedor
      *           Recompile only. New field 'scheduled kill date' added
      *           to sales movement header file.
      *
      * 10/17/00  LeAnne Fedor
      *           Recompile only. New field 'load type' added to the sales
      *           movement head file.
      *
      * 06/05/01  LeAnne Fedor
      *           Recompile only. New field 'multisite' added to Farm Site file.
      *
      * 09/04/01  LeAnne Fedor
      *           Recompile only. New 'square feet' fields added to Farm Site file.
      *
      * 12/18/02  LeAnne Fedor
      *           Production Phase/Type were removed from Sales Movement Header file.
      *           Modified logic here to use group production phase/type in Sales
      *           section.
      *
      * 07/06/09  LeAnne Ramsey
      *           Recompile only. Added new field 'Continuous Flow Flag' to Hog Group file.
      *
      * 12/09/11  LeAnne Ramsey (E1843)
      *           Added Origin and Destination State Code.
      *           And, if/when you are writing Transfer records to the workfile, we will
      *           check the LDA for the user's 'Transfers Across State Line' selection.
      *           The selction will be I=Include, E=Exclude or O=Only.
      *
      * 10/15/13  LeAnne Ramsey (E2831)
      *           Recompile only. Added field 'MTech Reference'.
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     FHSP018    IF   E           K DISK
      *    FARM SITE
      *
      *
     FHSL034D   IF   E           K DISK
      *    HOG GROUP
      *
      *
     FHSP058    IF   E           K DISK
      *    MOVEMENT EVENT FILE   (records selected by OPNQRY)
      *
      *
     FHSP074    IF   E           K DISK
      *  TRANSFER MOVEMENT HEADER
      *
      *
     FHSP075    IF   E           K DISK
      *  TRANSFER MOVEMENT DETAIL
      *
      *
     FHSP084    IF   E           K DISK
      *  SALE MOVEMENT HEADER
      *
      *
     FHSP085    IF   E           K DISK
      *  SALE MOVEMENT DETAIL
      *
      *
     FHSP375    O    E             DISK
      *    HOG MOVEMENT WORKFILE
      *
      /EJECT
      ****************************************************************
      * Definition Specifications
      ****************************************************************
      *
      * FOR ERROR MESSAGE HANDLING, A PACKED INDEX (E) IS REQUIRED.
      * FOR PROGRAM READABILITY, DEFINE A CORRESPONDING ERROR COUNT
      * FIELD CALLED 'ERROR'. ALSO, DEFINE CURSOR LOCATION FIELDS.
      *
     D                 DS                  INZ
     D  ERROR                  1      2P 0
     D  E                      1      2P 0
      * THE FOLLOWING 3 DATA STRUCTURES ARE USED TO SPEED MESSAGE
      * HANDLING SINCE IT IS FASTER TO CLEAR DATA STRUCTURES THAN
      * ARRAYS.  EACH IS ASSOCIATED WITH A STANDARD MESSAGE ARRAY.
      *
     D MGI             DS           140    INZ
     D  MGID                   1    140
     D                                     DIM(20)                              MSG ID ARRAY
      *
     D MGD             DS          1000    INZ
     D  MGDT                   1   1000
     D                                     DIM(20)                              MSG PARMS
      *
     D MGWK            DS            50    INZ
     D  MGW                    1     50
     D                                     DIM(50)                              MSG WORK PARMS
      *
      *---------------------------------------------------------------
      * STANDARD PROGRAM STATUS DATA STRUCTURE
      *---------------------------------------------------------------
      *    EXTERNALLY DEFINED AS UTPGFR (RECORD FORMAT: PGMDSR)
     D PGMDS         ESDS                  EXTNAME(UTPGFR)
      *
      *---------------------------------------------------------------
      *  LDA -  WITH THE REQUESTED PRINT SELECTIONS
      *---------------------------------------------------------------
      *
     d lda            uds                  dtaara(*lda)
     D  ldaslfl               51     51
      *
      *---------------------------------------------------------------
      *  NAMED CONSTANTS
      *---------------------------------------------------------------
      *
     D YES             C                   CONST('Y')
     D NO              C                   CONST('N')
      *
      *
      * Control fields
      *
     D procfl          s              1
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * MAIN LINE
      ****************************************************************
      *
      * READ EACH EVENT RECORD SELECTED BY THE OPEN QUERY AND PROCESS
      * EACH UNIQUE MOVEMENT.
      *
     C     *LOVAL        SETLL     HSP058
      *
     C     *IN90         DOUEQ     *ON                                          DO MAIN
     C                   READ      HSP058                                 90
     C     *IN90         IFEQ      *OFF                                         IF EVENT
     C     MEMVSN        ANDNE     SVMVSN
      *
     C                   Z-ADD     MEMVSN        SVMVSN
      *
     C                   Z-ADD     MEMVSN        WFMVSN
     C                   MOVEL     MEMTCD        WFMTCD
     C                   Z-ADD     MEMEDT        WFMEDT
     C                   Z-ADD     MECVNO        WFCVNO
      *
     C                   SELECT
     C                   when      memtcd = 'S' and ldaslfl <> 'O'
     C                   EXSR      $SALE
      *
     C     MEMTCD        WHENEQ    'T'
     C                   EXSR      $TRAN
     C                   ENDSL
      *
     C                   EXSR      $CLEAR
      *
     C                   ENDIF                                                   IF EVENT
     C                   ENDDO                                                   MAIN DO
      *
     C                   SETON                                        LR
      /EJECT
      *----------------------------------------------------------------
      * PROCESS A SALES MOVEMENT
      *----------------------------------------------------------------
      *
     C     $SALE         BEGSR
      *
      * RETRIEVE SALES MOVEMENT HEADER INFO AND CONTINUE PROCESSING
      * IF THE STATUS OF THE MOVEMENT IS 'SCHEDULED'
      *
     C     MEMVSN        CHAIN     HSP084                             92
     C     *IN92         IFEQ      *OFF                                         IF SCHEDULED
     C     SHMSCD        ANDEQ     'SC'
      *
     C                   MOVEL     SHMSCD        WFMSCD
     C                   MOVEL     SHSTCD        WFSTCD
     C                   MOVEL     SHSTCD        WFCODE
     C                   Z-ADD     SHSCDT        WFSCDT
     C                   MOVEL     SHTICD        WFTICD
      * ORIGIN
     C                   MOVEL     SHFSBO        WFORBO
     C                   Z-ADD     SHFSCD        WFORFS
      *
      * RETRIEVE THE CUSTOMER NAME
      *
     C                   Z-ADD     WFCVNO        INAJD
     C                   MOVEL(P)  'C'           INAT1
     C                   EXSR      $JDEV
     C     JDEFL         IFEQ      YES                                          IF NOT FOUND
     C                   MOVEL(P)  'UNKNOWN'     WFDNNM
     C                   ELSE
     C                   MOVEL     OUALPH        WFDNNM
     C                   ENDIF                                                  IF NOT FOUND
      *
      * RETREIVE ALL DETAIL RECORDS FOR THIS SALES MOVEMENT AND
      * WRITE A WORKFILE RECORD FOR EACH DETAIL RECORD.
      *
     C     MEMVSN        SETLL     HSP085
      *
     C     *IN91         DOUEQ     *ON                                          DO SALES DTL
     C     MEMVSN        READE     HSP085                                 91
     C     *IN91         IFEQ      *OFF                                         IF FOUND
      *
     C                   Z-ADD     SGSCHD        WFSCHD
      *
      * RETRIEVE HOG GROUP INFO
      *
     C                   Z-ADD     SGHGSN        WFORSN
     C     SGHGSN        CHAIN     HSL034D                            92
     C     *IN92         IFEQ      *OFF
     C                   MOVEL     HGHGCD        WFORHG
     C                   MOVEL     HGBLCD        WFORBL
     C                   MOVEL     HGRMCD        WFORRM
     C                   MOVEL     HGPTCD        WFORPT
     C                   MOVEL     HGPPCD        WFORPP
     C                   ELSE
     C                   MOVE      *BLANK        WFORHG
     C                   MOVE      *BLANK        WFORBL
     C                   MOVE      *BLANK        WFORRM
     C                   MOVEL     *BLANK        WFORPT
     C                   MOVEL     *BLANK        WFORPP
     C                   ENDIF
      *
     C                   WRITE     WFREC
     C                   ENDIF                                                  IF FOUND
     C                   ENDDO                                                  DO SALES DTL
     C                   ENDIF                                                  IF SCHEDULED
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * PROCESS A TRANSFER MOVEMENT
      *----------------------------------------------------------------
      *
     C     $TRAN         BEGSR
      *
     C                   MOVEL     'TRAN '       WFCODE
      *
      * RETRIEVE TRANSFER MOVEMENT HEADER INFO AND CONTINUE
      * PROCESSING IF THE STATUS IS 'SCHEDULED'
      *
     C     MEMVSN        CHAIN     HSP074                             92
     C     *IN92         IFEQ      *OFF                                         IF SCHEDULED
     C     MHMSCD        ANDEQ     'SC'
     C                   MOVEL     MHMSCD        WFMSCD
     C                   Z-ADD     MHSCDT        WFSCDT
     C                   MOVEL     MHTICD        WFTICD
      * ORIGIN
     C                   MOVEL     MHORBO        WFORBO
     C                   MOVEL     MHORPT        WFORPT
     C                   MOVEL     MHORPP        WFORPP
     C                   Z-ADD     MHORFS        WFORFS
      *
      * RETRIEVE THE STATE OF THE ORIGIN FARM
      *
     C     WFORFS        CHAIN     HSP018                             92
     C     *IN92         IFEQ      *OFF
     C                   MOVEL     fssacd        wforsacd
     C                   else
     C                   move      *blank        wforsacd
     C                   ENDIF
      * DESTINATION
     C                   MOVEL     MHDNBO        WFDNBO
     C                   MOVEL     MHDNPT        WFDNPT
     C                   MOVEL     MHDNPP        WFDNPP
     C                   Z-ADD     MHDNFS        WFDNFS
      *
      * RETRIEVE THE NAME AND STATE OF THE DESTINATION FARM
      *
     C     WFDNFS        CHAIN     HSP018                             92
     C     *IN92         IFEQ      *OFF
     C                   MOVEL     FSFSNM        WFDNNM
     C                   MOVEL     fssacd        wfdnsacd
     C                   ELSE
     C                   MOVEL(P)  'UNKNOWN'     WFDNNM
     C                   move      *blank        wfdnsacd
     C                   ENDIF
      *
      * In December 2011, we added a new user selection on Transfers. Users
      * may elect to Include/Exclude/Only Print Transfers that Cross a
      * State line. So, check the user's selection in the LDA and determine
      * whether to continue processing.
      * toohere
     C                   move      no            procfl
     C                   select
     C                   when      ldaslfl = 'I'
     C                   move      yes           procfl
      *
     C                   when      ldaslfl = 'E' and wforsacd = wfdnsacd
     C                   move      yes           procfl
      *
     C                   when      ldaslfl = 'O' and wforsacd <> wfdnsacd
     C                   move      yes           procfl
     C                   endsl
      *
      * RETREIVE ALL TRANSFER MOVEMENT DETAIL RECORDS FOR THIS
      * MOVEMENT AND WRITE A WORKFILE RECORD FOR EACH DETAIL RECORD.
      *
     C                   if        procfl = yes                                 If process
     C     MEMVSN        SETLL     HSP075
      *
     C     *IN91         DOUEQ     *ON                                          DO TRAN DTL
     C     MEMVSN        READE     HSP075                                 91
     C     *IN91         IFEQ      *OFF                                         IF TRAN DTL
      *
     C                   Z-ADD     MDSCHD        WFSCHD
      *
      * RETRIEVE ORIGIN HOG GROUP INFO
      *
     C                   Z-ADD     MDORSN        WFORSN
     C     MDORSN        CHAIN     HSL034D                            92
     C     *IN92         IFEQ      *OFF
     C                   MOVEL     HGHGCD        WFORHG
     C                   MOVEL     HGBLCD        WFORBL
     C                   MOVEL     HGRMCD        WFORRM
     C                   ELSE
     C                   MOVE      *BLANK        WFORHG
     C                   MOVE      *BLANK        WFORBL
     C                   MOVE      *BLANK        WFORRM
     C                   ENDIF
      *
      * RETRIEVE DESTINATION HOG GROUP INFO
      *
     C                   Z-ADD     MDDNSN        WFDNSN
     C     MDDNSN        CHAIN     HSL034D                            92
     C     *IN92         IFEQ      *OFF
     C                   MOVEL     HGHGCD        WFDNHG
     C                   MOVEL     HGBLCD        WFDNBL
     C                   MOVEL     HGRMCD        WFDNRM
     C                   ELSE
     C                   MOVE      *BLANK        WFDNHG
     C                   MOVE      *BLANK        WFDNBL
     C                   MOVE      *BLANK        WFDNRM
     C                   ENDIF
      *
     C                   WRITE     WFREC
      *
     C                   ENDIF                                                  IF TRAN DTL
     C                   ENDDO                                                  DO TRAN DTL
     C                   endif                                                  If process
     C                   ENDIF                                                  IF SCHEDULED
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * CLEAR THE WORKFILE FIELDS
      *----------------------------------------------------------------
      *
     C     $CLEAR        BEGSR
      *
     C                   Z-ADD     0             WFMVSN
     C                   MOVE      *BLANK        WFMTCD
     C                   MOVE      *BLANK        WFCODE
     C                   MOVE      *BLANK        WFMSCD
     C                   Z-ADD     0             WFMEDT
     C                   Z-ADD     0             WFSCDT
     C                   MOVE      *BLANK        WFTICD
      * ORIGIN
     C                   MOVE      *BLANK        WFORBO
     C                   MOVE      *BLANK        WFORPT
     C                   MOVE      *BLANK        WFORPP
     C                   Z-ADD     0             WFORFS
     C                   MOVE      *BLANK        WFORSACD
     C                   Z-ADD     0             WFORSN
     C                   MOVE      *BLANK        WFORHG
     C                   MOVE      *BLANK        WFORBL
     C                   MOVE      *BLANK        WFORRM
     C                   Z-ADD     0             WFSCHD
      *
      * DESTINATION
     C                   MOVE      *BLANK        WFDNBO
     C                   MOVE      *BLANK        WFDNPT
     C                   MOVE      *BLANK        WFDNPP
     C                   Z-ADD     0             WFDNFS
     C                   MOVE      *BLANK        WFDNSACD
     C                   Z-ADD     0             WFDNSN
     C                   MOVE      *BLANK        WFDNHG
     C                   MOVE      *BLANK        WFDNBL
     C                   MOVE      *BLANK        WFDNRM
     C                   MOVE      *BLANK        WFDNNM
      * SALES INFO
     C                   MOVE      *BLANK        WFSTCD
     C                   Z-ADD     0             WFCVNO
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * CALL THE EXTERNAL PROGRAM TO VALIDATE JDE ADDRESS NUMBERS
      *----------------------------------------------------------------
      *
     C     $JDEV         BEGSR
      *
     C                   MOVEL     NO            JDEFL                          JDE ERRORS
      *
     C                   CALL      'HPJDEV'
     C                   PARM                    MGI                            MSG ID'S
     C                   PARM                    MGD                            MSG DATA FLD
     C                   PARM                    ERROR                          # ERR MSGS
     C                   PARM                    INAJD                          JDE ADDR #
     C                   PARM                    INAT1                          JDE ADDR TYP
     C                   PARM                    JDEFL                          ERROR FLAG
     C                   PARM      *BLANK        OUALPH                         JDE NAME
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * INITIALIZATION SUBROUTINE
      *----------------------------------------------------------------
      *
     C     *INZSR        BEGSR
      *
     C     *LIKE         DEFINE    MEMVSN        SVMVSN
      *
      * J. D. EDWARDS INFO
     C                   MOVE      *BLANK        JDEFL             1            JDE ERR FLAG
     C                   MOVE      *BLANK        INAT1             3            JDE ADDR TYP
     C                   MOVE      *BLANK        OUALPH           40            JDE NAME
     C     *LIKE         DEFINE    FSFAJD        INAJD                          JDE ADDR IN
      *
     C                   ENDSR
      /EJECT
