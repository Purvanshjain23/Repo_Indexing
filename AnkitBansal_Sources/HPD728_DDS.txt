     A*%%TS  SD  19950220  145957  ISLRAMS     REL-V3R0M5  5763-PW1
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION
     A*  DISPLAY FILE:  HPD728
     A*  TITLE:         VIEW AUTHORIZED FUNCTIONS FOR A USER
     A*  CREATED BY:    LEANNE FEDOR
     A*  DATE:          02/13/95
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A                                      CF04(04 'PROMPT')
     A                                      CF05(05 'REFRESH')
     A                                      CA12(12 'PREVIOUS')
     A*
     A*--------------------------------------------------------------*
     A* SUBFILE FORMAT ONE
     A*--------------------------------------------------------------*
     A          R HP728S1                   SFL
     A*%%TS  SD  19950213  103559  ISLRAMS     REL-V3R0M5  5763-PW1
     A  85                                  SFLNXTCHG
     A            HDRRN1         4Y 0H
     A            DFFNCD    R        O  9  2REFFLD(FNFNCD)
     A            DFFNDS    R        O  9 14REFFLD(FNFNDS)
     A            DFFNTY    R        O  9 51REFFLD(FNFNTY)
     A            DFBOFL    R        O  9 62REFFLD(FNBOFL)
     A            DFDVCD    R        O  9 72REFFLD(FNDVCD)
     A*
     A*--------------------------------------------------------------*
     A* SUBFILE CONTROL FORMAT ONE
     A*--------------------------------------------------------------*
     A          R HP728C1                   SFLCTL(HP728S1)
     A*%%TS  SD  19950220  145957  ISLRAMS     REL-V3R0M5  5763-PW1
     A                                      SFLSIZ(0050)
     A                                      SFLPAG(0013)
     A N82                                  ROLLUP(25 'PAGE FORWARD')
     A                                      RTNCSRLOC(&RCD &FLD)
     A                                      OVERLAY
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            RCD           10A  H
     A            FLD           10A  H
     A                                  1  2'HP728'
     A                                      DSPATR(HI)
     A                                  1 26'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  2 19'VIEW AUTHORIZED FUNCTIONS FOR A US-
     A                                      ER'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  4  2'User ..:'
     A            C1USCD    R        O  4 12REFFLD(AUUSCD)
     A            C1USNM    R        O  4 24REFFLD(AUUSNM)
     A                                  5 51'?'
     A                                      DSPATR(HI)
     A                                  6  2'Function'
     A                                      DSPATR(HI)
     A                                  6 47'Function'
     A                                      DSPATR(HI)
     A                                  6 61'B/O'
     A                                      DSPATR(HI)
     A                                  6 70'Division'
     A                                      DSPATR(HI)
     A                                  7  2'Code'
     A                                      DSPATR(HI)
     A                                  7 14'Description'
     A                                      DSPATR(HI)
     A                                  7 49'Type'
     A                                      DSPATR(HI)
     A                                  7 59'Required'
     A                                      DSPATR(HI)
     A                                  7 72'Code'
     A                                      DSPATR(HI)
     A            C1FNCD    R        B  8  2REFFLD(FNFNCD)
     A                                      DSPATR(HI)
     A            C1FNTY    R        B  8 51REFFLD(FNFNTY)
     A                                      DSPATR(HI)
     A*
     A*--------------------------------------------------------------*
     A* COMMAND KEY RECORD 1
     A*--------------------------------------------------------------*
     A          R HP728K1
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A N90N63                          23 11'F4=Prompt'
     A                                 23 22'F5=Refresh'
     A                                 23 34'F12=Previous'
      *
      *--------------------------------------------------------------*
      * ERROR MESSAGE SUBFILE FORMAT
      *--------------------------------------------------------------*
     A          R HP728ES                   SFL SFLMSGRCD(24)
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *                                                                *********
      *--------------------------------------------------------------*
      * ERROR MESSAGE SUBFILE CONTROL FORMAT
      *--------------------------------------------------------------*
     A          R HP728EC                   SFLCTL(HP728ES)
      *
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
