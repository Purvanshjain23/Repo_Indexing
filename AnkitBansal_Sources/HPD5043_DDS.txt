     A*%%TS  SD  20040310  142627  ISLRAMS     REL-V5R2M0  5722-WDS
     A*****************************************************************
     A*
     A*  SYSTEM:        HOG PRODUCTION
     A*  DISPLAY FILE:  SELECT FARM BUDGET WITH OPTIONS FOR STATUS
     A*  CREATED BY:    LEANNE FEDOR
     A*  DATE:          03/10/04
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      PRINT
     A                                      CA12(12 'PREVIOUS')
     A                                      CF04(04 'PROMPT')
     A N82                                  ROLLUP(25 'PAGE FORWARD')
     A*---------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP5043S1                  SFL
     A*%%TS  SD  20040310  135735  ISLRAMS     REL-V5R2M0  5722-WDS
     A            HDRRN1    R        H      REFFLD(@@RRN)
     A            D1FBSN    R        O  9  1REFFLD(@@FBSN)
     A                                      EDTCDE(Z)
     A            D1FSCD    R        O  9 10REFFLD(@@FSCD)
     A                                      EDTCDE(Z)
     A            D1FSNM    R        O  9 17REFFLD(FSFSNM)
     A            D1FBSCD   R        O  9 45REFFLD(@@FBSCD)
     A            D1BTCD    R        O  9 52REFFLD(HSRREF/@@BTCD *LIBL/HSPREF)
     A*
     A*---------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP5043C1                  SFLCTL(HP5043S1)
     A*%%TS  SD  20040310  142627  ISLRAMS     REL-V5R2M0  5722-WDS
     A                                      SFLSIZ(0100)
     A                                      SFLPAG(0008)
     A                                      WINDOW(HP5043EC)
     A                                      SETOF(51)
     A                                      SETOF(52)
     A                                      SETOF(53)
     A                                      RTNCSRLOC(&RCD &FLD)
     A                                      OVERLAY
     A                                      SFLCSRRRN(&HDRRN)
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A            HDPAG1    R        H      REFFLD(@@PAG)
     A                                      SFLRCDNBR
     A            RCD           10A  H
     A            FLD           10A  H
     A            HDRRN     R        H      REFFLD(@@RRN)
     A            SDPGM     R        O  1  1REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 24'SELECT FARM BUDGET'
     A                                      DSPATR(HI)
     A            C1DESC        32A  O  2 17DSPATR(HI)
     A                                  3  1'Position Cursor, Press Enter'
     A                                  5 13'?'
     A                                      DSPATR(HI)
     A N62                              5 45'?'
     A                                      DSPATR(HI)
     A                                  5 54'?'
     A                                      DSPATR(HI)
     A                                  6  2'Budget'
     A                                      DSPATR(HI)
     A                                  6 11'Farm'
     A                                      DSPATR(HI)
     A                                  6 43'Budget'
     A                                      DSPATR(HI)
     A                                  6 52'Budget'
     A                                      DSPATR(HI)
     A                                  7  2'Number'
     A                                      DSPATR(HI)
     A                                  7 11'Site'
     A                                      DSPATR(HI)
     A                                  7 17'Name'
     A                                      DSPATR(HI)
     A                                  7 43'Status'
     A                                      DSPATR(HI)
     A                                  7 51'Template'
     A                                      DSPATR(HI)
     A            C1FBSN    R        B  8  1REFFLD(HSRREF/@@FBSN *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A            C1FSCD    R        B  8 10REFFLD(@@FSCD)
     A                                      DSPATR(HI)
     A  51                                  DSPATR(PC)
     A            C1FSNM    R        B  8 17REFFLD(FSFSNM)
     A                                      CHGINPDFT(UL LC FE)
     A                                      DSPATR(HI)
     A N62        C1FBSCD   R        B  8 45REFFLD(HSRREF/@@AIST *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A  52                                  DSPATR(PC)
     A            C1BTCD    R        B  8 52REFFLD(HSRREF/@@BTCD *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A  53                                  DSPATR(PC)
     A*
     A*---------------------------------------------------------------
     A* COMMAND LINE LINE
     A*---------------------------------------------------------------
     A          R HP5043K1
     A                                      WINDOW(HP5043EC)
     A                                      OVERLAY
     A  43                             17  1'Message program failed. Call IS.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 18  1'F12=Previous'
     A                                 18 15'F4=Prompt'
      *
     A*-------------------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
     A*-------------------------------------------------------------------------
     A          R HP5043ES                  SFL SFLMSGRCD(19)
      *
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
     A*
     A*-------------------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*-------------------------------------------------------------------------
     A*
     A          R HP5043EC                  SFLCTL(HP5043ES)
     A*%%TS  SD  19980327  105628  ISLRAMS     REL-V3R7M0  5716-PW1
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A                                      WINDOW(*DFT 20 60)
     A                                      WDWBORDER((*DSPATR RI) (*CHAR '    -
     A                                          '))
     A            MSGPGM                    SFLPGMQ(10)
     A*
     A*---------------------------------------------------------------
     A* ASSUME FORMAT -- REQUIRED FOR WINDOWS
     A*---------------------------------------------------------------
     A          R ASSUME
     A*%%TS  SD  20011025  160559  ISLRAMS     REL-V4R4M0  5769-PW1
     A                                      ASSUME
     A                                      OVERLAY
     A                                 24 79' '
