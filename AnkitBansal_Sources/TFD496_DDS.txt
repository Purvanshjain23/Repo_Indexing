     A*%%TS  SD  20041223  100725  ISLRAMS     REL-V5R2M0  5722-WDS
     A*****************************************************************
     A*
     A*  SYSTEM:        TRIUMPH FOODS
     A*  DISPLAY FILE:  TFD496
     A*  TITLE:         WORK WITH BASE MARKETING FEES
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  CREATED:       12/16/04
     A*
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/TFPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CF01(01 'DATES')
     A                                      CA03(03 'EXIT')
     A                                      CF05(05 'REFRESH')
     A                                      CF06(06 'CREATE')
     A                                      CF07(07 'LISTING')
     A                                      CF17(17 'PRINT OPTIONS')
     A                                      CF20(20 'WRKSPLF')
     A*
     A*
     A*---------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*---------------------------------------------------------------
     A          R TF496S1                   SFL
     A*%%TS  SD  20041220  073354  ISLRAMS     REL-V5R2M0  5722-WDS
     A  85                                  SFLNXTCHG
     A            HDRRN1         4Y 0H
     A            D1OPT          2A  B  9  2CHGINPDFT(UL)
     A  30                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A N30                                  DSPATR(HI)
     A            D1YRNO    R        O  9  8REFFLD(@@YRNO)
     A                                      EDTCDE(M)
     A            D1BMCFEE  R        O  9 18REFFLD(TFREF/@@BMCFEE)
     A                                      EDTCDE(K)
     A            D1BMDFEE  R        O  9 30REFFLD(TFREF/@@BMDFEE)
     A                                      EDTCDE(K)
     A            D1BMTFEE  R        O  9 41REFFLD(TFREF/@@BMTFEE *LIBL/TFPREF)
     A                                      EDTCDE(K)
     A            D1YBMDY   R        O  9 53REFFLD(TFREF/@@DT06 *LIBL/TFPREF)
     A                                      EDTWRD('  /  /  ')
     A            D1YEMDY   R        O  9 66REFFLD(TFREF/@@DT06 *LIBL/TFPREF)
     A                                      EDTWRD('  /  /  ')
     A*
     A*---------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*---------------------------------------------------------------
     A          R TF496C1                   SFLCTL(TF496S1)
     A*%%TS  SD  20041223  100725  ISLRAMS     REL-V5R2M0  5722-WDS
     A                                      SFLSIZ(0050)
     A                                      SFLPAG(0012)
     A N82                                  ROLLUP(25 'PAGE FORWARD')
     A                                      SETOF(51)
     A                                      SETOF(52)
     A                                      OVERLAY
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 23'WORK WITH BASE MARKETING FEES'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USR)
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@JOB)
     A                                      DSPATR(HI)
     A                                  3  2'2=Revise'
     A                                  3 12'5=View'
     A                                  3 20'6=Print'
     A                                  5 54'Year'
     A                                      DSPATR(HI)
     A                                  5 68'Year'
     A                                      DSPATR(HI)
     A                                  6  8'Year'
     A                                      DSPATR(HI)
     A                                  6 17'Current'
     A                                      DSPATR(HI)
     A                                  6 29'Deferred'
     A                                      DSPATR(HI)
     A                                  6 42'Total'
     A                                      DSPATR(HI)
     A                                  6 52'Beginning'
     A                                      DSPATR(HI)
     A                                  6 67'Ending'
     A                                      DSPATR(HI)
     A                                  7  2'Opt'
     A                                      DSPATR(HI)
     A                                  7  7'Number'
     A                                      DSPATR(HI)
     A                                  7 21'Fee'
     A                                      DSPATR(HI)
     A                                  7 33'Fee'
     A                                      DSPATR(HI)
     A                                  7 44'Fee'
     A                                      DSPATR(HI)
     A                                  7 54'Date'
     A                                      DSPATR(HI)
     A                                  7 68'Date'
     A                                      DSPATR(HI)
     A            C1YRNO    R        B  8  8REFFLD(TFREF/@@YRNO *LIBL/TFPREF)
     A                                      DSPATR(HI)
     A                                      EDTCDE(M)
     A*
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R TF496K1
     A*%%TS  SD  20041216  141250  ISLRAMS     REL-V5R2M0  5722-WDS
     A  43                             21  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 22  2'F1=Refresh Dates'
     A                                 23  2'F3=Exit'
     A                                 23 11'F5=Refresh'
     A                                 23 23'F6=Create'
     A                                 23 34'F7=Listing'
     A                                 23 46'F17=Print Options'
     A                                 23 65'F20=Spool Files'
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *---------------------------------------------------------------
     A          R TF496ES                   SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE CONTROL FORMAT
      *---------------------------------------------------------------
     A          R TF496EC                   SFLCTL(TF496ES)
      *
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
