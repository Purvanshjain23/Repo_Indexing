     A*%%TS  SD  20040209  111340  ISLRAMS     REL-V5R2M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD1707
     A*  TITLE:         MAINTAIN FARM LINKS FOR TEMPLATE
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  CREATED:       02/06/04
     A*
     A*  INDICATOR 60 IS ON WHEN IN REVISE MODE
     A*  INDICATOR 61 IS ON WHEN IN CREATE MODE
     A*
     A*  INDICATOR 63 IS ON WHEN THE SUBFILE FIELDS ARE NON-DISPLAY
     A*               BECAUSE THE RECORD WAS SUCCESSFULLY DELETED
     A*               (THE HIDDEN SUBFILE FIELD HDNDFL = YES)
     A*
     A*  INDICATOR 64 IS ON WHEN A SUBFILE RECORD FIELDS MUST BE PROTECTED.
     A*               (THE HIDDEN SUBFILE FIELD HDPFL = YES). FIELDS ARE
     A*               PROTECTED IF:
     A*               1) THE SUBFILE RECORD WAS SUCCESSFULLY CREATED
     A*
     A*  INDICATOR 90 IS ON WHEN THE TEMPLATE THE USER SELECTED
     A*               FROM THE WORK WITH HAS BEEN DELETED BY ANOTHER USER
     A**************************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A N90                                  CF04(04 'PROMPT')
     A N90                                  CF05(05 'REFRESH')
     A N90                                  CF10(10 'CHANGE MODES')
     A*
     A*---------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP1707S1                  SFL
     A*%%TS  SD  20040206  171113  ISLRAMS     REL-V5R2M0  5722-WDS
     A  85                                  SFLNXTCHG
     A            HDRRN1         4Y 0H
     A            HDPFL     R        H      REFFLD(@@FLAG)
     A            HDNDFL    R        H      REFFLD(@@FLAG)
     A            D1OPT          2A  B 11  2CHGINPDFT(UL)
     A  61
     AO 64
     AO 90                                  DSPATR(PR)
     A  61
     AO 64
     AO 90                                  DSPATR(ND)
     A  30                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A N30N61N64
     A N90                                  DSPATR(HI)
     A  60N30N64
     A N90                                  DSPATR(UL)
     A*
     A            D1FSCD    R        B 11  7REFFLD(@@FSCD)
     A                                      EDTCDE(M)
     A  60
     AO 64
     AO 90                                  DSPATR(PR)
     A  63                                  DSPATR(ND)
     A  34
     AO 54                                  DSPATR(PC)
     A  34                                  DSPATR(RI)
     A N34N60N64
     A N90                                  DSPATR(HI)
     A N34N60N64
     A N90                                  DSPATR(UL)
     A*
     A            D1FSNM    R        O 11 15REFFLD(FSFSNM)
     A  63                                  DSPATR(ND)
     A            D1CELL    R        O 11 43REFFLD(@@CELL)
     A  63                                  DSPATR(ND)
     A                                      EDTCDE(K)
     A            D1TFCD    R        O 11 49REFFLD(@@TFCD)
     A  63                                  DSPATR(ND)
     A            D1PPCD    R        O 11 59REFFLD(@@PPCD)
     A  63                                  DSPATR(ND)
     A            D1PTCD    R        O 11 67REFFLD(@@PTCD)
     A  63                                  DSPATR(ND)
     A*
     A*---------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*---------------------------------------------------------------
     A*
     A          R HP1707C1                  SFLCTL(HP1707S1)
     A*%%TS  SD  20040209  111340  ISLRAMS     REL-V5R2M0  5722-WDS
     A                                      SFLSIZ(0100)
     A                                      SFLPAG(0010)
     A                                      SETOF(30)
     A                                      SETOF(34)
     A                                      SETOF(50)
     A                                      SETOF(51)
     A                                      SETOF(52)
     A                                      SETOF(53)
     A                                      SETOF(54)
     A                                      SETOF(63)
     A N82N90                               ROLLUP(25 'PAGE FORWARD')
     A                                      SFLCSRRRN(&HDRR1)
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND(*MORE)
     A                                      OVERLAY
     A                                      RTNCSRLOC(&RCD &FLD)
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            HDRR1          5S 0H
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 25'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A            C1MODE         6A  O  2  2DSPATR(HI)
     A                                  2 19'MAINTAIN FARM LINKS FOR TEMPLATE'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  4  2'Template code:'
     A            C1BTCD    R        O  4 18REFFLD(HSRREF/@@BTCD *LIBL/HSPREF)
     A            C1BTDS    R        O  4 26REFFLD(HSRREF/@@BTDS *LIBL/HSPREF)
     A N61N90                           6  2'4=Delete'
     A N90                              7  9'?'
     A                                      DSPATR(HI)
     A N90N61                           7 51'?'
     A                                      DSPATR(HI)
     A N90N61                           7 61'?'
     A                                      DSPATR(HI)
     A N90N61                           7 68'?'
     A                                      DSPATR(HI)
     A                                  8  7'Farm'
     A                                      DSPATR(HI)
     A                                  8 48'Type of'
     A                                      DSPATR(HI)
     A                                  8 58'--Production--'
     A                                      DSPATR(HI)
     A                                  9  2'Opt'
     A                                      DSPATR(HI)
     A                                  9  7'Site'
     A                                      DSPATR(HI)
     A                                  9 15'Name'
     A                                      DSPATR(HI)
     A                                  9 42'Cell'
     A                                      DSPATR(HI)
     A                                  9 49'Farm'
     A                                      DSPATR(HI)
     A                                  9 59'Phase'
     A                                      DSPATR(HI)
     A                                  9 67'Type'
     A                                      DSPATR(HI)
     A N90        C1FSCD    R        B 10  7REFFLD(HSRREF/@@FSCD *LIBL/HSPREF)
     A                                      EDTCDE(M)
     A  50                                  DSPATR(PC)
     A                                      DSPATR(HI)
     A  61                                  DSPATR(ND)
     A  61                                  DSPATR(PR)
     A*
     A N90        C1FSNM    R        B 10 15REFFLD(FSFSNM)
     A                                      CHGINPDFT(UL LC FE)
     A                                      DSPATR(HI)
     A  61                                  DSPATR(ND)
     A  61                                  DSPATR(PR)
     A*
     A N90        C1CELL    R        B 10 43REFFLD(HSRREF/@@CELL *LIBL/HSPREF)
     A                                      EDTCDE(M)
     A                                      DSPATR(HI)
     A  61                                  DSPATR(ND)
     A  61                                  DSPATR(PR)
     A*
     A N90        C1TFCD    R        B 10 49REFFLD(HSRREF/@@TFCD *LIBL/HSPREF)
     A  51                                  DSPATR(PC)
     A                                      DSPATR(HI)
     A  61                                  DSPATR(ND)
     A  61                                  DSPATR(PR)
     A*
     A N90        C1PPCD    R        B 10 59REFFLD(HSRREF/@@PPCD *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A  61                                  DSPATR(ND)
     A  61                                  DSPATR(PR)
     A  52                                  DSPATR(PC)
     A*
     A N90        C1PTCD    R        B 10 67REFFLD(HSRREF/@@PTCD *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A  61                                  DSPATR(ND)
     A  61                                  DSPATR(PR)
     A  53                                  DSPATR(PC)
     A*
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R HP1707K1
     A*%%TS  SD  20010125  155711  ISLRAMS     REL-V4R4M0  5769-PW1
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A                                      DSPATR(HI)
     A                                 23  2'F3=Exit'
     A N90                             23 11'F4=Prompt'
     A N90                             23 22'F5=Refresh'
     A  60N90                          23 34'F10=Go to Create'
     A  61N90                          23 34'F10=Go to Revise'
     A*
     A*---------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE FORMAT
     A*---------------------------------------------------------------
     A          R HP1707ES                  SFL SFLMSGRCD(24)
     A*
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
     A*
     A*---------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*---------------------------------------------------------------
     A          R HP1707EC                  SFLCTL(HP1707ES)
      *
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
