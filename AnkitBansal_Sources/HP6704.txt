      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Hog Production System
      * PROGRAM:     HP6704
      * TITLE:       Listing of Unlinked Farms
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     02/18/04
      *
      *            This listing is submitted from HP4700-Work With Budget Templates.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 10/16/13  LeAnne Ramsey (E2831)
      *           Recompile only. Added field 'MTech Reference'.
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fhsp018    if   e           k disk
      *    Farm sites
      *
      *
     Fhsl187a   if   e           k disk
      *    Budget template/farm links
      *
      *
     Fqprint    o    f   80        printer oflind(*inof)
      *
      /EJECT
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Standard fields
      *
     D prtfl           s              1    inz('N')
     D dash            s             80    inz(*all'-')
     d rtime           s              6  0
      *
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      *******************************************************************************
      * MAINLINE
      *******************************************************************************
      *
      * Prepare to read the file and print headings the first time through.
      *
     C     *loval        setll     hsp018
     C                   except    h1hdr
      *
      * Read all records in the file. If the farm is Active, continue.
      *
     C                   dou       *in90 = *on                                  Main do
     C                   read      hsp018                                 90
     C                   if        *in90 = *off and fsaist = 'A'                If not EOF
      *
      * See if the Farm is linked to any Budget Template. If not, print.
      *
     C     fsfscd        chain     hsl187a                            92
     C                   if        *in92 = *on                                  If no link
      *
     C                   if        *inof = *on
     C                   except    h1hdr
     C                   endif
     C                   except    l1dtl
     C                   movel     yes           prtfl
     C                   endif                                                  If no link
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Main do
      *
      *-----------------------------------------------------------------------------
      * EOF processing
      *-----------------------------------------------------------------------------
      *
      * If no records met the user's selections, print the standard 'no data...'
      * error message
      *
     C                   if        prtfl = no
     C                   except    nodata
     C                   endif
      *
     C                   seton                                        lr
      /eject
      *-------------------------------------------------------------------------------------
      * Initialization subroutine
      *-------------------------------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Key lists
      *  None
      *
      * Set up heading info.
      *
     C                   time                    rtime
      *
     C                   endsr
      /EJECT
      *-------------------------------------------------------------
      * Report heading lines
      *-------------------------------------------------------------
      *
     Oqprint    e            h1hdr          1 01
     O                       sdpgm               10
     O                                           50 'HOG PRODUCTION SYSTEM'
     O                                           70 'DATE'
     O                       udate         y     80
      *
     O          e            h1hdr          1
     O                       sdusr               10
     O                                           52 'LISTING OF UNLINKED FARMS'
     O                                           70 'TIME'
     O                       rtime               80 '  :  :  '
      *
     O          e            h1hdr          2
     O                                           70 'PAGE'
     O                       page          z     80
      *
      *
      *-------------------------------------------------------------
      * Column headings
      *-------------------------------------------------------------
      *
     O          e            h1hdr       0  1
     O                                            5 'Farm'
     O                                           58 'Type of'
     O                                           78 '-- Production --'
      *
      *
     O          e            h1hdr       0  1
     O                                            5 'Site'
     O                                           13 'Name'
     O                                           42 'Cell'
     O                                           56 'Farm'
     O                                           68 'Phase'
     O                                           77 'Type'
      *
     O          e            h1hdr       0  2
     O                       dash                80
      *
      *-------------------------------------------------------------
      * Detail line
      *-------------------------------------------------------------
      *
     O          e            l1dtl          1
     O                       fsfscd        m      6
     O                       fsfsnm              34
     O                       fscell        m     42
     O                       fstfcd              57
     O                       fsppcd              68
     O                       fsptcd              78
      *
      *-------------------------------------------------------------
      * No data message line
      *-------------------------------------------------------------
      *
     O          e            nodata      1
     O                                           20 'All farms are linked'
     O                                           46 ' to at least one template.'
