     A*%%TS  SD  20071130  104657  ISLRAMS     REL-V5R4M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD411
     A*  TITLE:         SPECIFY GROUP SOURCE REPORT OPTIONS
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  DATE:          12/16/99
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A N99                                  CF04(04 'PROMPT')
     A N99 44                               CF08(08 'SUBMIT')
     A                                      CA12(12 'PREVIOUS')
     A N99                                  CF17(17 'PRINT OPTIONS')
     A N99                                  CF20(20 'WRKSPLF')
     A*
     A*---------------------------------------------------------------
     A* RECORD FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP411R1
     A*%%TS  SD  20071130  104657  ISLRAMS     REL-V5R4M0  5722-WDS
     A                                      SETOF(30)
     A                                      SETOF(31)
     A                                      SETOF(32)
     A                                      SETOF(33)
     A                                      SETOF(34)
     A                                      SETOF(35)
     A                                      SETOF(36)
     A                                      SETOF(37)
     A                                      SETOF(38)
     A                                      SETOF(39)
     A                                      SETOF(41)
     A                                      SETOF(42)
     A                                      SETOF(51)
     A                                      SETOF(52)
     A                                      SETOF(53)
     A                                      SETOF(54)
     A                                      SETOF(55)
     A                                      SETOF(56)
     A                                      SETOF(57)
     A                                      SETOF(58)
     A                                      OVERLAY
     A                                      RTNCSRLOC(&RCD &FLD)
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 27'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  2 20'SPECIFY GROUP SOURCE REPORT OPTION-
     A                                      S'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  4  2'Business office .....'
     A                                  4 24'?'
     A            DFFSBO    R        B  4 26REFFLD(BOBOBO)
     A N31                                  DSPATR(HI)
     A  31
     AO 51                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A            DFBODS    R        O  4 34REFFLD(BOBODS)
     A                                  4 65'(Blank for all) '
     A                                  5  2'Report ..............'
     A                                  5 24'?'
     A            DFRPFL    R        B  5 26REFFLD(@@FLAG)
     A N42                                  DSPATR(HI)
     A  42                                  DSPATR(RI)
     A  42
     AO 52                                  DSPATR(PC)
     A  99                                  DSPATR(PR)
     A            DFRPDS    R        O  5 34REFFLD(@@DS30)
     A                                  6  2'Mixed group option ..'
     A                                  6 24'?'
     A            DFMGFL    R        B  6 26REFFLD(@@FLAG)
     A N33                                  DSPATR(HI)
     A  33                                  DSPATR(RI)
     A  33
     AO 53                                  DSPATR(PC)
     A  99                                  DSPATR(PR)
     A            DFMGDS        41A  O  6 34
     A            DFMGDS2       45A  O  7 34
     A                                  9  2'From closed date ....'
     A            DFCFMDY   R     Y  B  9 26REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A N30                                  DSPATR(HI)
     A  30                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A                                 10  2'To closed date ......'
     A            DFCTMDY   R     Y  B 10 26REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A N39                                  DSPATR(HI)
     A  39                                  DSPATR(PC)
     A  39                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A                                 11  6'--OR--'
     A                                      DSPATR(HI)
     A                                 12  2'From open date ......'
     A            DFOFMDY   R     Y  B 12 26REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A N32                                  DSPATR(HI)
     A  32                                  DSPATR(PC)
     A  32                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A                                 12 37'(Blank for groups that are current-
     A                                      ly open)'
     A                                 13  2'To open date ........'
     A            DFOTMDY   R     Y  B 13 26REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A N41                                  DSPATR(HI)
     A  41                                  DSPATR(PC)
     A  41                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A                                 13 37'(Blank for groups that are current-
     A                                      ly open)'
     A                                 16  2'Production type .....'
     A                                 16 24'?'
     A            DFPTCD    R        B 16 26REFFLD(PTPTCD)
     A N34                                  DSPATR(HI)
     A  34
     AO 54                                  DSPATR(PC)
     A  34                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A            DFPTDS    R        O 16 34REFFLD(PTPTDS)
     A                                 16 65'(Blank for all) '
     A                                 17  2'Production phase ....'
     A                                 17 24'?'
     A            DFPPCD    R        B 17 26REFFLD(PPPPCD)
     A N35                                  DSPATR(HI)
     A  35
     AO 55                                  DSPATR(PC)
     A  35                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A            DFPPDS    R        O 17 34REFFLD(PPPPDS)
     A                                 17 65'(Blank for all) '
     A                                 18  2'Farm site ...........'
     A                                 18 24'?'
     A            DFFSCD    R        B 18 26REFFLD(FSFSCD)
     A                                      EDTCDE(M)
     A N36                                  DSPATR(HI)
     A  36
     AO 56                                  DSPATR(PC)
     A  36                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A            DFFSNM    R        O 18 34REFFLD(FSFSNM)
     A                                 18 65'(Blank for all) '
     A                                 19  2'Group type ..........'
     A                                 19 24'?'
     A            DFGTCD    R        B 19 26REFFLD(HSRREF/@@GTCD *LIBL/HSPREF)
     A N38                                  DSPATR(HI)
     A  38
     AO 58                                  DSPATR(PC)
     A  38                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A            DFGTDS    R        O 19 34REFFLD(GTREC/GTGTDS *LIBL/HSP025)
     A                                 19 65'(Blank for all) '
     A                                 20  2'Hog group ...........'
     A                                 20 24'?'
     A            DFHGCD    R        B 20 26REFFLD(HSRREF/HGHGCD *LIBL/HSPREF)
     A N37                                  DSPATR(HI)
     A  37                                  DSPATR(RI)
     A  37
     AO 57                                  DSPATR(PC)
     A  99                                  DSPATR(PR)
     A                                 20 65'(Blank for all)'
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R HP411K1
     A*%%TS  SD  19950531  161912  ISLRAMS     REL-V3R0M5  5763-PW1
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A N99                             23 11'F4=Prompt'
     A  44                             23 22'F8=Submit'
     A N99                             23 33'F12-Previous'
     A N99                             23 47'F17=Print Options'
     A                                 23 65'F20=Spool Files'
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *---------------------------------------------------------------
     A          R HP411ES                   SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
     A*---------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*---------------------------------------------------------------
     A*
     A          R HP411EC                   SFLCTL(HP411ES)
     A*%%TS  SD  19940705  110806  ISRCENT     REL-V2R2M0  5738-PW1
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
