      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Hog Production System
      * PROGRAM:     HP372 - Preliminary Disposed Group Error Listing
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     07/14/03
      *
      * FUNCTION: This program prints a listing of each 'disposed' hog group that
      *           cannot be closed for the period due to errors.
      *
      *  Error Messages
      *
      *           Error messages are retrieved through a call to CLP UT80046 using the same
      *           user message file, arrays and array logic as in the interactive programs.
      *           However, they are printed from the CL using the command "CLPRINT' that was
      *           created by Ernie Wallace.
      *
      *           QPRINT is used for the output file since the command CLPRINT prints to
      *           QPRINT. QPRINT is overridden in the submission CL to 'SHARE = YES" to allow
      *           both this RPG program and the CLP UT80046 to write to the same print file.
      *
      *           Since the 'CLPRINT' command logic in UT80046 is not being used to print
      *           headings, count lines or page break, these functions are controlled from
      *           this RPG program by counting the lines printed.
      *
      *****************************************************************
      * MODIFICATIONS:
      *****************************************************************
      * DATE      PROGRAMMER
      *
      * 05/18/04  LeAnne Fedor
      *           The users can now generate this report on-demand with a cut-off date.
      *           For the on-demand version, Alice had us skip all FEED edits.
      *
      * 04/23/09  LeAnne Ramsey
      *           Recompile only. Costed Time/User added to Feed Ticket Header.
      *
      * 05/12/09  LeAnne Ramsey
      *           Recompile only. PickUp Ticket Type added to Feed Ticket Header.
      *
      * 07/06/09  LeAnne Ramsey
      *           Recompile only. Added new field 'Continuous Flow Flag' to Hog Group file.
      *
      * 09/21/09  LeAnne Ramsey
      *           Recompile only. Added new field 'Upload Date' to Feed Ticket Header.
      *
      * 03/26/20  Danny Nguyen - P405 - Farm Number Increase
      *           Recompile only. Increased Bin Code (@@BNCD) in HSPREF file from 5A to 6A.
      *
      * 05/10/22 Eric L SDN736 Recompiled ticket nbr increase (TKNO & RTNO 7.0 TO 9.0)
      /EJECT
      *****************************************************************
      * FILE SPECIFICATIONS
      *****************************************************************
      *
     Fhsl034e   if   e           k disk
      *    Hog group
      *
      *
     Fhsj034l   if   e           k disk    rename(hgrec:hgrecl) prefix(p1)
      *  Hog group + Rooms/Bin sets + Bin set bins + Feed ticket detail
      *
      *
     Fhsl038e   if   e           k disk    rename(fdrec:fdrece)
      *   Feed ticket detail
      *
      *
     Fhsj038h   if   e           k disk
      *   Feed ticket detail + feed ticket header + status codes
      *
      *
     Fhsl058d   if   e           k disk
      *    Movement event
      *
      *
     Fhsp074    if   e           k disk
      *  Transfer movement header
      *
      *
     Fhsp084    if   e           k disk
      *  Sales movement header
      *
      *
     Fhsp068    if   e           k disk
      *  Killed/dead hogs
      *
      *
     Fhsp069    if   e           k disk
      *  Inventory adjustments
      *
      *
     Fqprint    o    f  132        printer oflind(*inof)
      *
      /EJECT
      ****************************************************************
      * Definition specifications
      ****************************************************************
      *
      *---------------------------------------------------------------
      *  Named Constants
      *---------------------------------------------------------------
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      *---------------------------------------------------------------
      * Standalone fields
      *---------------------------------------------------------------
      *
      * Standard fields
      *
     D pgm             s             10
     D msgfl           s             10
     D routne          s              6
     D maxmsg          s              2  0  inz(20)
      *
     D dash            s            132    inz(*all'-')
     d rtime           s              6  0
      *
      *
      * Control/flag fields
      *
     D hs00470fl       s              1    inz('N')
     D hs00474fl       s              1    inz('N')
     D hs00475fl       s              1    inz('N')
     D hs00693fl       s              1    inz('N')
     D hs00694fl       s              1    inz('N')
     D hs00695fl       s              1    inz('N')
     D hs00696fl       s              1    inz('N')
     D hs00895fl       s              1    inz('N')
     D hs00896fl       s              1    inz('N')
     D hs00897fl       s              1    inz('N')
      *
     D quitfl          s              1    inz('N')
      *
     D wkdifl          s              3  0                                      lines left
     D wkovfl          s              3  0 inz(62)                              overflow line
     D wkcurl          s              3  0                                      current line
      *
      *
      *
      * Print fields
      *
     D href            s              9
      *
     D d1opdtmdy       s              6  0
     D d1dsdtmdy       s              6  0
      *
     D t1grps          s              5  0
     D t1errgrps       s              5  0
      *
      *
      * Workfields for date manipulations
      *
      *
     D wkisodate       s               D   datfmt(*iso)
      *
      *
      * Parm fields
      *
     D xxerror         s              1
      *
      *
      * Parm fields for the generic movement program
      *
     D zzcymd          s              8  0
      *
     D @@nu50          s              5  0
     D gpihd           s                   like(@@nu50)
     D pinhd           s                   like(@@nu50)
     D tinhd           s                   like(@@nu50)
     D tisphd          s                   like(@@nu50)
     D tidphd          s                   like(@@nu50)
     D pouhd           s                   like(@@nu50)
     D touhd           s                   like(@@nu50)
     D tosphd          s                   like(@@nu50)
     D todphd          s                   like(@@nu50)
     D rinhd           s                   like(@@nu50)
     D rouhd           s                   like(@@nu50)
     D qinhd           s                   like(@@nu50)
     D qouhd           s                   like(@@nu50)
     D morhd           s                   like(@@nu50)
     D inahd           s                   like(@@nu50)
      *
     D @@nu92          s              9  2
     D pinlb           s                   like(@@nu92)
     D tinlb           s                   like(@@nu92)
     D tisplb          s                   like(@@nu92)
     D tidplb          s                   like(@@nu92)
     D poulb           s                   like(@@nu92)
     D toulb           s                   like(@@nu92)
     D tosplb          s                   like(@@nu92)
     D todplb          s                   like(@@nu92)
     D rinlb           s                   like(@@nu92)
     D roulb           s                   like(@@nu92)
     D qinlb           s                   like(@@nu92)
     D qoulb           s                   like(@@nu92)
     D morlb           s                   like(@@nu92)
      *
      *
      *---------------------------------------------------------------
      * Standard error message handling data structures
      *---------------------------------------------------------------
      *
      * For error message handling, a packed index (3) is required.
      * For program readability, define a corresponding error count
      * field called 'error'
      *
     D                 ds                  inz
     D  error                         2p 0
     D   e                            2p 0 overlay(error)
      *
      * This data structure supplies the name of the message file to
      * the message handling CL program.  The field name MSGFIL must be
      * constant. The value in quotes is the name of the specific
      * message file containing the user defined messages.
      *
     D                 ds                  inz
     D  msgfil                       10    inz('HSMSGF    ')
      *
      *---------------------------------------------------------------
      *  Standard message data structures
      *---------------------------------------------------------------
      *
      * The following 3 data structures are used to speed message
      * handling since it is faster to clear data structures than
      * arrays.  Each is associated with a standard message array.
      *
     D mgi             ds                  inz
     D  mgid                          7    dim(20)
      *
     D mgd             ds                  inz
     D  mgdt                         50    dim(20)
      *
     D mgwk            ds                  inz
     D  mgw                           1    dim(50)
      *
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *----------------------------------------------------------------
      * LDA
      *----------------------------------------------------------------
      *
     D                uds
     D  ldbpdt                 1     08  0
     D  ldepdt                 9     16  0
     D  ldper                 17     18  0
     D  ldeffl                19     19
     D  ldacyr                20     23  0
     D  ldbmdy                60     66  0
     D  ldemdy                70     76  0
     D  lderfl                80     80
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * Mainline
      ****************************************************************
      *
      * Print headings
      *
     C                   exsr      $hrhdr
      *
      * Read each 'Disposed' group selected by the open query.
      *
     C     *loval        setll     hsl034e                                90
      *
     C                   dou       *in90 = *on                                  Main do loop
     C                   read      hsl034e                                90
     C                   if        *in90 = *off                                 If not EOP
      *
      * Increment counter of total groups to be disposed
      *
     C                   add       1             t1grps
      *
      * Edit group
     C                   exsr      $edit
      *
     C                   if        error > 0                                    If errors
     C                   exsr      $d1dtl
     C                   exsr      $wrmsg
     C                   z-add     0             error
     C                   exsr      $clmsg
     C                   endif                                                  If errors
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Main do loop
      *
      *-----------------------------------------------------------------------
      * EOF Processing
      *-----------------------------------------------------------------------
      *
      * Print out 'total' group blurbs.
      *
     C                   exsr      $t1tot
      *
      * Set 'error' parm value.
      *
     C                   if        t1errgrps <> 0
     C                   move      yes           xxerror
     C                   endif
      *
     C                   seton                                        lr
      /EJECT
      *--------------------------------------------------------------------------------------
      * Print hog group line
      *--------------------------------------------------------------------------------------
      *
     C     $d1dtl        begsr
      *
      * Increment counter of 'error' groups
      *
     C                   add       1             t1errgrps
      *
      * Format dates
     C                   exsr      $dates
      *
      * Check for overflow
      *
     C                   if        wkcurl > wkovfl
     C                   exsr      $hrhdr
     C                   endif
      *
      * Print detail line
     C                   except    d1dtl
     C                   add       2             wkcurl
      *
     C                   endsr
      /EJECT
      *--------------------------------------------------------------------------------------
      * Flip dates from CCYYMMDD to MMDDYY format for printing
      *--------------------------------------------------------------------------------------
      *
     C     $dates        begsr
      *
      * Open date
      *
     C     *iso          test(d)                 hgopdt                 92
     C                   if        *in92 = *off                                 If OK date
     C                   move      hgopdt        wkisodate
     C     *mdy          move      wkisodate     d1opdtmdy
     C                   endif                                                  If OK date
      *
      * Dispose date
      *
     C     *iso          test(d)                 hgdsdt                 92
     C                   if        *in92 = *off                                 If OK date
     C                   move      hgdsdt        wkisodate
     C     *mdy          move      wkisodate     d1dsdtmdy
     C                   endif                                                  If OK date
      *
     C                   endsr
      /EJECT
      *--------------------------------------------------------------------------------------
      * Edit group
      *--------------------------------------------------------------------------------------
      *
     C     $edit         begsr
      *
      * Do not perform FEED edits for the On-Demand Version.
      *
     C                   if        ldeffl = 'D'
     C                   else
     C                   exsr      $feed
     C                   endif
      *
      *
      * Check that group inventory is zero
      *
     C                   z-add     hgdsdt        zzcymd
     C                   call      'HPMOVE'      plist01
      *
     C                   if        gpihd <> 0 and error < maxmsg
     C                   add       1             error
     C                   movel     'HS00473'     mgid(e)
     C                   endif
      *
      * All movements for a group must be complete:
      *   1) The group cannot have any 'Scheduled' movements
      *   2) The group cannot have any 'Sales' movements with a status of
      *      NC=not complete or SH=shipped
      *   3) The group cannot have any dead/adjustment/sale movements with a
      *      date prior to the group's open date.
      *
      * The following flag fields ensure that you will only print each relevant
      * message 1 time per group.
      *
     C                   move      no            hs00470fl
     C                   move      no            hs00474fl
     C                   move      no            hs00475fl
     C                   move      no            hs00693fl
     C                   move      no            hs00694fl
     C                   move      no            hs00695fl
     C                   move      no            hs00696fl
      *
      *
     C     hghgsn        setll     hsl058d
      *
     C                   dou       *in93 = *on                                  Do events
     C     hghgsn        reade     hsl058d                                93
     C                   if        *in93 = *off                                 If not EOF
      *
      *----------------------
      * TRANSFERS
      *----------------------
     C                   select
     C                   when      memtcd = 'T'
     C     memvsn        chain     hsp074                             92
     C                   if        *in92 = *off and mhmscd = 'SC'               If SC transfer
     C                             and error < maxmsg
      *
     C                   select
     C                   when      meodfl = 'O' and hs00470fl = no
     C                   add       1             error
     C                   movel     'HS00470'     mgid(e)
     C                   move      yes           hs00470fl
      *
     C                   when      meodfl = 'D' and hs00474fl = no
     C                   add       1             error
     C                   movel     'HS00474'     mgid(e)
     C                   move      yes           hs00474fl
     C                   endsl
     C                   endif                                                  If SC transfer
      *
      *----------------------
      * SALES
      *----------------------
      *
     C                   when      memtcd = 'S'
     C     memvsn        chain     hsp084                             92
     C                   if        *in92 = *off                                 If a sale
      *
      * Sales movement cannot be dated prior to the group's open date
      *
     C                   if        shshdt < hgopdt and
     C                             shmscd <> 'SC' and error < maxmsg
     C                             and hs00696fl = no
     C                   add       1             error
     C                   movel     'HS00696'     mgid(e)
     C                   move      yes           hs00696fl
     C                   endif
      *
      * Sales movement cannot have a status of scheduled
      *
     C                   select
     C                   when      shmscd = 'SC' and error < maxmsg
     C                             and hs00693fl = no
     C                   add       1             error
     C                   movel     'HS00693'     mgid(e)
     C                   move      yes           hs00693fl
      *
      * Sales movement must be fully paid
      *
     C                   when      shmscd = 'SH' or shmscd = 'NC'
     C                   if        error < maxmsg
     C                             and hs00475fl = no
     C                   add       1             error
     C                   movel     'HS00475'     mgid(e)
     C                   move      yes           hs00475fl
     C                   endif
     C                   endsl
      *
     C                   endif                                                  If a sale
      *
      *----------------------
      * ADJUSTMENTS
      *----------------------
      *
      * No adjustment can be dated prior to the group's open date
      *
     C                   when      memtcd = 'A'
     C     memvsn        chain     hsp069                             92
     C                   if        *in92 = *off and iaiadt < hgopdt
     C                             and error < maxmsg
     C                             and hs00695fl = no
     C                   add       1             error
     C                   movel     'HS00695'     mgid(e)
     C                   move      yes           hs00695fl
     C                   endif
      *
      *----------------------
      * DAILY DEAD MOVEMENTS
      *----------------------
      *
      * No daily dead movement can be dated prior to the group's open date
      *
     C                   when      memtcd = 'D'
     C     memvsn        chain     hsp068                             92
     C                   if        *in92 = *off and kdkddt < hgopdt
     C                             and error < maxmsg
     C                             and hs00694fl = no
     C                   add       1             error
     C                   movel     'HS00694'     mgid(e)
     C                   move      yes           hs00694fl
     C                   endif
     C                   endsl
     C                   endif                                                  If not EOF
     C                   enddo                                                  Do events
      *
     C                   endsr
      /EJECT
      ****************************************************************
      * Feed ticket edits
      ****************************************************************
      *
     C     $feed         begsr
      *
      * All Feed must be costed
      *
     C     hghgsn        chain     hsj038h                            92
     C                   if        *in92 = *off and error < maxmsg
     C                   add       1             error
     C                   movel     'HS00471'     mgid(e)
     C                   endif
      *
      *
      * Check for Feed Tickets that have not been allocated
      *
     C     hghgsn        setll     hsj034l
     C                   move      no            quitfl
      *
     C                   dou       *in92 = *on or quitfl = yes                  Do allocated
     C     hghgsn        reade     hsj034l                                92
     C                   if        *in92 = *off and p1fdrtdt < hgdsdt
     C                             and error < maxmsg
     C                   move      yes           quitfl
     C                   add       1             error
     C                   movel     'HS00472'     mgid(e)
     C                   endif
     C                   enddo                                                  Do allocated
      *
      *
      * Check for Feed Tickets with dates after the dispose/closed date.
      *
     C                   move      no            hs00895fl
     C                   move      no            hs00896fl
     C                   move      no            hs00897fl
     C     hghgsn        setll     hsl038e
      *
     C                   dou       *in92 = *on                                  Do feed dates
     C     hghgsn        reade     hsl038e                                92
     C                   if        *in92 = *off and error < maxmsg              If not eof
      *
      * delivery
     C                   select
     C                   when      fdtrcd = 'D' and fdtkdt > hgdsdt
     C                             and hs00895fl = no
     C                   add       1             error
     C                   movel     'HS00895'     mgid(e)
     C                   move      yes           hs00895fl
      *
      * adjustment
     C                   when      fdtrcd = 'A' and fdtkdt > ldepdt
     C                             and hs00896fl = no
     C                   add       1             error
     C                   movel     'HS00896'     mgid(e)
     C                   move      yes           hs00896fl
      *
      * pickup
     C                   when      fdtrcd = 'P' and fdtkdt > ldepdt
     C                             and hs00897fl = no
     C                   add       1             error
     C                   movel     'HS00897'     mgid(e)
     C                   move      yes           hs00897fl
     C                   endsl
      *
     C                   endif                                                  If not eof
     C                   enddo                                                  Do feed dates
      *
     C                   endsr
      /EJECT
      ****************************************************************
      * Print report headings
      ****************************************************************
      *
      * Reset the Current Line to zero.
      * Print the Heading Lines
      * Increment the Current Line.
      *
     C     $hrhdr        begsr
      *
     C                   z-add     0             wkcurl
     C                   except    hrhdr
     C                   add       8             wkcurl
      *
     C                   endsr
      /EJECT
      *------------------------------------------------------------------------------------
      * Print a blank line before messages
      *------------------------------------------------------------------------------------
      *
      * Print a blank line and increment line counter.
      *
     C     $blank        begsr
      *
     C                   except    blank
     C                   add       1             wkcurl
      *
     C                   endsr
      /EJECT
      *--------------------------------------------------------------------------------------
      * Write/print error messages
      *--------------------------------------------------------------------------------------
      *
     C     $wrmsg        begsr
      *
      * Program UT80046 does not print headings or check for page breaks. Before calling
      * UT80046, determine if all the error messages can be printed on the current page.
      *
      * Print a Blank Line before any messages.
      *
     C                   exsr      $blank
      *
      * Calculate remaining print lines on the page using the overflow line and the current
      * line.
      *
     C     wkovfl        sub       wkcurl        wkdifl
      *
      * If you have more messages than will fit on the page, then do a page break and
      * print headings.
      *
     C                   if        error > wkdifl
     C                   exsr      $hrhdr
     C                   exsr      $blank
     C                   endif
      *
      * Increment the current line by the number of error messages that you will be
      * printing in UT80046 plus one for the space after. This sets the current line
      * number properly for when you return from UT80046.
      *
     C                   add       error         wkcurl
     C                   add       1             wkcurl
      *
     C                   call      'UT80046'                            43
     C                   parm                    mgid
     C                   parm                    mgdt
     C                   parm                    error
     C                   parm      msgfil        msgfl
      *
     C                   endsr
      /EJECT
      *----------------------------------------------------------------
      * Clear messages
      *----------------------------------------------------------------
      *
     C     $clmsg        begsr
      *
     C                   call      'UT80046'                            43
     C                   parm      *blank        mgi
     C                   parm      *blank        mgd
     C                   parm      *zero         error
     C                   parm      msgfil        msgfl
      *
     C                   endsr
      /EJECT
      *----------------------------------------------------------------
      * Print 'total' blurbs at end of listing.
      *----------------------------------------------------------------
      *
     C     $t1tot        begsr
      *
     C                   if        wkcurl > wkovfl
     C                   exsr      $hrhdr
     C                   endif
      *
     C                   except    t1tot
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Initialization
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Parm lists
      *
     C     *entry        plist
     C                   parm                    xxerror
      *
      *
     C     plist01       plist
     C                   parm                    hghgsn
     C                   parm                    zzcymd
      * Head
     C                   parm                    gpihd                          GROUP INV
     C                   parm                    pinhd                          PIG IN
     C                   parm                    tinhd                          TRSF IN
     C                   parm                    tisphd                         SAME PHASE
     C                   parm                    tidphd                         DIFF PHASE
     C                   parm                    pouhd                          PIGS OUT
     C                   parm                    touhd                          TRSF OUT
     C                   parm                    tosphd                         SAME PHASE
     C                   parm                    tosphd                         DIFF PHASE
     C                   parm                    rinhd                          REJECT IN
     C                   parm                    rouhd                          REJECT OUT
     C                   parm                    qinhd                          QUALITY IN
     C                   parm                    qouhd                          QUALITY OUT
     C                   parm                    morhd                          MORTALITY
     C                   parm                    inahd                          INV ADJ
      * Pounds
     C                   parm                    pinlb                          PIG IN
     C                   parm                    tinlb                          TRSF IN
     C                   parm                    tisplb                         SAME PHASE
     C                   parm                    tidplb                         DIFF PHASE
     C                   parm                    poulb                          PIG OUT
     C                   parm                    toulb                          TRSF OUT
     C                   parm                    tosplb                         SAME PHASE
     C                   parm                    todplb                         DIFF PHASE
     C                   parm                    rinlb                          REJECT IN
     C                   parm                    roulb                          REJECT OUT
     C                   parm                    qinlb                          QUALITY IN
     C                   parm                    qoulb                          QUALITY OUT
     C                   parm                    morlb                          MORTALITY
      *
      * Get Time into the print field
      *
     C                   time                    rtime
      *
      * Set the heading print values
      *
     C                   select
     C                   when      ldeffl = 'D'
     C                   eval      href = 'ON-DEMAND'
     C                   seton                                        98
      *
     C                   when      ldeffl = 'E'
     C                   eval      href = '  EDIT   '
      *
     C                   when      ldeffl = 'F'
     C                   eval      href = '  FINAL  '
     C                   endsl
      *
     C                   endsr
      /EJECT
      *-------------------------------------------------------------
      * Report heading lines
      *-------------------------------------------------------------
      *
     Oqprint    e            hrhdr          1 01
     O                       sdpgm               10
     O                                           78 'HOG PRODUCTION SYSTEM'
     O                                          122 'DATE'
     O                       udate         y    132
      *
     O          e            hrhdr          1
     O                       sdusr               10
     O                                           67 'PRELIMINARY DISPOSED'
     O                                           87 'GROUP ERROR LISTING'
     O                                          122 'TIME'
     O                       rtime              132 '  :  :  '
      *
     O          e            hrhdr          1
     O                       href                72
     O                                          122 'PAGE'
     O                       page          z    132
      *
      *
      * For the on-demand version only:
      *
     O          e    98      hrhdr          1
     O                                           15 'Cut-off date ..'
     O                       ldemdy        y     25
      *
      *
     O          e    98      hrhdr          1
     O                                           15 'Period/year ...'
     O                       ldper         z     19
     O                       ldacyr        z     25
      *
      *
      * For the edit/final versions only:
      *
     O          e   n98      hrhdr          1
     O                                            7 'PERIOD:'
     O                       ldper         m     11
     O                       ldbmdy        y     20
     O                                           23 'TO'
     O                       ldemdy        y     32
      *
      *
      *
      *-------------------------------------------------------------
      * Column headings
      *-------------------------------------------------------------
      *
     O          e            hrhdr       1  1
     O                                            5 'FARM'
     O                                           11 'HOG'
     O                                           44 'OPENED'
     O                                           55 'DISPOSED'
     O                                           69 'PRODUCTION'
     O                                           81 'PRODUCTION'
      *
     O          e            hrhdr          1
     O                                            5 'SITE'
     O                                           13 'GROUP'
     O                                           22 'BLDG'
     O                                           30 'ROOM'
     O                                           43 'DATE'
     O                                           53 'DATE'
     O                                           66 'PHASE'
     O                                           77 'TYPE'

     O          e            hrhdr          0
     O                       dash               132
      *
      *
      *-------------------------------------------------------------
      * Detail line for hog group
      *-------------------------------------------------------------
      *
     O          e            d1dtl       2
     O                       hgfscd        z      5
     O                       hghgcd              15
     O                       hgblcd              23
     O                       hgrmcd              31
     O                       d1opdtmdy           45 '  /  /  '
     O                       d1dsdtmdy           55 '  /  /  '
     O                       hgppcd              66
     O                       hgptcd              78
      *
      *-------------------------------------------------------------
      * A Blank Line is printed before the error messages
      *-------------------------------------------------------------
      *
     O          e            blank       1
     O                                            3 '   '
      *-------------------------------------------------------------
      * The 'total' blurbs for the End of the Report.
      *-------------------------------------------------------------
      *
     O          e            t1tot       1
     O                                           25 'Total Number of Disposed '
     O                                           32 'Groups:'
     O                       t1grps        k     43
      *
     O          e            t1tot       1
     O                                           23 'Total Number of Groups '
     O                                           35 'with Errors:'
     O                       t1errgrps     j     43
      *
