     h option(*SRCSTMT:*NODEBUGIO)
      *
      * ENVIRONMENT: Pork Division
      * SYSTEM:      Resource Optimization
      * PROGRAM:     RO227
      * TITLE:       Byproduct---Meat Cost Groups Special Calcs
      * PROGRAMMER:  Alice Brownfield
      * CREATED:     10/22/07
      *
      * FUNCTION:    This program calculates and updates some of the Meat Cost
      *              Groups Prices that were calculated and updated in RO212.
      *              It is basically an OVERRIDE program to handle the Meat Cost
      *              Groups that do not follow the normal Business Rules.
      *
      *              The Meat Cost Groups that are calculated & updated are:
      *                  1.  RBBN (4411) - Rib Ends
      *                  2.  RBLT (87012 & 87013) - Riblets
      *                  3.  SKPC (2310) - Picnic Skinsˆ<-- no longer processed E15592
      *
      ***************************************************************************************
      * MODIFICATIONS:
      ***************************************************************************************
      * DATE      PROGRAMMER
      *
      * 11/09/07  LeAnne Ramsey
      *           The riblet logic (RBLT) should be excuted when Produced Pounds are zero.
      *           The code was executing it when Produced Pounds was not zero.
      *           Also, the $RBLT code was overlaying the Group Price retrieved for BNIN.
      *
      * 11/28/07  Alice Brownfield
      *           Basically re-wrote the program when we combined the Credit Item Meat
      *           Costing files with the By-Product MIX files.
      *
      * 06/06/08  LeAnne Ramsey
      *           New fields (Ingredient and Other Cost) added to multiple files.
      *
      * 07/01/08  Alice Brownfield
      *           Changed to recalculate the amount fields based on the new Produced
      *           prices.  Originally it was leaving the amounts the same.
      *
      * 11/20/08  LeAnne Ramsey
      *           As a prep for synchronizing the TFS and Meat Cost LDAs, we
      *           removed the unused LDA fields.
      *
      * 11/21/08  LeAnne Ramsey
      *           As part of synchronizing the LDAs between the TFS Margin Adjustment Close
      *           and the Meat Costing, we changed the LDA positions.
      *
      * 12/31/08  LeAnne Ramsey
      *           Added 'Meat Cost Subgroup' to the key list on KEY01 and KEY03.
      *
      * 02/26/09  LeAnne Ramsey
      *           Changed comments only.
      *
      * 04/02/09  LeAnne Ramsey
      *           Recompile only. Field 'Producing Company' added to Byproduct Detail.
      *
      * 10/08/19  Brad Baden  E15592
      *           Comment out code related to SKPC-picnic skins.  These byproducts will
      *           not get reduced anymore.
      *
      /EJECT
      ***************************************************************************************
      * FILE SPECIFICATION
      ***************************************************************************************
      *
     frop101    uf   e           k disk
      *  Byproduct values summary
      *
      *
     frol101a   if   e           k disk    rename(gmrec:gmreca) prefix(p1)
      *  Byproduct values summary
      *
      *
     frol122a   if   e           k disk    rename(ydrec:ydreca) prefix(p2)
      *  Byproduct detail
      *
      *
     frol122d   uf   e           k disk
      *  Byproduct detail
      *
      /eject
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      ****************************************************************
      * ARRAYS AND TABLES
      ****************************************************************
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Workfields
      *
     D wkcgcd          s                   like(gmcgcd)
     D wksgcd          s                   like(gmsgcd)
     D wkitcd          s                   like(yditcd)
     D wkpupr          s                   like(gmpupr)
     D wknpupr         s                   like(gmnpupr)
      *
     d totnpupr        s                   like(gmnpupr)
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *---------------------------------------------------------------
      * Local data area
      *---------------------------------------------------------------
      *
     Dlda             uds                  dtaara(*lda)
      *
     D  ldwedt                29     36  0
      *
     D  ldcono                82     84  0
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      **********************************************************************************
      * Mainline
      **********************************************************************************
      *
      * Get the Net Produced Price/LB for the BNIN Meat Cost Group. This price is
      *    used for both RBBN and RBLT.
      * If we don't have a record for BNIN, we don't do anything in this program.
      *
     C                   move      'BNIN'        wkcgcd
     C                   move      'BNIN'        wksgcd
     C     key01         chain     rol101a                            92
     C                   if        *in92 = *off                                 If hit
     C                   z-add     p1gmnpupr     wknpupr
      *
      * Perform special calcs for:
      *  RBBN-Rib Ends
     C                   exsr      $RBBN
      *  RBLT-Riblets
     C                   exsr      $RBLT
      * ‚10/08/19 JBB E15592 - Stop splitting SKPC - picnic skins
      * ‚SKPC-Picnic Skins
     C*                  exsr      $SKPC                                       ˆNo longer called
     C                   endif                                                  If hit
      *
     C                   seton                                        lr
      /eject
      *-----------------------------------------------------------------------------------
      * Calculate a new Produced Price/Lb for RBBN - Rib Ends
      *-----------------------------------------------------------------------------------
      *
     C     $RBBN         begsr
      *
      * Using the BNIN Net Produced Price/LB retrieved previously, calculate a new
      * Produced Price/LB by:
      *   adding the costs (packaging, labor, ingredient, other) to the BNIN price
      *
      * Recalculate the amount fields to be based on the new prices
      *
      * Update all RBBN records in the Byproduct Detail file with new Produced Price/Lb.
      *    Note: currently there is only 1 Item (4411) in Group RBBN.
      *
     C                   move      'RBBN'        wkcgcd
     C                   move      'RBBN'        wksgcd
     C     key03         setll     rol122d
      *
     C                   dou       *in91 = *on                                  Do RBBN
     C     key03         reade     rol122d                                91
     C                   if        *in91 = *off                                 If not EOF
     c                   z-add     wknpupr       ydnpupr
     C                   exsr      $calc
      *
     c                   update    ydrec
     C                   endif                                                  If not EOF
     C                   enddo                                                  Do RBBN
      *
      * Update the Byproduct Values Summary record with a new Produced Price/LB
      *    and a new Produced amount
      *
     C     key01         chain     rop101                             92
     C                   if        *in92 = *off                                 If hit
     c                   z-add     wknpupr       gmnpupr
     c                   eval      gmpupr = gmnpupr + gmpco + gmlco
     c                                              + gmico + gmoco
     c                   eval      gmpuam = gmpupr * gmpulb
     c                   update    gmrec
     C                   endif                                                  If hit
      *
     C                   endsr
      /eject
      *-----------------------------------------------------------------------------------
      * calculate the new Produced Price/Lb and Produced amounts
      *-----------------------------------------------------------------------------------
      *
     C     $calc         begsr
     c                   eval      ydpupr = ydnpupr + ydpco + ydlco
     c                                              + ydico + ydoco
      * prod amount
     c                   eval      ydspuam = ydpupr * ydspulb
     c                   eval      ydtpuam = ydpupr * ydtpulb
     c                   eval      ydapuam = ydpupr * ydapulb
      * net prod amount
     c                   eval      ydsnpuam = ydnpupr * ydspulb
     c                   eval      ydtnpuam = ydnpupr * ydtpulb
     c                   eval      ydanpuam = ydnpupr * ydapulb
      *
     C                   endsr
      /eject
      *-----------------------------------------------------------------------------------
      * Calculate a new Produced Price/Lb for RBLT - Riblets
      *-----------------------------------------------------------------------------------
      *
     C     $RBLT         begsr
      *
      *  We only want to do this if they are TANKING most of the Riblets.
      *    i.e. the entire Produced Pounds for RBLT < 5000 lbs
      *  So, get the ByProduct Values Summary record for RBLT and check it.
      *
     C                   move      'RBLT'        wkcgcd
     C                   move      'RBLT'        wksgcd
     C     key01         chain     rol101a                            92
     C                   if        *in92 = *off and p1gmpulb < 5000              If tanking
      *
      * Using the BNIN Net Produced Price/LB retrieved previously, calculate a new
      * Produced Price/LB by:
      *   adding the costs (packaging, labor, ingredient, other) to the BNIN price
      *
      * Update all RBLT records in Byproduct Detail file with new Produced Price/Lb.
      *    Note: Currently there are only 2 items in this group (87012 & 87013)
      *
     C     key03         setll     rol122d
      *
     C                   dou       *in91 = *on                                  Do detail
     C     key03         reade     rol122d                                91
     C                   if        *in91 = *off                                 If not EOF
     c                   z-add     wknpupr       ydnpupr
      *
     C                   exsr      $calc
      *
     c                   update    ydrec
     C                   clear                   ydrec
     C                   endif                                                  If not EOF
     C                   enddo                                                  Do detail
      *
      * Update the Byproduct Values Summary record with a new Produced Price/LB
      *
     C     key01         chain     rop101                             92
     C                   if        *in92 = *off                                 If hit
     c                   z-add     wknpupr       gmnpupr
     c                   eval      gmpupr = gmnpupr + gmpco + gmlco
     c                                              + gmico + gmoco
     c                   eval      gmpuam = gmpupr * gmpulb
     c                   update    gmrec
     C                   endif                                                  If hit
      *
     C                   endif                                                  If tanking
      *
     C                   endsr
      /eject
      *-----------------------------------------------------------------------------------
      * Calculate a new Produced Price/Lb for SKPC - Picnic Skins
      *-----------------------------------------------------------------------------------
      *
     C     $SKPC         begsr
      *
      * The SKPC Meat Cost Group uses the Produced Prices of the individual
      *     items in the BNIN Meat Cost Group.  We retrieve these prices and then
      *     multiply by a hardcoded % to get the price we will use.
      *
      * Get the Individual BNIN Meat Cost records and accumulate the prices
      *
     c                   z-add     0             wkpupr
     c                   z-add     0             totnpupr
      * 96109 - 22.50%
     C                   z-add     96109         wkitcd
     C     key02         chain     rol122a                            92
     C                   if        *in92 = *off
     c                   eval      wkpupr = p2ydpupr * .2250
     c                   add       wkpupr        totnpupr
     C                   endif
      *
      * 96309 - 10.00%
     C                   z-add     0             wkpupr
     C                   z-add     96309         wkitcd
     C     key02         chain     rol122a                            92
     C                   if        *in92 = *off
     c                   eval      wkpupr = p2ydpupr * .1000
     c                   add       wkpupr        totnpupr
     C                   endif
      *
      * We retrieve and update all of the DETAIL records from the Byproduct Detail file
      *    for SKPC --  currently only 1 item in this group (2310)
      * We update the Byproduct Values Summary record.
      *
     C                   move      'SKPC'        wkcgcd
     C                   move      'SKPC'        wksgcd
     C     key03         setll     rol122d
      *
     C                   dou       *in91 = *on                                  Do
     C     key03         reade     rol122d                                91
     C                   if        *in91 = *off                                 If not EOF
     c                   z-add     totnpupr      ydnpupr
      *
     C                   exsr      $calc
      *
     c                   update    ydrec
     C                   endif                                                  If not EOF
     C                   enddo                                                  Do
      *
      * ByProduct Values Summary
      *
     c     key01         chain     rop101                             92
     c                   if        *in92 = *off                                 If hit
     c                   z-add     totnpupr      gmnpupr
     c                   eval      gmpupr = gmnpupr + gmpco + gmlco
     c                                              + gmico + gmoco
     c                   eval      gmpuam = gmpupr * gmpulb
     c                   update    gmrec
     c                   endif                                                  If hit
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Initialization
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Key lists
      *
     C     key01         klist
     C                   kfld                    ldcono
     C                   kfld                    ldwedt
     C                   kfld                    wkcgcd
     C                   kfld                    wksgcd
      *
     C     key02         klist
     C                   kfld                    ldwedt
     C                   kfld                    wkitcd
      *
     C     key03         klist
     C                   kfld                    ldwedt
     C                   kfld                    wkcgcd
     C                   kfld                    wksgcd
      *
     C                   endsr
      /eject
