     A*%%TS  SD  20021119  094954  ISLRAMS     REL-V5R1M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD1006
     A*  TITLE:         MAINTAIN TARGETS FOR A BGF TARGET TABLE
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  DATE:          09/18/00
     A*
     A*  INDICATOR 60 IS ON WHEN IN REVISE MODE
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A N90                                  CF05(05 'REFRESH')
     A N90                                  CF09(09 'ACCEPT')
     A N90 60                               CF11(11 'DELETE')
     A*
     A*-------------------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*-------------------------------------------------------------------------
     A          R HP1006S1                  SFL
     A*%%TS  SD  20021119  094954  ISLRAMS     REL-V5R1M0  5722-WDS
     A  85                                  SFLNXTCHG
     A            HDRRN1    R        H      REFFLD(@@RRN)
     A            D1CDYR    R        B  9  2REFFLD(BYREC/BYCDYR *LIBL/HSP096)
     A                                      EDTCDE(M)
     A  31                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A N31N90                               DSPATR(HI)
     A            D1TOSV    R        B  9  9REFFLD(BYREC/BYTOSV *LIBL/HSP096)
     A                                      EDTCDE(K)
     A  32                                  DSPATR(PC)
     A  32                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A N32N90                               DSPATR(HI)
     A            D1CRPR    R        B  9 17REFFLD(BYREC/BYCRPR *LIBL/HSP096)
     A                                      EDTCDE(K)
     A  33                                  DSPATR(PC)
     A  33                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A N33N90                               DSPATR(HI)
     A            D1FRPR    R        B  9 25REFFLD(BYREC/BYFRPR *LIBL/HSP096)
     A                                      EDTCDE(K)
     A  34                                  DSPATR(PC)
     A  34                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A N34N90                               DSPATR(HI)
     A            D1LTBAHD  R        B  9 33REFFLD(BYREC/BYLTBAHD *LIBL/HSP096)
     A                                      EDTCDE(K)
     A  35                                  DSPATR(PC)
     A  35                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A N35N90                               DSPATR(HI)
     A            D1SBPR    R        B  9 41REFFLD(BYREC/BYSBPR *LIBL/HSP096)
     A                                      EDTCDE(K)
     A  36                                  DSPATR(PC)
     A  36                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A N36N90                               DSPATR(HI)
     A            D1TOLSPR  R        B  9 49REFFLD(BYREC/BYTOLSPR *LIBL/HSP096)
     A                                      EDTCDE(K)
     A  37                                  DSPATR(PC)
     A  37                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A N37N90                               DSPATR(HI)
     A            D1TOWNHD  R        B  9 57REFFLD(BYREC/BYTOWNHD *LIBL/HSP096)
     A                                      EDTCDE(K)
     A  38                                  DSPATR(PC)
     A  38                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A N38N90                               DSPATR(HI)
     A            D1FMDDPR  R        B  9 65REFFLD(BYREC/BYFMDDPR *LIBL/HSP096)
     A                                      EDTCDE(K)
     A  39                                  DSPATR(PC)
     A  39                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A N39N90                               DSPATR(HI)
     A            D1ABORPR  R        B  9 73REFFLD(HSRREF/@@ABORPR *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A  40                                  DSPATR(PC)
     A  40                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A N40N90                               DSPATR(HI)
     A                                 10  7'Fall Out Rate'
     A            D1FOPR    R        B 10 21REFFLD(@@FOPR)
     A                                      EDTCDE(K)
     A  45                                  DSPATR(PC)
     A  45                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A N45N90                               DSPATR(HI)
     A                                 10 30'Female Wk Inv Head'
     A            D1FWINHD  R        B 10 49REFFLD(@@FWINHD)
     A                                      EDTCDE(K)
     A  46                                  DSPATR(PC)
     A  46                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A N46N90                               DSPATR(HI)
     A                                 10 57'Sow Wk Inv Head'
     A            D1SWINHD  R        B 10 73REFFLD(@@SWINHD)
     A                                      EDTCDE(K)
     A  47                                  DSPATR(PC)
     A  47                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A N47N90                               DSPATR(HI)
     A*
     A*-------------------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*-------------------------------------------------------------------------
     A          R HP1006C1                  SFLCTL(HP1006S1)
     A*%%TS  SD  20021119  094954  ISLRAMS     REL-V5R1M0  5722-WDS
     A                                      SFLSIZ(0050)
     A                                      SFLPAG(0006)
     A N90                                  ROLLUP(25 'PAGE FORWARD')
     A                                      SETOF(31)
     A                                      SETOF(32)
     A                                      SETOF(33)
     A                                      SETOF(34)
     A                                      SETOF(35)
     A                                      SETOF(36)
     A                                      SETOF(37)
     A                                      SETOF(38)
     A                                      SETOF(39)
     A                                      SETOF(40)
     A                                      SETOF(45)
     A                                      SETOF(46)
     A                                      SETOF(47)
     A                                      OVERLAY
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A            HDPAG1    R        H      REFFLD(@@PAG)
     A                                      SFLRCDNBR
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 28'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USR)
     A                                      DSPATR(HI)
     A            C1MODE    R        O  2  2REFFLD(@@MODE)
     A                                      DSPATR(HI)
     A                                  2 19'MAINTAIN TARGETS FOR A BGF TARGET -
     A                                      TABLE'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@JOB)
     A                                      DSPATR(HI)
     A                                  4  2'Target table ..:'
     A            C1TTBCD   R        O  4 20REFFLD(HSRREF/@@TTBCD *LIBL/HSPREF)
     A            C1TTBDS   R        O  4 27REFFLD(HSRREF/@@TTBDS *LIBL/HSPREF)
     A                                  5 52'%'
     A                                      DSPATR(HI)
     A                                  6 34'Born'
     A                                      DSPATR(HI)
     A                                  6 44'%'
     A                                      DSPATR(HI)
     A                                  6 51'Pre-'
     A                                      DSPATR(HI)
     A                                  6 58'Total'
     A                                      DSPATR(HI)
     A                                  6 67'%'
     A                                      DSPATR(HI)
     A                                  7 10'Total'
     A                                      DSPATR(HI)
     A                                  7 17'Concpt'
     A                                      DSPATR(HI)
     A                                  7 25'Farrow'
     A                                      DSPATR(HI)
     A                                  7 33'Alive/'
     A                                      DSPATR(HI)
     A                                  7 42'Still'
     A                                      DSPATR(HI)
     A                                  7 51'Wean'
     A                                      DSPATR(HI)
     A                                  7 59'Pigs'
     A                                      DSPATR(HI)
     A                                  7 65'Female'
     A                                      DSPATR(HI)
     A                                  7 75'%'
     A                                      DSPATR(HI)
     A                                  8  2'Year'
     A                                      DSPATR(HI)
     A                                  8  8'Service'
     A                                      DSPATR(HI)
     A                                  8 18'Rate'
     A                                      DSPATR(HI)
     A                                  8 26'Rate'
     A                                      DSPATR(HI)
     A                                  8 33'Litter'
     A                                      DSPATR(HI)
     A                                  8 43'Born'
     A                                      DSPATR(HI)
     A                                  8 51'Mort'
     A                                      DSPATR(HI)
     A                                  8 57'Weaned'
     A                                      DSPATR(HI)
     A                                  8 66'Mort'
     A                                      DSPATR(HI)
     A                                  8 73'Aborts'
     A                                      DSPATR(HI)
     A*
     A*-------------------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*-------------------------------------------------------------------------
     A          R HP1006K1
     A*%%TS  SD  20000926  113208  ISLRAMS     REL-V4R4M0  5769-PW1
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A N90                             23 11'F5=Refresh'
     A N90                             23 23'F9=Accept'
     A N90 60                          23 47'F11=Delete All Targets for Table'
      *
     A*-------------------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
     A*-------------------------------------------------------------------------
     A          R HP1006ES                  SFL SFLMSGRCD(24)
      *
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
     A*-------------------------------------------------------------------------
      * ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*-------------------------------------------------------------------------
     A          R HP1006EC                  SFLCTL(HP1006ES)
      *
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
