/********************************************************************/
/*  PROGRAM  - PPW0PCLP                                             */
/*  TITLE    - AUTO SUBMIT SALES BY ITEM STRUCTURE REPORT           */
/*  SYNOPSIS - CL PROGRAM TO BE RUN AT NIGHT                        */
/*                PRINT SALES BY ITEM STRUCTURE                     */
/*                THIS WILL PRINT NIGHTLY MONDAY THRU FRIDAY AND    */
/*                  WILL PRODUCE TWO SEPARATE REPORTS, ONE FOR      */
/*                  COMPANY 360, THEN ONE FOR COMPANY 960.          */
/*                TUESDAY THRU FRIDAY IT WILL PRINT FOR THE         */
/*                  PRIOR DAYS DATE                                 */
/*                MONDAY IT WILL PRINT FOR FRIDAY, SATURDAY,        */
/*                  AND SUNDAY'S DATES                              */
/*                THIS NEEDS TO RUN BEFORE MIDNIGHT EACH NIGHT      */
/*                                                                  */
/*             EVERY COMMAND IS FOLLOWED BY TRAPS TO CAPTURE ANY    */
/*                POSSIBLE ERROR THAT WOULD HALT THE RUN PROCESS.   */
/*                THESE TRAPS WILL SEND ERROR MESSAGE PAGES TO      */
/*                THE NECESSARY PARTIES AND SHOULD PREVENT THE      */
/*                JOB FROM HANGING UP WAITING FOR SOMEONG TO        */
/*                TO ANSWER A MESSAGE.                              */
/*                                                                  */
/*  SYSTEM   - ORDER MANAGEMENT SYSTEM                              */
/*  DATE     - 02/14/2006                                           */
/*  BY       - LARA BUSER                                           */
/*------------------------------------------------------------------*/
/* MAINTENANCE LOG                                                  */
/*------------------------------------------------------------------*/
/*  MODIFIED: NAME                DATE                              */
/*            MODIFICATION DESCRIPTION                              */
/*                                                                  */
/*  06/05/07 DLY029 JRB Replaced hard coded Company Number by       */
/*  adding &COMP as input to eliminate repeating the process        */
/*  for hard coded Company Numbers. This pgm will be called for     */
/*  each plant company found in Company Defaults Internal - PDJYREP */
/*  and is called from RTV Night Sls Rpt Job  XF - PMHRXFR          */
/*                                                                  */
/*  01/02/08 RMC ADDED EMAIL1 AND EMAIL2 PARMS. THEY'LL BE '' HERE  */
/********************************************************************/
 PPW0PCLP:   PGM        PARM(&OUTQ &HOLD &DIST &COMP)

        DCL    &COMPL     *DEC                   /*  COMPANY #      */
        DCL    &RUNDATE   *DEC                   /*  RUN DATE       */
        DCL    &FDATEL    *DEC                   /*  FR DATE NUM    */
        DCL    &TDATEL    *DEC                   /*  TO DATE NUM    */
        DCL    &COPYN     *DEC                   /*  # OF COPIES    */
        DCL    &SLSTYPE   *DEC                   /*  SALES TYPE     */
        DCL    &SLSGROUP  *DEC                   /*  SALES GROUP    */
        DCL    &SLSCLASS  *DEC                   /*  SALES CLASS    */
        DCL    &ITEML     *DEC                   /*  SALES ITEM CD  */
        DCL    &COMP      *DEC    3 0            /*  COMPANY 3.0    */
        DCL    &FDATE     *DEC    7 0            /*  WORK DATE      */
        DCL    &LSTRUNDT  *DEC    7 0            /*  LAST RUN DATE  */
        DCL    &CURRDT    *DEC    7 0            /*  CURRENT DATE   */
        DCL    &COUNT     *DEC    8 0            /*  DATE INCREMENT */
        DCL    &RETURN    *CHAR   7              /*  RETURN CODE    */
        DCL    &RUNCODE   *CHAR   10             /*  SLS RUN CODE   */
        DCL    &COMPA     *CHAR   3              /*  COMPANY ALPHA  */
        DCL    &DATEA     *CHAR   7              /*  ALPHA WORK DT  */
        DCL    &PGM       *CHAR   10             /*  PROGRAM NAME   */
        DCL    &HOLD      *CHAR   4              /*  HOLD SPOOL     */
        DCL    &SAVE      *CHAR   4              /*  SAVE SPOOL     */
        DCL    &OUTQ      *CHAR   10             /*  OUTPUT QUEUE   */
        DCL    &NIGHTQ    *CHAR   10             /*  NIGHT JOB QUEUE*/
        DCL    &EXPSLS    *CHAR   1              /*  EXPORT SALES   */
        DCL    &SLSRT     *CHAR   3              /*  SALES ROUTE    */
        DCL    &DTLSUM    *CHAR   1              /*  DETAIL/SUMMARY */
        DCL    &INCLCD    *CHAR   1              /*  INCL. CM/DM    */
        DCL    &FTP       *CHAR   1
        DCL    &DOCUM     *CHAR   8
        DCL    &WHSTYP    *CHAR   2
        DCL    &WHSCOD    *CHAR   3
        DCL    &PRTCUS    *CHAR   1
        DCL    &EMAIL1    *CHAR   25
        DCL    &EMAIL2    *CHAR   25

        DCL    VAR(&JOBDATE) TYPE(*CHAR) LEN(6)
        DCL    VAR(&JOBDT) TYPE(*CHAR) LEN(7)

/* SEND PROGRAM MESSAGE PARAMETERS  */

        DCL    &DIST      *CHAR   10             /*  MESSAGE DIST   */
        DCL    &USER      *CHAR   10              /* USER ID        */
        DCL    &JOBD      *CHAR   10              /* JOB DESCRIPTION*/
        DCL    &JOBNAME   *CHAR   10              /* JOB NAME       */
        DCL    &MSG       *CHAR   (512)           /* message        */
        DCL    &MSG1      *CHAR   (132)           /* MSG FOR PAGER  */
        DCL    &MSGID     *CHAR   (7)             /* message id     */
        DCL    &MSGL      *DEC    (5 0)

/*------------------------------------------------------------------*/
/* INITIALIZE PARAMETERS WITH DEFAULT VALUES                        */
/*------------------------------------------------------------------*/
             CHGVAR     VAR(&RETURN) VALUE('       ')
             CHGVAR     VAR(&RUNCODE) VALUE(SLSRPTDTE)
             CHGVAR     VAR(&PGM) VALUE('OMNPPFR')
             CHGVAR     VAR(&SAVE) VALUE('*YES')
             CHGVAR     VAR(&COPYN) VALUE(1)
             CHGVAR     VAR(&EXPSLS) VALUE(A)
             CHGVAR     VAR(&SLSRT) VALUE('   ')
             CHGVAR     VAR(&DTLSUM) VALUE(D)
             CHGVAR     VAR(&INCLCD) VALUE(N)
             CHGVAR     VAR(&SLSTYPE) VALUE(0)
             CHGVAR     VAR(&SLSGROUP) VALUE(0)
             CHGVAR     VAR(&SLSCLASS) VALUE(0)
             CHGVAR     VAR(&ITEML) VALUE(0)
             CHGVAR     VAR(&WHSTYP) VALUE('  ')
             CHGVAR     VAR(&WHSCOD) VALUE('   ')
             CHGVAR     VAR(&FTP) VALUE(' ')
             CHGVAR     VAR(&DOCUM) VALUE(' ')
             CHGVAR     VAR(&PRTCUS) VALUE('N')
             CHGVAR     VAR(&RUNDATE) VALUE(0)
             CHGVAR     VAR(&COUNT) VALUE(1)

/*------------------------------------------------------------------*/
/* RETRIEVE INFO FOR THE SNDPGRMSG COMMAND                          */
/*------------------------------------------------------------------*/
             RTVJOBA    JOB(&JOBNAME) USER(&USER)
             RTVUSRPRF  USRPRF(&USER) JOBD(&JOBD)
             RTVJOBA    DATE(&JOBDATE)
             CVTDAT     DATE(&JOBDATE) TOVAR(&JOBDT) FROMFMT(*MDY) +
                          TOFMT(*CYMD) TOSEP(*NONE)

/*------------------------------------------------------------------*/
/* RETRIEVE THE DATE THE REPORT WAS LAST RUN & TODAY'S DATE         */
/*------------------------------------------------------------------*/
             CHGVAR     VAR(&COMPL) VALUE(&COMP)
             CHGVAR     VAR(&COMPA) VALUE(&COMP) /* Change to alpha +
                          for message data */
             CALL       PGM(PPW1XFR) PARM(&RETURN &COMP &RUNCODE +
                          &RUNDATE)

/*------------------------------------------------------------------*/
/* TRAP FOR RETURN CODE AND FOR CPF MESSAGES                        */
/*------------------------------------------------------------------*/
             MONMSG     MSGID(CPF9999 RPG9999) EXEC(DO)
             CHGVAR     VAR(&DATEA) VALUE(&JOBDT)
             CHGVAR     VAR(&MSG1) VALUE('The Retrieve Last Run Date +
                          for the Sales by Item Structure Report on')
             CHGVAR     VAR(&MSG1) VALUE(&MSG1 *BCAT &DATEA)
             CHGVAR     VAR(&MSG1) VALUE(&MSG1 *BCAT 'ended in error')
             GOTO       CMDLBL(MSGTRAP)
             ENDDO

             IF         COND(&RETURN *NE '       ') THEN(DO)
             CHGVAR     VAR(&DATEA) VALUE(&JOBDT)
             CHGVAR     VAR(&MSG1) VALUE('The Retrieve Last Run Date +
                          for the Sales by Item Structure Report on')
             CHGVAR     VAR(&MSG1) VALUE(&MSG1 *BCAT &DATEA)
             CHGVAR     VAR(&MSG1) VALUE(&MSG1 *BCAT 'ended in error')
             GOTO       CMDLBL(MSGTRAP)
             ENDDO

             CHGVAR     VAR(&LSTRUNDT) VALUE(&RUNDATE)
             CHGVAR     VAR(&CURRDT) VALUE(&JOBDT)

/********************************************************************/
/* FIRST, RUN THE REPORT(S) FOR COMPANY 360:                        */
/* ON TUES THRU FRI THIS REPORT IS TO BE RUN FOR THE PRIOR DAY'S    */
/* DATE. ON MON THIS REPORT IS TO BE RUN FOR FRI, SAT, & SUN.       */
/* LOOP = ADD 1 TO THE LAST RUN DATE. RUN THE REPORT. IF THE NEW    */
/*        LAST RUN DATE = TODAY THEN END. IF NOT, ADD 1 TO THE      */
/*        LAST RUN DATE AND RUN THE REPORT AGAIN.                   */
/*        UPDATE THE COMPANY VALUES FILE WITH NEW LAST RUN DATE.    */
/********************************************************************/
 LOOP360:    IF         COND(&LSTRUNDT *LT &CURRDT) THEN(DO)

/* PUT THE DATE RESULT INTO 15.5 FIELDS AND PASS TO THE PGM         */

             CHGVAR     VAR(&FDATEL) VALUE(&LSTRUNDT)
             CHGVAR     VAR(&TDATEL) VALUE(&LSTRUNDT)

/* RUN THE SALES BY ITEM STRUCTURE REPORT FOR COMPANY 360           */


             CALL       PGM(OMNPPCLP) PARM(&PGM &COMPL &FDATEL +
                          &TDATEL &SLSTYPE &SLSGROUP &SLSCLASS +
                          &ITEML &OUTQ &HOLD &SAVE &COPYN &NIGHTQ +
                          &DTLSUM &EXPSLS &INCLCD &INCLCD &SLSRT +
                          &FTP &DOCUM &WHSTYP &WHSCOD &PRTCUS &EMAIL1 &EMAIL2)

/*------------------------------------------------------------------*/
/* TRAP FOR CPF MESSAGES                                            */
/*------------------------------------------------------------------*/
             MONMSG     MSGID(CPF9999) EXEC(DO)
             CHGVAR     VAR(&DATEA) VALUE(&LSTRUNDT)
             CHGVAR     VAR(&MSG1) VALUE('OMNPPCLP - Sales by Item +
                          Structure Report for' *BCAT &COMPA *BCAT +
                          &DATEA *BCAT 'ended in error')
             GOTO       CMDLBL(MSGTRAP)
             ENDDO

/*------------------------------------------------------------------*/
/* SEND A REPORT COMPLETED NORMALLY MESSAGE FOR EACH REPORT DATE    */
/*------------------------------------------------------------------*/
             CHGVAR     VAR(&DATEA) VALUE(&LSTRUNDT)
             CHGVAR     VAR(&MSG1) VALUE('OMNPPCLP - Sales by Item +
                          Structure Report for' *BCAT &COMPA *BCAT +
                          &DATEA *BCAT 'completed normally')
             SNDPGRMSG  TOPGR(&DIST) MSG(&MSG1) JOB(&JOBD/&JOBNAME)

/*------------------------------------------------------------------*/
/* ADD 1 TO THE LAST RUN DATE                                       */

             CALL       PGM(PDNKXFR) PARM(&RETURN &FDATE &COMP +
                          &LSTRUNDT &COUNT)

/*------------------------------------------------------------------*/
/* TRAP FOR RETURN CODE AND FOR CPF MESSAGES                        */
/*------------------------------------------------------------------*/
             MONMSG     MSGID(CPF9999) EXEC(DO)
             CHGVAR     VAR(&DATEA) VALUE(&LSTRUNDT)
             CHGVAR     VAR(&MSG1) VALUE('The Increment Last Run +
                          Date for the Sales by Item Structure +
                          Report for' *BCAT &COMPA *BCAT &DATEA +
                          *BCAT 'ended in error')
             GOTO       CMDLBL(MSGTRAP)
             ENDDO

             IF         COND(&RETURN *NE '       ') THEN(DO)
             CHGVAR     VAR(&DATEA) VALUE(&LSTRUNDT)
             CHGVAR     VAR(&MSG1) VALUE('The Increment Last Run +
                          Date for the Sales by Item Structure +
                          Report for' *BCAT &COMPA *BCAT &DATEA +
                          *BCAT 'ended in error')
             GOTO       CMDLBL(MSGTRAP)
             ENDDO

             CHGVAR     VAR(&LSTRUNDT) VALUE(&FDATE)

             GOTO       CMDLBL(LOOP360)
             ENDDO

/*------------------------------------------------------------------*/
/* UPDATE THE COMPANY VALUES FILE WITH THE REPORT'S LAST RUN DATE   */
/*------------------------------------------------------------------*/
             CHGVAR     VAR(&RUNDATE) VALUE(&LSTRUNDT)
             CALL       PGM(PPW2XFR) PARM(&RETURN &COMP &RUNCODE +
                          &RUNDATE)

/*------------------------------------------------------------------*/
/* TRAP FOR RETURN CODE AND FOR CPF MESSAGES                        */
/*------------------------------------------------------------------*/
             MONMSG     MSGID(CPF9999) EXEC(DO)
             CHGVAR     VAR(&DATEA) VALUE(&LSTRUNDT)
             CHGVAR     VAR(&MSG1) VALUE('The Update Last Run Date +
                          for the Sales by Item Structure Report +
                          for' *BCAT &COMPA *BCAT &DATEA *BCAT 'for +
                          yesterday ended in error')
             GOTO       CMDLBL(MSGTRAP)
             ENDDO

             IF         COND(&RETURN *NE '      ') THEN(DO)
             CHGVAR     VAR(&DATEA) VALUE(&LSTRUNDT)
             CHGVAR     VAR(&MSG1) VALUE('The Update Last Run Date +
                          for the Sales by Item Structure Report +
                          for' *BCAT &COMPA *BCAT &DATEA *BCAT 'for +
                          yesterday ended in error')
             GOTO       CMDLBL(MSGTRAP)
             ENDDO

/*------------------------------------------------------------------*/
 ENDREPORT:  GOTO       CMDLBL(ENDPGM)

/*----------------------RECEIVE MESSAGES----------------------------*/

 MSGTRAP:    RCVMSG     MSGTYPE(*EXCP) MSGDTA(&MSG) MSGDTALEN(&MSGL) +
                          MSGID(&MSGID)
             MONMSG     MSGID(CPF0000)

             SNDPGMMSG  MSGID(&MSGID) MSGF(QCPFMSG) MSGDTA(%SST(&MSG +
                          1 &MSGL)) MSGTYPE(*ESCAPE)
             MONMSG     MSGID(CPF0000)

             SNDPGRMSG  TOPGR(&DIST) MSG(&MSG1) JOB(&JOBD/&JOBNAME)

 ENDPGM:     ENDPGM
