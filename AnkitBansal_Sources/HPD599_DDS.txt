     A*%%TS  SD  19950613  090001  ISLRAMS     REL-V3R0M5  5763-PW1
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION
     A*  DISPLAY FILE:  HPD599 - SELECT SALES MOVEMENT STATUS
     A*                          (OMITS POSTED)
     A*  CREATED BY:    LEANNE FEDOR
     A*  DATE:          06/13/95
     A*
     A*****************************************************************
     A* MODIFICATIONS
     A* 99/99/99  PROGRAMMER
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      PRINT
     A                                      CA12(12 'PREVIOUS')
     A N82                                  ROLLUP(25 'PAGE FORWARD')
     A*----------------------------------------------------------------
     A* REFERENCE WINDOW RECORD FORMAT 1
     A*----------------------------------------------------------------
     A          R HP599R1
     A*%%TS  SD  19950510  132849  ISLRAMS     REL-V3R0M5  5763-PW1
     A                                      TEXT('Reference window')
     A                                      WINDOW(*DFT 12 28)
     A                                      WDWBORDER((*DSPATR RI) (*CHAR '    -
     A                                          '))
     A*----------------------------------------------------------------
     A* RECORD FORMAT ONE
     A*----------------------------------------------------------------
     A          R HP599R2
     A                                      TEXT('Written in RPG pgm to prevent-
     A                                       screen from clearing')
     A                                      CLRL(*NO)
     A                                      OVERLAY
     A*----------------------------------------------------------------
     A* RECORD FORMAT TWO
     A*----------------------------------------------------------------
     A* THIS FORMAT IS NOT WRITTEN IN THE RPG PGM.  IT PREVENTS THE
     A* CALLING_PROGRAM_SCREEN FROM BEING CLEARED.
     A*----------------------------------------------------------------
     A          R HP599R3
     A                                      TEXT('Dummy format not written in R-
     A                                      PG pgm.')
     A                                      ASSUME
     A                                 11  1' '
     A*---------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP599S1                   SFL
     A*%%TS  SD  19950510  132849  ISLRAMS     REL-V3R0M5  5763-PW1
     A            HDRRN1         4S 0H
     A            DFMSCD    R        O  5  2REFFLD(MSMSCD)
     A            DFMSDS    R        O  5  7REFFLD(MSMSDS)
     A*
     A*---------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP599C1                   SFLCTL(HP599S1)
     A*%%TS  SD  19950613  090001  ISLRAMS     REL-V3R0M5  5763-PW1
     A                                      SFLSIZ(0050)
     A                                      SFLPAG(0005)
     A                                      WINDOW(HP599R1)
     A                                      TEXT('control format one')
     A                                      OVERLAY
     A                                      SFLCSRRRN(&HDRRN)
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND(*MORE)
     A                                      RMVWDW
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            HDRRN          5S 0H
     A                                  1  1'SELECT SALES MOVEMENT STATUS'
     A                                      DSPATR(HI)
     A                                  2  1'Position Cursor, Press Enter'
     A                                  4  1'Code'
     A                                      DSPATR(HI)
     A                                  4  7'Description'
     A                                      DSPATR(HI)
     A*---------------------------------------------------------------
     A* COMMAND LINE
     A*---------------------------------------------------------------
     A          R HP599K1
     A                                      OVERLAY
     A                                      WINDOW(HP599R1)
     A                                 11  1'F12=Previous'
