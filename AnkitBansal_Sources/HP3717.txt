      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Hog Production System
      * PROGRAM:     HP3717-Listing of HPS Item/Item Type Files after Upload
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     03/03/04
      *
      * Function:    This function lists the HPS Item and Item Type Master files after a
      *              successful upload from MP2.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 10/13/04  LeAnne Fedor
      *           Recompile only. Cost Per Unit Amount was changed from
      *           9,2 to 9,4.
      /eject
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fhsp180    if   e           k disk
      *    Item types
      *
      *
     Fhsp181    if   e           k disk
      *    Items
      *
      *
     Fqprint    o    f  198        printer oflind(*inof)
      *
      /eject
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      *----------------------------------------------------------------
      * CONSTANTS
      *----------------------------------------------------------------
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
     D sec1            c                   const('Section 1: Items')
     D sec2            c                   const('Section 2: Item Types')
      *
      *----------------------------------------------------------------
      * ARRAYS AND TABLES
      *----------------------------------------------------------------
      *
      *----------------------------------------------------------------
      * STANDALONE FIELDS
      *----------------------------------------------------------------
      *
      * Standard fields
      *
     D dash            s            198    inz(*all'-')
     d rtime           s              6  0
      *
      *
      * Control fields
      *
     D sec1fl          s              1    inz('N')
     D sec2fl          s              1    inz('N')
      *
      *
      * Print fields
      *
     D hrdesc          s             30
      *
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * Mainline
      ****************************************************************
      *
      * The listing prints in 2 sections:
      *     Section 1: Items
      *     Section 2: Item Types
      *
     C                   exsr      $sec1
     C                   exsr      $sec2
      *
      * EOF processing
      *
     C                   seton                                        lr
      /eject
      *-----------------------------------------------------------------------------------
      * Section 1: Print each record from the HPS Item Master file
      *-----------------------------------------------------------------------------------
      *
     C     $sec1         begsr
      *
     C                   seton                                        of
     C                   move      yes           sec1fl
     C                   eval      hrdesc = sec1
      *
      * Read/print each record in the HPS Item Master file.
      *
     C     *loval        setll     hsp181
      *
     C                   dou       *in91 = *on                                  Do items
     C                   read      hsp181                                 91
     C                   if        *in91 = *off                                 If not EOF
     C                   exsr      $prtrec
     C                   endif                                                  If not EOF
     C                   enddo                                                  Do items
      *
     C                   endsr
      /eject
      *-----------------------------------------------------------------------------------
      * Section 2: Print each record from the HPS Item Type Master file
      *-----------------------------------------------------------------------------------
      *
     C     $sec2         begsr
      *
     C                   seton                                        of
     C                   move      no            sec1fl
     C                   move      yes           sec2fl
     C                   eval      hrdesc = sec2
      *
      * Read/print each record from the HPS Item Type Master fiel.
      *
     C     *loval        setll     hsp180
      *
     C                   dou       *in91 = *on                                  Do types
     C                   read      hsp180                                 91
     C                   if        *in91 = *off                                 If not EOF
     C                   exsr      $prtrec
     C                   endif                                                  If not EOF
     C                   enddo                                                  Do types
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Print a record
      *---------------------------------------------------------------
      *
     C     $prtrec       begsr
      *
      * Print headings if necessary
      *
     C                   if        *inof = *on
     C                   exsr      $prthdr
     C                   endif
      *
      * Print detail record
      *
     C                   select
     C                   when      sec1fl = yes
     C                   except    l1dtl
      *
     C                   when      sec2fl = yes
     C                   except    l2dtl
     C                   endsl
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Print headings
      *---------------------------------------------------------------
      *
     C     $prthdr       begsr
      *
     C                   select
     C                   when      sec1fl = yes
     C                   except    hrhdr
     C                   except    h1hdr
      *
     C                   when      sec2fl = yes
     C                   except    hrhdr
     C                   except    h2hdr
     C                   endsl
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Set up heading info.
      *
     C                   time                    rtime
      *
     C                   endsr
      /eject
      *-------------------------------------------------------------
      * Report heading lines
      *-------------------------------------------------------------
      *
     Oqprint    e            hrhdr          1 01
     O                       sdpgm               10
     O                                          100 'HOG PRODUCTION SYSTEM'
     O                                          188 'DATE'
     O                       udate         y    198
      *
     O          e            hrhdr          1
     O                       sdusr               10
     O                                          101 'LISTING OF HPS ITEM/ITEM'
     O                                          188 'TIME'
     O                       rtime              198 '  :  :  '
      *
     O          e            hrhdr          1
     O                                          101 ' TYPE FILES AFTER UPLOAD'
     O                                          188 'PAGE'
     O                       page          z    198
      *
     O          e            hrhdr       0  2
     O                       hrdesc              30
      *
      *-------------------------------------------------------------
      * Section 1: Column headings
      *-------------------------------------------------------------
      *
     O          e            h1hdr       0  1
     O                                          119 'Item'
     O                                          127 'Crit'
     O                                          133 'Bdgt'
     O                                          167 'A/I'
     O                                          174 'Stock'
     O                                          198 'Cost per Unit'
      *
     O          e            h1hdr       0  1
     O                                           11 'Item Number'
     O                                           43 'Item Description'
     O                                           83 'UOM'
     O                                          103 'Volume'
     O                                          110 'UOW'
     O                                          119 'Type'
     O                                          127 'Flag'
     O                                          133 'Flag'
     O                                          148 'Budget Item'
     O                                          167 'St'
     O                                          177 'Location'
     O                                          198 'Amount'
      *
     O          e            h1hdr       0  2
     O                       dash               198
      *
      *-------------------------------------------------------------
      * Section 1: Detail line
      *-------------------------------------------------------------
      *
     O          e            l1dtl       0  1
     O                       imitcd              25
     O                       imitds              77
     O                       imuom               88
     O                       imvol         k    104
     O                       imuow              112
     O                       imittycd           121
     O                       imcritfl           125
     O                       imbgfl             132
     O                       imbgit             162
     O                       imaist             166
     O                       imstkloc           184
     O                       imcpuam       k    198
      *
      *-------------------------------------------------------------
      * Section 2: Column headings
      *-------------------------------------------------------------
      *
     O          e            h2hdr       0  1
     O                                            4 'Item'
      *
     O          e            h2hdr       0  1
     O                                            4 'Type'
      *
     O          e            h2hdr       0  1
     O                                            4 'Code'
     O                                           21 'Description'
     O                                           49 'Status'
      *
     O          e            h2hdr       0  2
     O                       dash               198
      *
      *-------------------------------------------------------------
      * Section 2: Detail line
      *-------------------------------------------------------------
      *
     O          e            l2dtl          1
     O                       tmittycd             6
     O                       tmittyds            40
     O                       tmaist              46
      /eject
