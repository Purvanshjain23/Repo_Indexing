      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Triumph Foods
      * PROGRAM:     Datamart MARGIN: Delete Records
      * PROGRAMMER:  LeAnne Ramsey
      * CREATED:     02/05/08
      *
      * Function:    Typically, the Margin Datamart function will build a week of data
      *              one time when the Margin Close is finalized for that week.
      *
      *              But, there are 2 scenarios where we would delete/rebuild:
      *              1) I.S. wants to force a rebuild of existing data and has manually
      *                 set the Datamart Last End Date to be 1 day less that the
      *                 WeekEnding Date that we want to rebuild. (Data area DATFMARG).
      *              2) I.S. has reopened closed week(s) of the TFS Margin Adjustment Close
      *                 function at the direction of the users. When this reopen happens,
      *                 the reopen logic sets the Datamart Last End Date to be 1 day less
      *                 than the WeekEnding Date of the Reopened Week. (Data area DATFMARG).
      *
      *              So, in general, we will not be deleting existing Datamart Margin
      *              records. But, just in case we are rebuilding, we run this
      *              "delete" function.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 03/19/08  LeAnne Ramsey
      *           Changed text.
      *
      * 06/02/10  LeAnne Ramsey
      *           Recompile only. Added WeekEndingDate in *USA format to TFP8014, TFP8015.
      *
      * 06/15/11  LeAnne Ramsey (E1588)
      *           Recompile only. Added 2 fields to TFP8014:
      *              ADEXLB-Exception Lbs
      *              ADLESSEXLB-Production Lbs less Exception Lbs
      *
      * 02/21/12  LeAnne Ramsey (E1950)
      *           Recompile only. Added 3 fields to TFP8014:
      *              ADPIECEFL-Piece Flag
      *              ADPIECES-Number of Pieces
      *              ADISCQTY-ISC Sales Production Quantity
      *
      *           NOTE: These fields don't really have ANYTHING to do with TFS...but Brad H.
      *           wanted them in this cube because it already had Produced Lbs.
      *
      * 05/30/13  LeAnne Ramsey (E2610)
      *           Added deletion logic for new Datamart file TFP8018-Volume Summary Weekly.
      *           We are adding it so that Duke Sand can have Converting %s on his Dashboard.
      *
      * 06/11/13  LeAnne Ramsey (E2610)
      *           Recompile only.
      *           Added a new field to new Datamart file TFP8018-Volume Summary Weekly.
      /eject
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Ftfl8014a  uf   e           k disk
      *  Datamart TFS: Weekly margins
      *
      *
     Ftfl8015a  uf   e           k disk
      *  Datamart TFS: Weekly mix adjustments
      *
      *
     Ftfl8018a  uf   e           k disk
      *  Datamart TFS: Volume summary weekly
      *
      /eject
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *---------------------------------------------------------------
      * Definition for external data area DATFMARG
      *---------------------------------------------------------------
      *
     Ddatfmarg        uds
     D  da099dt               30     37  0
     D  dadmdt                80     87  0
     D  dasbmfl              100    100
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * Mainline
      ****************************************************************
      *
      * Weekly Margins
      *
     C     dadmdt        setgt     tfl8014a
      *
     C                   dou       *in90 = *on
     C                   read      tfl8014a                               90
     C                   if        *in90 = *off
     C                   delete    adrec
     C                   endif
     C                   enddo
      *
      * Weekly Mix Adjustments
      *
     C     dadmdt        setgt     tfl8015a
      *
     C                   dou       *in90 = *on
     C                   read      tfl8015a                               90
     C                   if        *in90 = *off
     C                   delete    mwrec
     C                   endif
     C                   enddo
      *
      * Volume Summary--Weekly
      *
     C     dadmdt        setgt     tfl8018a
      *
     C                   dou       *in90 = *on
     C                   read      tfl8018a                               90
     C                   if        *in90 = *off
     C                   delete    vwrec
     C                   endif
     C                   enddo
      *
     C                   seton                                        lr
      /eject
      *---------------------------------------------------------------
      * Initialization
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      *
     C                   endsr
      /eject
