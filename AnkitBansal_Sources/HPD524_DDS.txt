     A*%%TS  SD  19990923  161126  ISLRAMS     REL-V4R2M0  5769-PW1
     A*****************************************************************
     A*
     A*  SYSTEM:        HOG PRODUCTION
     A*  DISPLAY FILE:  HPD524 - SELECT ACTIVE ROOM
     A*  RPG PROGRAM:   HP524  - SELECT ACTIVE ROOM
     A*  CREATED BY:    LEANNE FEDOR
     A*  DATE:          03/31/94
     A*
     A*****************************************************************
     A* MODIFICATIONS
     A* 12/13/95  LEANNE FEDOR
     A*           CHANGED TO DISPLAY ONLY 'ACTIVE' ROOMS.
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      PRINT
     A                                      CA12(12 'PREVIOUS')
     A N82                                  ROLLUP(25 'PAGE FORWARD')
     A*--------------------------------------------------------------*
     A* REFERENCE WINDOW RECORD FORMAT 1
     A*--------------------------------------------------------------*
     A          R HP524R1
     A*%%TS  SD  19990923  161126  ISLRAMS     REL-V4R2M0  5769-PW1
     A                                      TEXT('Reference window')
     A                                      WINDOW(*DFT 16 30)
     A                                      WDWBORDER((*DSPATR RI) (*CHAR '    -
     A                                          '))
     A*--------------------------------------------------------------*
     A* RECORD FORMAT ONE
     A*--------------------------------------------------------------*
     A          R HP524R2
     A                                      TEXT('Written in RPG pgm to prevent-
     A                                       screen from clearing')
     A                                      CLRL(*NO)
     A                                      OVERLAY
     A*--------------------------------------------------------------*
     A* RECORD FORMAT TWO
     A*--------------------------------------------------------------*
     A* THIS FORMAT IS NOT WRITTEN IN THE RPG PGM.  IT PREVENTS THE
     A* CALLING_PROGRAM_SCREEN FROM BEING CLEARED.
     A*--------------------------------------------------------------*
     A          R HP524R3
     A                                      TEXT('Dummy format not written in R-
     A                                      PG pgm.')
     A                                      ASSUME
     A                                 11  1' '
     A*--------------------------------------------------------------*
     A* SUBFILE FORMAT ONE
     A*--------------------------------------------------------------
     A          R HP524S1                   SFL
     A*%%TS  SD  19990923  161126  ISLRAMS     REL-V4R2M0  5769-PW1
     A                                      TEXT('subfile record format one')
     A            HDRRN1         4S 0H
     A            DFBLCD    R        O  7  1REFFLD(HSRREF/@@BLCD *LIBL/HSPREF)
     A            DFRMCD    R        O  7  9REFFLD(BRRMCD)
     A*--------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*--------------------------------------------------------------
     A          R HP524C1                   SFLCTL(HP524S1)
     A*%%TS  SD  19990923  161126  ISLRAMS     REL-V4R2M0  5769-PW1
     A                                      SFLSIZ(0050)
     A                                      SFLPAG(0007)
     A                                      WINDOW(HP524R1)
     A                                      TEXT('control format one')
     A                                      OVERLAY
     A                                      SFLCSRRRN(&HDRRN)
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND(*MORE)
     A                                      RMVWDW
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            HDRRN          5S 0H
     A                                  1  4'SELECT ACTIVE ROOM'
     A                                      DSPATR(HI)
     A                                  2  1'Position Cursor, Press Enter'
     A                                  4  1'Bldg'
     A                                      DSPATR(HI)
     A                                  4  9'Room'
     A                                      DSPATR(HI)
     A                                  5  1'Code'
     A                                      DSPATR(HI)
     A                                  5  9'Code'
     A                                      DSPATR(HI)
     A            C1BLCD    R        B  6  1REFFLD(HSRREF/@@BLCD *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A          R HP524K1
     A*%%TS  SD  19940316  131942  ISRCENT     REL-V2R2M0  5738-PW1
     A                                      WINDOW(HP524R1)
     A                                      OVERLAY
     A                                 15  1'F12=Previous'
