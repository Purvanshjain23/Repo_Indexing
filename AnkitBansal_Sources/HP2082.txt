      *****************  RPG PROGRAM HEADING  ************************
     h option(*SRCSTMT:*NODEBUGIO)
      ****************************************************************
      *
      * SYSTEM:      Hog Production System
      * PROGRAM:     HP2082 - Move Receiving Data to Editing
      * PROGRAMMER:  Brad Baden
      * PROJECT:     E010821
      * CREATED:     09/15/2017
      *
      * FUNCTION:    This function reads the Receiving file of uploaded
      *              Scheduled Movement data and writes the recordds to
      *              Editng file.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE        PROGRAMMER      TASK
      * 10/09/2018  Brad Baden      E13848
      *             After all of ther records are processed for the
      *             Business Office, reread all records and delete
      *             All records where all of the fields are blank
      *             or zero.
      *
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
      *
     Fhsp034    if   e           k disk
      *  Hog Group file
      *
      *
     Fhsl2081a  uf   e           k disk
      *  Receiving file for uploaded scheduled movements
      *
      *
     Fhsp2082   uf a e             disk
      *   Editing file for uploaded scheduled movements
      *
      *
     Fhsl9081a  uf a e           k disk
      *   History--Receiving file for uploaded scheduled movements
      *
      *
     Fhsl9082a  uf a e           k disk
      *   History--Editing file for uploaded scheduled movements
      *
      *
      /EJECT
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      *
      ****************************************************************
      * ARRAYS AND TABLES
      ****************************************************************
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Control and save fields
      *
     D svmdy           s              6  0
      *
      *
      * Parameters
      *
     D p1bobo          s              5
      *
      *
      * Workfields
      *
     D wkdldt          s                   like(hgopdt)
      *
      *
      * Workfields for date manipulation
      *
     D wkisodate       s               D   datfmt(*iso)
      *
     D shdtsv          s                   like(u1scshdt)
     D shtmsv          s                   like(u1scshtm)
     D shsqsv          s                   like(u1scshsq)
     D kldtsv          s                   like(u1sckldt)
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      ****************************************************************
      * Data areas
      ****************************************************************
      *
     Dnextmvsn         s              7  0 dtaara(damvsn)
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ***************************************************************************************
      * MAINLINE
      ***************************************************************************************
      *
      * Read each record in the Receiving file for the Business Office code
      *
     C     p1bobo        setll     hsl2081a
     C                   if        %equal
     C                   dou       *in90 = *on                                  Main do

      * Process file for Business Office
     C     p1bobo        reade     hsl2081a                               90

     C                   if        *in90 = *off                                 If not EOF
      *
      * Only process a record if there are values in at least 1 of the fields.
      * The XLS is tricky; the users have to do a lot of cutting/pasting into
      * it before the upload. So, sometimes, they end up with blank lines.
      * (Note: We will exclude the MMDDYY field on this check since Steve Brown
      * is doing extra cutting/pasting on MMDDYY and may accidentally leave it
      * on a line he meant to blank out.)
      *
     C                   select
     C                   when      u1stcd   <> 'MRKTS'
      *                    skip this record
      *
     C                   when      u1cusn    = 0 and
     C                             u1scshdt  = 0 and
     C                             u1scshtm  = 0 and
     C                             u1scshsq  = 0 and
     C                             u1sckldt  = 0 and
     C                             u1ldno    = 0 and
     C                             u1stcd    = *blank and
     C                             u1ltcd    = *blank and
     C                             u1ticd    = *blank and
     C                             u1hgcd    = *blank and
     C                             u1schd    = 0
      *                    skip this record
      *
     C                   other
      *
      * Sometimes the user will not repeat dates, time, or sequence number in the
      * spreadsheet rows uploaded to the AS400.  In that case, use the previous
      * value for each of the fields.
      *
      * Save Scheduled Ship Date
      *
     C                   if        u1scshdt <> 0 and u1scshdt <> shdtsv
     C                   z-add     u1scshdt      shdtsv
     C                   endif
      *
      * Save Scheduled Ship Time
      *
     C                   if        u1scshtm <> 0 and u1scshtm <> shtmsv
     C                   z-add     u1scshtm      shtmsv
     C                   endif
      *
      * Save Scheduled Ship Sequence
      *
     C                   if        u1scshsq <> 0 and u1scshsq <> shsqsv
     C                   z-add     u1scshsq      shsqsv
     C                   endif
      *
      * Save Scheduled Kill Date
      *
     C                   if        u1sckldt <> 0 and u1sckldt <> kldtsv
     C                   z-add     u1sckldt      kldtsv
     C                   endif
      *
      * Populate fields
     C                   exsr      $loadeflds
      *
      *
      * Write 1) an Editing record and 2) a "history" Editing record.
      *
     C                   write     e1rec                                        Write Editing record

     C                   write     h2rec
     C                   clear                   e1rec
      *
      * Write a "history" record for the Receiving file.
      * Then, delete the Receiving record.
      *
     C                   write     h1rec

     C                   delete    u1rec

     C                   endsl                                                  Select when
     C                   endif                                                  If *IN90
     C                   enddo                                                  Main do
     C                   endif                                                  If %equal
      *
      * EOF Processing

      * Reread all records and delete if all fields are blanks or zeros
     C     *loval        setll     hsl2081a
     C                   if        %found                                       if %found
     C                   dou       %eof                                         dou %eof
     C                   read      hsl2081a
     C                   if        not %eof                                     if not %eof

      * If all fields are blank or zero, delete the record
     C                   if        u1bocd    = *blank and                       if u1bocd = *blank
     C                             u1cusn    = 0 and
     C                             u1scshdt  = 0 and
     C                             u1scshtm  = 0 and
     C                             u1scshsq  = 0 and
     C                             u1sckldt  = 0 and
     C                             u1ldno    = 0 and
     C                             u1stcd    = *blank and
     C                             u1ltcd    = *blank and
     C                             u1ticd    = *blank and
     C                             u1hgcd    = *blank and
     C                             u1schd    = 0

     C                   delete    u1rec
     C                   endif                                                  fi u1bocd = *blank
     C                   endif                                                  fi not %eof
     C                   enddo                                                  uod %eof
     C                   endif                                                  fi %found

     C                   seton                                        lr
      /EJECT
      *----------------------------------------------------------------
      * Load receiving fields to editing, receiving history, editing history
      *----------------------------------------------------------------
      *
     C     $loadeflds    begsr
      *
      * Load like values from receiving file into editing file fields
     C                   move      *blank        e1editfl
     C                   move      u1bocd        e1bocd
     C                   z-add     u1cusn        e1cusn
      *
     C                   z-add     shdtsv        e1scshdt
     C                   z-add     shtmsv        e1scshtm
     C                   z-add     shsqsv        e1scshsq
     C                   z-add     kldtsv        e1sckldt
      *
     C                   z-add     u1ldno        e1ldno
     C                   move      u1stcd        e1stcd
     C                   move      u1ltcd        e1ltcd
     C                   move      u1ticd        e1ticd
     C                   move      u1hgcd        e1hgcd
     C                   move      u1schd        e1schd
      *
      *
      * Retrieve Farm Site, Building, and Room from Hog Group file
      * Move retrieved values to the Editing file fields
      *
     C     u1hgcd        chain     hsp034                             92
     C                   if        %found
     C                   z-add     hgfscd        e1fscd
     C                   move      hgblcd        e1blcd
     C                   move      hgrmcd        e1rmcd
     C                   else
     C                   z-add     *zeros        e1fscd
     C                   move      *blanks       e1blcd
     C                   move      *blanks       e1rmcd
     C                   move      *blanks       e1rmcd
     C                   endif                                                  %found(hsp034)
      *
      * Load receiving values into receiving history file fields
     C                   move      u1bocd        h1bocd
     C                   z-add     u1cusn        h1cusn
      *
     C                   z-add     shdtsv        h1scshdt
     C                   z-add     shtmsv        h1scshtm
     C                   z-add     shsqsv        h1scshsq
     C                   z-add     kldtsv        h1sckldt
      *
     C                   z-add     u1ldno        h1ldno
     C                   move      u1stcd        h1stcd
     C                   move      u1ltcd        h1ltcd
     C                   move      u1ticd        h1ticd
     C                   move      u1hgcd        h1hgcd
     C                   move      u1schd        h1schd
      *
      *
      * Load editing values into editing history file fields
     C                   move      e1editfl      h2editfl
     C                   move      e1bocd        h2bocd
     C                   z-add     e1cusn        h2cusn
      *
     C                   z-add     shdtsv        h2scshdt
     C                   z-add     shtmsv        h2scshtm
     C                   z-add     shsqsv        h2scshsq
     C                   z-add     kldtsv        h2sckldt
      *
     C                   z-add     e1ldno        h2ldno
     C                   move      e1stcd        h2stcd
     C                   move      e1ltcd        h2ltcd
     C                   move      e1ticd        h2ticd
     C                   z-add     e1fscd        h2fscd
     C                   move      e1blcd        h2blcd
     C                   move      e1rmcd        h2rmcd
     C                   move      e1hgcd        h2hgcd
     C                   move      e1schd        h2schd
      *
      * Use the same timestamp information as the receiving history file
     C                   z-add     h1uldt        h2uldt
     C                   z-add     h1ultm        h2ultm
     C                   move      h1crusr       h2crusr
      *
     C                   endsr
      /EJECT
      *----------------------------------------------------------------
      * Delete old History records
      *----------------------------------------------------------------
      *
      * Our 2 "history" files are only for debugging/proving Upload issues.
      * We will only keep the records for 60 days. So, we will clean up/delete
      * old records in this subroutine.
      *
      *
     C     $dlthist      begsr
      *
      * Historical Receiving File
      *
     C     p1bobo        setll     hsl9081a
     C                   if        %equal
      *
     C                   dou       *in91 = *on or h1uldt > wkdldt               Do receiving
     C                   reade     hsl9081a                               91
     C                   if        *in91 = *off and h1uldt < wkdldt
     C                   delete    h1rec
     C                   endif
     C                   enddo                                                  Do receiving
     C                   endif
      *
      *
      * Historical Editing File
      *
     C     p1bobo        setll     hsl9082a
     C                   if        %equal
      *
     C                   dou       *in91 = *on or h2uldt > wkdldt               Do editing
     C                   reade     hsl9082a                               91
     C                   if        *in91 = *off and h2uldt < wkdldt
     C                   delete    h2rec
     C                   endif
     C                   enddo                                                  Do editing
     C                   endif
      *
     C                   clear                   h1rec
     C                   clear                   h2rec
      *
     C                   endsr
      /EJECT
      *----------------------------------------------------------------
      * Initialization subroutine
      *----------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
     C     *entry        plist
     C                   parm                    p1bobo
      *
      * We will delete all "old" historical records....older than two months.
      * So, calc that "deletion" date as the System Date less 60 days and delete.
      *
     C     *mdy          move      udate         wkisodate
     C                   subdur    60:*days      wkisodate
     C                   move      wkisodate     wkdldt
     C                   exsr      $dlthist
      *
      * Set up the date/time/user for writing the Historical files.
      *
     C     *mdy          move      udate         wkisodate
     C                   move      wkisodate     h1uldt
     C                   time                    h1ultm
     C                   move      sdusr         h1crusr
      *
     C                   endsr
      /EJECT
