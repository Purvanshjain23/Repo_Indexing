     H debug(*yes)
     H option(*srcstmt:*nodebugio)
      *****************  RPG PROGRAM HEADING  ************************
      * SYSTEM:      Hog Production
      * PROGRAM:     HP4024
      * TITLE:       Specify Nur/Fin Targets to Copy
      * PROGRAMMER:  Sharon Zurn
      * CREATED:     01/08/07
      *
      * FUNCTION: Allows user to enter options for copying/duplicating data.
      *
      * NOTE: THIS IS A POP-UP WINDOW PROGRAM. PLEASE NOTE THAT IN
      *       WINDOW PROGRAMS THE RECORD FORMAT CONTAINING THE WINDOW
      *       DEFINITION MUST BE WRITTEN FIRST. SO, WE WRITE THE
      *       ERROR MESSAGE SUBFILE CONTROL RECORD FIRST FOLLOWED BY
      *       THE COMMAND LINE RECORD FORMAT.  THIS IS OPPOSITE OF
      *       THE NON-WINDOW PROGRAMS.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 09/18/07  LeAnne Ramsey
      *           Clean up code.
      *
      * 12/10/08  LeAnne Ramsey
      *           Recompile only.
      *           Added GF "GDU" targets to the Nur/Fin Yearly Targets file.
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fhpd4024   cf   e             workstn infds(iofeed)
      *
      *
     Fhsp122    if   e           k disk
      * Nur/Fin target master
      *
      *
     Fhsl122a   if   e           k disk    rename(ntrec:ntreca)
      * Nur/Fin target master
      *
      *
     Fhsp123    if   e           k disk
      * Nur/Fin yearly targets
      *
      *
     Fhsl123a   if   e           k disk    rename(nyrec:nyreca)
      * Nur/Fin yearly targets
      *
      *
     Fhsl123c   if   e           k disk    rename(nyrec:nyrecc)
      * Nur/Fin yearly targets
      *
      *
     Fhsl123d   if   e           k disk    rename(nyrec:nyrecd)
      * Nur/Fin yearly targets
      *
      /EJECT
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
     D set1            c                   'SET1  '
     D scrn1           c                   'SCRN1 '
     D exit            c                   'EXIT  '
      *
      *
      ****************************************************************
      * ARRAYS AND TABLES
      ****************************************************************
      *
      *
      *---------------------------------------------------------------
      *  Compile time array for SBMJOB used in QCMDEXC
      *---------------------------------------------------------------
     D cmd             s              1    dim(80) ctdata perrcd(80)
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Standard fields
      *
     D pgm             s             10
     D msgfl           s             10
     D routne          s              6
     D maxmsg          s              2  0  inz(20)
      *
      *
      * Control fields
      *
     D warnfl          s              1     inz('N')
     D svfcdyr         s                   like(d1fcdyr)
     D svtcdyr         s                   like(d1fcdyr)
     D svfobcell       s                   like(d1fobcell)
     D svfobpod        s                   like(d1fobpod)
     D svtobcell       s                   like(d1fobcell)
     D svtobpod        s                   like(d1fobpod)
      *
      * Parms
      *
     D xxcdyr          s                   like(d1fcdyr)
     D xxobcell        s                   like(d1fobcell)
     D xxobpod         s                   like(d1fobpod)
     D xxalphcell      s              2
     D xxalphpod       s              3
     D xxalphyear      s              4
      *
     D rtncd           s              2
     D xxjobq          s             10     inz('*JOBD     ')
      *
      *  Parms for use with QCMDEXC.  'LENGTH' is the length of the
      *  command that will be issued with QCMDEXC.  QCMDEXC requires
      *  the field containing length to be defined as 15 with 5 decimal
      *  positions.  The field 'CMDSBM' will hold the values of the
      *  submit job command when passed as a parm on the call to
      *  QCMDEXC.
      *
     D length          s             15  5 inz(80)
     D cmdsbm          s             80
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * standard error message handling data structures
      *---------------------------------------------------------------
      *
      * For error message handling, a packed index (3) is required.
      * For program readability, define a corresponding error count
      * field called 'error'
      *
     D                 ds                  inz
     D  error                         2p 0
     D   e                            2p 0 overlay(error)
      *
      * This data structure supplies the name of the message file to
      * the message handling CL program.  The field name MSGFIL must be
      * constant. The value in quotes is the name of the specific
      * message file containing the user defined messages.
      *
     D                 ds                  inz
     D  msgfil                       10    inz('HSMSGF    ')
      *
      *---------------------------------------------------------------
      *  Standard message data structures
      *---------------------------------------------------------------
      *
      * The following 3 data structures are used to speed message
      * handling since it is faster to clear data structures than
      * arrays.  Each is associated with a standard message array.
      *
     D mgi             ds                  inz
     D  mgid                          7    dim(20)
      *
     D mgd             ds                  inz
     D  mgdt                         50    dim(20)
      *
     D mgwk            ds                  inz
     D  mgw                           1    dim(50)
      *
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *---------------------------------------------------------------
      * Standard workstation information data structure
      *---------------------------------------------------------------
      *    externally defined as UTWSFR (record format: UTIDFRR)
     D iofeed        e ds                  extname(utwsfr)
      *
      *
      *---------------------------------------------------------------
      * Standard database file information data structure
      *---------------------------------------------------------------
      *    externally defined as UTDBGR (record format: FDBCKD)
     D dbfeed        e ds                  extname(utdbfr)
      *
      *
      *---------------------------------------------------------------
      * Data areas
      *---------------------------------------------------------------
      *
      * Local data area.
      *
     Dlda             uds                  dtaara(*lda)
     D  ldfcdyr                1      4  0
     D  ldfobcell              5      6  0
     D  ldfobpod               7      9  0
      *
     D  ldtcdyr               10     13  0
     D  ldtobcell             14     15  0
     D  ldtobpod              16     18  0
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /eject
      ****************************************************************
      * MAINLINE
      ****************************************************************
      *
     C                   eval      routne = set1
      *
     C     *inlr         doweq     *off
     C     routne        caseq     set1          $set1
     C     routne        caseq     scrn1         $scrn1
     C     routne        caseq     exit          $exit
     C                   endcs
     C                   enddo
      /eject
      *----------------------------------------------------------------
      * Set environment for screen 1
      *----------------------------------------------------------------
      *
     C     $set1         begsr
      *
      *  Set routine to display screen
      *
     C                   eval      routne = scrn1
      *
     C                   endsr
      /EJECT
      *----------------------------------------------------------------
      * Perform operator I/O for screen 1
      *----------------------------------------------------------------
      *
     C     $scrn1        begsr
      *
      * Write the error messages from the error arrays to the
      * error message subfile
      *
     C                   exsr      $wrmsg
      *
      * Write screen 1 to CRT.
      *
     C                   write     hp4024ec
     C                   write     hp4024k1
     C                   exfmt     hp4024r1
      *
      * Clear messages
     C                   exsr      $clmsg
      *
      *
      * Get user's input and set routine
      *
     C                   select
      *
     C                   when      *in03 = *on                                  F3-exit
     C                   eval      routne = exit
      *
     C                   when      *in08 = *on                                  F8-submit
     C                   exsr      $edit1
     C                   if        error = 0 and warnfl = no
     C                   exsr      $proc1
     C                   else
     C                   eval      routne = scrn1
     C                   endif
     C                   other
      *
     C                   exsr      $edit1
     C                   eval      routne = scrn1
     C                   endsl
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Edit screen 1
      *---------------------------------------------------------------
      *
     C     $edit1        begsr
      *
      * FROM/TO Year
     C                   exsr      $fcdyr
     C                   exsr      $tcdyr
      * FROM/TO Cell
     C                   exsr      $fcell
     C                   exsr      $tcell
      * FROM/TO Pod
     C                   exsr      $fpod
     C                   exsr      $tpod
      *
      * Validate that all froms/tos are not identical
      *
     C                   if        error = 0
     C                   exsr      $allsame
     C                   endif
      *
      * Check to see if you will be overlaying existing data.
      *
     C                   if        error = 0
     C                   exsr      $overlay
     C                   endif
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Validate From Year
      *---------------------------------------------------------------
      *
     C     $fcdyr        begsr
      *
     C                   select
     C                   when      d1fcdyr = 0
     C                   seton                                        31
     C                   if        error < maxmsg
     C                   add       1             error
     C                   eval      mgid(e) = 'HS09001'
     C                   endif
      *
     C                   when      d1fcdyr < 0
     C                   seton                                        31
     C                   if        error < maxmsg
     C                   add       1             error
     C                   eval      mgid(e) = 'HS09000'
     C                   endif
     C                   other
      *
      * Are there ANY targets for this year?
      *
     C     d1fcdyr       chain     hsl123c                            92
     C                   if        *in92 = *on                                  If no targets
     C                   seton                                        31
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS02030'     mgid(e)
     C                   clear                   mgw
     C                   move      d1fcdyr       xxalphyear
     C                   movea     xxalphyear    mgw(1)
     C                   movea     mgw           mgdt(e)
     C                   endif
     C                   endif                                                  If no targets
     C                   endsl
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Validate To Year
      *---------------------------------------------------------------
      *
     C     $tcdyr        begsr
      *
     C                   select
     C                   when      d1tcdyr = 0
     C                   seton                                        32
     C                   if        error < maxmsg
     C                   add       1             error
     C                   eval      mgid(e) = 'HS09001'
     C                   endif
      *
     C                   when      d1tcdyr < 0
     C                   seton                                        32
     C                   if        error < maxmsg
     C                   add       1             error
     C                   eval      mgid(e) = 'HS09000'
     C                   endif
     C                   endsl
      *
      * If From/To Years are both valid, verify that the TO year
      * is not prior to the FROM year.
      *
     C                   if        *in31 = *off and *in32 = *off
     C                             and d1tcdyr < d1fcdyr
     C                   seton                                        32
     C                   if        error < maxmsg
     C                   add       1             error
     C                   eval      mgid(e) = 'HS02027'
     C                   endif
     C                   endif
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Validate FROM Cell
      *---------------------------------------------------------------
      *
     C     $fcell        begsr
      *
     C                   select
     C                   when      d1fobcell = 0
     C                   if        d1tobcell <> 0
     C                   seton                                        33
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS02033'     mgid(e)
     C                   endif
     C                   endif
      *
     C                   if        d1fobpod > 0
     C                   seton                                        33
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS02036'     mgid(e)
     C                   endif
     C                   endif
      *
     C                   when      d1fobcell < 0
     C                   seton                                        33
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS09000'     mgid(e)
     C                   endif
     C                   other
      *
      * The user has entered a FROM Cell:
      *   1) Is there any record for this Cell in the Nur/Fin Target Master
      *   2) Is there any record for this Cell in the Nur/Fin Yearly Targets
      *
     C     d1fobcell     chain     hsp122                             92
     C                   if        *in92 = *on                                  If no master
     C                   seton                                        33
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS02028'     mgid(e)
     C                   clear                   mgw
     C                   move      d1fobcell     xxalphcell
     C                   movea     xxalphcell    mgw(1)
     C                   movea     mgw           mgdt(e)
     C                   endif
     C                   endif                                                  If no master
      *
     C     d1fobcell     chain     hsp123                             92
     C                   if        *in92 = *on                                  If no yearly
     C                   seton                                        33
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS02029'     mgid(e)
     C                   clear                   mgw
     C                   move      d1fobcell     xxalphcell
     C                   movea     xxalphcell    mgw(1)
     C                   movea     mgw           mgdt(e)
     C                   endif
     C                   endif                                                  If no yearly
     C                   endsl
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Validate TO Cell
      *---------------------------------------------------------------
      *
     C     $tcell        begsr
      *
     C                   select
     C                   when      d1tobcell = 0
     C                   if        d1fobcell <> 0
     C                   seton                                        34
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS02035'     mgid(e)
     C                   endif
     C                   endif
      *
     C                   when      d1tobcell < 0
     C                   seton                                        34
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS09000'     mgid(e)
     C                   endif
     C                   other
      *
      *
      * The user has entered a TO Cell:
      *   1) Is there any record for this Cell in the Nur/Fin Target Master
      *
     C     d1tobcell     chain     hsp122                             92
     C                   if        *in92 = *on                                  If no master
     C                   seton                                        34
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS02028'     mgid(e)
     C                   clear                   mgw
     C                   move      d1tobcell     xxalphcell
     C                   movea     xxalphcell    mgw(1)
     C                   movea     mgw           mgdt(e)
     C                   endif
     C                   endif                                                  If no master
     C                   endsl
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Validate FROM Pod
      *---------------------------------------------------------------
      *
     C     $fpod         begsr
      *
     C                   select
      *    When zero
     C                   when      d1fobpod = 0
      *
     C                   if        d1tobpod <> 0
     C                   seton                                        35
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS02034'     mgid(e)
     C                   endif
     C                   endif
      *
     C                   if        d1fobcell <> d1tobcell and d1fobcell > 0
     C                   seton                                        35
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS02037'     mgid(e)
     C                   endif
     C                   endif
      *   When negative
     C                   when      d1fobpod < 0
     C                   seton                                        35
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS09000'     mgid(e)
     C                   endif
     C                   other
      *
      * A FROM Pod has been entered:
      *   1) Is there any record for this Pod in the Nur/Fin Target Master
      *   2) Is there any record for this Pod in the Nur/Fin Yearly Targets
      *
     C     d1fobpod      chain     hsl122a                            92
     C                   if        *in92 = *on                                  If no master
     C                   seton                                        35
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS02031'     mgid(e)
     C                   clear                   mgw
     C                   move      d1fobpod      xxalphpod
     C                   movea     xxalphpod     mgw(1)
     C                   movea     mgw           mgdt(e)
     C                   endif
     C                   endif                                                  If no master
      *
     C     d1fobpod      chain     hsl123d                            92
     C                   if        *in92 = *on                                  If no yearly
     C                   seton                                        35
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS02032'     mgid(e)
     C                   clear                   mgw
     C                   move      d1fobpod      xxalphpod
     C                   movea     xxalphpod     mgw(1)
     C                   movea     mgw           mgdt(e)
     C                   endif
     C                   endif                                                  If no yearly
     C                   endsl
      *
      * If: FROM Pod is entered/valid
      *     FROM Cell is entered/valid
      * Validate that:
      *   1) A record for this Cell/Pod exists in the Nur/Fin Target Master
      *   2) A record for this Cell/Pod exists in the Nur/Fin Yearly Targets
      *
     C                   if        d1fobcell > 0 and d1fobpod > 0 and           If OK so far
     C                             *in33 = *off and *in35 = *off
     C     key01         chain     hsp122                             92
     C                   if        *in92 = *on                                  If no hit
     C                   seton                                        3335
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS02040'     mgid(e)
     C                   endif
     C                   else
      *
      *      Validate that the Year exists for the Table
      *
     C     key02         chain     hsp123                             92
     C                   if        *in92 = *on                                  If nothing
     C                   seton                                        31
     C                   if        error < maxmsg
     C                   add       1             error
     C                   eval      mgid(e) = 'HS02024'
     C                   endif
     C                   endif                                                  If nothing
     C                   endif                                                  If no hit
     C                   endif                                                  If OK so far
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Validate TO Pod
      *---------------------------------------------------------------
      *
     C     $tpod         begsr
      *
     C                   select
      *    When zero
     C                   when      d1tobpod = 0
      *
     C                   if        d1fobpod <> 0
     C                   seton                                        36
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS02039'     mgid(e)
     C                   endif
     C                   endif
      *
     C                   if        d1fobcell <> d1tobcell and d1tobcell > 0
     C                   seton                                        36
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS02038'     mgid(e)
     C                   endif
     C                   endif
      *    When negative
     C                   when      d1tobpod < 0
     C                   seton                                        36
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS09000'     mgid(e)
     C                   endif
     C                   other
      *
      * A TO Pod has been entered:
      *   1) Is there any record for this Pod in the Nur/Fin Target Master
      *
     C     d1tobpod      chain     hsl122a                            92
     C                   if        *in92 = *on                                  If no master
     C                   seton                                        36
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS02031'     mgid(e)
     C                   clear                   mgw
     C                   move      d1tobpod      xxalphpod
     C                   movea     xxalphpod     mgw(1)
     C                   movea     mgw           mgdt(e)
     C                   endif
     C                   endif                                                  If no master
     C                   endsl
      *
      * If: TO Pod is entered/valid
      *     TO Cell is entered/valid
      * Validate that:
      *   1) A record for this Cell/Pod exists in the Nur/Fin Target Master
      *
     C                   if        d1tobcell > 0 and d1tobpod > 0 and           If OK so far
     C                             *in34 = *off and *in36 = *off
     C     key03         chain     hsp122                             92
     C                   if        *in92 = *on                                  If no hit
     C                   seton                                        3436
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS02041'     mgid(e)
     C                   endif
     C                   endif                                                  If no hit
     C                   endif                                                  If OK so far
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Validate that the 'froms' and 'tos' are not all the same
      *---------------------------------------------------------------
      *
     C     $allsame      begsr
      *
     C                   if        d1fcdyr = d1tcdyr and                        If same
     C                             d1fobcell = d1tobcell and
     C                             d1fobpod = d1tobpod
     C                   seton                                        323436
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS09179'     mgid(e)
     C                   endif
     C                   endif                                                  If same
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------------------------
      * Check to see if you will be overlaying existing data
      *---------------------------------------------------------------------------------
      *
     C     $overlay      begsr
      *
     C                   if        svfcdyr = d1fcdyr and                        If already warned
     C                             svtcdyr = d1tcdyr and
     C                             svfobcell = d1fobcell and
     C                             svtobcell = d1tobcell and
     C                             svfobpod = d1fobpod and
     C                             svtobpod = d1tobpod
     C                   move      no            warnfl
     C                   else
      *
      * Check to see if you will be overlaying existing data with this copy.
      * If so, we will display an on-screen message 1 time.
      *
     C                   select
     C                   when      d1tobcell <> 0 and d1tobpod <> 0
     C     key04         chain     hsp123                             92
     C                   when      d1tobcell <> 0
     C     key05         chain     hsl123a                            92
     C                   other
     C     d1tcdyr       chain     hsl123a                            92
     C                   endsl
      *
     C                   if        *in92 = *off
     C                   move      yes           warnfl
     C                   seton                                        39
     C                   endif
      *
      * Save the screen fields. This allows us to make the check/warning
      * only 1 time for each 'set' of values entered by the user.
      *
     C                   z-add     d1fcdyr       svfcdyr
     C                   z-add     d1fobcell     svfobcell
     C                   z-add     d1fobpod      svfobpod
      *
     C                   z-add     d1tcdyr       svtcdyr
     C                   z-add     d1tobcell     svtobcell
     C                   z-add     d1tobpod      svtobpod
     C                   endif                                                  If already warned
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Write error messages
      *---------------------------------------------------------------
      *
     C     $wrmsg        begsr
      *
     C                   call      'UT80045J'                           43
     C                   parm                    mgid
     C                   parm                    mgdt
     C                   parm                    error
     C                   parm      sdpgm         pgm
     C                   parm      msgfil        msgfl
      *
      * If call to UT80045J failed, a message hardcoded in the DSPF
      * command key record will be set on. So, redispaly the screen to the user.
      *
     C                   if        *in43 = *on
     C                   movel     set1          routne
     C                   endif
      *
     C                   endsr
      /eject
      *----------------------------------------------------------------
      * Clear messages
      *----------------------------------------------------------------
      *
     C     $clmsg        begsr
      *
     C                   call      'UT80045J'                           43
     C                   parm      *blank        mgi
     C                   parm      *blank        mgd
     C                   parm      *zero         error
     C                   parm      sdpgm         pgm
     C                   parm      msgfil        msgfl
      *
      *
      * If call to UT80045J failed, a message hardcoded in the DSPF
      * command key record will be set on. So, redispaly the screen to the user.
      *
     C                   if        *in43 = *on
     C                   movel     set1          routne
     C                   endif
      *
     C                   endsr
      /eject
      *----------------------------------------------------------------
      * Set on last record indicator and end job
      *----------------------------------------------------------------
      *
     C     $exit         begsr
     C                   seton                                        LR
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Process user's request
      *---------------------------------------------------------------
      *
      * This subroutine is only executed if the user submits the copy
      *
     C     $proc1        begsr
      *
      * Move submission values into LDA
      *
     C                   z-add     d1fcdyr       ldfcdyr
     C                   z-add     d1tcdyr       ldtcdyr
     C                   z-add     d1fobcell     ldfobcell
     C                   z-add     d1tobcell     ldtobcell
     C                   z-add     d1fobpod      ldfobpod
     C                   z-add     d1tobpod      ldtobpod
      *
      * Move the 80-character compile time array 'CMD' to an
      * 80-character field for passing as a parm with QCMDEXC.
      *
      *
     C                   out       lda
     C                   movea     xxjobq        cmd(51)
     C                   movea     cmd           cmdsbm
     C                   call      'QCMDEXC'
     C                   parm                    cmdsbm
     C                   parm                    length
      *
      * Issue the standard request submission message
      *
     C                   if        error < maxmsg
     C                   add       1             error
     C                   eval      mgid(e) = 'HS09003'
     C                   endif
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Parm lists
      *
     C     *entry        plist
     C     d1fobcell     parm                    xxobcell
     C     d1fobpod      parm                    xxobpod
     C     d1fcdyr       parm                    xxcdyr
     C                   parm                    rtncd
      *
      * If this is called for a specific Table, just default that Table into
      * the TO Table fields.
      *
     C                   if        d1fobcell <> 0
     C                   z-add     d1fobcell     d1tobcell
     C                   z-add     d1fobpod      d1tobpod
     C                   endif
      *
     C     key01         klist
     C                   kfld                    d1fobcell
     C                   kfld                    d1fobpod
      *
     C     key02         klist
     C                   kfld                    d1fobcell
     C                   kfld                    d1fobpod
     C                   kfld                    d1fcdyr
      *
     C     key03         klist
     C                   kfld                    d1tobcell
     C                   kfld                    d1tobpod
      *
     C     key04         klist
     C                   kfld                    d1tobcell
     C                   kfld                    d1tobpod
     C                   kfld                    d1tcdyr
      *
     C     key05         klist
     C                   kfld                    d1tcdyr
     C                   kfld                    d1tobcell
      *
      * Initialize LDA fields
      *
     C                   z-add     0             ldfcdyr
     C                   z-add     0             ldfobcell
     C                   z-add     0             ldfobpod
      *
     C                   z-add     0             ldtcdyr
     C                   z-add     0             ldtobcell
     C                   z-add     0             ldtobpod
      *
      * The following standard code must be included to make the
      * standard error message handling program function properly.
      * This code sets and clears the program message queue.
      *
     C                   movel     '*'           msgpgm
     C                   exsr      $clmsg
      *
      * This window logic keeps the screen in the calling program from being
      * cleared.
     C                   write     assume
      *
     C                   endsr
      /eject
      *****************************************************************
      * COMPILE TIME ARRAY 'CMD' USED IN QCMDEXC
      *****************************************************************
** CMD table 'CMD' submits listing when F8 selected
SBMJOB CMD(CALL PGM(HP4024CL)) JOB(COPYPRED) JOBQ(__________)
