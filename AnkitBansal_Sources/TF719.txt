      *
      * SYSTEM:      Triumph Foods
      * PROGRAM:     TF719
      * TITLE:       Margin: View Product Exception
      * PROGRAMMER:  LeAnne Ramsey
      * CREATED:     09/11/06
      *
      *
      * FUNCTION: This View displays a specific record from the Product Exception file.
      *
      *           Note: This file is also used in Meat Costing where 'system-
      *                 generated' records are written automatically during the
      *                 Meat Costing run.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 04/16/07  LeAnne Ramsey
      *           We switched the following flag fields:
      *             1) Exclude from Mix Flag    is now Mix Flag
      *             2) Exclude from Volume Flag is now Volume Flag
      *           Before this change Y=Yes meant EXCLUDE the product from processing
      *           After this change  Y=Yes means INCLUDE the product in processing
      *
      * 06/12/08  LeAnne Ramsey
      *           We have added fields to TFP019; the unique key has changed.
      *           Changed this logic to retrive by the new unique key.
      *
      * 07/01/08  LeAnne Ramsey
      *           Increased decimal positions: "pounds" fields to 4 decimals
      *                                        "amount" fields to 6 decimals
      *
      * 08/08/08  LeAnne Ramsey
      *           Added all our "new" fields to the view screen.
      *
      * 12/31/08  LeAnne Ramsey
      *           Added 'Exception Number' to the screen.
      /eject
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Ftfd719    cf   e             workstn infds(iofeed)
      *
      *
     Ftfp019    if   e           k disk
      *    Product exception
      *
      *
     Fombyrel1  if   e           k disk
      *    Item Default
      *
      /eject
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
     D set1            c                   'SET1  '
     D scrn1           c                   'SCRN1 '
     D exit            c                   'EXIT  '
      *
      *
      ****************************************************************
      * ARRAYS AND TABLES
      ****************************************************************
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Standard fields
      *
     D mode            s              6
     D pgm             s             10
     D msgfl           s             10
     D routne          s              6
     D rtncd           s              2
     D maxmsg          s              2  0  inz(20)
      *
      *
      * Workfields for date manipulation
      *
     D wkisodate       s               D   datfmt(*iso)
      *
      *
      * Parm fields
      *
     D xxpesn          s                   like(pepesn)
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * standard error message handling data structures
      *---------------------------------------------------------------
      *
      * For error message handling, a packed index (3) is required.
      * For program readability, define a corresponding error count
      * field called 'error'
      *
     D                 ds                  inz
     D  error                         2p 0
     D   e                            2p 0 overlay(error)
      *
      * This data structure supplies the name of the message file to
      * the message handling CL program.  The field name MSGFIL must be
      * constant. The value in quotes is the name of the specific
      * message file containing the user defined messages.
      *
     D                 ds                  inz
     D  msgfil                       10    inz('HSMSGF    ')
      *
      *---------------------------------------------------------------
      *  Standard message data structures
      *---------------------------------------------------------------
      *
      * The following 3 data structures are used to speed message
      * handling since it is faster to clear data structures than
      * arrays.  Each is associated with a standard message array.
      *
     D mgi             ds                  inz
     D  mgid                          7    dim(20)
      *
     D mgd             ds                  inz
     D  mgdt                         50    dim(20)
      *
     D mgwk            ds                  inz
     D  mgw                           1    dim(50)
      *
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *---------------------------------------------------------------
      * Standard workstation information data structure
      *---------------------------------------------------------------
      *    externally defined as UTWSFR (record format: UTIDFRR)
     D iofeed        e ds                  extname(utwsfr)
      *
      *
      *---------------------------------------------------------------
      * Standard database file information data structure
      *---------------------------------------------------------------
      *    externally defined as UTDBGR (record format: FDBCKD)
     D dbfeed        e ds                  extname(utdbfr)
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /eject
      ****************************************************************
      * MAINLINE
      ****************************************************************
      *
     C                   eval      routne = set1
      *
     C     *inlr         doweq     *off                                         Main do loop
     C     routne        caseq     set1          $set1
     C     routne        caseq     scrn1         $scrn1
     C     routne        caseq     exit          $exit
     C                   endcs
     C                   enddo                                                  Main do loop
      /eject
      *----------------------------------------------------------------
      * Set environment for Screen 1
      *----------------------------------------------------------------
      *
     C     $set1         begsr
      *
      * Retrieve the Product Exception record.
      *
     C     key01         chain     tfp019                             92
     C                   if        *in92 = *on                                  If not found
     C                   add       1             error
     C                   movel     'HS09008'     mgid(e)
     C                   else
      *
     C                   z-add     peprcd        d1prcd
     C                   move      peitycd       d1itycd
     C                   move      pesysfl       d1sysfl
     C                   move      pepgm         d1pgm
      *
     C                   move      pecgcd        d1cgcd
     C                   move      pesgcd        d1sgcd
     C                   move      pemixgrp      d1mixgrp
      *
     C                   move      petfcgcd      d1tfcgcd
     C                   move      petfclcd      d1tfclcd
     C                   z-add     peistycd      d1istycd
     C                   z-add     peisgrcd      d1isgrcd
     C                   z-add     peisclcd      d1isclcd
     C                   move      peisclds      d1isclds
      *
     C                   move      peexcom       d1excom
     C                   z-add     pepesn        d1pesn
     C                   move      pemixfl       d1mixfl
     C                   move      pevolfl       d1volfl
      *
     C                   z-add     pesexsllb     d1sexsllb
     C                   z-add     pesexnpram    d1sexnpram
     C                   z-add     pesexpulb     d1sexpulb
     C                   z-add     pesexpuam     d1sexpuam
      *
     C                   z-add     petexsllb     d1texsllb
     C                   z-add     petexnpram    d1texnpram
     C                   z-add     petexpulb     d1texpulb
     C                   z-add     petexpuam     d1texpuam
      *
     C                   z-add     peyr          d1yr
     C                   z-add     pewk          d1wk
      *
      * Flip Week-Ending Date to MMDDYY format
      *
     C     *iso          test(d)                 pewedt                 92
     C                   if        *in92 = *off                                 If OK date
     C                   move      pewedt        wkisodate
     C     *mdy          move      wkisodate     d1wedtmdy
     C                   else
     C                   z-add     0             d1wedtmdy
     C                   endif                                                  If OK date
      *
      * Retrieve Product Description
      *
     C                   exsr      $desc
     C                   endif                                                  If not found
      *
      * Display screen 1
      *
     C                   eval      routne = scrn1
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Perform operator I/O for screen 1
      *---------------------------------------------------------------
      *
     C     $scrn1        begsr
      *
      * Write the error messages from the error arrays to the
      * error message subfile
      *
     C                   exsr      $wrmsg
      *
      * Write screen 1 to CRT
      *
     C                   write     tf719k1
     C                   write     tf719ec
     C                   exfmt     tf719r1
      *
      * Clear all messages
      *
     C                   exsr      $clmsg
      *
      *
      * Process user's entry and set routine
      *
     C                   select
     C                   when      *in03 = *on                                  F3-exit
     C                   move      '03'          rtncd
     C                   eval      routne = exit
      *
     C                   when      *in05 = *on                                  F5-refresh
     C                   eval      routne = set1
     C                   other
      *
     C                   eval      routne = exit
     C                   endsl
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Retrieve Product Description
      *---------------------------------------------------------------
      *
     C     $desc         begsr
      *
     C     d1prcd        chain     ombyrel1                           92
     C                   if        *in92 = *off
     C                   eval      d1prds  = byavna
     C                   else
     C                   eval      d1prds = 'Unknown'
     C                   endif
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Write error messages
      *---------------------------------------------------------------
      *
     C     $wrmsg        begsr
      *
     C                   call      'UT80045J'                           43
     C                   parm                    mgid
     C                   parm                    mgdt
     C                   parm                    error
     C                   parm      sdpgm         pgm
     C                   parm      msgfil        msgfl
      *
      * If call to UT80045J failed, a message hardcoded in the DSPF
      * command key record will be set on. So, redispaly the screen to the user.
      *
     C                   if        *in43 = *on
     C                   movel     set1          routne
     C                   endif
      *
     C                   endsr
      /eject
      *----------------------------------------------------------------
      * Clear messages
      *----------------------------------------------------------------
      *
     C     $clmsg        begsr
      *
     C                   call      'UT80045J'                           43
     C                   parm      *blank        mgi
     C                   parm      *blank        mgd
     C                   parm      *zero         error
     C                   parm      sdpgm         pgm
     C                   parm      msgfil        msgfl
      *
      *
      * If call to UT80045J failed, a message hardcoded in the DSPF
      * command key record will be set on. So, redispaly the screen to the user.
      *
     C                   if        *in43 = *on
     C                   movel     set1          routne
     C                   endif
      *
     C                   endsr
      /eject
      *----------------------------------------------------------------
      * End job
      *----------------------------------------------------------------
      *
     C     $exit         begsr
     C                   seton                                        lr
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Parm lists
      *
     C     *entry        plist
     C                   parm                    xxpesn
     C                   parm                    d1prcd
     C                   parm                    rtncd
      *
      *
      * Key lists
      *
     C     key01         klist
     C                   kfld                    xxpesn
     C                   kfld                    d1prcd
      *
      *
      * The following standard code must be included to make the
      * standard error message handling program function properly.
      * This code sets and clears the program message queue.
      *
     C                   movel     '*'           msgpgm
     C                   exsr      $clmsg
      *
     C                   endsr
      /eject
