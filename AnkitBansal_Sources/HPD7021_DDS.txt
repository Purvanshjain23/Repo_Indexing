     A*%%TS  SD  20040712  171846  ISLRAMS     REL-V5R2M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION
     A*  DISPLAY FILE:  HPD7021
     A*  TITLE:         VIEW BGF WEEKLY WEAN PROJECTIONS
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  DATE:          07/06/04
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CF03(03 'EXIT')
     A                                      CF05(05 'REFRESH')
     A*
     A*-----------------------------------------------------------------
     A* RECORD FORMAT ONE
     A*-----------------------------------------------------------------
     A          R HP7021R1
     A*%%TS  SD  20040712  171846  ISLRAMS     REL-V5R2M0  5722-WDS
     A                                      OVERLAY
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 29'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  2 24'VIEW BGF WEEKLY WEAN PROJECTIONS'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  4  2'Farm site:'
     A            D1FSCD    R        O  4 13REFFLD(HGFSCD)
     A                                      EDTCDE(M)
     A            D1FSNM    R        O  4 20REFFLD(FSFSNM)
     A                                  5 18'Report week/year:'
     A                                      DSPATR(HI)
     A            D1RPCDWK  R        O  5 36REFFLD(HSRREF/@@RPCDWK *LIBL/HSPREF)
     A                                      EDTCDE(M)
     A                                      COLOR(PNK)
     A            D1RPCDYR  R        O  5 40REFFLD(HSRREF/@@RPCDYR *LIBL/HSPREF)
     A                                      EDTCDE(M)
     A                                      COLOR(PNK)
     A                                  5 52'Week-ending date:'
     A            D1RPWEMDY R        O  5 71REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A                                      COLOR(PNK)
     A                                  6  2'1 Week Out Wean Projections Week:'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A            D1PWCDWK1 R        O  6 36REFFLD(HSRREF/@@PWCDWK *LIBL/HSPREF)
     A                                      EDTCDE(Z)
     A                                      COLOR(PNK)
     A            D1PWCDYR1 R        O  6 40REFFLD(HSRREF/@@PWCDYR *LIBL/HSPREF)
     A                                      EDTCDE(Z)
     A                                      COLOR(PNK)
     A                                  6 52'Week-ending date:'
     A            D1PWWEMDY1R        O  6 71REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A                                      COLOR(PNK)
     A                                  7 44'Sunday'
     A            D1SUPWHD1 R        O  7 52REFFLD(HSRREF/@@SUPWHD *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A                                  8 44'Monday'
     A            D1MOPWHD1 R        O  8 52REFFLD(HSRREF/@@MOPWHD *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A                                  9 43'Tuesday'
     A            D1TUPWHD1 R        O  9 52REFFLD(HSRREF/@@TUPWHD *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A                                 10 41'Wednesday'
     A            D1WEPWHD1 R        O 10 52REFFLD(HSRREF/@@WEPWHD *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A                                 11 42'Thursday'
     A            D1THPWHD1 R        O 11 52REFFLD(HSRREF/@@THPWHD *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A                                 12 44'Friday'
     A            D1FRPWHD1 R        O 12 52REFFLD(HSRREF/@@FRPWHD *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A                                 13 42'Saturday'
     A            D1SAPWHD1 R        O 13 52REFFLD(HSRREF/@@SAPWHD *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A                                      DSPATR(UL)
     A                                 14 18'1 week out total piglets weaned:'
     A            D1TOPWHD1 R        O 14 52REFFLD(HSRREF/@@TOPWHD *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A                                 15 27'1 week out weaned sows:'
     A            D1PWSOHD1 R        O 15 52REFFLD(HSRREF/@@PWSOHD *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A                                 16 29'1 week out sow culls:'
     A            D1SOCUHD1 R        O 16 52REFFLD(HSRREF/@@PWSOHD *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A                                 17 28'1 week out boar culls:'
     A            D1BOCUHD1 R        O 17 52REFFLD(HSRREF/@@PWSOHD *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A                                 19  2'2 Week Out Wean Projections Week:'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A            D1PWCDWK2 R        O 19 36REFFLD(HSRREF/@@PWCDWK *LIBL/HSPREF)
     A                                      EDTCDE(Z)
     A                                      COLOR(PNK)
     A            D1PWCDYR2 R        O 19 40REFFLD(HSRREF/@@PWCDYR *LIBL/HSPREF)
     A                                      EDTCDE(Z)
     A                                      COLOR(PNK)
     A                                 19 52'Week-ending date:'
     A            D1PWWEMDY2R        O 19 71REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A                                      COLOR(PNK)
     A                                 20 18'2 week out total piglets weaned:'
     A            D1TOPWHD2 R        O 20 52REFFLD(HSRREF/@@TOPWHD *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A                                 21 27'2 week out weaned sows:'
     A            D1PWSOHD2 R        O 21 52REFFLD(HSRREF/@@PWSOHD *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A*
     A*-----------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*-----------------------------------------------------------------
     A          R HP7021K1
     A*%%TS  SD  20040706  113301  ISLRAMS     REL-V5R2M0  5722-WDS
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A                                 23 11'F5=Refresh'
      *
      *------------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *-----------------------------------------------------------------
     A          R HP7021ES                  SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
      *------------------------------------------------------------------
      * ERROR MESSAGE SUBFILE CONTROL FORMAT
      *-----------------------------------------------------------------
     A          R HP7021EC                  SFLCTL(HP7021ES)
      *
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
