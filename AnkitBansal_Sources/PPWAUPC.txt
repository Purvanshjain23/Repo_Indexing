/********************************************************************/
/*                                                                  */
/*  SYNOPSIS -  PRINT PFS RESTORATION DIFFERENCES                   */
/*                                                                  */
/*  SYSTEM    - INVENTORY MANAGEMENT SYSTEM                         */
/*  PROGRAM   - PRINT REPORTS                                       */
/*  DATE      - 01/19/06                                            */
/*                                                                  */
/*      PARAMETERS:                                                 */
/*                                                                  */
/*     O    &RETURN     - RETURN CODE                               */
/*     B    &OUTQ       -  OUTPUT QUEUE                             */
/*                         IF THIS IS BLANK AT START, PROMPT        */
/*                         PGM IS CALLED; OTHERWISE, THE PRINT CL   */
/*                         IS SUBMITTED WITHOUT PROMPTING           */
/*     B    &HOLD       -  HOLD STATUS                              */
/*     B    &SAVE       -  SAVE STATUS                              */
/*     B    &COPYN      - NO. OF COPIES  (PASSED IN)                */
/*     B    &COMPN      - COMPANY                                   */
/*     B    &TRDTN      - TRANSACTION DATE                          */
/*     B    &WHSE       - WAREHOUSE                                 */
/*                                                                  */
/********************************************************************/
/*                                                                  */
/********************************************************************/
 PPWAUPC:    PGM        PARM(&RETURN &OUTQ &HOLD &SAVE &COPYN  +
                          &COMPN &TRDTN &WHSE)

/* PARMS                                                                      */
        DCL    &RETURN     *CHAR   (7)           /* Return Code               */
        DCL    &OUTQ       *CHAR   (10)          /* Output Queue              */
        DCL    &HOLD       *CHAR   (4)           /* Hold Status               */
        DCL    &SAVE       *CHAR   (4)           /* Save Status               */
        DCL    &COPYN      *DEC                  /* No. of Copies             */
        DCL    VAR(&COMPN) TYPE(*DEC)            /* Company NUMBER */
        DCL    VAR(&TRDTN) TYPE(*DEC)           /* TRANS DATE      */
        DCL    VAR(&WHSE) TYPE(*CHAR) LEN(3)   /* WAREHOUSE       */

/* INTERNAL FIELDS                                                            */
        DCL    &COPIES     *DEC    (2 0)         /* Number of copies          */
        DCL    &COMP       *DEC    (3 0)         /* COMPANY                   */
        DCL    &TRDAT      *DEC    (8 0)         /* TRANS DATE                */

        DCL    &COMMAND    *CHAR   (512)
/* ERROR MESSAGE FIELDS                                                       */
        DCL    &MSG        *CHAR   (512)         /* message                   */
        DCL    &MSGID      *CHAR   (7)           /* message id                */
        DCL    &MSGL       *DEC    (5 0)
        DCL    &RTN        *CHAR   (7)           /* message id                */

/********************************************************************/
/*   INITIALIZE VARIABLES                                           */
/********************************************************************/

             CHGVAR     VAR(&RETURN) VALUE('       ')
             CHGVAR     VAR(&COPIES) VALUE(&COPYN)
             CHGVAR     VAR(&COMP) VALUE(&COMPN)
             CHGVAR     VAR(&TRDAT) VALUE(&TRDTN)
/********************************************************************/
/*   MONITOR FOR ALL MESSAGES AT PROGRAM LEVEL                      */
/********************************************************************/

             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(MSGTRAP))

/********************************************************************/
/*   CALL PRINT PROGRAM                                        */
/********************************************************************/

             SBDPRTOVR  CMDVAR(&COMMAND) FILE(PPV8PFR$) OUTQ(&OUTQ) +
                          COPIES(&COPIES) HOLD(&HOLD) SAVE(&SAVE)

/* EXECUTE THE OVERRIDE PRINTER FILE COMMAND                        */

             CALL       PGM(QCMDEXC) PARM(&COMMAND 512)

/* CALL THE SYNON PGM                                               */
 PRINT:      CALL       PGM(PPV8PFR) PARM(&RTN &COMP &TRDAT &WHSE)
             GOTO       CMDLBL(END)

/*----------------------RECEIVE MESSAGES----------------------------*/

 MSGTRAP:    RCVMSG     MSGTYPE(*EXCP) MSGDTA(&MSG) MSGDTALEN(&MSGL) +
                          MSGID(&MSGID)
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(END))

             SNDPGMMSG  MSGID(&MSGID) MSGF(QCPFMSG) MSGDTA(%SST(&MSG +
                          1 &MSGL)) MSGTYPE(*ESCAPE)
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(END))

 END:        ENDPGM

