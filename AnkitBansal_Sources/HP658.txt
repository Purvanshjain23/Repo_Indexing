      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      HOG PRODUCTION
      * PROGRAM:     HP658 - INVENTORY ADJUSTMENT REPORT BY FARM
      * PROGRAMMER:  LEANNE FEDOR
      * CREATED:     04/07/95
      *
      * FUNCTION:    PRINTS THE INVENTORY ADJUSTMENTS FOR GROUPS
      *              SEQUENCED BY FARM.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 11/01/95  LEANNE FEDOR
      *           RECOMPILE ONLY. NEW FIELD 'GROUP TYPE' ADDED TO
      *           HOG GROUP FILE.
      *
      * 06/03/97  LEANNE FEDOR
      *           RECOMPILE ONLY. NEW FIELDS 'PURGE FLAG' AND 'DATE OF
      *           FIRST PURGE' ADDED TO HOG GROUP FILE.
      *
      * 06/28/01  LeAnne Fedor
      *           Recompile only.
      *           Manager and supervisor fields removed from Hog Group file.
      *
      * 12/04/02  LeAnne Fedor
      *           Removed Production Type and Phase from the Inventory Adjustment file.
      *           Changed program logic to use these values from the Hog Group master file.
      *
      * 07/06/09  LeAnne Ramsey
      *           Recompile only. Added new field 'Continuous Flow Flag' to Hog Group file.
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     FHSP010    IF   E           K DISK
      *    PRODUCTION TYPE
      *
      *
     FHSP011    IF   E           K DISK
      *    PRODUCTION PHASE
      *
      *
     FHSJ069B   IF   E           K DISK
      *    INVENTORY ADJUSTMENTS + HOG GROUP  (records selected/keyed in OPNQRY)
      *
      *
     FQPRINT    O    F  132        PRINTER OFLIND(*INOF)
      *
      /EJECT
      ****************************************************************
      * TABLE AND ARRAY SPECIFICATIONS
      ****************************************************************
      *
      *  PRINT ARRAYS FOR TOTALS
      *
     D HD              S             11  0 DIM(5)
      /EJECT
      ****************************************************************
      * INPUT SPECS
      ****************************************************************
      *
      *---------------------------------------------------------------
      * STANDARD PROGRAM STATUS DATA STRUCTURE
      *---------------------------------------------------------------
      *    EXTERNALLY DEFINED AS UTPGFR (RECORD FORMAT: PGMDSR)
     D PGMDS         ESDS                  EXTNAME(UTPGFR)
      *
      *---------------------------------------------------------------
      *  DATE FORMATS
      *---------------------------------------------------------------
      *
      * DATE IN MMDDYY FORMAT
      *
     D                 DS
     D  WKMDY                  1      6  0
     D  WKMM                   1      2  0
     D  WKDD                   3      4  0
     D  WKYY                   5      6  0
      *
      * DATE IN CCYYMMDD FORMAT
      *
     D                 DS
     D  WKCYMD                 1      8  0
     D  WKCEN                  1      2  0
     D  WKYER                  3      4  0
     D  WKMON                  5      6  0
     D  WKDAY                  7      8  0
      *
      *---------------------------------------------------------------
      *  NAMED CONSTANTS
      *---------------------------------------------------------------
      *
     D YES             C                   CONST('Y')
     D NO              C                   CONST('N')
     D UNKNWN          C                   CONST('UNKNOWN')
     D ALL             C                   CONST('ALL')
      *
      *----------------------------------------------------------------
      * LDA - REPORT SELECTIONS
      *----------------------------------------------------------------
      *
     D                UDS
     D  LDFSBO                 1      5
     D  LDFYMD                 6     13  0
     D  LDTYMD                14     21  0
     D  LDPPCD                22     26
     D  LDFSCD                27     31  0
     D  LDPTCD                48     52
     D  LDHGSN                53     59  0
     D  LDFMDY                60     65  0
     D  LDTMDY                66     71  0
      *
     D  LDBODS                72    101
     D  LDPTDS               102    111
     D  LDPPDS               112    141
     D  LDFSNM               142    166
     D  LDHGCD               167    173
     D  LDRPFL               172    172
      *
     D  LDOUTU               401    410
     D  LDHOLD               411    411
     D  LDCOPY               412    412
     D  LDOUTQ               413    422
      /EJECT
      ****************************************************************
      * MAIN LINE
      ****************************************************************
      *
      * READ AND PRINT ALL ADJUSTMENTS SELECTED BY OPNQRY
      *
     C     *IN90         DOUEQ     *ON                                          MAIN DO
     C                   READ      HSJ069B                                90
     C     *IN90         IFEQ      *OFF                                         IF NOT EOF
      *
     C     FIRST         IFEQ      YES                                          IF FIRST
     C                   EXSR      $PTDS
     C                   EXSR      $PPDS
     C                   EXSR      $HRHDR
     C                   MOVEL     HGPTCD        SVPTCD
     C                   MOVEL     HGPPCD        SVPPCD
     C                   MOVEL     HGHGCD        SVHGCD
     C                   Z-ADD     IAFSCD        SVFSCD
     C                   Z-ADD     IAHGSN        SVHGSN
     C                   MOVEL     NO            FIRST
     C                   ENDIF                                                  IF FIRST
      *
      * CHECK FOR CONTROL BREAKS
      *
     C                   SELECT
     C     HGPTCD        WHENNE    SVPTCD                                       PROD TYPE
     C                   EXSR      $HGTOT
     C                   EXSR      $FSTOT
     C                   EXSR      $PPTOT
     C                   EXSR      $PTTOT
     C                   EXSR      $PTDS
     C                   EXSR      $PPDS
     C                   EXSR      $HRHDR
      *
     C     HGPPCD        WHENNE    SVPPCD                                       PROD PHASE
     C                   EXSR      $HGTOT
     C                   EXSR      $FSTOT
     C                   EXSR      $PPTOT
     C                   EXSR      $PPDS
     C                   EXSR      $HRHDR
      *
     C     IAFSCD        WHENNE    SVFSCD                                       FARM
     C                   EXSR      $HGTOT
     C                   EXSR      $FSTOT
     C                   EXCEPT    BLANK
      *
     C     IAHGSN        WHENNE    SVHGSN                                       FARM
     C                   EXSR      $HGTOT
     C                   EXCEPT    BLANK
     C                   ENDSL
      *
      * FLIP ADJUSTMENT DATE TO MDY
      *
     C                   Z-ADD     IAIADT        WKCYMD
     C                   EXSR      $TOMDY
     C                   Z-ADD     WKMDY         DTIADT
      *
      *
      * ACCUMULATE TOTALS
     C                   ADD       IAIAHD        HD
      *
     C     *INOF         IFEQ      *ON
     C                   EXSR      $HRHDR
     C                   ENDIF
      *
      * PRINT DETAIL AND SETOFF INDICATOR THAT CONTROLS PRINTING OF
      * FARM SITE CODE
     C                   EXCEPT    DTLINE
     C                   SETOFF                                       8889
      *
     C                   ENDIF                                                  IF NOT EOF
     C                   ENDDO                                                  MAIN DO
      *
      *----------------------------------------------------------------
      * EOF PROCESSING
      *----------------------------------------------------------------
      *  IF NO DETAIL RECORDS MATCHED THE SELECTION CRITERIA, THEN
      *  PRINT THE STANDARD 'NO DATA...' ERROR MESSAGE
      *
     C     FIRST         IFEQ      YES                                          IF NO RECS
     C                   EXCEPT    HRHDR
     C                   EXCEPT    NODATA
     C                   ELSE
      *
     C                   EXSR      $HGTOT
     C                   EXSR      $FSTOT
     C                   EXSR      $PPTOT
     C                   EXSR      $PTTOT
     C                   EXCEPT    TRTOT
     C                   ENDIF                                                  IF NO RECS
      *
     C                   SETON                                        LR
      /EJECT
      *---------------------------------------------------------------
      * $TOMDY   FLIP DATE FROM YYMMDD TO MMDDYY
      *---------------------------------------------------------------
      *
     C     $TOMDY        BEGSR
      *
     C                   Z-ADD     WKMON         WKMM
     C                   Z-ADD     WKDAY         WKDD
     C                   Z-ADD     WKYER         WKYY
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * $HRHDR - PRINT REPORT HEADINGS
      *---------------------------------------------------------------
      *
     C     $HRHDR        BEGSR
      *
     C                   EXCEPT    HRHDR
     C                   EXCEPT    HTHDR
     C                   SETON                                        8889
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      *  $PTDS - RETRIEVE THE PRODUCTION TYPE DESCRIPTION
      *----------------------------------------------------------------
      *
     C     $PTDS         BEGSR
      *
     C     HGPTCD        CHAIN     HSP010                             91
     C     *IN91         IFEQ      *OFF                                         IF FOUND
     C                   MOVEL     PTPTDS        HTPTDS
     C                   ELSE
     C                   MOVEL(P)  UNKNWN        HTPTDS
     C                   ENDIF                                                  IF FOUND
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $PPDS - RETRIEVE THE PRODUCTION PHASE DESCRIPTION
      *----------------------------------------------------------------
      *
     C     $PPDS         BEGSR
      *
     C     HGPPCD        CHAIN     HSP011                             91
     C     *IN91         IFEQ      *OFF                                         IF FOUND
     C                   MOVEL     PPPPDS        HTPPDS
     C                   ELSE
     C                   MOVEL(P)  UNKNWN        HTPPDS
     C                   ENDIF                                                  IF FOUND
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      *  $PTTOT - PRODUCTION TYPE TOTALS
      *----------------------------------------------------------------
      *
     C     $PTTOT        BEGSR
      *
     C     *INOF         IFEQ      *ON
     C                   EXSR      $HRHDR
     C                   ENDIF
      *
     C                   EXCEPT    TTTOT
     C                   MOVEL     HGPTCD        SVPTCD
      *
     C                   ENDSR
      *
      *----------------------------------------------------------------
      * $PPTOT - PRODUCTION PHASE TOTALS
      *----------------------------------------------------------------
      *
     C     $PPTOT        BEGSR
      *
     C     *INOF         IFEQ      *ON
     C                   EXSR      $HRHDR
     C                   ENDIF
      *
     C                   EXCEPT    TPTOT
     C                   MOVEL     HGPPCD        SVPPCD
      *
     C                   ENDSR
      *
      *----------------------------------------------------------------
      * $FSTOT - FARM SITE TOTALS
      *----------------------------------------------------------------
      *
     C     $FSTOT        BEGSR
      *
     C     *INOF         IFEQ      *ON
     C                   EXSR      $HRHDR
     C                   ENDIF
      *
     C                   EXCEPT    TFTOT
      *
     C                   Z-ADD     IAFSCD        SVFSCD
     C                   SETON                                        89
      *
     C                   ENDSR
      *
      *----------------------------------------------------------------
      * $HGTOT - HOG GROUP TOTALS
      *----------------------------------------------------------------
      *
     C     $HGTOT        BEGSR
      *
     C                   EXCEPT    UNDER
      *
     C     *INOF         IFEQ      *ON
     C                   EXSR      $HRHDR
     C                   ENDIF
      *
     C                   EXCEPT    TGTOT
      *
     C                   MOVEL     IAHGSN        SVHGSN
     C                   MOVEL     HGHGCD        SVHGCD
     C                   SETON                                        88
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * *INZSR - INITIALIZATION SUBROUTINE
      *---------------------------------------------------------------
      *
     C     *INZSR        BEGSR
      *
      * DEFINE PRINT FIELDS FOR PRODUCTION TYPE HEADING
      *
     C     *LIKE         DEFINE    PTPTDS        HTPTDS
     C     *LIKE         DEFINE    PPPPDS        HTPPDS
      *
      * DEFINE PRINT FIELDS FOR DETAIL LINE
      *
     C                   Z-ADD     0             DTIADT            6 0
      *
      * DEFINE SAVE FIELDS FOR CONTROL BREAKS
      *
     C     *LIKE         DEFINE    HGPTCD        SVPTCD
     C     *LIKE         DEFINE    HGPPCD        SVPPCD
     C     *LIKE         DEFINE    HGHGCD        SVHGCD
     C     *LIKE         DEFINE    IAFSCD        SVFSCD
     C     *LIKE         DEFINE    IAHGSN        SVHGSN
      *
      *--------------------------------------------------------------
      * SET UP REPORT HEADINGS
      *--------------------------------------------------------------
      *
      * ALL BUSINESS OFFICES
      *
     C     LDFSBO        IFEQ      *BLANK
     C                   MOVEL     ALL           LDFSBO
     C                   ENDIF
      *
      * ALL PRODUCTION TYPES
      *
     C     LDPTCD        IFEQ      *BLANK
     C                   MOVEL     ALL           LDPTCD
     C                   ENDIF
      *
      * ALL PRODUCTION PHASES
      *
     C     LDPPCD        IFEQ      *BLANK
     C                   MOVEL     ALL           LDPPCD
     C                   ENDIF
      *
      *  ALL FARM SITES
      *
     C     LDFSCD        IFEQ      0
     C                   SETON                                        81
     C                   ENDIF
      *
      * DETERMINE IF USER SELECTED SPECIFIC HOG GROUP OR "ALL"
      *
     C     LDHGCD        IFEQ      *BLANK
     C                   MOVEL     ALL           LDHGCD
     C                   ENDIF
      *
      * SET UP HEADING UNDERLINE FIELD FOR PRINT
      * RETRIEVE TIME FOR REPORT HEADING
      * SET UP A FIRST TIME FLAG
      *
     C                   MOVEL     *ALL'-'       DASH             80
     C                   TIME                    RTIME             6 0
     C                   MOVE      YES           FIRST             1
      *
     C                   ENDSR
      /EJECT
      *-------------------------------------------------------------
      * REPORT HEADING LINES
      *-------------------------------------------------------------
      *
     OQPRINT    E            HRHDR          1 01
     O                       SDPGM               10
     O                                           50 'HOG PRODUCTION SYSTEM'
     O                                           70 'DATE'
     O                       UDATE         Y     80
      *
     O          E            HRHDR          1
     O                       SDUSR               10
     O                                           47 'INVENTORY ADJUSTMENTS'
     O                                           55 'BY FARM'
     O                                           70 'TIME'
     O                       RTIME               80 '  :  :  '
      *
     O          E            HRHDR          1
     O                                           70 'PAGE'
     O                       PAGE          Z     80
      *
     O          E            HRHDR          1
     O                                           23 'BUSINESS OFFICE .......'
     O                       LDFSBO              30
     O                       LDBODS              63
      *
     O          E            HRHDR          1
     O                                           23 'FROM ADJUSTMENT DATE ..'
     O                       LDFMDY        Y     33
      *
     O          E            HRHDR          1
     O                                           23 'TO ADJUSTMENT DATE ....'
     O                       LDTMDY        Y     33
      *
     O          E            HRHDR          1
     O                                           23 'PRODUCTION TYPE .......'
     O                       LDPTCD              30
     O                       LDPTDS              43
      *
     O          E            HRHDR          1
     O                                           23 'PRODUCTION PHASE ......'
     O                       LDPPCD              30
     O                       LDPPDS              73
      *
     O          E            HRHDR          1
     O                                           23 'FARM SITE .............'
     O              N81      LDFSCD        M     29
     O               81                          28 'ALL'
     O                       LDFSNM              57
      *
     O          E            HRHDR          2
     O                                           23 'HOG GROUP .............'
     O                       LDHGCD              32
      *
      *-------------------------------------------------------------
      * COLUMN HEADING LINES
      *-------------------------------------------------------------
      *
     O          E            HRHDR          1
     O                                            5 'FARM'
     O                                           28 'ADJUSTMENT'
     O                                           44 'ADJUSTMENT'
      *
     O          E            HRHDR          1
     O                                            5 'SITE'
     O                                           13 'GROUP'
     O                                           25 'DATE'
     O                                           44 'HEAD'
      *
     O          E            HRHDR          1
     O                       DASH                80
      *
      *-------------------------------------------------------------
      * PRODUCTION TYPE / PRODUCTION PHASE HEADING
      *-------------------------------------------------------------
      *
     O          E            HTHDR       1  2
     O                       HTPTDS              15
     O                                           17 '-'
     O                       HTPPDS              48
      *
      *-------------------------------------------------------------
      *  DETAIL LINE
      *-------------------------------------------------------------
      *
     O          E            DTLINE         1
     O               89      IAFSCD        MB     6
     O               88      HGHGCD         B    15
     O                       DTIADT        YB    27
     O                       IAIAHD        KB    45
      *
      *-------------------------------------------------------------
      * HOG GROUP TOTAL LINE
      *-------------------------------------------------------------
      *
     O          E            TGTOT          1
     O                                           21 '*'
     O                       SVHGCD              29
     O                       HD(1)         KB    45
     O                                           47 '*'
      *
      *-------------------------------------------------------------
      * FARM SITE TOTAL LINE
      *-------------------------------------------------------------
      *
     O          E            TFTOT          1
     O                                           21 '**'
     O                       SVFSCD        M     28
     O                       HD(2)         KB    45
     O                                           48 '**'
      *
      *-------------------------------------------------------------
      * PRODUCTION PHASE TOTAL LINE
      *-------------------------------------------------------------
      *
     O          E            TPTOT          1
     O                                           21 '***'
     O                       SVPPCD              27
     O                       HD(3)         KB    45
     O                                           49 '***'
      *
      *-------------------------------------------------------------
      * PRODUCTION TYPE TOTAL LINE
      *-------------------------------------------------------------
      *
     O          E            TTTOT          1
     O                                           21 '****'
     O                       SVPTCD              27
     O                       HD(4)         KB    45
     O                                           50 '****'
      *
      *-------------------------------------------------------------
      * REPORT TOTALS
      *-------------------------------------------------------------
      *
     O          E            TRTOT          1
     O                                           29 'REPORT TOTAL'
     O                       HD(5)         KB    45
      *
      *-------------------------------------------------------------
      * PRINT A BLANK LINE
      *-------------------------------------------------------------
      *
     O          E            BLANK          1
      *
      *-------------------------------------------------------------
      * PRINT UNDERSCORE
      *-------------------------------------------------------------
      *
     O          E            UNDER       0  1
     O                                           44 '--------------'
      *
      *-------------------------------------------------------------
      * NO DATA MESSAGE LINE
      *-------------------------------------------------------------
      *
     O          E            NODATA         1
     O                                           19 'NO DATA MEETS YOUR'
     O                                           38 'SELECTION CRITERIA'
