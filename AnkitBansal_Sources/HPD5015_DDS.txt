     A*%%TS  SD  20011109  094222  ISLRAMS     REL-V4R4M0  5769-PW1
     A*****************************************************************
     A*
     A*  SYSTEM:        HOG PRODUCTION
     A*  DISPLAY FILE:  HPD5015 - SELECT SIRE LINE SOURCE
     A*  CREATED BY:    LEANNE FEDOR
     A*  DATE:          10/25/01
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      PRINT
     A                                      CA12(12 'PREVIOUS')
     A N82                                  ROLLUP(25 'PAGE FORWARD')
     A*---------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP5015S1                  SFL
     A*%%TS  SD  20011109  094055  ISLRAMS     REL-V4R4M0  5769-PW1
     A            HDRRN1    R        H      REFFLD(@@RRN)
     A            D1SSCD    R        O  6  1REFFLD(@@SSCD)
     A            D1SSDS    R        O  6  8REFFLD(@@SSDS)
     A            D1AIST    R        O  6 46REFFLD(HSRREF/@@AIST *LIBL/HSPREF)
     A*
     A*---------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP5015C1                  SFLCTL(HP5015S1)
     A*%%TS  SD  20011109  094222  ISLRAMS     REL-V4R4M0  5769-PW1
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0008)
     A                                      WINDOW(HP5015EC)
     A                                      OVERLAY
     A                                      SFLCSRRRN(&HDRRN)
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A            HDPAG1    R        H      REFFLD(@@PAG)
     A                                      SFLRCDNBR
     A            HDRRN     R        H      REFFLD(@@RRN)
     A                                  1 14'SELECT SIRE LINE SOURCE'
     A                                      DSPATR(HI)
     A            C1DESC    R        O  2 11REFFLD(@@DS30)
     A                                      DSPATR(HI)
     A                                  3  1'Position Cursor, Press Enter'
     A                                  4 39'Active/Inactive'
     A                                      DSPATR(HI)
     A                                  5  1'Code'
     A                                      DSPATR(HI)
     A                                  5  8'Description'
     A                                      DSPATR(HI)
     A                                  5 44'Status'
     A                                      DSPATR(HI)
     A*---------------------------------------------------------------
     A* COMMAND LINE LINE
     A*---------------------------------------------------------------
     A          R HP5015K1
     A                                      WINDOW(HP5015EC)
     A                                      OVERLAY
     A  43                             15  1'Message program failed. Call IS.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 16  1'F12=Previous'
      *
     A*-------------------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
     A*-------------------------------------------------------------------------
     A          R HP5015ES                  SFL SFLMSGRCD(17)
      *
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
     A*
     A*-------------------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*-------------------------------------------------------------------------
     A*
     A          R HP5015EC                  SFLCTL(HP5015ES)
     A*%%TS  SD  19980327  105628  ISLRAMS     REL-V3R7M0  5716-PW1
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A                                      WINDOW(*DFT 18 55)
     A                                      WDWBORDER((*DSPATR RI) (*CHAR '    -
     A                                          '))
     A            MSGPGM                    SFLPGMQ(10)
     A*
     A*---------------------------------------------------------------
     A* ASSUME FORMAT -- REQUIRED FOR WINDOWS
     A*---------------------------------------------------------------
     A          R ASSUME
     A*%%TS  SD  19980327  102611  ISLRAMS     REL-V3R7M0  5716-PW1
     A                                      ASSUME
     A                                      OVERLAY
     A                                 24 79' '
