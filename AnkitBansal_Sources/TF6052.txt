      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Triumph Foods
      * PROGRAM:     TF6052
      * TITLE:       Cash Distribution Report
      * PROGRAMMER:  LeAnne Ramsey
      * CREATED:     06/06/08
      *
      * FUNCTION:  Batch program to print the Cash Distribution Header and, optionally,
      *            the associated Cash Distribution Detail records.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Ftfp3052   if   e           k disk
      *  Workfile: Cash distribution report--header
      *
      *
     Ftfp013    if   e           k disk
      *  Cash distribution detail
      *
      *
     Fprint198  o    f  198        printer oflind(*inof)
      *
      /EJECT
      ************************************************************************************
      * DEFINITION SPECIFICATIONS
      ************************************************************************************
      *
      ************************************************************************************
      * CONSTANTS
      ************************************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Standard fields
      *
     D dash            s            198    inz(*all'-')
     d rtime           s              6  0
      *
      *
      * Control fields
      *
     D first           s              1    inz('Y')
      *
      *
      * Workfields for date manipulation
      *
     D wkisodate       s               D   datfmt(*iso)
      *
      *
      * Print fields
      *
     D l2rwemdy        s              6  0
      *
     D t1cpam          s             13  2
     D t1cdam          s             13  2
     D t1varam         s             13  2
     D t1asxcdam       s             13  2
     D t1scdam         s             13  2
     D t1tcdam         s             13  2
      *
      ****************************************************************
      * ARRAYS AND TABLES
      ****************************************************************
      *
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *---------------------------------------------------------------
      * Local data area.
      *---------------------------------------------------------------
      *
     Dlda             uds                  dtaara(*lda)
     D  ldfddt                 7     14  0
     D  ldfmdy                15     20  0
      *
     D  ldtddt                21     28  0
     D  ldtmdy                29     34  0
      *
     D  lddsfl                35     35
     D  lddsds                36     45
      *
     D  ldincfl               46     46
     D  ldincds               47     76
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ***************************************************************************************
      * MAINLINE
      ***************************************************************************************
      *
     C                   exsr      $h1hdr
      *
      * Read/print all records from the Workfile (ie: Cash Distribution Header).
      *
     C                   dou       *in90 = *on                                  Main do
     C                   read      tfp3052                                90
     C                   if        *in90 = *off                                 If not EOF
     C                   move      no            first
      *
      * Accumulate for Report Total
      *
     C                   add       w1cpam        t1cpam
     C                   add       w1cdam        t1cdam
     C                   add       w1varam       t1varam
     C                   add       w1asxcdam     t1asxcdam
     C                   add       w1scdam       t1scdam
     C                   add       w1tcdam       t1tcdam
      *
      * Print header/summary record
      *
     C                   exsr      $l1dtl
      *
      * Conditionally print detail
      *
     C                   if        lddsfl = 'D'
     C                   exsr      $detail
     C                   endif
     C                   endif                                                  If not EOF
      *
     C                   enddo                                                  Main do
      *
      * EOF processing
     C                   if        first = yes
     C                   except    nodata
     C                   else
     C                   exsr      $t1tot
     C                   endif
      *
     C                   seton                                        lr
      /EJECT
      *----------------------------------------------------------------------------------------
      * Retrieve/print Cash Distribution Detail for this Batch
      *----------------------------------------------------------------------------------------
      *
     C     $detail       begsr
      *
     C     w1btsn        setll     tfp013
      *
     C                   dou       *in91 = *on                                  Do detail
     C     w1btsn        reade     tfp013                                 91
     C                   if        *in91 = *off                                 If not EOF
      *
      * Flip Revenue Week-ending Date
      *
     C     *iso          test(d)                 cdrwedt                92
     C                   if        *in92 = *off                                 If OK
     C                   move      cdrwedt       wkisodate
     C     *mdy          move      wkisodate     l2rwemdy
     C                   endif                                                  If OK
      *
     C                   exsr      $l2dtl
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Do detail
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Print report/column headings
      *---------------------------------------------------------------
      *
     C     $h1hdr        begsr
      *
     C                   except    h1hdr
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Print the Cash Distribution Header/Summary record
      *---------------------------------------------------------------
      *
     C     $l1dtl        begsr
      *
     C                   if        *inof = *on
     C                   exsr      $h1hdr
     C                   endif
      *
     C                   except    l1dtl
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Print the Cash Distribution Detail line
      *---------------------------------------------------------------
      *
     C     $l2dtl        begsr
      *
     C                   if        *inof = *on
     C                   exsr      $h1hdr
     C                   endif
      *
      * When the record was NOT system generated (ie: we made a manual adjustment)
      * set on the indicator to print "Manual Adjustment" on the detail line.
      *
     C                   if        cdsysfl = no
     C                   seton                                        87
     C                   else
     C                   setoff                                       87
     C                   endif
      *
     C                   except    l2dtl
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Print the Report Total line
      *---------------------------------------------------------------
      *
     C     $t1tot        begsr
      *
     C                   if        *inof = *on
     C                   exsr      $h1hdr
     C                   endif
      *
     C                   except    t1tot
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Key lists
      *  none
      *
      * Retrieve time for report heading.
      *
     C                   time                    rtime
      *
     C                   endsr
      /EJECT
      *-------------------------------------------------------------
      * Report heading lines
      *-------------------------------------------------------------
      *
     Oprint198  e            h1hdr          1 01
     O                       sdpgm               10
     O                                          100 'CASH DISTRIBUTION REPORT'
     O                                          188 'DATE'
     O                       udate         y    198
      *
     O          e            h1hdr          1
     O                       sdusr               10
     O                       lddsds              95
     O                                          188 'TIME'
     O                       rtime              198 '  :  :  '
      *
     O          e            h1hdr          2
     O                                          188 'PAGE'
     O                       page          z    198
      *
     O          e            h1hdr          1
     O                                           19 'From daily date:'
     O                       ldfmdy              28 '  /  /  '
      *
     O          e            h1hdr          2
     O                                           19 'To daily date:'
     O                       ldtmdy              28 '  /  /  '
      *
     O          e            h1hdr          2
     O                                           19 'Batches to include:'
     O                       ldincfl             25
     O                       ldincds             59
      *
      *-------------------------------------------------------------
      * Column headings
      *-------------------------------------------------------------
      *
     O          e            h1hdr       0  1
     O                                           38 'Cash'
     O                                           58 'Total'
     O                                           91 'Revenue'
     O                                          100 'Record'
     O                                          119 'AGG Sales Exp'
     O                                          140 'Seaboard'
     O                                          161 'Triumph'
      *
     O          e            h1hdr       0  1
     O                                            6 'Daily'
     O                                           17 'Batch'
     O                                           38 'Payment'
     O                                           58 'Distributed'
     O                                           78 'Variance'
     O                                           91 'Week-Ending'
     O                                          100 'Exempt'
     O                                          119 'Distributed'
     O                                          140 'Distributed'
     O                                          161 'Distributed'
      *
     O          e            h1hdr       0  1
     O                                            6 'Date'
     O                                           17 'Number'
     O                                           38 'Amount'
     O                                           58 'Amount'
     O                                           78 'Amount'
     O                                           91 'Date'
     O                                           99 'Flag'
     O                                          119 'Amount'
     O                                          140 'Amount'
     O                                          161 'Amount'
      *
     O          e            h1hdr          1
     O                       dash               198
      *
      *-------------------------------------------------------------
      * Detail line (cash distribution header/summary)
      *-------------------------------------------------------------
      *
     O          e            l1dtl       1  0
     O                       w1dmdy               8 '  /  /  '
     O                       w1btsn        z     17
     O                       w1cpam        k     39
     O                       w1cdam        k     59
     O                       w1varam       k     79
      *
     O                       w1asxcdam     k    120
     O                       w1scdam       k    141
     O                       w1tcdam       k    162
      *
     O          e            l1dtl       0  1
     O                       w1dmdy               8 '  /  /  '
     O                       w1btsn        z     17
     O                       w1cpam        k     39
     O                       w1cdam        k     59
     O                       w1varam       k     79
      *
     O                       w1asxcdam     k    120
     O                       w1scdam       k    141
     O                       w1tcdam       k    162
      *
      *-------------------------------------------------------------
      * Detail line (cash distribution detail
      *-------------------------------------------------------------
      *
     O          e            l2dtl          1
     O                       l2rwemdy       b    90 '  /  /  '
     O                       cdrcexfl            98
     O                       cdasxcdam     k    121
     O                       cdscdam       k    141
     O                       cdtcdam       k    162
     O               87                         180 'Manual Adjustment'
      *
      *
      *-------------------------------------------------------------
      * Report total
      *-------------------------------------------------------------
      *
     O          e            t1tot       1
     O                                           39 '-----------------'
     O                                           59 '-----------------'
     O                                           79 '-----------------'
     O                                          120 '-----------------'
     O                                          141 '-----------------'
     O                                          162 '-----------------'
      *
     O          e            t1tot       1
     O                                           17 'Report Total:'
     O                       t1cpam        k     39
     O                       t1cdam        k     59
     O                       t1varam       k     79
      *
     O                       t1asxcdam     k    120
     O                       t1scdam       k    141
     O                       t1tcdam       k    162
      *
      *-------------------------------------------------------------
      * No data message line
      *-------------------------------------------------------------
      *
     O          e            nodata      1
     O                                           19 'No data meets your'
     O                                           39 'selection criteria.'
