     A*%%TS  SD  20030106  165455  ISLRAMS     REL-V5R1M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION
     A*  DISPLAY FILE:  HPD129
     A*  TITLE:         MAINTAIN SCHEDULED TRANSFER
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  DATE:          10/06/95
     A*
     A*----------------------------------------------------------------
     A*  INDICATORS:
     A*
     A*  60  REVISE MODE
     A*  61  CREATE MODE
     A*  90  THE TRANSFER THE USER HAS SELECTED FROM THE WORK WITH HAS
     A*      BEEN DELETED BY ANOTHER USER SINCE SELECTED
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A N90                                  CF04(04 'PROMPT')
     A N90                                  CF05(05 'REFRESH')
     A N90                                  CF09(09 'ACCEPT')
     A  60N90                               CF11(11 'DELETE')
     A                                      CA12(12 'PREVIOUS')
     A                                      CF13(13 'FOLD/DROP')
     A*---------------------------------------------------------------
     A* SUBFILE FORMAT 1
     A*---------------------------------------------------------------
     A*
     A          R HP129S1                   SFL
     A*%%TS  SD  20021210  173857  ISLRAMS     REL-V5R1M0  5722-WDS
     A  85                                  SFLNXTCHG
     A            HDRRN1         4Y 0H
     A            HDLNNO    R        H      REFFLD(MDLNNO)
     A            HDORHGSN  R        H      REFFLD(HGHGSN)
     A            HDDNHGSN  R        H      REFFLD(HGHGSN)
     A            DFORBLCD  R        B 18  2REFFLD(@@BLCD)
     A  35
     AO 55                                  DSPATR(PC)
     A  35                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A  42N35N90                            DSPATR(HI)
     A  42N35N90                            DSPATR(UL)
     A            DFORRMCD  R        B 18  8REFFLD(@@RMCD)
     A  40
     AO 50                                  DSPATR(PC)
     A  40                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A  42N40N90                            DSPATR(HI)
     A  42N40N90                            DSPATR(UL)
     A            DFORHGCD  R        B 18 14REFFLD(HGHGCD)
     A  34
     AO 54                                  DSPATR(PC)
     A  34                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A N34N90 42                            DSPATR(HI)
     A N34N90 42                            DSPATR(UL)
     A            DFSCHD    R        B 18 22REFFLD(MDSCHD)
     A                                      EDTCDE(K)
     A  38                                  DSPATR(PC)
     A  38                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A N38N90 42                            DSPATR(HI)
     A N38N90 42                            DSPATR(UL)
     A            DFORGSCD  R        O 18 30REFFLD(HGGSCD)
     A            DFORPTCD  R        O 18 33REFFLD(HSRREF/@@PTCD *LIBL/HSPREF)
     A            DFORPPCD  R        O 18 39REFFLD(HSRREF/@@PPCD *LIBL/HSPREF)
     A            DFDNBLCD  R        B 18 45REFFLD(@@BLCD)
     A  37
     AO 57                                  DSPATR(PC)
     A  37                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A  42N37N90                            DSPATR(HI)
     A  42N37N90                            DSPATR(UL)
     A            DFDNRMCD  R        B 18 51REFFLD(@@RMCD)
     A  48
     AO 58                                  DSPATR(PC)
     A  48                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A  42N48N90                            DSPATR(HI)
     A  42N48N90                            DSPATR(UL)
     A            DFDNHGCD  R     A  B 18 57REFFLD(HGHGCD)
     A  36
     AO 56                                  DSPATR(PC)
     A  36                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A N36N90 42                            DSPATR(HI)
     A N36N90 42                            DSPATR(UL)
     A            DFDNGSCD  R        O 18 65REFFLD(HGGSCD)
     A            DFDNPTCD  R        O 18 68REFFLD(HSRREF/@@PTCD *LIBL/HSPREF)
     A            DFDNPPCD  R        O 18 74REFFLD(HSRREF/@@PPCD *LIBL/HSPREF)
     A                                 19 14'Grp typ:'
     A            DFORGTCD  R        O 19 23REFFLD(HSRREF/@@GTCD *LIBL/HSPREF)
     A                                 19 57'Grp typ:'
     A            DFDNGTCD  R        O 19 66REFFLD(HSRREF/@@GTCD *LIBL/HSPREF)
     A*
     A*----------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT  1
     A*----------------------------------------------------------------
     A*
     A          R HP129C1                   SFLCTL(HP129S1)
     A*%%TS  SD  20030106  165455  ISLRAMS     REL-V5R1M0  5722-WDS
     A                                      SFLSIZ(0100)
     A                                      SFLPAG(0002)
     A N90                                  ROLLUP(25 'PAGE FORWARD')
     A                                      SFLCSRRRN(&HDRR1)
     A                                      OVERLAY
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A  88                                  SFLFOLD(CF13)
     A  89                                  SFLDROP(CF13)
     A                                      SFLMODE(&FOLD)
     A                                      SETOF(30)
     A                                      SETOF(31)
     A                                      SETOF(32)
     A                                      SETOF(33)
     A                                      SETOF(34)
     A                                      SETOF(35)
     A                                      SETOF(36)
     A                                      SETOF(37)
     A                                      SETOF(38)
     A                                      SETOF(39)
     A                                      SETOF(40)
     A                                      SETOF(48)
     A                                      SETOF(50)
     A                                      SETOF(51)
     A                                      SETOF(52)
     A                                      SETOF(53)
     A                                      SETOF(54)
     A                                      SETOF(55)
     A                                      SETOF(56)
     A                                      SETOF(57)
     A                                      SETOF(58)
     A                                      RTNCSRLOC(&RCD &FLD)
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            HDRR1          5S 0H
     A            FOLD           1A  H
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 26'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A            DFMODE    R        O  2  2REFFLD(@@MODE)
     A                                      DSPATR(HI)
     A                                  2 24'MAINTAIN SCHEDULED TRANSFER'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  4  2'Movement number ......:'
     A            C1MVSN    R        O  4 28REFFLD(@@MVSN)
     A                                      EDTCDE(Z)
     A                                  5  2'Scheduled ship date ...'
     A            C1MDY     R     S  B  5 28REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A  39                                  DSPATR(PC)
     A  39                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A N39N90 42                            DSPATR(HI)
     A N39N90 42                            DSPATR(UL)
     A                                  6  2'Trucker ...............'
     A N90                              6 26'?'
     A            C1TICD    R        B  6 28REFFLD(@@TICD)
     A  33
     AO 53                                  DSPATR(PC)
     A  33                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A  42N33N90                            DSPATR(HI)
     A  42N33N90                            DSPATR(UL)
     A            C1TINM    R        O  6 38REFFLD(@@TINM)
     A                                  8  2'Total scheduled head ..'
     A            C1SCHD    R        B  8 28REFFLD(MDSCHD)
     A                                      EDTCDE(K)
     A  30                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A  42N30N90                            DSPATR(HI)
     A  42N30N90                            DSPATR(UL)
     A                                 10  2'-------------- ORIGIN -------------
     A                                      --------'
     A                                      DSPATR(HI)
     A                                 10 45'------------ DESTINATION ----------
     A                                      -'
     A                                      DSPATR(HI)
     A                                 11  2'Farm ..'
     A N90                             11 10'?'
     A            C1ORFSCD  R        B 11 12REFFLD(FSFSCD)
     A  31
     AO 51                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A  42N31N90                            DSPATR(HI)
     A  42N31N90                            DSPATR(UL)
     A                                 11 45'Farm ..'
     A N90                             11 53'?'
     A            C1DNFSCD  R        B 11 55REFFLD(FSFSCD)
     A  32
     AO 52                                  DSPATR(PC)
     A  32                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A  42N32N90                            DSPATR(HI)
     A  42N32N90                            DSPATR(UL)
     A            C1ORFSNM  R        O 12 12REFFLD(FSFSNM)
     A            C1DNFSNM  R        O 12 55REFFLD(FSFSNM)
     A N90                             14  3'?'
     A N90                             14 10'?'
     A N90                             14 16'?'
     A N90                             14 47'?'
     A N90                             14 52'?'
     A N90                             14 59'?'
     A                                 15 23'Sched'
     A                                 15 33'Production'
     A                                 15 68'Production'
     A                                 16  2'Bldg'
     A                                 16  8'Room'
     A                                 16 14'Group'
     A                                 16 24'Head'
     A                                 16 30'St'
     A                                 16 33'Type'
     A                                 16 39'Phase'
     A                                 16 45'Bldg'
     A                                 16 51'Room'
     A                                 16 57'Group'
     A                                 16 65'St'
     A                                 16 68'Type'
     A                                 16 74'Phase'
     A*----------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*----------------------------------------------------------------
     A          R HP129K1
     A*%%TS  SD  20021210  175440  ISLRAMS     REL-V5R1M0  5722-WDS
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A N90                             23 10'F4=Prompt'
     A N90                             23 20'F5=Refresh'
     A N90                             23 31'F9=Accept'
     A N90 60                          23 41'F11=Delete'
     A                                 23 52'F12=Previous'
     A                                 23 65'F13=Fold/Unfold'
      *----------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *----------------------------------------------------------------
     A          R HP129ES                   SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
     A*----------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*----------------------------------------------------------------
     A*
     A          R HP129EC                   SFLCTL(HP129ES)
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
