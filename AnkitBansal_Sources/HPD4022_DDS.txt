     A*%%TS  SD  20070920  092402  ISLRAMS     REL-V5R4M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD4022
     A*  TITLE:         WORK WITH NUR/FIN TARGET TABLES
     A*  PROGRAMMER:    SHARON ZURN
     A*  DATE:          01/03/07
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A                                      CF05(05 'REFRESH')
     A                                      CF06(06 'CREATE')
     A                                      CF07(07 'LISTING')
     A                                      CF10(10 'COPY')
     A                                      CF17(17 'PRINT OPTIONS')
     A                                      CF20(20 'WRKSPLF')
     A*
     A*---------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP4022S1                  SFL
     A*%%TS  SD  20070907  164201  ISLRAMS     REL-V5R4M0  5722-WDS
     A  85                                  SFLNXTCHG
     A            HDRRN1         4Y 0H
     A            D1OPT          2A  B 10  2CHGINPDFT(UL)
     A  30                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A N30                                  DSPATR(HI)
     A            D1OBCELL  R        O 10 10REFFLD(HSRREF/NTOBCELL *LIBL/HSPREF)
     A                                      EDTCDE(M)
     A            D1OBPOD   R        O 10 19REFFLD(HSRREF/NTOBPOD *LIBL/HSPREF)
     A                                      EDTCDE(M)
     A            D1NTDS    R        O 10 27REFFLD(HSRREF/NTNTDS *LIBL/HSPREF)
     A*
     A*---------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP4022C1                  SFLCTL(HP4022S1)
     A*%%TS  SD  20070918  140739  ISLRAMS     REL-V5R4M0  5722-WDS
     A                                      SFLSIZ(0050)
     A                                      SFLPAG(0011)
     A N82                                  ROLLUP(25 'PAGE FORWARD')
     A                                      SETOF(30)
     A                                      OVERLAY
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 27'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  2 22'WORK WITH NUR/FIN TARGET TABLES'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  4  2'2=Revise'
     A                                  4 12'4=Delete'
     A                                  4 22'7=Work With Target Years'
     A                                  6  7'--Target Table---'
     A                                      DSPATR(HI)
     A                                  7  8'Origin'
     A                                      DSPATR(HI)
     A                                  7 17'Origin'
     A                                      DSPATR(HI)
     A                                  8  2'Opt'
     A                                      DSPATR(HI)
     A                                  8  7'BGF Cell'
     A                                      DSPATR(HI)
     A                                  8 17'BGF Pod'
     A                                      DSPATR(HI)
     A                                  8 27'Description'
     A                                      DSPATR(HI)
     A            C1OBCELL  R        B  9 10REFFLD(HSRREF/NTOBCELL *LIBL/HSPREF)
     A                                      EDTCDE(M)
     A                                      DSPATR(HI)
     A            C1OBPOD   R        B  9 19REFFLD(HSRREF/NTOBPOD *LIBL/HSPREF)
     A                                      EDTCDE(M)
     A                                      DSPATR(HI)
     A*
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R HP4022K1
     A*%%TS  SD  20070920  092402  ISLRAMS     REL-V5R4M0  5722-WDS
     A  43                             21  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 22  2'F3=Exit'
     A                                 22 11'F5=Refresh'
     A                                 22 23'F6=Create'
     A                                 22 34'F7=Listing'
     A                                 22 46'F10=Copy'
     A                                 22 56'F17=Print Options'
     A                                 23 56'F20=Spool Files'
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *---------------------------------------------------------------
     A          R HP4022ES                  SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE CONTROL FORMAT
      *---------------------------------------------------------------
     A          R HP4022EC                  SFLCTL(HP4022ES)
      *
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
