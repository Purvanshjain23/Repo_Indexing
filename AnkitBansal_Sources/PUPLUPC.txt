/**************************************************************************/
/*  SYSTEM    - ORDER MANAGEMENT SYSTEM                                   */
/*  PROGRAM   - PUPLUPC - PRT SLS CST SOLD TO LOGCL EXECUTE USER PROGRAM  */
/*  PROJECT   - S16143 - SALES SOLD TO HIERARCHY                          */
/*  BY        - DANNY NGUYEN                                              */
/*  DATE      - 02/18/2020                                                */
/*  PURPOSE   - Email the Sales Customer Sales Sold To Log report.        */
/*                                                                        */
/*  PARAMETERS:                                                           */
/*   &FRMDATN    - CREATED FROM DATE                                      */
/*   &THRDATN    - CREATED THRU DATE                                      */
/*   &SHTOCST    - SHIP TO CUSTOMER NUMBER                                */
/*   &SLSSDTO    - SALES SOLD TO                                          */
/*   &EMAIL      - EMAIL ADDRESS                                          */
/*                                                                        */
/**************************************************************************/
/* MODIFICATION HISTORY                                                   */
/*========================================================================*/
/* DATE     INT  PROJECT DESCRIPTION                                      */
/* -------- ---- ------- -------------------------------------------------*/
/* MM/DD/YY BY   XXXXXX                                                   */
/**************************************************************************/

             PGM        PARM(&FRMDATN &THRDATN &SHTOCST &SLSSDTO +
                          &EMAIL)

/* PARMS */
             DCL        VAR(&FRMDATN) TYPE(*DEC) /* From Date (Passed) */
             DCL        VAR(&THRDATN) TYPE(*DEC) /* Thru Date (Passed) */
             DCL        VAR(&SHTOCST) TYPE(*DEC) LEN(7 0) /* Ship To Cust# */
             DCL        VAR(&SLSSDTO) TYPE(*DEC) LEN(5 0) /* Sales Sold To# */
             DCL        VAR(&EMAIL) TYPE(*CHAR) LEN(50) /* Email Address */

/* INTERNAL FIELDS */
             DCL        VAR(&FROMDTE) TYPE(*DEC) LEN(7 0)
             DCL        VAR(&FRDATEA) TYPE(*CHAR) LEN(7)
             DCL        VAR(&FDMMDDYY) TYPE(*CHAR) LEN(8)
             DCL        VAR(&TODTE) TYPE(*DEC) LEN(7 0)
             DCL        VAR(&TODTEA) TYPE(*CHAR) LEN(7)
             DCL        VAR(&TDMMDDYY) TYPE(*CHAR) LEN(8)
             DCL        VAR(&STCA) TYPE(*CHAR) LEN(7)
             DCL        VAR(&SSTA) TYPE(*CHAR) LEN(5)
             DCL        VAR(&FLIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&HLIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&EMLMSG) TYPE(*CHAR) LEN(100)
             DCL        VAR(&BYSTC) TYPE(*CHAR) LEN(20)
             DCL        VAR(&BYSST) TYPE(*CHAR) LEN(20)


/********************************************************************/
/* INITIALIZE VARIABLES                                             */
/********************************************************************/
             CHGVAR     VAR(&FROMDTE) VALUE(&FRMDATN)
             CHGVAR     VAR(&FRDATEA) VALUE(&FROMDTE)
             CHGVAR     VAR(&TODTE) VALUE(&THRDATN)
             CHGVAR     VAR(&TODTEA) VALUE(&TODTE)
             CHGVAR     VAR(&STCA) VALUE(&SHTOCST)
             CHGVAR     VAR(&SSTA) VALUE(&SLSSDTO)
             CHGVAR     VAR(&EMLMSG) VALUE('Sales Customer Sold To +
                          Log by')
             CHGVAR     VAR(&BYSTC) VALUE('Ship To Customer')
             CHGVAR     VAR(&BYSST) VALUE('Sales Sold To')

    /* Determine If In PROD or TEST Env.          */
             CALL       PGM(CAQXXFR) PARM(' ' &FLIB &HLIB)

    /* Convert CYMD Dates to MM/DD/YY Date Format */
             CALL       PGM(PUJLXFR) PARM('' &FROMDTE &FDMMDDYY) +
                          /*From Date*/
             CALL       PGM(PUJLXFR) PARM('' &TODTE &TDMMDDYY) +
                          /*Thru Date*/

/*********************************************************************/
/* RUN SEQUEL - BY SHIP TO CUSTOMER                                  */
/*********************************************************************/
             IF         COND(&SHTOCST *NE 0) THEN(DO)
    /* Format Email Message */
             CHGVAR     VAR(&EMLMSG) VALUE(&EMLMSG *BCAT &BYSTC +
                          *BCAT &STCA *BCAT '- From Date Range' +
                          *BCAT &FDMMDDYY *BCAT '-' *BCAT &TDMMDDYY)

    /* Concat 'TEST' to Email Message If Not PROD Data Library */
             IF         COND(&FLIB *NE 'PRKFLIB') THEN(DO)
             CHGVAR     VAR(&EMLMSG) VALUE(&EMLMSG *BCAT '- TEST')
             ENDDO

             EXECUTE    VIEW(SLSSDTOLG1) PCFMT(*XLS) +
                          RECIPIENT(&EMAIL) EMLMSG(&EMLMSG) +
                          SETVAR((&FROMDTE &FRDATEA) (&TODTE +
                          &TODTEA) (&SHTOCST &STCA))
             ENDDO

/*********************************************************************/
/* RUN SEQUEL - BY SALES SOLD TO                                     */
/*********************************************************************/
             IF         COND(&SLSSDTO *NE 0) THEN(DO)
    /* Format Email Message */
             CHGVAR     VAR(&EMLMSG) VALUE(&EMLMSG *BCAT &BYSST +
                          *BCAT &SSTA *BCAT '- From Date Range' +
                          *BCAT &FDMMDDYY *BCAT '-' *BCAT &TDMMDDYY)

    /* Concat 'TEST' to Email Message If Not PROD Data Library */
             IF         COND(&FLIB *NE 'PRKFLIB') THEN(DO)
             CHGVAR     VAR(&EMLMSG) VALUE(&EMLMSG *BCAT '- TEST')
             ENDDO

             EXECUTE    VIEW(SLSSDTOLG2) PCFMT(*XLS) +
                          RECIPIENT(&EMAIL) EMLMSG(&EMLMSG) +
                          SETVAR((&FROMDTE &FRDATEA) (&TODTE +
                          &TODTEA) (&SLSSDTO &SSTA))
             ENDDO

 END:        ENDPGM
