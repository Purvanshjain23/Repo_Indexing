      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      HOG PRODUCTION
      * PROGRAM:     HP673
      * TITLE:       LISTING OF FARM COST BY FARM SITE
      * PROGRAMMER:  LEANNE FEDOR
      * CREATED:     6/28/94
      *
      * FUNCTION: THIS PROGRAM IS CALLED FROM HP474CL.  THE USER'S
      *           SELECTIONS ARE PASSED IN THE LDA AND AN OPNQRY
      *           IS USED IN HP474CL TO SELECT THE RECORDS.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 09/12/94  LEANNE FEDOR
      *           RECOMPILE ONLY. REMOVED NEXT MOVEMENT NUMBER FROM
      *           BUSINESS OFFICE FILE.
      *
      * 11/04/94  LEANNE FEDOR
      *           ADDED 'COST PER POUND' TO FILE AND RENAMED THE
      *           'COST PER HEAD' FIELD.
      *
      * 04/21/95  LEANNE FEDOR
      *           CHANGED FARM SITE CODE FROM ALPHA TO NUMERIC.
      *
      * 04/25/96  LEANNE FEDOR
      *           THE WORK 'PAGE' WAS NOT PRINTING IN THE HEADING.
      *
      * 04/13/98  LeAnne Fedor
      *           Recompile only. New field 'state' added to farm site file.
      *
      * 06/05/01  LeAnne Fedor
      *           Recompile only. New field 'multisite' added to Farm Site file.
      *
      * 06/28/01  LeAnne Fedor
      *           Recompile only.
      *           Three fields (manager/supervisor/multisite) renamed in Farm Site file.
      *
      * 09/04/01  LeAnne Fedor
      *           Recompile only. New 'square feet' fields added to Farm Site file.
      *
      * 03/21/05  LeAnne Fedor
      *           Recompile only. Field size changed in Farm Cost file.
      *
      * 10/16/13  LeAnne Ramsey (E2831)
      *           Recompile only. Added field 'MTech Reference'.
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     FHSP009    IF   E           K DISK
      *    BUSINESS OFFICE           (KEY: BOBOBO)
      *
     FHSP018    IF   E           K DISK
      *    FARM SITE                 (KEY: FSFSCD)
      *
     FHSL028B   IF   E           K DISK
      *    FARM COSTS                (KEY: FCFSBO FCFSCD FCEFDT)
      *
     FQPRINT    O    F  132        PRINTER OFLIND(*INOF)
      *
      /EJECT
      ****************************************************************
      * INPUT SPECS
      ****************************************************************
      *
      *---------------------------------------------------------------
      *  LOCAL DATA AREA
      *---------------------------------------------------------------
      *
     D                UDS
     D  LDFSBO                 1      5
     D  LDSQ                   6      6
     D  LDEFDT                 7     14  0
     D  LDFSCD                15     19  0
     D  LDMDY                 20     25  0
      *
      *---------------------------------------------------------------
      * STANDARD PROGRAM STATUS DATA STRUCTURE
      *---------------------------------------------------------------
      *    EXTERNALLY DEFINED AS UTPGFR (RECORD FORMAT: PGMDSR)
     D PGMDS         ESDS                  EXTNAME(UTPGFR)
      *
      *---------------------------------------------------------------
      *  DATE FORMATS
      *---------------------------------------------------------------
      *
      * DATE IN MMDDYY FORMAT
      *
     D                 DS
     D  WKMDY                  1      6  0
     D  WKMM                   1      2  0
     D  WKDD                   3      4  0
     D  WKYY                   5      6  0
      *
      * DATE IN CCYYMMDD FORMAT
      *
     D                 DS
     D  WKEFDT                 1      8  0
     D  WKCEN                  1      2  0
     D  WKYER                  3      4  0
     D  WKMON                  5      6  0
     D  WKDAY                  7      8  0
      *
      *---------------------------------------------------------------
      *  NAMED CONSTANTS
      *---------------------------------------------------------------
      *
     D YES             C                   CONST('Y')
     D NO              C                   CONST('N')
      *
      *
     D                 DS
     D  DUMMYD                 1      1
      *  THE PRECEDING DUMMY DATA STRUCTURE IS ONLY TO KEEP THE I-SPEC
      *  INFO FROM RUNNING OVER INTO THE 'CALC SPECS' ON THE COMPILE
      *  LISTING.  IT SERVES NO OTHER PURPOSE AND CAN BE DELETED
      *  AT ANY TIME!!!
      /EJECT
      ****************************************************************
      * MAIN LINE
      ****************************************************************
      *
      * READ ALL FARM COST RECORDS SELECTED BY THE OPNQRY
      *
     C                   EXCEPT    HRHDR
      *
     C     *IN92         DOUEQ     *ON                                          MAIN DO
     C                   READ      HSL028B                                92
     C     *IN92         IFEQ      *OFF                                         IF NOT EOF
      *
      * RETRIEVE FARM SITE NAME FROM FARM SITE MASTER
      *
     C     FCFSCD        IFNE      SVFSCD                                       IF NEW FARM
     C     FCFSCD        CHAIN     HSP018                             93
     C     *IN93         IFEQ      *OFF
     C                   MOVEL     FSFSNM        WKFSNM
     C                   ELSE
     C                   MOVEL(P)  'UNKNOWN'     WKFSNM
     C                   ENDIF
     C                   EXCEPT    HFHDR
     C                   Z-ADD     FCFSCD        SVFSCD
     C                   ENDIF                                                  IF NEW FARM
      *
      * PRINT HEADINGS IF NECESSARY
      *
     C     *INOF         IFEQ      *ON
     C                   EXCEPT    HRHDR
     C                   EXCEPT    HFHDR
     C                   ENDIF
      *
      * FLIP EFFECTIVE DATE FROM CCYYMMDD TO MMDDYY FOR PRINTING
      *
     C                   Z-ADD     FCEFDT        WKEFDT
     C                   Z-ADD     WKMON         WKMM
     C                   Z-ADD     WKDAY         WKDD
     C                   Z-ADD     WKYER         WKYY
      *
      * PRINT DETAIL LINE
      *
     C                   EXCEPT    DTLINE
     C                   MOVE      YES           PRTFLG
     C                   ENDIF                                                  IF NOT EOF
     C                   ENDDO                                                  MAIN DO
      *
      *---------------------------------------------------------------
      * EOF PROCESSING
      *---------------------------------------------------------------
      * PRINT STANDARD 'NO DATA...' ERROR MESSAGE IF NO DATA MET THE
      * USER'S SELECTION CRITERIA
      *
     C     PRTFLG        IFEQ      NO
     C                   EXCEPT    NODATA
     C                   ENDIF
      *
     C                   SETON                                        LR
      /EJECT
      *---------------------------------------------------------------
      * *INZSR - INITIALIZATION SUBROUTINE
      *---------------------------------------------------------------
      *
     C     *INZSR        BEGSR
      *
     C     *LIKE         DEFINE    BOBODS        WKBODS
     C     *LIKE         DEFINE    FSFSNM        WKFSNM
     C     *LIKE         DEFINE    FCFSCD        SVFSCD
      *
      * CHAIN TO BUSINESS OFFICE FILE FOR DESCRIPTION IN HEADING
      *
     C     LDFSBO        CHAIN     HSP009                             91
     C     *IN91         IFEQ      *OFF
     C                   MOVEL     BOBODS        WKBODS
     C                   ELSE
     C                   MOVEL(P)  'UNKNOWN'     WKBODS
     C                   ENDIF
      *
      * SET INDICATORS TO CONTROL PRINTING OF SELECTION CRITERIA IN
      * HEADING.
      *
     C     LDMDY         IFEQ      0                                            IF ALL DATES
     C                   SETON                                        80
     C                   ENDIF                                                  IF ALL DATES
      *
     C     LDFSCD        IFEQ      0                                            IF ALL FARMS
     C                   SETON                                        81
     C                   ENDIF                                                  IF ALL FARMS
      *
      *
      * SET PRINT FLAG TO DETERMINE IF ANY RECORDS WERE PRINTED
      * SET UP DASHED LINE FOR UNDERLINING THE HEADINGS
      * RETRIEVE TIME FOR REPORT HEADING
      *
     C                   MOVE      NO            PRTFLG            1
     C                   MOVE      *ALL'-'       DASH             80
     C                   TIME                    RTIME             6 0
      *
     C                   ENDSR
      /EJECT
      *-------------------------------------------------------------
      * REPORT HEADING LINES
      *-------------------------------------------------------------
      *
     OQPRINT    E            HRHDR          1 01
     O                       SDPGM               10
     O                                           50 'HOG PRODUCTION SYSTEM'
     O                                           70 'DATE'
     O                       UDATE         Y     80
      *
     O          E            HRHDR          1
     O                       SDUSR               10
     O                                           45 'LISTING OF FARM COSTS'
     O                                           58 'BY FARM SITE'
     O                                           70 'TIME'
     O                       RTIME               80 '  :  :  '
      *
     O          E            HRHDR          1
     O                                           70 'PAGE'
     O                       PAGE          Z     80
      *
     O          E            HRHDR          1
     O                                           24 'SELECTED BUSINESS OFFICE'
     O                                           25 ':'
     O                       LDFSBO              32
     O                       BOBODS              65
      *
     O          E            HRHDR          1
     O                                           25 'SELECTED EFFECTIVE DATE:'
     O              N80      LDMDY         Y     35
     O               80                          30 'ALL'
      *
     O          E            HRHDR          2
     O                                           25 'SELECTED FARM SITE:'
     O              N81      LDFSCD        M     33
     O               81                          30 'ALL'
      *
     O          E            HRHDR          1
     O                                            9 'FARM SITE'
     O                                           47 'EFFECTIVE DATE'
     O                                           64 'COST PER HEAD'
     O                                           80 'COST PER POUND'
      *
     O          E            HRHDR          0
     O                       DASH                80
      *
      *-------------------------------------------------------------
      * FARM HEADING LINE
      *-------------------------------------------------------------
      *
     O          E            HFHDR       2
     O                       FCFSCD        M      6
     O                       WKFSNM              32
      *-------------------------------------------------------------
      * FARM COST DETAIL LINE
      *-------------------------------------------------------------
      *
     O          E            DTLINE      1
     O                       WKMDY         Y     43
     O                       FCHDRT        K     62
     O                       FCLBRT        K     77
      *
      *-------------------------------------------------------------
      * NO DATA MESSAGE LINE
      *-------------------------------------------------------------
      *
     O          E            NODATA      1
     O                                           19 'NO DATA MEETS YOUR'
     O                                           38 'SELECTION CRITERIA'
