      *****************  RPG PROGRAM HEADING  ***********************
      *
      * SYSTEM:      HOG PRODUCTION
      * PROGRAM:     HP153
      * TITLE:       MAINTAIN INSTALLED EQUIPMENT
      * PROGRAMMER:  LEANNE FEDOR
      * CREATED:     03/18/94
      *
      * FUNCTION: THIS PROGRAM USES A SUBFILE TO MAINTAIN RECORDS
      *           IN THE INSTALLED EQUIPMENT MASTER FILE.
      *
      *           THE PROGRAM WILL CREATE, DELETE AND UPDATE RECORDS
      *           FOR A PARTICULAR BUILDING/ROOM FOR A SPECIFIC FARM
      *           SITE.
      *
      *           THIS PROGRAM IS CALLED FROM HP151-MAINTAIN BUILDINGS.
      *           THIS PROGRAM IS CALLED FROM HP152-MAINTAIN ROOMS.
      *
      *           IT DOES NOT HAVE SELECTORS OR POSITIONERS.
      *           IT DOES HAVE F4-PROMPT LOGIC.
      *           IT LOADS THE SUBFILE A PAGE AT A TIME.
      *           IT USES THE STANDARD MESSAGE HANDLING PROGRAM.
      *           IT HAS STANDARD SUBFILE OPTION EDITING.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 04/19/95  LEANNE FEDOR
      *           CHANGED FARM SITE CODE FROM ALPHA TO NUMERIC
      *
      *
      * 12/12/95  LEANNE FEDOR
      *           RECOMPILE ONLY. NEW FIELD 'ROOM STATUS' ADDED TO
      *           BUILDING ROOM FILE.
      *
      * 01/08/97  LEANNE FEDOR
      *           FIXED LOGIC THAT ALLOWED F4-PROMPT ON PROTECTED
      *           SUBFILE FIELD.
      *
      * 04/13/98  LeAnne Fedor
      *           Recompile only. New field 'state' added to farm site file.
      *
      * 06/05/01  LeAnne Fedor
      *           Recompile only. New field 'multisite' added to Farm Site file.
      *
      * 06/26/01  LeAnne Fedor
      *           Recompile only.
      *           Three fields (manager/supervisor/multisite) renamed in Farm Site file.
      *
      * 09/04/01  LeAnne Fedor
      *           Recompile only...'hog capacity' removed from Farm Buildings file.
      *           'Square feet' fields added to Farm Site file.
      *
      * 01/16/06  LeAnne Fedor
      *           Recompile only.
      *           We have removed "Bin Set" from HSP020-Building/Rooms and put it in our new
      *           HSP113-Rooms/Bin Sets file.
      *
      * 10/15/13  LeAnne Ramsey (E2831)
      *           Recompile only. Added field 'MTech Reference'.
      /EJECT
      ****************************************************************
      * FILE SPECIFICAITONS
      ****************************************************************
      *
     FHPD153    CF   E             WORKSTN SFILE(HP153S1:HDRRN1)
     F                                     INFDS(IOFEED)
      *
     FHSP018    IF   E           K DISK
      *    FARM SITE
      *
     FHSP019    IF   E           K DISK
      *    FARM BUILDING
      *
     FHSP020    IF   E           K DISK
      *    ROOMS
      *
     FHSP022    IF   E           K DISK
      *    EQUIPMENT MASTER
      *
     FHSP023    UF A E           K DISK    INFDS(DBFEED)
      *  INSTALLED EQUIPMENT
      *
      /EJECT
      ****************************************************************
      * TABLE AND ARRAY SPECIFICATIONS
      ****************************************************************
      *
      *---------------------------------------------------------------
      *  STANDARD MESSAGE ARRAYS
      *---------------------------------------------------------------
      *
      /EJECT
      ****************************************************************
      * INPUT SPECIFICATIONS
      ****************************************************************
      *
      *---------------------------------------------------------------
      * STANDARD ERROR MESSAGE HANDLING DATA STRUCTURES
      *---------------------------------------------------------------
      *
      * FOR ERROR MESSAGE HANDLING, A PACKED INDEX (E) IS REQUIRED.
      * FOR PROGRAM READABILITY, DEFINE A CORRESPONDING ERROR COUNT
      * FIELD CALLED 'ERROR'. ALSO DEFINE CURSOR LOCATION FIELDS.
      *
     D                 DS                  INZ
     D  ERROR                  1      2P 0
     D  E                      1      2P 0
      *
      * THIS DATA STRUCTURE SUPPLIES THE NAME OF THE MESSAGE FILE TO
      * THE MESSAGE HANDLING CL PROGRAM.  THE FIELD NAME MSGFIL MUST BE
      * CONSTANT. THE VALUE IN QUOTES IS THE NAME OF THE SPECIFIC
      * MESSAGE FILE CONTAINING THE USER DEFINED MESSAGES.
      *
     D                 DS                  INZ
     D  MSGFIL                 1     10    INZ('HSMSGF    ')
      *
      * THE FOLLOWING 3 DATA STRUCTURES ARE USED TO SPEED MESSAGE
      * HANDLING SINCE IT IS FASTER TO CLEAR DATA STRUCTURES THAN
      * ARRAYS.  EACH IS ASSOCIATED WITH A STANDARD MESSAGE ARRAY.
      *
     D MGI             DS           140    INZ
     D  MGID                   1    140
     D                                     DIM(20)                              MSG ID ARRAY
      *
     D MGD             DS          1000    INZ
     D  MGDT                   1   1000
     D                                     DIM(20)                              MSG PARMS
      *
     D MGWK            DS            50    INZ
     D  MGW                    1     50
     D                                     DIM(50)                              MSG WORK PARMS
      *
      *---------------------------------------------------------------
      * STANDARD PROGRAM STATUS DATA STRUCTURE
      *---------------------------------------------------------------
      *    EXTERNALLY DEFINED AS UTPGFR (RECORD FORMAT: PGMDSR)
     D PGMDS         ESDS                  EXTNAME(UTPGFR)
      *
      *---------------------------------------------------------------
      * STANDARD WORKSTATION INFORMATION DATA STRUCTURE
      *---------------------------------------------------------------
      *    EXTERNALLY DEFINED AS UTWSFR (RECORD FORMAT: UTIDFRR)
     D IOFEED        E DS                  EXTNAME(UTWSFR)
      *
      *---------------------------------------------------------------
      * STANDARD DATABASE FILE INFORMATION DATA STRUCTURE
      *---------------------------------------------------------------
      *    EXTERNALLY DEFINED AS UTDBFR (RECORD FORMAT: FDBCKD)
     D DBFEED        E DS                  EXTNAME(UTDBFR)
      *
      *---------------------------------------------------------------
      * STANDARD SUBFILE OPTION CONVERSION
      *---------------------------------------------------------------
     D                 DS
     D  DFOPT                  1      2
     D  WKOPT1                 1      1
     D  WKOPT2                 2      2
      *
      *---------------------------------------------------------------
      *  NAMED CONSTANTS
      *---------------------------------------------------------------
      *
     D CREATE          C                   CONST('CREATE')
     D REVISE          C                   CONST('REVISE')
     D DELETE          C                   CONST('DELETE')
     D VIEW            C                   CONST('VIEW  ')
     D SELECT          C                   CONST('SELECT')
     D YES             C                   CONST('Y')
     D NO              C                   CONST('N')
     D SET1            C                   CONST('SET1  ')
     D EDIT1           C                   CONST('EDIT1 ')
     D SCRN1           C                   CONST('SCRN1 ')
     D EXIT            C                   CONST('EXIT  ')
     D UPDATE          C                   CONST('UPDATE')
      *
      /EJECT
      ****************************************************************
      * MAINLINE
      ****************************************************************
      *
     C                   MOVEL     SET1          ROUTNE
      *
     C     *INLR         DOWEQ     *OFF                                         MAIN DO LOOP
      *
     C     ROUTNE        CASEQ     SET1          $SET1
     C     ROUTNE        CASEQ     SCRN1         $SCRN1
     C     ROUTNE        CASEQ     EDIT1         $EDIT1
      *
     C     ROUTNE        CASEQ     EXIT          $EXIT
     C                   ENDCS
     C                   ENDDO                                                  MAIN DO LOOP
      *
      /EJECT
      *----------------------------------------------------------------
      * $SET1 - SUBROUTINE TO SET ENVIRONMENT FOR SCREEN 1
      *----------------------------------------------------------------
      *
     C     $SET1         BEGSR
      *
      * VERIFY THAT THE FARM SITE THAT THE USER SELECTED STILL EXISTS.
      *
     C     C1FSCD        CHAIN     HSP018                             90
     C     *IN90         IFEQ      *ON                                          IF NOT FOUND
     C                   MOVEL     YES           NOTFLG
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS00121'     MGID(E)
     C                   CLEAR                   MGW
     C                   MOVE      C1FSCD        XXFSCD
     C                   MOVEA     XXFSCD        MGW(1)
     C                   MOVEA     MGW           MGDT(E)
     C                   ENDIF
     C                   ENDIF                                                  IF NOT FOUND
      *
      * VERIFY THAT THE BUILDING THE USER SELECTED STILL EXISTS.
      *
     C     K1P019        CHAIN     HSP019                             90
     C     *IN90         IFEQ      *ON                                          IF NOT FOUND
     C                   MOVEL     YES           NOTFLG
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS00122'     MGID(E)
     C                   CLEAR                   MGW
     C                   MOVEA     C1BLCD        MGW(1)
     C                   MOVEA     MGW           MGDT(E)
     C                   ENDIF
     C                   ENDIF                                                  IF NOT FOUND
      *
      * IF A ROOM HAS BEEN PASSED INTO THIS PROGRAM, THEN
      * VERIFY THAT THE ROOM THE USER SELECTED STILL EXISTS.
      *
     C     C1PPCD        IFNE      *BLANK                                       IF A ROOM
     C     K3P020        CHAIN     HSP020                             90
     C     *IN90         IFEQ      *ON                                          IF NOT FOUND
     C                   MOVEL     YES           NOTFLG
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS00120'     MGID(E)
     C                   CLEAR                   MGW
     C                   MOVEA     C1RMCD        MGW(1)
     C                   MOVEA     MGW           MGDT(E)
     C                   ENDIF
     C                   ENDIF                                                  IF NOT FOUND
     C                   ENDIF                                                  IF A ROOM
      *
      *  CLEAR/FILL THE SUBFILE
      *
     C                   EXSR      $CLR1
     C                   EXSR      $FRSH1
      *
      *  SET ROUTINE TO DISPLAY SCREEN
      *
     C                   MOVEL     SCRN1         ROUTNE
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $SCRN1 - SUBROUTINE TO PERFORM OPERATOR I/O FOR SCREEN 1
      *----------------------------------------------------------------
      *
     C     $SCRN1        BEGSR
      *
      * WRITE THE ERROR MESSAGES FROM THE ERROR ARRAYS TO THE
      * ERROR MESSAGE SUBFILE
      *
     C                   EXSR      $WRMSG
      *
      *  WRITE SCREEN 1 TO CRT
      *
     C                   WRITE     HP153K1
     C                   WRITE     HP153EC
     C                   EXFMT     HP153C1
      *
      * SET THE 'RESET' FLAG TO NO.  THIS FLAG IS USED TO DETERMINE
      * WHETHER TO REPOSITION THE USER TO A PARTICULAR PLACE IN THE
      * SUBFILE (RESET=YES) OR TO PLACE THE USER AT THE BEGINNNING OF
      * THE SUBFILE (RESET=NO)
      *
     C                   MOVEL     NO            RESET
      *
      * RETRIEVE THE LOWEST SUBFILE RRN ON THE SCREEN FROM THE DSPF
      * FEEDBACK AREA (IDFLOW) AND MOVE IT TO A WORK FIELD.  THIS VALUE
      * WILL BE USED LATER TO CONTROL THE REPOSITIONING OF THE SUBFILE.
      *
     C                   Z-ADD     IDFLOW        WKFLOW
      *
      * CLEAR ALL MESSAGES
      *
     C                   EXSR      $CLMSG
      *
      * GET USER'S ENTRY AND RESET STANDARD RESPONSE
      *
     C                   SELECT
      *
     C     *IN03         WHENEQ    *ON                                          F3-EXIT
     C                   MOVEL     '03'          RTNCD
     C                   MOVEL     EXIT          ROUTNE
      *
     C     *IN04         WHENEQ    *ON                                          F4-PROMPT
     C                   EXSR      $F4S1
     C                   MOVEL     SCRN1         ROUTNE
      *
     C     *IN05         WHENEQ    *ON                                          F5-REFRESH
     C     MODE          IFEQ      REVISE
     C                   MOVEL     YES           RESET
     C                   ENDIF
     C                   MOVEL     SET1          ROUTNE
      *
     C     *IN06         WHENEQ    *ON                                          F6-CREATE
     C                   MOVEL     CREATE        MODE
     C                   MOVEL     CREATE        DFMODE
     C                   MOVEL     SET1          ROUTNE
      *
     C     *IN12         WHENEQ    *ON                                          F12-PREVIOUS
     C     MODE          IFEQ      REVISE
     C                   MOVEL     '12'          RTNCD
     C                   MOVEL     EXIT          ROUTNE
     C                   ELSE
     C                   MOVEL     REVISE        MODE
     C                   MOVEL     REVISE        DFMODE
     C                   MOVEL     SET1          ROUTNE
     C                   ENDIF
      *
     C     *IN25         WHENEQ    *ON                                          F25-ROLLUP
     C                   EXSR      $LOAD1
     C                   MOVEL     SCRN1         ROUTNE
     C                   OTHER
      *
      * USER PRESSED 'ENTER'
      *
     C     MODE          IFEQ      REVISE                                       IF NOTHING
     C     HDRRN1        ANDEQ     0
     C                   MOVEL     EXIT          ROUTNE
     C                   ELSE
     C                   MOVEL     EDIT1         ROUTNE
     C                   ENDIF                                                  IF NOTHING
      *
     C                   ENDSL                                                  F25-ROLLUP
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $EDIT1 - SUBROUTINE TO EDIT INPUT FIELDS FOR SCREEN 1
      *----------------------------------------------------------------
      *
     C     $EDIT1        BEGSR
      *
      * CLEAR CALLED PROGRAM FLAG
      *
     C                   MOVEL     NO            CALFLG
      *
      * FIRST, EDIT ALL THE SELECTIONS ENTERED BY THE USER IN THE
      * SUBFILE OPTION FIELD, TO SEE THAT VALID SELECTIONS WERE INPUT
      *
     C     MODE          IFEQ      REVISE
     C                   EXSR      $EDOP1
     C                   ENDIF
      *
      * IF THERE ARE NO ERRORS IN THE SUBFILE OPTIONS ENTERED
      * BY THE USER, THEN READ THE SELECTED SUBFILE RECORDS AND PROCESS
      *
     C     ERROR         IFEQ      0                                            IF NO ERROR
     C                   EXSR      $PROC1
     C                   ENDIF                                                  IF NO ERROR
      *
      * IF THERE ARE NO ERRORS,
      *    IF THE USER IS IN REVISE MODE, EXIT THE PROGRAM
      *    IF THE USER IS IN CREATE MODE, GO TO REVISE MODE
      *
     C     ERROR         IFEQ      0                                            IF STILL OK
      *
     C     MODE          IFEQ      CREATE                                       IF CREATE
     C                   MOVEL     REVISE        MODE
     C                   MOVEL     REVISE        DFMODE
     C                   MOVEL     SET1          ROUTNE
     C                   ELSE
      *
     C                   SELECT
     C     RTNCD         WHENEQ    '12'
     C                   Z-ADD     WKFLOW        HDPAG1
     C                   MOVEL     SCRN1         ROUTNE
      *
     C     RTNCD         WHENEQ    '03'
     C                   MOVEL     EXIT          ROUTNE
      *
      *
     C     CALFLG        WHENEQ    YES
     C                   Z-ADD     WKFLOW        HDPAG1
     C                   MOVEL     SCRN1         ROUTNE
      *
     C     DELFLG        WHENEQ    YES                                          IF DELETED
     C                   MOVEL     NO            DELFLG
     C                   MOVEL     YES           RESET
     C                   MOVEL     SET1          ROUTNE
     C                   OTHER
      *
     C                   MOVEL     EXIT          ROUTNE
     C                   ENDSL
      *
     C                   ENDIF                                                  IF CREATE
     C                   ELSE
      *
      * IF THERE ARE SUBFILE ERRORS, SET THE SCREEN TO REDISPLAY
      *
     C                   MOVEL     SCRN1         ROUTNE
     C                   ENDIF                                                  IF STILL OK
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * $CLR1  - SUBROUTINE TO CLEAR THE SUBFILE ON SCREEN 1
      *---------------------------------------------------------------
      *
     C     $CLR1         BEGSR
      *
     C                   Z-ADD     0             SVRRN1
      *
     C                   CLEAR                   HP153S1
     C                   SETON                                        83        SFLCLR
     C                   WRITE     HP153C1
     C                   SETOFF                                       818283
      *                                                    81=SFLDSP
      *                                                    82=SFLEND
      *                                                    83=SFLCLR
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * $FRSH1 - SUBROUTINE TO REFRESH/FILL THE SUBFILE FOR SCREEN 1
      *---------------------------------------------------------------
      *
     C     $FRSH1        BEGSR
      *
      * SET INDICATORS TO CONTROL DSPF DISPLAY
      *
     C     MODE          IFEQ      CREATE
     C                   SETON                                        61
     C                   ELSE
     C                   SETON                                        60
     C                   ENDIF
      *
      *  IF THE FARM SITE RECORD HAS BEEN DELETED OUT FROM UNDER
      *  THE USER, THEN SET *IN90 BACK ON TO CONTROL AVAILABLE FUNCTION
      *  KEYS AND INPUT FIELDS.  (*IN90 WAS SET OFF ABOVE WHEN THE
      *  SUBFILE WAS CLEARED)
      *
     C     NOTFLG        IFEQ      YES                                          IF NOT FOUND
     C                   SETON                                        90
     C                   ENDIF                                                  IF NOT FOUND
      *
      * SET POSITION IN DATABASE FILE HSP023-INSTALLED EQUIPMENT
      *
     C     K6P023        SETLL     HSP023
      *
      * THE FIELD 'RESET' IS A FLAG FIELD TO INDICATE WHETHER YOU ARE
      * ATTEMPTING TO REPOSITION THE USER CLOSE TO WHERE HE WAS IN THE
      * SUBFILE.
      * THE LOWEST SUBFILE RRN ON THE SCREEN FROM THE DSPF
      * FEEDBACK AREA (IDFLOW) WAS RETRIEVED EARLIER AND PUT IN WKFLOW.
      * ADD TO WKFLOW THE NUMBER OF LINES ON YOUR SUBFILE PAGE OR THE
      * NUMBER LESS 1 TO DETERMINE A SUBFILE RRN TO REPOSITION TO.
      * IF RESET IS 'NO', DON'T REPOSITION THE USER; JUST START OVER
      *
     C     RESET         IFEQ      YES
     C     WKFLOW        ADD       WKRLIN        MAXRRN
     C                   ELSE
     C                   Z-ADD     WKPLIN        MAXRRN
     C                   ENDIF
      *
      * LOAD THE SUBFILE UNTIL YOU HAVE THE REQUIRED NUMBER OF RECORDS
      * BY COMPARING THE HIDDEN RRN IN THE SUBFILE TO DESIRED MAXIMUM.
      * THE MAXIMUM WILL DEPEND ON WHETHER YOU ARE REPOSITIONING THE
      * SUBFILE OR NOT. STOP IF YOU GET TO THE END OF THE SUBFILE.
      *
     C     SVRRN1        DOUGE     MAXRRN
     C     *IN82         OREQ      *ON
     C                   EXSR      $LOAD1
     C                   ENDDO
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * $LOAD1- SUBROUTINE TO LOAD THE SUBFILE FOR SCREEN 1
      *---------------------------------------------------------------
      *
     C     $LOAD1        BEGSR
      *
     C                   Z-ADD     SVRRN1        HDRRN1
      *
      * SET THE COUNT OF RECORDS ON THE SUBFILE PAGE TO ZERO.
      * COUNT IS USED TO COUNT THE RECORDS WRITTEN TO THE SUBFILE.
      *
     C                   Z-ADD     0             COUNT             2 0
      *
      * FILL THE SUBFILE UNTIL THE END OF THE DATABASE FILE OR YOU
      * HAVE FILLED ALL THE LINES ON THE SUBFILE PAGE
      *
     C                   SETOFF                                       82
     C     *IN82         DOUEQ     *ON                                          DO FILL LOOP
     C     COUNT         ORGE      WKPLIN
      *
      * IF IN REVISE MODE, READ EQUIPMENT RECORDS FOR THE
      * FARM/BUILDING/ROOM PASSED IN AS A PARM.
      * PLACE AN 'N' IN POSITION 53 TO PREVENT RECORD LOCKING
      *
     C     MODE          IFEQ      REVISE
     C     K6P023        READE(N)  HSP023                                 82
     C                   ENDIF
      *
     C     *IN82         IFEQ      *OFF                                          IF FOUND
      *
     C                   ADD       1             COUNT
     C                   ADD       1             HDRRN1
     C                   MOVE      *BLANK        DFOPT
     C                   MOVE      *BLANK        DFACT
      *
      * IF IN REVISE MODE, FILL SUBFILE FIELDS FROM DATABASE FILE
      * AND RETRIEVE EQUIPMENT DESCRIPTION FROM EQUIPMENT MASTER
      *
     C     MODE          IFEQ      REVISE
     C                   MOVEL     IEEQCD        DFEQCD
     C                   MOVEL     IEETCD        DFETCD
     C                   EXSR      $EQCD
     C                   ELSE
      *
      * IF IN CREATE MODE, INITIALIZE SUBFILE FIELDS.
      *
     C                   MOVE      *BLANK        DFEQCD
     C                   MOVE      *BLANK        DFEQDS
     C                   MOVE      *BLANK        DFETCD
     C                   ENDIF
      *
     C                   WRITE     HP153S1
     C                   SETON                                        81
      *
      * TO CONTROL THE PAGE OF THE SUBFILE TO DISPLAY, SET THE VALUE
      * FOR THE DSPF SFLRCDNBR FIELD TO BE EQUAL TO THE SFL HIDDEN RRN
      *
     C                   Z-ADD     HDRRN1        HDPAG1
      *
     C                   ENDIF                                                  IF FOUND
     C                   ENDDO                                                  DO FILL LOOP
      *
      * IF NO RECORDS WERE WRITTEN TO THE SUBFILE, THEN ISSUE
      * THE STANDARD ERROR MESSAGE OF NO DATA FOUND.
      *
     C     HDRRN1        IFEQ      0
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS09002'     MGID(E)
     C                   ENDIF
     C                   ENDIF
      *
     C                   Z-ADD     HDRRN1        SVRRN1
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * $EDOP1 - SUBROUTINE TO READ CHANGED RECORDS IN SUBFILE AND
      *          EDIT FOR VALID SUBFILE SELECTOR OPTIONS ON SCREEN 1
      *---------------------------------------------------------------
      *
     C     $EDOP1        BEGSR
      *
      * READ ALL THE CHANGED SUBFILE RECORDS EDITING FOR VALID SUBFILE
      * SELECTOR OPTIONS
      *
     C                   SETOFF                                       80
     C     *IN80         DOUEQ     *ON                                          DO READ C
     C                   READC     HP153S1                                80
     C     *IN80         IFEQ      *OFF                                         IF REC READ
     C                   SETON                                        85
      *
      * SET OFF ERROR INDICATOR FOR SUBFILE SELECTOR
      *
     C                   SETOFF                                       30
      *
      * CONVERT OPTION USER ENTERED TO STANDARD FORMAT. FOR EXAMPLE,
      * A ' 1', '01', AND '1 ' MUST BE STANDARDIZED TO ' 1'.
      *
     C     WKOPT2        IFEQ      *BLANK                                       IF 2 = BLANK
     C                   MOVE      WKOPT1        WKOPT2
     C                   MOVE      *BLANKS       WKOPT1
     C                   ENDIF                                                  IF 2 = BLANK
      *
     C     WKOPT1        IFEQ      *ZEROS                                       IF 1 IS ZERO
     C                   MOVE      *BLANKS       WKOPT1
     C                   ENDIF                                                  IF 1 IS ZERO
      *
      * TEST FOR VALID SUBFILE OPTION ENTRY ON ALL CHANGED RECORDS
      *
     C     DFOPT         IFNE      ' 4'
     C     DFOPT         ANDNE     '  '
     C                   SETON                                        30
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS09006'     MGID(E)
     C                   ENDIF
     C                   EXSR      $ERR1
     C                   ENDIF
      *
     C                   EXSR      $IND
     C                   UPDATE    HP153S1
      *
      * SET OFF ERROR INDICATOR FOR SUBFILE SELECTOR
      *
     C                   SETOFF                                       30
     C                   ENDIF                                                  IF REC READ
     C                   ENDDO                                                  DO READ C
      * RESET MDT
     C                   SETOFF                                       85
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * $PROC1 - SUBROUTINE TO READ AND EDIT CHANGED SUBFILE RECORDS
      *          FOR SCREEN 1
      *---------------------------------------------------------------
      *
     C     $PROC1        BEGSR
      *
      * READ ALL THE CHANGED SUBFILE RECORDS PROCESSING THE SUBFILE
      * SELECTION
      *
     C                   SETOFF                                       80
     C     *IN80         DOUEQ     *ON                                          DO PROCESS
     C                   READC     HP153S1                                80
     C     *IN80         IFEQ      *OFF                                         IF REC READ
     C                   SETON                                        85
      *
      * SET OFF THE ERROR INDICATORS
      *
     C                   SETOFF                                       303151
      *
     C     RTNCD         IFEQ      '00'                                         IF ENTER
     C                   MOVEL     NO            ERRFLG
      *
      *
      * IF CREATE MODE AND THE RECORD HAS NOT ALREADY
      * BEEN CREATED
      *
      *
     C                   SELECT
     C     MODE          WHENEQ    CREATE
     C     DFACT         ANDEQ     *BLANK
     C     DFEQCD        IFEQ      *BLANK
     C                   MOVE      *BLANK        DFEQDS
     C                   MOVE      *BLANK        DFETCD
     C                   ELSE
     C                   EXSR      $EDCR1
     C                   ENDIF
      *
      *
      * IF REVISE MODE AND RECORD HAS NOT ALREADY BEEN
      * DELETED.
      *
     C     MODE          WHENEQ    REVISE
     C     DFACT         ANDEQ     *BLANK
     C                   EXSR      $EDRV1
     C                   ENDSL
      *
      * NOW, IF THE USER HAS ENTERED A SUBFILE OPTION, GO PERFORM
      * THAT PROCESSING IF THERE ARE NO ERRORS.
      *
     C                   MOVEL     NO            CALFLG
      *
     C     ERRFLG        IFEQ      NO                                           IF DO CALLS
     C                   MOVE      *BLANKS       DFOPT
     C                   SETOFF                                       85
     C                   ENDIF
     C                   ENDIF                                                  RTNCD = 00
      *
      *  IF THERE WERE ANY ERRORS, SET ON THE SFLNXTCHG INDICATOR
      *
     C                   EXSR      $IND
     C                   UPDATE    HP153S1
      *
      * SET OFF THE ERROR INDICATORS
      *
     C                   SETOFF                                       303151
      *
     C                   ENDIF                                                  IF REC READ
     C                   ENDDO                                                  DO PROCESS
      * SET MDT
     C                   SETOFF                                       85
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $EQCD - RETRIEVE/VALIDATE EQUIPMENT CODE IN EQUIPMENT FILE
      *----------------------------------------------------------------
      *
     C     $EQCD         BEGSR
      *
     C     DFEQCD        CHAIN     HSP022                             91
     C     *IN91         IFEQ      *OFF
     C                   MOVEL(P)  EQEQDS        DFEQDS
     C                   MOVEL(P)  EQETCD        DFETCD
     C                   ELSE
     C                   MOVEL(P)  'UNKNOWN'     DFEQDS
     C                   ENDIF
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $EDCR1 - SUBROUTINE TO EDIT/WRITE RECORDS TO DATABASE FILE
      *          FOR SCREEN 1
      *----------------------------------------------------------------
      *
     C     $EDCR1        BEGSR
      *
      * VALIDATE THE EQUIPMENT CODE AND RETRIEVE DESCRIPTION
      *
     C                   EXSR      $EQCD
      *
      * IF EQUIPMENT CODE IS INVALID
      *
     C     *IN91         IFEQ      *ON                                          IF INVALID
     C                   SETON                                        31
     C                   MOVEL     YES           ERRFLG
     C     ERROR         IFLT      MAXMSG                                       IF NOT MAX
     C                   ADD       1             ERROR
     C                   MOVEL     'HS00106'     MGID(E)
     C                   CLEAR                   MGW
     C                   MOVEA     DFEQCD        MGW(1)
     C                   MOVEA     MGW           MGDT(E)
     C                   ENDIF                                                  IF NOT MAX
     C                   EXSR      $ERR1
     C                   ENDIF                                                  IF INVALID
      *
      * CHECK THAT THE USER HAS NOT ENTERED THE SAME PIECE OF
      * EQUIPMENT TWICE.
      *
     C     K5P023        CHAIN(N)  HSP023                             94
     C     *IN94         IFEQ      *OFF                                         IF DUP EQUIP
     C                   SETON                                        31
     C                   MOVEL     YES           ERRFLG
      *
     C     ERROR         IFLT      MAXMSG                                       IF MORE OK
     C                   ADD       1             ERROR
     C                   CLEAR                   MGW
      *
     C     C1RMCD        IFEQ      *BLANK                                       IF NO ROOM
     C                   MOVEL     'HS00127'     MGID(E)
     C                   MOVEA     DFEQCD        MGW(1)
     C                   MOVEA     C1BLCD        MGW(6)
     C                   MOVE      C1FSCD        XXFSCD
     C                   MOVEA     XXFSCD        MGW(11)
     C                   MOVEA     MGW           MGDT(E)
     C                   ELSE
     C                   MOVEL     'HS00125'     MGID(E)
     C                   MOVEA     DFEQCD        MGW(1)
     C                   MOVEA     C1RMCD        MGW(6)
     C                   MOVEA     C1BLCD        MGW(11)
     C                   MOVEA     MGW           MGDT(E)
     C                   ENDIF                                                  IF NO ROOM
     C                   ENDIF                                                  IF MORE OK
      *
     C                   EXSR      $ERR1
     C                   ELSE
      *
      * THE USER CANNOT ENTER MORE THAN 1 PIECE OF EACH TYPE OF
      * EQUIPMENT. CHECK FOR EXISTING PIECES OF EQUIPMENT OF THIS TYPE
      *
     C     K4P023        CHAIN(N)  HSP023                             92
     C     *IN92         IFEQ      *OFF                                         IF EXISTS
     C                   SETON                                        31
     C                   MOVEL     YES           ERRFLG
      *
     C     ERROR         IFLT      MAXMSG                                       IF LT MAX
     C                   ADD       1             ERROR
     C                   CLEAR                   MGW
      *
     C     C1RMCD        IFEQ      *BLANK                                       IF NO ROOM
     C                   MOVEL     'HS00128'     MGID(E)
     C                   MOVEA     DFETCD        MGW(1)
     C                   MOVEA     C1BLCD        MGW(6)
     C                   MOVE      C1FSCD        XXFSCD
     C                   MOVEA     XXFSCD        MGW(11)
     C                   MOVEA     MGW           MGDT(E)
     C                   ELSE
     C                   MOVEL     'HS00126'     MGID(E)
     C                   MOVEA     DFETCD        MGW(1)
     C                   MOVEA     C1RMCD        MGW(6)
     C                   MOVEA     C1BLCD        MGW(11)
     C                   MOVEA     MGW           MGDT(E)
     C                   ENDIF                                                  IF NO ROOM
     C                   EXSR      $ERR1
     C                   ENDIF                                                  IF LT MAX
     C                   ENDIF                                                  IF EXISTS
     C                   ENDIF                                                  IF DUP EQUIP
      *
      * WHEN A RECORD IS CREATED, THE CONSTANT '*CREATED' IS MOVED TO
      * A SUBFILE FIELD.  THIS IS DONE SO THAT IF SOME SUBFILE
      * RECORDS HAVE ERRORS AND THE SCREEN IS REDISPLAYED WITH ERROR
      * MESSAGES, THE USER WILL KNOW THAT THIS RECORD HAS BEEN CREATED
      * SUCCESSFULLY.  SETTING ON *IN62 WILL PROTECT THE FIELD.
      *
     C     ERRFLG        IFEQ      NO                                           IF NO ERROR
     C                   Z-ADD     C1FSCD        IEFSCD
     C                   MOVEL     C1BLCD        IEBLCD
     C                   MOVEL     C1RMCD        IERMCD
     C                   MOVEL     DFEQCD        IEEQCD
     C                   MOVEL     DFETCD        IEETCD
     C                   WRITE     IEREC
     C                   MOVEL     '*CREATED'    DFACT
     C                   SETON                                        62
     C                   ELSE
     C                   MOVE      *BLANK        DFACT
     C                   SETOFF                                       62
     C                   ENDIF                                                  IF NO ERROR
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $EDRV1 - SUBROUTINE TO EDIT/UPDATE/DELETE A DATABASE RECORD
      *          FOR SCREEN 1
      *----------------------------------------------------------------
      *
     C     $EDRV1        BEGSR
      *
      * RETRIEVE RECORD AND LOCK BEFORE UPDATE/DELETE
      *
     C     ERRFLG        IFEQ      NO                                           IF NO ERROR
     C     K5P023        CHAIN     HSP023                             94
      *
      * WHEN A RECORD IS DELETED, THE CONSTANT '*DELETED' IS MOVED TO
      * THE SUBFILE FIELD.  THIS IS DONE SO THAT IF SOME SUBFILE
      * RECORDS HAVE ERRORS AND THE SCREEN IS REDISPLAYED WITH ERROR
      * MESSAGES, THE USER WILL KNOW THAT THIS RECORD HAS BEEN DELETED
      * SUCCESSFULLY.  *IN62 WILL PROTECT THE FIELDS.
      *
     C     *IN94         IFEQ      *OFF                                         IF FOUND
     C     DFOPT         IFEQ      ' 4'                                         IF DEL
     C                   MOVEL     YES           DELFLG
     C                   DELETE    IEREC
      *
     C                   MOVE      *BLANK        DFOPT
     C                   MOVEL     '*DELETED'    DFACT
     C                   SETON                                        62
     C                   ELSE
      * NOT OPTION '4'
     C                   MOVE      *BLANK        DFACT
     C                   SETOFF                                       62
     C                   UPDATE    IEREC
     C                   ENDIF                                                  IF DEL
     C                   ELSE
      *
      * THE RECORD HAS BEEN DELETED SINCE THIS PROGRAM RETRIEVED IT,
      * SO ISSUE THE STANDARD ERROR MESSAGE.
      *
     C     DFOPT         IFEQ      *BLANK
     C                   SETON                                        31
     C                   ELSE
     C                   SETON                                        30
     C                   ENDIF
      *
     C                   MOVEL     YES           ERRFLG
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS09008'     MGID(E)
     C                   ENDIF
     C                   EXSR      $ERR1
     C                   ENDIF                                                  IF FOUND
     C                   ENDIF                                                  IF NO ERROR
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * $F4S1 - F4 PROMPT ON SCREEN 1                                    *
      *---------------------------------------------------------------
      *
     C     $F4S1         BEGSR
      *
      *
      * SET OFF PROMPT INDICATORS IN ALL SUBFILE RECORDS
      *
     C                   EXSR      $OFFIN
      *
     C                   SELECT
      *
      * IF EQUIPMENT CODE
      *
     C     FLD           WHENEQ    'DFEQCD'
      *
     C                   EXSR      $RRN1
     C     *IN97         IFEQ      *OFF                                         IF DFEQCD HIT
      *
     C     DFACT         IFEQ      *BLANK                                       IF NO PROTECT
      *
     C                   CALL      'HP502'
     C                   PARM      *BLANK        XXEQCD
     C                   PARM      *BLANK        XXEQDS
     C                   PARM      *BLANK        XXETCD
     C     XXEQCD        IFNE      *BLANK                                       IF SELECTED
     C                   MOVEL(P)  XXEQCD        DFEQCD
     C                   MOVEL(P)  XXEQDS        DFEQDS
     C                   MOVEL(P)  XXETCD        DFETCD
     C                   ENDIF                                                  IF SELECTED
     C                   ELSE
      *
      * THE FIELD IS PROTECTED BECAUSE THE RECORD HAS
      * ALREADY BEEN CREATED OR DELETED.
      *
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS09011'     MGID(E)
     C                   ENDIF
     C                   ENDIF                                                  IF NO  PROTECT
      *
      * RESET SUBFILE RECORD INDICATORS AND UPDATE SUBFILE
      * RECORD.
      *
     C                   EXSR      $IND
     C                   SETON                                        8551
     C                   UPDATE    HP153S1
     C                   SETOFF                                       8551
      *
      * RESET THE SUBFILE RELATIVE RECORD NUMBER TO WHAT IT WAS BEFORE
      * THE USER TOOK F4 (IT WILL BE OUT-OF-SYNCH IF THE USER HAS
      * SELECTED A RECORD THUS CAUSING THE SUBFILE RECORD TO BE
      * RETRIEVED AND UPDATED.)  BUT, SET THE PAGE OF THE SUBFILE TO
      * BE DISPLAYED TO THE PAGE ON WHICH THE USER TOOK F4.
      *
     C                   Z-ADD     SVRRN1        HDRRN1
     C                   Z-ADD     HDRR1         HDPAG1
     C                   ENDIF                                                  IF DFEQCD HIT
      *
     C                   OTHER
      *
      * F4 NOT VALID FOR THIS FIELD SO ISSUE MESSAGE
      *
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR                                        013
     C                   MOVEL     'HS09011'     MGID(E)
     C                   ENDIF
      *
     C                   ENDSL
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * $OFFIN - REWRITE THE SUBFILE RECORDS WITH THE INDICATORS OFF
      *---------------------------------------------------------------
      *
      * THIS SUBROUTINE KEEPS THE CURSOR FROM BEING ON A PREVIOUSLY
      * PROMPTED RECORD IN THE SUBFILE.
      *
     C     $OFFIN        BEGSR
      *
      * READ/PROCESS ALL THE CHANGED SUBFILE RECORDS
      *
     C                   SETOFF                                       80
     C     *IN80         DOUEQ     *ON                                          DO PROCESS
     C                   READC     HP153S1                                80
     C     *IN80         IFEQ      *OFF                                         IF REC READ
      *
      * SETON MDT
     C                   SETON                                        85
      *
      * SET OFF THE ERROR INDICATORS IN THE SUBFILE
      * AND THEN RESET INDICATORS
      *
     C                   SETOFF                                       51
     C                   EXSR      $IND
     C                   UPDATE    HP153S1
     C                   ENDIF                                                  IF REC READ
     C                   ENDDO                                                  DO PROCESS
      * SET MDT
     C                   SETOFF                                       85
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * $IND - RESET INDICATORS IN SUBFILE
      *---------------------------------------------------------------
      *
      * RESET INDICTORS FOR SUBFILE RECORDS
      *
     C     $IND          BEGSR
      *
      *  IF THE RECORD HAS BEEN DELETED OUT FROM UNDER THE USER,
      *  THEN SET *IN90 BACK ON TO CONTROL AVAILABLE FUNCTION
      *  KEYS AND INPUT FIELDS.
      *
     C                   SETOFF                                       90
     C     NOTFLG        IFEQ      YES                                          IF NOT FOUND
     C                   SETON                                        90
     C                   ENDIF                                                  IF NOT FOUND
      *
      * IF THE SUBFILE RECORD HAS ALREADY BEEN
      * CREATED OR DELETED.
      *
     C                   SETOFF                                       62
     C     DFACT         IFNE      *BLANK
     C                   SETON                                        62
     C                   ENDIF
      *
      *
     C                   SETOFF                                       6061
     C     MODE          IFEQ      CREATE
     C                   SETON                                        61
     C                   ELSE
     C                   SETON                                        60
     C                   ENDIF
      *
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * $ERR1  - SUBROUTINE TO SET THE SUBFILE TO REDISPLAY AT
      *          THE FIRST SCREEN THAT HAS ERRORS ON SCREEN 1
      *---------------------------------------------------------------
      *
     C     $ERR1         BEGSR
      *
     C     ERROR         IFEQ      1
     C                   Z-ADD     HDRRN1        HDPAG1
     C                   ENDIF
      *
     C                   ENDSR
      *
      *---------------------------------------------------------------
      * $EXIT - SUBROUTINE TO SET ON LR AND END JOB
      *---------------------------------------------------------------
      *
     C     $EXIT         BEGSR
     C                   SETON                                        LR
     C                   ENDSR
      *
      *---------------------------------------------------------------
      * $WRMSG - SUBROUTINE TO WRITE ERROR MESSAGES
      *---------------------------------------------------------------
      *
     C     $WRMSG        BEGSR
      *
     C                   CALL      'UT80045J'                           43
     C                   PARM                    MGID
     C                   PARM                    MGDT
     C                   PARM                    ERROR
     C                   PARM      SDPGM         PGM
     C                   PARM      MSGFIL        MSGFL
      *
      * IF CALL TO UT80045J FAILED, A MESSAGE HARDCODED IN THE DSPF
      * COMMAND KEY RECORD WILL BE SET ON. SO, REDISPLAY SCREEN TO USER
      *
     C     *IN43         IFEQ      *ON
     C                   MOVEL     SET1          ROUTNE
     C                   ENDIF
      *
     C                   ENDSR
      *
      *----------------------------------------------------------------
      * $CLMSG - SUBROUTINE TO CLEAR MESSAGES
      *----------------------------------------------------------------
      *
     C     $CLMSG        BEGSR
      *
     C                   CALL      'UT80045J'                           43
     C                   PARM      *BLANKS       MGI
     C                   PARM      *BLANKS       MGD
     C                   PARM      *ZEROS        ERROR
     C                   PARM      SDPGM         PGM
     C                   PARM      MSGFIL        MSGFL
      *
      * IF CALL TO UT80045J FAILED, A MESSAGE HARDCODED IN THE DSPF
      * COMMAND KEY RECORD WILL BE SET ON. SO, REDISPLAY SCREEN TO USER
      *
     C     *IN43         IFEQ      *ON
     C                   MOVEL     SET1          ROUTNE
     C                   ENDIF
      *
     C                   ENDSR
      /EJECT
      ****************************************************************
      * $RRN1  - ROUTINE TO FIND/RETRIEVE THE SUBFILE RECORD TO UPDATE
      *          WHEN RETURNING FROM A SELECT PROGRAM ON SCREEN 1
      ****************************************************************
      *
      * HDRR1 IS THE HIDDEN FIELD ASSOCIATED WITH THE KEYWORD SFLCSRRRN
      * ON THE CONTROL RECORD IN THE DSPF.
      * HDRRN1 IS THE VALUE ASSOCIATED WITH THE KEYWORD 'KSFILE' IN
      * THE F-SPECS FOR THE WORKSTATION.
      *
     C     $RRN1         BEGSR
      *
     C                   SETOFF                                       97
      *
     C     HDRR1         IFNE      0
     C                   Z-ADD     HDRR1         HDRRN1
     C     HDRRN1        CHAIN     HP153S1                            97
     C                   ENDIF
      *
     C                   ENDSR                                                                016
      /EJECT
      *---------------------------------------------------------------
      * *INZSR - INITIALIZATION SUBROUTINE
      *---------------------------------------------------------------
      *
     C     *INZSR        BEGSR
      *
      * KEY FOR FILE HSP019-FARM BUILDING
      *
     C     K1P019        KLIST
     C                   KFLD                    C1FSCD
     C                   KFLD                    C1BLCD
      *
      * KEY FOR FILE HSP020-ROOMS
      *
     C     K3P020        KLIST
     C                   KFLD                    C1FSCD
     C                   KFLD                    C1BLCD
     C                   KFLD                    C1RMCD
      *
      * KEY FOR FILE HSP023-INSTALLED EQUIPEMNT
      *
     C     K4P023        KLIST
     C                   KFLD                    C1FSCD
     C                   KFLD                    C1BLCD
     C                   KFLD                    C1RMCD
     C                   KFLD                    DFETCD
      *
      * KEY FOR FILE HSP023-INSTALLED EQUIPEMNT
      *
     C     K5P023        KLIST
     C                   KFLD                    C1FSCD
     C                   KFLD                    C1BLCD
     C                   KFLD                    C1RMCD
     C                   KFLD                    DFETCD
     C                   KFLD                    DFEQCD
      *
      * KEY FOR FILE HSP023-INSTALLED EQUIPEMNT
      *
     C     K6P023        KLIST
     C                   KFLD                    C1FSCD
     C                   KFLD                    C1BLCD
     C                   KFLD                    C1RMCD
      *
      * PARM LISTS
      *
     C     *ENTRY        PLIST
     C                   PARM                    C1FSCD
     C                   PARM                    C1FSNM
     C                   PARM                    C1BLCD
     C                   PARM                    C1BLDS
     C                   PARM                    C1RMCD
     C                   PARM                    RTNCD
      *
      * GLOBAL DEFINES
      *
     C                   MOVEL     *BLANK        PGM              10            USED IN MSG
     C                   MOVEL     *BLANK        MSGFL            10            USED IN MSG
     C                   MOVEL     *BLANK        ROUTNE            6            ROUTINE
     C                   Z-ADD     0             MAXRRN            4 0          REPOSITION
     C                   MOVEL     NO            RESET             1            REPOSITION
     C                   MOVEL     NO            ERRFLG            1
     C                   MOVEL     NO            NOTFLG            1
     C                   MOVEL     NO            DELFLG            1
     C                   MOVEL     NO            CALFLG            1
     C     *LIKE         DEFINE    DFMODE        MODE                           SFL POSITION
     C     *LIKE         DEFINE    IDFLOW        WKFLOW                         SFL POSITION
     C                   MOVEL     '00'          RTNCD             2            RETURN CODE
     C     *LIKE         DEFINE    HDRRN1        SVRRN1
      *
      * PARM FIELDS USED IN CALLS AND MESSAGES
      *
     C                   MOVE      *BLANK        XXFSCD            5
     C     *LIKE         DEFINE    DFEQCD        XXEQCD
     C     *LIKE         DEFINE    DFEQDS        XXEQDS
     C     *LIKE         DEFINE    DFETCD        XXETCD
      *
      * TO SUPPORT F5-REFRESH, LOGIC REQUIRES THE NUMBER OF LINES ON
      * THE SUBFILE PAGE AND ALSO THE NUMBER OF LINES LESS 1.  BY
      * SETTING THESE VALUES UP AS CONSTANT WORK FIELDS, THE PROCESSING
      * LOGIC CAN BE STANDARDIZED BETWEEN PROGRAMS.
      *
     C                   Z-ADD     11            WKPLIN            2 0          SFLPAG(0011)
     C     WKPLIN        SUB       1             WKRLIN            2 0          SFLPAG - 1
      *
      * ALWAYS BRING THE SCREEN UP IN REVISE MODE--EVEN IF THERE ARE
      * NOT ANY RECORDS TO REVISE.
      *
     C                   MOVEL     REVISE        MODE
     C                   MOVEL     REVISE        DFMODE
     C                   SETON                                        62
      *
      * THE FOLLOWING STANDARD CODE MUST BE INCLUDED TO MAKE THE
      * STANDARD ERROR MESSAGE HANDLING PROGRAM FUNCTION PROPERLY.
      * THIS CODE SETS AND CLEARS THE PROGRAM MESSAGE QUEUE.
      *
     C                   Z-ADD     20            MAXMSG            2 0
     C                   MOVEL     '*'           MSGPGM                         SET QUEUE
     C                   EXSR      $CLMSG                                       CLEAR MSG
      *
     C                   ENDSR
      /EJECT
