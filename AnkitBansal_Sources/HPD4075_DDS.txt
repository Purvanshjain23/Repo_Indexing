     A*%%TS  SD  20001107  141500  ISLRAMS     REL-V4R4M0  5769-PW1
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD4075
     A*  TITLE:         SPECIFY MOVEMENT VERIFICATION REPORT OPTIONS
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  DATE:          11/06/00
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A N99                                  CF04(04 'PROMPT')
     A N99 44                               CF08(08 'SUBMIT')
     A N99                                  CF17(17 'PRINT OPTIONS')
     A N99                                  CF20(20 'WRKSPLF')
     A*
     A*----------------------------------------------------------------
     A* RECORD FORMAT ONE
     A*----------------------------------------------------------------
     A          R HP4075R1
     A*%%TS  SD  20001106  133106  ISLRAMS     REL-V4R4M0  5769-PW1
     A                                      SETOF(30)
     A                                      SETOF(31)
     A                                      SETOF(32)
     A                                      SETOF(33)
     A                                      SETOF(34)
     A                                      SETOF(35)
     A                                      SETOF(36)
     A                                      SETOF(37)
     A                                      SETOF(38)
     A                                      SETOF(39)
     A                                      SETOF(40)
     A                                      SETOF(41)
     A                                      SETOF(51)
     A                                      SETOF(52)
     A                                      SETOF(53)
     A                                      SETOF(54)
     A                                      SETOF(56)
     A                                      SETOF(57)
     A                                      SETOF(58)
     A                                      SETOF(59)
     A                                      OVERLAY
     A                                      RTNCSRLOC(&RCD &FLD)
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 27'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  2 14'SPECIFY MOVEMENT VERIFICATION LIST-
     A                                      ING OPTIONS'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  4  2'From date .....................'
     A            D1FMDY    R     Y  B  4 36REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A N30                                  DSPATR(HI)
     A  30                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A                                  5  2'To date .......................'
     A            D1TMDY    R     Y  B  5 36REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A N31                                  DSPATR(HI)
     A  31                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A                                  7  2'The following fields are optional.-
     A                                        Leave them blank for All.'
     A                                      DSPATR(HI)
     A                                  9  2'Origin business office ........'
     A                                  9 34'?'
     A            D1ORFSBO  R        B  9 36REFFLD(BOBOBO)
     A N32                                  DSPATR(HI)
     A  32
     AO 52                                  DSPATR(PC)
     A  32                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A            D1ORBODS  R        O  9 46REFFLD(BOBODS)
     A                                 10  2'Origin production type ........'
     A                                 10 34'?'
     A            D1ORPTCD  R        B 10 36REFFLD(@@PTCD)
     A N33                                  DSPATR(HI)
     A  33
     AO 53                                  DSPATR(PC)
     A  33                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A            D1ORPTDS  R        O 10 46REFFLD(PTPTDS)
     A                                 11  2'Origin production phase .......'
     A                                 11 34'?'
     A            D1ORPPCD  R        B 11 36REFFLD(@@PPCD)
     A N34                                  DSPATR(HI)
     A  34
     AO 54                                  DSPATR(PC)
     A  34                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A            D1ORPPDS  R        O 11 46REFFLD(PPPPDS)
     A                                 12  2'Origin cell ...................'
     A            D1ORCELL  R        B 12 36REFFLD(HSRREF/@@CELL *LIBL/HSPREF)
     A                                      EDTCDE(M)
     A N35                                  DSPATR(HI)
     A  35                                  DSPATR(PC)
     A  35                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A                                 13  2'Origin farm site ..............'
     A                                 13 34'?'
     A            D1ORFSCD  R        B 13 36REFFLD(@@FSCD)
     A                                      EDTCDE(M)
     A N36                                  DSPATR(HI)
     A  36
     AO 56                                  DSPATR(PC)
     A  36                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A            D1ORFSNM  R        O 13 46REFFLD(FSFSNM)
     A                                 16  2'Destination business office ...'
     A                                 16 34'?'
     A            D1DNFSBO  R        B 16 36REFFLD(BOBOBO)
     A N37                                  DSPATR(HI)
     A  37
     AO 57                                  DSPATR(PC)
     A  37                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A            D1DNBODS  R        O 16 46REFFLD(BOBODS)
     A                                 17  2'Destination production type ...'
     A                                 17 34'?'
     A            D1DNPTCD  R        B 17 36REFFLD(@@PTCD)
     A N38                                  DSPATR(HI)
     A  38
     AO 58                                  DSPATR(PC)
     A  38                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A            D1DNPTDS  R        O 17 46REFFLD(PTPTDS)
     A                                 18  2'Destination production phase ..'
     A                                 18 34'?'
     A            D1DNPPCD  R        B 18 36REFFLD(@@PPCD)
     A N39                                  DSPATR(HI)
     A  39
     AO 59                                  DSPATR(PC)
     A  39                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A            D1DNPPDS  R        O 18 46REFFLD(PPPPDS)
     A                                 19  2'Destination cell ..............'
     A            D1DNCELL  R        B 19 36REFFLD(HSRREF/@@CELL *LIBL/HSPREF)
     A                                      EDTCDE(M)
     A N40                                  DSPATR(HI)
     A  40                                  DSPATR(PC)
     A  40                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A                                 20  2'Destination farm site .........'
     A                                 20 34'?'
     A            D1DNFSCD  R        B 20 36REFFLD(@@FSCD)
     A                                      EDTCDE(M)
     A N41                                  DSPATR(HI)
     A  41
     AO 51                                  DSPATR(PC)
     A  41                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A            D1DNFSNM  R        O 20 46REFFLD(FSFSNM)
     A*
     A*----------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*----------------------------------------------------------------
     A          R HP4075K1
     A*%%TS  SD  20001107  141500  ISLRAMS     REL-V4R4M0  5769-PW1
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A N99                             23 11'F4=Prompt'
     A  44                             23 22'F8=Submit'
     A N99                             23 33'F17=Print Options'
     A N99                             23 52'F20=Spool Files'
      *
      *----------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *----------------------------------------------------------------
     A          R HP4075ES                   SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
     A*
     A*----------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*----------------------------------------------------------------
     A*
     A          R HP4075EC                   SFLCTL(HP4075ES)
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
