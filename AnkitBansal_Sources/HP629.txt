      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Hog Production
      * PROGRAM:     HP629 - Open Group Cost of Sales Report
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     12/28/00
      *
      *
      * FUNCTION: This report was requested by the controller Jeff Sherbondy.
      *
      *           It is basically a clone of HP624-Closing Period Cost Report
      *           which prints the supporting data from the period cost file that
      *           was used in arriving at the values in the "COS" record for each
      *           farm in the End of Period file.
      *
      *           The differences that Jeff Sherbondy demanded in this report are:
      *           1) Print whole numbers without decimals
      *           2) Sort by phase with phase and report totals
      *           3) Only included groups that are not closed...but show all dollars
      *              including prior period amounts
      *           4) Omit transfer dollars
      *
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 08/02/01  LeAnne Fedor
      *           We will only print records that have values for a) head or b) amount.
      *           Also, added Group Type to detail line.
      *
      * 08/23/01  LeAnne Fedor
      *           They now want dollars for prior periods as well as  current periods.
      *
      * 07/06/09  LeAnne Ramsey
      *           Recompile only. Added new field 'Continuous Flow Flag' to Hog Group file.
      *
      * 10/16/13  LeAnne Ramsey (E2831)
      *           Recompile only. Added field 'MTech Reference'.
      *
      * 01/21/21  ISE            TSN593
      *           Replaced *loval with *start
      *
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
      *
     Fhsp011    if   e           k disk
      *    Production phase
      *
      *
     Fhsp018    if   e           k disk
      *    Farm site
      *
      *
     Fhsp034    if   e           k disk
      *    Hog group
      *    (records selected/keyed in opnqry; no BGF's)
      *
     Fhsp055    if   e           k disk
      *    Period costs for hogs moved out
      *
      *
     Fqprint    o    f  198        printer oflind(*inof)
      *
      /EJECT
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      *---------------------------------------------------------------
      *  NAMED CONSTANTS
      *---------------------------------------------------------------
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      *
      ****************************************************************
      * ARRAYS AND TABLES
      ****************************************************************
      *
      * Define arrays for 3 total sales lines (farm, phase, report)
      *
     D arppsam         s             13  0 dim(3)                               Prior period
     D arcpsam         s             13  0 dim(3)                               Current period
     D arsam           s             13  0 dim(3)                               Amount
     D arslvlb         s             15  2 dim(3)                               Pounds
     D arscrlb         s             15  2 dim(3)                               Carcass pounds
     D arslvhd         s             15  0 dim(3)                               Head
      *
      * Define array to hold description for 3 total lines (farm, phase, report)
      *
     D ardesc          s             13    dim(3)
      *
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Array index
     D x               s              1  0
      *
      * Control and save fields
      *
     D first           s              1    inz('Y')
     D svfscd          s                   like(pcfscd)
     D svppcd          s                   like(pcppcd)
      *
      *
      * Print fields
      *
     D rtime           s              6  0
     D dash            s            198    inz(*all'-')
      *
     D h1ppds          s                   like(ppppds)
     D h2fsnm          s                   like(fsfsnm)
      *
     D d1hgcd          s                   like(hghgcd)
     D d1gtcd          s                   like(hggtcd)
     D d1slvhd         s                   like(pcslvhd)
     D d1slvlb         s                   like(t1slvlb)
     D d1scrlb         s                   like(t1scrlb)
     D d1ppsam         s                   like(t1sam)
     D d1cpsam         s                   like(t1sam)
     D d1sam           s                   like(t1sam)
     D d1hdrt          s                   like(pchdrt)
     D d1lbrt          s                   like(pclbrt)
      *
     D t1ppsam         s             13  0
     D t1cpsam         s             13  0
     D t1sam           s             13  0
     D t1slvlb         s             15  0
     D t1scrlb         s             15  0
     D t1slvhd         s             15  0
      *
      *
      * Work fields
      *
     D wkcpsam         s             15  2
     D wkppsam         s             15  2
     D wksam           s             15  2
     D wkscrlb         s             15  4
     D wkslvlb         s             15  4
     D wkslvhd         s             15  0
      *
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *---------------------------------------------------------------
      *  LDA
      *---------------------------------------------------------------
      *
     Dlda             uds                  dtaara(*lda)
     D  ldper                 17     18  0
     D  ldccyy                20     23  0
     D  ldbmdy                60     66  0
     D  ldemdy                70     76  0
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * MAINLINE
      ****************************************************************
      *
      * Read all groups selected by the open query.
      * (Open query selects open and disposed groups; omits all BGF's.)
      *
593  C*    *loval        setll     hsp034
593  C     *start        setll     hsp034
      *
     C                   dou       *in90 = *on                                  Main do loop
     C                   read      hsp034                                 90
     C                   if        *in90 = *off                                 If not EOF
      *
     C                   movel     hghgcd        d1hgcd
     C                   movel     hggtcd        d1gtcd
      *
      * Read each period cost record where this group is the origin group.
      *
     C                   exsr      $sales
      *
     C                   if        d1slvhd <> 0 or d1sam <> 0 or                If continue
     C                             d1ppsam <> 0 or d1cpsam <> 0
      *
     C                   if        first = yes
     C                   exsr      $ppds
     C                   exsr      $fsnm
     C                   seton                                        of
     C                   move      no            first
     C                   endif
      *
      * Check for control breaks:
      *   1) farm site
      *   2) phase
      *
     C                   select
     C                   when      hgppcd <> svppcd
     C                   exsr      $brkphase
      *
     C                   when      hgfscd <> svfscd
     C                   exsr      $brkfarm
     C                   endsl
      *
      * Calculate rate per head and rate per live pounds.
      *
     C                   if        d1slvhd <> 0
     C     d1sam         div(h)    d1slvhd       d1hdrt
     C                   endif
      *
     C                   if        d1slvlb <> 0
     C     d1sam         div(h)    d1slvlb       d1lbrt
     C                   endif
      *
      *
      * Accumulate totals
      *
     C                   add       d1slvhd       arslvhd
     C                   add       d1slvlb       arslvlb
     C                   add       d1scrlb       arscrlb
     C                   add       d1sam         arsam
     C                   add       d1cpsam       arcpsam
     C                   add       d1ppsam       arppsam
      *
      * Print detail line
     C                   if        *inof = *on
     C                   except    h1hdr
     C                   except    h2hdr
     C                   endif
      *
     C                   except    d1dtl
      *
     C                   endif                                                  If continue
     C                   endif                                                  If not EOF
     C                   enddo                                                  Main do loop
      *
      *---------------------------------------------------------------
      * EOF processing
      *---------------------------------------------------------------
      * If the file was empty, print the standard 'no data...'
      * error message; otherwise, print totals.
      *
     C                   if        first = yes
     C                   except    h1hdr
     C                   except    nodata
     C                   else
      * Farm total
     C                   z-add     1             x
     C                   exsr      $t1tot
      * Phase total
     C                   z-add     2             x
     C                   exsr      $t1tot
      * Report total
     C                   z-add     3             x
     C                   exsr      $t1tot
     C                   endif
      *
     C                   seton                                        lr
      /EJECT
      *---------------------------------------------------------------
      * Retrieve all sales info from all period records for group
      *---------------------------------------------------------------
      *
     C     $sales        begsr
      *
     C                   z-add     0             wkslvhd
     C                   z-add     0             wkslvlb
     C                   z-add     0             wkscrlb
     C                   z-add     0             wksam
     C                   z-add     0             wkppsam
     C                   z-add     0             wkcpsam
      *
     C     hghgsn        setll     hsp055                                 91
      *
     C                   dou       *in91 = *on                                  Do periods
     C     hghgsn        reade     hsp055                                 91
     C                   if        *in91 = *off                                 If not EOF
     C                   add       pcsam         wksam
     C                   add       pcslvhd       wkslvhd
     C                   add       pcslvlb       wkslvlb
     C                   add       pcscrlb       wkscrlb
      *
     C                   if        pcacyr = ldccyy and
     C                             pcacpe = ldper
     C                   add       pcsam         wkcpsam
     C                   else
     C                   add       pcsam         wkppsam
     C                   endif
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Do periods
      *
      * Move sales values to print fields that have no decimals.
      *
     C                   z-add(h)  wkslvhd       d1slvhd
     C                   z-add(h)  wkslvlb       d1slvlb
     C                   z-add(h)  wkscrlb       d1scrlb
     C                   z-add(h)  wkppsam       d1ppsam
     C                   z-add(h)  wkcpsam       d1cpsam
     C                   z-add(h)  wksam         d1sam
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Break on production phase
      *---------------------------------------------------------------
      *
     C     $brkphase     begsr
      *
      * Print farm total
     C                   z-add     1             x
     C                   exsr      $t1tot
      * Print phase total
     C                   z-add     2             x
     C                   exsr      $t1tot
      *
      * Retrieve new farm and phase descriptions
      *
     C                   exsr      $fsnm
     C                   exsr      $ppds
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Retrieve production phase description
      *---------------------------------------------------------------
      *
     C     $ppds         begsr
      *
     C     hgppcd        chain     hsp011                             92
     C                   if        *in92 = *off
     C                   movel     ppppds        h1ppds
     C                   else
     C                   movel(p)  'Unknown'     h1ppds
     C                   endif
      *
      * Save the new phase and
      * Set on overflow to force a page break for the new phase
      *
     C                   move      hgppcd        svppcd
     C                   seton                                        of
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Break on farm
      *---------------------------------------------------------------
      *
     C     $brkfarm      begsr
      *
      * Print farm total
     C                   z-add     1             x
     C                   exsr      $t1tot
      *
      * Retrieve new farm
      *
     C                   exsr      $fsnm
      *
      * Print new farm heading
      *
     C                   if        *inof = *on
     C                   except    h1hdr
     C                   endif
     C                   except    h2hdr
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Retrieve farm site name
      *---------------------------------------------------------------
      *
     C     $fsnm         begsr
      *
     C     hgfscd        chain     hsp018                             92
     C                   if        *in92 = *off
     C                   movel     fsfsnm        h2fsnm
     C                   else
     C                   movel(p)  'Unknown'     h2fsnm
     C                   endif
      *
     C                   z-add     hgfscd        svfscd
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Print total line
      *---------------------------------------------------------------
      *
     C     $t1tot        begsr
      *
      * Move array value to print fields
      *
      *
     C                   z-add     arcpsam(x)    t1cpsam
     C                   z-add     arppsam(x)    t1ppsam
     C                   z-add     arsam  (x)    t1sam
     C                   z-add     arslvlb(x)    t1slvlb
     C                   z-add     arscrlb(x)    t1scrlb
     C                   z-add     arslvhd(x)    t1slvhd
      *
      * Initialize array values
      *
     C                   z-add     0             arppsam(x)
     C                   z-add     0             arcpsam(x)
     C                   z-add     0             arsam  (x)
     C                   z-add     0             arslvlb(x)
     C                   z-add     0             arscrlb(x)
     C                   z-add     0             arslvhd(x)
      *
     C                   if        *inof = *on
     C                   except    h1hdr
     C                   endif
      *
      * Set indicator that controls printing of underscores. Only print
      * underscores with 'farm' total line.
      *
     C                   if        x = 1
     C                   seton                                        87
     C                   else
     C                   setoff                                       87
     C                   endif
      *
     C                   except    t1tot
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * *inzsr - initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      *
      * Populate time for report heading
      *
     C                   time                    rtime
      *
      * Set up total line descriptions into an array.
      *
     C                   eval      ardesc(1) = *blank
     C                   eval      ardesc(2) = ' Phase total:'
     C                   eval      ardesc(3) = 'Report total:'
      *
     C                   endsr
      /EJECT
      *-------------------------------------------------------------
      * Report Headings
      *-------------------------------------------------------------
      *
     Oqprint    e            h1hdr          1 01
     O                       sdpgm               10
     O                                          106 'HOG PRODUCTION SYSTEM'
     O                                          188 'DATE'
     O                       udate         y    198
      *
     O          e            h1hdr          1
     O                       sdusr               10
     O                                          104 'OPEN GROUP COST OF SALES'
     O                                          111 ' REPORT'
     O                                          188 'TIME'
     O                       rtime              198 '  :  :  '
      *
      *
     O          e            h1hdr          1
     O                                          188 'PAGE'
     O                       page          z    198
      *
      *
     O          e            h1hdr          1
     O                                            7 'PERIOD:'
     O                       ldper         z     11
     O                       ldbmdy        y     21
     O                                           24 'TO'
     O                       ldemdy        y     33
      *
      *-------------------------------------------------------------
      * Column heading lines
      *-------------------------------------------------------------
      *
     O          e            h1hdr          1
     O                                          147 'PRIOR'
     O                                          167 'CURRENT'
      *
     O          e            h1hdr          1
     O                                           28 'HOG'
     O                                           39 'GROUP'
     O                                           82 'LIVE'
     O                                          104 'CARCASS'
     O                                          114 'COST'
     O                                          125 'COST'
     O                                          147 'PERIODS'
     O                                          167 'PERIOD'
     O                                          197 'TOTAL'
      *
     O          e            h1hdr          1
     O                                           18 'FARM'
     O                                           30 'GROUP'
     O                                           39 'TYPE'
     O                                           60 'HEAD'
     O                                           82 'POUNDS'
     O                                          104 'POUNDS'
     O                                          116 'PER HEAD'
     O                                          127 'PER POUND'
     O                                          147 'AMOUNT'
     O                                          167 'AMOUNT'
     O                                          197 'AMOUNT'
      *
     O          e            h1hdr          1
     O                       dash               198
      *
     O          e            h1hdr          0
     O                                            6 'PHASE:'
     O                       h1ppds              38
      *
      *-------------------------------------------------------------
      * Farm site heading
      *-------------------------------------------------------------
      *
     O          e            h2hdr       2  1
     O                       svfscd        z     18
     O                       h2fsnm              50
      *
      *-------------------------------------------------------------
      * Detail line
      *-------------------------------------------------------------
      *
     O          e            d1dtl       1
     O                       d1hgcd         b    32
     O                       d1gtcd         b    37
     O                       d1slvhd       kb    61
     O                       d1slvlb       kb    83
     O                       d1scrlb       kb   105
     O                       d1hdrt        kb   116
     O                       d1lbrt        kb   127
     O                       d1ppsam       kb   148
     O                       d1cpsam       kb   168
     O                       d1sam         kb   198
      *
      *-------------------------------------------------------------
      * Total lines
      *-------------------------------------------------------------
      *
     O          e            t1tot       1
     O               87                          60 '---------------'
     O               87                          82 '---------------'
     O               87                         104 '---------------'
     O               87                         148 '---------------'
     O               87                         168 '---------------'
     O               87                         198 '---------------'
      *
     O          e            t1tot       1
     O                       ardesc (x)          39
     O                       t1slvhd       kb    61
     O                       t1slvlb       kb    83
     O                       t1scrlb       kb   105
     O                       t1ppsam       kb   148
     O                       t1cpsam       kb   168
     O                       t1sam         kb   198
      *
      *-------------------------------------------------------------
      * No data message line
      *-------------------------------------------------------------
      *
     O          e            nodata      2
     O                                           19 'NO DATA MEETS YOUR'
     O                                           38 'SELECTION CRITERIA'
