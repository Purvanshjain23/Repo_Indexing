/***************************************************************** */
/*                                                                 */
/*  PROGRAM NUMBER:  HPBLDTRANS                                    */
/*  PROGRAM NAME:    BUILD DATAMART FIELS FOR TRANSPORATION        */
/*  PROGRAMMER:      BARB GUTIERREZ                                */
/*  CREATE DATE:     02/16/04                                      */
/*                                                                 */
/*  FUNCTION:        THIS CL IS SUBMITTED NIGHTLY FROM THE JOB     */
/*                   SCHEDULER ON BIGBYTE (BIBLDTRAN)              */
/*                                                                 */
/*                   IT PERFORMS THE FOLLOWING FUNCTIONS:          */
/*                   A) CLEARS THE DATAMART FILES ON BIGBYTE       */
/*                   B) COPIES THE DELIVERY TRACKING FILE FROM     */
/*                      PRKFGUP TO PRKDMFLIB                       */
/*                                                                 */
/*******************************************************************/
/* MODIFIED:                                                       */
/*                                                                 */
/* 08/14/09  ALICE BROWNFIELD                                      */
/*           E00472 - Data Mart Build changes for DR Test          */
/*           Change CPYF from PRKFGUP to PRKDMFLIB to be CRTDUPOBJ */
/*           ----  this works better for MIMIX                     */
/*                                                                 */
/* 09/28/09  BARB GUTIERREZ                                        */
/*           E00472 - getting an allocation error on the files on  */
/*           the crtdupobj command with data *yes.  Changing to do */
/*           a crtdupobj with data *no then copying the file.      */
/*                                                                 */
/* 04/29/10  LeAnne Ramsey  (C719)                                 */
/*           Changed logic to use ESEND instead of MPLUS/MPLUS.    */
/*                                                                 */
/*******************************************************************/

             PGM

             DCL        VAR(&ERR)    TYPE(*CHAR) LEN(1) VALUE('N')

             DCL        VAR(&EMAIL)    TYPE(*CHAR) LEN(38)
             DCL        VAR(&TSTPRDFL) TYPE(*CHAR) LEN(1)
             DCL        VAR(&SUBJECT)  TYPE(*CHAR) LEN(60) +
                          VALUE('HPBLDTRANS-Datamart TRANS: Bld +
                          Files...DIST LIST: prk cognos')

/*------------------------------------------------------------------*/
/* Set up the Email Recipient (either Test or Production)           */
/*------------------------------------------------------------------*/

/* Retrieve the Data Area that lets us know whether we are in TEST  */
/* or PRODUCTION. (Always default to the 'TEST' Distribution List.) */

             CHGVAR     VAR(&EMAIL) VALUE('prk cognos test')
             RTVDTAARA  DTAARA(DATSTPRD (1 1)) RTNVAR(&TSTPRDFL)
             IF         COND(&TSTPRDFL *EQ 'P') THEN(CHGVAR +
                          VAR(&EMAIL) VALUE('prk cognos'))

/*-----------------------------------------------------------------*/
/* DELETE DATAMART FILES in PRKDMFLIB                              */
/*-----------------------------------------------------------------*/

             DLTF       FILE(PRKDMFLIB/PLAFCPP) /* DELIVERY TRACKING */
             MONMSG     MSGID(CPF0600 CPF2100 CPF3200)

             DLTF       FILE(PRKDMFLIB/PLANREP) /* Cont Hauler Rsn   */
             MONMSG     MSGID(CPF0600 CPF2100 CPF3200)

/*********************************************************************/
/* CREATE THE FILES INTO PRKDMFLIB                                  */
/*********************************************************************/

/* DELIVERY TRACKING                                                */

             CRTDUPOBJ  OBJ(PLAFCPP) FROMLIB(PRKFGUP) OBJTYPE(*FILE) +
                          TOLIB(PRKDMFLIB) DATA(*NO)
             MONMSG     MSGID(CPF2100) EXEC(CHGVAR VAR(&ERR) +
                          VALUE('Y'))
             CPYF       FROMFILE(PRKFGUP/PLAFCPP) +
                          TOFILE(PRKDMFLIB/PLAFCPP) MBROPT(*REPLACE)
             MONMSG     MSGID(CPF0000)

/* CONTRACT HAULER REASON                                           */

             CRTDUPOBJ  OBJ(PLANREP) FROMLIB(PRKFGUP) OBJTYPE(*FILE) +
                          TOLIB(PRKDMFLIB) DATA(*NO)
             MONMSG     MSGID(CPF2100) EXEC(CHGVAR VAR(&ERR) +
                          VALUE('Y'))
             CPYF       FROMFILE(PRKFGUP/PLANREP) +
                          TOFILE(PRKDMFLIB/PLANREP) MBROPT(*REPLACE)
             MONMSG     MSGID(CPF0000)

/*---------------------------------------------------------------------*/
/* SEND COMPLETION MESSAGE TO I.S. FOLKS                               */
/*---------------------------------------------------------------------*/

             IF         COND(&ERR = 'Y') THEN(ESNDMAIL +
                          RECIPIENT(&EMAIL) SUBJECT(&SUBJECT) +
                          MSG('File(s) PLAFCPP and/or PLANREP did +
                          not get recreated in PRKDMFLIB. If the +
                          preceeding DELETEs were successful, you +
                          may be missing the files in PRKDMFLIB. +
                          Investigate and rerun the build.') +
                          IMPORTANCE(*HIGH))

             IF         COND(&ERR = 'N') THEN(ESNDMAIL +
                          RECIPIENT(&EMAIL) SUBJECT(&SUBJECT) +
                          MSG('All Delivery Tracking files were created +
                          successfully in PRKDMFLIB; the build completed +
                          normally.') IMPORTANCE(*HIGH))

             ENDPGM

