      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Hog Production System
      * PROGRAM:     HP6012
      * TITLE:       Listing of Drivers
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     12/23/02
      *
      * FUNCTION:  Batch program to list the records in the file.
      *            This listing is submitted from HP4012-Work With Drivers.
      *
      *            The listing 'selections' will be the positioner/selector values
      *            in the control record at the time the user submitted the listing.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 02/16/09  LeAnne Ramsey
      *           We have added "default trucker code" to the layout.
      *
      * 10/15/13  LeAnne Ramsey (E2831)
      *           Recompile only. Added field 'MTech Reference'.
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fhsp107    if   e           k disk
      *    Drivers
      *
      *
     Fqprint    o    f   95        printer oflind(*inof)
      *
      /EJECT
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Standard fields
      *
     D prtfl           s              1    inz('N')
     D dash            s             95
     d rtime           s              6  0
      *
      * Workfields
      *
     D wkshift         s              1
      *
      *
      * Print fields
      *
     D h1driver        s             75
     D h1status        s             75
     D h1office        s             75
     D h1shift         s             75
     D h1ticd          s             75
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *---------------------------------------------------------------
      * Local data area.
      *---------------------------------------------------------------
      *
     Dlda             uds                  dtaara(*lda)
     D  lddicd                 1      5
     D  ldaist                 6      6
     D  ldtocd                 7     11
     D  ldshift               12     12  0
     D  ldticd                13     17
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * MAINLINE
      ****************************************************************
      *
      * Prepare to read the file and print headings the first time through
      *
     C     lddicd        setll     hsp107
     C                   except    h1hdr
      *
      * Read all records in the file. Check the user's selected status with
      * the status in the record before printing.
      *
     C                   dou       *in90 = *on                                  Main do
     C                   read      hsp107                                 90
     C                   if        *in90 = *off and                             If not EOF
     C                             (ldaist = *blank or ldaist = diaist) and
     C                             (ldtocd = *blank or ldtocd = ditocd) and
     C                             (ldticd = *blank or ldticd = diticd) and
     C                             (ldshift = 0  or ldshift = dishift)
      *
      * Print detail line
      *
     C                   if        *inof = *on
     C                   except    h1hdr
     C                   endif
      *
     C                   except    l1dtl
     C                   movel     yes           prtfl
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Main do
      *
      *---------------------------------------------------------------
      * EOF processing
      *---------------------------------------------------------------
      * If the file was empty, print the standard 'no data...' message
      *
     C                   if        prtfl = no
     C                   except    nodata
     C                   endif
      *
     C                   seton                                        lr
      /EJECT
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Set up a line of underscores and retrieve time for report heading.
      *
     C                   movel     *all'-'       dash
     C                   time                    rtime
      *
      * Set up headings for user selections:
      *
     C                   if        lddicd = *blank
     C                   eval      h1driver = 'Listing begins with the ' +
     C                                        'first driver'
     C                   else
     C                   eval      h1driver = 'Listing begins with driver ' +
     C                                        lddicd
     C                   endif
      *
      * Active/inactive status
      *
     C                   select
     C                   when      ldaist = *blank
     C                   eval      h1status = 'Listing includes both Active ' +
     C                                        'and Inactive drivers'
     C                   when      ldaist = 'A'
     C                   eval      h1status = 'Listing includes only Active ' +
     C                                        'drivers'
     C                   when      ldaist = 'I'
     C                   eval      h1status = 'Listing includes only ' +
     C                                        'Inactive drivers'
     C                   endsl
      *
      *
      * Transportation office
      *
     C                   select
     C                   when      ldtocd = *blank
     C                   eval      h1office = 'Listing includes drivers from' +
     C                                        ' all Transportation Offices'
     C                   other
     C                   eval      h1office = 'Listing only includes drivers' +
     C                                        ' from Transportation Office ' +
     C                                        ldtocd
     C                   endsl
      *
      *
      * Shift
     C                   select
     C                   when      ldshift = 0
     C                   eval      h1shift = 'Listing includes drivers from' +
     C                                        ' all Shifts'
     C                   other
     C                   move      ldshift       wkshift
     C                   eval      h1shift = 'Listing only includes drivers' +
     C                                        ' from Shift ' + wkshift
     C                   endsl
      * Trucker code
     C                   select
     C                   when      ldticd = *blank
     C                   eval      h1ticd = 'Listing includes all Trucker Codes'
     C                   other
     C                   eval      h1ticd = 'Listing only includes Trucker Code'
     C                                      + ' ' + ldticd
     C                   endsl
      *
     C                   endsr
      /EJECT
      *-------------------------------------------------------------
      * Report heading lines
      *-------------------------------------------------------------
      *
     Oqprint    e            h1hdr          1 01
     O                       sdpgm               10
     O                                           52 'HOG PRODUCTION SYSTEM'
     O                                           85 'DATE'
     O                       udate         y     95
      *
     O          e            h1hdr          1
     O                       sdusr               10
     O                                           50 'LISTING OF DRIVERS'
     O                                           85 'TIME'
     O                       rtime               95 '  :  :  '
      *
     O          e            h1hdr          2
     O                                           85 'PAGE'
     O                       page          z     95
      *
      *
     O          e            h1hdr          1
     O                       h1driver            75
      *
     O          e            h1hdr          1
     O                       h1ticd              75
      *
     O          e            h1hdr          1
     O                       h1office            75
      *
     O          e            h1hdr          1
     O                       h1shift             75
      *
     O          e            h1hdr          2
     O                       h1status            75
      *
      *
     O          e            h1hdr          1
     O                                           53 'DEFAULT'
     O                                           72 'DEFAULT'
     O                                           83 'DEFAULT'
     O                                           95 'ACTIVE/'
      *
     O          e            h1hdr          1
     O                                            6 'DRIVER'
     O                                           42 'TRUCKER'
     O                                           51 'TRANS'
     O                                           62 'DEFAULT'
     O                                           70 'TRUCK'
     O                                           83 'TRAILER'
     O                                           95 'INACTIVE'
     O          e            h1hdr          1
     O                                            4 'CODE'
     O                                           11 'NAME'
     O                                           39 'CODE'
     O                                           52 'OFFICE'
     O                                           61 'SHIFT'
     O                                           71 'NUMBER'
     O                                           82 'NUMBER'
     O                                           94 'STATUS'
      *
     O          e            h1hdr          1
     O                       dash                95
      *
      *-------------------------------------------------------------
      * Detail line
      *-------------------------------------------------------------
      *
     O          e            l1dtl       1
     O                       didicd               5
     O                       didinm              32
     O                       diticd              40
     O                       ditocd              51
     O                       dishift       z     59
     O                       ditruck             80
     O                       ditrail             91
     O                       diaist              92
      *
      *-------------------------------------------------------------
      * No data message line
      *-------------------------------------------------------------
      *
     O          e            nodata      1
     O                                           19 'NO DATA MEETS YOUR'
     O                                           38 'SELECTION CRITERIA'
