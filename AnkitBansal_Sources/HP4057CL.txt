/***************************************************************** */
/*                                                                 */
/*  PROGRAM NUMBER:  HP4057CL                                      */
/*  PROGRAM NAME:    ROYALTY PAYMENT REPORT                        */
/*  PROGRAMMER:      LEANNE FEDOR                                  */
/*  CREATE DATE:     12/14/05                                      */
/*                                                                 */
/*  FUNCTION:        THIS CL IS SUBMITTED WITH QCMDEXC FROM        */
/*                   HP4057-SPECIFY OPTIONS FOR ROYALTY REPORT.    */
/*                   OPTIONS ARE PASSED THROUGH THE LDA.           */
/*                                                                 */
/*                                                                 */
/*******************************************************************/
/* MODIFIED:                                                       */
/*******************************************************************/
/*                                                                 */
/* 01/08/07  LEANNE RAMSEY                                         */
/*           ADDED THE "DISTRIBUTION" LIST AS PART OF THE E-MAIL   */
/*           THAT IS SENT.                                         */
/*                                                                 */
/* 08/11/08  LEANNE RAMSEY                                         */
/*           CHANGED LDA DATE POSITIONS TO MATCH HPS PERIOD CLOSE. */
/*           WE WILL NOW GET THE EMAIL RECIPIENT FROM THE LDA.     */
/*           ALSO, ADDED CHECK DETAIL WORKFILE AND LOGIC.          */
/*                                                                 */
/* 02/29/16    Rose Centonze    E5290                              */
/*             Added company code TO LDA, add to opnqry select    */
/*             JOIN FILES TO HSL034D WITH SYS HOG GROUP, SELECT COMPANY  */
/*******************************************************************/

             PGM

             DCL        VAR(&QDATA) TYPE(*CHAR) LEN(110) /* E5290 -WAS 90 */

             DCL        VAR(&LDCOCD) TYPE(*CHAR) LEN(3)   /*COMPANY E5290  */
             DCL        VAR(&LDFCYMD) TYPE(*DEC) LEN(8)
             DCL        VAR(&LDTCYMD) TYPE(*DEC) LEN(8)
             DCL        VAR(&LDEMAIL) TYPE(*CHAR) LEN(50)

             CHGVAR     VAR(&LDFCYMD) VALUE(%SST(*LDA 1 8))
             CHGVAR     VAR(&LDTCYMD) VALUE(%SST(*LDA 9 8))
             CHGVAR     VAR(&LDCOCD) VALUE(%SST(*LDA 200 3))
             CHGVAR     VAR(&LDEMAIL) VALUE(%SST(*LDA 300 50))

/*------------------------------------------------------------------*/
/* CREATE WORKFILES                                                 */
/*------------------------------------------------------------------*/

             CRTDUPOBJL OBJ(HSP3057) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)    /* Group Header */

             CRTDUPOBJL OBJ(HSP3058) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)    /* Distribution */

             CRTDUPOBJL OBJ(HSP3059) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)    /* Check Detail */


/*------------------------------------------------------------------*/
/* MARKET SALE LOGIC:                                               */
/* SELECT CHECK DETAIL RECORDS WITH A KILL DATE IN THE RANGE        */
/*------------------------------------------------------------------*/

             CHGVAR     VAR(&QDATA) VALUE(' ')
             CHGVAR     VAR(%SST(&QDATA 1 10)) VALUE('CDKLDT *GE')
             CHGVAR     VAR(%SST(&QDATA 12 8)) VALUE(&LDFCYMD)
             CHGVAR     VAR(%SST(&QDATA 21 15)) VALUE('*AND CDKLDT +
                          *LE')
             CHGVAR     VAR(%SST(&QDATA 37 8)) VALUE(&LDTCYMD)

/*   SELECT COMPANY IN PARM          E5290                     */

             CHGVAR     VAR(%SST(&QDATA 46 15)) VALUE('*AND HGCOCD +
                          *EQ')
             CHGVAR     VAR(%SST(&QDATA 62 3)) VALUE(&LDCOCD)

/* OVERRIDE CHECK DETAIL FILE FOR MARKET SALES                      */

             OVRDBF     FILE(HSL064I) SHARE(*YES)
  /*         OPNQRYF    FILE((HSL064I)) QRYSLT(&QDATA)     ORIGINAL        */

             OPNQRYF    FILE((HSL064I) (HSL034D)) FORMAT(HSL064I) +
                          QRYSLT(&QDATA) KEYFLD((CDHGSN) (CDMVSN) +
                          (CDCVNO) (CDCHDT) (CDTPCD)) JFLD((CDHGSN +
                          HGHGSN))


/*----------------------------------------------------------------------------*/
/* LOGIC FOR FEEDER PIG SALES...AND FOR MARKET SALES WITH NO KILL DATE.       */
/* 1) WE EXPECT MARKET SALES TO ALWAYS HAVE A KILL DATE..BUT JUST IN CASE ... */
/* 2) WE WILL ALWAYS EXCLUDE ALL CULL SALES.                                  */
/*----------------------------------------------------------------------------*/

             CHGVAR     VAR(&QDATA) VALUE(' ')
             CHGVAR     VAR(%SST(&QDATA 1 10)) VALUE('SHRCDT *GE')
             CHGVAR     VAR(%SST(&QDATA 12 8)) VALUE(&LDFCYMD)
             CHGVAR     VAR(%SST(&QDATA 21 15)) VALUE('*AND SHRCDT +
                          *LE')
             CHGVAR     VAR(%SST(&QDATA 37 8)) VALUE(&LDTCYMD)
             CHGVAR     VAR(%SST(&QDATA 46 23)) VALUE('*AND SHSTCD +
                          *NE "CULLS"')
             CHGVAR     VAR(%SST(&QDATA 70 17)) VALUE('*AND CDKLDT +
                          *EQ 0')

/*   SELECT COMPANY IN PARM          E5290                     */

             CHGVAR     VAR(%SST(&QDATA 88 15)) VALUE('*AND HGCOCD +
                          *EQ')
             CHGVAR     VAR(%SST(&QDATA 104 3)) VALUE(&LDCOCD)


/* OVERRIDE CHECK DETAIL FILE FOR FPS (FEEDER PIG SALES)            */

             OVRDBF     FILE(HSJ064F) SHARE(*YES)
 /*     OPNQRYF    FILE((HSJ064F)) QRYSLT(&QDATA) KEYFLD(*FILE)  ORIGINAL */

             OPNQRYF    FILE((HSJ064F) (HSL034D)) FORMAT(HSJ064F) +
                          QRYSLT(&QDATA) KEYFLD((CDHGSN)) +
                          JFLD((CDHGSN HGHGSN))

/*------------------------------------------------------------------*/
/* CALL PROGRAM TO BUILD CHECK DETAIL WORKFILE                      */
/*------------------------------------------------------------------*/

             CALL       PGM(HP3059)

             CLOF       OPNID(HSL064I)
             CLOF       OPNID(HSJ064F)
             DLTOVR     FILE(*ALL)

/*------------------------------------------------------------------*/
/* CALL PROGRAM TO BUILD GROUP AND DISTRIBUTION WORKFILES           */
/*------------------------------------------------------------------*/

             CALL       PGM(HP3057)

/*------------------------------------------------------------------*/
/* RUN A SEQUEL VIEW TO CREATE THE SPREADSHEET AND EMAIL IT         */
/*------------------------------------------------------------------*/

             EXECUTE    VIEW(*LIBL/HP4057V) PCFMT(*XLS) +
                          REPLACE(*YES) RECIPIENT(&LDEMAIL) +
                          EMLMSG('Distribution List: +
                          HPS-ROYALTY.PAY') TEXT('HPS Royalty +
                          Payment Data')
             MONMSG     MSGID(CPF0000)

             ENDPGM

