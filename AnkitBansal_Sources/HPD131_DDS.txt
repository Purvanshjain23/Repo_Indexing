     A*%%TS  SD  20010822  105044  ISLRAMS     REL-V4R4M0  5769-PW1
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION
     A*  DISPLAY FILE:  HPD131
     A*  TITLE:         REVISE EXPENSE RECORD IN EDIT FILE
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  DATE:          01/05/98
     A*
     A*  INDICATOR 60 IS ON WHEN IN REVISE MODE
     A*  INDICATOR 62 IS ON WHEN IN DELETE MODE
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A  60N62                               CF05(05 'REFRESH')
     A  60N62                               CF11(11 'DELETE')
     A                                      CA12(12 'PREVIOUS')
     A*
     A*---------------------------------------------------------------
     A* RECORD FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP131R1
     A*%%TS  SD  20010822  105044  ISLRAMS     REL-V4R4M0  5769-PW1
     A                                      SETOF(32)
     A                                      OVERLAY
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 29'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A            DFMODE         6A  O  2  2DSPATR(HI)
     A                                  2 23'REVISE EXPENSE RECORD IN EDIT FILE'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  4  2'Expense code ....:'
     A            DFEXCD    R        O  4 22REFFLD(@@EXCD)
     A            DFEXDS    R        O  4 44REFFLD(EXEXDS)
     A                                  5  2'Account .........:'
     A            DFOBJ     R        O  5 22REFFLD(@@OBJ)
     A                                  6  2'Cost center .....:'
     A            DFCJD     R        O  6 22REFFLD(@@CJD)
     A                                  8  2'Amount ...........'
     A            DFAM      R        B  8 22REFFLD(EEAM)
     A                                      EDTCDE(K)
     A N32N62                               DSPATR(HI)
     A  32                                  DSPATR(PC)
     A  32                                  DSPATR(RI)
     A  62                                  DSPATR(PR)
     A N62                                  DSPATR(UL)
     A*
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R HP131K1
     A*%%TS  SD  19980105  140844  ISLRAMS     REL-V3R7M0  5716-PW1
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A N62                             23 11'F5=Refresh'
     A N62                             23 23'F11=Delete'
     A                                 23 35'F12=Previous'
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *---------------------------------------------------------------
     A          R HP131ES                   SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE CONTROL FORMAT
      *---------------------------------------------------------------
     A          R HP131EC                   SFLCTL(HP131ES)
      *
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
