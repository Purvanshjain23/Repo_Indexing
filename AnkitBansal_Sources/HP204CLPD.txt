/***************************************************************** */
/*                                                                 */
/*  PROGRAM NUMBER:  HP204CLPD                                     */
/*  PROGRAM NAME:    BUILD JDE UPLOAD FILE: HPS PIG DAYS           */
/*  PROGRAMMER:      LEANNE FEDOR                                  */
/*  CREATE DATE:     02/28/02                                      */
/*                                                                 */
/*  FUNCTION:        THIS CL IS SUBMITTED FROM THE DRIVER CL       */
/*                   HP404CL.                                      */
/*                   OPTIONS ARE PASSED THROUGH THE LDA.           */
/*                                                                 */
/*******************************************************************/
/* MODIFICATIONS:                                                  */
/* 10/10/18  Danny Nguyen  (S13682)                                */
/*           Retrieve Company Code from LDA. Change OPNQRYF logic  */
/*           for HSL034E & HSL018I to select by Company.           */
/*                                                                 */
/*******************************************************************/

             PGM

             DCL        VAR(&QDATA) TYPE(*CHAR) LEN(140)

             DCL        VAR(&LDACYR) TYPE(*DEC) LEN(4)
             DCL        VAR(&LDACPE) TYPE(*DEC) LEN(2)
             DCL        VAR(&LDBPDT) TYPE(*DEC) LEN(8)
             DCL        VAR(&LDEPDT) TYPE(*DEC) LEN(8)
             DCL        VAR(&LDCOCD) TYPE(*CHAR) LEN(3)

             CHGVAR     VAR(&LDACYR) VALUE(%SST(*LDA 1 4))
             CHGVAR     VAR(&LDACPE) VALUE(%SST(*LDA 5 2))
             CHGVAR     VAR(&LDBPDT) VALUE(%SST(*LDA 7 8))
             CHGVAR     VAR(&LDEPDT) VALUE(%SST(*LDA 15 8))
             CHGVAR     VAR(&LDCOCD) VALUE(%SST(*LDA 25 3))

/*********************************************************************/
/* THE PIG DAYS VALUES WE WILL ULTIMATELY NEED MUST MATCH            */
/* THE VALUES ON EXISTING REPORTS THAT RUN OVER WORKFILES.  SO, WE   */
/* USE THE EXISTING PROGRAMS TO BUILD WORKFILES FOR OUR UPLOAD LOGIC.*/
/*********************************************************************/

/*-------------------------------------------------------------------*/
/* BUILD A PIG DAYS WORKFILE                                         */
/*-------------------------------------------------------------------*/

             CRTDUPOBJL OBJ(HSP309) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)

/*  ALWAYS OMIT HOG GROUPS WITH A STATUS OF VOID                 */

             CHGVAR     VAR(&QDATA) VALUE(' ')
             CHGVAR     VAR(%SST(&QDATA 1 15)) VALUE('HGGSCD +
                          *NE "VD"')

/*  ALWAYS OMIT GROUPS THAT OPENED AFTER THE END OF THE PERIOD      */

             CHGVAR     VAR(%SST(&QDATA 17 15)) VALUE('*AND HGOPDT +
                          *LE')
             CHGVAR     VAR(%SST(&QDATA 33 8)) VALUE(&LDEPDT)


/*  ALWAYS OMIT DISPOSED GROUPS WITH A DISPOSE DATE BEFORE OR ON     */
/*  THE BEGINNING PERIOD DATE                                        */

             CHGVAR     VAR(%SST(&QDATA 42 18)) VALUE('*AND (HGDSDT +
                          *EQ 0')
             CHGVAR     VAR(%SST(&QDATA 61 14)) VALUE('*OR HGDSDT +
                          *GT')
             CHGVAR     VAR(%SST(&QDATA 76 8)) VALUE(&LDBPDT)
             CHGVAR     VAR(%SST(&QDATA 84 1)) VALUE(')')

/*  ALWAYS OMIT BGF GROUPS                                       */

             CHGVAR     VAR(%SST(&QDATA 86 23)) VALUE('*AND HGPPCD +
                          *NE "BGF  "')

/*  S13682-ALWAYS SELECT BY COMPANY CODE.                        */

             CHGVAR     VAR(%SST(&QDATA 110 17)) VALUE('*AND HGCOCD +
                          *EQ "')
             CHGVAR     VAR(%SST(&QDATA 127 3)) VALUE(&LDCOCD)
             CHGVAR     VAR(%SST(&QDATA 130 1)) VALUE('"')

             OVRDBF     FILE(HSL034E) SHARE(*YES)
             OPNQRYF    FILE((HSL034E)) QRYSLT(&QDATA) KEYFLD(*FILE)

             CALL       PGM(HP309) PARM(&LDBPDT &LDEPDT)

             CLOF       OPNID(HSL034E)
             DLTOVR     FILE(HSL034E)


/*********************************************************************/
/*  SETUP/RUN OPEN QUERIES FOR FILES OTHER THAN THE WORKFILES ABOVE  */
/*********************************************************************/

/* ALWAYS SELECT ONLY FARMS THAT HAVE A COST CENTER VALUE         */

             CHGVAR     VAR(&QDATA) VALUE(' ')
             CHGVAR     VAR(%SST(&QDATA 1 13)) VALUE('FSCJD *NE " "')

/*  S13682-ALWAYS SELECT BY COMPANY CODE.                        */

             CHGVAR     VAR(%SST(&QDATA 15 17)) VALUE('*AND FSCOCD +
                          *EQ "')
             CHGVAR     VAR(%SST(&QDATA 32 3)) VALUE(&LDCOCD)
             CHGVAR     VAR(%SST(&QDATA 35 1)) VALUE('"')

             OVRDBF     FILE(HSL018I) SHARE(*YES)
             OPNQRYF    FILE((HSL018I)) QRYSLT(&QDATA) KEYFLD(*FILE) +
                          SEQONLY(*YES)


/*********************************************************************/
/*  CALL PROGRAM TO BUILD INTERFACE FILE OF HPS PIG DAYS             */
/*********************************************************************/

             CALL       PGM(HP204PD)

/*******************************************************************/
/*  DELETE FILE OVERRIDES                                          */
/*******************************************************************/

             CLOF       OPNID(HSL018I)
             DLTOVR     FILE(HSL018I)

             ENDPGM

