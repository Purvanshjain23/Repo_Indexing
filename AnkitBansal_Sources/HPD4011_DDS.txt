     A*%%TS  SD  20240821  090855  ISJMIST     REL-V7R5M0  5770-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD4011
     A*  TITLE:         WORK WITH WEEKLY SIRE LINE DOSES
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  CREATED:       10/30/01
     A*
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A                                      CF04(04 'PROMPT')
     A                                      CF05(05 'REFRESH')
     A                                      CF06(06 'CREATE')
     A                                      CF07(07 'DOSES LISTING')
     A                                      CF09(09 'MISSING DOSES LISTING')
     A                                      CF15(15 'VIEW ALL')
     A                                      CF17(17 'PRINT OPTIONS')
     A                                      CF20(20 'WRKSPLF')
     A                                      CF16(16 'ENTER WEEK ENDING')
     A*
     A*-------------------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*-------------------------------------------------------------------------
     A          R HP4011S1                  SFL
     A*%%TS  SD  20240813  072504  ISJMIST     REL-V7R5M0  5770-WDS
     A  85                                  SFLNXTCHG
     A            HDRRN1    R        H      REFFLD(@@RRN)
     A            D1OPT     R        B 12  2REFFLD(@@OPT)
     A                                      CHGINPDFT(UL)
     A  30                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A N30                                  DSPATR(HI)
     A            D1FSCD    R        O 12  8REFFLD(@@FSCD)
     A                                      EDTCDE(M)
     A            D1CDYR    R        O 12 17REFFLD(HSRREF/@@CDYR *LIBL/HSPREF)
     A                                      EDTCDE(M)
     A            D1CDWK    R        O 12 24REFFLD(HSRREF/@@CDWK *LIBL/HSPREF)
     A                                      EDTCDE(M)
     A            D1MDYWEDT R        O 12 32REFFLD(HSRREF/@@DT06 *LIBL/HSPREF)
     A                                      EDTWRD('  /  /  ')
     A            D1CELL    R        O 12 47REFFLD(FSREC/FSCELL *LIBL/HSP018)
     A                                      EDTCDE(M)
     A*
     A*-------------------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*-------------------------------------------------------------------------
     A          R HP4011C1                  SFLCTL(HP4011S1)
     A*%%TS  SD  20240813  072504  ISJMIST     REL-V7R5M0  5770-WDS
     A                                      SFLSIZ(0050)
     A                                      SFLPAG(0008)
     A N82                                  ROLLUP(25 'PAGE FORWARD')
     A                                      SETOF(30)
     A                                      SETOF(31)
     A                                      SETOF(51)
     A                                      SETOF(52)
     A                                      RTNCSRLOC(&RCD &FLD)
     A                                      OVERLAY
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 28'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USR)
     A                                      DSPATR(HI)
     A                                  2 22'WORK WITH WEEKLY SIRE LINE DOSES'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@JOB)
     A                                      DSPATR(HI)
     A                                  4  2'Show only the most recent week for-
     A                                       each farm ..'
     A                                  4 50'?'
     A            C1YNFL    R        B  4 52REFFLD(HSRREF/@@FLAG *LIBL/HSPREF)
     A  31
     AO 51                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A N31                                  DSPATR(HI)
     A                                  6  2'2=Maintain doses'
     A                                  6 20'4=Delete'
     A                                  8 11'?'
     A                                      DSPATR(HI)
     A                                  9  9'Farm'
     A                                      DSPATR(HI)
     A                                  9 17'-Calendar-'
     A                                      DSPATR(HI)
     A                                  9 31'Week-Ending'
     A                                      DSPATR(HI)
     A                                 10  2'Opt'
     A                                      DSPATR(HI)
     A                                 10  9'Site'
     A                                      DSPATR(HI)
     A                                 10 17'Year'
     A                                      DSPATR(HI)
     A                                 10 23'Week'
     A                                      DSPATR(HI)
     A                                 10 35'Date'
     A                                      DSPATR(HI)
     A                                 10 46'Cell'
     A                                      DSPATR(HI)
     A            C1FSCD    R        B 11  8REFFLD(@@FSCD)
     A                                      DSPATR(HI)
     A                                      EDTCDE(M)
     A                                      DSPATR(PC)
     A            C1CDYR    R        B 11 17REFFLD(HSRREF/@@CDYR *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A N31N51N52                            DSPATR(PC)
     A            C1CDWK    R        B 11 24REFFLD(HSRREF/@@CDWK *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A                                      EDTCDE(M)
     A            C1MDYWEDT R        B 11 32REFFLD(@@DT06)
     A                                      DSPATR(HI)
     A                                      EDTWRD('  /  /  -')
     A            C1CELL    R        B 11 47REFFLD(FSCELL)
     A                                      EDTCDE(M)
     A                                      DSPATR(HI)
     A  52                                  DSPATR(PC)
     A*
     A*-------------------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*-------------------------------------------------------------------------
     A          R HP4011K1
     A*%%TS  SD  20240821  090855  ISJMIST     REL-V7R5M0  5770-WDS
     A  43                             20  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 21  2'F3=Exit'
     A                                 21 11'F4=Prompt'
     A                                 21 26'F7=Doses Listing'
     A                                 21 60'F17-Print Options'
     A                                 22 11'F5=Refresh'
     A                                 22 26'F9=Missing Doses Report'
     A                                 22 60'F20=Spool Files'
     A                                 23 11'F6=Create'
     A                                 23 25'F15=View All Farms/All Weeks'
     A                                 23 60'F16=Send To AgView'
      *
     A*-------------------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
     A*-------------------------------------------------------------------------
     A          R HP4011ES                  SFL SFLMSGRCD(24)
      *
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
     A*-------------------------------------------------------------------------
      * ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*-------------------------------------------------------------------------
     A          R HP4011EC                  SFLCTL(HP4011ES)
      *
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
     A          R HPD4011R3
     A*%%TS  SD  20240821  010002  ISJMIST     REL-V7R5M0  5770-WDS
     A                                      WINDOW(HPD4011R2)
     A                                      OVERLAY
     A            SDPGM         10A  O  1  1COLOR(WHT)
     A                                  1 19'SPECIFY WEEK ENDING DATE'
     A                                      COLOR(WHT)
     A                                  5  4'Enter Week Ending Date:'
     A                                      COLOR(BLU)
     A            D3MDYWEDT      6Y 0B  5 30COLOR(WHT)
     A                                      EDTWRD('  /  /  -')
     A  32                                  DSPATR(RI)
     A  32                                  DSPATR(PC)
     A          R HPD4011R4
     A*%%TS  SD  20240820  070201  ISJMIST     REL-V7R5M0  5770-WDS
     A                                      OVERLAY
     A                                      WINDOW(HPD4011R2)
     A                                 14  2'F3=Exit'
      *----------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *----------------------------------------------------------------
     A          R HPD4011R1
     A                                      TEXT('Error message subfile')
     A                                      SFL SFLMSGRCD(15)
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
     A*
     A*----------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT WITH WINDOW DEFINITION
     A*----------------------------------------------------------------
     A          R HPD4011R2                 SFLCTL(HPD4011R1)
     A*%%TS  SD  20000628  103649  ISLRAMS     REL-V4R2M0  5769-PW1
     A                                      TEXT('Error message subfile')
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A                                      WINDOW(*DFT 16 60)
     A                                      WDWBORDER((*DSPATR RI) (*CHAR '    -
     A                                          '))
     A            MSGPGM                    SFLPGMQ(10)
     A*
     A*----------------------------------------------------------------
     A* ASSUME FORMAT - REQUIRED FOR WINDOWS
     A*----------------------------------------------------------------
     A*
     A          R ASSUME
     A                                      TEXT('ASSUME FORMAT FOR WINDOWS')
     A                                      ASSUME
     A                                      OVERLAY
     A                                 24 79' '
