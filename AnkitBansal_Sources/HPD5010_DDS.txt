     A*%%TS  SD  20010521  154543  ISLRAMS     REL-V4R4M0  5769-PW1
     A*****************************************************************
     A*
     A*  SYSTEM:        HOG PRODUCTION
     A*  DISPLAY FILE:  HPD5010 - SELECT BGF FARM SITE WITH
     A*                          FARM SITE POSITIONERS
     A*  CREATED BY:    LEANNE FEDOR
     A*  DATE:          05/31/00
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA12(12 'PREVIOUS')
     A N82                                  ROLLUP(25 'PAGE FORWARD')
     A*--------------------------------------------------------------*
     A* REFERENCE WINDOW RECORD FORMAT 1
     A*--------------------------------------------------------------*
     A          R HP5010R1
     A*%%TS  SD  19950206  111504  ISLRAMS     REL-V3R0M5  5763-PW1
     A                                      TEXT('Reference window')
     A                                      WINDOW(*DFT 17 42)
     A                                      WDWBORDER((*DSPATR RI) (*CHAR '    -
     A                                          '))
     A*--------------------------------------------------------------*
     A* RECORD FORMAT ONE
     A*--------------------------------------------------------------*
     A          R HP5010R2
     A                                      TEXT('Written in RPG pgm to prevent-
     A                                       screen from clearing')
     A                                      CLRL(*NO)
     A                                      OVERLAY
     A*--------------------------------------------------------------*
     A* RECORD FORMAT TWO
     A*--------------------------------------------------------------*
     A* THIS FORMAT IS NOT WRITTEN IN THE RPG PGM.  IT PREVENTS THE
     A* CALLING_PROGRAM_SCREEN FROM BEING CLEARED.
     A*--------------------------------------------------------------*
     A          R HP5010R3
     A                                      TEXT('Dummy format not written in R-
     A                                      PG pgm.')
     A                                      ASSUME
     A                                 11  1' '
     A*--------------------------------------------------------------*
     A* SUBFILE FORMAT ONE
     A*--------------------------------------------------------------
     A          R HP5010S1                  SFL
     A*%%TS  SD  20010521  154543  ISLRAMS     REL-V4R4M0  5769-PW1
     A                                      TEXT('subfile record format one')
     A            HDRRN1         4S 0H
     A            DFFSCD    R        O  8  1REFFLD(HSRREF/HGFSCD *LIBL/HSPREF)
     A                                      EDTCDE(M)
     A            DFFSNM    R        O  8  9REFFLD(HSRREF/FSFSNM *LIBL/HSPREF)
     A            DFCELL    R        O  8 38REFFLD(HSRREF/@@CELL *LIBL/HSPREF)
     A                                      EDTCDE(Z)
     A*--------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*--------------------------------------------------------------
     A          R HP5010C1                  SFLCTL(HP5010S1)
     A*%%TS  SD  20010521  154543  ISLRAMS     REL-V4R4M0  5769-PW1
     A                                      SFLSIZ(0050)
     A                                      SFLPAG(0007)
     A                                      WINDOW(HP5010R1)
     A                                      TEXT('control format one')
     A                                      OVERLAY
     A                                      SFLCSRRRN(&HDRRN)
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND(*MORE)
     A                                      RMVWDW
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            HDRRN          5S 0H
     A                                  1 10'SELECT BGF FARM SITE'
     A                                      DSPATR(HI)
     A                                  2  1'Position Cursor, Press Enter'
     A                                  4  1'Business office:'
     A                                      DSPATR(HI)
     A            C1FSBO    R        O  4 19REFFLD(HSRREF/HGFSBO *LIBL/HSPREF)
     A                                  6  1'Farm'
     A                                      DSPATR(HI)
     A                                  6  9'Name'
     A                                      DSPATR(HI)
     A                                  6 37'Cell'
     A                                      DSPATR(HI)
     A            C1FSCD    R        B  7  1REFFLD(HSRREF/HGFSCD *LIBL/HSPREF)
     A                                      EDTCDE(M)
     A                                      DSPATR(HI)
     A            C1CELL    R        B  7 38REFFLD(HSRREF/@@CELL *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A          R HP5010K1
     A*%%TS  SD  19940511  110146  ISRCENT     REL-V2R2M0  5738-PW1
     A                                      OVERLAY
     A                                      WINDOW(HP5010R1)
     A                                 16  1'F12=Previous'
