      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Hog Production System
      * PROGRAM:     Workfile: Process BGF-to-Grow Finish Transfers
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     12/16/02
      *
      * FUNCTION:    This function writes records to the Workfile: BGF-to-Grow Finish
      *              Transfers. One record is written for each unique combination of:
      *                         grow finish group
      *                         source BGF farm
      *                         calendar year
      *                         calendar week
      *
      *              Transfers into GDU grow finish groups are omitted from processing.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 07/06/09  LeAnne Ramsey
      *           Recompile only. Added new field 'Continuous Flow Flag' to Hog Group file.
      *
      * 10/15/13  LeAnne Ramsey (E2831)
      *           Recompile only. Added field 'MTech Reference'.
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fhsl034d   if   e           k disk
      *    Hog group
      *
      *
     Fhsp018    if   e           k disk
      *    Farm site
      *
      *
     Fhsj075g   if   e           k disk
      *  Transfer detail + transfer header
      *  (Records selected in open query)
      *
      *
     Fhsp351    uf a e           k disk
      *  Workfile: BGF-to-Grow Finish Transfers
      *
      /EJECT
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      ****************************************************************
      * ARRAYS AND TABLES
      ****************************************************************
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Save fields
      *
     D svrcdt          s                   like(mhrcdt)
      *
      *
      * Workfields
      *
     D wksvdt          s                   like(mhrcdt)
      *
      *
      * Workfields for date manipulation
      *
     D wkcymdiso       s               d   datfmt(*iso)
      *
      *
      * Parm fields
      *
     D xxhpscdyr       s                   like(wcsvyr)
     D xxhpscdwk       s                   like(wcsvwk)
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ***************************************************************************************
      * MAINLINE
      ***************************************************************************************
      *
      * Process all BGF-to-Grow Finish transfers from 1999 forward that have quality or
      * rejected head....where the Grow Finish group is not a GDU group.
      * (Records selected with open query and file keyed by Received Date.)
      *
     C                   dou       *in90 = *on                                  Main do loop
     C                   read      hsj075g                                90
     C                   if        *in90 = *off and                             If not EOF
     C                             (mdqlhd <> 0 or mdrjhd <> 0)
      *
      * Determine if Grow Finish group is a GDU group. If it is, do not
      * process this record.
      *
     C     mddnsn        chain     hsl034d                            92        If not GDU
     C                   if        *in92 = *off and hggtcd <> 'G'
      *
      *
      * Determine year/week serviced
      *
     C                   if        mhrcdt <> svrcdt
     C                   exsr      $svyrwk
     C                   z-add     mhrcdt        svrcdt
     C                   endif
      *
      * Either update or write a record to the workfile for this combination of:
      * destination grow finish group/origin BGF farm/calendar year/calendar week
      *
     C     key01         chain     hsp351                             92
     C                   if        *in92 = *off
     C                   exsr      $upd351
     C                   else
     C                   exsr      $wrt351
     C                   endif
      *
     C                   endif
     C                   endif                                                  If not EOF
     C                   enddo                                                  Main do loop
      *
      * EOF Processing
      *
     C                   seton                                        lr
      /eject
      *--------------------------------------------------------------
      * Update a record
      *--------------------------------------------------------------
      *
     C     $upd351       begsr
      *
     C                   add       mdqlhd        wcrchd
     C                   add       mdrjhd        wcrchd
     C                   update    wcrec
      *
     C                   endsr
      /eject
      *--------------------------------------------------------------
      * Write a record
      *--------------------------------------------------------------
      *
     C     $wrt351       begsr
      *
     C                   move      hghgcd        wcdnhgcd
     C                   move      hgppcd        wcdnppcd
      *
      * Retrieve destination farm site cell
      *
     C     mhdnfs        chain     hsp018                             92
     C                   if        *in92 = *off
     C                   z-add     fscell        wcdncell
     C                   else
     C                   z-add     0             wcdncell
     C                   endif
      *
      * Retrieve origin farm site cell
      *
     C     mhorfs        chain     hsp018                             92
     C                   if        *in92 = *off
     C                   z-add     fscell        wcorcell
     C                   else
     C                   z-add     0             wcorcell
     C                   endif
      *
     C                   z-add     mhorfs        wcorfscd
     C                   z-add     mhdnfs        wcdnfscd
     C                   z-add     mddnsn        wcdnhgsn
     C                   z-add     xxhpscdyr     wcsvyr
     C                   z-add     xxhpscdwk     wcsvwk
     C     mdrjhd        add       mdqlhd        wcrchd
      *
     C                   write     wcrec
      *
     C                   endsr
      /eject
      *--------------------------------------------------------------
      * Determine year/week serviced
      *--------------------------------------------------------------
      *
     C     $svyrwk       begsr
      *
      * First, determine the 'serviced' date as 131 days prior to the
      * received date of the movement into this Grow Finish group.
      *
     C                   movel     mhrcdt        wkcymdiso
     C                   subdur    131:*days     wkcymdiso
     C                   movel     wkcymdiso     wksvdt
      *
      * Now, you need the HPS calendar year/week associated with this serviced
      * date.
      *
     C                   call      'HP8010'
     C                   parm                    wksvdt
     C                   parm      0             xxhpscdyr
     C                   parm      0             xxhpscdwk
      *
     C                   endsr
      /EJECT
      *----------------------------------------------------------------
      * Initialization subroutine
      *----------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
     C     key01         klist
     C                   kfld                    mddnsn
     C                   kfld                    mhorfs
     C                   kfld                    xxhpscdyr
     C                   kfld                    xxhpscdwk
      *
     C                   endsr
      /EJECT
