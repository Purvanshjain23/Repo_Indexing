      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Triumph Systems
      * PROGRAM:     TF816
      * TITLE:       Retrieve Beginning/Ending Dates for a Period
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     10/20/05
      *
      * FUNCTION:  Given an incoming year/period, this program retrieves the
      *            beginning/ending period dates and returns them to the calling
      *            program in 3 formats:
      *               1) CCYYMMDD
      *               2) CYYMMDD
      *               3) MMDDYY
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 11/30/06  LeAnne Ramsey
      *           Changed to use the Datamart Calendar file.
      *
      * 12/06/06  LeAnne Ramsey
      *           We changed the file name/text on the Calendar file.
      *
      * 09/10/07  LeAnne Ramsey
      *           Recompile only. We added "sales date" fields to the Calendar file.
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fcalcalc   if   e           k disk
      *  Calendar file
      *
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      ****************************************************************
      * ARRAYS AND TABLES
      ****************************************************************
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Workfields
      *
     D wkcocd          s              3  0 inz(350)
      *
      *
      * Workfields for date manipulation
      *
     D wkisodate       s               D   datfmt(*iso)
      *
      *
      * Parms
      *
     D xxhpsyr         s              4  0
     D xxhpspe         s              2  0
      *
     D xxpbdt          s              8  0
     D xxpbsyndt       s              7  0
     D xxpbmdy         s              6  0
      *
     D xxpedt          s              8  0
     D xxpesyndt       s              7  0
     D xxpemdy         s              6  0
      *
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * Mainline
      ****************************************************************
      *
      * Retrieve the beginning date for the period.
      *
     C     key01         chain     calcalc                            92
     C                   if        *in92 = *off                                 If hit
     C                   z-add     catdt08       xxpbdt
     C                   z-add     catdtsy       xxpbsyndt
     C                   endif                                                  If hit
      *
      *
      * Retrieve the ending date for the period.
      *
     C     key01         setgt     calcalc
     C     key01         readpe    calcalc                                92
     C                   if        *in92 = *off                                 If anything
     C                   z-add     catdt08       xxpedt
     C                   z-add     catdtsy       xxpesyndt
     C                   endif                                                  If anything
      *
      * Flip Beginning Period date to MMDDYY format.
      *
     C     *iso          test(d)                 xxpbdt                 92
     C                   if        *in92 = *off                                 If good date
     C     *iso          move      xxpbdt        wkisodate
     C     *mdy          move      wkisodate     xxpbmdy
     C                   endif                                                  If good date
      *
      * Flip Ending Period date to MMDDYY format.
      *
     C     *iso          test(d)                 xxpedt                 92
     C                   if        *in92 = *off                                 If good date
     C     *iso          move      xxpedt        wkisodate
     C     *mdy          move      wkisodate     xxpemdy
     C                   endif                                                  If good date
      *
     C                   seton                                        lr
      /EJECT
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Parm lists
      *
     C     *entry        plist
     C                   parm                    xxhpsyr
     C                   parm                    xxhpspe
     C                   parm                    xxpbdt
     C                   parm                    xxpbsyndt
     C                   parm                    xxpbmdy
     C                   parm                    xxpedt
     C                   parm                    xxpesyndt
     C                   parm                    xxpemdy
      *
      * Key lists
      *
     C     key01         klist
     C                   kfld                    wkcocd
     C                   kfld                    xxhpsyr
     C                   kfld                    xxhpspe
      *
     C                   endsr
      /EJECT
