      *****************  RPG PROGRAM HEADING  ************************
      * SYSTEM:      HOG PRODUCTION
      * PROGRAM:     HP648
      * TITLE:       ANNUAL SALES FOR TAX REPORT
      * PROGRAMMER:  LEANNE FEDOR
      * CREATED:     03/15/96
      *
      *  FUNCTION:   THIS PROGRAM PRINTS A REPORT OF GROSS SALES
      *              AMOUNTS FOR A SELECTED YEAR.  THE GROSS SALES
      *              ARE ACCUMULATED AT THE BUSINESS OFFICE AND
      *              CUSTOMER STATE LEVEL.  IF THERE WERE CUSTOMERS
      *              WITHOUT STATES IN J. D. EDWAREDS, A LIST OF THE
      *              CUSTOMERS WILL PRINT
      *
      *              TWO WORKFILES ARE BUILT IN HP348. AN OPEN QUERY
      *              STATEMENT IS USED TO SELECT RECORDS FROM THE
      *              SALES MOVEMENT HEADER FILE IN HP448CL.
      *
      *              SELECTION CRITERIA IS PASSED THROUGH THE LDA.
      *
      *              THIS PROGRAM IS CALLED IN HP448CL WHICH SUBMITTED
      *              FROM HP448 THROUGH QCMDEXC.
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 99/99/99
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     FHSP009    IF   E           K DISK
      *  BUSINESS OFFICE             (KEY: BOBOBO)
      *
     FHSP347    IF   E           K DISK
      *                              (KEY: WCCVNO)
      *  ANNUAL SALES FOR TAX REPORT WORKFILE
      *  (CUSTOMERS WITHOUT STATES)
      *
     FHSP348    IF   E           K DISK
      *                              (KEY: WFFSBO WFSTAT)
      *  ANNUAL SALES FOR TAX REPORT WORKFILE
      *  (GROSS SALES DOLLARS)
      *
     FQPRINT    O    F   80        PRINTER OFLIND(*INOF)
      /EJECT
      ****************************************************************
      * INPUT SPECS
      ****************************************************************
      *
      *---------------------------------------------------------------
      *  LDA
      *---------------------------------------------------------------
      *
     D                UDS
     D  LDFSBO                 1      5
     D  LDFYMD                 6     13  0
     D  LDTYMD                14     21  0
     D  LDCCYY                22     25  0
     D  LDBODS                72    101
      *
      *
      *---------------------------------------------------------------
      * STANDARD PROGRAM STATUS DATA STRUCTURE
      *---------------------------------------------------------------
      *    EXTERNALLY DEFINED AS UTPGFR (RECORD FORMAT: PGMDSR)
     D PGMDS         ESDS                  EXTNAME(UTPGFR)
      *
      *
     D  DUMMYD                 1      1
      *
      *---------------------------------------------------------------
      *  NAMED CONSTANTS
      *---------------------------------------------------------------
      *
     D YES             C                   CONST('Y')
     D NO              C                   CONST('N')
     D ALL             C                   CONST('ALL')
      *  THE PRECEDING DUMMY DATA STRUCTURE IS ONLY TO KEEP THE I-SPEC
      *  INFO FROM RUNNING OVER INTO THE 'CALC SPECS' ON THE COMPILE
      *  LISTING.  IT SERVES NO OTHER PURPOSE AND CAN BE DELETED
      *  AT ANY TIME!!!
      /EJECT
      ****************************************************************
      * MAIN LINE
      ****************************************************************
      *
     C     *IN90         DOUEQ     *ON                                          MAIN DO
     C                   READ      HSP348                                 90
     C     *IN90         IFEQ      *OFF                                         IF NOT EOF
      *
     C     FIRST         IFEQ      YES
     C                   EXCEPT    HRHDR
     C                   EXSR      $FSBO
     C                   MOVEL     NO            FIRST
     C                   ENDIF
      *
      *---------------------------------------------------------------
      * CHECK FOR CONTROL BREAK ON BUSINESS OFFICE
      *---------------------------------------------------------------
      *
     C     WFFSBO        IFNE      SVFSBO
     C                   EXCEPT    TBTOT
     C                   EXSR      $FSBO
     C                   ENDIF
      *
      *---------------------------------------------------------------
      * DETAIL PROCESSING
      *---------------------------------------------------------------
      *
      * ACCUMULATE VALUES FOR TOTAL LINES
      *
     C                   ADD       WFGRAM        TBGRAM
     C                   ADD       WFGRAM        TRGRAM
      *
     C     *INOF         IFEQ      *ON
     C                   EXCEPT    HRHDR
     C                   EXCEPT    HBHDR
     C                   ENDIF
     C                   EXCEPT    DTLINE
      *
     C                   ENDIF                                                  IF NOT EOF
     C                   ENDDO                                                  MAIN DO
      *
      *----------------------------------------------------------------
      * EOF PROCESSING
      *----------------------------------------------------------------
      *  IF NO DETAIL RECORDS MATCHED THE SELECTION CRITERIA, THEN
      *  PRINT THE STANDARD 'NO DATA...' ERROR MESSAGE
      *
     C     FIRST         IFEQ      YES                                          IF NOTHING
     C                   EXCEPT    HRHDR
     C                   EXCEPT    NODATA
     C                   ELSE
      *
      * PRINT BUSINESS OFFICE TOTALS
      *
     C     *INOF         IFEQ      *ON
     C                   EXCEPT    HRHDR
     C                   EXCEPT    HBHDR
     C                   ENDIF
     C                   EXCEPT    TBTOT
      *
      * PRINT REPORT OFFICE TOTALS
      *
     C     *INOF         IFEQ      *ON
     C                   EXCEPT    HRHDR
     C                   EXCEPT    HBHDR
     C                   ENDIF
     C                   EXCEPT    TRTOT
     C                   ENDIF                                                  IF NOTHING
      *
      *---------------------------------------------------------------
      * PRINT ALL CUSTOMERS WITHOUT STATES
      *---------------------------------------------------------------
      *
     C                   MOVEL     YES           FIRST
      *
     C     *IN90         DOUEQ     *ON                                          DO NO STATE
     C                   READ      HSP347                                 90
     C     *IN90         IFEQ      *OFF                                         IF NOT EOF
      *
     C     FIRST         IFEQ      YES                                          IF FIRST
     C     *INOF         IFEQ      *ON                                          IF OVERFLOW
     C                   EXCEPT    HRHDR
     C                   ELSE
     C                   EXCEPT    BLANK
     C                   ENDIF                                                  IF OVERFLOW
     C                   EXCEPT    HCHDR
     C                   MOVEL     NO            FIRST
     C                   ENDIF                                                  IF FIRST
      *
     C     *INOF         IFEQ      *ON
     C                   EXCEPT    HRHDR
     C                   EXCEPT    HCHDR
     C                   ENDIF
     C                   EXCEPT    DCLINE
      *
     C                   ENDIF                                                  IF NOT EOF
     C                   ENDDO                                                  DO NO STATE
      *
      *
     C                   SETON                                        LR
      /EJECT
      *----------------------------------------------------------------
      * $FSBO - RETRIEVE BUSINESS OFFICE DESCRIPTION
      *----------------------------------------------------------------
      *
     C     $FSBO         BEGSR
      *
     C     WFFSBO        CHAIN     HSP009                             92
     C     *IN92         IFEQ      *OFF
     C                   MOVEL     BOBODS        HBBODS
     C                   ELSE
     C                   MOVEL(P)  'UNKNOWN'     HBBODS
     C                   ENDIF
      *
     C                   EXCEPT    HBHDR
      *
     C                   MOVEL     WFFSBO        SVFSBO
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * *INZSR - INITIALIZATION SUBROUTINE
      *---------------------------------------------------------------
      *
     C     *INZSR        BEGSR
      *
      * DEFINE SAVE FIELDS FOR CONTROL BREAKS
      *
     C     *LIKE         DEFINE    WFFSBO        SVFSBO
      *
      * DEFINE PRINT FIELDS FOR TOTAL LINES
      *
     C     *LIKE         DEFINE    WFGRAM        TBGRAM
     C     *LIKE         DEFINE    WFGRAM        TRGRAM
      *
      * DEFINE PRINT FIELDS FOR HEADING LINES
      *
     C     *LIKE         DEFINE    BOBODS        HBBODS
      *
      * SET UP REPORT HEADINGS:
      *
      * BUSINESS OFFICE
      *
     C     LDFSBO        IFEQ      *BLANK
     C                   MOVEL(P)  ALL           LDFSBO
     C                   ENDIF
      *
      * SET UP A FIRST TIME FLAG
      * SET UP HEADING UNDERLINE FIELD FOR PRINT
      * RETRIEVE TIME FOR REPORT HEADING
      *
     C                   MOVEL     YES           FIRST             1
     C                   MOVEL     *ALL'-'       DASH             52
     C                   TIME                    RTIME             6 0
      *
     C                   ENDSR
      /EJECT
      *-------------------------------------------------------------
      * REPORT HEADING LINES
      *-------------------------------------------------------------
      *
     OQPRINT    E            HRHDR          1 01
     O                       SDPGM               10
     O                                           47 'HOG PRODUCTION SYSTEM'
     O                                           70 'DATE'
     O                       UDATE         Y     80
      *
     O          E            HRHDR          1
     O                       SDUSR               10
     O                                           44 'ANNUAL SALES FOR TAX '
     O                                           50 'REPORT'
     O                                           70 'TIME'
     O                       RTIME               80 '  :  :  '
      *
     O          E            HRHDR          1
     O                                           70 'PAGE'
     O                       PAGE          Z     80
      *
     O          E            HRHDR          1
     O                                           18 'BUSINESS OFFICE ..'
     O                       LDFSBO              25
     O                       LDBODS              57
      *
     O          E            HRHDR          2
     O                                           18 'YEAR .............'
     O                       LDCCYY        Z     24
      *
      *-------------------------------------------------------------
      * COLUMN HEADING LINES
      *-------------------------------------------------------------
      *
     O          E            HRHDR          1
     O                                           18 'CUSTOMER'
     O                                           39 'GROSS SALES'
      *
     O          E            HRHDR          1
     O                                           15 'STATE'
     O                                           39 'AMOUNT'
      *
     O          E            HRHDR          2
     O                       DASH                52
      *
      *-------------------------------------------------------------
      * HEADING FOR BUSINESS OFFICE
      *-------------------------------------------------------------
      *
     O          E            HBHDR          2
     O                                           16 'BUSINESS OFFICE:'
     O                       WFFSBO              23
     O                       HBBODS              55
      *
      *-------------------------------------------------------------
      * DETAIL LINE OF GROSS SALES AMOUNTS BY STATE
      *-------------------------------------------------------------
      *
     O          E            DTLINE         1
     O                       WFSTAT              15
     O                       WFGRAM        K     40
      *
      *-------------------------------------------------------------
      * BUSINESS OFFICE TOTAL LINE
      *-------------------------------------------------------------
      *
     O          E            TBTOT       1  2
     O                                           13 'TOTAL FOR'
     O                       SVFSBO              19
     O                       TBGRAM        KB    40
      *
      *-------------------------------------------------------------
      * REPORT TOTALS
      *-------------------------------------------------------------
      *
     O          E            TRTOT          1
     O                                           19 'REPORT TOTAL'
     O                       TRGRAM        K     40
      *
      *
      *-------------------------------------------------------------
      * PRINT A BLANK LINE BEFORE
      *-------------------------------------------------------------
      *
     O          E            BLANK       2  1
      *
      *-------------------------------------------------------------
      * HEADING LINES FOR CUSTOMERS WITHOUT STATES.
      *-------------------------------------------------------------
      *
     O          E            HCHDR          2
     O                                           22 'CUSTOMERS WHO DID NOT'
     O                                         +  1 'HAVE A STATE IN J. D.'
     O                                         +  1 'EDWARDS'
     O          E            HCHDR          1
     O                                           10 'CUSTOMER'
      *
     O          E            HCHDR          2
     O                                           10 'NUMBER'
     O                                           28 'CUSTOMER NAME'
      *
      *-------------------------------------------------------------
      * DETAIL LINE OF CUSTOMERS WITHOUT STATES.
      *-------------------------------------------------------------
      *
     O          E            DCLINE         1
     O                       WCCVNO        Z     10
     O                       WCNAME              55
      *
      *-------------------------------------------------------------
      * NO DATA MESSAGE LINE
      *-------------------------------------------------------------
      *
     O          E            NODATA         1
     O                                           19 'NO DATA MEETS YOUR'
     O                                           38 'SELECTION CRITERIA'
