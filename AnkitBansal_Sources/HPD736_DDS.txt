     A*%%TS  SD  19980302  103503  ISLRAMS     REL-V3R7M0  5716-PW1
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION
     A*  DISPLAY FILE:  HPD736
     A*  TITLE:         VIEW CHECK DETAIL
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  DATE:          06/21/95
     A*
     A*----------------------------------------------------------------
     A*  INDICATORS:
     A*
     A*  90  THE CHECK THE USER HAS SELECTED FROM THE WORK WITH HAS
     A*      BEEN DELETED BY ANOTHER USER SINCE SELECTED
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(FE)
     A                                      PRINT
     A*---------------------------------------------------------------
     A* SUBFILE FORMAT 1
     A*---------------------------------------------------------------
     A*
     A          R HP736S1                   SFL
     A*%%TS  SD  19980302  102555  ISLRAMS     REL-V3R7M0  5716-PW1
     A  85                                  SFLNXTCHG
     A            HDRRN1         4Y 0H
     A            HDLNNO    R        H      REFFLD(CDLNNO)
     A            DFOPT          2A  B 17  2
     A  30N90                               DSPATR(PC)
     A  30N90                               DSPATR(RI)
     A N30N90                               DSPATR(UL)
     A N30N90                               DSPATR(HI)
     A  90                                  DSPATR(ND)
     A  90                                  DSPATR(PR)
     A            DFTPCD    R        O 17  6REFFLD(@@TPCD)
     A            DFMVSN    R        O 17  9REFFLD(@@MVSN)
     A                                      EDTCDE(Z)
     A            DFHGCD    R        O 17 17REFFLD(HGHGCD)
     A            DFCYFL    R        O 17 25REFFLD(@@FLAG)
     A            DFLVHD    R        O 17 27REFFLD(CDLVHD)
     A                                      EDTCDE(M)
     A            DFLVLB    R        O 17 34REFFLD(CDLVLB)
     A                                      EDTCDE(M)
     A            DFDOHD    R        O 17 41REFFLD(CDDOHD)
     A                                      EDTCDE(M)
     A            DFDOLB    R        O 17 48REFFLD(CDDOLB)
     A                                      EDTCDE(M)
     A            DFGRAM    R        O 17 55REFFLD(CDGRAM)
     A                                      EDTCDE(K)
     A            DFKMDY    R     Y  O 17 66REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A            DFTANO    R     Y  O 17 76REFFLD(CDTANO)
     A                                      EDTCDE(Z)
     A            DFCDHD    R        O 18 27REFFLD(CDCDHD)
     A                                      EDTCDE(Z)
     A            DFCDLB    R        O 18 34REFFLD(CDCDLB)
     A                                      EDTCDE(Z)
     A            DFYDHD    R        O 18 41REFFLD(CDYDHD)
     A                                      EDTCDE(Z)
     A            DFYDLB    R        O 18 48REFFLD(CDYDLB)
     A                                      EDTCDE(Z)
     A*
     A*----------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT  1
     A*----------------------------------------------------------------
     A*
     A          R HP736C1                   SFLCTL(HP736S1)
     A*%%TS  SD  19980302  102555  ISLRAMS     REL-V3R7M0  5716-PW1
     A                                      SFLSIZ(0050)
     A                                      SFLPAG(0002)
     A                                      CF03(03 'EXIT')
     A N90                                  CF05(05 'REFRESH')
     A N90                                  CF13(13 'DEDUCT/ADDONS')
     A N90                                  CF18(18 'TOP OF SUBFILE')
     A N90N82                               ROLLUP(25 'PAGE FORWARD')
     A                                      OVERLAY
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND(*MORE)
     A                                      SFLDROP(CF16)
     A                                      SETOF(30)
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 26'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  2 28'VIEW CHECK DETAIL'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  4  2'Sale type ........:'
     A            C1STCD    R        O  4 23REFFLD(HSRREF/STSTCD *LIBL/HSPREF)
     A            C1STDS    R        O  4 30REFFLD(HSRREF/STSTDS *LIBL/HSPREF)
     A                                  4 70'**Per CWT**'
     A                                  5 70'Carcass lbs'
     A                                  6  2'Customer number ..:'
     A            C1CVNO    R        O  6 23REFFLD(CHCVNO)
     A                                      EDTCDE(Z)
     A                                  6 35'Base amount ......:'
     A            C1BAAM    R        O  6 55REFFLD(CHCHAM)
     A                                      EDTCDE(K)
     A            C1BCWT    R        O  6 71REFFLD(@@NU72)
     A                                      EDTCDE(K)
     A            C1CNAM    R        O  7  2REFFLD(@@DS30 HSPREF)
     A                                  7 35'Premium/discount .:'
     A            C1PDAM    R        O  7 55REFFLD(CHCHAM)
     A                                      EDTCDE(K)
     A            C1PCWT    R        O  7 71REFFLD(@@NU72)
     A                                      EDTCDE(K)
     A                                  8 73'Live lbs'
     A                                  9  2'Check date ...:'
     A            C1CMDY    R        O  9 19REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A                                  9 35'Gross amount .....:'
     A            C1GRAM    R        O  9 55REFFLD(CHCHAM)
     A                                      EDTCDE(K)
     A            C1GCWT    R        O  9 71REFFLD(@@NU72)
     A                                      EDTCDE(K)
     A                                 10  2'Check number .:'
     A            C1CHNO    R        O 10 19REFFLD(HSRREF/CHCHNO *LIBL/HSPREF)
     A                                      EDTCDE(Z)
     A                                 10 35'Deduction/add-on .:'
     A            C1DAAM    R        O 10 55REFFLD(CHCHAM)
     A                                      EDTCDE(K)
     A            C1DCWT    R        O 10 71REFFLD(@@NU72)
     A                                      EDTCDE(K)
     A                                 11  2'Check amount .:'
     A            C1CHAM    R        O 11 19REFFLD(CHCHAM)
     A                                      EDTCDE(K)
     A                                 11 35'Net amount .......:'
     A            C1NTAM    R        O 11 55REFFLD(CHCHAM)
     A                                      EDTCDE(K)
     A            C1NCWT    R        O 11 71REFFLD(@@NU72)
     A                                      EDTCDE(K)
     A                                 12  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                 13  2'5=View Carcass Data and Premiums/d-
     A                                      iscounts'
     A                                      COLOR(BLU)
     A                                 14 28'Head'
     A                                      DSPATR(HI)
     A                                 14 33'Pounds'
     A                                      DSPATR(HI)
     A                                 14 42'Head'
     A                                      DSPATR(HI)
     A                                 14 47'Pounds'
     A                                      DSPATR(HI)
     A                                 15  5'Pay'
     A                                      DSPATR(HI)
     A                                 15 11'Sales'
     A                                      DSPATR(HI)
     A                                 15 25'C'
     A                                      DSPATR(HI)
     A                                 15 28'*** Live **'
     A                                      DSPATR(HI)
     A                                 15 42'*** DOA ***'
     A                                      DSPATR(HI)
     A                                 15 59'Gross'
     A                                      DSPATR(HI)
     A                                 15 68'Kill'
     A                                      DSPATR(HI)
     A                                 16  1'Opt'
     A                                      DSPATR(HI)
     A                                 16  5'Typ'
     A                                      DSPATR(HI)
     A                                 16 12'Move'
     A                                      DSPATR(HI)
     A                                 16 19'Group'
     A                                      DSPATR(HI)
     A                                 16 25'Y'
     A                                      DSPATR(HI)
     A                                 16 28'*Condemned*'
     A                                      DSPATR(HI)
     A                                 16 42'*Yard Dead*'
     A                                      DSPATR(HI)
     A                                 16 58'Amount'
     A                                      DSPATR(HI)
     A                                 16 68'Date'
     A                                      DSPATR(HI)
     A                                 16 75'Tattoo'
     A                                      DSPATR(HI)
     A*
     A*----------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*----------------------------------------------------------------
     A          R HP736K1
     A*%%TS  SD  19980302  103503  ISLRAMS     REL-V3R7M0  5716-PW1
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A N90                             23 11'F5=Refresh'
     A N90                             23 23'F13-View Deductions/Add-ons'
     A N90                             23 52'F16=Fold/Unfold'
     A                                 23 69'F18-Top'
      *----------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *----------------------------------------------------------------
     A          R HP736ES                   SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
     A*----------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*----------------------------------------------------------------
     A*
     A          R HP736EC                   SFLCTL(HP736ES)
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
