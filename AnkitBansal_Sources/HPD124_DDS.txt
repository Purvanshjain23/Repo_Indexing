     A*%%TS  SD  19950301  160728  ISLRAMS     REL-V3R0M5  5763-PW1
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD124
     A*  TITLE:         REPLACE A FUNCTION FOR AUTHORIZED USERS
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  DATE:          02/14/95
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A                                      CF04(04 'PROMPT')
     A  44                                  CF09(09 'REPLACE')
     A                                      CA12(12 'PREVIOUS')
     A*
     A*--------------------------------------------------------------*
     A* RECORD FORMAT ONE
     A*--------------------------------------------------------------*
     A          R HP124R1
     A*%%TS  SD  19950301  160728  ISLRAMS     REL-V3R0M5  5763-PW1
     A                                      SETOF(30)
     A                                      SETOF(31)
     A                                      SETOF(50)
     A                                      SETOF(51)
     A                                      OVERLAY
     A                                      RTNCSRLOC(&RCD &FLD)
     A            RCD           10A  H
     A            FLD           10A  H
     A                                  1  3'HP124'
     A                                      DSPATR(HI)
     A                                  1 27'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  2 17'REPLACE A FUNCTION FOR AUTHORIZED -
     A                                      USERS'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  4  2'Function code to be replaced:'
     A                                  6  7'Function code ..'
     A                                  6 24'?'
     A            C1FNCD    R        B  6 26REFFLD(FNFNCD)
     A N30                                  DSPATR(HI)
     A  30                                  DSPATR(RI)
     A  30
     AO 50                                  DSPATR(PC)
     A            C1FNDS    R        O  6 38REFFLD(FNFNDS)
     A                                  8  2'Replace all authorized function re-
     A                                      cords in HSP003-Authorized Function-
     A                                      s'
     A                                  9  2'for the above function code with t-
     A                                      he secured function entered below:'
     A                                 11  7'Function code ..'
     A                                 11 24'?'
     A            DFFNCD    R        B 11 26REFFLD(FNFNCD)
     A N31                                  DSPATR(HI)
     A  31                                  DSPATR(RI)
     A  31
     AO 51                                  DSPATR(PC)
     A            DFFNDS    R        O 11 38REFFLD(FNFNDS)
     A*
     A*--------------------------------------------------------------*
     A* COMMAND KEY RECORD 1
     A*--------------------------------------------------------------*
     A          R HP124K1
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A                                 23 11'F4=Prompt'
     A  44                             23 22'F9=Replace'
     A                                 23 34'F12=Previous'
      *
      *--------------------------------------------------------------*
      * ERROR MESSAGE SUBFILE FORMAT
      *--------------------------------------------------------------*
     A          R HP124ES                   SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
     A*                                                                *********
     A*--------------------------------------------------------------*
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*--------------------------------------------------------------*
     A*
     A          R HP124EC                   SFLCTL(HP124ES)
     A*%%TS  SD  19940705  110806  ISRCENT     REL-V2R2M0  5738-PW1
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
