/*                                                                   */
/*  Program  . . . . .   J0028DTA                                    */
/*                                                                   */
/*  Description  . . .   Convert Date and User Data Area Creation    */
/*                                                                   */
/*  Program Revision Log                                             */
/*  --------------------                                             */
/*                                                                   */
/*       Date        Programmer        Description                   */
/*     --------      ----------     -------------------------------- */
/*     10/19/92          KREUTZ     SAR # 411694                     */
/*     12/15/95          BECK       SAR # 1089414                    */
/*                                                                   */
/*********************************************************************/
J0028DTA:    PGM
/*                                                                   */
/* --------- Define program file(s) and variable(s). --------------- */
/*                                                                   */
             DCL      VAR(&FRMT    ) TYPE(*CHAR)  LEN(3)
             DCL      VAR(&FRMT2   ) TYPE(*CHAR)  LEN(4)
             DCL      VAR(&DSEP    ) TYPE(*CHAR)  LEN(1)
             DCL      VAR(&LNGP    ) TYPE(*CHAR)  LEN(2)
             DCL      VAR(&CO      ) TYPE(*CHAR)  LEN(5)
             DCL      VAR(&DECF    ) TYPE(*CHAR)  LEN(1)
             DCL      VAR(&VRSP    ) TYPE(*CHAR)  LEN(6)
             DCL      VAR(&CUR     ) TYPE(*CHAR)  LEN(1)
             DCL      VAR(&CTR     ) TYPE(*CHAR)  LEN(3)
             DCL      VAR(&UPREF   ) TYPE(*CHAR)  LEN(22)
             DCL      VAR(&LANGMSGF) TYPE(*CHAR)  LEN(10)
/*                                                                   */
/* --------- Global CPF error intercept ---------------------------- */
/*                                                                   */
             MONMSG     MSGID(CPF0000 CPC0000 CPD0000) EXEC(GOTO ABEND)
/*                                                                   */
/* --------- Load Default Values for Date and Separator ------------ */
/*                                                                   */
             RTVSYSVAL  SYSVAL(QDATFMT) RTNVAR(&FRMT)
             RTVSYSVAL  SYSVAL(QDATSEP) RTNVAR(&DSEP)
             RTVSYSVAL  SYSVAL(QCURSYM) RTNVAR(&CUR)
             RTVSYSVAL  SYSVAL(QDECFMT) RTNVAR(&DECF)
/*                                                                   */
/* ---Load Data Area with User Display Preferences ----------------- */
/*                                                                   */
             CHGVAR     VAR(&UPREF ) VALUE(&FRMT || &DSEP ||         +
                        &LNGP || &CO || &DECF || &VRSP || &CUR || &CTR)
             CALL       PGM(X00281  ) PARM(&UPREF)
/*                                                                   */
/* --------- Create User Data Area --------------------------------- */
/*                                                                   */
             CRTDTAARA DTAARA(QTEMP/X0028) TYPE(*CHAR)               +
                       LEN(512) VALUE( &UPREF )                      +
                     TEXT('System Date and User Preference Data Area.')
             MONMSG     MSGID(CPF0000 CPC0000 CPD0000) EXEC(DO)
                 RCVMSG     MSGQ(*PGMQ) RMV(*YES)
                 SNDPGMMSG  MSGID(JDE9005) MSGF(QJDEMSG) TOPGMQ(*EXT)
                 MONMSG     MSGID(CPF0000 CPC0000 CPD0000)
                 GOTO       EOJ
             ENDDO
/*                                                                   */
/* --------- Change Job for Data Area Attributes ------------------- */
/*                                                                   */
             CHGVAR     VAR(&FRMT2) VALUE('*' || %SST(&UPREF 1 3))
             CHGVAR     VAR(&DSEP ) VALUE(%SST(&UPREF 4 1))
             CHGJOB     DATFMT(&FRMT2) DATSEP(&DSEP)
             MONMSG     MSGID(CPF0000 CPC0000 CPD0000)
/*                                                                   */
/* --------- Branch to normal EOJ ---------------------------------- */
/*                                                                   */
             RMVMSG     CLEAR(*ALL)
GOTO  EOJ
/*                                                                   */
/* --------- Abnormal end of job processing ------------------------ */
/*                                                                   */
ABEND:
             SNDPGMMSG MSGID(JDE9999) MSGF(QJDEMSG) +
                       MSGDTA('J0028DTA  ') TOPGMQ(*EXT)
             MONMSG     MSGID(CPF0000 CPC0000 CPD0000)
             CHGJOB     LOG(4 00 *SECLVL) SWS(1XXXXXXX)
             MONMSG     MSGID(CPF0000 CPC0000 CPD0000)
/*                                                                   */
/* --------- End of Job -------------------------------------------- */
/*                                                                   */
EOJ:
             ENDPGM
