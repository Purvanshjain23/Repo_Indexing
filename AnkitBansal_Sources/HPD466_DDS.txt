     A*%%TS  SD  20000207  151312  ISLRAMS     REL-V4R2M0  5769-PW1
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD466
     A*  TITLE:         SPECIFY EXPENSE TICKET REPORT
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  DATE:          05/31/94
     A*  PROGRAMMER:    BARB GUTIERREZ   04/15/16
     A*                 BUSINESS OFFICE IS NOW A REQUIRED ENTRY SINCE
     A*                 WE HAVE MULTIPLE LIVE OFFICES (OKLIV AND IFLIV).
     A*                  - CHANGED FIELD NAME DFALBO TO DFBOBO.  SHOULD PULL
     A*                    FROM REGULAR BUSINESS OFFICE, NOT ALLOCATED BUSINESS
     A*                    OFFICE.
     A*                 (E5290)
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A N99                                  CF04(04 'PROMPT')
     A N99 44                               CF08(08 'SUBMIT')
     A N99                                  CF17(17 'PRINT OPTIONS')
     A N99                                  CF20(20 'WRKSPLF')
     A*
     A*--------------------------------------------------------------*
     A* RECORD FORMAT ONE
     A*--------------------------------------------------------------*
     A          R HP466R1
     A*%%TS  SD  19950119  144532  ISLRAMS     REL-V3R0M5  5763-PW1
     A                                      SETOF(30)
     A                                      SETOF(31)
     A                                      SETOF(33)
     A                                      SETOF(34)
     A                                      SETOF(35)
     A                                      SETOF(36)
     A                                      SETOF(37)
     A                                      SETOF(38)
     A                                      SETOF(39)
     A                                      SETOF(40)
     A                                      SETOF(47)
     A                                      SETOF(48)
     A                                      SETOF(50)
     A                                      SETOF(51)
     A                                      SETOF(53)
     A                                      SETOF(54)
     A                                      SETOF(55)
     A                                      SETOF(56)
     A                                      SETOF(57)
     A                                      SETOF(58)
     A                                      OVERLAY
     A                                      RTNCSRLOC(&RCD &FLD)
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 27'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  2 19'SPECIFY EXPENSE TICKET REPORT OPTI-
     A                                      ONS'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  4  2'Business office ...'
     A                                  4 22'?'
     A            DFBOBO    R        B  4 24REFFLD(BOBOBO)
     A N31N99                               DSPATR(HI)
     A  31
     AO 51                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A            DFBODS    R        O  4 38REFFLD(BOBODS)
     A                                  5  2'From begin date ...'
     A            DFFMDY    R     Y  B  5 24REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A N39N99                               DSPATR(HI)
     A  39                                  DSPATR(PC)
     A  39                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A                                  6  2'To begin date .....'
     A            DFTMDY    R     Y  B  6 24REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A N40N99                               DSPATR(HI)
     A  40                                  DSPATR(PC)
     A  40                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A                                  8  2'Region ............'
     A                                  8 22'?'
     A            DFRGCD    R        B  8 24REFFLD(XTREC/XTRGCD *LIBL/HSP048)
     A N30N99                               DSPATR(HI)
     A  30
     AO 50                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A            DFRGDS    R        O  8 38REFFLD(RGREC/RGRGDS *LIBL/HSP008)
     A                                  9  2'Production phase ..'
     A                                  9 22'?'
     A            DFPPCD    R        B  9 24REFFLD(PPPPCD)
     A N33N99                               DSPATR(HI)
     A  33
     AO 53                                  DSPATR(PC)
     A  33                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A            DFPPDS    R        O  9 38REFFLD(@@DS30)
     A                                 11  2'Farm site .........'
     A                                 11 22'?'
     A            DFFSCD    R        B 11 24REFFLD(FSFSCD)
     A                                      EDTCDE(M)
     A N34N99                               DSPATR(HI)
     A  34
     AO 54                                  DSPATR(PC)
     A  34                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A            DFFSNM    R        O 11 38REFFLD(@@NM25)
     A                                 12  2'Hog group .........'
     A                                 12 22'?'
     A            DFHGCD    R        B 12 24REFFLD(HGREC/HGHGCD *LIBL/HSP034)
     A N35N99                               DSPATR(HI)
     A  35
     AO 55                                  DSPATR(PC)
     A  35                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A                                 13  2'Cost center .......'
     A                                 13 22'?'
     A            DFCJD     R        B 13 24REFFLD(XTCJD)
     A                                      CHECK(RB)
     A N47N99                               DSPATR(HI)
     A  47
     AO 48                                  DSPATR(PC)
     A  47                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A            DFCCDS        30   O 13 38
     A                                 14  2'Expense class .....'
     A                                 14 22'?'
     A            DFECCD    R        B 14 24REFFLD(XTREC/XTECCD *LIBL/HSP048)
     A N36N99                               DSPATR(HI)
     A  36
     AO 56                                  DSPATR(PC)
     A  36                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A            DFECDS    R        O 14 38REFFLD(HSRREF/ECECDS *LIBL/HSPREF)
     A                                 15  2'Expense subclass ..'
     A                                 15 22'?'
     A            DFESCD    R        B 15 24REFFLD(XTREC/XTESCD *LIBL/HSP048)
     A N37N99                               DSPATR(HI)
     A  37
     AO 57                                  DSPATR(PC)
     A  37                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A            DFESDS    R        O 15 38REFFLD(HSRREF/ESESDS *LIBL/HSPREF)
     A                                 16  2'Expense code ......'
     A                                 16 22'?'
     A            DFEXCD    R        B 16 24REFFLD(XTREC/XTEXCD *LIBL/HSP048)
     A N38N99                               DSPATR(HI)
     A  38
     AO 58                                  DSPATR(PC)
     A  38                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A            DFEXDS    R        O 16 38REFFLD(HSRREF/EXEXDS *LIBL/HSPREF)
     A*
     A*--------------------------------------------------------------*
     A* COMMAND KEY RECORD 1
     A*--------------------------------------------------------------*
     A          R HP466K1
     A*%%TS  SD  20000207  151312  ISLRAMS     REL-V4R2M0  5769-PW1
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A N99                             23 11'F4=Prompt'
     A  44N99                          23 22'F8=Submit'
     A N99                             23 33'F17=Print Options'
     A N99                             23 52'F20=Spool Files'
      *
      *--------------------------------------------------------------*
      * ERROR MESSAGE SUBFILE FORMAT
      *--------------------------------------------------------------*
     A          R HP466ES                   SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
     A*--------------------------------------------------------------*
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*--------------------------------------------------------------*
     A*
     A          R HP466EC                   SFLCTL(HP466ES)
     A*%%TS  SD  19940415  103325  ISRCENT     REL-V2R2M0  5738-PW1
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
