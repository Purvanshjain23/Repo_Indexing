      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Hog Production
      * PROGRAM:     HP967
      * TITLE:       Purge Feed Ticket files HSP037, HSP038, HSP059
      * PROGRAMMER:  Brad Baden
      * CREATED:      8/07/2020
      * WHD / TFS:   WHD67665 / 16740
      *
      * FUNCTION:    This batch program purges records in the Feed Ticket files
      *
      /EJECT
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * xx/xx/xxxx  programmer...   tfs#...
      *             xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
      *             xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
      *
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fhsl034d   if   e           k disk
      *  Hog group
      *
      *
     Fhsl037l   uf   e           k disk
      *  Feed ticket header       (LF selects only Posted tickets)
      *
      *
     Fhsl037a   if   e           k disk    rename(fhrec:fhreca) prefix(p1)
      *  Feed ticket header
      *
      *
     Fhsp038    uf   e           k disk
      *  Feed ticket detail
      *
      *
     Fhsp059    uf   e           k disk
      *  Feed pickup ticket
      *
      *
      *----------------------------------------------------------------
      * Purge files that will be backed up to tape
      *----------------------------------------------------------------
      *
     Fhsp037prx o    e           k disk    rename(fhrec:fhrecpr)
      *  Feed ticket header
      *
      *
     Fhsp038prx o    e           k disk    rename(fdrec:fdrecpr)
      *  Feed ticket detail
      *
      *
     Fhsp059prx o    e           k disk    rename(fprec:fprecpr)
      *  Feed pickup ticket
      *
      *
      /EJECT
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      *
      ****************************************************************
      * ARRAYS AND TABLES
      ****************************************************************
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Save/Control fields
      *
     d purgfl          s              1    inz('Y')
     d svfmbo          s                   like(fhfmbo)
     d svtrcd          s                   like(fhtrcd)
     d svrtno          s                   like(fhrtno)
     d svtkno          s                   like(fhtkno)
      *
      *
      * Parameters
      *
     d cutoffdate      s              8  0
     d feedmilloffice  s              5
      *
      *
      * Workfields
      *
     d wkhgsn          s                   like(hghgsn)
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *---------------------------------------------------------------
      * Standard database file information data structure
      *---------------------------------------------------------------
      *    externally defined as UTDBGR (record format: FDBCKD)
     D dbfeed        e ds                  extname(utdbfr)
      *
      *
      *---------------------------------------------------------------
      * Local data area.
      *---------------------------------------------------------------
      *
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ***************************************************************************************
      * Mainline
      ***************************************************************************************
      *
      * Feed tickets
     C                   exsr      $feed
      *
      *
     C                   seton                                        lr
      /EJECT
      *------------------------------------------------------------------------------------
      * Feed Tickets
      *------------------------------------------------------------------------------------
      *
      * Feed ticket must be posted to Period End with a Post Date on/before the
      * Purge Date.  (Logical selects only Posted tickets)
      *
     C     $feed         begsr
      *
     C     *loval        setll     hsl037l
      *
     C                   dou       *in91 = *on or fhepdt > cutoffdate           Do feed
     C                   read      hsl037l                                91
     C                   if        *in91 = *off and                             If not EOF
     C                             fhepdt <= cutoffdate and
     C                             fhfmbo = feedmilloffice
      *
     C                   move      yes           purgfl
     C                   movel     fhfmbo        svfmbo
     C                   z-add     fhrtno        svrtno
     C                   z-add     fhtkno        svtkno
     C                   movel     fhtrcd        svtrcd
      *
      * For Delivery Tickets, check that all tickets with this Reference
      * Ticket Number are Posted.
      *
     C                   if        fhtrcd = 'D'
     C                   exsr      $feedref
     C                   endif
      *
      * Verify that all groups on the ticket are Closed/Voided.
      *
     C                   if        purgfl = yes
     C                   exsr      $feedgrp
     C                   endif
      *
      * Delete Feed Ticket Header/Detail
      *
     C                   if        purgfl = yes
     C                   exsr      $feeddlt
     C                   endif
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Do feed
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------------------
      * Check the Status of all Tickets for the Reference Ticket
      *---------------------------------------------------------------------------
      *
      * A Delivery Feed Ticket may be referenced by 'PICKUP' or 'ADJUSTMENT' feed
      * tickets. All of the tickets must have a Status of Posted and a Posted Date
      * on/before the Purge Date.
      *
     C     $feedref      begsr
      *
     C     key01         setll     hsl037a
      *
     C                   dou       *in93 = *on or purgfl = no                   Do reference
     C     key01         reade     hsl037a                                93
     C                   if        *in93 = *off and                             If not EOF
     C                             (p1fhtscd <> 'PP' or p1fhepdt > cutoffdate)
     C                   move      no            purgfl
     C                   endif                                                  If not EOF
     C                   enddo                                                  Do reference
      *
     C                   endsr
      /EJECT
      *-------------------------------------------------------------------------
      * Check the Status of all Groups on the Feed Ticket
      *-------------------------------------------------------------------------
      *
      * All groups on the Feed Ticket must be Closed/Voided.
      * (Except BGF groups--which are never Closed.)
      *
     C     $feedgrp      begsr
      *
     C     key02         setll     hsp038
      *
     C                   dou       *in93 = *on or purgfl = no                   Do feed group
     C     key02         reade(n)  hsp038                                 93
     C                   if        *in93 = *off                                 If not EOF
      *
     C     fdhgsn        chain(n)  hsl034d                            92
     C                   if        *in92 = *off and                             If not eligible
     C                             hgppcd <> 'BGF  ' and
     C                             hggscd <> 'CL' and
     C                             hggscd <> 'VD'
     C                   move      no            purgfl
     C                   endif                                                  If not eligible
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Do feed group
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------------------
      * Delete Feed Ticket Detail and Header
      *---------------------------------------------------------------------------
      *
     C     $feeddlt      begsr
      *
      * Delete all Feed Ticket Detail records for this header
      *
     C     key02         setll     hsp038
      *
     C                   dou       *in94 = *on                                  Do detail
     C     key02         reade     hsp038                                 94
     C                   if        *in94 = *off
      *
     C*                  z-add     fdhgsn        wkhgsn
     C*                  exsr      $hgsn
      *
     C                   write     fdrecpr
     C                   delete    fdrec
     C                   endif
     C                   enddo                                                  Do detail
      *
      * If this is a Pickup Ticket, delete all records for the ticket in the
      * file that holds the "TO" side of the Pickup Ticket.
      *
     C                   if        svtrcd = 'P'                                 If pickup
      *
     C     key02         setll     hsp059
     C                   dou       *in94 = *on                                  Do to pickup
     C     key02         reade     hsp059                                 94
     C                   if        *in94 = *off
     C                   write     fprecpr
     C                   delete    fprec
     C                   endif
     C                   enddo                                                  Do to pickup
      *
     C                   endif                                                  If pickup
      *
      *
      * Delete Feed Ticket Header
      *
     C                   write     fhrecpr
     C                   delete    fhrec
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------------------------------
      * Initialization
      *---------------------------------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Parm lists
     c     *entry        plist
     c                   parm                    cutoffdate
     c                   parm                    feedmilloffice
      *
      * Key fields
      *
     C     key01         klist
     C                   kfld                    svfmbo
     C                   kfld                    svrtno
      *
     C     key02         klist
     C                   kfld                    svfmbo
     C                   kfld                    svtkno
     C                   kfld                    svtrcd
      *
     C                   endsr
      /EJECT
