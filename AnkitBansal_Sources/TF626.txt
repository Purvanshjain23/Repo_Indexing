      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Triumph Foods
      * PROGRAM:     TF626 - AR Adjustment Listing
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     01/04/06
      *
      * Function:    This program prints the AR Adjustment Listing.
      *
      *              The listing is generated:
      *                1) on demand
      *                2) from the Weekly Revenue Close function (detail version)
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 01/22/08  LeAnne Ramsey
      *           Changed workfile to a permanent file.
      *           Changed layout. Changed name from TF610 to TF626.
      *
      * 01/30/08  LeAnne Ramsey
      *           We have added more selections; added them to the Report Heading.
      *
      * 06/04/08  LeAnne Ramsey
      *           Added logic to condition printing of From/To WeekEnding Date in
      *           the heading. When this report is generated from the Revenue Close
      *           we will print WeekEnding Date instead of the From/To Dates.
      *
      * 09/09/09  LeAnne Ramsey
      *           Recompile only. New field "AR Customer Number" was added to
      *           the AR Adjustments file.
      *
      * 09/10/09  LeAnne Ramsey
      *           1) Changed Sales Expense fields      (new/changed fields in TFP026)
      *           2) Added a selection on AR Customer  (new field in TFP026)
      *           3) Added a "summary" version
      *
      * 10/01/09  LeAnne Ramsey
      *           Added 2 new selections:   G/L Post Method and Payout Code
      *           Added 2 new columns:      G/L Post Method and Payout Code
      *           Added a new report total: AR Adjustment Payout Amount
      *           Changed sort sequence:    Inserted Post Method before Cost Center
      *
      * 09/18/16  Danny Nguyen (E6933)
      *           On detail version report, added '*' next to the Actual Ship Date if
      *           and when A/R Header Ext field 'AR Ord Ref Actual Ship Date' is not 0.
      *           Add constant heading to let user know the meaning behind '*'.
      *           The '*' identifies Actual Ship Date that was derived from the 'old' logic
      *           in the '$getdt' routine in TF213 program.
      *           A conversion process was built to populate the 'AR Ord Ref Actual Ship Date'
      *           when the Order does not have a Remaining Bal Due in the A/R Header record.
      *           Conversion process will go live on late Sun night, 11/27/16. TF program changes
      *           will go live on Mon, 11/28/16, after WE Revenue Close is completed.
      *
      * 05/30/17  Danny Nguyen (T9996)
      *           Added field to TFP026 - AR Adjustments file:
      *             AJINSF - Invoice Suffix
      *           Added Invoice Suffix to report.
      /eject
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Ftfl026a   if   e           k disk
      *  AR Adjustments  (records selected with OPNQRY)
      *
      *
E6933FPBC0CPL1  if   e           k disk
  |   *   AR Header Ext
  |   *
E6933 *
     Fprint198  o    f  198        printer oflind(*inof)
      *
      /eject
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      ****************************************************************
      * ARRAYS AND TABLES
      ****************************************************************
      *
     D artram          s             11  2 dim(4)
     D artnam          s             11  2 dim(4)
     D arsnam          s             11  2 dim(4)
     D arasxam         s             11  2 dim(4)
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Standard fields
      *
     D dash            s            198    inz(*all'-')
     d rtime           s              6  0
E6933 **
  |   ** Go live date for Actual Ship Date derived from Original Invoice Ref. Is the date
  |   ** when the conversion program to build the A/R Header Ext goes live.
E6933D w_asdgolivedte  s              8    inz('11/27/16')
      *
      *
      * Control fields
      *
     D count           s              3  0
     D linecount       s              3  0
     D first           s              1    inz('Y')
     D svcono          s                   like(ajcono)
     D svrcexfl        s                   like(ajrcexfl)
     D svcc            s                   like(ajcc)
     D svobj           s                   like(ajobj)
     D svsub           s                   like(ajsub)
      *
      * Note: We are not breaking on the save fields below. BUT, Alice wanted to print
      * them on the Summary Report. So, we just save them at the control breaks.
      *
     D svajty          s                   like(ajajty)
     D svpocd          s                   like(ajpocd)
     D svpmcd          s                   like(ajpmcd)
      *
      *
      * Workfields for date manipulation
      *
     D wkisodate       s               D   datfmt(*iso)
      *
      *
      * Workfields for accumulating
      *
     D wktrnonam       s                   like(ajtram)
     D wksnonam        s                   like(ajtram)
     D wktnonam        s                   like(ajtram)
     D wkanonam        s                   like(ajtram)
      *
     D wktrexam        s                   like(ajtram)
     D wksexam         s                   like(ajtram)
     D wktexam         s                   like(ajtram)
     D wkaexam         s                   like(ajtram)
      *
      *
      * Parm fields
      *
     d xxdsfl          s              1
      *
      *
      * Print fields
      *
     d l1asdtmdy       s              6  0
     d l1cwedtmdy      s              6  0
     d l1dpdtmdy       s              6  0
     d l1yn            s              3
E6933d l1asterisk      s              1
      *
     d l2yn            s              3
     d l2tsxam         s                   like(ajtsxam)
     d l2tafam         s                   like(ajtafam)
     d l2tbcam         s                   like(ajtbcam)
     d l2toxam         s                   like(ajtoxam)
      *
     d h1desc          s             25
     d h1dsds          s              7
E6933d h1asteriskcmt1  s            198
E6933d h1asteriskcmt2  s            198
      *
     d t1desc          s             22
      *
     D t2tafam         s                   like(ajtafam)
     D t2tbcam         s                   like(ajtafam)
     D t2tsxam         s                   like(ajtafam)
     D t2toxam         s                   like(ajtafam)
     D t2arapam        s                   like(ajarapam)
      *
      * Array index
      *
     D x               s              3  0
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *---------------------------------------------------------------
      * Local data area
      *---------------------------------------------------------------
      * Be careful in changing the LDA since this is generated from
      * multiple functions.
      *
     d lda            uds                  dtaara(*lda)
     D  ldpfcd                37     37
      *
     D  ldfmdy                39     44  0
     D  ldtmdy                45     50  0
     D  ldfcymd               52     59  0
     D  ldtcymd               60     67  0
      *
     D  ldcono                71     73  0
     D  ldajty                74     76
     D  ldcc                  77     88
     D  ldobj                 89     94
     D  ldsub                 95    102
      *
     D  ldcuno               103    109  0
     D  ldcunm               110    139
      *
     D  ldpmcd               141    141
     D  ldpmds               142    171
     D  ldpocd               172    172
     D  ldpods               173    192
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      *************************************************************************************
      * Mainline
      *************************************************************************************
      *
      * Print headings
     C                   exsr      $h1hdr
      *
      * Process each AR Adjustment record for the Week-ending Date(s).
      * (records were selected with OPNQRY)
      *
     C     *loval        setll     tfl026a
      *
     C                   dou       *in90 = *on                                  Main do
     C                   read      tfl026a                                90
     C                   if        *in90 = *off                                 If not EOF
      * Control breaks
     C                   select
     C                   when      first = yes
     C                   move      no            first
     C                   z-add     ajcono        svcono
     C                   move      ajrcexfl      svrcexfl
     C                   move      ajcc          svcc
     C                   move      ajobj         svobj
     C                   move      ajsub         svsub
     C                   move      ajajty        svajty
     C                   move      ajpocd        svpocd
     C                   move      ajpmcd        svpmcd
      * Company
     C                   when      svcono <> ajcono
     C                   exsr      $account
     C                   exsr      $rcexfl
     C                   exsr      $cono
      * Record exempt
     C                   when      svrcexfl <> ajrcexfl
     C                   exsr      $account
     C                   exsr      $rcexfl
     C                   exsr      $blank
      * Account
     C                   when      svcc  <> ajcc  or
     C                             svobj <> ajobj or
     C                             svsub <> ajsub
     C                   exsr      $account
     C                   endsl
      *
      * Accumulate amounts into total arrays
      *
     C                   add       ajtram        artram
     C                   add       ajsaranam     arsnam
     C                   add       ajtaranam     artnam
     C                   add       ajaarasxam    arasxam
      *
      * Accumulate Payout Amount
      *
     C                   add       ajarapam      t2arapam
      *
      * Accumulate Triumph Sales Expense Amounts for report total.
      *
     C                   add       ajtafam       t2tafam
     C                   add       ajtbcam       t2tbcam
     C                   add       ajtoxam       t2toxam
     C                   add       ajtsxam       t2tsxam
      *
      * If you are generating the Detail Version, print the AR Adjustment line
      * If you are generating the Summary Version, accumualte Triumph Sales Expenses
      * for printing on the Summary detail line.
      *
     C                   select
     C                   when      xxdsfl = 'D'
     C                   exsr      $l1dtl
      *
     C                   when      xxdsfl = 'S'
     C                   add       ajtafam       l2tafam
     C                   add       ajtbcam       l2tbcam
     C                   add       ajtoxam       l2toxam
     C                   add       ajtsxam       l2tsxam
     C                   endsl
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Main do
      *
      *-----------------
      * EOF processing
      *-----------------
      *
      * No adjustments
     C                   select
     C                   when      first = yes
     C                   except    nodata
     C                   other
      *
      * Print last totals
     C                   exsr      $account
     C                   exsr      $rcexfl
     C                   exsr      $cono
      *
      * Print: Report Summary Section
      *
     C                   exsr      $t2tot
     C                   endsl
      *
     C                   seton                                        lr
      /eject
      *---------------------------------------------------------------
      * Account Total
      *---------------------------------------------------------------
      *
     C     $account      begsr
      *
     C                   z-add     1             x
      *
      * Detail
     C                   select
     C                   when      xxdsfl = 'D' and count > 1
     C                   exsr      $underscore
     C                   eval      t1desc = '        Account Total:'
     C                   exsr      $t1tot
      * Summary
     C                   when      xxdsfl = 'S'
     C                   move      *blank        t1desc
     C                   if        svrcexfl = yes
     C                   eval      l2yn = 'Yes'
     C                   else
     C                   eval      l2yn = 'No '
     C                   endif
     C                   exsr      $l2dtl
     C                   endsl
      *
      * Clear array entry, save control fields
      *
     C                   exsr      $clear
     C                   move      ajcc          svcc
     C                   move      ajobj         svobj
     C                   move      ajsub         svsub
     C                   move      ajajty        svajty
     C                   move      ajpocd        svpocd
     C                   move      ajpmcd        svpmcd
      *
      * If printing the Detail Report, print a blank line.
      *
     C                   if        xxdsfl = 'D'
     C                   exsr      $blank
     C                   z-add     0             count
     C                   endif
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Record Exempt/NonExempt Totals
      *---------------------------------------------------------------
      *
     C     $rcexfl       begsr
      *
     C                   z-add     2             x
      *
     C                   select
     C                   when      svrcexfl = yes
     C                   eval      t1desc = '         Exempt Total:'
     C                   add       artram(x)     wktrexam
     C                   add       arsnam(x)     wksexam
     C                   add       artnam(x)     wktexam
     C                   add       arasxam(x)    wkaexam
     C                   other
      *
     C                   eval      t1desc = '     Non-Exempt Total:'
     C                   add       artram(x)     wktrnonam
     C                   add       arsnam(x)     wksnonam
     C                   add       artnam(x)     wktnonam
     C                   add       arasxam(x)    wkanonam
     C                   endsl
      *
      * If you are printing the Summary version, print the underscores.
      *
     C                   if        xxdsfl = 'S'
     C                   exsr      $underscore
     C                   endif
      *
      * Print total, clear array entry, save control field
      *
     C                   exsr      $t1tot
     C                   exsr      $clear
     C                   move      ajrcexfl      svrcexfl
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Company Total
      *---------------------------------------------------------------
      *
     C     $cono         begsr
      *
     C                   z-add     3             x
      *
     C                   eval      t1desc = '        Company Total:'
      *
      * Print total, clear array entry, save control field
      *
     C                   exsr      $t1tot
     C                   exsr      $clear
     C                   z-add     ajcono        svcono
      *
      * Print a blank line
     C                   exsr      $blank
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Clear array entries for specified index
      *---------------------------------------------------------------
      *
     C     $clear        begsr
      *
     C                   z-add     0             artram(x)
     C                   z-add     0             arsnam(x)
     C                   z-add     0             artnam(x)
     C                   z-add     0             arasxam(x)
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Print the AR Adjustment Detail line
      *---------------------------------------------------------------
      *
     C     $l1dtl        begsr
      *
     C                   select
     C                   when      ajrcexfl = yes
     C                   eval      l1yn = 'Yes'
      *
     C                   when      ajrcexfl = no
     C                   eval      l1yn = 'No '
     C                   endsl
E6933 *
  |   ** Print '*' if 'AR Ord Ref Actual Ship Date' <> 0.
  |  C                   eval      l1asterisk = *blank
  |  C     key01         chain     PBC0CPL1                           92
  |  C                   if        *in92 = *off
  |  C                   if        C0V8DT <> 0
  |  C                   eval      l1asterisk = '*'                             Print '*'
  |  C                   endif
  |  C                   endif
E6933 *
      *
      * Flip Actual Ship Date
      *
     C     *iso          test(d)                 ajasdt                 92
     C                   if        *in92 = *off                                 If OK date
     C                   move      ajasdt        wkisodate
     C     *mdy          move      wkisodate     l1asdtmdy
     C                   else
     C                   z-add     0             l1asdtmdy
     C                   endif                                                  If OK date
      *
      * Flip Deposit Date
      *
     C     *iso          test(d)                 ajdpdt                 92
     C                   if        *in92 = *off                                 If good date
     C                   move      ajdpdt        wkisodate
     C     *mdy          move      wkisodate     l1dpdtmdy
     C                   else
     C                   z-add     0             l1dpdtmdy
     C                   endif                                                  If good date
      *
      * Flip Cash Week-ending Date
      *
     C     *iso          test(d)                 ajcwedt                92
     C                   if        *in92 = *off                                 If OK
     C                   move      ajcwedt       wkisodate
     C     *mdy          move      wkisodate     l1cwedtmdy
     C                   else
     C                   z-add     0             l1cwedtmdy
     C                   endif                                                  If OK
      *
     C                   if        *inof = *on
     C                   exsr      $h1hdr
     C                   endif
      *
     C                   except    l1dtl
     C                   add       1             count
     C                   add       1             linecount
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Print detail line on the Summary Report
      *---------------------------------------------------------------
      *
     C     $l2dtl        begsr
      *
     C                   if        *inof = *on
     C                   exsr      $h1hdr
     C                   endif
      *
     C                   except    l2dtl
     C                   add       1             linecount
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Print report heading
      *---------------------------------------------------------------
      *
     C     $h1hdr        begsr
      *
     C                   except    h1hdr
     C                   z-add     12            linecount
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Print Total line
      *---------------------------------------------------------------
      *
     C     $t1tot        begsr
      *
     C                   if        *inof = *on
     C                   exsr      $h1hdr
     C                   endif
      *
     C                   except    t1tot
     C                   add       1             linecount
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Print Report Total Summary Section
      *---------------------------------------------------------------
      *
     C     $t2tot        begsr
      *
     C                   if        linecount > 72
     C                   exsr      $h1hdr
     C                   endif
      *
     C                   z-add     4             x
     C                   except    t2tot
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Print underscores
      *---------------------------------------------------------------
      *
     C     $underscore   begsr
      *
     C                   except    underscore
     C                   add       1             linecount
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Print a blank line
      *---------------------------------------------------------------
      *
     C     $blank        begsr
      *
     C                   except    blank
     C                   add       1             linecount
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Initialization
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Parm lists
      *
     C     *entry        plist
     C                   parm                    xxdsfl
      *
E6933 * Key lists
  |   *
  |  C     key01         klist
  |  C                   kfld                    AJCONO                         Company
  |  C                   kfld                    AJINNO                         Invoice #
E6933 *
      * Heading setup
     C                   select
     C                   when      xxdsfl = 'D'
E6933 *
  |  ** Add verbiage tp denote asterisk for Actual Ship Date.
  |  C                   eval      h1asteriskcmt1 = '(*) Pre ' + w_asdgolivedte
  |  C                             + ' - Sales History Actual Ship Date derived'
  |  C                             + ' from AR Invoice Ref. After '
  |  C                             + w_asdgolivedte + ' - Sales History Actual'
  |  C                             + ' Ship Date derived from Original Invoice'
  |  C                             + ' Ref,'
  |  C                   eval      h1asteriskcmt2 = 'which ensures each AR'
  |  C                             + ' transaction is tied back to original'
  |  C                             + ' Order/Invoice within the applicable'
E6933C                             + ' week ending.'
     C                   eval      h1dsds = 'Detail'
      *
     C                   when      xxdsfl = 'S'
     C                   eval      h1dsds = 'Summary'
     C                   seton                                        87
     C                   endsl
      *
     C                   time                    rtime
      *
      * This report is generated on-demand and also from the submitted
      * Weekly Revenue Close function.
      *
     C                   select
     C                   when      ldpfcd = 'D'
     C                   eval      h1desc = '        On-Demand        '
     C                   seton                                        85
      *
     C                   when      ldpfcd = 'F'
     C                   eval      h1desc = '   Final Revenue Close   '
      *
     C                   when      ldpfcd = 'P'
     C                   eval      h1desc = 'Preliminary Revenue Close'
     C                   endsl
      *
      *
      * Company
     C                   if        ldcono = 0
     C                   seton                                        80
     C                   endif
      * A/R Customer
     C                   if        ldcuno = 0
     C                   seton                                        86
     C                   endif
      * Adjustment Type
     C                   if        ldajty = *blank
     C                   seton                                        81
     C                   endif
      * Cost Center
     C                   if        ldcc = *blank
     C                   seton                                        82
     C                   endif
      * Object
     C                   if        ldobj = *blank
     C                   seton                                        83
     C                   endif
      * Subsidiary
     C                   if        ldsub = *blank
     C                   seton                                        84
     C                   endif
      * G/L Post method
     C                   if        ldpmcd = *blank
     C                   seton                                        88
     C                   endif
      * Payout code
     C                   if        ldpocd = *blank
     C                   seton                                        89
     C                   endif
      *
     C                   endsr
      /eject
      *-------------------------------------------------------------
      * Report heading lines
      *-------------------------------------------------------------
      *
     Oprint198  e            h1hdr          1 01
     O                       sdpgm               10
     O                                          105 'AR Adjustments Listing'
     O                                          188 'DATE'
     O                       udate         y    198
      *
     O          e            h1hdr          1
     O                       sdusr               10
     O                       h1desc             107
     O                                          188 'TIME'
     O                       rtime              198 '  :  :  '
      *
     O          e            h1hdr          2
     O                       h1dsds              98
     O                                          188 'PAGE'
     O                       page          z    198
      *
      * Print From/To WeekEnding Dates when this report is On-Demand.
      * Print Revenue Close WeekEnding Date when this report is generated from
      * the Revenue Close function
      *
     O          e            h1hdr          1
     O               85                          22 'From week-ending date:'
     O               85      ldfmdy              32 '  /  /  '
      *
     O              n85                          22 'Week-ending date:'
     O              n85      ldtmdy              32 '  /  /  '
      *
     O                                           50 'Cost center:'
     O               82                          55 'All'
     O              n82      ldcc                64
      *
     O                                          112 'G/L post method:'
     O               88                         117 'All'
     O              n88      ldpmcd             115
     O              n88      ldpmds             147
      *
      *
     O          e            h1hdr          1
     O               85                          22 'To week-ending date:'
     O               85      ldtmdy              32 '  /  /  '
      *
     O                                           50 'Object:'
     O               83                          55 'All'
     O              n83      ldobj               58
      *
     O                                          112 'Payout code:'
     O               89                         117 'All'
     O              n89      ldpocd             115
     O              n89      ldpods             137
      *
     O          e            h1hdr          1
     O                                           22 'Company:'
     O               80                          27 'All'
     O              n80      ldcono        z     27
      *
     O                                           50 'Subsidiary:'
     O               84                          55 'All'
     O              n84      ldsub               60
      *
E6933O**        e            h1hdr          2
E6933O          e            h1hdr          1
     O                                           22 'Adjustment type:'
     O               81                          27 'All'
     O              n81      ldajty              27
      *
     O                                           50 'A/R customer:'
     O               86                          55 'All'
     O              n86      ldcuno        z     59
     O              n86      ldcunm              94
      *
E6933 ** For Detail Report.
  |  O          e   n87      h1hdr          1
  |  O                       h1asteriskcmt1     198
  |   *
  |  O          e   n87      h1hdr          2
  |  O                       h1asteriskcmt2     198
E6933 *
      *-------------------------------------------------------------
      * Column headings
      *-------------------------------------------------------------
      * n87 = Detail Report
      *  87 = Summary Report
      *
     O          e            h1hdr          1
      * Detail
     O              n87                          99 'Aggregate AR'
     O              n87                         118 'Seaboard'
     O              n87                         137 'Triumph'
     O              n87                         173 'Cash'
      * Summary
     O               87                          91 'Aggregate AR'
     O               87                         110 'Seaboard'
     O               87                         129 'Triumph'
     O               87                         146 'Triumph'
     O               87                         163 'Triumph'
     O               87                         180 'Triumph'
     O               87                         197 'Triumph'
      *
     O          e            h1hdr          1
      *
     O                                            3 'Com'
     O                                            8 'Exe'
     O                                           13 'Post'
T9996O**                                         46 'Adj'
  |  O                                           45 'Adj'
  |  O**                                         55 'Payout'
T9996O                                           55 'Pay'
      * Detail
T9996O              n87                          50 'Inv'
     O              n87                          64 'Deposit'
     O              n87                          80 'Transaction'
     O              n87                          99 'Adjustment Sales'
     O              n87                         118 'AR Adjustment'
     O              n87                         137 'AR Adjustment'
     O              n87                         150 'Agg Gross'
     O              n87                         161 'SBF Net'
     O              n87                         176 'Week-Ending'
     O              n87                         186 'Invoice'
     O              n87                         196 'Actual'
      * Summary
     O               87                          72 'Transaction'
     O               87                          91 'Adjustment Sales'
     O               87                         110 'AR Adjustment'
     O               87                         129 'AR Adjustment'
     O               87                         146 'Total Sales'
     O               87                         163 'Absorbed'
     O               87                         180 'Broker'
     O               87                         197 'Other Sales'
     O          e            h1hdr          1
     O                                            3 'Nbr'
     O                                            8 'Rec'
     O                                           13 'Meth'
     O                                           25 'Cost Center'
     O                                           32 'Object'
     O                                           36 'Sub'
T9996O**                                         47 'Type'
  |  O                                           45 'Typ'
  |  O**                                         54 'Code'
T9996O                                           55 'Out'
      * Detail
T9996O              n87                          50 'Sfx'
     O              n87                          63 'Date'
     O              n87                          80 'Amount'
     O              n87                          99 'Expense Amount'
     O              n87                         118 'Net Amount'
     O              n87                         137 'Net Amount'
     O              n87                         150 'Sales Exp%'
     O              n87                         162 'Prod Rev%'
     O              n87                         173 'Date'
     O              n87                         186 'Number'
     O              n87                         198 'Ship Date'
      * Summary
     O               87                          72 'Amount'
     O               87                          91 'Expense Amount'
     O               87                         110 'Net Amount'
     O               87                         129 'Net Amount'
     O               87                         146 'Expense Amount'
     O               87                         163 'Freight Amount'
     O               87                         180 'Comm Amount'
     O               87                         197 'Expense Amount'
      *
     O          e            h1hdr          1
     O                       dash               198
      *
      *-------------------------------------------------------------
      * Detail line (used when you are generating the Detail report)
      *-------------------------------------------------------------
      *
     O          e            l1dtl          1
     O                       ajcono        z      3
     O                       l1yn                 8
     O                       ajpmcd              12
     O                       ajcc                25
     O                       ajobj               32
     O                       ajsub               41
T9996O**                     ajajty              46
  |  O                       ajajty              45
  |  O                       ajinsf              49
  |  O**                     ajpocd              52
T9996O                       ajpocd              54
     O                       l1dpdtmdy      b    64 '  /  /  '
      *
     O                       ajtram        k     81
     O                       ajaarasxam    k    100
     O                       ajsaranam     k    119
     O                       ajtaranam     k    138
      *
     O                       ajagsxpc      k    151
     O                       ajsnprpc      k    162
     O                       l1cwedtmdy     b   174 '  /  /  '
     O                       ajinno        z    186
E6933O                       l1asterisk         189
     O                       l1asdtmdy      b   198 '  /  /  '
      *
      *--------------------------------------------------------------
      * Detail line (used when you are generating the Summary Report)
      *--------------------------------------------------------------
      *
     O          e            l2dtl          1
     O                       svcono        z      3
     O                       l2yn                 8
     O                       svpmcd              12
     O                       svcc                25
     O                       svobj               32
     O                       svsub               41
T9996O**                     svajty              46
  |  O                       svajty              45
  |  O**                     svpocd              52
T9996O                       svpocd              54
      *
     O                       artram (x)    k     73
     O                       arasxam(x)    k     92
     O                       arsnam (x)    k    111
     O                       artnam (x)    k    130
      *
     O                       l2tsxam       kb   147
     O                       l2tafam       kb   164
     O                       l2tbcam       kb   181
     O                       l2toxam       kb   198
      *
      *--------------------------------------------------------------------------------------
      * Total line
      *--------------------------------------------------------------------------------------
      * Although the fieldss being printed are the same for the Detail and Summary Reports,
      * the positions that these fields are in are different. So, we condition the layout:
      *   n87 = Detail Report
      *    87 = Summary Report
      *
     O          e            t1tot          1
     O              n87      t1desc              64
     O              n87      artram (x)    k     81
     O              n87      arasxam(x)    k    100
     O              n87      arsnam (x)    k    119
     O              n87      artnam (x)    k    138
      *
     O               87      t1desc              55
     O               87      artram (x)    k     73
     O               87      arasxam(x)    k     92
     O               87      arsnam (x)    k    111
     O               87      artnam (x)    k    130
      *
      *-------------------------------------------------------------
      * Report Total Summary Section
      *-------------------------------------------------------------
      *
      * Although the fieldss being printed are the same for the Detail and Summary versions,
      * the positions that these fields are in are different. So, we condition:
      *   n87 = Detail Report
      *    87 = Summary Report
      *
     O          e            t2tot       2  1
     O              n87                          64 'Report Summary:'
     O              n87                          80 'Transaction'
     O              n87                          99 'Sales Expense'
     O              n87                         118 'Seaboard Net'
     O              n87                         137 'Triumph Net'
      *
     O               87                          55 'Report Summary:'
     O               87                          72 'Transaction'
     O               87                          91 'Sales Expense'
     O               87                         110 'Seaboard Net'
     O               87                         129 'Triumph Net'
      *
     O                                          190 'Triumph Sales Expense'
     O                                          198 ' Amounts'
     O          e            t2tot          1
     O              n87                          80 '--------------'
     O              n87                          99 '--------------'
     O              n87                         118 '--------------'
     O              n87                         137 '--------------'
      *
     O               87                          72 '--------------'
     O               87                          91 '--------------'
     O               87                         110 '--------------'
     O               87                         129 '--------------'
      *
     O                                          190 '--------------------------'
     O                                          198 '--------'
      *
     O          e            t2tot          1
     O              n87                          63 'Non-Exempt:'
     O              n87      wktrnonam     k     81
     O              n87      wkanonam      k    100
     O              n87      wksnonam      k    119
     O              n87      wktnonam      k    138
      *
     O               87                          55 'Non-Exempt:'
     O               87      wktrnonam     k     73
     O               87      wkanonam      k     92
     O               87      wksnonam      k    111
     O               87      wktnonam      k    130
      *
     O                                          182 'Total:'
     O                       t2tsxam       k    198
      *
     O          e            t2tot          1
     O              n87                          63 'Exempt:'
     O              n87      wktrexam      k     81
     O              n87      wkaexam       k    100
     O              n87      wksexam       k    119
     O              n87      wktexam       k    138
      *
     O               87                          55 'Exempt:'
     O               87      wktrexam      k     73
     O               87      wkaexam       k     92
     O               87      wksexam       k    111
     O               87      wktexam       k    130
      *
     O                                          182 'Absorbed Freight:'
     O                       t2tafam       k    198
      *
     O          e            t2tot          1
     O              n87                          80 '--------------'
     O              n87                          99 '--------------'
     O              n87                         118 '--------------'
     O              n87                         137 '--------------'
      *
     O               87                          72 '--------------'
     O               87                          91 '--------------'
     O               87                         110 '--------------'
     O               87                         129 '--------------'
      *
     O                                          182 'Broker Commission:'
     O                       t2tbcam       k    198
      *
     O          e            t2tot          1
     O                                           14 'Payout Amount:'
     O                       t2arapam      k     30
     O              n87                          63 'Total:'
     O              n87      artram (x)    k     81
     O              n87      arasxam(x)    k    100
     O              n87      arsnam (x)    k    119
     O              n87      artnam (x)    k    138
      *
     O               87                          55 'Total:'
     O               87      artram (x)    k     73
     O               87      arasxam(x)    k     92
     O               87      arsnam (x)    k    111
     O               87      artnam (x)    k    130
      *
     O                                          182 'Other:'
     O                       t2toxam       k    198
      *
      *-------------------------------------------------------------
      * Underscores for total line
      *-------------------------------------------------------------
      *   n87 = Detail Report
      *    87 = Summary Report
      *
     O          e            underscore     1
     O              n87                          80 '--------------'
     O              n87                          99 '--------------'
     O              n87                         118 '--------------'
     O              n87                         137 '--------------'
      *
     O               87                          72 '--------------'
     O               87                          91 '--------------'
     O               87                         110 '--------------'
     O               87                         129 '--------------'
      *-------------------------------------------------------------
      * Blank
      *-------------------------------------------------------------
      *
     O          e            blank       1
      *
      *-------------------------------------------------------------
      * No data message line
      *-------------------------------------------------------------
      *
     O          e            nodata      1
     O                                           19 'No data meets your'
     O                                           39 'selection criteria.'
      /eject
