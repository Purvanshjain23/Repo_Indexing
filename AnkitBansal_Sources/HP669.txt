      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      HOG PRODUCTION
      * PROGRAM:     HP669
      * TITLE:       INVENTORY REPORT BY SITE
      * PROGRAMMER:  MARY PALMER
      * CREATED:     6/17/94
      *
      *  FUNCTION:   THIS PROGRAM PRINTS A SINGLE LINE REPORT
      *              OF THE INVENTORY COUNTS OF A HOG GROUP FOR A
      *              SPECIFIED CUT-OFF DATE.
      *
      *              The user may elect to print a summary version which
      *              does not print the group detail; it prints the farm
      *              total lines.
      *
      *              HP669CL RUNS OPNQRY ON HOG GROUP FILE AND CALLS
      *              THIS PROGRAM.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      * 08/22/94  LEANNE FEDOR
      *           AS REQUESTED BY DAVE O'SULLIVAN, ADDED A SEPARATE
      *           COLUMN FOR INVENTORY ADJUSTMENTS. BEFORE THIS CHANGE
      *           INVENTORY ADJUSTMENTS WERE INCLUDED IN THE MORTALITY
      *           COLUMN.
      *
      * 09/14/94  LEANNE FEDOR
      *           RENAMED THE REPORT TO BE 'INVENTORY REPORT BY SITE'.
      *           ADDED GENETIC LINE TO THE REPORT.
      *
      * 09/20/94  MARY PALMER
      *           CHANGED PIGS IN COLUMN TO BECOME TRANSFERS IN WITHIN
      *           PHASE AND TRANSFERS IN TO NEXT PHASE.  CHANGED TRANS-
      *           OUT COLUMN TO TRANSFERS OUT WITHIN PHASE AND TRANSFER
      *           OUT TO NEXT PHASE.
      *
      * 09/20/94  MARY PALMER
      *           CHANGED PROGRAM TO READ RECORDS FROM WORKFILE HSP369
      *           AND REMOVED CALLS TO HPMOVE AND HPSALE.
      *
      * 02/06/95  LEANNE FEDOR
      *           INCREASED FIELD SIZES OF TOTAL FIELDS.
      *
      * 03/21/95  LEANNE FEDOR
      *           A NEW SELECTION CRITERIA WAS ADDED THAT ALLOWS THE
      *           USER TO DESIGNATE WHETHER THE REPORT SHOULD PAGE
      *           BREAK ON FARM SITE OR NOT. SOMETIMES THE USERS WANT
      *           TO SEPARATE THE REPORT BY FARM SITE AND DISTRIBUTE
      *           THE PAGES TO THE FARM MANAGERS. SOMETIMES THEY JUST
      *           WANT THE ENTIRE REPORT IN ONE PIECE.
      *           DO NOT PRINT PHASE, TYPE AND REPORT TOTALS WHEN THE
      *           REPORT PAGE BREAKS ON FARM SITE.
      *
      * 04/13/95  LEANNE FEDOR
      *           A NEW SELECTION CRITERIA WAS ADDED THAT ALLOWS THE
      *           USER TO CHOOSE WHETHER TO PRINT AN UNDERSCORE BESIDE
      *           EACH FIELD TO ALLOW THE FARM MANAGERS TO PENCIL IN
      *           THEIR NUMBER OF HEAD.
      *
      * 04/27/95  LEANNE FEDOR
      *           CHANGED FARM SITE CODE FROM ALPHA TO NUMERIC.
      *           ADDED 2 NEW COLUMNS: CULL PERCENT AND DEATH PERCENT.
      *           MOVED THE 'DASHED' ENTRY FIELDS TO A SECOND DETAIL
      *           LINE. SO, ENTRY FIELDS ARE BELOW THE VALUES INSTEAD
      *           OF BESIDE THE VALUES.
      *           DECREASED SIZE OF TOTAL FIELDS FROM 12,0 TO 9,0.
      *
      * 07/17/95  LEANNE FEDOR
      *           CHANGED FOR MOVEMENT REWRITE.
      *
      * 08/30/95  LEANNE FEDOR
      *           USERS MAY NOW SELECT 'ALL' BUSINES OFFICES. CHANGED
      *           REPORT HEADINGS TO SAY 'ALL'.
      *
      * 10/10/96  LEANNE FEDOR
      *           CHANGED LOGIC TO NOT PRINT THE FARM TOTAL LINE
      *           IF THERE WAS ONLY ONE DETAIL LINE (ONE GROUP) FOR
      *           A FARM.
      *
      * 12/01/99  LeAnne Fedor
      *           The user can now elect to print this report as 'detail' or
      *           summary.  Detail prints the group info; summary prints the
      *           farm totals without any group info.
      *
      * 05/31/07  LeAnne Ramsey
      *           Some users are having printing problems. So, we are switching
      *           from QPRINT to PRINT198 for the printer file and replacing
      *           John Erikson's customized printer override command in the CL with
      *           the standard override.
      *
      * 10/16/13  LeAnne Ramsey (E2831)
      *           Recompile only. Added field 'MTech Reference'.
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     FHSP010    IF   E           K DISK
      *    PRODUCTION TYPE         (KEY: PTPTCD)
      *
     FHSP011    IF   E           K DISK
      *    PRODUCTION PHASE         (KEY: PPPPCD)
      *
     FHSP018    IF   E           K DISK
      *    FARM SITE                 (KEY: FSFSCD)
      *
     FHSP369    IF   E           K DISK
      *    WORKFILE OF GROUP
      *    (KEY: WGFSBO WGPTCD WGPPCD WGFSCD WGHGCD)
      *
     Fprint198  o    f  198        printer oflind(*inof)
     F                                     INFDS(DS#PRT)
      *
      /EJECT
      ****************************************************************
      * TABLE/ARRAY SPECIFICATIONS
      ****************************************************************
      *
      *  PRINT ARRAYS FOR TOTALS
      *
     D LBS             S             15  2 DIM(4)                               TRANI POUNDS
     D FED             S             15  2 DIM(4)                               FEED POUNDS
     D TIS             S              9  0 DIM(4)                               TRANI SAME PHA
     D TID             S              9  0 DIM(4)                               TRANI DIFF PHA
     D CUL             S              9  0 DIM(4)                               CULLED
     D DED             S              9  0 DIM(4)                               DEAD/MORTALITY
     D TOS             S              9  0 DIM(4)                               TRANO SAME PHA
     D TOD             S              9  0 DIM(4)                               TRANO DIFF PHA
     D MKT             S              9  0 DIM(4)                               MARKET SALES
     D ADJ             S              9  0 DIM(4)                               ADJUSTMENTS
     D INV             S              9  0 DIM(4)                               INVENTORY
      /EJECT
      ****************************************************************
      * INPUT SPECS
      ****************************************************************
      *
      *---------------------------------------------------------------
      * STANDARD PROGRAM STATUS DATA STRUCTURE
      *---------------------------------------------------------------
      *    EXTERNALLY DEFINED AS UTPGFR (RECORD FORMAT: PGMDSR)
     D PGMDS         ESDS                  EXTNAME(UTPGFR)
      *
      *---------------------------------------------------------------
      *  NAMED CONSTANTS
      *---------------------------------------------------------------
      *
     D YES             C                   CONST('Y')
     D NO              C                   CONST('N')
     D UNKNWN          C                   CONST('UNKNOWN')
     D ALL             C                   CONST('ALL')
      *
      *----------------------------------------------------------------
      *  DS#PRT: PRINTER CONTROL FIELDS...
      *----------------------------------------------------------------
     D DS#PRT          DS
     D  OVFLIN               188    189B 0
     D  CURLIN               367    368B 0
      *
      *----------------------------------------------------------------
      * LDA - REPORT SELECTIONS
      *----------------------------------------------------------------
      *
     D                UDS
     D  LDFSBO                 1      5
     D  LDCYMD                 6     13  0
     D  LDPTCD                14     18
     D  LDPPCD                19     23
     D  LDFSCD                24     28  0
     D  LDHGSN                29     35  0
     D  LDRPFL                36     36
     D  LDCMDY                37     42  0
     D  LDPBFL                43     43
     D  LDENFL                44     44
     D  LDCELL                45     46  0
     D  LDGTCD                47     47
      *
     D  LDBODS                72    101
     D  LDPTDS               102    111
     D  LDPPDS               112    141
     D  LDFSNM               142    166
     D  LDHGCD               167    173
     D  LDGTDS               174    188
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * MAIN LINE
      ****************************************************************
      *
      * PROCESS ALL GROUP RECORDS IN THE WORKFILE
      *
     C     *IN90         DOUEQ     *ON                                          MAIN DO
     C                   READ      HSP369                                 90
     C     *IN90         IFEQ      *OFF                                         IF NOT EOF
      *
     C     FIRST         IFEQ      YES                                          IF FIRST
     C                   EXSR      $FIRST
     C                   ENDIF                                                  IF FIRST
      *
      * BREAK ON PRODUCTION TYPE
      *
     C     WGPTCD        IFNE      SVPTCD                                       IF PT BREAK
     C                   EXSR      $PTBRK
     C                   ENDIF                                                  IF PT BREAK
      *
      * BREAK ON PRODUCTION PHASE
      *
     C     WGPPCD        IFNE      SVPPCD                                       IF PP BREAK
     C                   EXSR      $PPBRK
     C                   ENDIF                                                  IF PP BREAK
      *
      * BREAK ON FARM SITE
      *
     C     WGFSCD        IFNE      SVFSCD                                       IF FS BREAK
     C                   EXSR      $FSBRK
     C                   ENDIF                                                  IF FS BREAK
      *
      *  ACCUMULATE VALUES FOR TOTAL INTO ARRAYS
      *
     C                   ADD       WGISLB        LBS
     C                   ADD       WGIDLB        LBS
      *
     C                   ADD       WGFDLB        FED
      *
     C                   ADD       WGISHD        TIS
     C                   ADD       WGIDHD        TID
     C                   ADD       WGCUHD        CUL
     C                   ADD       WGDDHD        DED
     C                   ADD       WGOSHD        TOS
     C                   ADD       WGODHD        TOD
     C                   ADD       WGSLHD        MKT
     C                   ADD       WGAJHD        ADJ
     C                   ADD       WGEIHD        INV
      *
      * CALCULATE CULL PERCENT AND DEAD PERCENT
      *
     C     WGISHD        ADD       WGIDHD        WKHD
     C     WKHD          IFNE      0
     C     WGCUHD        DIV       WKHD          WKPR
     C     WKPR          MULT(H)   100           WKCUPR
     C     WGDDHD        DIV(H)    WKHD          WKPR
     C     WKPR          MULT(H)   100           WKDDPR
     C     WGSLHD        DIV(H)    WKHD          WKPR
     C     WKPR          MULT(H)   100           WKSLPR
     C                   ENDIF
      *
     C     *INOF         IFEQ      *ON
     C                   EXSR      $OFLOW
     C                   ENDIF
     C                   EXCEPT    DTLINE
      *
     C                   ADD       1             WKCT
      *
     C                   ENDIF                                                  IF NOT EOF
     C                   ENDDO                                                  MAIN DO
      *
      *----------------------------------------------------------------
      * EOF PROCESSING
      *----------------------------------------------------------------
      *
      *  IF NO DETAIL RECORDS MATCHED THE SELECTION CRITERIA, THEN
      *  PRINT THE STANDARD 'NO DATA...' ERROR MESSAGE
      *
     C     FIRST         IFEQ      YES                                          IF NO RECS
     C                   EXCEPT    HRHDR
     C                   EXCEPT    NODATA
     C                   ELSE
      *
      *  CHECK CURRENT LINE FOR ROOM TO PRINT ALL TOTALS
      *
     C                   ADD       7             CURLIN
     C     CURLIN        IFGT      OVFLIN
     C     *INOF         OREQ      *ON
     C                   EXSR      $OFLOW
     C                   ENDIF
      *
      * PRINT FARM SITE TOTAL
      *
     C                   EXSR      $TFTOT
      *
      * PRINT THE FOLLOWING TOTALS IF THE USER IS NOT PAGE BREAKING
      * ON FARM SITE.
      *     PRODUCTION PHASE
      *     PRODUCTION TYPE
      *     REPORT TOTALS
      *
     C     LDPBFL        IFEQ      NO
     C                   EXSR      $TPTOT
     C                   EXSR      $TTTOT
      *
      * CALCULATE THE FOLLOWING:
      *  1) PERCENTS: CULL SALE, DEAD, MARKET SALE
      *  2) AVERAGE POUNDS TRANSFERRED IN
      *  3) AVERAGE FEED POUNDS CONSUMED
      *
     C     TIS(4)        ADD       TID(4)        WKHD
     C     WKHD          IFNE      0
     C     CUL(4)        DIV       WKHD          WKPR
     C     WKPR          MULT(H)   100           WKCUPR
     C     DED(4)        DIV(H)    WKHD          WKPR
     C     WKPR          MULT(H)   100           WKDDPR
     C     MKT(4)        DIV(H)    WKHD          WKPR
     C     WKPR          MULT(H)   100           WKSLPR
      *
     C     LBS(4)        DIV(H)    WKHD          WKIALB
     C     FED(4)        DIV(H)    WKHD          WKFALB
     C                   ENDIF
      *
     C                   EXCEPT    TRTOT
     C                   ENDIF
     C                   ENDIF                                                  IF NO RECS
      *
     C                   SETON                                        LR
      /EJECT
      *****************************************************************
      * $OFLOW - OVERFLOW SUBROUTINE
      *****************************************************************
      *
     C     $OFLOW        BEGSR
      *
     C                   EXCEPT    HRHDR
     C                   EXCEPT    HFHDR
      *
     C                   ENDSR
      /EJECT
      *****************************************************************
      * $FSNM - RETRIEVE THE FARM SITE NAME
      *****************************************************************
      *
     C     $FSNM         BEGSR
      *
     C                   Z-ADD     WGFSCD        HFFSCD
      *
     C     WGFSCD        CHAIN     HSP018                             91
     C     *IN91         IFEQ      *OFF                                         IF FOUND
     C                   MOVEL     FSFSNM        HFFSNM
     C                   MOVEL     FSFSNM        SVFSNM
     C                   ELSE
     C                   MOVEL(P)  UNKNWN        HFFSNM
     C                   MOVE      *blank        SVFSNM
     C                   ENDIF                                                  IF FOUND
      *
     C                   ENDSR
      /EJECT
      *****************************************************************
      * $PTDS - RETRIEVE THE PRODUCTION TYPE DESCRIPTION
      *****************************************************************
      *
     C     $PTDS         BEGSR
      *
     C     WGPTCD        CHAIN     HSP010                             91
     C     *IN91         IFEQ      *OFF                                         IF FOUND
     C                   MOVEL     PTPTDS        HRPTDS
     C                   ELSE
     C                   MOVEL(P)  UNKNWN        HRPTDS
     C                   ENDIF                                                  IF FOUND
      *
     C                   ENDSR
      /EJECT
      *****************************************************************
      * $PPDS - RETRIEVE THE PRODUCTION PHASE DESCRIPTION
      *****************************************************************
      *
     C     $PPDS         BEGSR
      *
     C     WGPPCD        CHAIN     HSP011                             91
     C     *IN91         IFEQ      *OFF                                         IF FOUND
     C                   MOVEL     PPPPDS        HRPPDS
     C                   ELSE
     C                   MOVEL(P)  UNKNWN        HRPPDS
     C                   ENDIF                                                  IF FOUND
      *
     C                   ENDSR
      /EJECT
      *****************************************************************
      * $FSBRK - CONTROL BREAK ON FARM SITE CODE
      *****************************************************************
      *
      * PRINT FARM TOTAL
      * RETRIEVE NEW FARM NAME
      * PRINT HEADINGS FOR NEW FARM
      *
     C     $FSBRK        BEGSR
      *
     C                   EXSR      $TFTOT
     C                   ADD       2             CURLIN
      *
      * RETRIEVE NEW FARM NAME
      *
     C                   EXSR      $FSNM
      *
      * PRINT FARM HEADING
      *
     C     CURLIN        IFGT      OVFLIN
     C     *INOF         OREQ      *ON
     C     LDPBFL        OREQ      YES
     C                   EXSR      $OFLOW
     C                   ELSE
     C                   EXCEPT    HFHDR
     C                   ENDIF
      *
     C                   ENDSR
      /EJECT
      *****************************************************************
      * $PPBRK - CONTROL BREAK ON PRODUCTION PHASE
      *****************************************************************
      *
     C     $PPBRK        BEGSR
      *
      *  CHECK CURRENT LINE FOR ROOM TO PRINT TOTALS
      *
     C                   ADD       4             CURLIN
     C     CURLIN        IFGT      OVFLIN
     C     *INOF         OREQ      *ON
     C                   EXSR      $OFLOW
     C                   ENDIF
      * PRINT FARM TOTAL
     C                   EXSR      $TFTOT
      * PRINT PHASE TOTAL
     C                   EXSR      $TPTOT
      *
      * RETRIEVE NEW PHASE DESCRIPTION AND FARM NAME
      *
     C                   EXSR      $PPDS
     C                   EXSR      $FSNM
      *
      * ALWAYS PAGE BREAK
     C                   EXSR      $OFLOW
      *
     C                   ENDSR
      /EJECT
      *****************************************************************
      * $PTBRK - CONTROL BREAK ON PRODUCTION TYPE
      *****************************************************************
      *
     C     $PTBRK        BEGSR
      *
      *  CHECK CURRENT LINE FOR ROOM TO PRINT TOTALS
      *
     C                   ADD       6             CURLIN
     C     CURLIN        IFGT      OVFLIN
     C     *INOF         OREQ      *ON
     C                   EXSR      $OFLOW
     C                   ENDIF
      *
      * PRINT TOTALS: FARM, PRODUCTION PHASE, PRODUCTION TYPE
      *
     C                   EXSR      $TFTOT
     C                   EXSR      $TPTOT
     C                   EXSR      $TTTOT
      *
      * RETRIEVE NEW DESCRIPTIONS/NAMES: TYPE, PHASE, FARM
      *
     C                   EXSR      $PTDS
     C                   EXSR      $PPDS
     C                   EXSR      $FSNM
      *
      * ALWAYS PAGE BREAK
      *
     C                   EXSR      $OFLOW
      *
     C                   ENDSR
      /EJECT
      *****************************************************************
      * $TTTOT - PRINT PRODUCTION TYPE TOTALS
      *****************************************************************
      *
     C     $TTTOT        BEGSR
      *
      * ONLY PRINT THE TOTAL WHEN THE USER IS NOT PAGE BREAKING ON
      * FARM SITE.
      *
     C     LDPBFL        IFEQ      NO
      *
      * CALCULATE THE FOLLOWING:
      *  1) PERCENTS: CULL SALE, DEAD, MARKET SALE
      *  2) AVERAGE POUNDS TRANSFERRED IN
      *  3) AVERAGE FEED POUNDS CONSUMED
      *
     C     TIS(3)        ADD       TID(3)        WKHD
     C     WKHD          IFNE      0
     C     CUL(3)        DIV       WKHD          WKPR
     C     WKPR          MULT(H)   100           WKCUPR
     C     DED(3)        DIV(H)    WKHD          WKPR
     C     WKPR          MULT(H)   100           WKDDPR
     C     MKT(3)        DIV(H)    WKHD          WKPR
     C     WKPR          MULT(H)   100           WKSLPR
      *
     C     LBS(3)        DIV(H)    WKHD          WKIALB
     C     FED(3)        DIV(H)    WKHD          WKFALB
     C                   ENDIF
      *
     C                   EXCEPT    TTTOT
     C                   ENDIF
      *
     C                   MOVEL     WGPTCD        SVPTCD
      *
      *
      * CLEAR TWO ARRAYS WHOSE VALUES ARE NOT PRINTED
      *
     C                   Z-ADD     0             LBS(3)
     C                   Z-ADD     0             FED(3)
      *
     C                   ENDSR
      *
      *****************************************************************
      * $TPTOT - PRINT PRODUCTION PHASE TOTALS
      *****************************************************************
      *
     C     $TPTOT        BEGSR
      *
      * ONLY PRINT THE TOTAL WHEN THE USER IS NOT PAGE BREAKING ON
      * FARM SITE.
      *
     C     LDPBFL        IFEQ      NO
      *
      * CALCULATE THE FOLLOWING:
      *  1) PERCENTS: CULL SALE, DEAD, MARKET SALE
      *  2) AVERAGE POUNDS TRANSFERRED IN
      *  3) AVERAGE FEED POUNDS CONSUMED
      *
      *
     C     TIS(2)        ADD       TID(2)        WKHD
     C     WKHD          IFNE      0
     C     CUL(2)        DIV       WKHD          WKPR
     C     WKPR          MULT(H)   100           WKCUPR
     C     DED(2)        DIV(H)    WKHD          WKPR
     C     WKPR          MULT(H)   100           WKDDPR
     C     MKT(2)        DIV(H)    WKHD          WKPR
     C     WKPR          MULT(H)   100           WKSLPR
      *
     C     LBS(2)        DIV(H)    WKHD          WKIALB
     C     FED(2)        DIV(H)    WKHD          WKFALB
     C                   ENDIF
      *
     C                   EXCEPT    TPTOT
     C                   ENDIF
      *
     C                   MOVEL     WGPPCD        SVPPCD
      *
      * CLEAR TWO ARRAYS WHOSE VALUES ARE NOT PRINTED
      *
     C                   Z-ADD     0             LBS(2)
     C                   Z-ADD     0             FED(2)
      *
      *
     C                   ENDSR
      *
      *****************************************************************
      * $TFTOT - PRINT FARM SITE TOTAL
      *****************************************************************
      *
     C     $TFTOT        BEGSR
      *
      * THE FARM TOTAL LINE IS ONLY PRINTED IF A) THERE WAS MORE THAN ONE
      * GROUP PRINTED FOR THE FARM IN THE DETAIL VERSION OR B) YOU ARE
      * PRINTING THE SUMMARY VERSION. SO, CONDITIONALLY CALCULATE THE FARM TOTAL
      * LINE VALUES AND PRINT THE TOTAL LINE.
      * THE CALCULATED VALUES ARE:
      *
      *  1) PERCENTS: CULL SALE, DEAD, MARKET SALE
      *  2) AVERAGE POUNDS TRANSFERRED IN
      *  3) AVERAGE FEED POUNDS CONSUMED
      *
     C                   if        wkct > 1 or ldrpfl = 'S'                     IF TOTAL
      *
     C     TIS(1)        ADD       TID(1)        WKHD
      *
     C     WKHD          IFNE      0                                            IF HEAD IN
      *
     C     CUL(1)        DIV       WKHD          WKPR
     C     WKPR          MULT(H)   100           WKCUPR
      *
     C     DED(1)        DIV(H)    WKHD          WKPR
     C     WKPR          MULT(H)   100           WKDDPR
      *
     C     MKT(1)        DIV(H)    WKHD          WKPR
     C     WKPR          MULT(H)   100           WKSLPR
      *
     C     LBS(1)        DIV(H)    WKHD          WKIALB
     C     FED(1)        DIV(H)    WKHD          WKFALB
     C                   ENDIF                                                  IF HEAD IN
      *
     C                   EXCEPT    TFTOT
     C                   ENDIF                                                  IF TOTAL
      *
      * RESET THE COUNTER FOR GROUPS IN EACH FARM
      *
     C                   Z-ADD     0             WKCT
      *
     C                   Z-ADD     WGFSCD        SVFSCD
      *
      * CLEAR ALL FIELDS THAT ARE PRINTED ON THE FARM TOTAL LINE SINCE
      * THE LINE MAY OR MAY NOT HAVE BEEN PRINTED. THUS, THE 'BLANK AFTER'
      * ON THE PRINT LINE MAY OR MAY NOT HAVE BEEN ACTIVATED.
      *
     C                   Z-ADD     0             LBS(1)
     C                   Z-ADD     0             FED(1)
     C                   Z-ADD     0             TIS(1)
     C                   Z-ADD     0             TOD(1)
     C                   Z-ADD     0             INV(1)
     C                   Z-ADD     0             TOD(1)
     C                   Z-ADD     0             TID(1)
     C                   Z-ADD     0             CUL(1)
     C                   Z-ADD     0             DED(1)
     C                   Z-ADD     0             TOS(1)
     C                   Z-ADD     0             MKT(1)
     C                   Z-ADD     0             ADJ(1)
     C                   Z-ADD     0             ADJ(1)
     C                   Z-ADD     0             WKIALB
     C                   Z-ADD     0             WKFALB
     C                   Z-ADD     0             WKCUPR
     C                   Z-ADD     0             WKDDPR
     C                   Z-ADD     0             WKSLPR
      *
     C                   ENDSR
      /EJECT
      *****************************************************************
      * $FIRST - FIRST TIME THROUGH
      *****************************************************************
      *
     C     $FIRST        BEGSR
      *
     C                   MOVEL     NO            FIRST
      *
     C                   MOVEL     WGPTCD        SVPTCD
     C                   MOVEL     WGPPCD        SVPPCD
     C                   Z-ADD     WGFSCD        SVFSCD
      *
      * RETRIEVE DESCRIPTIONS/NAMES: PRODUCTION TYPE, PHASE, FARM
      *
     C                   EXSR      $PTDS
     C                   EXSR      $PPDS
     C                   EXSR      $FSNM
      *
      * PRINT HEADINGS: REPORT AND FARM
      *
     C                   EXCEPT    HRHDR
     C                   EXCEPT    HFHDR
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * *INZSR - INITIALIZATION SUBROUTINE
      *---------------------------------------------------------------
      *
     C     *INZSR        BEGSR
      *
      * DEFINE PRINT FIELDS FOR THE REPORT HEADING
      *
     C     *LIKE         DEFINE    PTPTDS        HRPTDS
     C     *LIKE         DEFINE    PPPPDS        HRPPDS
     C                   MOVE      *BLANK        HRGTCD            3
      *
      * DEFINE PRINT FIELDS FOR DETAIL LINE
      *
     C     *LIKE         DEFINE    FSFSNM        DTFSNM
     C                   Z-ADD     0             WKCUPR            5 2
     C                   Z-ADD     0             WKDDPR            5 2
     C                   Z-ADD     0             WKSLPR            5 2
     C     *LIKE         DEFINE    WGIALB        WKIALB
     C     *LIKE         DEFINE    WGFALB        WKFALB
      *
      * WORKFIELDS
     C                   Z-ADD     0             WKHD             15 0
     C                   Z-ADD     0             WKPR             15 7
     C                   Z-ADD     0             WKCT              3 0
      *
      * DID THE USER ELECT TO PAGE BREAK ON FARM SITE?
      *
     C     LDPBFL        IFEQ      YES
     C                   MOVEL     'YES'         HRPBFL            3
     C                   ELSE
     C                   MOVEL     'NO '         HRPBFL
     C                   ENDIF
      *
      * DEFINE PRINT FIELDS FOR THE FARM HEADING
      *
     C     *LIKE         DEFINE    FSFSNM        HFFSNM
     C     *LIKE         DEFINE    FSFSCD        HFFSCD
      *
      * DEFINE SAVE FIELDS FOR CONTROL BREAKS
      *
     C     *LIKE         DEFINE    WGPTCD        SVPTCD
     C     *LIKE         DEFINE    WGPPCD        SVPPCD
     C     *LIKE         DEFINE    WGFSCD        SVFSCD
     C     *LIKE         DEFINE    FSFSNM        SVFSNM
      *
      * IF THE USER ELECTED TO PRINT AN UNDERSCORE FOR PENCILED
      * ENTRY OF DATA, SET ON THE INDICATOR TO PRINT THE UNDERSCORE.
      *
     C     LDENFL        IFEQ      YES
     C                   SETON                                        81
     C                   ENDIF
      *
      * IF THE USER ELECTED THE SUMMARY VERSION, SET AN INDICATOR TO
      * CONTROL PRINTING.
      *
     C     LDRPFL        IFEQ      'S'
     C                   SETON                                        87
     C                   ENDIF
      *
      * SET UP REPORT HEADINGS
      *
      * BUSINESS OFFICE
      *
     C     LDFSBO        IFEQ      *BLANK
     C                   MOVEL(P)  ALL           LDFSBO
     C                   ENDIF
      *
      * PRODUCTION TYPE
      *
     C     LDPTCD        IFEQ      *BLANK
     C                   MOVEL(P)  ALL           LDPTCD
     C                   ENDIF
      *
      * PRODUCTION PHASE
      *
     C     LDPPCD        IFEQ      *BLANK
     C                   MOVEL(P)  ALL           LDPPCD
     C                   ENDIF
      *
      * ALL GROUP TYPES
      *
     C     LDGTCD        IFEQ      *BLANK
     C                   MOVEL(P)  ALL           HRGTCD
     C                   ELSE
     C                   MOVEL     LDGTCD        HRGTCD
     C                   ENDIF
      *
      * ALL CELLS
     C     LDCELL        IFEQ      0
     C                   SETON                                        88
     C                   ENDIF
      *
      * ALL FARMS
      *
     C     LDFSCD        IFEQ      0
     C                   SETON                                        89
     C                   ENDIF
      *
      * HOG GROUP CODE
      *
     C     LDHGCD        IFEQ      *BLANK
     C                   MOVEL     ALL           LDHGCD
     C                   ENDIF
      *
      * SET UP A FIRST TIME FLAG
      * SET UP HEADING UNDERLINE FIELD FOR PRINT
      * RETRIEVE TIME FOR REPORT HEADING
      *
     C                   MOVE      YES           FIRST             1
     C                   MOVEL     *ALL'-'       DASH            198
     C                   TIME                    RTIME             6 0
      *
     C                   ENDSR
      /EJECT
      *-------------------------------------------------------------
      * REPORT HEADING LINES
      *-------------------------------------------------------------
      *
     Oprint198  e            hrhdr          1 01
     O                       SDPGM               10
     O                                          108 'HOG PRODUCTION SYSTEM'
     O                                          189 'DATE'
     O                       UDATE         Y    198
      *
     O          E            HRHDR          1
     O                       SDUSR               10
     O                                          110 'INVENTORY REPORT BY SITE'
     O                                          189 'TIME'
     O                       RTIME              198 '  :  :  '
      *
     O          E            HRHDR          1
     O               87                         101 'SUMMARY'
     O              N87                         101 'DETAIL'
     O                                          189 'PAGE'
     O                       PAGE          Z    198
      *
     O          E            HRHDR          1
     O                                           16 'BUSINESS OFFICE '
     O                                           22 '.....'
     O                       LDFSBO              29
     O                       LDBODS              63
      *
     O          E            HRHDR          1
     O                                           13 'CUT-OFF DATE '
     O                                           22 '........'
     O                       LDCMDY        Y     32
      *
      *
     O          E            HRHDR          1
     O                                           22 'PRODUCTION TYPE ......'
     O                       LDPTCD              29
     O                       LDPTDS              43
      *
     O          E            HRHDR          1
     O                                           22 'PRODUCTION PHASE .....'
     O                       LDPPCD              29
     O                       LDPPDS              63
      *
     O          E            HRHDR          1
     O                                           22 'GROUP TYPE ...........'
     O                       HRGTCD              27
     O                       LDGTDS              48
      *
     O          E            HRHDR          1
     O                                           22 'CELL .................'
     O              N88      LDCELL        M     27
     O               88                          27 'ALL'
      *
     O          E            HRHDR          1
     O                                           22 'FARM SITE ............'
     O              N89      LDFSCD        M     30
     O               89                          27 'ALL'
     O                       LDFSNM              58
      *
     O          E            HRHDR          1
     O                                           22 'PAGE BREAK ON FARM ...'
     O                       HRPBFL              27
      *
     O          E            HRHDR          2
     O                                           22 'HOG GROUP ............'
     O                       LDHGCD              31
      *
      *-------------------------------------------------------------
      * PRODUCTION TYPE / PRODUCTION PHASE
      *-------------------------------------------------------------
      *
     O          E            HRHDR          2
     O                       HRPTDS              15
     O                                           17 '-'
     O                       HRPPDS              48
      *
      *-------------------------------------------------------------
      * COLUMN HEADING LINES
      *-------------------------------------------------------------
      *
     O          E            HRHDR          1
     O                                           88 'TRANSFER IN HEAD'
     O                                           95 'AVG'
     O                                          104 'FEED'
     O                                          137 'DAILY DEAD'
     O                                          158 'TRANSFER OUT HEAD'
      *
     O          E            HRHDR          1
     O                                            7 'WK-DAYS'
     O                                           13 'HOG'
     O                                           24 'GROUP'
     O                                           30 'FARM'
     O                                           68 'GEN'
      *
     O                                           79 'WITHIN'
     O                                           87 'PRIOR'
     O                                           95 'LBS'
     O                                          105 'LBS PER'
     O                                          121 'CULLS SOLD'
     O                                          135 'AND DOA'
     O                                          148 'WITHIN'
     O                                          157 'NEXT'
     O                                          174 'MARKET SALES'
     O                                          187 'INVENTORY'
      *
     O          E            HRHDR          1
     O                                            8 'IN PHASE'
     O                                           15 'GROUP'
     O                                           23 'TYPE'
     O                                           30 'SITE'
     O                                           41 'FARM NAME'
     O                                           57 'BLDG'
     O                                           63 'ROOM'
     O                                           69 'LINE'
     O                                           79 'PHASE'
     O                                           87 'PHASE'
      *
     O                                           95 'IN'
     O                                          105 'HEAD IN'
     O                                          113 'HEAD'
     O                                          122 'PERCENT'
     O                                          130 'HEAD'
     O                                          139 'PERCENT'
     O                                          148 'PHASE'
     O                                          157 'PHASE'
     O                                          166 'HEAD'
     O                                          175 'PERCENT'
     O                                          187 'ADJUSTMENT'
     O                                          198 'INVENTORY'
      *
     O          E            HRHDR          2
     O                       DASH               198
      *
      *------------------------------------------------------------------
      * FARM SITE HEADING (only prints for 'detail' version--not summary)
      *------------------------------------------------------------------
      *
     O          E   N87      HFHDR          1
     O                       HFFSCD        M     31
     O                       HFFSNM              57
      *
      *-------------------------------------------------------------
      * DETAIL LINE (only prints for 'detail' version--not summary)
      *-------------------------------------------------------------
      *
     O          E   N87      DTLINE         1
     O                       WGWK          ZB     2
     O                                            4 '-'
     O                       WGDAY         ZB     6
     O                       WGHGCD         B    17
     O                       WGGTCD         B    22
     O                       WGBLCD         B    58
     O                       WGRMCD         B    64
     O                       WGGNCD         B    70
      *
     O                       WGISHD        KB    80
     O                       WGIDHD        KB    88
      *
     O                       WGIALB        KB    96
     O                       WGFALB        KB   106
      *
     O                       WGCUHD        KB   114
     O                       WKCUPR        KB   123
     O                       WGDDHD        KB   131
     O                       WKDDPR        KB   139
      *
     O                       WGOSHD        KB   149
     O                       WGODHD        KB   158
      *
     O                       WGSLHD        KB   167
     O                       WKSLPR        KB   176
     O                       WGAJHD        KB   186
     O                       WGEIHD        KB   198
      *
     O          E   N87 81   DTLINE         1
     O                                           80 '______'
     O                                           88 '______'
     O                                          114 '______'
     O                                          131 '______'
     O                                          149 '______'
     O                                          158 '______'
     O                                          167 '______'
     O                                          186 '______'
     O                                          198 '______'
      *
      *-------------------------------------------------------------
      * FARM SITE TOTAL LINE
      *-------------------------------------------------------------
      *
     O          E            TFTOT       1  1
     O              N87                          24 '* FARM'
     O                       SVFSCD        M     31
     O               87      SVFSNM              57
     O                       TIS(1)        KB    80
     O                       WKIALB        KB    96
     O                       WKFALB        KB   106
     O                       WKCUPR        KB   123
     O                       WKDDPR        KB   139
     O                       TOD(1)        KB   158
     O                       WKSLPR        KB   176
     O                       INV(1)        KB   198
      *
     O          E            TFTOT          2
     O                       TID(1)        KB    88
     O                       CUL(1)        KB   114
     O                       DED(1)        KB   131
     O                       TOS(1)        KB   149
     O                       MKT(1)        KB   167
     O                       ADJ(1)        KB   186
      *
      *-------------------------------------------------------------
      * PRODUCTION PHASE TOTAL LINE
      *-------------------------------------------------------------
      *
     O          E            TPTOT       1  1
     O                                           10 '** PHASE'
     O                       SVPPCD              17
     O                       TIS(2)        KB    80
     O                       WKIALB        KB    96
     O                       WKFALB        KB   106
     O                       WKCUPR        KB   123
     O                       WKDDPR        KB   139
     O                       TOD(2)        KB   158
     O                       WKSLPR        KB   176
     O                       INV(2)        KB   198
      *
     O          E            TPTOT          2
     O                       TID(2)        KB    88
     O                       CUL(2)        KB   114
     O                       DED(2)        KB   131
     O                       TOS(2)        KB   149
     O                       MKT(2)        KB   167
     O                       ADJ(2)        KB   186
      *
      *-------------------------------------------------------------
      * PRODUCTION TYPE TOTAL LINE
      *-------------------------------------------------------------
      *
     O          E            TTTOT       1  1
     O                                            9 '*** TYPE'
     O                       SVPTCD              17
     O                       TIS(3)        KB    80
     O                       WKIALB        KB    96
     O                       WKFALB        KB   106
     O                       WKCUPR        KB   123
     O                       WKDDPR        KB   139
     O                       TOD(3)        KB   158
     O                       WKSLPR        KB   176
     O                       INV(3)        KB   198
      *
     O          E            TTTOT          2
     O                       TID(3)        KB    88
     O                       CUL(3)        KB   114
     O                       DED(3)        KB   131
     O                       TOS(3)        KB   149
     O                       MKT(3)        KB   167
     O                       ADJ(3)        KB   186
      *
      *-------------------------------------------------------------
      * REPORT TOTALS
      *-------------------------------------------------------------
      *
     O          E            TRTOT          1
     O                                            4 '****'
     O                                           17 'REPORT TOTAL'
     O                       TIS(4)        KB    80
     O                       WKIALB        KB    96
     O                       WKFALB        KB   106
     O                       WKCUPR        KB   123
     O                       WKDDPR        KB   139
     O                       TOD(4)        KB   158
     O                       WKSLPR        KB   176
     O                       INV(4)        KB   198
      *
     O          E            TRTOT          1
     O                       TID(4)        KB    88
     O                       CUL(4)        KB   114
     O                       DED(4)        KB   131
     O                       TOS(4)        KB   149
     O                       MKT(4)        KB   167
     O                       ADJ(4)        KB   186
      *
      *-------------------------------------------------------------
      * NO DATA MESSAGE LINE
      *-------------------------------------------------------------
      *
     O          E            NODATA         1
     O                                           19 'NO DATA MEETS YOUR'
     O                                           38 'SELECTION CRITERIA'
