     A*%%TS  SD  19950419  130922  ISLRAMS     REL-V3R0M5  5763-PW1
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD153
     A*  TITLE:         MAINTAIN INSTALLED EQUIPEMNT
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  CREATED:       03/21/94
     A*
     A*  INDICATOR 60 IS ON WHEN IN REVISE MODE
     A*  INDICATOR 61 IS ON WHEN IN CREATE MODE
     A*
     A*  INDICATOR 62 IS ON WHEN USER HAS DELETED OR CREATED A RECORD
     A*               IN THE SUBFILE
     A*  INDICATOR 90 IS ON WHEN THE BUILDING/ROOM THE USER HAS
     A*               SELECTED FROM THE WORK WITH HAS BEEN DELETED
     A*               BY ANOTHER USER
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A N60                                  CF04(04 'PROMPT')
     A N90                                  CF05(05 'REFRESH')
     A  60N90                               CF06(06 'CREATE')
     A                                      CA12(12 'PREVIOUS')
     A N82N90                               ROLLUP(25 'PAGE FORWARD')
     A*
     A*--------------------------------------------------------------*
     A* SUBFILE FORMAT ONE
     A*--------------------------------------------------------------*
     A          R HP153S1                   SFL
     A*%%TS  SD  19950419  130922  ISLRAMS     REL-V3R0M5  5763-PW1
     A                                      SETOF(30)
     A                                      SETOF(31)
     A                                      SETOF(62)
     A  85                                  SFLNXTCHG
     A            HDRRN1         4Y 0H
     A            DFOPT          2A  B 11  2CHGINPDFT(UL)
     A  61
     AO 62
     AO 90                                  DSPATR(PR)
     A  61
     AO 90                                  DSPATR(ND)
     A  30                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A N61N62N90
     A N30                                  DSPATR(HI)
     A N61N62N90
     A N30 60                               DSPATR(UL)
     A            DFEQCD    R        B 11  8REFFLD(BRRMCD)
     A  60
     AO 62
     AO 90                                  DSPATR(PR)
     A  61 90                               DSPATR(ND)
     A  31                                  DSPATR(RI)
     A  31
     AO 51                                  DSPATR(PC)
     A N60N62N90
     A N31 61                               DSPATR(HI)
     A N60N62N90
     A N31 61                               DSPATR(UL)
     A            DFEQDS    R        O 11 20REFFLD(EQEQDS)
     A            DFETCD    R        O 11 45REFFLD(EQETCD)
     A            DFACT          8A  O 11 58
     A*
     A*--------------------------------------------------------------*
     A* SUBFILE CONTROL FORMAT ONE
     A*--------------------------------------------------------------*
     A          R HP153C1                   SFLCTL(HP153S1)
     A*%%TS  SD  19950419  130922  ISLRAMS     REL-V3R0M5  5763-PW1
     A                                      SFLSIZ(0100)
     A                                      SFLPAG(0011)
     A                                      SFLCSRRRN(&HDRR1)
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A                                      OVERLAY
     A                                      RTNCSRLOC(&RCD &FLD)
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            HDRR1          5S 0H
     A            RCD           10A  H
     A            FLD           10A  H
     A            C1PPCD    R        H      REFFLD(FSPPCD)
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 25'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A            DFMODE    R        O  2  2REFFLD(@@MODE)
     A                                      DSPATR(HI)
     A                                  2 22'MAINTAIN INSTALLED EQUIPMENT'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  4  2'Farm site ..:'
     A            C1FSCD    R        O  4 17REFFLD(FSFSCD)
     A                                      EDTCDE(M)
     A            C1FSNM    R        O  4 24REFFLD(FSFSNM)
     A                                  5  2'Building ...:'
     A            C1BLCD    R        O  5 17REFFLD(FBBLCD)
     A            C1BLDS    R        O  5 24REFFLD(FBBLDS)
     A                                  6  2'Room .......:'
     A            C1RMCD    R        O  6 17REFFLD(IERMCD)
     A N61                              8  2'4=Delete'
     A                                  9 10'?'
     A                                      DSPATR(HI)
     A                                  9 43'Equipment'
     A                                      DSPATR(HI)
     A N61                             10  2'Opt'
     A                                      DSPATR(HI)
     A                                 10  8'Equipment'
     A                                      DSPATR(HI)
     A                                 10 20'Description'
     A                                      DSPATR(HI)
     A                                 10 45'Type'
     A                                      DSPATR(HI)
     A*
     A*--------------------------------------------------------------*
     A* COMMAND KEY RECORD 1
     A*--------------------------------------------------------------*
     A          R HP153K1
     A*%%TS  SD  19940321  103222  ISLRAMS     REL-V2R2M0  5738-PW1
     A                                      SETOF(60)
     A                                      SETOF(61)
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A                                      DSPATR(HI)
     A                                 23  2'F3=Exit'
     A N60N90                          23 11'F4=Prompt'
     A N90                             23 22'F5=Refresh'
     A  60N90                          23 34'F6=Create'
     A                                 23 45'F12=Previous'
     A*
     A*--------------------------------------------------------------*
     A* ERROR MESSAGE SUBFILE FORMAT
     A*--------------------------------------------------------------*
     A          R HP153ES                   SFL SFLMSGRCD(24)
     A*
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
     A*                                                                *********
     A*--------------------------------------------------------------*
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*--------------------------------------------------------------*
     A          R HP153EC                   SFLCTL(HP153ES)
     A*
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
