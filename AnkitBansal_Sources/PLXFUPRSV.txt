     H*****************************************************************
     H*
     H*  Program number:  PLXFUPR
     H*  Program name:    Read Workfile for OM Invoices & Print W/M3 DIM ACCTS
     H*  Programmer:      ROSE CENTONZE AS COPIED FROM OMINVJDE3
     H*  Date created:    06/26/15
     H*  Program purpose: This program will read the workfile created
     H*                   in OMINVJDE1 by account id to generate
     H*                   a report. THIS PRINTS THE M3 DIM1-DIM7 INSTEAD OF THE JDE #S
     H*****************************************************************
     A*  RMC 1/20/17  Project  E8287
     A*               Changed LF OML300A to OML300F  SEL LIVE WITH M3 GL =Y,P FOR  Yes or Parallel
     A*  RMC 6/05/17  Project  E9083
     A*               get m3 acct with cgl code if CC/OBJ/Sub is blank
     H*****************************************************************
     F*
     F*OML300A   IF   E           K DISK
     FOML300F   IF   E           K DISK
     F* OM Workfile            (Key: Co, Account ID, Order #)
     F**PLB8REL0  IF   E           K DISK                   used in xf to get DIMs
     F** G/L JDE to M3 Cross Ref (Key: JDE CC OBJ SUB)
     Fprint198  O    F  198        PRINTER OFLIND(*INOF)
     F*
     D*
     D***  ACTUAL SHIP DATE FROM WORKFILE  YYMMDD
     D*
     D                 DS
     D  GLYMD                  1      7  0
     D  YYGL                   2      3  0
     D  MMGL                   4      5  0
     D  DDGL                   6      7  0
     D*
     D***  G/L DATE MMDDYY
     D*
     D                 DS
     D  GLMDY                  1      6  0
     D  GLMM                   1      2  0
     D  GLDD                   3      4  0
     D  GLYY                   5      6  0
     D*
     D***  INVALID ACCOUNT ERROR MESSAGE
     D*
     D                 DS
     D  MSG                    1     15
     D  ERR1                   1      7    INZ('INVALID')
     D  ERR2                   9     15    INZ('ACCOUNT')
     I*
     C*
     C*----------------------------------------------------------------
     C* MAIN PROCESSING LOOP
     C*----------------------------------------------------------------
     C*   Read Workfile for the all records in the file
     C*
     C     *loval        SETLL     OMP300R
     C     *IN50         DOUEQ     *ON
     C                   READ      OMP300R                                50
     C     *IN50         IFEQ      *OFF                                         HAVE RECORD
     C*  ---------
     C* FIRST TIME THROUGH SETUP BATCH NUMBER
     C* --------
     C     *IN91         IFEQ      *OFF                                          BEG ONE TIME
      *
     C                   MOVE      *ON           *IN91
     C*
     C*  get new company name
     C*
     c                   exsr      $newco
     C*
      * get new account description
     C*
     c                   exsr      $newacct
     C*
     C                   ENDIF                                                  END ONE TIME
     C* ________________
      * If the Account or the Company changes = print totals
     C*
     C                   IF        omaid <> SVGLID or
     C                             omco# <> svco#
     C*
     C* Print the account description on overflow
     C*
     C                   IF        *inof = *on
     C                   EXCEPT    HDG
     C                   EXCEPT    ACTDES
     C                   ENDIF
     C                   EXCEPT    TOTACT
     C                   Z-ADD     0             ACTamt
     C                   Z-ADD     0             ACTunit
     C*
     C*_____________
     C* if company changes get new company name
     C*
     C                   IF        omco# <> svco#
     c                   exsr      $newco
     C                   endif                                                  END ONE TIME
     C*
      * get new account description
     C*
     c                   exsr      $newacct
     C                   Endif                                                   DIFFERENT
     C*
     C*  CONVERT ACTUAL SHIP DATE FROM YYMMDD TO MMDDYY
     C*
     C                   Z-ADD     OMASDT        GLYMD
     C                   Z-ADD     YYGL          GLYY
     C                   Z-ADD     MMGL          GLMM
     C                   Z-ADD     DDGL          GLDD
     C*
     C* ACCUMULATE AMOUNT and pounds INTO TOTAL FOR ACCOUNT
     C*
     C                   ADD       OMAMT         ACTamt           15 2
     C                   ADD       OMunit        ACTunit          15 2
     C                   MOVEL     OMTEXT        WKTXT            23
     C*
     C                   MOVEL     OMitxt        wkitxt           20
     C*
     C* Print the account description on overflow
     C*
     C                   IF        *inof = *on
     C                   EXCEPT    HDG
     C                   EXCEPT    ACTDES
     C                   endif
     C                   EXCEPT    DET
     C*
     C                   ENDIF                                                  50 = OFF
     C                   ENDDO                                                  50 DOUEQ ON
     C*
     C/EJECT
     C*----------------------------------------------------------------
     C* EOF PROCESSING:
     C*----------------------------------------------------------------
     C*
     C* BG990420:  IF NO REORDS TO PROCESS, PRINT MESSAGE
     C*
     C     *IN91         IFEQ      *OFF
     C                   EXCEPT    HDG
     C                   EXCEPT    NOREC
     C                   ELSE
     C*
     C* Print the account description on overflow
     C*
     C                   IF        *inof = *on
     C                   EXCEPT    HDG
     C                   EXCEPT    ACTDES
     C                   endif
     C                   EXCEPT    TOTACT
     C                   ENDIF
     C*
     C                   MOVE      *ON           *INLR
      *-----------------------------------------------------------------------------------
      * COMPANY BREAK - get company name
      *-----------------------------------------------------------------------------------
      *
     C     $newco        begsr
      *
     C                   MOVE      OMco#         SVco#             3 0
     C                   MOVE      *BLANKS       WKNAM            30
     C                   CALL      'PDGZXFR'
     C                   PARM                    RETRN             7
     C                   PARM      svco#         CO#               3 0
     C     WKNAM         PARM                    NAME             30
     C                   PARM                    ERROR             1
     C*
     C                   TIME                    RTIME             6 0
     C                   EXCEPT    HDG
      *
     C                   endsr
      *-----------------------------------------------------------------------------------
      * ACCOUNT # BREAK
      *-----------------------------------------------------------------------------------
      *
     C     $newacct      begsr
      *
     C                   MOVE      OMAID         SVGLID            8
      *
     C*    SVGLID        CHAIN     I0901                              55
     C*    *IN55         IFEQ      *ON
     C*                  MOVEL     MSG           WKADES           30
     C*                  MOVE      *on           *in95
     C*                  ELSE
     C*                  MOVEL     GMDL01        WKADES
     C*                  MOVE      *off          *in95
     C*                  ENDIF
     C***********************************************************
     C*  GOTO    JDE-M3 XREF TABLE FOR DIMS
     C***********************************************************
     C*                  MOVE      *off          *in95
     c     ommcu         ifne      *blanks
     C                   CALL      'PLXGXFR'
     C                   PARM                    Retrn
     C                   PARM                    RTNCDE            7
     C                   PARM                    OMMCU
     C                   PARM                    OMOBJ
     C                   PARM                    OMSUB
     C                   PARM                    DSDIM1            8
     C                   PARM                    DSDIM2            8
     C                   PARM                    DSDIM3            8
     C                   PARM                    DSDIM4            8
     C                   PARM                    DSDIM5            8
     C                   PARM                    DSDIM6            8
     C                   PARM                    DSDIM7            8
     C                   PARM                    wkades           30
     C     rtncde        IFEQ      'Y2U0005'                                    m3 DIMs not found
     C                   MOVEL     MSG           WKADES           30
     C                   MOVE      *on           *in95
     c                   else
     c                   move      *off          *in95
     C                   ENDIF
     c                   else
     c* Get DIMs with CGL Code
     c     omaid         ifne      *blanks
     C                   CALL      'PLXZXFR'
     C                   PARM                    RTNCDE            7
     C                   PARM                    OMAID
     C                   PARM                    DSDIM1            8
     C                   PARM                    DSDIM2            8
     C                   PARM                    DSDIM3            8
     C                   PARM                    DSDIM4            8
     C                   PARM                    DSDIM5            8
     C                   PARM                    DSDIM6            8
     C                   PARM                    DSDIM7            8
     C                   PARM                    wkades           30
     C     rtncde        IFEQ      'Y2U0005'                                    m3 DIMs not found
     C                   MOVE      *on           *in95
     c                   else
     c                   move      *off          *in95
     C                   ENDIF
     C                   ENDIF
     C                   ENDIF
      *
     C     omsbl         IFne      *blanks                                      m3 DIMs not found
     c                   evalr     dsdim7=%subst(omsbl:%check('0':omsbl))       left adj, remv 00-
     C                   eval      dsdim7= %triml(dsdim7)
     C                   ENDIF
     C*
     C   OF              EXCEPT    HDG
     C                   EXCEPT    ACTDES
     C                   endsr
     C/EJECT
     Oprint198  E            HDG            1 01
     O                                            7 'PLXFUPR'
     O                                           63 'ORDER MANAGEMENT SY'
     O                                           67 'STEM'
     O                       UDATE         Y    131
     O          E            HDG            1
     O                                           57 'INVOICE POST LIST'
     O                                           68 'BY ACCOUNT'
     O                       RTIME              131 '0 :  :  '
     O          E            HDG            1
     O                                          127 'PAGE'
     O                       PAGE          Z    131
      *
     O          E            HDG            2
     O                                           18 'COMPANY ... :'
     O                       svco#         Z     22
     O                       WKNAM               53
      *
     O          E            HDG            1
     O*                                          19 'COST CENTER  OBJECT'
     O*                                          28 'SUB CODE'
     O*                                          90 'FIN/ACC'
     O                                           26 'DIM1     DIM2     DIM3'
     O                                           53 'DIM4     DIM5     DIM6'
     O                                           62 'DIM7'
      *
     O          E            HDG            2
     O                                           24 'ORDER # SHIP DATE'
     O                                           46 'DOCUMENT  ITEM    GRP'
     O                                           62 'CLS DESCRIPTION'
     O                                           80 'SUBLEDGER'
     O                                          103 'CODE   DESCRIPTION'
     O                                          126 'AMOUNT'
     O                                          156 'UNITS'
      *
     O          E            ACTDES         2
     O               95      OMMCU               12
     O               95      OMOBJ               19
     O               95      OMSUB               28
     O              N95      dsdim1              12
     O              N95      dsdim2              21
     O              N95      dsdim3              30
     O              N95      dsdim4              39
     O              N95      dsdim5              48
     O              N95      dsdim6              57
     O              N95      dsdim7              66
     O              n95      WKADES             100
     O               95      WKADES              59
     O               95      msg                 73
     O               95                          80 '*****'
      *
     O          E            DET            1
     O                       OMORD#        Z     10
     O                                           11 '-'
     O                       OMSEQ#        Z     14
     O                       GLMDY         Y     23
     O                       OMDOC#        Z     33
     O                       OMSITM        Z     42
     O                       OMIGRP        Z     46
     O                       OMICLS        Z     50
     O                       wkitxt              71
     O                       omsbl               80
     O                       omsblty             82
     O                       omsblfl             84
     O                       OMFTYP              88
     O                       OMCODE              92
     O                       WKTXT              118
     O                       OMAMT         J    134
     O                       OMunit        J    158
      *
     O          E            TOTACT      1  2
     O                       ACTamt        J    133
     O                       ACTunit       J    158
     O                                          161 '**'
      *
     O          E            NOREC       1  2
     O                                           55 'No records to process'
