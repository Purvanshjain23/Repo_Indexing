     A*%%TS  SD  19960328  102846  ISLRAMS     REL-V3R1M0  5763-PW1
     A*****************************************************************
     A*
     A*  SYSTEM:        HOG PRODUCTION
     A*  DISPLAY FILE:  HPD5001 - SELECT MOVEMENT TYPE OF SALE
     A*                           OR TRANSFER
     A*  CREATED BY:    LEANNE FEDOR
     A*  DATE:          03/28/96
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      PRINT
     A                                      CA12(12 'PREVIOUS')
     A N82                                  ROLLUP(25 'PAGE FORWARD')
     A*----------------------------------------------------------------
     A* REFERENCE WINDOW RECORD FORMAT 1
     A*----------------------------------------------------------------
     A          R HP5001R1
     A*%%TS  SD  19960328  102846  ISLRAMS     REL-V3R1M0  5763-PW1
     A                                      TEXT('Reference window')
     A                                      WINDOW(*DFT 08 41)
     A                                      WDWBORDER((*DSPATR RI) (*CHAR '    -
     A                                          '))
     A*----------------------------------------------------------------
     A* RECORD FORMAT ONE
     A*----------------------------------------------------------------
     A          R HP5001R2
     A                                      TEXT('Written in RPG pgm to prevent-
     A                                       screen from clearing')
     A                                      CLRL(*NO)
     A                                      OVERLAY
     A*----------------------------------------------------------------
     A* RECORD FORMAT TWO
     A*----------------------------------------------------------------
     A* THIS FORMAT IS NOT WRITTEN IN THE RPG PGM.  IT PREVENTS THE
     A* CALLING_PROGRAM_SCREEN FROM BEING CLEARED.
     A*----------------------------------------------------------------
     A          R HP5001R3
     A                                      TEXT('Dummy format not written in R-
     A                                      PG pgm.')
     A                                      ASSUME
     A                                 08  1' '
     A*----------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*----------------------------------------------------------------
     A          R HP5001S1                  SFL
     A                                      TEXT('subfile record format one')
     A            HDRRN1         4S 0H
     A            DFMTCD    R        O  4  1REFFLD(@@MTCD)
     A            DFMTDS    R        O  4  5REFFLD(MTMTDS)
     A*----------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*----------------------------------------------------------------
     A          R HP5001C1                  SFLCTL(HP5001S1)
     A*%%TS  SD  19960328  102846  ISLRAMS     REL-V3R1M0  5763-PW1
     A                                      SFLSIZ(0005)
     A                                      SFLPAG(0002)
     A                                      WINDOW(HP5001R1)
     A                                      TEXT('control format one')
     A                                      OVERLAY
     A                                      SFLCSRRRN(&HDRRN)
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND(*MORE)
     A                                      RMVWDW
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            HDRRN          5S 0H
     A                                  1  1'SELECT MOVEMENT TYPE OF SALE OR TR-
     A                                      ANSFER'
     A                                      DSPATR(HI)
     A                                  2  1'Position Cursor, Press Enter'
     A*----------------------------------------------------------------
     A* COMMAND LINE ONE
     A*----------------------------------------------------------------
     A          R HP5001K1
     A                                      WINDOW(HP5001R1)
     A                                  7  1'F12=Previous'
