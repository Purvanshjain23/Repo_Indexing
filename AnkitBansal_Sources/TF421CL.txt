/********************************************************************* */
/*                                                                     */
/*  SYSTEM:          TRIUMPH FOODS                                     */
/*  PROGRAM NUMBER:  TF421CL                                           */
/*  PROGRAM NAME:    VERIFICATION FOR FREIGHT VARIANCE REPORT/DOWNLOAD */
/*  PROGRAMMER:      LEANNE FEDOR                                      */
/*  CREATE DATE:     04/25/06                                          */
/*                                                                     */
/*  FUNCTION:        VERIFICATION FOR FREIGHT VARIANCE REPORT/DOWNLOAD */
/*                                                                     */
/***********************************************************************/
/* MODIFICATIONS:                                                      */
/*                                                                     */
/* 05/07/09  LEANNE RAMSEY                                             */
/*           CHANGED PRINT LOGIC TO MATCH MEAT COSTING.                */
/***********************************************************************/

             PGM

             DCL        VAR(&OUTQ) TYPE(*CHAR) LEN(10)
             DCL        VAR(&HOLD) TYPE(*CHAR) LEN(4)
             DCL        VAR(&SAVE) TYPE(*CHAR) LEN(4)
             DCL        VAR(&COPY) TYPE(*DEC)  LEN(1)

             DCL        VAR(&LDPFCD)  TYPE(*CHAR) LEN(1)
             DCL        VAR(&LDYR)    TYPE(*DEC) LEN(4)
             DCL        VAR(&LDPE)    TYPE(*DEC) LEN(2)
             DCL        VAR(&LDRDFL)  TYPE(*CHAR) LEN(1)
             DCL        VAR(&LDEMAIL) TYPE(*CHAR) LEN(50)

             CHGVAR     VAR(&LDPFCD)    VALUE(%SST(*LDA 1 1))
             CHGVAR     VAR(&LDYR)      VALUE(%SST(*LDA 2 4))
             CHGVAR     VAR(&LDPE)      VALUE(%SST(*LDA 6 2))
             CHGVAR     VAR(&LDRDFL)    VALUE(%SST(*LDA 300 1))
             CHGVAR     VAR(&LDEMAIL)   VALUE(%SST(*LDA 350 50))


/*----------------------------------------------------------------*/
/* CREATE A WORKFILE OF DATA                                      */
/*----------------------------------------------------------------*/

             CRTDUPOBJ  OBJ(TFP321) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)
             CALL       PGM(TF321)


/*----------------------------------------------------------------*/
/* WHEN THE USER SELECTED THE "DOWNLOAD", EMAIL A SPREADSHEET     */
/*----------------------------------------------------------------*/

             IF         COND(&LDRDFL *EQ 'D') THEN(DO)

             CHGVAR     VAR(&LDEMAIL) VALUE(%SST(*LDA 350 50))

             EXECUTE    VIEW(*LIBL/TF321V) PCFMT(*XLS) REPLACE(*YES) +
                          RECIPIENT(&LDEMAIL) +
                          EMLMSG('TF321V-Verification for Freight +
                          Variance') TEXT('TF321V-Verification for +
                          Freight Variance')
             MONMSG     MSGID(CPF0000)

             ENDDO


/*---------------------------------------------------------------------*/
/* WHEN THE USER SELECTED THE "REPORT", PRINT THE REPORT               */
/*---------------------------------------------------------------------*/

             IF         COND(&LDRDFL *EQ 'R') THEN(DO)

/* SET PARAMETERS FOR LISTINGS                                    */

 OUTQ:       CHGVAR     VAR(&OUTQ) VALUE(%SST(*LDA 401 10))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(*JOB))

 HOLD:       CHGVAR     VAR(&HOLD) VALUE(%SST(*LDA 411 4))
             IF         COND(&HOLD *NE '*YES' *AND &HOLD *NE '*NO ') +
                          THEN(CHGVAR VAR(&HOLD) VALUE(*NO))

 COPY:       CHGVAR     VAR(&COPY) VALUE(%SST(*LDA 419 1))
             IF         COND(&COPY = 0) THEN(CHGVAR VAR(&COPY) +
                          VALUE(1))

 SAVE:       CHGVAR     VAR(&SAVE) VALUE(%SST(*LDA 415 4))
             IF         COND(&SAVE *NE '*YES' *AND &SAVE *NE '*NO ') +
                          THEN(CHGVAR VAR(&SAVE) VALUE(*NO))

             OVRPRTF    FILE(PRINT198) OUTQ(&OUTQ) COPIES(&COPY) +
                          HOLD(&HOLD) SAVE(&SAVE)

             CALL       PGM(TF621)
             ENDDO

             ENDPGM

