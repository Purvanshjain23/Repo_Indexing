      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      HOG PRODUCTION
      * PROGRAM:     HP655 - DAILY DEATH REPORT BY FARM
      * PROGRAMMER:  LEANNE FEDOR
      * CREATED:     04/05/95
      *
      * FUNCTION:    PRINTS THE DEATHS FOR GROUPS SEQUENCED BY FARM.
      *
      *              THE USER MAY ELECT A 'DETAIL' OR 'SUMMARY' VERSION
      *              OF THIS REPORT. THE 'SUMMARY' DOES NOT PRINT GROUP
      *              DATA--IT PRINTS A LINE FOR EACH FARM.
      *
      *              INDICATOR '87' IS USED TO CONTROL THE LOGIC AND
      *              PRINTING FOR THE TWO VERSIONS. INDICATOR 87 IS ON
      *              WHEN YOU ARE PRINTING THE SUMMARY VERSION AND OFF
      *              WHEN YOU ARE PRINTING THE DETAIL VERSION.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      * 10/11/95  LEANNE FEDOR
      *           ADDED LOGIC FOR A 'SUMMARY' VERSION OF THE REPORT
      *           WHICH PRINTS A LINE PER FARM WITHOUT PRINTING ANY
      *           GROUP DETAIL.
      *
      * 11/01/95  LEANNE FEDOR
      *           RECOMPILE ONLY. NEW FIELD 'GROUP TYPE' ADDED TO
      *           HOG GROUP FILE.
      *
      * 06/03/97  LEANNE FEDOR
      *           RECOMPILE ONLY. NEW FIELDS 'PURGE FLAG' AND 'DATE OF
      *           FIRST PURGE' ADDED TO HOG GROUP FILE.
      *
      * 04/13/98  LeAnne Fedor
      *           Recompile only. New field 'state' added to farm site file.
      *
      * 11/03/99  LeAnne Fedor
      *           Added building/room to detail version of report.
      *
      * 06/05/01  LeAnne Fedor
      *           Recompile only. New field 'multisite' added to Farm Site file.
      *
      * 06/28/01  LeAnne Fedor
      *           Recompile only.
      *           Three fields (manager/supervisor/multisite) renamed in Farm Site file.
      *           Manager and supervisor fields removed from Hog Group file.
      *
      * 09/06/01  LeAnne Fedor
      *           'Group type' added as a new report option. Also, new 'square feet'
      *           fields added to Farm Site file requiring a recompile.
      *
      * 12/02/02  LeAnne Fedor
      *           'Production Phase' and 'Production Type' were removed from the Killed/Dead
      *           file. So, logic was changed here to use the values from the Hog Group Master.
      *
      * 07/06/09  LeAnne Ramsey
      *           Recompile only. Added new field 'Continuous Flow Flag' to Hog Group file.
      *
      * 10/16/13  LeAnne Ramsey (E2831)
      *           Recompile only. Added field 'MTech Reference'.
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     FHSP010    IF   E           K DISK
      *    PRODUCTION TYPE
      *
      *
     FHSP011    IF   E           K DISK
      *    PRODUCTION PHASE
      *
      *
     FHSP018    IF   E           K DISK
      *    FARM SITE
      *
      *
     FHSP041    IF   E           K DISK
      *    DEATH/DISEASE REASONS
      *
      *
     FHSJ068A   IF   E           K DISK
      *    DEAD HOGS + HOG GROUP
      *    (RECORDS SELECTED/KEYED IN OPNQRY)
      *
     FQPRINT    O    F  132        PRINTER OFLIND(*INOF)
      *
      /EJECT
      ****************************************************************
      * TABLE AND ARRAY SPECIFICATIONS
      ****************************************************************
      *
      *  PRINT ARRAYS FOR TOTALS
      *
     D LBS             S             11  0 DIM(5)
     D HD              S             11  0 DIM(5)
      /EJECT
      ****************************************************************
      * INPUT SPECS
      ****************************************************************
      *
      *---------------------------------------------------------------
      * STANDARD PROGRAM STATUS DATA STRUCTURE
      *---------------------------------------------------------------
      *    EXTERNALLY DEFINED AS UTPGFR (RECORD FORMAT: PGMDSR)
     D PGMDS         ESDS                  EXTNAME(UTPGFR)
      *
      *---------------------------------------------------------------
      *  DATE FORMATS
      *---------------------------------------------------------------
      *
      * DATE IN MMDDYY FORMAT
      *
     D                 DS
     D  WKMDY                  1      6  0
     D  WKMM                   1      2  0
     D  WKDD                   3      4  0
     D  WKYY                   5      6  0
      *
      * DATE IN CCYYMMDD FORMAT
      *
     D                 DS
     D  WKCYMD                 1      8  0
     D  WKCEN                  1      2  0
     D  WKYER                  3      4  0
     D  WKMON                  5      6  0
     D  WKDAY                  7      8  0
      *
      *---------------------------------------------------------------
      *  NAMED CONSTANTS
      *---------------------------------------------------------------
      *
     D YES             C                   CONST('Y')
     D NO              C                   CONST('N')
     D UNKNWN          C                   CONST('UNKNOWN')
     D ALL             C                   CONST('ALL')
      *
      *----------------------------------------------------------------
      * LDA - REPORT SELECTIONS
      *----------------------------------------------------------------
      *
     D                UDS
     D  LDFSBO                 1      5
     D  LDFYMD                 6     13  0
     D  LDTYMD                14     21  0
     D  LDPPCD                22     26
     D  LDFSCD                27     31  0
     D  LDSDFL                32     32
     D  ldgtcd                47     47
     D  LDPTCD                48     52
     D  LDHGSN                53     59  0
     D  LDFMDY                60     65  0
     D  LDTMDY                66     71  0
      *
     D  LDBODS                72    101
     D  LDPTDS               102    111
     D  LDPPDS               112    141
     D  LDFSNM               142    166
     D  LDHGCD               167    173
     D  LDRPFL               174    174
     D  LDSDDS               175    181
     D  ldgtds               182    196
      *
     D  LDOUTU               401    410
     D  LDHOLD               411    411
     D  LDCOPY               412    412
     D  LDOUTQ               413    422
      *
      /EJECT
      ****************************************************************
      * MAIN LINE
      ****************************************************************
      *
      * READ AND PRINT ALL DEAD HOGS SELECTED BY OPNQRY
      *
     C     *IN90         DOUEQ     *ON                                          MAIN DO
     C                   READ      HSJ068A                                90
     C     *IN90         IFEQ      *OFF                                         IF NOT EOF
      *
     C     FIRST         IFEQ      YES                                          IF FIRST
     C                   EXSR      $PTDS
     C                   EXSR      $PPDS
     C                   EXSR      $HRHDR
     C                   MOVEL     HGPTCD        SVPTCD
     C                   MOVEL     HGPPCD        SVPPCD
     C                   MOVEL     HGHGCD        SVHGCD
     C                   Z-ADD     KDFSCD        SVFSCD
     C                   Z-ADD     KDHGSN        SVHGSN
     C                   MOVEL     NO            FIRST
     C                   ENDIF                                                  IF FIRST
      *
      *------------------------------------------------------------
      * CONTROL BREAK LOGIC
      *------------------------------------------------------------
      * NOTE:  PRINT THE GROUP TOTAL LINE ONLY FOR THE 'DETAIL'
      *        VERSION OF THE REPORT
      *
     C                   SELECT
     C     HGPTCD        WHENNE    SVPTCD                                       PROD TYPE
      *
     C     LDSDFL        IFEQ      'D'
     C                   EXSR      $HGTOT
     C                   ENDIF
     C                   EXSR      $FSTOT
     C                   EXSR      $PPTOT
     C                   EXSR      $PTTOT
     C                   EXSR      $PTDS
     C                   EXSR      $PPDS
     C                   EXSR      $HRHDR
      *
     C     HGPPCD        WHENNE    SVPPCD                                       PROD PHASE
     C     LDSDFL        IFEQ      'D'
     C                   EXSR      $HGTOT
     C                   ENDIF
     C                   EXSR      $FSTOT
     C                   EXSR      $PPTOT
     C                   EXSR      $PPDS
     C                   EXSR      $HRHDR
      *
     C     KDFSCD        WHENNE    SVFSCD                                       FARM
     C     LDSDFL        IFEQ      'D'
     C                   EXSR      $HGTOT
     C                   ENDIF
     C                   EXSR      $FSTOT
     C                   EXCEPT    BLANK
      *
     C     KDHGSN        WHENNE    SVHGSN                                       FARM
     C     LDSDFL        IFEQ      'D'
     C                   EXSR      $HGTOT
     C                   EXCEPT    BLANK
     C                   ENDIF
     C                   ENDSL
      *
      *------------------------------------------------------------
      * DETAIL PROCESSING FOR BOTH 'DETAIL' AND 'SUMMARY' VERSIONS
      *------------------------------------------------------------
      *
      * ACCUMULATE TOTALS
     C                   ADD       KDKDLB        LBS
     C                   ADD       KDKDHD        HD
      *
      *------------------------------------------------------------
      * PROCESSING FOR THE 'DETAIL' VERSION OF THE REPORT ONLY
      *------------------------------------------------------------
      *      FLIP DEAD DATE TO MDY
      *      RETRIEVE DEATH REASON DESCRIPTION
      *      PRINT GROUP DETAIL LINE ON THE REPORT
      *
     C     LDSDFL        IFEQ      'D'                                          IF DETAIL
     C                   Z-ADD     KDKDDT        WKCYMD
     C                   EXSR      $TOMDY
     C                   Z-ADD     WKMDY         DTKDDT
      *
      * RETRIEVE DEATH REASON DESCRIPTION
      *
     C     KDDRCD        CHAIN     HSP041                             91
     C     *IN91         IFEQ      *OFF
     C                   MOVEL     DRDRDS        DTDRDS
     C                   ELSE
     C                   MOVEL(P)  UNKNWN        DTDRDS
     C                   ENDIF
      *
     C     *INOF         IFEQ      *ON
     C                   EXSR      $HRHDR
     C                   ENDIF
      *
      * PRINT DETAIL AND SETOFF INDICATOR THAT CONTROLS PRINTING OF
      * FARM SITE CODE
     C                   EXCEPT    DTLINE
     C                   SETOFF                                       8889
     C                   ENDIF                                                  IF DETAIL
      *
     C                   ENDIF                                                  IF NOT EOF
     C                   ENDDO                                                  MAIN DO
      *
      *----------------------------------------------------------------
      * EOF PROCESSING
      *----------------------------------------------------------------
      *  IF NO DETAIL RECORDS MATCHED THE SELECTION CRITERIA, THEN
      *  PRINT THE STANDARD 'NO DATA...' ERROR MESSAGE
      *
     C     FIRST         IFEQ      YES                                          IF NO RECS
     C                   EXCEPT    HRHDR
     C                   EXCEPT    NODATA
     C                   ELSE
      *
     C     LDSDFL        IFEQ      'D'                                          IF DETAIL
     C                   EXSR      $HGTOT
     C                   ENDIF                                                  IF DETAIL
      *
     C                   EXSR      $FSTOT
     C                   EXSR      $PPTOT
     C                   EXSR      $PTTOT
     C                   EXCEPT    TRTOT
     C                   ENDIF                                                  IF NO RECS
      *
     C                   SETON                                        LR
      /EJECT
      *---------------------------------------------------------------
      * $TOMDY - FLIP DATE FROM YYMMDD TO MMDDYY
      *---------------------------------------------------------------
      *
     C     $TOMDY        BEGSR
      *
     C                   Z-ADD     WKMON         WKMM
     C                   Z-ADD     WKDAY         WKDD
     C                   Z-ADD     WKYER         WKYY
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * $HRHDR - PRINT REPORT HEADINGS
      *---------------------------------------------------------------
      *
     C     $HRHDR        BEGSR
      *
     C                   EXCEPT    HRHDR
     C                   EXCEPT    HTHDR
     C                   SETON                                        8889
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      *  $PTDS - RETRIEVE THE PRODUCTION TYPE DESCRIPTION
      *----------------------------------------------------------------
      *
     C     $PTDS         BEGSR
      *
     C     HGPTCD        CHAIN     HSP010                             91
     C     *IN91         IFEQ      *OFF                                         IF FOUND
     C                   MOVEL     PTPTDS        HTPTDS
     C                   ELSE
     C                   MOVEL(P)  UNKNWN        HTPTDS
     C                   ENDIF                                                  IF FOUND
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $PPDS - RETRIEVE THE PRODUCTION PHASE DESCRIPTION
      *----------------------------------------------------------------
      *
     C     $PPDS         BEGSR
      *
     C     HGPPCD        CHAIN     HSP011                             91
     C     *IN91         IFEQ      *OFF                                         IF FOUND
     C                   MOVEL     PPPPDS        HTPPDS
     C                   ELSE
     C                   MOVEL(P)  UNKNWN        HTPPDS
     C                   ENDIF                                                  IF FOUND
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      *  $PTTOT - PRODUCTION TYPE TOTALS
      *----------------------------------------------------------------
      *
     C     $PTTOT        BEGSR
      *
     C     *INOF         IFEQ      *ON
     C                   EXSR      $HRHDR
     C                   ENDIF
      *
     C                   EXCEPT    TTTOT
     C                   MOVEL     HGPTCD        SVPTCD
      *
     C                   ENDSR
      *
      *----------------------------------------------------------------
      * $PPTOT - PRODUCTION PHASE TOTALS
      *----------------------------------------------------------------
      *
     C     $PPTOT        BEGSR
      *
     C     *INOF         IFEQ      *ON
     C                   EXSR      $HRHDR
     C                   ENDIF
      *
     C                   EXCEPT    TPTOT
     C                   MOVEL     HGPPCD        SVPPCD
      *
     C                   ENDSR
      *
      *----------------------------------------------------------------
      * $FSTOT - FARM SITE TOTALS
      *----------------------------------------------------------------
      *
     C     $FSTOT        BEGSR
      *
     C     *INOF         IFEQ      *ON
     C                   EXSR      $HRHDR
     C                   ENDIF
      *
      * IF THE USER IS PRINTING THE 'SUMMARY' VERSION, THEN RETRIEVE
      * THE FARM SITE NAME FOR PRINTING ON THE REPORT.
      *
     C     LDSDFL        IFEQ      'S'
     C     SVFSCD        CHAIN     HSP018                             92
     C     *IN92         IFEQ      *OFF
     C                   MOVEL(P)  FSFSNM        WKFSNM
     C                   ELSE
     C                   MOVEL(P)  UNKNWN        WKFSNM
     C                   ENDIF
     C                   ENDIF
      *
     C                   EXCEPT    TFTOT
      *
     C                   Z-ADD     KDFSCD        SVFSCD
     C                   SETON                                        89
      *
     C                   ENDSR
      *
      *----------------------------------------------------------------
      * $HGTOT - HOG GROUP TOTALS
      *----------------------------------------------------------------
      *
     C     $HGTOT        BEGSR
      *
     C                   EXCEPT    UNDER
      *
     C     *INOF         IFEQ      *ON
     C                   EXSR      $HRHDR
     C                   ENDIF
      *
     C                   EXCEPT    TGTOT
      *
     C                   MOVEL     KDHGSN        SVHGSN
     C                   MOVEL     HGHGCD        SVHGCD
     C                   SETON                                        88
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * *INZSR - INITIALIZATION SUBROUTINE
      *---------------------------------------------------------------
      *
     C     *INZSR        BEGSR
      *
      * DEFINE PRINT FIELDS FOR PRODUCTION TYPE HEADING
      *
     C     *LIKE         DEFINE    PTPTDS        HTPTDS
     C     *LIKE         DEFINE    PPPPDS        HTPPDS
      *
      * DEFINE PRINT FIELDS FOR DETAIL LINE
      *
     C                   Z-ADD     0             DTKDDT            6 0
     C     *LIKE         DEFINE    DRDRDS        DTDRDS
      *
      * DEFINE PRINT FIELD FOR FARM NAME ON SUMMARY VERSION
      *
     C     *LIKE         DEFINE    FSFSNM        WKFSNM
      *
      * DEFINE SAVE FIELDS FOR CONTROL BREAKS
      *
     C     *LIKE         DEFINE    HGPTCD        SVPTCD
     C     *LIKE         DEFINE    HGPPCD        SVPPCD
     C     *LIKE         DEFINE    KDFSCD        SVFSCD
     C     *LIKE         DEFINE    KDHGSN        SVHGSN
     C     *LIKE         DEFINE    HGHGCD        SVHGCD
      *
      *----------------------------------------------------------------
      * SET UP REPORT HEADINGS
      *----------------------------------------------------------------
      *
      * BUSINESS OFFICE
      *
     C     LDFSBO        IFEQ      *BLANK
     C                   MOVEL     ALL           LDFSBO
     C                   ENDIF
      *
      * PRODUCTION TYPE
      *
     C     LDPTCD        IFEQ      *BLANK
     C                   MOVEL     ALL           LDPTCD
     C                   ENDIF
      *
      * ALL PRODUCTION PHASES
      *
     C     LDPPCD        IFEQ      *BLANK
     C                   MOVEL     ALL           LDPPCD
     C                   ENDIF
      *
      *  ALL FARM SITES
      *
     C     LDFSCD        IFEQ      0
     C                   SETON                                        81
     C                   ENDIF
      *
      *  SUMMARY OR DETAIL REPORT
      *
     C     LDSDFL        IFEQ      'S'
     C                   SETON                                        87
     C                   ENDIF
      *
      * DETERMINE IF USER SELECTED SPECIFIC HOG GROUP OR "ALL"
      *
     C     LDHGCD        IFEQ      *BLANK                                       IF ALL GROUP
     C                   MOVEL     ALL           LDHGCD
     C                   ENDIF                                                  IF ALL GROUP
      *
      * Group type
      *
     C                   if        ldgtcd = *blank
     C                   seton                                        82
     C                   endif
      *
      * SET UP HEADING UNDERLINE FIELD FOR PRINT
      * RETRIEVE TIME FOR REPORT HEADING
      * SET UP A FIRST TIME FLAG
      *
     C                   MOVEL     *ALL'-'       DASH            132
     C                   TIME                    RTIME             6 0
     C                   MOVEL     YES           FIRST             1
      *
     C                   ENDSR
      /EJECT
      *-------------------------------------------------------------
      * REPORT HEADING LINES
      *-------------------------------------------------------------
      *
     OQPRINT    E            HRHDR          1 01
     O                       SDPGM               10
     O                                           68 'HOG PRODUCTION SYSTEM'
     O                                          122 'DATE'
     O                       UDATE         Y    132
      *
     O          E            HRHDR          1
     O                       SDUSR               10
     O                                           50 'DAILY'
     O                                           71 'DEATH REPORT BY FARM'
     O                                          122 'TIME'
     O                       RTIME              132 '  :  :  '
      *
     O          E            HRHDR          1
     O                       LDSDDS              61
     O                                          122 'PAGE'
     O                       PAGE          Z    132
      *
     O          E            HRHDR       1  1
     O                                           19 'BUSINESS OFFICE ...'
     O                       LDFSBO              26
     O                       LDBODS              61
      *
     O          E            HRHDR          1
     O                                           19 'FROM DEATH DATE ...'
     O                       LDFMDY        Y     29
      *
     O          E            HRHDR          1
     O                                           19 'TO DEATH DATE .....'
     O                       LDTMDY        Y     29
      *
     O          E            HRHDR          1
     O                                           19 'PRODUCTION TYPE ...'
     O                       LDPTCD              26
     O                       LDPTDS              41
      *
     O          E            HRHDR          1
     O                                           19 'PRODUCTION PHASE ..'
     O                       LDPPCD              26
     O                       LDPPDS              61
      *
      *
     O          e            hrhdr          1
     O                                           19 'GROUP TYPE ........'
     O              n82      ldgtcd              22
     O               82                          24 'ALL'
     O                       ldgtds              46
      *
      *
     O          E            HRHDR          1
     O                                           19 'FARM SITE .........'
     O              N81      LDFSCD        M     27
     O               81                          24 'ALL'
     O                       LDFSNM              56
      *
     O          E            HRHDR          2
     O                                           19 'HOG GROUP .........'
     O                       LDHGCD              28
      *
      *-------------------------------------------------------------
      * COLUMN HEADING LINES
      *-------------------------------------------------------------
      * NOTE: INDICATOR 87 IS ON WHEN THE USER HAS SELECTED THE
      *       'SUMMARY' VERSION OF THE REPORT. SOME COLUMN HEADINGS
      *       ARE PRINTED ON THE 'DETAIL' VERSION BUT OMITTED FROM
      *       THE 'SUMMARY' VERSION.
      *
     O          E            HRHDR          1
     O                                            5 'FARM'
     O              N87                          39 'DEATH'
     O                                           59 'DEAD'
     O                                           75 'DEAD'
     O              N87                         112 '------- DEATH REASON'
     O              N87                        +  1 ' -------'
      *
     O          E            HRHDR          1
     O                                            5 'SITE'
     O              N87                          13 'GROUP'
     O              N87                          22 'BLDG'
     O              N87                          29 'ROOM'
     O              N87                          38 'DATE'
     O                                           59 'HEAD'
     O                                           75 'POUNDS'
     O              N87                          89 'DESTROYED'
     O              N87                          96 'CODE'
     O              N87                         110 'DESCRIPTION'
      *
     O          E            HRHDR          1
     O                       DASH               132
      *
      *-------------------------------------------------------------
      * PRODUCTION TYPE / PRODUCTION PHASE HEADING
      *-------------------------------------------------------------
      *
     O          E            HTHDR       1  2
     O                       HTPTDS              15
     O                                           17 '-'
     O                       HTPPDS              48
      *
      *-------------------------------------------------------------
      * DEAD HOG DETAIL LINE   (ONLY PRINTED ON DETAIL VERSION)
      *-------------------------------------------------------------
      *
     O          E            DTLINE         1
     O               89      KDFSCD        MB     6
     O               88      HGHGCD         B    15
     O               88      HGBLCD         B    23
     O               88      HGRMCD         B    30
     O                       DTKDDT        YB    40
     O                       KDKDHD        KB    60
     O                       KDKDLB        KB    76
     O                       KDDSFL         B    85
     O                       KDDRCD         B    97
     O                       DTDRDS         B   119
      *
      *-------------------------------------------------------------
      * HOG GROUP TOTAL LINE
      *-------------------------------------------------------------
      *
     O          E            TGTOT          1
     O                                           21 '*'
     O                       SVHGCD              29
     O                       HD(1)         KB    60
     O                       LBS(1)        KB    76
     O                                           77 '*'
      *
      *-------------------------------------------------------------
      * FARM SITE TOTAL LINE
      *-------------------------------------------------------------
      *
      * NOTE: INDICATOR 87 IS ON WHEN THE USER HAS SELECTED THE
      *       'SUMMARY' VERSION OF THE REPORT. WHEN PRINTING THE
      *       'SUMMARY' VERSION, NO GROUP DETAIL IS PRINTED AND
      *       THIS FARM TOTAL LINE BECOMES, ACTUALLY, THE DETAIL
      *       LINE ON THE REPORT.
      *
     O          E            TFTOT          1
     O               87      SVFSCD        M      6
     O               87      WKFSNM         B    50
     O              N87                          36 '**'
     O              N87      SVFSCD        M     43
     O                       HD(2)         KB    60
     O                       LBS(2)        KB    76
     O              N87                          78 '**'
      *
      *-------------------------------------------------------------
      * PRODUCTION PHASE TOTAL LINE
      *-------------------------------------------------------------
      *
      * NOTE: INDICATOR 87 IS ON WHEN THE USER HAS SELECTED THE
      *       'SUMMARY' VERSION OF THE REPORT. WHEN PRINTING THE
      *       'SUMMARY' VERSION, AN EXTRA BLANK LINE IS PRINTED
      *       BEFORE THE PRODUCTION PHASE TOTAL LINE.
      *
     O          E    87      TPTOT          1
      *
     O          E            TPTOT          1
     O                                           36 '***'
     O                       SVPPCD              42
     O                       HD(3)         KB    60
     O                       LBS(3)        KB    76
     O                                           79 '***'
      *
      *-------------------------------------------------------------
      * PRODUCTION TYPE TOTAL LINE
      *-------------------------------------------------------------
      *
     O          E            TTTOT          1
     O                                           36 '****'
     O                       SVPTCD              42
     O                       HD(4)         KB    60
     O                       LBS(4)        KB    76
     O                                           80 '****'
      *
      *-------------------------------------------------------------
      * REPORT TOTALS
      *-------------------------------------------------------------
      *
     O          E            TRTOT          1
     O                                           44 'REPORT TOTAL'
     O                       HD(5)         KB    60
     O                       LBS(5)        KB    76
      *
      *-------------------------------------------------------------
      * PRINT A BLANK LINE
      *-------------------------------------------------------------
      *
     O          E            BLANK          1
      *
      *-------------------------------------------------------------
      * PRINT UNDERSCORE
      *-------------------------------------------------------------
      *
     O          E            UNDER       0  1
     O                                           59 '--------------'
     O                                           75 '--------------'
      *
      *-------------------------------------------------------------
      * NO DATA MESSAGE LINE
      *-------------------------------------------------------------
      *
     O          E            NODATA         1
     O                                           19 'NO DATA MEETS YOUR'
     O                                           38 'SELECTION CRITERIA'
