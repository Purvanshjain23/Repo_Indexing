     A*%%TS  SD  20010917  132540  ISLRAMS     REL-V4R4M0  5769-PW1
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD444
     A*  TITLE:         SPECIFY DEATHS AND CULL SALES BY WEEKS IN PHASE
     A*                 REPORT OPTIONS
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  DATE:          10/11/96
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A N99                                  CF04(04 'PROMPT')
     A N99 44                               CF08(08 'SUBMIT')
     A                                      CA12(12 'PREVIOUS')
     A N99                                  CF17(17 'PRINT OPTIONS')
     A N99                                  CF20(20 'WRKSPLF')
     A*
     A*---------------------------------------------------------------
     A* RECORD FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP444R1
     A*%%TS  SD  20010917  132540  ISLRAMS     REL-V4R4M0  5769-PW1
     A                                      SETOF(31)
     A                                      SETOF(32)
     A                                      SETOF(33)
     A                                      SETOF(34)
     A                                      SETOF(35)
     A                                      SETOF(36)
     A                                      SETOF(37)
     A                                      SETOF(38)
     A                                      SETOF(41)
     A                                      SETOF(42)
     A                                      SETOF(47)
     A                                      SETOF(51)
     A                                      SETOF(52)
     A                                      SETOF(54)
     A                                      SETOF(55)
     A                                      SETOF(56)
     A                                      SETOF(57)
     A                                      SETOF(58)
     A                                      SETOF(67)
     A                                      SETOF(71)
     A                                      SETOF(72)
     A                                      SETOF(73)
     A                                      SETOF(74)
     A                                      OVERLAY
     A                                      RTNCSRLOC(&RCD &FLD)
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 27'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  2  6'SPECIFY DEATHS AND CULL SALES BY W-
     A                                      EEKS IN PHASE REPORT OPTIONS'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  4  2'Business office ....'
     A                                  4 23'?'
     A            DFFSBO    R        B  4 25REFFLD(@@BOCD)
     A N31                                  DSPATR(HI)
     A  31
     AO 51                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A            DFBODS    R        O  4 34REFFLD(BOBODS)
     A                                  4 65'(Blank for all) '
     A                                  5  2'From date ..........'
     A            DFFMDY    R     Y  B  5 25REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A N32                                  DSPATR(HI)
     A  32                                  DSPATR(PC)
     A  32                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A                                  6  2'To date ............'
     A            DFTMDY    R     Y  B  6 25REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A N33                                  DSPATR(HI)
     A  33                                  DSPATR(PC)
     A  33                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A                                  7  2'Production type ....'
     A                                  7 23'?'
     A            DFPTCD    R        B  7 25REFFLD(@@PTCD)
     A N34                                  DSPATR(HI)
     A  34
     AO 54                                  DSPATR(PC)
     A  34                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A            DFPTDS    R        O  7 34REFFLD(PTPTDS)
     A                                  7 65'(Blank for all) '
     A                                  8  2'Production phase ...'
     A                                  8 23'?'
     A            DFPPCD    R        B  8 25REFFLD(@@PPCD)
     A N35                                  DSPATR(HI)
     A  35
     AO 55                                  DSPATR(PC)
     A  35                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A            DFPPDS    R        O  8 34REFFLD(PPPPDS)
     A                                  8 65'(Blank for all) '
     A                                  9  2'Breed stock ........'
     A                                  9 23'?'
     A            DFBSCD    R        B  9 25REFFLD(@@BSCD)
     A N42                                  DSPATR(HI)
     A  42
     AO 52                                  DSPATR(PC)
     A  42                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A            DFBSDS    R        O  9 34REFFLD(BSBSDS)
     A                                  9 65'(Blank for all)'
     A                                 10  2'Group type .........'
     A                                 10 23'?'
     A            DFGTCD    R        B 10 25REFFLD(HSRREF/@@GTCD *LIBL/HSPREF)
     A N38                                  DSPATR(HI)
     A  38
     AO 58                                  DSPATR(PC)
     A  38                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A            DFGTDS    R        O 10 34REFFLD(HSRREF/GTGTDS *LIBL/HSPREF)
     A                                 10 65'(Blank for all)'
     A                                 11  2'Cell ...............'
     A            DFCELL    R     Y  B 11 25REFFLD(@@CELL)
     A                                      EDTCDE(M)
     A N41                                  DSPATR(HI)
     A  41                                  DSPATR(PC)
     A  41                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A                                 11 65'(Blank for all) '
     A                                 12  2'Farm site ..........'
     A                                 12 23'?'
     A            DFFSCD    R        B 12 25REFFLD(@@FSCD)
     A                                      EDTCDE(M)
     A N36                                  DSPATR(HI)
     A  36
     AO 56                                  DSPATR(PC)
     A  36                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A            DFFSNM    R        O 12 34REFFLD(FSFSNM)
     A                                 12 65'(Blank for all) '
     A                                 13  2'Hog group ..........'
     A                                 13 23'?'
     A            DFHGCD    R        B 13 25REFFLD(@@HGCD)
     A N37                                  DSPATR(HI)
     A  37                                  DSPATR(RI)
     A  37
     AO 57                                  DSPATR(PC)
     A  99                                  DSPATR(PR)
     A                                 13 65'(Blank for all) '
     A                                 14  2'Print percents .....'
     A                                 14 23'?'
     A            DFPPFL    R        B 14 25REFFLD(@@FLAG)
     A N47                                  DSPATR(HI)
     A  47
     AO 67                                  DSPATR(PC)
     A  47                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A                                 16  2'PLEASE X BELOW THE CATEGORIES OF D-
     A                                      ATA THAT YOU WISH TO INCLUDE ON THE-
     A                                       REPORT:'
     A                                      DSPATR(HI)
     A            DFTDFL    R        B 17  3REFFLD(@@FLAG)
     A N71                                  DSPATR(HI)
     A  71                                  DSPATR(PC)
     A  71                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A                                 17  5'DOAs from transfers'
     A            DFSDFL    R        B 17 28REFFLD(@@FLAG)
     A N72                                  DSPATR(HI)
     A  72                                  DSPATR(PC)
     A  72                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A                                 17 30'DOAs from sales'
     A            DFDDFL    R        B 17 49REFFLD(@@FLAG)
     A N73                                  DSPATR(HI)
     A  73                                  DSPATR(PC)
     A  73                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A                                 17 51'Daily deads'
     A            DFCSFL    R        B 17 67REFFLD(@@FLAG)
     A N74                                  DSPATR(HI)
     A  74                                  DSPATR(PC)
     A  74                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A                                 17 69'Cull sales'
     A*
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R HP444K1
     A*%%TS  SD  19950531  161912  ISLRAMS     REL-V3R0M5  5763-PW1
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A N99                             23 11'F4=Prompt'
     A  44N99                          23 22'F8=Submit'
     A N99                             23 33'F12-Previous'
     A N99                             23 47'F17=Print Options'
     A N99                             23 66'F20=Spool Files'
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *---------------------------------------------------------------
     A          R HP444ES                   SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
     A*---------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*---------------------------------------------------------------
     A*
     A          R HP444EC                   SFLCTL(HP444ES)
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
