      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      HPS Data Mart
      * PROGRAM:     HP205 - Update BGF Weekly Production with Milk Supplemate
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     03/11/02
      *
      * FUNCTION:    This program updates the BGF Datamart Weekly Production records with
      *              milk supplemate data that originally comes from the MP2 System.
      *
      *              Only BGF farms are included.
      *
      *              For a History run, all records will be processed.
      *              For a Current run, only 'current' records are processed.
      *
      *************************************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 12/26/02  LeAnne Fedor
      *           Added logic to perform either  1) history or 2) current processing.
      *
      * 06/30/04  LeAnne Fedor
      *           Recompile only.
      *
      * 02/01/05  LeAnne Fedor
      *           Recompile only. New fields (Sire Line Company and Prime Line) added
      *           to Datamart BGF: Weekly Production file.
      *
      * 02/07/05  LeAnne Fedor
      *           Recompile only due to file changes in Datamart BGF Weekly Production file:
      *                    Added: Transferred In Open Gilts
      *                  Removed: Total Lactation Pounds
      *
      * 07/05/05  LeAnne Fedor
      *           Recompile only due to file changes in Datamart BGF Weekly Production file:
      *           Added 1 new field: Average Daily Beginning Piglet Inventory
      *
      * 08/23/05  LeAnne Fedor
      *           Recompile only. New field added to Datamart BGF Weekly Production file:
      *                Field: Man hours
      *
      * 03/23/06  LeAnne Fedor
      *           Recompile only. New fields added to Datamart BGF Weekly Production file.
      *
      * 04/24/06  LeAnne Fedor
      *           Recompile only. New fields added to Datamart BGF Weekly Production file.
      *                 Cognos Report Grouping
      *                 Weaning Cognos Report Grouping
      *                 Report PCP Cognos Report Grouping
      *                 Report Farrowed Cognos Report Grouping
      *
      * 06/15/06  LeAnne Ramsey
      *           Recompile only. New field added to Datamart BGF Weekly Production file:
      *                Field: Total Gilt Weight @ 28 Weeks
      *
      * 08/04/08  LeAnne Ramsey
      *           Recompile only. Fields deleted from Weekly Production file.
      *                           BPPSGLSV - Nbr Gilts Bred w/Passed Date
      *                           BPWSLB   - Weekly Ending Lbs Supplement Used
      *                           BPP2LT   - Total P2 Litters Farrowed
      *                           BPTDGB   - Total Days Gilt Breeding Age
      *                           BPIOGLHD - Transferred in Open Gilts
      *                           BPOOGLHD - Gilts Transferred Out Open
      *                           BP17WNLT - 17+ Day Weaned Sows
      *                           BPTGLLB  - Total Gilt Weight @ 28 Weeks
      *
      * 11/07/08  LeAnne Ramsey
      *           Recompile only:
      *           Added 3 new fields to HPPB092-Datamart BGF: Weekly Production
      *            1) Target open gilt pool eligible
      *            2) Target litters farrowed/mated female/year
      *            3) Target pigs weaned/mated female/year
      *
      * 05/28/09  LeAnne Ramsey
      *           Recompile only. Added new field "Farrowing Crates" to:
      *                HPPB092-Datamart BGF: Weekly Production
      *
      * 09/01/09  LeAnne Ramsey
      *           Recompile only. Added new field "Report Total Litters Farrowed" to:
      *                 HPPB092-Datamart BGF: Weekly Production
      *
      * 01/14/10  LeAnne Ramsey
      *           Recompile only. Changed text on BPLVHD and BPLVLB in:
      *                HPPB092-Datamart BGF: Weekly Production
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS:
      ****************************************************************
      *
     Fhppb092   uf   e           k disk
      *  Datamart BGF: Weekly production
      *
      *
     Fmpp200    if   e           k disk
      *  Datamart MP2: Milk consumption
      *
      /eject
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      *
      ****************************************************************
      * ARRAYS AND TABLES
      ****************************************************************
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      *
      * Workfields
      *
     D  wkfscd         s                   like(bpfscd)
     D  wklb           s                   like(bpdlwslb)
      *
      *
      * Workfields for dates
      *
     D wkbegiso        s                   like(mcisdt)
      *
      *
      * Parm fields
      *
     D xxccyymmdd      s              8  0
     D xxsatdt         s              8  0
     D xxcdyr          s              4  0
     D xxcdwk          s              2  0
      *
     D xxcurbegdt      s              8  0
     D xxhstbegdt      s              8  0
     D xxcurhist       s              1
      *
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * MAINLINE
      ****************************************************************
      *
      * Read each record in the Datamart MP2 Milk Consumption file.
      * Only process:
      *  1) the records that have a value in 'farm' field and
      *  2) for a History run, the issue date is on/after the 'history' beginning date
      *  3) for a Current run, the issue date is on/after the 'current' beginning date
      *
      *
     C                   dou       *in90 = *on                                  Main do
     C                   read      mpp200                                 90
     C                   if        *in90 = *off and                             If not EOF
     C                             mcfrm# > 0 and
     C                             mcisdt >= wkbegiso
      *
      * Retrieve the Saturday week-ending date associated with the MP2
      * 'issue' date.
      *
     C                   exsr      $saturday
      *
      * Retrieve the Datamart BGF Weekly Production record for this
      * farm/year/week and update the 'delivered supplement' pounds. (NOTE:
      * the 'quantity' from MP2 represents 'bags'...each bag is 50 pounds.)
      *
     C                   z-add     mcfrm#        wkfscd
     C     key01         chain     hppb092                            92
     C                   if        *in92 = *off
     C     50            mult      mcqty         wklb
     C                   add       wklb          bpdlwslb
     C                   update    bprec
     C                   endif
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Main do
      *
     C                   seton                                        lr
      /eject
      *-------------------------------------------------------------------------
      * Retrieve the Saturday week-ending date associated with the 'issue' date
      *-------------------------------------------------------------------------
      *
     C     $saturday     begsr
      *
      * Get the 'issue' date into an 8,0 field.
      *
     C     *iso          movel     mcisdt        xxccyymmdd
      *
      * Call a generic program to retrieve the date info.
      *
     C                   call      'HP8007'
     C                   parm                    xxccyymmdd
     C                   parm      0             xxsatdt
     C                   parm      0             xxcdyr
     C                   parm      0             xxcdwk
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
     C     *entry        plist
     C                   parm                    xxcurhist
     C                   parm                    xxhstbegdt
     C                   parm                    xxcurbegdt
      *
      * Key list
      *
     C     key01         klist
     C                   kfld                    xxcdyr
     C                   kfld                    xxcdwk
     C                   kfld                    wkfscd
      *
      * Get appropriate incoming date (history/current) into *iso format.
      *
     C                   select
     C                   when      xxcurhist = 'H'
     C     *iso          movel     xxhstbegdt    wkbegiso
      *
     C                   when      xxcurhist = ' '
     C     *iso          movel     xxcurbegdt    wkbegiso
     C                   endsl
      *
     C                   endsr
      /eject
