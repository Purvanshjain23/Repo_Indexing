     A*%%TS  SD  20080320  111503  ISLRAMS     REL-V5R4M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        TRIUMPH FOODS
     A*  DISPLAY FILE:  TFD447
     A*  TITLE:         SPECIFY MARGIN ADJUSTMENT REOPEN
     A*  PROGRAMMER:    LEANNE RAMSEY
     A*  CREATED:       03/13/08
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/TFPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A                                      CF08(08 'UPDATE')
     A                                      CF20(20 'WRKSPLF')
     A*
     A*---------------------------------------------------------------
     A* RECORD FORMAT ONE
     A*---------------------------------------------------------------
     A          R TF447R1
     A*%%TS  SD  20080320  111503  ISLRAMS     REL-V5R4M0  5722-WDS
     A                                      SETOF(30)
     A                                      OVERLAY
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 21'SPECIFY MARGIN ADJUSTMENT REOPEN'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USR)
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@JOB)
     A                                      DSPATR(HI)
     A                                  4 36'Year'
     A                                      DSPATR(UL)
     A                                      COLOR(BLU)
     A                                  4 42'Week'
     A                                      DSPATR(UL)
     A                                      COLOR(BLU)
     A                                  4 48'Period'
     A                                      DSPATR(UL)
     A                                      COLOR(BLU)
     A                                  5  2'Week-ending date ..'
     A            D1WEMDY   R        B  5 23REFFLD(TFREF/@@DT06 *LIBL/TFPREF)
     A                                      EDTWRD('  /  /  -')
     A  30                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A N30                                  DSPATR(HI)
     A            D1YR      R        O  5 36REFFLD(TFREF/@@YR *LIBL/TFPREF)
     A                                      EDTCDE(Z)
     A            D1WK      R        O  5 43REFFLD(TFREF/@@WK *LIBL/TFPREF)
     A                                      EDTCDE(Z)
     A            D1PE      R        O  5 50REFFLD(TFREF/@@PE *LIBL/TFPREF)
     A                                      EDTCDE(Z)
     A                                  8  2'Reopening a single week will cause-
     A                                       that week and ALL subsequent weeks-
     A                                       to be'
     A                                      COLOR(BLU)
     A                                  9  2'reclosed.  This will affect not on-
     A                                      ly TFS data but the data in two COG-
     A                                      NOS/'
     A                                      COLOR(BLU)
     A                                 10  2'Datamart Cubes (Sales and TFS Sale-
     A                                      s Production Margin).'
     A                                      COLOR(BLU)
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R TF447K1
     A*%%TS  SD  20080313  154610  ISLRAMS     REL-V5R4M0  5722-WDS
     A  43                             21  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A                                 23 12'F8=Interactive Update'
     A                                 23 63'F20=Spool Files'
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *---------------------------------------------------------------
     A          R TF447ES                   SFL SFLMSGRCD(24)
      *
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
     A*---------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*---------------------------------------------------------------
     A*
     A          R TF447EC                   SFLCTL(TF447ES)
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
