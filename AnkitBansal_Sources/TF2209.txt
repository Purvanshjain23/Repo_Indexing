      *****************  RPG PROGRAM HEADING  ************************
     h option(*SRCSTMT:*NODEBUGIO)
      ****************************************************************
      *
      * SYSTEM:      Triumph Foods
      * PROGRAM:     Datamart MARGIN: Update Data Area
      * PROGRAMMER:  LeAnne Ramsey
      * CREATED:     12/28/07
      *
      * AS the first step of the TFS Margin build, we will:
      *   1) retrieve the Data Area that controls the build
      *   2) retrieve the "last end date" from the MARGIN record in the TFS Function
      *      Control file; update the Data Area with this date
      *   3) compare the "last end date" from the Function Control record with
      *      the "last end date" for Datamart processing (from the Data Area).
      *   4) if the "last" Datamart date is less than the "last" TFP099 date,
      *          we will build data
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 03/19/08  LeAnne Ramsey
      *           Changed XXPROCFL to be XXBLDFL.
      /eject
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Ftfp099    if   e           k disk
      *    Function control
      *
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      *
      ****************************************************************
      * ARRAYS AND TABLES
      ****************************************************************
      *
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Parm fields
      *
     D xxbldfl         s              1
     D xxalphdt        s              8
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *---------------------------------------------------------------
      * Definition for external data area DATFMARG
      *---------------------------------------------------------------
     D
     Ddatfmarg        uds
     D  da099dt               30     37  0
     D  dadmdt                80     87  0
     D  dasbmfl              100    100
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ***************************************************************************************
      * MAINLINE
      ***************************************************************************************
      *
     C                   move      no            xxbldfl
      *
      * Check the 'submitted flag' in the Data Area. If the flag is Y=Yes,
      * then a build is already running. So, we do not want to process.
      *
     C                   if        dasbmfl = yes                                If submitted
     C                   else
      *
      * Retrieve the Function Control record to get the date of the "last"
      * Margin Close; update the Data Area with this date.
      *
     C     'MARGIN    '  chain     tfp099                             92
     C                   if        *in92 = *off and
     C                             fnledt > da099dt
     C                   z-add     fnledt        da099dt
     C                   endif
      *
      * If our "last" Datamart Date is less than the "last" Margin Close,
      * we want to build Datamart Data.
      *
     C                   if        dadmdt < da099dt
     C                   move      yes           xxbldfl
     C                   move      yes           dasbmfl
     C                   move      da099dt       xxalphdt
     C                   endif
      *
     C                   endif                                                  If submitted
      *
     C                   seton                                        lr
      /eject
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
     C     *entry        plist
     C                   parm                    xxbldfl
     C                   parm                    xxalphdt
      *
     C                   endsr
      /eject
