      *         *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      HOG PRODUCTION
      * PROGRAM:     HP393
      * TITLE:       REOPEN CLOSED GROUPS
      * PROGRAMMER:  LEANNE FEDOR
      * CREATED:     10/20/94
      *
      *  FUNCTION:   THERE ARE 3 STEPS IN REOPENING CLOSED GROUPS:
      *
      *             1) DELETE ALL RECORDS FOR THE GROUP FROM
      *                 HSP033-CLOSED GROUP FILE.
      *
      *              2) DELETE ALL RECORDS FOR THE GROUP FROM
      *                 HSP039-CLOSED GROUP EQUIPMENT FILE.
      *
      *              3) RESET THE FOLLOWING FIELDS FOR THE GROUP
      *                 IN HSP034-HOG GROUP.
      *                   HGGSCD-STATUS               SET TO DS
      *                   HGCLDT-CLOSE DATE           SET TO ZERO
      *                   HGFAFL-FEED ALLOCATED FLAG  SET TO BLANK
      *
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 11/01/99  LeAnne Fedor
      *           Changed the terminology 'unclose' to 'reopen' since this function
      *           will now be on a user's menu. Farm site is now a required selection.
      *           Production type and phase are no longer selections.
      *
      * 08/21/01  LeAnne Fedor
      *           With the changes that we are making to the closing groups and
      *           EOP functions, we will no longer allow individual groups to be reopened.
      *           Now, it is reopen all-or-nothing for the period that is currently open.
      *
      * 05/04/03  LeAnne Fedor
      *           Added an entry parm to control printing of the listing.
      *
      *           When the program is called from the Period Close function,
      *           the incoming value will be N=No and NO LISTING IS PRINTED.
      *
      *           When the program is called from the actual REOPEN function, the
      *           incoming value will be Y=Yes and a listing of reopened groups will be
      *           printed.
      *
      * 07/06/09  LeAnne Ramsey
      *           Recompile only. Added new field 'Continuous Flow Flag' to Hog Group file.
      *
      * 10/15/13  LeAnne Ramsey (E2831)
      *           Recompile only. Added field 'MTech Reference'.
      /eject
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
     H
     FHSP011    IF   E           K DISK
      *    PRODUCTION PHASE
      *
      *
     FHSP018    IF   E           K DISK
      *    FARM SITE
      *
      *
     FHSP033    UF   E           K DISK
      *    CLOSED HOG GROUP
      *
      *
     FHSL034I   IF   E           K DISK
      *    HOG GROUP  (groups selected in OPNQRY)
      *
      *
     FHSL034D   UF   E           K DISK    RENAME(HGREC:HGRECD)
      *    HOG GROUP
      *
      *
     FHSP039    UF   E           K DISK
      *    CLOSED GROUP EQUIPMENT
      *
      *
     FQPRINT    O    F  132        PRINTER OFLIND(*INOF)
      *
      /EJECT
      ****************************************************************
      * INPUT SPECS
      ****************************************************************
      *
      *---------------------------------------------------------------
      *  DATE FORMATS
      *---------------------------------------------------------------
      *
      * DATE IN MMDDYY FORMAT
      *
     D                 DS
     D  WKMDY                  1      6  0
     D  WKMM                   1      2  0
     D  WKDD                   3      4  0
     D  WKYY                   5      6  0
      *
      * DATE IN CCYYMMDD FORMAT
      *
     D                 DS
     D  WKCYMD                 1      8  0
     D  WKCEN                  1      2  0
     D  WKYER                  3      4  0
     D  WKMON                  5      6  0
     D  WKDAY                  7      8  0
      *
      *---------------------------------------------------------------
      *  LDA -  WITH THE REQUESTED PRINT SELECTIONS
      *---------------------------------------------------------------
      *
     D                UDS
     D  LDBPDT                 1     08  0
     D  LDEPDT                 9     16  0
     D  LDPER                 17     18  0
     D  LDEFFL                19     19
     D  LDCCYY                20     23  0
     D  LDBMDY                60     66  0
     D  LDEMDY                70     76  0
      *
      *
      *---------------------------------------------------------------
      * STANDARD PROGRAM STATUS DATA STRUCTURE
      *---------------------------------------------------------------
      *    EXTERNALLY DEFINED AS UTPGFR (RECORD FORMAT: PGMDSR)
     D PGMDS         ESDS                  EXTNAME(UTPGFR)
      *
      *---------------------------------------------------------------
      *  NAMED CONSTANTS
      *---------------------------------------------------------------
      *
     D YES             C                   CONST('Y')
     D NO              C                   CONST('N')
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * MAIN LINE
      ****************************************************************
      *
      * PROCESS EACH HOG GROUP RECORD SELECTED BY THE OPEN QUERY
      *
     C     *IN90         DOUEQ     *ON                                          MAIN DO
     C                   READ      HSL034I                                90
      *
      *
      * PRINT THE GROUP ON THE REPORT
      *
     C     *IN90         IFEQ      *OFF                                         IF NOT EOF
      *
     C                   IF        XXREOPEN = YES
     C                   EXSR      $PRINT
     C                   ELSE
     C                   MOVE      NO            FIRST
     C                   ENDIF
      *
      * FOR EACH CLOSED HOG GROUP, DELETE ALL CLOSED GROUP
      * RECORDS.
      *
     C     HGHGSN        SETLL     HSP033
     C                   SETOFF                                       91
      *
     C     *IN91         DOUEQ     *ON                                          DO CLOSED
     C     HGHGSN        READE     HSP033                                 91
     C     *IN91         IFEQ      *OFF                                         IF MORE REC
     C                   DELETE    CGREC
     C                   ENDIF                                                  IF MORE REC
     C                   ENDDO                                                  DO CLOSED
      *
      * FOR EACH CLOSED HOG GROUP, DELETE ALL CLOSED GROUP
      * EQUIPMENT RECORDS.
      *
     C     HGHGSN        SETLL     HSP039
     C                   SETOFF                                       92
      *
     C     *IN92         DOUEQ     *ON                                          DO EQUIP
     C     HGHGSN        READE     HSP039                                 92
     C     *IN92         IFEQ      *OFF                                         IF NOT EOF
     C                   DELETE    CEREC
     C                   ENDIF                                                  IF NOT EOF
     C                   ENDDO                                                  DO EQUIP
      *
      * FOR EACH CLOSED HOG GROUP, UPDATE THE HOG GROUP FILE
      *
     C     HGHGSN        CHAIN     HSL034D                            93
     C     *IN93         IFEQ      *OFF
     C                   MOVEL     'DS'          HGGSCD
     C                   MOVE      *BLANK        HGFAFL
     C                   Z-ADD     0             HGCLDT
     C                   UPDATE    HGRECD
     C                   ENDIF
      *
     C                   ENDIF                                                  IF NOT EOF
     C                   ENDDO                                                  MAIN DO
      *
      *
      *--------------------------------------------------------------
      * EOF PROCESSING
      *--------------------------------------------------------------
      *
      *  IF NO DETAIL RECORDS MATCHED THE SELECTION CRITERIA, THEN
      *  PRINT THE STANDARD 'NO DATA...' ERROR MESSAGE; OTHERWISE
      *  PRINT THE TOTALS.
      *
     C                   IF        FIRST = YES AND XXREOPEN = YES
     C                   EXCEPT    RPHDR
     C                   EXCEPT    NODATA
     C                   ENDIF
      *
     C                   SETON                                        LR
      *
      /EJECT
      ****************************************************************
      * PRINT A LINE FOR EACH REOPENED GROUP
      ****************************************************************
      *
     C     $PRINT        BEGSR
      *
      * FLIP DATES FROM CCYYMMDD TO MMDDYY
      *
     C                   Z-ADD     HGCRDT        WKCYMD
     C                   EXSR      $TOMDY
     C                   Z-ADD     WKMDY         DTCRDT
      *
     C                   Z-ADD     HGOPDT        WKCYMD
     C                   EXSR      $TOMDY
     C                   Z-ADD     WKMDY         DTOPDT
      *
     C                   Z-ADD     HGDSDT        WKCYMD
     C                   EXSR      $TOMDY
     C                   Z-ADD     WKMDY         DTDSDT
      *
     C                   Z-ADD     HGCLDT        WKCYMD
     C                   EXSR      $TOMDY
     C                   Z-ADD     WKMDY         DTCLDT
      *
      * FIRST TIME PROCESSING
      *
     C     FIRST         IFEQ      YES
     C                   EXSR      $FIRST
     C                   ENDIF
      *
      * BREAK ON PRODUCTION PHASE
      *
     C     HGPPCD        IFNE      SVPPCD                                       IF PP BREAK
     C                   EXSR      $FSBRK
     C                   EXSR      $PPBRK
     C                   ENDIF                                                  IF PP BREAK
      *
      * BREAK ON FARM SITE
      *
     C     HGFSCD        IFNE      SVFSCD                                       IF FS BREAK
     C                   EXSR      $FSBRK
     C     *INOF         IFEQ      *ON
     C                   EXSR      $HEAD
     C                   ELSE
     C                   EXCEPT    FSHDR
     C                   ENDIF
     C                   ENDIF                                                  IF FS BREAK
      *
      * FOR DETAIL PROCESSING, PRINT THE HOG GROUP LINE
      *
     C     *INOF         IFEQ      *ON
     C                   EXSR      $HEAD
     C                   ENDIF
     C                   EXCEPT    DTL
      *
     C                   ENDSR
      /EJECT
      ****************************************************************
      * $TOMDY - FLIP DATE FROM YYMMDD TO MMDDYY
      ****************************************************************
      *
     C     $TOMDY        BEGSR
      *
     C                   Z-ADD     WKMON         WKMM
     C                   Z-ADD     WKDAY         WKDD
     C                   Z-ADD     WKYER         WKYY
      *
     C                   ENDSR
      /EJECT
      *****************************************************************
      * $FSBRK - CONTROL BREAK ON FARM SITE
      *****************************************************************
      *
     C     $FSBRK        BEGSR
      *
     C                   Z-ADD     HGFSCD        SVFSCD
      *
      * IF YOU ARE NOT DOING YOUR EOF PROCESSING,
      *    RETRIEVE NEW FARM SITE DESCRIPTION
      *
     C     *IN90         IFEQ      *OFF                                         IF NOT EOF
     C                   EXSR      $FSNM
     C                   ENDIF                                                  IF NOT EOF
      *
     C                   ENDSR
      /EJECT
      *****************************************************************
      * $PPBRK - CONTROL BREAK IN PRODUCTION PHASE
      *****************************************************************
      *
     C     $PPBRK        BEGSR
      *
      * IF YOU ARE NOT DOING YOUR EOF PROCESSING,
      *    RETRIEVE NEW PRODUCTION PHASE DESCRIPTION
      *    PRINT HEADINGS
      *
     C     *IN90         IFEQ      *OFF                                         IF NOT EOF
     C                   EXSR      $PPDS
     C                   EXSR      $HEAD
     C                   MOVEL     HGPPCD        SVPPCD
     C                   ENDIF                                                  IF NOT EOF
      *
     C                   ENDSR
      /EJECT
      *****************************************************************
      * $HEAD - OVERFLOW SUBROUTINE
      *****************************************************************
      *
      * THIS SUBROUTINE PRINTS THE REPORT HEADING, PRODUCTION PHASE
      * HEADING AND FARM SITE HEADING.
      *
     C     $HEAD         BEGSR
      *
     C                   EXCEPT    RPHDR
     C                   EXCEPT    PPHDR
     C                   EXCEPT    FSHDR
      *
     C                   ENDSR
      /EJECT
      *****************************************************************
      * $PPDS - RETRIEVE PRODUCTION PHASE DESCRIPTION
      *****************************************************************
      *
     C     $PPDS         BEGSR
      *
     C     HGPPCD        CHAIN     HSP011                             94
     C     *IN94         IFEQ      *ON                                          IF NOT FOUND
     C                   MOVEL(P)  'UNKNOWN'     PPPPDS
     C                   ENDIF                                                  IF NOT FOUND
      *
     C                   ENDSR
      /EJECT
      ****************************************************************
      * $FSNM - RETRIEVE FARM SITE NAME
      ****************************************************************
      *
     C     $FSNM         BEGSR
      *
     C     HGFSCD        CHAIN     HSP018                             95
     C     *IN95         IFEQ      *ON
     C                   MOVEL(P)  'UNKNOWN'     FSFSNM
     C                   ENDIF
      *
     C                   ENDSR
      /EJECT
      *****************************************************************
      * $FIRST - FIRST TIME THROUGH
      *****************************************************************
      *
     C     $FIRST        BEGSR
      *
     C                   MOVEL     NO            FIRST
      *
      * SAVE THE CONTROL BREAK FIELDS
      *
     C                   MOVEL     HGPPCD        SVPPCD
     C                   Z-ADD     HGFSCD        SVFSCD
      *
      * RETRIEVE DESCRIPTIONS FOR HEADINGS
      *
     C                   EXSR      $PPDS
     C                   EXSR      $FSNM
      *
      * PRINT HEADINGS FOR FIRST TIME THROUGH
      *
     C                   EXSR      $HEAD
      *
     C                   ENDSR
      /EJECT
      ****************************************************************
      * *INZSR - INITIALIZATION SUBROUTINE
      ****************************************************************
      *
     C     *INZSR        BEGSR
      *
      * Parm lists
      * We added an entry parm to control printing of the listing.
      * When the program is called from the Period Close function, the incoming
      * value will be N=No and NO LISTING IS PRINTED.
      * When the program is called from the actual REOPEN function, the incoming value
      * will be Y=Yes and the listing of reopened groups will be printed.
      *
     C     *entry        plist
     C                   parm                    xxreopen          1
      *
      * DEFINE SAVE FIELDS FOR CONTROL BREAKS
      *
     C     *LIKE         DEFINE    HGPPCD        SVPPCD
     C     *LIKE         DEFINE    HGFSCD        SVFSCD
      *
      * DEFINE PRINT FIELDS FOR THE DETAIL LINE
      *
     C                   Z-ADD     0             DTDSDT            6 0
     C     *LIKE         DEFINE    DTDSDT        DTCRDT
     C     *LIKE         DEFINE    DTDSDT        DTOPDT
     C     *LIKE         DEFINE    DTDSDT        DTCLDT
      *
      *
      * RETRIEVE TIME FOR REPORT HEADING
      * SET UP A FIRST TIME FLAG
      * SET UP A DASHED LINE FOR UNDERLINING THE HEADING
      *
     C                   TIME                    RTIME             6 0
     C                   MOVEL     YES           FIRST             1
     C                   MOVEL     *ALL'-'       DASH            132
      *
     C                   ENDSR
      /EJECT
      *-------------------------------------------------------------
      * REPORT HEADING LINES
      *-------------------------------------------------------------
      *
     OQPRINT    E            RPHDR          1 01
     O                       SDPGM               10
     O                                           75 'HOG PRODUCTION SYSTEM'
     O                       UDATE         Y    132
     O          E            RPHDR          1
     O                       SDUSR               10
     O                                           67 'CLOSED GROUPS THAT'
     O                                         +  1 'WERE REOPENED'
     O                       RTIME              132 '  :  :  '
      *
     O          E            RPHDR          1
     O                                          126 'PAGE'
     O                       PAGE          Z    132
      *
     O          E            RPHDR          1
     O                                           22 'FROM CLOSED DATE .....'
     O                       LDBMDY        Y     32
      *
     O          E            RPHDR          1
     O                                           22 'TO CLOSED DATE .......'
     O                       LDEMDY        Y     32
      *
      *
     O          E            RPHDR          1
     O                                            5 'GROUP'
     O                                           17 'GROUP'
     O                                           26 'DATE'
     O                                           37 'DATE'
     O                                           48 'DATE'
     O                                           59 'DATE'
     O                                           85 'BREED'
     O                                           95 'GENETIC'
      *
     O          E            RPHDR          1
     O                                            4 'CODE'
     O                                           17 'NUMBER'
     O                                           28 'CREATED'
     O                                           38 'OPENED'
     O                                           50 'DISPOSED'
     O                                           60 'CLOSED'
     O                                           68 'BLDG'
     O                                           76 'ROOM'
     O                                           85 'STOCK'
     O                                           92 'LINE'
      *
     O          E            RPHDR          2
     O                       DASH               132
      *
      *-------------------------------------------------------------
      * PRODUCTION PHASE HEADING
      *-------------------------------------------------------------
      *
     O          E            PPHDR          2
     O                       PPPPDS              30
      *
      *-------------------------------------------------------------
      * FARM SITE HEADING
      *-------------------------------------------------------------
      *
     O          E            FSHDR          1
     O                                            5 'Farm:'
     O                       FSFSCD        Z     11
     O                       FSFSNM              38
      *
      *-------------------------------------------------------------
      * DETAIL LINE
      *-------------------------------------------------------------
      *
     O          E            DTL            1
     O                       HGHGCD               7
     O                       HGHGSN        Z   +  3
     O                       DTCRDT        Y   +  3
     O                       DTOPDT        Y   +  3
     O                       DTDSDT        Y   +  3
     O                       DTCLDT        Y   +  3
     O                       HGBLCD            +  3
     O                       HGRMCD            +  3
     O                       HGBSCD            +  3
     O                       HGGNCD            +  3
      *
      *-------------------------------------------------------------
      * NO DATA MESSAGE LINE
      *-------------------------------------------------------------
     O          E            NODATA         1
     O                                           19 'NO DATA MEETS YOUR'
     O                                           38 'SELECTION CRITERIA'
