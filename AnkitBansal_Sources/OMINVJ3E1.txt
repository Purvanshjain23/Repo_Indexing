     H*****************************************************************
     H*
     H*  Program number:  OMINVJ3E1 - Copied from OMINVJDE3 - MOD TO USE E1 FILES
     H*  Program name:    Read Workfile for OM Invoices & Print
     H*  Programmer:      Rose Centonze
     H*  Date created:    03/25/2020
     H*  Program purpose: This program will read the workfile created
     H*                   in OMINVJDE1 by account id to generate
     H*                   a report.
      *  CHANGES MADE -        06/29/96     BARB GUTIERREZ
      *                        RECOMPILED FOR JDE RELEASE A7.1 ONLY
      *                        04/20/99     BARB GUTIERREZ
      *                        ADDED MESSAGE TO PRINT IF NO RECORDS TO
      *                        PROCESS.
      *                        SCAN:  BG990420
     H*  Modified BY     -> Barb Gutierrez       DATE  -> 11/17/03
     H*                     Added the subledger to the report. Shrunk omitxt
     H*                     on report to 20.  It was 30.
     H*                     Also converted program to ILE.
     H*  Modified BY     -> Barb Gutierrez       DATE  -> 07/22/04
     H*                     Accumulate pounds and print on report.  Increased
     H*                     qprint from 132 to 198.  Renamed acttot to actamt.
     H*  Modified BY     -> Alice Brownfield     DATE  -> 11/08/05
     H*                     Changed to accomodate TF Posting requirements.
     H*                     print all companies in one pass instead of calling
     H*                     the program for each hard-coded company.
     H*  Modified BY     -> Barb Gutierrez       DATE  -> 12/21/05
     H*                     Changed to print invalid account message on the right
     H*                     of the report in addition to where it is currently printing
     H*                     to help the error stand out more.
     H*  Modified BY     -> Barb Gutierrez       DATE  -> 01/03/06
     H*                     Changed to print the account description on overflow.
     H*  Modified BY     -> Barb Gutierrez       DATE  -> 02/28/06
     H*                     Changed qprint to print198
     H*  Modified by:    -> Barb Gutierrez     04/24/08
     H*                     With the addition of High Plains BioEnergy, they would
     H*                     like to write net gallons to the units field in JDE.
     H*                     Renamed field omlbs in omp300 to omunit and changed
     H*                     heading name on report from LBS to UNITS.
     H*  Modified by:    -> Barb Gutierrez     09/18/08
     H*                     Added subledger type to the report.  We can now have different
     H*                     ones.  Project: E00193
     A*  MODIFIED BY: ROSE CENTONZE        01/20/17    Project  E8287
     A*               Changed LF OML300A to SEL LIVE WITH M3 GL =N,P FOR  No or Parallel
     A*  MODIFIED BY: ROSE CENTONZE        05/02/17    Project  E8287
     A*               Changed LF OML300A to a file with no sel - use oml300i
     H*  Modified by:     Rose Centonze    03/25/20   Scan:  P16169
     H*                   Copied from OMINVJDE3 and modified to use E1IDEVMDL tables and pgms
     H*                   F0901  -->  E10901
     H*               10/29/21 RMC change to new oml300k - added subledger to the key, and print
     H*                   subtotals at subledger break
     H*****************************************************************
     F*
     F*OML300I   IF   E           K DISK      10-28-21
     FOML300K   IF   E           K DISK
     F* OM Workfile            (Key: Co, Account ID, Order #)
     F*F0901LA   IF   E           K DISK
     FE10901LA  IF   E           K DISK
     F* JDE ACCOUNT MASTER     (Key: Account ID)
     Fprint198  O    F  198        PRINTER OFLIND(*INOF)
     F*
     D*
     D***  ACTUAL SHIP DATE FROM WORKFILE  YYMMDD
     D*
     D                 DS
     D  GLYMD                  1      7  0
     D  YYGL                   2      3  0
     D  MMGL                   4      5  0
     D  DDGL                   6      7  0
     D*
     D***  G/L DATE MMDDYY
     D*
     D                 DS
     D  GLMDY                  1      6  0
     D  GLMM                   1      2  0
     D  GLDD                   3      4  0
     D  GLYY                   5      6  0
     D*
     D***  INVALID ACCOUNT ERROR MESSAGE
     D*
     D                 DS
     D  MSG                    1     15
     D  ERR1                   1      7    INZ('INVALID')
     D  ERR2                   9     15    INZ('ACCOUNT')
     I*
     C*
     C*----------------------------------------------------------------
     C* MAIN PROCESSING LOOP
     C*----------------------------------------------------------------
     C*   Read Workfile for the all records in the file
     C*
     C     *loval        SETLL     OMP300R
     C     *IN50         DOUEQ     *ON
     C                   READ      OMP300R                                50
     C     *IN50         IFEQ      *OFF                                         HAVE RECORD
     C*  ---------
     C* FIRST TIME THROUGH SETUP BATCH NUMBER
     C* --------
     C     *IN91         IFEQ      *OFF                                          BEG ONE TIME
      *
     C                   MOVE      *ON           *IN91
     C*
     C*  get new company name
     C*
     c                   exsr      $newco
     C*
      * get new account description
     C*
     c                   exsr      $newacct
     C*
     C                   ENDIF                                                  END ONE TIME
     C* ________________
      * If the Account or the Company changes = print totals
     C*
     C                   IF        omaid <> SVGLID or
     C                             omco# <> svco#
     C*
     C* Print the account description on overflow
     C*
     C                   IF        *inof = *on
     C                   EXCEPT    HDG
     C                   EXCEPT    ACTDES
     C                   ENDIF
     C*
     c                   if        SVSBL <> *blank
     C                   EXCEPT    TOTSBL                                            10/28/21
     C                   END
     C                   EXCEPT    TOTACT
     C                   Z-ADD     0             ACTamt
     C                   Z-ADD     0             ACTunit
     C                   Z-ADD     0             SBLamt
     C*
     C*_____________
     C* if company changes get new company name
     C*
     C                   IF        omco# <> svco#
     c                   exsr      $newco
     C                   endif                                                  END ONE TIME
     C*
      * get new account description
     C*
     c                   exsr      $newacct
     C                   Endif                                                   DIFFERENT
     C*
      * If the Subledger changes      10-28-21
     C*
     C                   IF        omsbl <> SVsbl
     c                   if        SVSBL <> *blank
     C                   IF        *inof = *on                                   if not blank
     C                   EXCEPT    HDG
     C                   EXCEPT    ACTDES
     C                   ENDIF
     c                   except    TOTSBL
     c                   endif
     c                   z-add     0             sblamt
     c                   movel     omsbl         svsbl             8
     c                   endif
     C*
     C*  CONVERT ACTUAL SHIP DATE FROM YYMMDD TO MMDDYY
     C*
     C                   Z-ADD     OMASDT        GLYMD
     C                   Z-ADD     YYGL          GLYY
     C                   Z-ADD     MMGL          GLMM
     C                   Z-ADD     DDGL          GLDD
     C*
     C* ACCUMULATE AMOUNT and pounds INTO TOTAL FOR ACCOUNT
     C*
     C                   ADD       OMAMT         ACTamt           15 2
     C                   ADD       OMunit        ACTunit          15 2
     C                   MOVEL     OMTEXT        WKTXT            23
     c                   ADD       omamt         SBLamt           15 2
     C*
     C                   MOVEL     OMitxt        wkitxt           20
     C*
     C* Print the account description on overflow
     C*
     C                   IF        *inof = *on
     C                   EXCEPT    HDG
     C                   EXCEPT    ACTDES
     C                   endif
     C                   EXCEPT    DET
     C*
     C                   ENDIF                                                  50 = OFF
     C                   ENDDO                                                  50 DOUEQ ON
     C*
     C/EJECT
     C*----------------------------------------------------------------
     C* EOF PROCESSING:
     C*----------------------------------------------------------------
     C*
     C* BG990420:  IF NO REORDS TO PROCESS, PRINT MESSAGE
     C*
     C     *IN91         IFEQ      *OFF
     C                   EXCEPT    HDG
     C                   EXCEPT    NOREC
     C                   ELSE
     C*
     C* Print the account description on overflow
     C*
     C                   IF        *inof = *on
     C                   EXCEPT    HDG
     C                   EXCEPT    ACTDES
     C                   endif
     C                   EXCEPT    TOTACT
     C                   ENDIF
     C*
     C                   MOVE      *ON           *INLR
      *-----------------------------------------------------------------------------------
      * COMPANY BREAK - get company name
      *-----------------------------------------------------------------------------------
      *
     C     $newco        begsr
      *
     C                   MOVE      OMco#         SVco#             3 0
     C                   MOVE      *BLANKS       WKNAM            30
     C                   CALL      'PDGZXFR'
     C                   PARM                    RETRN             7
     C                   PARM      svco#         CO#               3 0
     C     WKNAM         PARM                    NAME             30
     C                   PARM                    ERROR             1
     C*
     C                   TIME                    RTIME             6 0
     C                   EXCEPT    HDG
      *
     C                   endsr
      *-----------------------------------------------------------------------------------
      * ACCOUNT # BREAK
      *-----------------------------------------------------------------------------------
      *
     C     $newacct      begsr
      *
     C                   MOVE      OMAID         SVGLID            8
     C                   MOVEl     OMSBL         SVSBL                             10-28-21
      *
     C**   SVGLID        CHAIN     I0901                              55           p16169
     C     SVGLID        CHAIN     I0901la                            55           p16169
     C     *IN55         IFEQ      *ON
     C                   MOVE      *BLANKS       WKADES
     C                   MOVEL     MSG           WKADES           30
     C                   MOVE      *on           *in95
     C                   ELSE
     C                   MOVEL     GMDL01        WKADES
     C                   MOVE      *off          *in95
     C                   ENDIF
     C*
     C   OF              EXCEPT    HDG
     C                   EXCEPT    ACTDES
     C                   endsr
     C/EJECT
     Oprint198  E            HDG            1 01
     O                                            9 'OMINVJ3E1'
     O                                           63 'ORDER MANAGEMENT SY'
     O                                           67 'STEM'
     O                       UDATE         Y    131
     O          E            HDG            1
     O                                           57 'INVOICE POST LIST'
     O                                           68 'BY ACCOUNT'
     O                       RTIME              131 '0 :  :  '
     O          E            HDG            1
     O                                          127 'PAGE'
     O                       PAGE          Z    131
      *
     O          E            HDG            2
     O                                           18 'COMPANY ... :'
     O                       svco#         Z     22
     O                       WKNAM               53
      *
     O          E            HDG            1
     O                                           19 'COST CENTER  OBJECT'
     O                                           28 'SUB CODE'
     O                                           90 'FIN/ACC'
      *
     O          E            HDG            2
     O                                           24 'ORDER # SHIP DATE'
     O                                           46 'DOCUMENT  ITEM    GRP'
     O                                           62 'CLS DESCRIPTION'
     O                                           80 'SUBLEDGER'
     O                                          103 'CODE   DESCRIPTION'
     O                                          126 'AMOUNT'
     O                                          156 'UNITS'
      *
     O          E            ACTDES         2
     O                       OMMCU               12
     O                       OMOBJ               19
     O                       OMSUB               28
     O                       WKADES              59
     O               95      msg                 73
     O               95                          80 '*****'
      *
     O          E            DET            1
     O                       OMORD#        Z     10
     O                                           11 '-'
     O                       OMSEQ#        Z     14
     O                       GLMDY         Y     23
     O                       OMDOC#        Z     33
     O                       OMSITM        Z     42
     O                       OMIGRP        Z     46
     O                       OMICLS        Z     50
     O                       wkitxt              71
     O                       omsbl               80
     O                       omsblty             82
     O                       omsblfl             84
     O                       OMFTYP              88
     O                       OMCODE              92
     O                       WKTXT              118
     O                       OMAMT         J    134
     O                       OMunit        J    158
      * 10/28/21
     O          E            TOTSBL      1  2
     O                       SBLamt        J    134
     O                                          136 '*'
      *
     O          E            TOTACT      1  2
     O                       ACTamt        J    134
     O                       ACTunit       J    158
     O                                          161 '**'
      *
     O          E            NOREC       1  2
     O                                           55 'No records to process'
