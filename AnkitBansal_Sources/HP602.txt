      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      HOG PRODUCTION
      * PROGRAM:     HP602
      * TITLE:       LISTING OF MANAGERS
      * PROGRAMMER:  MARY PALMER
      * CREATED:     3/30/94
      *
      * FUNCTION: BATCH PROGRAM TO LIST THE RECORDS IN THE
      *           MANAGER MASTER FILE
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 04/20/95  LEANNE FEDOR
      *           CHANGED TO USE PROGRAM DATA STRUCTURE FOR PROGRAM
      *           ID AND USER NAME IN REPORT HEADING.
      *
      * 10/15/13  LeAnne Ramsey (E2831)
      *           Recompile only. Added field 'MTech Reference'.
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     FHSP016    IF   E           K DISK
      *    Manager
      *
     FQPRINT    O    F  132        PRINTER OFLIND(*INOF)
      *
      /EJECT
      ****************************************************************
      * INPUT SPECS
      ****************************************************************
      *
      *---------------------------------------------------------------
      * STANDARD PROGRAM STATUS DATA STRUCTURE
      *---------------------------------------------------------------
      *    EXTERNALLY DEFINED AS UTPGFR (RECORD FORMAT: PGMDSR)
     D PGMDS         ESDS                  EXTNAME(UTPGFR)
      *
      *---------------------------------------------------------------
      *  NAMED CONSTANTS
      *---------------------------------------------------------------
      *
     D YES             C                   CONST('Y')
     D NO              C                   CONST('N')
      *
      /EJECT
      ****************************************************************
      * MAIN LINE
      ****************************************************************
      *
      * PREPARE TO READ THE MANAGER MASTER FILE AND
      * PRINT HEADINGS FOR FIRST TIME THROUGH
      *
     C     *LOVAL        SETLL     HSP016
     C                   EXCEPT    HRHDR
      *
      * READ AND PRINT ALL RECORDS IN THE MANAGER MASTER.
      *
     C     *IN92         DOUEQ     *ON                                          MAIN DO
     C                   READ      HSP016                                 92
     C     *IN92         IFEQ      *OFF                                         IF NOT EOF
      *
     C     *INOF         IFEQ      *ON
     C                   EXCEPT    HRHDR
     C                   ENDIF
      *
      * Concatenate telephone area code and number into 1 field.
      *
     C     mgtlacno      mult      10000000      dttel
     C                   add       mgtlphno      dttel
      *
      * Concatenate FAX area code and number into 1 field.
      *
     C     mgfxacno      mult      10000000      dtfax
     C                   add       mgfxphno      dtfax
      *
      * PRINT MANAGER LINE
      *
     C                   EXCEPT    DTLINE
     C                   MOVEL     YES           PRTFLG
     C                   ENDIF                                                  IF NOT EOF
     C                   ENDDO                                                  MAIN DO
      *
      *---------------------------------------------------------------
      * EPF PROCESSING
      *---------------------------------------------------------------
      *  IF THE MASTER FILE WAS EMPTY AND NO RECORDS WERE PRINTED,
      *  PRINT THE STANDARD 'NO DATA...' ERROR MESSAGE
      *
     C     PRTFLG        IFEQ      NO
     C                   EXCEPT    NODATA
     C                   ENDIF
      *
     C                   SETON                                        LR
      /EJECT
      *---------------------------------------------------------------
      * *INZSR - INITIALIZATION SUBROUTINE
      *---------------------------------------------------------------
      *
     C     *INZSR        BEGSR
      *
      * SET PRINT FLAG TO DETERMINE IF ANY RECORDS WERE PRINTED
      * RETRIEVE TIME FOR REPORT HEADING
      *
     C                   MOVEL     NO            PRTFLG            1
     C                   MOVEL     *ALL'-'       DASH            132
     C                   TIME                    RTIME             6 0
      *
     C                   z-add     0             dttel            10 0
     C                   z-add     0             dtfax            10 0
      *
     C                   ENDSR
      /EJECT
      *-------------------------------------------------------------
      * REPORT HEADING LINES
      *-------------------------------------------------------------
      *
     OQPRINT    E            HRHDR          1 01
     O                       SDPGM               10
     O                                           68 'HOG PRODUCTION SYSTEM'
     O                                          122 'DATE'
     O                       UDATE         Y    132
      *
     O          E            HRHDR          1
     O                       SDUSR               10
     O                                           67 'LISTING OF MANAGERS'
     O                                          122 'TIME'
     O                       RTIME              132 '  :  :  '
      *
     O          E            HRHDR          2
     O                                          122 'PAGE'
     O                       PAGE          Z    132
      *
     O          E            HRHDR          1
     O                                            7 'MANAGER'
     O                                           57 'TELEPHONE'
      *
     O          E            HRHDR          1
     O                                            6 'CODE'
     O                                           15 'NAME'
     O                                           43 'STATUS'
     O                                           56 'NUMBER'
     O                                           73 'FAX NUMBER'
     O                                           96 'E-MAIL ADDRESS'
      *
     O          E            HRHDR          1
     O                       DASH               132
      *
      *-------------------------------------------------------------
      * MANAGER DETAIL LINE
      *-------------------------------------------------------------
      *
     O          E            DTLINE      1
     O                       MGMGCD               6
     O                       MGMGNM              36
     O                       MGMGST              40
     O                       dttel               58 '   -   -    '
     O                       dtfax               74 '   -   -    '
     O                       mgemail            132
      *
      *-------------------------------------------------------------
      * NO DATA MESSAGE LINE
      *-------------------------------------------------------------
      *
     O          E            NODATA      1
     O                                           19 'NO DATA MEETS YOUR'
     O                                           38 'SELECTION CRITERIA'
