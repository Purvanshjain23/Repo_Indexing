      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      HPS
      * PROGRAM:     HP292 - Validate Driver (Active Drivers Only)
      * PROGRAMMER:  LeAnne Ramsey
      * CREATED:     06/18/09
      *
      * FUNCTION:    This program validates incoming Driver for SYNON
      *
      *              Since some Synon programs needed both Active/Inactive and some didn't,
      *              we cloned HP272 and added a check for "active/inactive".
      *
      *************************************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 10/15/13  LeAnne Ramsey (E2831)
      *           Recompile only. Added field 'MTech Reference'.
      *
      * 10/22/13  LeAnne Ramsey (E2831)
      *           We will now pass the MTech References back out
      *           to the calling Synon program.
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS:
      ****************************************************************
      *
     Fhsp046    if   e           k disk
      *  Trucker codes
      *
      *
     Fhsp107    if   e           k disk
      *  Driver
      *
      /eject
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      *
      ****************************************************************
      * ARRAYS AND TABLES
      ****************************************************************
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Parm fields
      *
     D xxreturn        s              7
     D xxdicd          s                   like(didicd)
     D xxdinm          s                   like(didinm)
     D xxdiref         s                   like(dimtechref)
     D xxtocd          s                   like(ditocd)
     D xxaist          s                   like(diaist)
     D xxtruck         s                   like(ditruck)
     D xxtrail         s                   like(ditrail)
     D xxticd          s                   like(diticd)
     D xxtinm          s                   like(titinm)
     D xxtiref         s                   like(timtechref)
      *
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * MAINLINE
      ****************************************************************
      *
     C     xxdicd        chain     hsp107                             92
     C                   if        *in92 = *on or diaist = 'I'                  If error
     C                   eval      xxreturn = 'ERROR'
     C                   else
     C                   movel     didinm        xxdinm
     C                   movel     ditocd        xxtocd
     C                   movel     diaist        xxaist
     C                   movel     ditruck       xxtruck
     C                   movel     ditrail       xxtrail
     C                   move      diticd        xxticd
     C                   move      dimtechref    xxdiref
      *
      * Retrieve Trucker Name
      *
     C     diticd        chain     hsp046                             92
     C                   if        *in92 = *off
     C                   move      titinm        xxtinm
     C                   move      timtechref    xxtiref
     C                   else
     C                   move      *blank        xxtinm
     C                   move      *blank        xxtiref
     C                   endif
      *
     C                   endif                                                  If error
      *
     C                   seton                                        lr
      /eject
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
     C     *entry        plist
     C                   parm                    xxreturn
     C                   parm                    xxdicd
     C                   parm                    xxdinm
     C                   parm                    xxtocd
     C                   parm                    xxaist
     C                   parm                    xxtruck
     C                   parm                    xxtrail
     C                   parm                    xxticd
     C                   parm                    xxtinm
     C                   parm                    xxtiref
     C                   parm                    xxdiref
      *
     C                   endsr
      /eject
