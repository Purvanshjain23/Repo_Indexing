/***************************************************************** */
/*                                                                 */
/*  PROGRAM NUMBER:  PMLMUPC                                       */
/*  PROGRAM NAME:    PURGE RECORDS TO HISTORY USING SQL            */
/*  PROGRAMMER:      LARA BUSER                                    */
/*  CREATE DATE:     08/10/2007                                    */
/*                                                                 */
/*  FUNCTION:        THIS CL WILL COPY RECORDS FROM THE FILE TO    */
/*                   THE HISTORY FILE THEN DELETE THE COPIED       */
/*                   RECORDS FROM THE FILE. IT WILL THEN           */
/*                   DELETE RECORDS FROM THE HISTORY FILE.         */
/*                   COPY AND DELETES ARE BASED ON THE PASSED      */
/*                   IN PURGE DATE AND CALC'D HISTORY DATE.        */
/*                                                                 */
/*  PUT THE HISTORY FILE NAME IN THE TAPE FILE FIELD               */
/*                                                                 */
/*  PARAMETERS:                                                    */
/*    &PDTE       7.0  PURGE FROM PRODUCTION TO HISTORY DATE       */
/*    &HDTE       7.0  PURGE FROM HISTORY DATE                     */
/*    &PCGCODE    3    GROUP CODE                                  */
/*    &PCGLIB    10    GROUP CODE LIBRARY                          */
/*    &FILE      10    FILE TO PURGE                               */
/*    &PCFTAPE   10    PURGE TO HISTORY FILE NAME                  */
/*    &DATEFIELD  6    FIELD TO COMPARE DATE TO FOR PURGING        */
/*                     (MUST BE A DATE FIELD)                      */
/*    &DATEFMT   10    DATE FORMAT IF NOT *CYMD, ELSE, BLANKS      */
/*******************************************************************/
/*  Modification History                                           */
/*                                                                 */
/*******************************************************************/
             PGM        PARM(&RTNCODE &PDTE &PCGCODE &PCGLIB +
                          &PCAPPLIC &FILE &PCFLGL &PCFTAPE &PCFIELD +
                          &DATEFMT)

/* PARAMETERS AND DECLARES                                         */
             DCL        VAR(&RTNCODE) TYPE(*CHAR) LEN(7)
             DCL        VAR(&PDTE) TYPE(*DEC)  LEN(7 0)
             DCL        VAR(&PCGCODE) TYPE(*CHAR) LEN(3)
             DCL        VAR(&PCGLIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&PCAPPLIC) TYPE(*CHAR) LEN(6)
             DCL        VAR(&FILE) TYPE(*CHAR) LEN(10)
             DCL        VAR(&PCFLGL) TYPE(*CHAR) LEN(10)
             DCL        VAR(&PCFTAPE) TYPE(*CHAR) LEN(10)
             DCL        VAR(&PCFIELD) TYPE(*CHAR) LEN(6)
             DCL        VAR(&DATEFMT) TYPE(*CHAR) LEN(10)
             DCL        VAR(&PDTEA) TYPE(*CHAR)  LEN(10)
             DCL        VAR(&HDTE) TYPE(*DEC)  LEN(7 0)
             DCL        VAR(&HDTEA) TYPE(*CHAR)  LEN(10)
             DCL        VAR(&SEL)  TYPE(*CHAR) LEN(512) /* SELECT */
             DCL        VAR(&MSG) TYPE(*CHAR) LEN(100)  /* MESSAGE */

/*-----------------------------------------------------------------*/
/* INITIALIZE WORK FIELDS, CALL CLC PURGE DATE XF TO CALCULATE     */
/* THE PURGE FROM HISTORY DATE.                                    */
/*-----------------------------------------------------------------*/
             CHGVAR     VAR(&PDTEA) VALUE(&PDTE) /* PURGE DATE */
             CALL       PGM(POGGXFR) PARM(&RTNCODE &PCGCODE PCGLIB +
                          &PCAPPLIC &PDTE &HDTE)
             CHGVAR     VAR(&HDTEA) VALUE(&HDTE) /* PURGE HIST DATE */

/*-----------------------------------------------------------------*/
/* MAINLINE                                                        */
/*-----------------------------------------------------------------*/
/* Insert into To HISTORY */
             CHGVAR     VAR(&SEL) VALUE('Select * from ' *CAT &FILE +
                          *CAT ' where ' *CAT &PCFIELD *BCAT '<' +
                          *BCAT &PDTEA)

             INSERT     INTO(&PCFTAPE) SQL(&SEL)
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(INSERTHST))

/* Delete from PRODUCTION */
             CHGVAR     VAR(&SEL) VALUE('from ' *CAT &FILE *CAT ' +
                          where ' *CAT &PCFIELD *BCAT '<' *BCAT +
                          &PDTEA)

             DELETE     SQL(&SEL)
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(DELETEPRD))

/* Delete from HISTORY */
             CHGVAR     VAR(&SEL) VALUE('from ' *CAT &PCFTAPE *CAT ' +
                          where ' *CAT &PCFIELD *BCAT '<' *BCAT +
                          &HDTEA)

             DELETE     SQL(&SEL)
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(DELETEHST))

/* Successful Completion */
             GOTO       CMDLBL(PGMMSG)
/*-----------------------------------------------------------------*/
/*                     ERROR MESSAGES                              */
/*-----------------------------------------------------------------*/
 INSERTHST:  SNDPGRMSG  TOPGR(PRKPURGE) MSG('INSERT ERROR into file +
                          ' *CAT &PCFTAPE) CONFMSG(*NO)
             GOTO       CMDLBL(ERROR)

 DELETEPRD:  SNDPGRMSG  TOPGR(PRKPURGE) MSG('DELETE ERROR from file +
                          ' *CAT &FILE) CONFMSG(*NO)
             GOTO       CMDLBL(ERROR)

 DELETEHST:  SNDPGRMSG  TOPGR(PRKPURGE) MSG('DELETE ERROR from file +
                          ' *CAT &PCFTAPE) CONFMSG(*NO)
             GOTO       CMDLBL(ERROR)

 ERROR:      SNDPGRMSG  TOPGR(PRKPURGE) MSG('Purge of ' *CAT &FILE +
                          *CAT ' and ' *CAT &PCFTAPE *CAT ' Ended +
                          Abnormally') CONFMSG(*NO)

             CHGJOB     LOG(4 00 *SECLVL)

             GOTO       CMDLBL(ENDPGM)
/*-----------------------------------------------------------------*/
/*                      PROGRAM END                                */
/*-----------------------------------------------------------------*/
 PGMMSG:     SNDPGRMSG  TOPGR(PRKPURGE) MSG('Purge of ' *CAT &FILE +
                          *CAT ' to history, and purge of ' *CAT +
                          &PCFTAPE *CAT ' Ended Normally') CONFMSG(*NO)
 ENDPGM:     ENDPGM
