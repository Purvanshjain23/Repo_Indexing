      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Resource Optimization
      * PROGRAM:     RO210 - Delete Weekly Meat Cost Calculation Records
      * PROGRAMMER:  LeAnne Ramsey
      * CREATED:     12/06/06
      *
      * Function:    When the Weekly Meat Cost function is run, we call this program
      *              first to delete any records that may already exist for the
      *              Company/Week.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 04/10/07  LeAnne Ramsey
      *           We are switching from "sales" to "produced".
      *
      * 04/18/07  LeAnne Ramsey
      *           We now have 'byproduct mix' files/logic for the TF Fees & Payments.
      *
      * 08/09/07  LeAnne Ramsey
      *           We have more 'byproduct mix' files/logic for the TF Fees & Payments.
      *
      * 10/04/07  LeAnne Ramsey
      *           Recompile only. New field 'Produced Start Weight' added to
      *           Credit Item Group Meat Cost file.
      *
      * 11/02/07  LeAnne Ramsey
      *           Removed code for 2 deleted files:
      *             ROP104-Ham group/item sales
      *             ROP105-Ham group matrix detail
      *
      * 12/21/07  Alice Brownfield
      *           Changed Text on comments to Byproduct instead of Credit Item
      *           No actual programming changes.
      *
      * 06/06/08  LeAnne Ramsey
      *           Recompile only. New fields (Ingredient and Other Cost) added to
      *           multiple files.
      *
      * 06/11/08  LeAnne Ramsey
      *           Add logic to delete the Product Exception records that were "system-
      *           generated".
      *           Reset the data area for "temporary" system numbers back to
      *           a starting number of 9000000.
      *
      * 06/26/08  LeAnne Ramsey
      *           Removed logic for ROP125. We have deleted this file from the system.
      *
      * 07/01/08  LeAnne Ramsey
      *           Recompile only.
      *           Increased decimal positions for "pounds" and "amount" fields in
      *           Product Exception file.
      *
      * 07/24/08  LeAnne Ramsey
      *           Our logic to reset the data area for Temporary Product Exception
      *           Numbers does not work if/when you have multiple "weeks" in the
      *           "Preliminary" stage. I changed this logic to only reset when there
      *           is only 1 "preliminary" week.
      *
      * 08/29/08  LeAnne Ramsey
      *           Add logic to delete records from the new TF ByProduct Volume file
      *
      * 11/21/08  LeAnne Ramsey
      *           As part of synchronizing the LDAs between the TFS Margin Adjustment Close
      *           and the Meat Costing, we changed the LDA positions.
      *
      * 12/02/08  LeAnne Ramsey
      *           Recompile only.
      *
      * 03/02/09  LeAnne Ramsey
      *           Recompile only. Field 'Smoker Yield %' added to Resource Optimization Header.
      *
      * 04/01/09  LeAnne Ramsey
      *           Recompile only. Field 'Producing Company' added to Byproduct Detail.
      *
      * 06/17/09  LeAnne Ramsey
      *           Recompile only. Three fields were added to ROP103-Item Meat Cost:
      *               TF Classification Code, TF Class Group Code, Item Structure Type Code
      *
      * 02/17/10  LeAnne Ramsey
      *           Recompile only. Two new fields were added to the Resource Optimization Header:
      *                Cap&Blade Allocation % for Special Trim
      *                Cap&Blade Allocation % for Regular Trim
      *
      * 02/01/11  LeAnne Ramsey (E1298)
      *           We will no longer build records in ROP124-TF ByProduct Mix
      *           Exceptions. So, I have removed the file/logic.
      *
      * 03/08/12  LeAnne Ramsey (E1984)
      *           Recompile only.
      *           Added 6 new Skirt Meat Allocation Flag fields to ROP100.
      *
      * 05/02/12  LeAnne Ramsey (E2077)
      *           Recompile only.
      *           We added a new field to ROP125-TF ByProduct Volume:
      *              YVT72APR-Special Trim Price/LB
      /eject
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Ftfl019b   uf   e           k disk
      *  Product exception
      *
      *
     Frol100b   if   e           k disk
      *  Resource optimization header
      *
      *
     Frop101    uf   e           k disk
      *  Byproduct values summary
      *
      *
     Frop102    uf   e           k disk
      *  Component item meat cost
      *
      *
     Frop103    uf   e           k disk
      *  Item meat cost
      *
      *
     Frop106    uf   e           k disk
      *  Ham group matrix summary
      *
      *
     Frop107    uf   e           k disk
      *  BOM yield overrides
      *
      *
     Frop121    uf   e           k disk
      *  TF Byproduct mix summary
      *
      *
     Frop122    uf   e           k disk
      *  Byproduct detail
      *
      *
     Frop123    uf   e           k disk
      *  TF Byproduct mix recap
      *
      *
     Frop125    uf   e           k disk
      *  TF Byproduct volume
      *
      /eject
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      *---------------------------------------------------------------
      * CONSTANTS
      *---------------------------------------------------------------
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      * Workfields
      *
     D count           s              3  0
      *
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *-------------------------------------------------------------------------
      * Definition for external data area DATFTMSN-Temporary System Numbers
      *-------------------------------------------------------------------------
      *
     Dnexttmsn         s              7  0 dtaara(datftmsn)
      *
      *---------------------------------------------------------------
      *  Local data area
      *---------------------------------------------------------------
      *
     d lda            uds                  dtaara(*lda)
     D  ldwedt                29     36  0
      *
     D  ldcono                82     84  0
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * Mainline
      ****************************************************************
      *
      * Product Exceptions
      *
     C     ldwedt        setll     tfl019b
      *
     C                   dou       *in90 = *on
     C     ldwedt        reade     tfl019b                                90
     C                   if        *in90 = *off and pesysfl = yes
     C                   delete    perec
     C                   endif
     C                   enddo
      *
      * Byproduct Value Summary
      *
     C     key01         setll     rop101
      *
     C                   dou       *in90 = *on
     C     key01         reade     rop101                                 90
     C                   if        *in90 = *off
     C                   delete    gmrec
     C                   endif
     C                   enddo
      *
      *
      * Component Item Meat Cost
      *
     C     key01         setll     rop102
      *
     C                   dou       *in90 = *on
     C     key01         reade     rop102                                 90
     C                   if        *in90 = *off
     C                   delete    cmrec
     C                   endif
     C                   enddo
      *
      *
      * Item Meat Cost
      *
     C     key01         setll     rop103
      *
     C                   dou       *in90 = *on
     C     key01         reade     rop103                                 90
     C                   if        *in90 = *off
     C                   delete    imrec
     C                   endif
     C                   enddo
      *
      *
      * Ham group matrix summary
      *
     C     key01         setll     rop106
      *
     C                   dou       *in90 = *on
     C     key01         reade     rop106                                 90
     C                   if        *in90 = *off
     C                   delete    msrec
     C                   endif
     C                   enddo
      *
      *
      * BOM yield overrides
      *
     C     key01         setll     rop107
      *
     C                   dou       *in90 = *on
     C     key01         reade     rop107                                 90
     C                   if        *in90 = *off
     C                   delete    borec
     C                   endif
     C                   enddo
      *
      *
      * TF Byproduct mix summary
      *
     C     ldwedt        setll     rop121
      *
     C                   dou       *in90 = *on
     C     ldwedt        reade     rop121                                 90
     C                   if        *in90 = *off
     C                   delete    ysrec
     C                   endif
     C                   enddo
      *
      *
      * Byproduct detail
      *
     C     ldwedt        setll     rop122
      *
     C                   dou       *in90 = *on
     C     ldwedt        reade     rop122                                 90
     C                   if        *in90 = *off
     C                   delete    ydrec
     C                   endif
     C                   enddo
      *
      *
      * TF Byproduct mix recap
      *
     C     ldwedt        setll     rop123
      *
     C                   dou       *in90 = *on
     C     ldwedt        reade     rop123                                 90
     C                   if        *in90 = *off
     C                   delete    yrrec
     C                   endif
     C                   enddo
      *
      *
      * TF Byproduct volume
      *
     C     ldwedt        setll     rop125
      *
     C                   dou       *in90 = *on
     C     ldwedt        reade     rop125                                 90
     C                   if        *in90 = *off
     C                   delete    yvrec
     C                   endif
     C                   enddo
      *
      *
      * Potentially reset the "temporary" system-assigned numbers to 9000000.
      * We use these temp numbers on "preliminary" runs. So, if there is ONLY
      * a single "preliminary" run, reset.
      *
     C     *loval        setll     rol100b
     C                   dou       *in90 = *on
     C                   read      rol100b                                90
     C                   if        *in90 = *off
     C                   add       1             count
     C                   endif
     C                   enddo
      *
     C                   if        count = 1
     C     *lock         in        nexttmsn
     C                   z-add     9000000       nexttmsn
     C                   out       nexttmsn
     C                   endif
      *
      *
     C                   seton                                        lr
      /eject
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Key lists
      *
     C     key01         klist
     C                   kfld                    ldcono
     C                   kfld                    ldwedt
      *
      *
     C                   endsr
      /eject
