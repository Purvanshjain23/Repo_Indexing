/*********************************************************************/
/*                                                                   */
/*  SYNOPSIS -  PASS PARAMETERS TO SEQUEL REPORT AND EMAIL           */
/*                                                                   */
/*  SYSTEM    - ORDER MANAGEMENT SYSTEM                              */
/*  PROGRAM   - EMAIL ITEM FREIGHT COMPARISON REPORT AS AN           */
/*              EXCEL SPREADSHEET                                    */
/*  DATE      - 05/21/2008                                           */
/*                                                                   */
/*  PARAMETERS:                                                      */
/*                                                                   */
/*     &RETURN     - RETURN CODE                                     */
/*     &COMPNYN    - COMPANY NUMBER (15.0) (HIDDEN)                  */
/*     &SHIPTON    - SHIP TO CUSTOMER NUMBER (15.0) (OPTIONAL)       */
/*     &CARRIER    - CARRIER CODE (OPTIONAL) (MUST BE IN QUOTES)     */
/*     &FRDATE     - FROM DATE (15.0)                                */
/*     &LOADID     - LOAD ID (OPTIONAL)                              */
/*     &TODATE     - TO DATE (15.0)                                  */
/*     &EMAIL      - EMAIL ADDRESS                                   */
/*     &SELECT     - SEQUEL REPORT SELECTION                         */
/*********************************************************************/
/*                        MODIFICATIONS                              */
/*-------------------------------------------------------------------*/
/* 05/29/2009 LJB E00411  ADD SHIPPED ITEM CODE                      */
/* 11/19/2008 LJB S00566  CHANGE PRKDEVQRY TO *LIBL                  */
/*********************************************************************/
 PMU0UPC:    PGM        PARM(&RETURN &CARRIER &COMPNYN &SHIPTON +
                          &FRDATE &TODATE &LOADIDN &ITEMN &EMAIL +
                          &SELECT)

/* PARMS                                                             */
        DCL    &RETURN     *CHAR   (7)  /* RETURN CODE               */
        DCL    &COMPNYN    *DEC         /* COMPANY NUMBER (15.0)     */
        DCL    &FRDATE     *DEC         /* FROM DATE (15.0)          */
        DCL    &TODATE     *DEC         /* TO DATE (15.0)            */
        DCL    &SHIPTON    *DEC         /* SHIP TO CUSTOMER NUMBER   */
        DCL    &LOADIDN    *DEC         /* LOAD ID (15.0)            */
        DCL    &ITEMN      *DEC         /* SHIPPED ITEM (15.5)       */
        DCL    &SHPTOCUST  *DEC   (7 0) /* SHIP TO CUSTOMER NUMBER   */
        DCL    &ITEM       *DEC   (7 0) /* SHIPPED ITEM CODE         */
        DCL    &CARRIER    *CHAR   (5)  /* CARRIER CODE WITH QUOTES  */
        DCL    &EMAIL      *CHAR   (50) /* EMAIL ADDRESS             */
        DCL    &EMAILFMT   *CHAR   (5)  /* EMAIL FORMAT              */
        DCL    &SELECT     *CHAR   (1)  /* SEQUEL REPORT SELECTION   */

/* INTERNAL FIELDS - CHAR FIELDS ARE TO FOR SEQUEL VIEW              */
        DCL    &COMP       *DEC    (3 0)        /* COMPANY NUMBER    */
        DCL    &LOADID     *DEC    (7 0)        /* LOAD ID           */
        DCL    &FDATE      *DEC    (7 0)        /* FROM DATE         */
        DCL    &TDATE      *DEC    (7 0)        /* TO DATE           */
        DCL    &COMPA      *CHAR   (3)          /* COMPANY ALPHA     */
        DCL    &FDATEA     *CHAR   (7)          /* FROM DATE ALPHA   */
        DCL    &TDATEA     *CHAR   (7)          /* TO DATE ALPHA     */
        DCL    &LOADIDA    *CHAR   (7)          /* LOAD ID ALPHA     */
        DCL    &ITEMA      *CHAR   (7)          /* ITEM ALPHA        */
        DCL    &SHPTOCUSTA *CHAR   (7)          /* SHIP TO CUSTOMER  */

/* ERROR MESSAGE FIELDS                                                       */
        DCL    &MSG        *CHAR   (512)         /* message                   */
        DCL    &MSGID      *CHAR   (7)           /* message id                */
        DCL    &MSGL       *DEC    (5 0)

/* MONITOR MESSAGES                                                  */
             MONMSG     MSGID(CPF0000 CPF9999) EXEC(GOTO +
                          CMDLBL(MSGTRAP))

/* CONVERT 15.5 VARIABLES TO 2.0 & 4.0, CONVERT NUMERIC TO CHARACTER */
             CHGVAR     VAR(&RETURN) VALUE('       ')
             CHGVAR     VAR(&COMP) VALUE(&COMPNYN)
             CHGVAR     VAR(&COMPA) VALUE(&COMP)
             CHGVAR     VAR(&FDATE) VALUE(&FRDATE)
             CHGVAR     VAR(&FDATEA) VALUE(&FDATE)
             CHGVAR     VAR(&TDATE) VALUE(&TODATE)
             CHGVAR     VAR(&TDATEA) VALUE(&TDATE)
             CHGVAR     VAR(&SHPTOCUST) VALUE(&SHIPTON)
             CHGVAR     VAR(&SHPTOCUSTA) VALUE(&SHPTOCUST)
             CHGVAR     VAR(&LOADID) VALUE(&LOADIDN)
             CHGVAR     VAR(&LOADIDA) VALUE(&LOADID)
             CHGVAR     VAR(&ITEM) VALUE(&ITEMN)
             CHGVAR     VAR(&ITEMA) VALUE(&ITEM)

/*-------------------------------------------------------------------*/
/* RUN AND EMAIL THE SEQUEL REPORT                                   */
/*-------------------------------------------------------------------*/
/* SPREADSHEET FOR ALL FOR DATE RANGE:                               */
             IF         COND(&SELECT *EQ '1') THEN(DO)
             RUNSCRIPT  SCRIPT(IFRTCMP1.S) SETVAR((&EMAIL +
                          &EMAIL) (&COMP &COMPA) (&FDATE &FDATEA) +
                          (&TDATE &TDATEA))
             GOTO       CMDLBL(EXIT)
             ENDDO

/* SPREADSHEET FOR SHIP TO CUSTOMER NUMBER                           */
             IF         COND(&SELECT *EQ '2') THEN(DO)
             RUNSCRIPT  SCRIPT(*LIBL/IFRTCMP2.S) SETVAR((&EMAIL +
                          &EMAIL) (&COMP &COMPA) (&SHIPTOCUST +
                          &SHPTOCUSTA) (&FDATE &FDATEA) (&TDATE +
                          &TDATEA))
             GOTO       CMDLBL(EXIT)
             ENDDO

/* SPREADSHEET FOR LOAD ID:                                          */
             IF         COND(&SELECT *EQ '3') THEN(DO)
             RUNSCRIPT  SCRIPT(*LIBL/IFRTCMP3.S) SETVAR((&EMAIL +
                          &EMAIL) (&LOADID &LOADIDA) (&FDATE +
                          &FDATEA) (&TDATE &TDATEA))
             GOTO       CMDLBL(EXIT)
             ENDDO

/* SPREADSHEET FOR CARRIER CODE:                                     */
             IF         COND(&SELECT *EQ '4') THEN(DO)
             RUNSCRIPT  SCRIPT(*LIBL/IFRTCMP4.S) SETVAR((&EMAIL +
                          &EMAIL) (&COMP &COMPA) (&CARRIER +
                          &CARRIER) (&FDATE &FDATEA) (&TDATE &TDATEA))
             GOTO       CMDLBL(EXIT)
             ENDDO

/* SPREADSHEET FOR ALL FOR DATE RANGE & COMPANY = *ALL               */
             IF         COND(&SELECT *EQ '5') THEN(DO)
             RUNSCRIPT  SCRIPT(*LIBL/IFRTCMP5.S) SETVAR((&EMAIL +
                          &EMAIL) (&FDATE &FDATEA) (&TDATE &TDATEA))
             GOTO       CMDLBL(EXIT)
             ENDDO

/* SPREADSHEET FOR SHIPPED ITEM CODE:                                */
             IF         COND(&SELECT *EQ '6') THEN(DO)
             RUNSCRIPT  SCRIPT(*LIBL/IFRTCMP6.S) SETVAR((&EMAIL +
                          &EMAIL) (&ITEM &ITEMA) (&FDATE &FDATEA) +
                          (&TDATE &TDATEA))
             GOTO       CMDLBL(EXIT)
             ENDDO

/*----------------------RECEIVE MESSAGES----------------------------*/
 MSGTRAP:    RCVMSG     MSGTYPE(*EXCP) MSGDTA(&MSG) MSGDTALEN(&MSGL) +
                          MSGID(&MSGID)
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(EXIT))

             SNDPGMMSG  MSGID(&MSGID) MSGF(QCPFMSG) MSGDTA(%SST(&MSG +
                          1 &MSGL)) MSGTYPE(*ESCAPE)
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(EXIT))
             CHGVAR     VAR(&RETURN) VALUE('USR0417')

EXIT:  ENDPGM
