     A*%%TS  SD  20021106  074016  ISLRAMS     REL-V5R1M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION
     A*  DISPLAY FILE:  HPD4430
     A*  TITLE:         SPECIFY OPTIONS: REPORTED DAILY ACTIVITY LISTING
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  DATE:          10/22/02
     A*
     A*  NOTE:  THIS IS A POP-UP WINDOW. THE WINDOW DEFINITION IS IN
     A*         RECORD FORMAT HP500EC
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CF03(03 'EXIT')
     A                                      CF04(04 'PROMPT')
     A                                      CF08(08 'SUBMIT')
     A*
     A*----------------------------------------------------------------
     A* RECORD FORMAT ONE
     A*----------------------------------------------------------------
     A          R HP4430R1
     A*%%TS  SD  20021106  074016  ISLRAMS     REL-V5R1M0  5722-WDS
     A                                      WINDOW(HP4430EC)
     A                                      TEXT('INPUT SCREEN')
     A                                      SETOF(31)
     A                                      SETOF(32)
     A                                      SETOF(33)
     A                                      SETOF(34)
     A                                      SETOF(51)
     A                                      OVERLAY
     A                                      RTNCSRLOC(&RCD &FLD)
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  1REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 15'SPECIFY OPTIONS: REPORTED DAILY AC-
     A                                      TIVITY LISTING'
     A                                      DSPATR(HI)
     A                                  3  1'From daily date ..'
     A            D1FMDY    R        B  3 22REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  ')
     A  32                                  DSPATR(PC)
     A  32                                  DSPATR(RI)
     A N32                                  DSPATR(HI)
     A                                  4  1'To daily date ....'
     A            D1TMDY    R        B  4 22REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  ')
     A  33                                  DSPATR(PC)
     A  33                                  DSPATR(RI)
     A N33                                  DSPATR(HI)
     A                                  5  1'Cell .............'
     A            D1CELL    R        B  5 22REFFLD(HSRREF/@@CELL *LIBL/HSPREF)
     A                                      EDTCDE(M)
     A  34                                  DSPATR(PC)
     A  34                                  DSPATR(RI)
     A N34                                  DSPATR(HI)
     A                                  5 56'(Blank for All)'
     A                                  6  1'Farm site ........'
     A                                  6 20'?'
     A            D1FSCD    R        B  6 22REFFLD(HSRREF/@@FSCD *LIBL/HSPREF)
     A                                      EDTCDE(M)
     A  31
     AO 51                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A N31                                  DSPATR(HI)
     A            D1FSNM    R        O  6 30REFFLD(HSRREF/FSFSNM *LIBL/HSPREF)
     A                                  6 56'(Blank for All)'
     A*
     A*----------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*----------------------------------------------------------------
     A          R HP4430K1
     A*%%TS  SD  20000605  133618  ISLRAMS     REL-V4R2M0  5769-PW1
     A                                      WINDOW(HP4430EC)
     A                                      TEXT('COMMAND KEYS')
     A                                      OVERLAY
     A  43                              7  2'Message program failed. Call IS.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                  8  2'F3=Exit'
     A                                  8 22'F8=Submit'
     A                                  8 11'F4=Prompt'
      *
      *----------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *----------------------------------------------------------------
     A          R HP4430ES
     A                                      TEXT('Error message subfile')
     A                                      SFL SFLMSGRCD(09)
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
     A*
     A*----------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT WITH WINDOW DEFINITION
     A*----------------------------------------------------------------
     A          R HP4430EC                  SFLCTL(HP4430ES)
     A*%%TS  SD  20000607  150524  ISLRAMS     REL-V4R2M0  5769-PW1
     A                                      TEXT('Error message subfile')
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A                                      WINDOW(*DFT 10 70)
     A                                      WDWBORDER((*DSPATR RI) (*CHAR '    -
     A                                          '))
     A            MSGPGM                    SFLPGMQ(10)
      *
      *----------------------------------------------------------------
      * ASSUME FORMAT - REQUIRED FOR WINDOWS
      *----------------------------------------------------------------
      *
     A          R ASSUME
     A                                      TEXT('ASSUME FORMAT FOR WINDOWS')
     A                                      ASSUME OVERLAY
     A                                 24 79' '
