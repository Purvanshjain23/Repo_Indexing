      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      FSBS: Datamart
      * PROGRAM:     HP2188 - Build Closed Budgets
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     05/02/06
      *
      * Function:    We will write 1 record for each item on each Closed Farm Budget.
      *              Since Closed budgets cannot be reactivated, we will write records
      *              to this file when a budget closes. We will not clear the file in
      *              a History run or delete records for a Current run. So, once we
      *              write records to this file, they stay.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      /eject
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fhsj193i   if   e           k disk
      *    Request detail + Item Master
      *
      *
     Fhsp189    if   e           k disk
      *  Farm budget detail
      *
      *
     Fhsp8300   if   e           k disk
      *  DataMart BUD: Workfile--Farm budget header
      *
      *
     Fhsp8188   uf a e           k disk
      *  DataMart BUD: Closed budgets
      *
      /eject
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      *---------------------------------------------------------------
      * CONSTANTS
      *---------------------------------------------------------------
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      *---------------------------------------------------------------
      * STANDALONE FIELDS
      *---------------------------------------------------------------
      *
      * Workfields
      *
     D wkrqqt          s                   like(qdrqqt)
     D wkapqt          s                   like(qdapqt)
     D wkusqt          s                   like(qdusqt)
     D wkover          s                   like(d3over)
      *
      *
      * Parms
      *
     D xxccyymmdd      s                   like(d380wedt)
     D xxacyr          s                   like(d3acyr)
     D xxacpe          s                   like(d3acpe)
     D xxacqr          s                   like(d3acqr)
     D xxacwk          s                   like(d3acwk)
     D xxpicdt         s                   like(d3picdt)
     D xxcdyr          s                   like(d3cdyr)
     D xxcdwk          s                   like(d3cdwk)
      *
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * Mainline
      ****************************************************************
      *
      * Process each 'closed' Farm Budget from the workfile.
      *
     C     *loval        setll     hsp8300
      *
     C                   dou       *in90 = *on                                  Main do
     C                   read      hsp8300                                90
     C                   if        *in90 = *off and                             If not EOF
     C                             w1fbscd = 'C'
      *
      * Since we never delete/clear the Datamart Closed Budget file, if you
      * already have any records for this Farm Budget, you will not process
      * the budget. So, continue processing only when you DON'T have
      * any records for this Farm Budget in the Datamart Closed Budget file.
      *
     C     w1fbsn        chain(n)  hsp8188                            92
     C                   if        *in92 = *on                                  If not found
      *
      * Retrieve all Budget data for this Farm Budget writing a record
      * to the Datamart file for each Budgeted Item.
      *
     C                   exsr      $budget
      *
      * Now, retrieve all Order data for this Farm Budget updating the
      * Datamart records just written (or writing a record if one does
      * not exist.)
     C                   exsr      $orders
     C                   endif                                                  If not found
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Main do
      *
      * EOF processing
      *
     C                   seton                                        lr
      /eject
      *--------------------------------------------------------------------------------------
      * Retrieve all Budgeted Items for this Closed Farm Budget
      *--------------------------------------------------------------------------------------
      *
      * Process each record (budget item) for this Farm Budget in the Farm Budget Detail file.
      *
     C     $budget       begsr
      *
     C     w1fbsn        setll     hsp189
      *
     C                   dou       *in91 = *on                                  Do items
     C     w1fbsn        reade     hsp189                                 91
     C                   if        *in91 = *off                                 If item
      *
     C                   clear                   d3rec
     C                   move      jdbgit        d3bgit
     C                   z-add     jdbgqt        d3bgqt
     C                   z-add     jdfbam        d3fbam
     C                   exsr      $write
      *
     C                   endif                                                  If item
     C                   enddo                                                  Do items
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------------------------------
      * Retrieve all order data for this Farm Budget
      *---------------------------------------------------------------------------------------
      *
     C     $orders       begsr
      *
      * Read each Request Detail record for this Farm Budget. (Do not
      * process records that have a status of Hold, Deleted, Error.)
      *
     C     w1fbsn        setll     hsj193i
      *
     C                   dou       *in93 = *on                                  Do orders
     C     w1fbsn        reade     hsj193i                                93
     C                   if        *in93 = *off and                             If not EOF
     C                             qdrqscd <> 'H' and
     C                             qdrqscd <> 'D' and
     C                             qdrqscd <> 'E'
      *
      *
      * We need the request quantities in the same units they budget in since
      * we compare them to the 'budget quantity'. So, since quantities in the
      * Request Detail record are in the UOM units, we have to flip to the
      * UOW units if an Item has 'volume'.
      *
     C                   if        imvol = 0
     C                   z-add     qdrqqt        wkrqqt
     C                   z-add     qdapqt        wkapqt
     C                   z-add     qdusqt        wkusqt
     C                   else
     C     imvol         mult      qdrqqt        wkrqqt
     C     imvol         mult      qdapqt        wkapqt
     C     imvol         mult      qdusqt        wkusqt
     C                   endif
      *
      * If the Requested Quantity is greater than the Approved Quantity or
      * there is an Override Reason Code, we will count ths as a
      * "time that they tried to order over budget".
      *
     C                   if        qdrqqt > qdapqt or qdorcd <> *blank
     C                   z-add     1             wkover
     C                   else
     C                   z-add     0             wkover
     C                   endif
      *
      * Update or write a Datamart Closed Budget record for this budget item.
      *
     C     key01         chain     hsp8188                            92
     C                   if        *in92 = *off                                 If exists
     C                   add       wkrqqt        d3rqqt
     C                   add       wkapqt        d3apqt
     C                   add       wkusqt        d3usqt
     C                   add       qdtcam        d3usam
     C                   add       wkover        d3over
     C                   update    d3rec
     C                   else
      *
     C                   clear                   d3rec
     C                   move      imbgit        d3bgit
     C                   z-add     wkrqqt        d3rqqt
     C                   z-add     wkapqt        d3apqt
     C                   z-add     wkusqt        d3usqt
     C                   z-add     qdtcam        d3usam
     C                   z-add     wkover        d3over
     C                   exsr      $write
     C                   endif                                                  If exists
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Do Detail
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------------------------------
      * Write a Datamart Closed Budget record
      *---------------------------------------------------------------------------------------
      *
     C     $write        begsr
      *
      * Populate fields that are a "direct" map from the driver file.
      *
     C                   z-add     w1fscd        d3fscd
     C                   z-add     w1fbsn        d3fbsn
     C                   move      w1cscd        d3cscd
     C                   move      w1btcd        d3btcd
     C                   z-add     w1btwk        d3btwk
     C                   z-add     w1bawk        d3bawk
      *
     C                   z-add     w1bfdt        d3bfdt
     C                   z-add     w1btdt        d3btdt
     C                   z-add     w1btdt        d380wedt
     C                   move      w1btdt        d3wedt
      *
      * Retrieve "calendar" stuff for the Budget To Date
      *
     C                   call      'HP8017'
     C                   parm      d3btdt        xxccyymmdd
     C     d3acyr        parm      0             xxacyr
     C     d3acpe        parm      0             xxacpe
     C     d3acqr        parm      0             xxacqr
     C     d3acwk        parm      0             xxacwk
     C     d3picdt       parm      0             xxpicdt
     C     d3cdyr        parm      0             xxcdyr
     C     d3cdwk        parm      0             xxcdwk
      *
     C                   write     d3rec
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Key lists
      *
     C     key01         klist
     C                   kfld                    w1fbsn
     C                   kfld                    imbgit
      *
     C                   endsr
      /eject
