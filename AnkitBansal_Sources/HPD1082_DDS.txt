     A*%%TS  SD  20171011  161019  ISJBADE     REL-V7R1M0  5770-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION
     A*  DISPLAY FILE:  HPD1082
     A*  TITLE:         UT: EDIT (MAINTAIN) UPLOADED TRANSFERS
     A*  PROGRAMMER:    LEANNE RAMSEY
     A*  DATE:          11/11/99
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A                                      CF04(04 'PROMPT')
     A                                      CF05(05 'REFRESH')
     A                                      CF09(09 'ACCEPT')
     A                                      CF13(13 'FOLD/DROP')
     A*---------------------------------------------------------------
     A* SUBFILE FORMAT 1
     A*---------------------------------------------------------------
     A*
     A          R HP1082S1                  SFL
     A*%%TS  SD  20090728  142318  ISLRAMS     REL-V5R4M0  5722-WDS
     A  85                                  SFLNXTCHG
     A            HDRRN1         4Y 0H
     A*
     A            HDCUSN    R        H      REFFLD(HSRREF/@@AJD  *LIBL/HSPREF)
     A            HDCUSNM   R        H      REFFLD(HSRREF/@@DS30 *LIBL/HSPREF)
     A            HDSCSHDT  R        H      REFFLD(HSRREF/@@DT08 *LIBL/HSPREF)
     A            HDSCSHTM  R        H      REFFLD(HSRREF/@@TM06 *LIBL/HSPREF)
     A            HDSCSHSQ  R        H      REFFLD(HSRREF/@@NU60 *LIBL/HSPREF)
     A            HDSCKLDT  R        H      REFFLD(HSRREF/@@DT08 *LIBL/HSPREF)
     A            HDLDNO    R        H      REFFLD(HSRREF/@@NU30 *LIBL/HSPREF)
     A            HDSTCD    R        H      REFFLD(HSRREF/@@STCD *LIBL/HSPREF)
     A            HDLTCD    R        H      REFFLD(HSRREF/@@LTCD *LIBL/HSPREF)
     A            HDTICD    R        H      REFFLD(HSRREF/@@TICD *LIBL/HSPREF)
     A            HDFSCD    R        H      REFFLD(HSRREF/@@FSCD *LIBL/HSPREF)
     A            HDBLCD    R        H      REFFLD(HSRREF/@@BLCD *LIBL/HSPREF)
     A            HDRMCD    R        H      REFFLD(HSRREF/@@RMCD *LIBL/HSPREF)
     A            HDHGCD    R        H      REFFLD(HSRREF/@@HGCD *LIBL/HSPREF)
     A            HDSCHD    R        H      REFFLD(HSRREF/@@HD05 *LIBL/HSPREF)
     A            HDFSNM    R        H      REFFLD(HSRREF/@@NM25 *LIBL/HSPREF)
     A            HDFSBO    R        H      REFFLD(HSRREF/@@BOCD *LIBL/HSPREF)
     A
     A            D1EDITFL  R        H      REFFLD(HSRREF/@@FLAG *LIBL/HSPREF)
     A            D1BOCD    R        H      REFFLD(HSRREF/@@BOCD *LIBL/HSPREF)
     A*
     A            D1CUSN    R        B  7  2REFFLD(HSRREF/@@AJD  *LIBL/HSPREF)
     A  31                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A N31                                  DSPATR(HI)
     A N31                                  DSPATR(UL)
     A                                      EDTCDE(Z)
     A*
     A            D1SHIPDT  R        B  7 11REFFLD(HSRREF/@@DT06 *LIBL/HSPREF)
     A  32                                  DSPATR(PC)
     A  32                                  DSPATR(RI)
     A N32                                  DSPATR(HI)
     A N32                                  DSPATR(UL)
     A                                      EDTWRD('  /  /  ')
     A*
     A            D1SHIPTM  R        B  7 20REFFLD(HSRREF/@@TM04 *LIBL/HSPREF)
     A  33                                  DSPATR(PC)
     A  33                                  DSPATR(RI)
     A N33                                  DSPATR(HI)
     A N33                                  DSPATR(UL)
     A                                      EDTWRD('  :  ')
     A            D1SCSHSQ  R        B  7 26REFFLD(HSRREF/@@NU60 *LIBL/HSPREF)
     A  34                                  DSPATR(PC)
     A  34                                  DSPATR(RI)
     A N34                                  DSPATR(HI)
     A N34                                  DSPATR(UL)
     A                                      EDTCDE(Z)
     A
     A            D1KILLDT  R        B  7 33REFFLD(HSRREF/@@DT06 *LIBL/HSPREF)
     A  35                                  DSPATR(PC)
     A  35                                  DSPATR(RI)
     A N35                                  DSPATR(HI)
     A N35                                  DSPATR(UL)
     A                                      EDTWRD('  /  /  ')
     A*
     A            D1SCHD    R        B  7 42REFFLD(HSRREF/@@HD05 *LIBL/HSPREF)
     A  36                                  DSPATR(PC)
     A  36                                  DSPATR(RI)
     A N36                                  DSPATR(HI)
     A N36                                  DSPATR(UL)
     A                                      EDTCDE(Z)
     A*
     A            D1LDNO    R        B  7 50REFFLD(HSRREF/@@NU30 *LIBL/HSPREF)
     A  37                                  DSPATR(PC)
     A  37                                  DSPATR(RI)
     A N37                                  DSPATR(HI)
     A N37                                  DSPATR(UL)
     A                                      EDTCDE(Z)
     A*
     A            D1HGCD    R        B  7 55REFFLD(HSRREF/@@HGCD *LIBL/HSPREF)
     A  38                                  DSPATR(PC)
     A  38                                  DSPATR(RI)
     A N38                                  DSPATR(HI)
     A N38                                  DSPATR(UL)
     A*
     A            D1FSCD    R        O  7 63REFFLD(HSRREF/@@FSCD *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A  39                                  DSPATR(RI)
     A                                      EDTCDE(Z)
     A*
     A            D1BLCD    R        O  7 69REFFLD(HSRREF/@@BLCD *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A*
     A            D1RMCD    R        O  7 75REFFLD(HSRREF/@@RMCD *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A*
     A                                  8 13'Sales Type:'
     A*                                     COLOR(BLU)
     A            D1STCD    R        O  8 25REFFLD(HSRREF/@@STCD *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A*                                     DSPATR(RI)
     A*
     A                                  8 33'Load Type ?:'
     A            D1LTCD    R        B  8 46REFFLD(HSRREF/@@LTCD *LIBL/HSPREF)
     A  40                                  DSPATR(PC)
     A  40                                  DSPATR(RI)
     A N40                                  DSPATR(HI)
     A N40                                  DSPATR(UL)
     A*
     A                                  8 51'Trucker ?:'
     A            D1TICD    R        B  8 62REFFLD(HSRREF/@@TICD *LIBL/HSPREF)
     A  41                                  DSPATR(PC)
     A  41                                  DSPATR(RI)
     A N41                                  DSPATR(HI)
     A N41                                  DSPATR(UL)
     A*
     A*----------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT  1
     A*----------------------------------------------------------------
     A*
     A          R HP1082C1                  SFLCTL(HP1082S1)
     A*%%TS  SD  20090728  142318  ISLRAMS     REL-V5R4M0  5722-WDS
     A                                      SFLSIZ(0100)
     A                                      SFLPAG(0007)
     A N82                                  ROLLUP(25 'PAGE FORWARD')
     A                                      SFLCSRRRN(&HDRR1)
     A                                      OVERLAY
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND(*MORE)
     A  78                                  SFLFOLD(CF13)
     A  79                                  SFLDROP(CF13)
     A                                      SFLMODE(&FOLD)
     A                                      SETOF(31)
     A                                      SETOF(32)
     A                                      SETOF(33)
     A                                      SETOF(34)
     A                                      SETOF(35)
     A                                      SETOF(36)
     A                                      SETOF(37)
     A                                      SETOF(38)
     A                                      SETOF(39)
     A                                      SETOF(40)
     A                                      SETOF(41)
     A                                      SETOF(42)
     A                                      SETOF(43)
     A                                      SETOF(44)
     A                                      SETOF(45)
     A                                      SETOF(48)
     A                                      SETOF(50)
     A                                      SETOF(51)
     A                                      SETOF(52)
     A                                      SETOF(54)
     A                                      SETOF(55)
     A                                      SETOF(56)
     A                                      SETOF(57)
     A                                      SETOF(58)
     A                                      RTNCSRLOC(&RCD &FLD)
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            HDRR1          5S 0H
     A            FOLD           1A  H
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 26'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A            D1MODE    R        O  2  2REFFLD(@@MODE)
     A                                      DSPATR(HI)
     A                                  2 14'EDIT (MAINTAIN) UPLOADED SCHEDULED'
     A                                      DSPATR(HI)
     A                                  2 49'MOVEMENTS'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  3  2'Business Office.....'
     A                                      COLOR(BLU)
     A            SDBOBO         5A  O  3 23
     A                                      DSPATR(HI)
     A            SDBODS        30A  O  3 30
     A                                      COLOR(BLU)
     A                                  4 11'<------------ SCHEDULED'
     A                                  4 35'----------->'
     A                                  5  6'?'
     A                                  5 13'Ship'
     A                                  5 20'Ship'
     A                                  5 28'Ship'
     A                                  5 35'Kill'
     A                                  5 56'?'
     A                                  6  2'Customer'
     A                                  6 13'Date'
     A                                  6 20'Time'
     A                                  6 29'Seq'
     A                                  6 35'Date'
     A                                  6 43'Head'
     A                                  6 49'Load'
     A                                  6 55'Group'
     A                                  6 64'Farm'
     A                                  6 69'Bldg'
     A                                  6 75'Room'
     A*----------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*----------------------------------------------------------------
     A          R HP1082K1
     A*%%TS  SD  19991111  095829  ISLRAMS     REL-V4R2M0  5769-PW1
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A                                 23 11'F4=Prompt'
     A                                 23 22'F5=Refresh'
     A                                 23 34'F9=Accept'
     A                                 23 59'F13=Fold/Unfold'
      *----------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *----------------------------------------------------------------
     A          R HP1082ES                  SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
     A*----------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*----------------------------------------------------------------
     A*
     A          R HP1082EC                  SFLCTL(HP1082ES)
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
