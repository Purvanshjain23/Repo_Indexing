     A*%%TS  SD  19950228  110351  ISLRAMS     REL-V3R0M5  5763-PW1
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION
     A*  DISPLAY FILE:  HPD115
     A*  TITLE:         REVISE PREMIUM/DISCOUNT CODES
     A*  PROGRAMMER:    ROSE CENTONZE
     A*  DATE:          05/03/94
     A*
     A*  INDICATOR 60 IS ON WHEN IN REVISE MODE
     A*  INDICATOR 61 IS ON WHEN IN CREATE MODE
     A*  INDICATOR 62 IS ON WHEN IN DELETE MODE
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A                                      CF04(04 'PROMPT')
     A  60N62                               CF05(05 'REFRESH')
     A  60N62                               CF11(11 'DELETE')
     A                                      CA12(12 'PREVIOUS')
     A*
     A*--------------------------------------------------------------*
     A* RECORD FORMAT ONE
     A*--------------------------------------------------------------*
     A          R HP115R1
     A*%%TS  SD  19950228  110351  ISLRAMS     REL-V3R0M5  5763-PW1
     A                                      SETOF(31)
     A                                      SETOF(32)
     A                                      SETOF(33)
     A                                      SETOF(34)
     A                                      SETOF(51)
     A                                      SETOF(52)
     A                                      OVERLAY
     A                                      RTNCSRLOC(&RCD &FLD)
     A            RCD           10A  H
     A            FLD           10A  H
     A                                  1  2'HP115'
     A                                      DSPATR(HI)
     A                                  1 27'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A            DFMODE         6A  O  2  2DSPATR(HI)
     A                                  2 24'REVISE PREMIUM/DISCOUNT CODE'
     A                                      DSPATR(HI)
     A                                  2 27'WORK WITH FARM SUPERVISORS'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  4  2'Premium/discount code ..'
     A            DFPDCD    R        B  4 29REFFLD(PDPDCD)
     A  61N31                               DSPATR(HI)
     A  31                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A  60
     AO 62                                  DSPATR(PR)
     A  61                                  DSPATR(UL)
     A                                      TEXT('Premium/Discount code')
     A                                  5  2'Description ............'
     A            DFPDDS    R        B  5 29REFFLD(PDPDDS)
     A N62N32                               DSPATR(HI)
     A  32                                  DSPATR(PC)
     A  32                                  DSPATR(RI)
     A  62                                  DSPATR(PR)
     A N62                                  DSPATR(UL)
     A                                      TEXT('premium/discount code')
     A                                  6  2'Premium/discount flag ..'
     A                                  6 27'?'
     A            DFPDFL    R        B  6 29REFFLD(PDPDFL)
     A                                      TEXT('premium/discount flag')
     A N62N33                               DSPATR(HI)
     A  51
     AO 33                                  DSPATR(PC)
     A  33                                  DSPATR(RI)
     A  62                                  DSPATR(PR)
     A N62                                  DSPATR(UL)
     A                                  7  2'Report code ............'
     A                                  7 27'?'
     A            DFRPCD    R        B  7 29REFFLD(PDRPCD)
     A                                      TEXT('report CODE')
     A N62N34                               DSPATR(HI)
     A  52
     AO 34                                  DSPATR(PC)
     A  34                                  DSPATR(RI)
     A  62                                  DSPATR(PR)
     A N62                                  DSPATR(UL)
     A            DFRLDS    R        O  7 36REFFLD(RPRLDS)
     A*
     A*
     A*--------------------------------------------------------------*
     A* COMMAND KEY RECORD 1
     A*--------------------------------------------------------------*
     A          R HP115K1
     A*%%TS  SD  19940330  133000  ISMPALM     REL-V2R2M0  5738-PW1
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A N62                             23 11'F4=Prompt'
     A N62N61                          23 22'F5=Refresh'
     A N62N61                          23 34'F11=Delete'
     A                                 23 46'F12=Previous'
      *
      *--------------------------------------------------------------*
      * ERROR MESSAGE SUBFILE FORMAT
      *--------------------------------------------------------------*
     A          R HP115ES                   SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
     A*                                                                *********
     A*--------------------------------------------------------------*
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*--------------------------------------------------------------*
     A*
     A          R HP115EC                   SFLCTL(HP115ES)
     A*%%TS  SD  19940503  091615  ISRCENT     REL-V2R2M0  5738-PW1
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
