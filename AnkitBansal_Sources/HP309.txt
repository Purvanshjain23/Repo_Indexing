      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      HOG PRODUCTION
      * PROGRAM:     HP309 - Workfile: Pig Days
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     12/12/01
      *
      * FUNCTION: This program builds a workfile showing pig days by group.
      *
      *           This program can be called from multiple functions to build
      *           the file for various reports.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 07/06/09  LeAnne Ramsey
      *           Recompile only. Added new field 'Continuous Flow Flag' to Hog Group file.
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fhsl034e   if   e           k disk
      *    Hog group
      *    (records selected with open query)
      *
      *
     Fhsp309    o    e           k disk
      *    Workfile: Pig days
      *
      /EJECT
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      *---------------------------------------------------------------
      *  NAMED CONSTANTS
      *---------------------------------------------------------------
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      *---------------------------------------------------------------
      * STANDALONE FIELDS
      *---------------------------------------------------------------
      *
      * Parm fields
      *
     D xxfromdt        s              8  0
     D xxtodt          s              8  0
     D xxfymd          s              8  0
     D xxtymd          s              8  0
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * MAINLINE
      ****************************************************************
      *
      * Process all hog groups selected by the open query.
      *
     C     *loval        setll     hsl034e
      *
     C                   dou       *in90 = *on                                  Main do
     C                   read      hsl034e                                90
     C                   if        *in90 = *off                                 If not EOF
      *
     C                   z-add     hgfscd        wpfscd
     C                   move      hghgcd        wphgcd
     C                   z-add     hghgsn        wphgsn
     C                   move      hggscd        wpgscd
     C                   z-add     hgopdt        wpopdt
     C                   z-add     hgdsdt        wpdsdt
      *
      * Retrieve pig days
      *
     C                   call      'HPPDAY'
     C                   parm                    hghgsn
     C                   parm      xxfromdt      xxfymd
     C                   parm      xxtodt        xxtymd
     C                   parm      0             wppigday
      *
     C                   write     wprec
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Main do
      *
     C                   seton                                        lr
      /EJECT
      *--------------------------------------------------------------------------------------
      * Initialization subroutine
      *--------------------------------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
     C     *entry        plist
     C                   parm                    xxfromdt
     C                   parm                    xxtodt
      *
     C                   endsr
      /EJECT
