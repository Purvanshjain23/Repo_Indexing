      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Triumph Foods
      * PROGRAM:     TF662 - AR OnAccount Balance Listing
      * PROGRAMMER:  LeAnne Ramsey
      * CREATED:     11/04/09
      *
      * Function:    This program prints On Account records from the AR Header file.
      *
      *              This report was created as a result of the prolonged and excruciating
      *              reconcilation that went on in 2007-2010 between the TFS Cash Distribution
      *              data and the Accounts Receivable System data.
      *
      *              The listing is entirely over Synon files; so, it could have been created
      *              in the ARS model. But, to make it consistant with other TFS reports, we
      *              wrote this in TFS.
      *
      *              The listing is generated:
      *                1) on demand
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      /eject
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Farbdcpl0  if   e           k disk
      *  AR Header
      *
      *
     Fcabbrel0  if   e           k disk
      *  Customer
      *
      *
     Fprint198  o    f  198        printer oflind(*inof)
      *
      /eject
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      ****************************************************************
      * ARRAYS AND TABLES
      ****************************************************************
      *
     D artram          s             11  2 dim(2)
     D arduam          s             11  2 dim(2)
     D arbcam          s             11  2 dim(2)
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Standard fields
      *
     D dash            s            198    inz(*all'-')
     d rtime           s              6  0
      *
      *
      * Control fields
      *
     D first           s              1    inz('Y')
     D svcono          s                   like(bdaic3)
      *
      *
      * Workfields for date manipulation
      *
     D wkisodate       s               d   datfmt(*iso)
      *
      *
      * Print fields
      *
     d h1mdy           s              6  0
      *
     d l1trmdy         s              6  0
     d l1pdmdy         s              6  0
     d l1bcam          s             11  2
     d l1cunm          s             30
      *
     d t1desc          s             14
     d t1openam        s             11  2
      *
      *
      * Array index
      *
     D x               s              3  0
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *---------------------------------------------------------------
      * Local data area
      *---------------------------------------------------------------
      *
     d lda            uds                  dtaara(*lda)
      *
     D  ldsyndt               30     36  0
      *
     D  ldcono                71     73  0
      *
     D  ldcuno               103    109  0
     D  ldcunm               110    139
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      *************************************************************************************
      * Mainline
      *************************************************************************************
      *
      * Print headings
     C                   exsr      $h1hdr
      *
      * Read each AR Header record selected by the Open Query.
      *
     C     *loval        setll     arbdcpl0
      *
     C                   dou       *in90 = *on                                  Main do
     C                   read      arbdcpl0                               90
     C                   if        *in90 = *off                                 If not EOF
      *
      * Control breaks
     C                   select
     C                   when      first = yes
     C                   move      no            first
     C                   z-add     bdaic3        svcono
      * Company
     C                   when      svcono <> bdaic3
     C                   exsr      $cono
     C                   endsl
      *
      * Calc Balance Before CutOff:
      *
     C                   if        bdbgva = 0 and bdh4dt > ldsyndt
     C                   z-add     bdbsva        l1bcam
     C                   else
     C                   z-add     0             l1bcam
     C                   endif
      *
      * Accumulate Amounts into arrays
      *
     C                   add       bdbsva        artram
     C                   add       bdbgva        arduam
     C                   add       l1bcam        arbcam
      *
      * Print detail line
      *
     C                   exsr      $l1dtl
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Main do
      *
      *-----------------
      * EOF processing
      *-----------------
     C                   select
     C                   when      first = yes
     C                   except    nodata
     C                   other
      * Print:
      *  Company total
     C                   exsr      $cono
      *  Report total
     C                   z-add     2             x
     C                   eval      t1desc = ' Report Total:'
     C     arduam(x)     add       arbcam(x)     t1openam
     C                   seton                                        87
     C                   exsr      $t1tot
     C                   endsl
      *
     C                   seton                                        lr
      /eject
      *---------------------------------------------------------------
      * Company Total
      *---------------------------------------------------------------
      *
     C     $cono         begsr
      *
     C                   z-add     1             x
      *
     C                   eval      t1desc = 'Company Total:'
      *
      * Print total, clear array entry, save control field
      *
     C                   exsr      $t1tot
     C                   exsr      $clear
     C                   z-add     bdaic3        svcono
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Clear array entries for specified index
      *---------------------------------------------------------------
      *
     C     $clear        begsr
      *
     C                   z-add     0             artram(x)
     C                   z-add     0             arduam(x)
     C                   z-add     0             arbcam(x)
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Print the Detail Line
      *---------------------------------------------------------------
      *
     C     $l1dtl        begsr
      *
      * Flip Transaction Date
      *
     C                   z-add     0             l1trmdy
     C                   if        bdb4dt <> 0
     C     *cymd         test(d)                 bdb4dt                 92
     C                   if        *in92 = *off                                 If OK date
     C     *cymd         move      bdb4dt        wkisodate
     C     *mdy          move      wkisodate     l1trmdy
     C                   endif                                                  If OK date
     C                   endif
      *
      * Flip Paid in Full Date
      *
     C                   z-add     0             l1pdmdy
     C                   if        bdh4dt <> 0
     C     *cymd         test(d)                 bdh4dt                 92
     C                   if        *in92 = *off                                 If OK date
     C     *cymd         move      bdh4dt        wkisodate
     C     *mdy          move      wkisodate     l1pdmdy
     C                   endif                                                  If OK date
     C                   endif                                                  If OK date
      *
      * Retrieve AR Customer Name
      *
     C     bdanc7        chain     cabbrel0                           92
     C                   if        *in92 = *off and                             If hit
     C                             bbbkc7 = bbanc7
     C                   eval      l1cunm = bbaytx
     C                   else
     C                   eval      l1cunm = 'Unknown'
     C                   endif
      *
     C                   if        *inof = *on
     C                   exsr      $h1hdr
     C                   endif
      *
     C                   except    l1dtl
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Print report heading
      *---------------------------------------------------------------
      *
     C     $h1hdr        begsr
      *
     C                   except    h1hdr
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Print Total line
      *---------------------------------------------------------------
      *
     C     $t1tot        begsr
      *
     C                   if        *inof = *on
     C                   exsr      $h1hdr
     C                   endif
      *
     C                   except    t1tot
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Initialization
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
     C                   time                    rtime
      *
      * Set up heading selections:
      *
     C     *cymd         move      ldsyndt       wkisodate
     C     *mdy          move      wkisodate     h1mdy

      * Company
     C                   if        ldcono = 0
     C                   seton                                        80
     C                   endif
      * A/R Customer
     C                   if        ldcuno = 0
     C                   seton                                        82
     C                   endif
      *
     C                   endsr
      /eject
      *-------------------------------------------------------------
      * Report heading lines
      *-------------------------------------------------------------
      *
     Oprint198  e            h1hdr          1 01
     O                       sdpgm               10
     O                                          100 'AR OnAccount Balance '
     O                                          107 'Listing'
     O                                          188 'DATE'
     O                       udate         y    198
      *
     O          e            h1hdr          1
     O                       sdusr               10
     O                                          188 'TIME'
     O                       rtime              198 '  :  :  '
      *
     O          e            h1hdr          1
     O                                          188 'PAGE'
     O                       page          z    198
      *
      *
     O          e            h1hdr          1
     O                                           13 'CutOff date:'
     O                       h1mdy               23 '  /  /  '
      *
     O          e            h1hdr          1
     O                                           13 'Company:'
     O               80                          18 'All'
     O              n80      ldcono        z     18
      *
     O                                           13 'A/R customer:'
     O               82                          18 'All'
     O              n82      ldcuno        z     22
     O              n82      ldcunm              54
      *
      *-------------------------------------------------------------
      * Column headings
      *-------------------------------------------------------------
      *
     O          e            h1hdr       1  1
     O                                           51 'Remaining'
     O                                           70 'Balance'
     O                                          118 'A/R'
      *
     O          e            h1hdr          1
     O                                            4 'Comp'
     O                                           14 'Invoice'
     O                                           32 'Transaction'
     O                                           51 'Balance'
     O                                           70 'Before'
     O                                           79 'Bank'
     O                                           95 'Transaction'
     O                                          109 'Paid In'
     O                                          121 'Customer'
      *
     O          e            h1hdr          1
     O                                            3 'Nbr'
     O                                           14 'Number'
     O                                           32 'Amount'
     O                                           51 'Due'
     O                                           70 'CutOff Date'
     O                                           79 'Code'
     O                                           95 'Date'
     O                                          109 'Full Date'
     O                                          121 'Number'
     O                                          128 'Name'
      *
     O          e            h1hdr          1
     O                       dash               198
      *
      *-------------------------------------------------------------
      * Detail line one
      *-------------------------------------------------------------
      *
     O          e            l1dtl       1  0
      *
     O                       bdaic3        z      3
     O                       bdconb        z     14
     O                       bdbsva        k     33
     O                       bdbgva        k     52
     O                       l1bcam        kb    71
     O                       bdg4cd              81
     O                       l1trmdy        b    95 '  /  /  '
     O                       l1pdmdy        b   109 '  /  /  '
     O                       bdanc7        z    121
     O                       l1cunm             154
      *
      *-------------------------------------------------------------
      * Total line
      *-------------------------------------------------------------
      *
     O          e            t1tot       1  1
      *
     O              n87                          32 '--------------'
     O              n87                          51 '--------------'
     O              n87                          70 '--------------'
     O          e            t1tot          1
     O                       t1desc              14
     O                       artram(x)     k     33
     O                       arduam(x)     k     52
     O                       arbcam(x)     k     71
     O               87                          93 'Total Open Amount:'
     O               87      t1openam      k    109
      *
      /eject
      *-------------------------------------------------------------
      * No data message line
      *-------------------------------------------------------------
      *
     O          e            nodata      1
     O                                           19 'No data meets your'
     O                                           39 'selection criteria.'
      /eject
