/***************************************************************** */
/*                                                                 */
/*  PROGRAM NUMBER:  PBM5UPC                                       */
/*  PROGRAM NAME:    Print Production Packaging                    */
/*  PROGRAMMER:      Julius High                                   */
/*  CREATE DATE:     06/18/2014                                    */
/*                                                                 */
/*  FUNCTION:        This CLP  generates the Production            */
/*                   Packaging Report (PBM4PFR) and generates a    */
/*                   Excel Spreadsheet via SEQUEL Viewpoint that   */
/*                   is emailed.                                   */
/*                                                                 */
/*  OPTIONS:         1) PBMZPVR    - On demand from PMTRCD         */
/*                   2) INVNIGHTRN - Inv Post and Prod Sched - GUY */
/*                   2) INVNIGHTRS - Inv Post and Prod Sched - STJ */
/*                                                                 */
/*******************************************************************/
/*  Modification History                                           */
/*                                                                 */
/*  09/11/15 JJH     SEND THE PACKAGING DETAIL REPORT TO THE SAME  */
/*                   DISTRIBUTION LIST FOR THE PACKAGING SUMMARY   */
/*                                                                 */
/*  02/15/17 JBB     E9064 - ADD COMPANY 440 TO THIS PROGRAM.      */
/***************************************************************** */
             PGM        PARM(&PDEV &HOLD &SAVE &XCOPIES &COMPA +
                          &PRDDTFMA &PRDDTTOA &WHSE &EMAILADR)

/*-----------------------------------------------------------------*/
/*                      PARAMETER                                  */
/*-----------------------------------------------------------------*/
/* Print Options                                                   */
             DCL        VAR(&PDEV) TYPE(*CHAR) LEN(10)
             DCL        VAR(&HOLD) TYPE(*CHAR) LEN(4)
             DCL        VAR(&SAVE) TYPE(*CHAR) LEN(4)
             DCL        VAR(&XCOPIES) TYPE(*CHAR) LEN(2)

/* Function Specific                                               */
             DCL        VAR(&MDATE) TYPE(*CHAR) LEN(10)
             DCL        VAR(&XDATE) TYPE(*CHAR) LEN(7)

             DCL        VAR(&COMPA) TYPE(*CHAR) LEN(3)
             DCL        VAR(&PRDDTFMA) TYPE(*CHAR) LEN(7)
             DCL        VAR(&PRDDTTOA) TYPE(*CHAR) LEN(7)
             DCL        VAR(&WHSE) TYPE(*CHAR) LEN(3)
             DCL        VAR(&EMAILADR) TYPE(*CHAR) LEN(50)

             DCL        VAR(&DATA) TYPE(*CHAR) LEN(10) /* DATA +
                          FILES      */
             DCL        VAR(&DATAH) TYPE(*CHAR) LEN(10) /* HIST +
                          FILES      */

/*-----------------------------------------------------------------*/
/* Program Variables                                               */
             DCL        VAR(&COPIES) TYPE(*DEC) LEN(2 0)
             DCL        VAR(&COMPN) TYPE(*DEC) LEN(3 0)
             DCL        VAR(&PRDDTFM) TYPE(*DEC) LEN(7 0)
             DCL        VAR(&PRDDTTO) TYPE(*DEC) LEN(7 0)

             DCL        VAR(&DESC) TYPE(*CHAR) LEN(50)

             DCL        VAR(&USER) TYPE(*CHAR) LEN(10)
             DCL        VAR(&USRCLS) TYPE(*CHAR) LEN(10)

/* Setup to email the Production Packaging via Spread Sheet        */
             DCL        VAR(&TEXT) TYPE(*CHAR) LEN(50)
             DCL        VAR(&TEXTDTL) TYPE(*CHAR) LEN(50)

             IF         COND(&COMPA *EQ '360') THEN(DO)
             CHGVAR     VAR(&TEXT) VALUE('Production Packaging 360 - +
                          GP1')
             CHGVAR     VAR(&TEXTDTL) VALUE('Production Packaging +
                          Detail 360 - GP1')
             ENDDO

             IF         COND(&COMPA *EQ '440') THEN(DO)
             CHGVAR     VAR(&TEXT) VALUE('Production Packaging 440 - +
                          ST1')
             CHGVAR     VAR(&TEXTDTL) VALUE('Production Packaging +
                          Detail 440 - ST1')
             ENDDO

             IF         COND(&COMPA *EQ '960') THEN(DO)
             CHGVAR     VAR(&TEXT) VALUE('Production Packaging 960 - +
                          SJ1')
             CHGVAR     VAR(&TEXTDTL) VALUE('Production Packaging +
                          Detail 960 - SJ1')
             ENDDO

/* Retrieve Job Attributes                                           */
             RTVJOBA    USER(&USER)
             RTVUSRPRF  USRPRF(&USER) USRCLS(&USRCLS)

/*-----------------------------------------------------------------*/
/* Initialization                                                  */
             CHGVAR     VAR(&COMPN) VALUE(&COMPA)
             CHGVAR     VAR(&PRDDTFM) VALUE(&PRDDTFMA)
             CHGVAR     VAR(&PRDDTTO) VALUE(&PRDDTTOA)
             CHGVAR     VAR(&COPIES) VALUE(&XCOPIES)
/*           CHGVAR     VAR(&DATE) VALUE(&XDATE)                   */
/*           CVTDAT     DATE(&XDATE) TOVAR(&MDATE) FROMFMT(*CYMD) +*/
/*                        TOFMT(*MDYY) TOSEP(/)                    */
/*           CHGVAR     VAR(&DESC) VALUE('Make Sheet for Company' +*/
/*                        *BCAT &COMPA *BCAT 'for' *BCAT &MDATE)   */
/*                                                                 */
/*-----------------------------------------------------------------*/
/*                           MAIN                                  */
/*-----------------------------------------------------------------*/
/* Overide user print options                                      */

             OVRPRTF    FILE(PBM4PFR$) OUTQ(&PDEV) COPIES(&COPIES) +
                          HOLD(&HOLD) SAVE(&SAVE)

/*------------------------------------------------------------------*/
/* Retrieve Data Libraries from System Values                       */

             CALL      PGM(CAQXXFR) PARM(' ' &DATA &DATAH)

/*------------------------------------------------------------------*/
/* Create PS Prod Item Schedule Pkg (Work File) in QTEMP            */

             CHKOBJ     OBJ(QTEMP/PBBKCPP) OBJTYPE(*FILE)
             MONMSG     MSGID(CPF9801) EXEC(DO)
/*                            object not found                      */
             CRTDUPOBJ  OBJ(PBBKCPP) FROMLIB(&DATA) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)
             CRTDUPOBJ  OBJ(PBBKCPL0) FROMLIB(&DATA) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)
             CRTDUPOBJ  OBJ(PBBKCPL1) FROMLIB(&DATA) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)

             CLRPFM     FILE(QTEMP/PBBKCPP)
                          ENDDO

/********************************************************************/
/* PBM4PFR - Production Packaging Report creates PBBKCPP - PS Prod  */
/* Item Schedule Pkg workfile in QTEMP which is used to generate    */
/* Excel Spreadsheet with SEQUEL Viewpoint.                         */
/*                                                                  */
             CALL       PGM(PBM4PFR) PARM(' ' &COMPN &PRDDTFM +
                          &PRDDTTO &WHSE)

/********************************************************************/
/* Create Excel Spreadsheet with SEQUEL Viewpoint and email to      */
/* receipient(s).                                                   */
/* OVRDBF is required to allow EXECUTE VIEW to read file in QTEMP   */
/*                                                                  */
             OVRDBF     FILE(PBBKCPP) TOFILE(QTEMP/PBBKCPP)

             EXECUTE    VIEW(*LIBL/PRDPKGTB) PCFMT(*XLS) +
                          RECIPIENT(&EMAILADR) EMLMSG(&TEXT) +
                          TEXT(&TEXT)

             EXECUTE    VIEW(*LIBL/PRDPKGDTL) PCFMT(*XLS) +
                          RECIPIENT(&EMAILADR) +
                          EMLMSG(&TEXTDTL) TEXT(&TEXTDTL)

/* Clear workfile in QTEMP                                     */
             CLRPFM     FILE(QTEMP/PBBKCPP)

/*-----------------------------------------------------------------*/
/*                      PROGRAM END                                */
/*-----------------------------------------------------------------*/
 ENDPGM:
             DLTOVR     FILE(*ALL)
             ENDPGM
