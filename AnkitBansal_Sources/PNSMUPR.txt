/********************************************************************/
/*  SYSTEM    - ORDER MANAGEMENT                                    */
/*  PROGRAM   - PNSMUPR  BUYER PRICE SHEET FAX/EMAIL                */
/*  DATE      - 05/21/2010                                          */
/*                                                                  */
/*******************************************************************/
/* MODIFICATION LOG                                                */
/* DATE     PGMR CHANGE DESCRIPTION                                */
/* -------- ---- --------------------------------------------------*/
/* XX/XX/XX XXX  XXXXX...                                          */
/*******************************************************************/
 POD2UPC:    PGM        PARM(&BUYER &FAXN &EMAIL &FXEM &PDATE &ORG +
                          &FXSRVR)

/* ------------------- INPUT PARMS ----------------------------------*/
             DCL        VAR(&BUYER) TYPE(*CHAR) LEN(3)
             DCL        VAR(&FAXN) TYPE(*CHAR) LEN(25)
             DCL        VAR(&EMAIL) TYPE(*CHAR) LEN(50)
             DCL        VAR(&FXEM) TYPE(*CHAR) LEN(1)
             DCL        VAR(&PDATE) TYPE(*DEC) LEN(7 0)
             DCL        VAR(&ORG) TYPE(*CHAR) LEN(40)
             DCL        VAR(&FXSRVR) TYPE(*CHAR) LEN(40)

/* ------------------- PROGRAM VARIABLES ----------------------------*/
             DCL        VAR(&PDATEA) TYPE(*CHAR) LEN(7)
             DCL        VAR(&PDATES) TYPE(*CHAR) LEN(8)
             DCL        VAR(&SUBJECT) TYPE(*CHAR) LEN(60)
             DCL        VAR(&RECIPIENT) TYPE(*CHAR) LEN(50)

/* ------------------- CONVERT VARIABLES ----------------------------*/
             CHGVAR     VAR(&PDATEA) VALUE(&PDATE)
             CVTDAT     DATE(&PDATEA) TOVAR(&PDATES) FROMFMT(*CYMD) +
                          TOFMT(*MDY) TOSEP(/)
             CHGVAR     VAR(&SUBJECT) VALUE(&PDATES *BCAT &ORG *BCAT +
                          'Price Sheet')

/* ------------------- SEND PRICE SHEET ----------------------------*/
             IF         COND(&FXEM *EQ 'E') THEN(DO)
                 CHGVAR     VAR(&RECIPIENT) VALUE(&EMAIL)
                 EXECUTE    VIEW(PSBUYER) PCFMT(*XLS) +
                          RECIPIENT(&RECIPIENT) SETVAR((&BUYER +
                          &BUYER) (&PDATE &PDATEA)) TEXT(&SUBJECT)
             ENDDO

             IF         COND(&FXEM *EQ 'F') THEN(DO)
                 CHGVAR     VAR(&RECIPIENT) VALUE(&FAXN *TCAT &FXSRVR)
                 OVRPRTF    FILE(PNSOPFR$) DEV(FAXQ) OUTQ(*DEV)
                 CALL       PGM(PNSOPFR) PARM(' ' &PDATE &BUYER)
                 ESNDMAIL   RECIPIENT(&RECIPIENT) SUBJECT(&SUBJECT) +
                          MSG(*NONE) ATTLIST((* *PDF *N PNSOPFR$))
                 DLTSPLF    FILE(PNSOPFR$)
             ENDDO

             GOTO       CMDLBL(END)

/* ------------------- END PROGRAM       ----------------------------*/
 END:        ENDPGM
