/***************************************************************** */
/*                                                                 */
/*  PROGRAM NUMBER:    HP408CL                                     */
/*  PROGRAM NAME:      WORK WITH FEED DELIVERY ZONES               */
/*                       PRINT LISTINGS                            */
/*  PROGRAMMER:        LEANNER FEDOR                               */
/*  DATE:              06/16/97                                    */
/*                                                                 */
/*  PROGRAM PURPOSE:   PRINT LISTINGS FROM W/W FEED DELIVERY ZONES.*/
/*                                                                 */
/*                     TWO LISTINGS CAN BE PRINTED:                */
/*                       L=LISTING OF ZONES                        */
/*                       P=PRINT FARM SITES FOR A SINGLE ZONE      */
/*******************************************************************/

             PGM

/* DEFINE THE VARIABLE REQUIRED IN THE COMMAND TO OVERRIDE THE     */
/* PRINT FILE TO A LASER PRINTER                                   */

             DCL        VAR(&CMD) TYPE(*CHAR) LEN(512)

             DCL        VAR(&RPFL) TYPE(*CHAR) LEN(1)
             DCL        VAR(&HOLD) TYPE(*CHAR) LEN(10)
             DCL        VAR(&COPYS) TYPE(*DEC) LEN(2)
             DCL        VAR(&OUTQ) TYPE(*CHAR) LEN(10)

             CHGVAR     VAR(&RPFL) VALUE(%SST(*LDA 61 1))

/*------------------------------------------------------------------*/
/* SET OVERRIDES FOR PRINT FILES                                    */
/*------------------------------------------------------------------*/

 HOLD:       IF         COND(%SST(*LDA 411 1) = 'Y') THEN(CHGVAR +
                          VAR(&HOLD) VALUE(*YES))
             ELSE       CMD(CHGVAR VAR(&HOLD) VALUE(*NO))

 COPYS:      CHGVAR     VAR(&COPYS) VALUE(%SST(*LDA 412 1))
             IF         COND(&COPYS = 0) THEN(CHGVAR VAR(&COPYS) +
                          VALUE(1))

 OUTQ:       CHGVAR     VAR(&OUTQ) VALUE(%SST(*LDA 413 10))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(%SST(*LDA 401 10)))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(*JOB))

/*------------------------------------------------------------------*/
/* OVERRIDE PRINT FILE                                              */
/*------------------------------------------------------------------*/

/* USE THE CUSTOMIZED SEABOARD COMMAND TO OVERRIDE THE PRINT FILE.  */
/* THIS COMMAND ALLOWS THE REPORT TO BE GENERATED ON A STANDARD LINE*/
/* PRINTER OR A LASER PRINTER. TO USE THIS OVERRIDE YOU MUST DECLARE*/
/* A COMMAND VARIABLE AND FOLLOW THE OVERRIDE WITH A CALL TO        */
/* QCMDEXC.                                                         */

/* BOTH LISTINGS PRINT AT 10 CPI ON STANDARD PAPER.                 */

             SBDPRTOVR  CMDVAR(&CMD) FILE(QPRINT) OUTQ(&OUTQ) +
                          WIDTH(132) COPIES(&COPYS) HOLD(&HOLD)

             CALL       PGM(QCMDEXC) PARM(&CMD 512)

/*------------------------------------------------------------------*/
/*  LISTING OF ALL ZONES                                            */
/*------------------------------------------------------------------*/

             IF         COND(&RPFL *EQ 'L') THEN(DO)
             CALL       PGM(HP608)
             ENDDO

/*------------------------------------------------------------------*/
/*  PRINT ALL FARMS FOR A SINGLE ZONE                               */
/*------------------------------------------------------------------*/

             IF         COND(&RPFL *EQ 'P') THEN(DO)
             CALL       PGM(HP618)
             ENDDO

             DLTOVR     FILE(*ALL)

             ENDPGM

