 /***************************************************************** */
/*                                                                 */
/*  PROGRAM NUMBER:  PWASUPR                                       */
/*  PROGRAM NAME:    Email Delivery Schedule Reports               */
/*  PROGRAMMER:      Julius High                                   */
/*  CREATE DATE:     01/13/2017                                    */
/*                                                                 */
/*  FUNCTION:        This CLP function is used to Email a PDF      */
/*                   copy of the Delivery Schedule Report to each  */
/*                   Producer.  The Producer Distribution WRK file */
/*                   is built in QTEMP to list each report that is */
/*                   to sent.  Input may come from the Prt         */
/*                   Delivery Schedule PV or from the Web Applica- */
/*                   tion that generates the Delivery Sched Detail */
/*                   UPL file.                                     */
/*                                                                 */
/*******************************************************************/
/*  Modification History                                           */
/*  E14585 JJH 03/04/19 - Company Name in Email Subject            */
/*  Modification History                                           */
/*         - Add Input Parm &COMPA to PWG7XFR                      */
/*         - Add Input Parm &COMPA to PWG8XFR                      */
/*         - Add Input Parm &COMPA to PWFHXFR                      */
/*  T13559 DN  08/10/18 - Allow to build Delivery Schedule report  */
/*           by Producer/Producer Location level.                  */
/*         - Change process to build the Producer Loc Distrib Wrk  */
/*           file in order to email PDF/XLS report by Producer/    */
/*           Producer Location level.                              */
/*         - Added parm &PRODLOC to call of PWAOXFR.               */
/*         - Added new process PWG7XFR & PWG8XFR to print report   */
/*           by Producer/Producer Location level.                  */
/*  E12120 JJH 02/27/18 - Add Producer Location Logic              */
/*         - Change Parameters to use Prt Delv Sched CL Parms      */
/*         - Add new input parm: PDW Producer Loc Code             */
/*         - Add logic to process Delivery Schedule Report and     */
/*           for Producer Location.                                */
/*  E12120 JJH 11/28/17 - Email with PDF or XLS                    */
/*******************************************************************/
             PGM        PARM(&PDWINPUT &COMPA &PRODUCER &PRODLOC +
                          &KILLDTFMA &KILLDTTOA &SRTYPE &SOURCE +
                          &FROMLIB &EMAILPROD)
/*                                                                 */
/*-----------------------------------------------------------------*/
/*                      PARAMETER                                  */
/*-----------------------------------------------------------------*/
/* Function Specific                                               */
             DCL        VAR(&PDWINPUT) TYPE(*CHAR) LEN(1)
             DCL        VAR(&COMPA) TYPE(*CHAR) LEN(3)
             DCL        VAR(&PRODUCER) TYPE(*CHAR) LEN(6)
             DCL        VAR(&PRODLOC) TYPE(*CHAR) LEN(6)
             DCL        VAR(&KILLDTFMA) TYPE(*CHAR) LEN(7)
             DCL        VAR(&KILLDTTOA) TYPE(*CHAR) LEN(7)
             DCL        VAR(&SRTYPE) TYPE(*CHAR) LEN(1)
             DCL        VAR(&SOURCE) TYPE(*CHAR) LEN(3)
             DCL        VAR(&FROMLIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&EMAILPROD) TYPE(*CHAR) LEN(1)

/*-----------------------------------------------------------------*/
/* Program Variables                                               */
             DCL        VAR(&COMPN) TYPE(*DEC) LEN(3 0)
             DCL        VAR(&KILLDTFM) TYPE(*DEC) LEN(7 0)
             DCL        VAR(&KILLDTTO) TYPE(*DEC) LEN(7 0)
/*                                                                 */
/*-----------------------------------------------------------------*/
/* Initialization                                                  */
             CHGVAR     VAR(&COMPN) VALUE(&COMPA)
             CHGVAR     VAR(&KILLDTFM) VALUE(&KILLDTFMA)
             CHGVAR     VAR(&KILLDTTO) VALUE(&KILLDTTOA)
/*                                                                 */
/*-----------------------------------------------------------------*/
/*                      MAIN                                       */
/*-----------------------------------------------------------------*/
/* Create Producer Distribution WRK (Work File) in QTEMP           */
             CHKOBJ     OBJ(QTEMP/PWABCPP) OBJTYPE(*FILE)
             MONMSG     MSGID(CPF9801) EXEC(DO)
/*                         object not found                        */
/*                                                                 */
             CRTDUPOBJ  OBJ(PWABCPP) FROMLIB(&FROMLIB) +
                          OBJTYPE(*FILE) TOLIB(QTEMP)
             CRTDUPOBJ  OBJ(PWABCPL0) FROMLIB(&FROMLIB) +
                          OBJTYPE(*FILE) TOLIB(QTEMP)
             CRTDUPOBJ  OBJ(PWABCPL1) FROMLIB(&FROMLIB) +
                          OBJTYPE(*FILE) TOLIB(QTEMP)
             ENDDO

             CLRPFM     FILE(QTEMP/PWABCPP)

             OVRDBF     FILE(PWABCPP)  TOFILE(QTEMP/PWABCPP)
             OVRDBF     FILE(PWABCPL0) TOFILE(QTEMP/PWABCPL0)
             OVRDBF     FILE(PWABCPL1) TOFILE(QTEMP/PWABCPL1)
/*-----------------------------------------------------------------*/
/* Create Producer Loc Distrib WRK (Work File) in QTEMP            */
             CHKOBJ     OBJ(QTEMP/PWAMCPP) OBJTYPE(*FILE)
             MONMSG     MSGID(CPF9801) EXEC(DO)
/*                         object not found                        */
/*                                                                 */
             CRTDUPOBJ  OBJ(PWAMCPP) FROMLIB(&FROMLIB) +
                          OBJTYPE(*FILE) TOLIB(QTEMP)
             CRTDUPOBJ  OBJ(PWAMCPL0) FROMLIB(&FROMLIB) +
                          OBJTYPE(*FILE) TOLIB(QTEMP)
             CRTDUPOBJ  OBJ(PWAMCPL1) FROMLIB(&FROMLIB) +
                          OBJTYPE(*FILE) TOLIB(QTEMP)
             ENDDO

             CLRPFM     FILE(QTEMP/PWAMCPP)

             OVRDBF     FILE(PWAMCPP)  TOFILE(QTEMP/PWAMCPP)
             OVRDBF     FILE(PWAMCPL0) TOFILE(QTEMP/PWAMCPL0)
             OVRDBF     FILE(PWAMCPL1) TOFILE(QTEMP/PWAMCPL1)
/*-----------------------------------------------------------------*/
/* Build Production Distribution WRK workfile in QTEMP which is    */
/* used to Print Delivery Schedule Report and sent via Email.      */
/*                                                                 */
/* T13559 DN 08/10/18 - Added Producer Location (&PRODLOC) as parm */
/*        in order to build the Producer Loc Distrib Wrk file in   */
/*        QTEMP.                                                   */

             IF         COND(&PDWINPUT = 'P') THEN(DO)
             CALL       PGM(PWAOXFR) PARM('' &PDWINPUT &COMPN +
                          &PRODUCER &KILLDTFM &KILLDTTO &SRTYPE +
                          &SOURCE &PRODLOC)
             ENDDO

/*-----------------------------------------------------------------*/
/* Create/Send Email Report as PDF by Producer/Producer Location.  */
             IF         COND(&EMAILPROD = 'P') THEN(DO)

/* T13559 DN 08/20/18 - Added process to print by Producer/Producer Loc */
             IF         COND(&PRODLOC *EQ ' ') THEN(DO)
      /* Print by Producer */
             CALL       PGM(PWA7XFR) PARM('' &COMPA)
      /* Print by Producer Location */
             CALL       PGM(PWG7XFR) PARM('' &COMPA)
             ENDDO
             ELSE       CMD(DO)
      /* Print by Producer Location */
             CALL       PGM(PWG7XFR) PARM('' &COMPA)
             ENDDO

             ENDDO

/*-----------------------------------------------------------------*/
/* Create Delivery Schedule Rpt Wk (Work File) in QTEMP for XLS    */
             IF         COND(&EMAILPROD = 'X') THEN(DO)
             CHKOBJ     OBJ(QTEMP/PWAJCPP) OBJTYPE(*FILE)
             MONMSG     MSGID(CPF9801) EXEC(DO)
/*                         object not found                        */
/*                                                                 */
             CRTDUPOBJ  OBJ(PWAJCPP) FROMLIB(&FROMLIB) +
                          OBJTYPE(*FILE) TOLIB(QTEMP)
             CRTDUPOBJ  OBJ(PWAJCPL0) FROMLIB(&FROMLIB) +
                          OBJTYPE(*FILE) TOLIB(QTEMP)
             CRTDUPOBJ  OBJ(PWAJCPL1) FROMLIB(&FROMLIB) +
                          OBJTYPE(*FILE) TOLIB(QTEMP)
             ENDDO

             OVRDBF     FILE(PWAJCPP) TOFILE(QTEMP/PWAJCPP)
             OVRDBF     FILE(PWAJCPL0) TOFILE(QTEMP/PWAJCPL0)
             OVRDBF     FILE(PWAJCPL1) TOFILE(QTEMP/PWAJCPL1)

             CLRPFM     FILE(QTEMP/PWAJCPP)

/* T13559 DN 08/22/18 - Added process to print by Producer/Producer Loc */
             IF         COND(&PRODLOC *EQ ' ') THEN(DO)
      /* Print by Producer */
             CALL       PGM(PWFHXFR) PARM('' &COMPA)
      /* Print by Producer Location */
             CALL       PGM(PWG8XFR) PARM('' &COMPA)
             ENDDO
             ELSE       CMD(DO)
      /* Print by Producer Location */
             CALL       PGM(PWG8XFR) PARM('' &COMPA)
             ENDDO

             ENDDO

/*-----------------------------------------------------------------*/
/*                      PROGRAM END                                */
/*-----------------------------------------------------------------*/
  ENDPGM:
             DLTOVR     FILE(*ALL)
             ENDPGM
