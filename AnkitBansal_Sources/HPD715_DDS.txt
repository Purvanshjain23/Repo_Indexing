     A*%%TS  SD  19950302  125640  ISLRAMS     REL-V3R0M5  5763-PW1
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION
     A*  DISPLAY FILE:  HPD715
     A*  TITLE:         VIEW AUTHORIZED USERS OF A FUNCTION
     A*  CREATED BY:    LEANNE FEDOR
     A*  DATE:          02/10/95
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A                                      CF05(05 'REFRESH')
     A                                      CA12(12 'PREVIOUS')
     A*
     A*--------------------------------------------------------------*
     A* SUBFILE FORMAT ONE
     A*--------------------------------------------------------------*
     A          R HP715S1                   SFL
     A*%%TS  SD  19950302  125640  ISLRAMS     REL-V3R0M5  5763-PW1
     A  85                                  SFLNXTCHG
     A            HDRRN1         4Y 0H
     A            DFUSCD    R        O  9  2REFFLD(AUUSCD)
     A            DFSLCD    R        O  9 14REFFLD(AUSLCD)
     A            DFRUFL    R        O  9 22REFFLD(AURUFL)
     A            DFMDFL    R        O  9 29REFFLD(AUMDFL)
     A            DFRMFL    R        O  9 37REFFLD(AURMFL)
     A            DFRVFL    R        O  9 42REFFLD(AURVFL)
     A            DFAFFL    R        O  9 50REFFLD(AUAFFL)
     A            DFUSNM        22   O  9 58
     A*
     A*--------------------------------------------------------------*
     A* SUBFILE CONTROL FORMAT ONE
     A*--------------------------------------------------------------*
     A          R HP715C1                   SFLCTL(HP715S1)
     A*%%TS  SD  19950302  125640  ISLRAMS     REL-V3R0M5  5763-PW1
     A                                      SFLSIZ(0050)
     A                                      SFLPAG(0013)
     A N82                                  ROLLUP(25 'PAGE FORWARD')
     A                                      OVERLAY
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A            HDPAG1         4S 0H      SFLRCDNBR
     A                                  1  2'HP715'
     A                                      DSPATR(HI)
     A                                  1 26'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  2 19'VIEW AUTHORIZED USERS FOR A FUNCTI-
     A                                      ON'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  4  2'Function ..:'
     A            C1FNCD    R        O  4 16REFFLD(FNFNCD)
     A            C1FNDS    R        O  4 28REFFLD(FNFNDS)
     A                                  6  2'User'
     A                                      DSPATR(HI)
     A                                  6 14'Secur'
     A                                      DSPATR(HI)
     A                                  6 21'Rstr'
     A                                      DSPATR(HI)
     A                                  6 27'Modify'
     A                                      DSPATR(HI)
     A                                  6 35'Restricted'
     A                                      DSPATR(HI)
     A                                  6 47'Auth to'
     A                                      DSPATR(HI)
     A                                  7  2'Code'
     A                                      DSPATR(HI)
     A                                  7 14'Loc'
     A                                      DSPATR(HI)
     A                                  7 21'User'
     A                                      DSPATR(HI)
     A                                  7 27'Data'
     A                                      DSPATR(HI)
     A                                  7 35'Maint'
     A                                      DSPATR(HI)
     A                                  7 41'View'
     A                                      DSPATR(HI)
     A                                  7 47'Functions'
     A                                      DSPATR(HI)
     A                                  7 58'User Name'
     A                                      DSPATR(HI)
     A            C1USCD    R        B  8  2REFFLD(AUUSCD)
     A                                      DSPATR(HI)
     A*
     A*--------------------------------------------------------------*
     A* COMMAND KEY RECORD 1
     A*--------------------------------------------------------------*
     A          R HP715K1
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A                                 23 11'F5=Refresh'
     A                                 23 23'F12=Previous'
      *
      *--------------------------------------------------------------*
      * ERROR MESSAGE SUBFILE FORMAT
      *--------------------------------------------------------------*
     A          R HP715ES                   SFL SFLMSGRCD(24)
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *                                                                *********
      *--------------------------------------------------------------*
      * ERROR MESSAGE SUBFILE CONTROL FORMAT
      *--------------------------------------------------------------*
     A          R HP715EC                   SFLCTL(HP715ES)
      *
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
