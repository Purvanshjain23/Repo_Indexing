     A*%%TS  SD  20090813  111333  ISLRAMS     REL-V5R4M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD754
     A*  TITLE:         VIEW FARMS WITH NO COST CENTER
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  DATE:          12/13/01
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A                                      CF04(04 'PROMPT')
     A                                      CF05(05 'REFRESH')
     A*
     A*
     A*---------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP754S1                   SFL
     A*%%TS  SD  20090813  111333  ISLRAMS     REL-V5R4M0  5722-WDS
     A  85                                  SFLNXTCHG
     A            HDRRN1         4Y 0H
     A            D1FSCD    R        O  8  3REFFLD(@@FSCD)
     A                                      EDTCDE(M)
     A            D1FSNM    R        O  8 10REFFLD(FSREC/FSFSNM *LIBL/HSP018)
     A            D1AIST    R        O  8 37REFFLD(HSRREF/@@AIST *LIBL/HSPREF)
     A            D1CELL    R        O  8 41REFFLD(FSREC/FSCELL *LIBL/HSP018)
     A                                      EDTCDE(M)
     A            D1PTCD    R        O  8 46REFFLD(FSPTCD)
     A            D1PPCD    R        O  8 53REFFLD(FSPPCD)
     A            D1CMGCD   R        O  8 60REFFLD(FSREC/FSCMGCD *LIBL/HSP018)
     A            D1PMGCD   R        O  8 67REFFLD(FSREC/FSPMGCD *LIBL/HSP018)
     A            D1MMGCD   R        O  8 74REFFLD(FSREC/FSMMGCD *LIBL/HSP018)
     A*
     A*
     A*
     A*---------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP754C1                   SFLCTL(HP754S1)
     A*%%TS  SD  20090813  111333  ISLRAMS     REL-V5R4M0  5722-WDS
     A                                      SFLSIZ(0050)
     A                                      SFLPAG(0014)
     A N82                                  ROLLUP(25 'PAGE FORWARD')
     A                                      SETOF(53)
     A                                      SETOF(54)
     A                                      SETOF(55)
     A                                      SETOF(56)
     A                                      SETOF(57)
     A                                      RTNCSRLOC(&RCD &FLD)
     A                                      OVERLAY
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 28'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  2 24'VIEW FARMS WITH NO COST CENTER'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  4 48'?'
     A                                      DSPATR(HI)
     A                                  4 55'?'
     A                                      DSPATR(HI)
     A                                  4 62'?'
     A                                      DSPATR(HI)
     A                                  4 69'?'
     A                                      DSPATR(HI)
     A                                  4 76'?'
     A                                      DSPATR(HI)
     A                                  5 36'A/I'
     A                                      DSPATR(HI)
     A                                  5 46'-Production-'
     A                                      DSPATR(HI)
     A                                  5 60'----- Managers ----'
     A                                      DSPATR(HI)
     A                                  6  4'Farm'
     A                                      DSPATR(HI)
     A                                  6 10'Name'
     A                                      DSPATR(HI)
     A                                  6 37'St'
     A                                      DSPATR(HI)
     A                                  6 40'Cell'
     A                                      DSPATR(HI)
     A                                  6 46'Type'
     A                                      DSPATR(HI)
     A                                  6 53'Phase'
     A                                      DSPATR(HI)
     A                                  6 60'Cell'
     A                                      DSPATR(HI)
     A                                  6 67'Prod'
     A                                      DSPATR(HI)
     A                                  6 74'Multi'
     A                                      DSPATR(HI)
     A            C1FSCD    R        B  7  3REFFLD(@@FSCD)
     A                                      DSPATR(HI)
     A                                      EDTCDE(M)
     A            C1CELL    R        B  7 41REFFLD(HSRREF/@@CELL *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A            C1PTCD    R        B  7 46REFFLD(FSPTCD)
     A                                      DSPATR(HI)
     A  53                                  DSPATR(PC)
     A  99                                  DSPATR(PR)
     A            C1PPCD    R        B  7 53REFFLD(FSPPCD)
     A                                      DSPATR(HI)
     A  54                                  DSPATR(PC)
     A  99                                  DSPATR(PR)
     A            C1CMGCD   R        B  7 60REFFLD(FSREC/FSCMGCD *LIBL/HSP018)
     A                                      DSPATR(HI)
     A  56                                  DSPATR(PC)
     A  99                                  DSPATR(PR)
     A            C1PMGCD   R        B  7 67REFFLD(FSREC/FSPMGCD *LIBL/HSP018)
     A                                      DSPATR(HI)
     A  55                                  DSPATR(PC)
     A  99                                  DSPATR(PR)
     A            C1MMGCD   R        B  7 74REFFLD(FSREC/FSMMGCD *LIBL/HSP018)
     A                                      DSPATR(HI)
     A  57                                  DSPATR(PC)
     A  99                                  DSPATR(PR)
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R HP754K1
     A*%%TS  SD  20000921  074322  ISLRAMS     REL-V4R4M0  5769-PW1
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A                                 23 11'F4=Prompt'
     A                                 23 22'F5=Refresh'
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *---------------------------------------------------------------
     A          R HP754ES                   SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE CONTROL FORMAT
      *---------------------------------------------------------------
     A          R HP754EC                   SFLCTL(HP754ES)
      *
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
