     A*%%TS  SD  20131021  131802  ISLRAMS     REL-V6R1M0  5761-WDS
     A*****************************************************************
     A*
     A*  SYSTEM:        HOG PRODUCTION
     A*  DISPLAY FILE:  HPD5003 - SELECT FARM SITE WITH FARM SITE POSITIONER
     A*  CREATED BY:    LEANNE FEDOR
     A*  DATE:          01/06/97
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3                  -
     A                                             27 132 *DS4)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA12(12 'PREVIOUS')
     A N82                                  ROLLUP(25 'PAGE FORWARD')
     A*--------------------------------------------------------------*
     A* REFERENCE WINDOW RECORD FORMAT 1
     A*--------------------------------------------------------------*
     A          R HP5003R1
     A*%%TS  SD  19970106  155508  ISLRAMS     REL-V3R6M0  5716-PW1
     A                                      TEXT('Reference window')
     A                                      WINDOW(*DFT 17 65)
     A                                      WDWBORDER((*DSPATR RI) (*CHAR '    -
     A                                          '))
     A*--------------------------------------------------------------*
     A* RECORD FORMAT ONE
     A*--------------------------------------------------------------*
     A          R HP5003R2
     A                                      TEXT('Written in RPG pgm to prevent-
     A                                       screen from clearing')
     A                                      CLRL(*NO)
     A                                      OVERLAY
     A*--------------------------------------------------------------*
     A* RECORD FORMAT TWO
     A*--------------------------------------------------------------*
     A* THIS FORMAT IS NOT WRITTEN IN THE RPG PGM.  IT PREVENTS THE
     A* CALLING_PROGRAM_SCREEN FROM BEING CLEARED.
     A*--------------------------------------------------------------*
     A          R HP5003R3
     A                                      TEXT('Dummy format not written in R-
     A                                      PG pgm.')
     A                                      ASSUME
     A                                 11  1' '
     A*--------------------------------------------------------------*
     A* SUBFILE FORMAT ONE
     A*--------------------------------------------------------------
     A          R HP5003S1                  SFL
     A*%%TS  SD  20131021  131250  ISLRAMS     REL-V6R1M0  5761-WDS
     A                                      TEXT('subfile record format one')
     A            HDRRN1         4S 0H
     A            DFFSCD    R        O  7  1REFFLD(HSRREF/HGFSCD *LIBL/HSPREF)
     A                                      EDTCDE(M)
     A            DFFSNM    R        O  7 10REFFLD(HSRREF/FSFSNM *LIBL/HSPREF)
     A            DFCELL    R        O  7 38REFFLD(HSRREF/@@CELL *LIBL/HSPREF)
     A                                      EDTCDE(Z)
     A            DFMTECHREFR        O  7 44REFFLD(HSRREF/@@MTECHREF +
     A                                             *LIBL/HSPREF)
     A*--------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*--------------------------------------------------------------
     A          R HP5003C1                  SFLCTL(HP5003S1)
     A*%%TS  SD  20131021  131802  ISLRAMS     REL-V6R1M0  5761-WDS
     A  *DS3                                SFLSIZ(0050)
     A  *DS4                                SFLSIZ(0050)
     A  *DS3                                SFLPAG(0008)
     A  *DS4                                SFLPAG(0008)
     A  *DS3                                WINDOW(HP5003R1)
     A  *DS4                                WINDOW(HP5003R1)
     A                                      TEXT('control format one')
     A                                      OVERLAY
     A                                      SFLCSRRRN(&HDRRN)
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND(*MORE)
     A                                      RMVWDW
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            HDRRN          5S 0H
     A                                  1 26'SELECT FARM SITE'
     A                                      DSPATR(HI)
     A                                  2  1'Position Cursor, Press Enter'
     A                                  4  1'Farm'
     A                                      DSPATR(HI)
     A                                  5  1'Site'
     A                                      DSPATR(HI)
     A                                  5 10'Name'
     A                                      DSPATR(HI)
     A                                  5 37'Cell'
     A                                      DSPATR(HI)
     A                                  5 44'MTech Reference'
     A                                      DSPATR(HI)
     A            C1FSCD    R        B  6  1REFFLD(HSRREF/HGFSCD *LIBL/HSPREF)
     A                                      EDTCDE(M)
     A                                      DSPATR(HI)
     A            C1CELL    R        B  6 38REFFLD(HSRREF/@@CELL *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A                                      EDTCDE(M)
     A            C1MTECHREFR        B  6 44REFFLD(HSRREF/@@MTECHREF *LIBL/HSPR-
     A                                      EF)
     A                                      DSPATR(HI)
     A          R HP5003K1
     A*%%TS  SD  19940511  110146  ISRCENT     REL-V2R2M0  5738-PW1
     A                                      OVERLAY
     A                                      WINDOW(HP5003R1)
     A                                 16  1'F12=Previous'
