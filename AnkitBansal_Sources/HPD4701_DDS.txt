     A*%%TS  SD  20040414  081337  ISLRAMS     REL-V5R2M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD4701
     A*  TITLE:         WORK WITH BUDGET ITEMS
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  DATE:          02/09/04
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A                                      CF04(04 'PROMPT')
     A                                      CF05(05 'REFRESH')
     A                                      CF07(07 'LISTING')
     A                                      CF10(10 'VIEW TEMPLATES')
     A                                      CF14(14 'VIEW BUDGETS')
     A                                      CF17(17 'PRINT OPTIONS')
     A                                      CF20(20 'WRKSPLF')
     A*
     A*-------------------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*-------------------------------------------------------------------------
     A          R HP4701S1                  SFL
     A*%%TS  SD  20040414  081337  ISLRAMS     REL-V5R2M0  5722-WDS
     A  85                                  SFLNXTCHG
     A            HDRRN1    R        H      REFFLD(@@RRN)
     A            D1OPT     R        B 13  2REFFLD(@@OPT)
     A                                      CHGINPDFT(UL)
     A  30                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A N30                                  DSPATR(HI)
     A            D1BGIT    R        O 13  6REFFLD(@@BGIT)
     A                                      COLOR(PNK)
     A            D1DESC        38   O 13 33COLOR(PNK)
     A            D1ITTYCD  R        O 13 73REFFLD(HSRREF/@@ITTYCD *LIBL/HSPREF)
     A                                      COLOR(PNK)
     A                                 14 48'uom:'
     A                                      COLOR(WHT)
     A            D1UOM     R        O 14 53REFFLD(HSRREF/@@UOM *LIBL/HSPREF)
     A                                 14 63'uow:'
     A                                      COLOR(WHT)
     A            D1UOW     R        O 14 68REFFLD(HSRREF/@@UOW *LIBL/HSPREF)
     A*
     A*-------------------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*-------------------------------------------------------------------------
     A          R HP4701C1                  SFLCTL(HP4701S1)
     A*%%TS  SD  20040413  104444  ISLRAMS     REL-V5R2M0  5722-WDS
     A                                      SFLSIZ(0050)
     A                                      SFLPAG(0004)
     A N82                                  ROLLUP(25 'PAGE FORWARD')
     A                                      SETOF(30)
     A                                      SETOF(54)
     A                                      RTNCSRLOC(&RCD &FLD)
     A                                      OVERLAY
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A            HDPAG1    R        H      REFFLD(@@PAG)
     A                                      SFLRCDNBR
     A            RCD       R        H      REFFLD(@@RCD)
     A            FLD       R        H      REFFLD(@@FLD)
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 28'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USR)
     A                                      DSPATR(HI)
     A                                  2 27'WORK WITH BUDGET ITEMS'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@JOB)
     A                                      DSPATR(HI)
     A                                  3  3'5=View Items'
     A                                  4  3'7=Remove Budget Item from Template-
     A                                      s where Budget Quantity is Zero'
     A                                  5  3'8=Remove Budget Item from All Temp-
     A                                      lates'
     A                                  6  3'9=Maintain Budget Item on Template-
     A                                      s'
     A                                  7  2'10=View Budget Item on Templates'
     A                                  8  2'11=Maintain Budget Item on Inactiv-
     A                                      e/Active Farm Budgets'
     A                                  9  2'12=View Budget Item on Inactive/Ac-
     A                                      tive Farm Budgets'
     A                                  9 74'?'
     A                                      DSPATR(HI)
     A                                 10 73'Item'
     A                                      DSPATR(HI)
     A                                 11  2'Op'
     A                                      DSPATR(HI)
     A                                 11  6'Budget Item'
     A                                      DSPATR(HI)
     A                                 11 33'Description'
     A                                      DSPATR(HI)
     A                                 11 73'Type'
     A                                      DSPATR(HI)
     A            C1BGIT    R        B 12  6REFFLD(HSRREF/@@BGIT *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A            C1DESC        38A  B 12 33DSPATR(HI)
     A            C1ITTYCD  R        B 12 73REFFLD(HSRREF/@@ITTYCD *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A  54                                  DSPATR(PC)
     A*
     A*-------------------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*-------------------------------------------------------------------------
     A          R HP4701K1
     A*%%TS  SD  20040310  165051  ISLRAMS     REL-V5R2M0  5722-WDS
     A  43                             21  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 22  2'F3=Exit'
     A                                 22 11'F4=Prompt'
     A                                 22 22'F5=Refresh'
     A                                 22 34'F7=List'
     A                                 22 43'F10=View All Budget Items/Template-
     A                                      s'
     A                                 23  2'F14=View All Budget Items/Farm Bud-
     A                                      gets'
     A                                 23 43'F17-Print Options'
     A                                 23 63'F20=Spool Files'
      *
     A*-------------------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
     A*-------------------------------------------------------------------------
     A          R HP4701ES                  SFL SFLMSGRCD(24)
      *
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
     A*-------------------------------------------------------------------------
      * ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*-------------------------------------------------------------------------
     A          R HP4701EC                  SFLCTL(HP4701ES)
      *
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
