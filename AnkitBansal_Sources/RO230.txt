     h option(*SRCSTMT:*NODEBUGIO)
      *
      * ENVIRONMENT: Pork Division
      * SYSTEM:      Resource Optimization
      * PROGRAM:     RO230
      * TITLE:       Byproduct--Pump Start Weight Exceptions  (BP/FG only)
      * PROGRAMMER:  Alice Brownfield
      * CREATED:     06/11/08
      *
      * FUNCTION:    This program processes all the BP records in the Byproduct Detail
      *              file to find the ones that have a Pump % NOT = 0.  We need to
      *              create Product Exception records in TFP019 to get Produced
      *              weights adjusted back to their "START" weights.
      *
      ***************************************************************************************
      * MODIFICATIONS:
      ***************************************************************************************
      * DATE      PROGRAMMER
      *
      * 09/11/08  LeAnne Ramsey
      *           Recompile only. Export/Domestic Flag was added to TFP010-
      *           Weekly Product Revenue Detail file.
      *
      * 11/20/08  LeAnne Ramsey
      *           As a prep for synchronizing the TFS and Meat Cost LDAs, we
      *           removed the unused LDA fields.
      *
      * 11/21/08  LeAnne Ramsey
      *           As part of synchronizing the LDAs between the TFS Margin Adjustment Close
      *           and the Meat Costing, we changed the LDA positions.
      *
      * 04/02/09  LeAnne Ramsey
      *           Recompile only. Field 'Producing Company' added to Byproduct Detail.
      *
      * 11/14/17  Danny Nguyen  (R12011A-Weekly Product Revenue)
      *           Recompile only. Added 25 STF fields to TFP010 file.
      *
      * 05/17/22  Danny Nguyen  (DO2484 - WI 479 STF Variance Reporting)
      *           Recompile only. Added 2 STF fields to TFP010 file.
      *           PRXYPC   - STF STD YIELD %
      *           PRXPMPPC - STF PUMP %
      *
      * W105621 RMC 10/10/2022 INCREASE FIELD SIZES TO HANDLE PRICES > 999.99
      *         CHANGE @@ANPRPR TO SIZE 10.6
      *           CHANGES PRANPRPR IN TFP010
      *
      *
      /EJECT
      ***************************************************************************************
      * FILE SPECIFICATION
      ***************************************************************************************
      *
     frop122    if   e           k disk
      *  Byproduct detail
      *
      *
     ftfp010    if   e           k disk
      *  Weekly product revenue detail
      *
      *
     ftfp019    o    e           k disk
      *  TF Product exception
      *
      /eject
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      ****************************************************************
      * ARRAYS AND TABLES
      ****************************************************************
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Parms
     D xxsn            s              7  0
     D xxpfcd          s              1
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *---------------------------------------------------------------
      * Local data area
      *---------------------------------------------------------------
      *
     Dlda             uds                  dtaara(*lda)
     D  ldpfcd                 1      1
     D  ldyr                   2      5  0
     D  ldwk                   6      7  0
      *
     D  ldwedt                29     36  0
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      **********************************************************************************
      * Mainline
      **********************************************************************************
      *
      * Read each record in the Byproduct Detail file for this Week.
      * Only process the records where the Pump % is NOT zero.
      * Write a record to the Product Exception file.
      *
     C     ldwedt        setll     rop122
      *
     C                   dou       *in90 = *on                                  Main do
     C     ldwedt        reade     rop122                                 90
     C                   if        *in90 = *off and ydpumppc <> 0               If process
     C                                          and ydtfcgcd = 'BP'
     C                   exsr      $wrt019
     C                   endif                                                  If process
     C                   enddo                                                  Main do
      *
      * EOF processing
     C                   seton                                        lr
      /eject
      *-----------------------------------------------------------------------------------
      * Write a record to the Product Exception file for the weight difference
      *-----------------------------------------------------------------------------------
      *
     C     $wrt019       begsr
      *
      * Calc Exception Pounds as:
      *   Start Weight - Produced Pounds
      *
     C     ydspuslb      sub       ydspulb       pesexpulb
     C     ydtpuslb      sub       ydtpulb       petexpulb
      *
      * If your Exception Pounds are not zero, write an Exception record.
      *
     C                   if        pesexpulb <> 0 or petexpulb <> 0             If pounds
     C                   z-add     ydwedt        pewedt
     C                   move      ydtfclcd      petfclcd
     C                   move      ydtfcgcd      petfcgcd
     C                   move      ydcgcd        pecgcd
     C                   move      ydsgcd        pesgcd
     C                   move      ydmixfl       pemixfl
     C                   move      ydmixgrp      pemixgrp
     C                   z-add     yditcd        peprcd
     C                   movel     ydittycd      peitycd
     C                   z-add     ydistycd      peistycd
     C                   z-add     ydisgrcd      peisgrcd
     C                   z-add     ydisclcd      peisclcd
     C                   move      ydisclds      peisclds
      *
      * Fields not in Byproduct Detail file
      *
     C                   z-add     ldyr          peyr
     C                   z-add     ldwk          pewk
      * Hardcoded values
     C                   move      'Y'           pesysfl
     C                   eval      pepgm = sdpgm
     C                   eval      peexcom = 'Prime BP to Start Wgt'
      *
      * Get a Product Exception Number  (system-assigned)
      *
     C                   call      'TF810'
     C                   parm      ldpfcd        xxpfcd
     C     pepesn        parm      0             xxsn
      *
      * Retrieve the "Include in Volume Flag"
      *
     C                   exsr      $volfl
      *
      * Write Product Exception record
      *
     c                   write     perec
     C                   clear                   perec
      *
      * Call the program to write the Product Exception record to the
      * ByProduct Detail file.
      *
     C                   call      'RO220'
     C                   parm                    xxsn
      *
     C                   endif                                                  If pounds
      *
     C                   endsr
      /eject
      *-----------------------------------------------------------------------------------
      * Retrieve the "Include in Volume" flag from the Weekly Produce Revenue Detail file
      *-----------------------------------------------------------------------------------
      *
     C     $volfl        begsr
      *
      * Key values:
      *  Product Code
      *  WeekEnding Date
      *
     C     key01         chain     tfp010                             92
     C                   if        *in92 = *off
     C                   move      prvolfl       pevolfl
     C                   else
     C                   move      'N'           pevolfl
     C                   endif
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Initialization
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Key lists
      *
     C     key01         klist
     C                   kfld                    peprcd
     C                   kfld                    pewedt
      *
     C                   endsr
      /eject
