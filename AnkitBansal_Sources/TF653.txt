      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Triumph Foods
      * PROGRAM:     TF653
      * TITLE:       Inventory Costs Listing
      * PROGRAMMER:  LeAnne Ramsey
      * CREATED:     03/30/07
      *
      * FUNCTION:  Batch program to print a record(s) in the Inventory Costs file.
      *
      *
      ***********************************************************************************
      * MODIFICATIONS:
      ***********************************************************************************
      * DATE      PROGRAMMER
      *
      * 12/10/07  LeAnne Ramsey
      *           New field (Freezer Interest Rate %) added to file and listing.
      *
      * 08/15/08  LeAnne Ramsey
      *           Users are no longer doing Holding/Inflow calcs.
      *           Holding Cost/Lb and Inflow Cost/Lb were removed from file and listing.
      /EJECT
      ***********************************************************************************
      * FILE SPECIFICATIONS
      ***********************************************************************************
      *
     Ftfp053    if   e           k disk
      *    Inventory costs      (records selected by open query)
      *
      *
     Fprint132  o    f  132        printer oflind(*inof)
      *
      /EJECT
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Standard fields
      *
     D dash            s            132    inz(*all'-')
     d rtime           s              6  0
      *
      *
      * Control fields
      *
     D first           s              1    inz('Y')
      *
      *
      * Workfields for date manipulation
      *
     D wkisodate       s               D   datfmt(*iso)
      *
      *
      * Print fields
      *
     D l1wemdy         s              6  0
      *
      ****************************************************************
      * ARRAYS AND TABLES
      ****************************************************************
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *
      *---------------------------------------------------------------
      * Local data area.
      *---------------------------------------------------------------
      *
     Dlda             uds                  dtaara(*lda)
     D  ldfwedt                1      8  0
     D  ldfwemdy               9     14  0
      *
     D  ldtwedt               15     22  0
     D  ldtwemdy              23     28  0
      *
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ***************************************************************************************
      * MAINLINE
      ***************************************************************************************
      *
      * Print headings
     C                   exsr      $h1hdr
      *
      * Read/print each record selected by the open query.
      *
     C                   dou       *in90 = *on                                  Main do
     C                   read      tfp053                                 90
     C                   if        *in90 = *off                                 If not EOF
     C                   move      no            first
      * Print record
     C                   exsr      $l1dtl
     C                   endif                                                  If not EOF
     C                   enddo                                                  Main do
      *
      *---------------------------------------------------------------
      * EOF processing
      *---------------------------------------------------------------
      * If the file was empty, print the standard 'no data...' message
      *
     C                   if        first = yes
     C                   except    nodata
     C                   endif
      *
     C                   seton                                        lr
      /EJECT
      *---------------------------------------------------------------
      * Print detail line
      *---------------------------------------------------------------
      *
     C     $l1dtl        begsr
      *
     C                   if        *inof = *on
     C                   exsr      $h1hdr
     C                   endif
      *
      * Flip Week-ending Date to MMDDYY format.
      *
     C     *iso          test(d)                 inefdt                 92
     C                   if        *in92 = *off                                 If OK date
     C                   move      inefdt        wkisodate
     C     *mdy          move      wkisodate     l1wemdy
     C                   endif                                                  If OK date
      *
     C                   except    l1dtl
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Print report heading
      *---------------------------------------------------------------
      *
     C     $h1hdr        begsr
      *
     C                   except    h1hdr
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Retrieve time for report heading.
      *
     C                   time                    rtime
      *
     C                   endsr
      /EJECT
      *-------------------------------------------------------------
      * Report heading lines
      *-------------------------------------------------------------
      *
     Oprint132  e            h1hdr          1 01
     O                       sdpgm               10
     O                                           80 'INVENTORY COSTS LISTING'
     O                                          122 'DATE'
     O                       udate         y    132
      *
     O          e            h1hdr          1
     O                       sdusr               10
     O                                          122 'TIME'
     O                       rtime              132 '  :  :  '
      *
      *
     O          e            h1hdr          1
     O                                          122 'PAGE'
     O                       page          z    132
      *
     O          e            h1hdr          1
     O                                           20 'From effective date:'
     O                       ldfwemdy            29 '  /  /  '
      *
      *
     O          e            h1hdr          2
     O                                           20 'To effective date:'
     O                       ldtwemdy            29 '  /  /  '
      *
      *
      *-------------------------------------------------------------
      * Column headings
      *-------------------------------------------------------------
      *
     O          e            h1hdr       0  1
     O                                           34 'Freezer'
     O          e            h1hdr       0  1
     O                                            9 'Effective'
     O                                           34 'Interest'
      *
     O          e            h1hdr       0  1
     O                                            6 'Date'
     O                                           15 'Year'
     O                                           22 'Week'
     O                                           34 'Rate (%)'
      *
     O          e            h1hdr          1
     O                       dash               132
      *
      *-------------------------------------------------------------
      * Detail line
      *-------------------------------------------------------------
      *
     O          e            l1dtl          1
     O                       l1wemdy              8 '  /  /  '
     O                       inyr          m     16
     O                       inwk          m     22
     O                       infipc        k     35
      *
      *
      *-------------------------------------------------------------
      * No data message line
      *-------------------------------------------------------------
      *
     O          e            nodata      1
     O                                           19 'No data meets your'
     O                                           39 'selection criteria.'
