      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      HOG PRODUCTION
      * PROGRAM:     HP6042
      * TITLE:       LISTING OF SHIP TO SBD CUSTOMER REF RECORDS
      * PROGRAMMER:  BRAD BADEN
      * CREATED:     11/02/2017
      *
      * FUNCTION: BATCH PROGRAM TO LIST THE RECORDS IN THE
      *           SHIP TO SBD CUSTOMER REF FILE HSP1951
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      * 11/27/17  Brad Baden   E010812
      *           Recompile for field changes in file HSL1951A
      *
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     FHSL1951A  IF   E           K DISK
      *    SHIP TO SBD CUSTOMER REF    (KEY: CRAJD)
      *
     FQPRINT    O    F  132        PRINTER OFLIND(*INOF)
      *
      /EJECT
      ****************************************************************
      * INPUT SPECS
      ****************************************************************
      *
      *---------------------------------------------------------------
      * STANDARD PROGRAM STATUS DATA STRUCTURE
      *---------------------------------------------------------------
      *    EXTERNALLY DEFINED AS UTPGFR (RECORD FORMAT: PGMDSR)
     D PGMDS         ESDS                  EXTNAME(UTPGFR)
      *
      *---------------------------------------------------------------
      *  NAMED CONSTANTS
      *---------------------------------------------------------------
      *
     D YES             C                   CONST('Y')
     D NO              C                   CONST('N')
      *
      /EJECT
      ****************************************************************
      * MAIN LINE
      ****************************************************************
      *
      * PREPARE TO READ THE SHIP TO SBD CUSTOMER REF FILE AND
      * PRINT HEADINGS FOR FIRST TIME THROUGH
      *
     C     *LOVAL        SETLL     HSL1951A
     C                   EXCEPT    HRHDR
      *
      * READ AND PRINT ALL RECORDS IN THE FEED TYPE MASTER.
      *
     C     *IN92         DOUEQ     *ON                                          MAIN DO
     C                   READ      HSL1951A                               92
     C     *IN92         IFEQ      *OFF                                         IF NOT EOF
      *
     C     *INOF         IFEQ      *ON
     C                   EXCEPT    HRHDR
     C                   ENDIF
      *
      * PRINT FEED TYPE DETAIL LINE
      *
     C                   EXCEPT    DTLINE
     C                   MOVEL     YES           PRTFLG
     C                   ENDIF                                                  IF NOT EOF
     C                   ENDDO                                                  MAIN DO
      *
      *---------------------------------------------------------------
      * EOF PROCESSING
      *---------------------------------------------------------------
      *  IF THE MASTER FILE WAS EMPTY AND NO RECORDS WERE PRINTED,
      *  PRINT THE STANDARD 'NO DATA...' ERROR MESSAGE
      *
     C     PRTFLG        IFEQ      NO
     C                   EXCEPT    NODATA
     C                   ENDIF
      *
     C                   EXCEPT    ENDRPT
      *
     C                   SETON                                        LR
      /EJECT
      *---------------------------------------------------------------
      * *INZSR - INITIALIZATION SUBROUTINE
      *---------------------------------------------------------------
      *
     C     *INZSR        BEGSR
      *
      * SET PRINT FLAG TO DETERMINE IF ANY RECORDS WERE PRINTED
      * RETRIEVE TIME FOR REPORT HEADING
      *
     C                   MOVEL     NO            PRTFLG            1
     C                   MOVEL     *ALL'-'       DASH             80
     C                   TIME                    RTIME             6 0
      *
     C                   ENDSR
      /EJECT
      *-------------------------------------------------------------
      * REPORT HEADING LINES
      *-------------------------------------------------------------
      *
     OQPRINT    E            HRHDR          1 01
     O                       SDPGM               10
     O                                           48 'HOG PRODUCTION SYSTEM'
     O                                           70 'DATE'
     O                       UDATE         Y     80
      *
     O          E            HRHDR          1
     O                       SDUSR               10
     O                                           46 'LISTING OF SHIP TO SBD CUS'
     O                                           56 'TOMER REFS'
     O                                           70 'TIME'
     O                       RTIME               80 '  :  :  '
      *
     O          E            HRHDR          2
     O                                           70 'PAGE'
     O                       PAGE          Z     80
      *
     O          E            HRHDR          1
     O                                           52 'CUST'
     O          E            HRHDR          1
     O                                           14 'CUST'
     O                                           52 'TYPE'
     O          E            HRHDR          1
     O                                            8 'CUSTOMER'
     O                                           14 'TYPE'
     O                                           41 'CUSTOMER TYPE DESCRIPTION'
     O                                           53 'VALUE'
     O                                           63 'STATUS'
      *
     O          E            HRHDR          1
     O                       DASH                80
      *
      *-------------------------------------------------------------
      * FEED TYPES DETAIL LINE
      *-------------------------------------------------------------
      *
     O          E            DTLINE      1
     O                       CRAJD                8
     O                       CRTYPNO             13
     O                       CRTYPDS             46
     O                       CRTYPCD             53
     O                       CRCRST              60
      *
      *-------------------------------------------------------------
      * END OF REPORT LINE
      *-------------------------------------------------------------
      *
     O          E            ENDRPT      2
     O                                           21 '*** END OF REPORT ***'
      *
      *-------------------------------------------------------------
      * NO DATA MESSAGE LINE
      *-------------------------------------------------------------
      *
     O          E            NODATA      1
     O                                           19 'NO DATA MEETS YOUR'
     O                                           38 'SELECTION CRITERIA'
