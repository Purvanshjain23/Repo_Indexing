/***************************************************************** */
/*                                                                 */
/*  PROGRAM NUMBER:  HP4063CL                                      */
/*  PROGRAM NAME:    UT: SPECIFY-MOVE EDITED TRANSFERS INTO HPS    */
/*  PROGRAMMER:      LEANNE RAMSEY                                 */
/*  CREATE DATE:     07/09/09                                      */
/*                                                                 */
/*  FUNCTION:        THIS CL IS SUBMITTED WITH QCMDEXC FROM        */
/*                   HP4063.                                       */
/*                                                                 */
/*=================================================================*/
/*  Modification History                                           */
/*  01/19/2017 JBB E7986  Add Business Office parameter.  If the   */
/*                        Business Office is 'OKLIV', set the      */
/*                        file name to HSP2063.  Otherwise, set    */
/*                        the file name to HSP2064.                */
/*                                                                 */
/*******************************************************************/

             PGM        PARM(&BUSOFC)

             DCL        VAR(&USER)    TYPE(*CHAR) LEN(10)
             DCL        VAR(&NODATA)  TYPE(*CHAR) LEN(1) VALUE('N')
             DCL        VAR(&PERCLO)  TYPE(*CHAR) LEN(1) VALUE('N')
             DCL        VAR(&PERRUN)  TYPE(*CHAR) LEN(1) VALUE('N')
             DCL        VAR(&EDITERR) TYPE(*CHAR) LEN(1) VALUE('N')
             DCL        VAR(&BUSOFC)  TYPE(*CHAR) LEN(5)
             DCL        VAR(&FILENM)  TYPE(*CHAR) LEN(10)

             RTVJOBA    USER(&USER)

/* Determine file name based upon Business Office value                    */
             IF         COND(&BUSOFC = 'OKLIV') THEN(CHGVAR +
                          VAR(&FILENM) VALUE('HSP2062'))
             IF         COND(&BUSOFC = 'IFLIV') THEN(CHGVAR +
                          VAR(&FILENM) VALUE('HSP2064'))

/* Hard Allocate on Editing File                                           */

             ALCOBJ     OBJ((&FILENM *FILE *EXCL))
             MONMSG     MSGID(CPF0000) EXEC(DO)
             SNDMSG     MSG('***** Cannot interface; the Editing +
                          File is in use. No one can be using any +
                          of the Transfer Interface options when +
                          this Final Interface is run. *****') +
                          TOUSR(&USER)
             GOTO       CMDLBL(EXIT)
                        ENDDO

/* Call program to write to HPS files                                      */

             CALL       PGM(HP2063) PARM(&NODATA &PERCLO &PERRUN +
                          &EDITERR &BUSOFC)


/* There is no data in the Editing file                                    */

             IF         COND(&NODATA *EQ 'Y') THEN(DO)
             SNDMSG     MSG('***** There were no Transfers to +
                          interface into HPS. Have you +
                          received/edited the Uploaded Data? +
                          *****') TOUSR(&USER)
             ENDDO

/* The data in the Editing file has not yet been edited                    */

             IF         COND(&EDITERR *EQ 'Y') THEN(DO)
             SNDMSG     MSG('***** You have not yet Edited the +
                          Transfer Data. You must Edit the Transfer +
                          Data at least 1 time before interfacing +
                          into HPS. *****') TOUSR(&USER)
             ENDDO

/* A Scheduled Ship Date is in a closed period                             */

             IF         COND(&PERCLO *EQ 'Y') THEN(DO)
             SNDMSG     MSG('***** The Transfer Interface was not +
                          performed. A Scheduled Ship Date is in a +
                          Period that has already been closed in +
                          HPS. *****') TOUSR(&USER)
             ENDDO

/* A Scheduled Ship Date is in a Period that is currently being closed     */

             IF         COND(&PERRUN *EQ 'Y') THEN(DO)
             SNDMSG     MSG('***** The Transfer Interface was not +
                          performed. A Scheduled Ship Date is in +
                          the Period that is currently being closed +
                          in HPS. *****') TOUSR(&USER)
             ENDDO

             DLCOBJ     OBJ((&FILENM *FILE *EXCL))
             MONMSG     MSGID(CPF0000)
EXIT:
             ENDPGM

