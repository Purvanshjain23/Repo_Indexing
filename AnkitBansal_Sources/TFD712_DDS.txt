     A*%%TS  SD  20110106  133821  ISLRAMS     REL-V6R1M0  5761-WDS
     A*****************************************************************
     A*
     A*  SYSTEM:        TRIUMPH FOODS
     A*  DISPLAY FILE:  TFD712
     A*  TITLE:         VIEW CLAIM ADJUSTMENTS
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  CREATED:       05/04/05
      *
      *========================================================================
      * MODIFICATIONS LOG:
      *------------------------------------------------------------------------
      * W105621 RMC 10/10/2022 INCREASE FIELD SIZES TO HANDLE PRICES > 999.99
      *         CHANGE @@ANPRPR TO SIZE 10.6
      *           CHANGES CLANPRPR IN TFP022
      *         CHANGE @@NCLPR  TO SIZE 10.6
      *           CHANGES CLNCLPR IN TFP022
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/TFPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CF03(03 'EXIT')
     A                                      CF05(05 'REFRESH')
     A*
     A*-----------------------------------------------------------------
     A* RECORD FORMAT ONE
     A*-----------------------------------------------------------------
     A          R TF712R1
     A*%%TS  SD  20110106  133821  ISLRAMS     REL-V6R1M0  5761-WDS
     A                                      OVERLAY
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 26'VIEW CLAIM ADJUSTMENT'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USR)
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@JOB)
     A                                      DSPATR(HI)
     A                                  3  2'Company number:'
     A                                      DSPATR(HI)
     A            D1CONO    R        O  3 18REFFLD(TFREF/@@CONO *LIBL/TFPREF)
     A                                      EDTCDE(M)
     A                                  3 51'Week dates         '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  4  2'Credit/debit memo number .:'
     A                                      DSPATR(HI)
     A            D1MMNO    R        O  4 30REFFLD(TFREF/@@MMNO *LIBL/TFPREF)
     A                                      EDTCDE(Z)
     A                                  4 51'Beginning:'
     A                                      DSPATR(HI)
     A            D1WBMDY   R        O  4 62REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A                                      DSPATR(HI)
     A                                      COLOR(PNK)
     A                                  5  2'Credit/debit memo line nbr:'
     A                                      DSPATR(HI)
     A            D1MMLNNO  R        O  5 30REFFLD(TFREF/@@MMLNNO *LIBL/TFPREF)
     A                                      EDTCDE(Z)
     A                                  5 51'Ending ..:'
     A                                      DSPATR(HI)
     A            D1WEMDY   R        O  5 62REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A                                      DSPATR(HI)
     A                                      COLOR(PNK)
     A                                  6  2'Data source ..............:'
     A                                      DSPATR(HI)
     A            D1DSDS    R        O  6 30REFFLD(TFREF/@@DSDS *LIBL/TFPREF)
     A                                  8  2'Claim / Line number ......:'
     A                                      DSPATR(HI)
     A            D1CLNO    R        O  8 30REFFLD(TFREF/@@CLNO *LIBL/TFPREF)
     A                                      EDTCDE(Z)
     A                                  8 38'/'
     A                                      DSPATR(HI)
     A            D1CLLNNO  R        O  8 40REFFLD(TFREF/@@CLLNNO *LIBL/TFPREF)
     A                                      EDTCDE(Z)
     A                                  8 51'Product code.....:'
     A                                      DSPATR(HI)
     A            D1PRCD    R        O  8 70REFFLD(TFREF/@@PRCD *LIBL/TFPREF)
     A                                      EDTCDE(Z)
     A                                  9  2'Memo type ......................:'
     A                                      DSPATR(HI)
     A            D1MMTY    R        O  9 36REFFLD(TFREF/@@MMTY *LIBL/TFPREF)
     A                                  9 51'Sale flag .......:'
     A                                      DSPATR(HI)
     A            D1SALEFL  R        O  9 70REFFLD(TFREF/@@SALEFL *LIBL/TFPREF)
     A                                 10  2'Original sale week-ending date .:'
     A                                      DSPATR(HI)
     A            D1OSMDY   R        O 10 36REFFLD(TFREF/@@DT06 *LIBL/TFPREF)
     A                                      EDTCDE(Y)
     A                                 10 51'Affect lbs flag .:'
     A                                      DSPATR(HI)
     A            D1AFSLFL  R        O 10 70REFFLD(TFREF/@@AFSLFL *LIBL/TFPREF)
     A                                 11  2'Net product revenue price/lb ...:'
     A                                      DSPATR(HI)
     A            D1ANPRPR  R        O 11 36REFFLD(TFREF/@@ANPRPR *LIBL/TFPREF)
     A                                      EDTCDE(K)
     A                                 12  2'Net claim price/lb .............:'
     A                                      DSPATR(HI)
     A            D1NCLPR   R        O 12 36REFFLD(TFREF/@@NCLPR *LIBL/TFPREF)
     A                                      EDTCDE(K)
     A                                 13  2'Aggregate claim pounds .........:'
     A                                      DSPATR(HI)
     A            D1ACLLB   R        O 13 36REFFLD(TFREF/@@ACLLB *LIBL/TFPREF)
     A                                      EDTCDE(K)
     A                                 14  2'Aggregate gross claim amount ...:'
     A                                      DSPATR(HI)
     A            D1AGCLAM  R        O 14 36REFFLD(TFREF/@@AGCLAM *LIBL/TFPREF)
     A                                      EDTCDE(K)
     A                                 15  2'Total claim sales expense amount:'
     A                                      DSPATR(HI)
     A            D1CLSXAM  R        O 15 36REFFLD(TFREF/@@CLSXAM *LIBL/TFPREF)
     A                                      EDTCDE(K)
     A  45                             16 46'This is an Affiliated Sales Claim.'
     A                                      COLOR(BLU)
     A                                 17  2'Net Claim Amounts        '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 17 30'Responsibility %'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 17 52'Responsible department:'
     A                                      DSPATR(HI)
     A            D1RPDEPT  R        O 17 76REFFLD(TFREF/@@RPDEPT *LIBL/TFPREF)
     A                                 18  2'Aggregate:'
     A                                      DSPATR(HI)
     A            D1ANCLAM  R        O 18 13REFFLD(TFREF/@@ANCLAM *LIBL/TFPREF)
     A                                      EDTCDE(K)
     A                                 18 52'Responsibility flag ..:'
     A                                      DSPATR(HI)
     A            D1RPFL    R        O 18 76REFFLD(TFREF/@@RPFL *LIBL/TFPREF)
     A                                 19  3'Seaboard:'
     A                                      DSPATR(HI)
     A            D1SNCLAM  R        O 19 13REFFLD(TFREF/@@SNCLAM *LIBL/TFPREF)
     A                                      EDTCDE(K)
     A            D1SRPPC   R        O 19 33REFFLD(TFREF/@@SRPPC *LIBL/TFPREF)
     A                                      EDTCDE(K)
     A                                 19 52'Reason code ..........:'
     A                                      DSPATR(HI)
     A            D1RSCD    R        O 19 76REFFLD(TFREF/@@RSCD *LIBL/TFPREF)
     A                                 20  4'Triumph:'
     A                                      DSPATR(HI)
     A            D1TNCLAM  R        O 20 13REFFLD(TFREF/@@TNCLAM *LIBL/TFPREF)
     A                                      EDTCDE(K)
     A            D1TRPPC   R        O 20 33REFFLD(TFREF/@@TRPPC *LIBL/TFPREF)
     A                                      EDTCDE(K)
     A                                 20 52'Product reason type ..:'
     A                                      DSPATR(HI)
     A            D1PRRSTY  R        O 20 76REFFLD(TFREF/@@PRRSTY *LIBL/TFPREF)
     A  46                             21  8'STF:'
     A                                      DSPATR(HI)
     A            D1XNCLAM  R        O 21 13REFFLD(TFREF/@@XNCLAM *LIBL/TFPREF)
     A                                      EDTCDE(K)
     A            D1XRPPC   R        O 21 33REFFLD(TFREF/@@XRPPC *LIBL/TFPREF)
     A                                      EDTCDE(K)
     A*
     A*-----------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*-----------------------------------------------------------------
     A          R TF712K1
     A*%%TS  SD  20040706  113301  ISLRAMS     REL-V5R2M0  5722-WDS
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A                                 23 11'F5=Refresh'
      *
      *------------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *-----------------------------------------------------------------
     A          R TF712ES                   SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
      *------------------------------------------------------------------
      * ERROR MESSAGE SUBFILE CONTROL FORMAT
      *-----------------------------------------------------------------
     A          R TF712EC                   SFLCTL(TF712ES)
      *
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
