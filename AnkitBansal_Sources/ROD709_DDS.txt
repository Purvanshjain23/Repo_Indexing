     A*%%TS  SD  20080616  092442  ISLRAMS     REL-V5R4M0  5722-WDS
     A*****************************************************************
     A*
     A*  ENVIRONMENT:   PORK DIVISION
     A*  SYSTEM:        RESOURCE OPTIMIZATION
     A*  DISPLAY FILE:  ROD709
     A*  TITLE:         VIEW BYPRODUCT ITEM VALUES
     A*  PROGRAMMER:    LEANNE RAMSEY
     A*  CREATED:       02/12/07
     A*
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/ROPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A                                      CF05(05 'REFRESH')
     A                                      CF07(07 'REPORT')
     A                                      CF13(13 'FOLD/DROP')
     A                                      CF17(17 'PRINT OPTIONS')
     A                                      CF20(20 'WRKSPLF')
     A*
     A*
     A*---------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*---------------------------------------------------------------
     A          R RO709S1                   SFL
     A*%%TS  SD  20080616  090449  ISLRAMS     REL-V5R4M0  5722-WDS
     A  85                                  SFLNXTCHG
     A            HDRRN1         4Y 0H
     A            D1ITCD    R        O 13  2REFFLD(ROREF/@@ITCD *LIBL/ROPREF)
     A                                      EDTCDE(M)
     A            D1PULB    R        O 13 11REFFLD(ROREF/@@PULB *LIBL/ROPREF)
     A                                      EDTCDE(K)
     A            D1PUPR    R        O 13 32REFFLD(ROREF/@@PUPR *LIBL/ROPREF)
     A                                      EDTCDE(K)
     A            D1NPUPR   R        O 13 44REFFLD(ROREF/@@NPUPR *LIBL/ROPREF)
     A                                      EDTCDE(K)
     A            D1PCO     R        O 13 56REFFLD(ROREF/@@PCO *LIBL/ROPREF)
     A                                      EDTCDE(K)
     A            D1LCO     R        O 13 69REFFLD(ROREF/@@LCO *LIBL/ROPREF)
     A                                      EDTCDE(K)
     A            D1ITDS        17A  O 14 11COLOR(PNK)
     A                                 14 29'Except Flg/Nbr:'
     A                                      COLOR(BLU)
     A            D1EXFL    R        O 14 45REFFLD(ROREF/@@EXFL *LIBL/ROPREF)
     A                                      COLOR(BLU)
     A            D1PESN    R        O 14 47REFFLD(ROREF/@@PESN *LIBL/ROPREF)
     A                                      EDTCDE(Z)
     A                                      COLOR(BLU)
     A            D1ICO     R        O 14 56REFFLD(ROREF/@@ICO *LIBL/ROPREF)
     A                                      EDTCDE(K)
     A                                      COLOR(PNK)
     A            D1OCO     R        O 14 69REFFLD(ROREF/@@OCO *LIBL/ROPREF)
     A                                      EDTCDE(K)
     A                                      COLOR(PNK)
     A*
     A*---------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*---------------------------------------------------------------
     A          R RO709C1                   SFLCTL(RO709S1)
     A*%%TS  SD  20080616  092442  ISLRAMS     REL-V5R4M0  5722-WDS
     A                                      SFLSIZ(0050)
     A                                      SFLPAG(0004)
     A N82                                  ROLLUP(25 'PAGE FORWARD')
     A                                      OVERLAY
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A  78                                  SFLFOLD(CF13)
     A  79                                  SFLDROP(CF13)
     A                                      SFLMODE(&FOLD)
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            FOLD           1A  H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 25'RESOURCE OPTIMIZATION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USR)
     A                                      DSPATR(HI)
     A                                  2 30'VIEW BYPRODUCT ITEMS'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@JOB)
     A                                      DSPATR(HI)
     A                                  3  2'Company :'
     A            C1CONO    R        O  3 12REFFLD(ROREF/@@CONO *LIBL/ROPREF)
     A                                      DSPATR(HI)
     A                                      EDTCDE(K)
     A                                  3 30'                  '
     A                                  4  2'W/E date:'
     A            C1WEMDY   R        O  4 12REFFLD(@@DT06)
     A                                      DSPATR(HI)
     A                                      EDTWRD('  /  /  -')
     A                                  4 22'Produced lbs:'
     A            C1PULB    R        O  4 36REFFLD(ROREF/@@PULB *LIBL/ROPREF)
     A                                      EDTCDE(K)
     A                                      DSPATR(HI)
     A                                  4 57'Sls Yield %:'
     A            C1SYPC    R        O  4 70REFFLD(ROREF/@@SYPC *LIBL/ROPREF)
     A                                      EDTCDE(K)
     A                                      DSPATR(HI)
     A                                  5 20'Prod value amt:'
     A            C1PUAM    R        O  5 36REFFLD(ROREF/@@PUAM *LIBL/ROPREF)
     A                                      EDTCDE(K)
     A                                      DSPATR(HI)
     A                                  5 62'Mult %:'
     A            C1MUPC    R        O  5 70REFFLD(ROREF/@@MUPC *LIBL/ROPREF)
     A                                      EDTCDE(K)
     A                                      DSPATR(HI)
     A                                  6  2'Group ..:'
     A            C1CGCD    R        O  6 12REFFLD(ROREF/@@CGCD *LIBL/ROPREF)
     A                                      DSPATR(HI)
     A                                  6 21'Prod price/LB:'
     A            C1PUPR    R        O  6 36REFFLD(ROREF/@@PUPR *LIBL/ROPREF)
     A                                      EDTCDE(K)
     A                                      DSPATR(HI)
     A                                  6 49'         Cost/LB              '
     A                                      DSPATR(UL)
     A                                  7  2'Subgroup:'
     A            C1SGCD    R        O  7 12REFFLD(ROREF/@@SGCD *LIBL/ROPREF)
     A                                      DSPATR(HI)
     A                                  7 22'Net price/LB:'
     A            C1NPUPR   R        O  7 36REFFLD(ROREF/@@NPUPR *LIBL/ROPREF)
     A                                      EDTCDE(K)
     A                                      DSPATR(HI)
     A                                  7 49'Pkg'
     A            C1PCO     R        O  7 53REFFLD(ROREF/@@PCO *LIBL/ROPREF)
     A                                      EDTCDE(K)
     A                                      DSPATR(HI)
     A                                  7 65'Lab'
     A            C1LCO     R        O  7 69REFFLD(ROREF/@@LCO *LIBL/ROPREF)
     A                                      EDTCDE(K)
     A                                      DSPATR(HI)
     A                                  8 49'Ing'
     A            C1ICO     R        O  8 53REFFLD(ROREF/@@ICO *LIBL/ROPREF)
     A                                      DSPATR(HI)
     A                                      EDTCDE(K)
     A                                  8 65'Oth'
     A            C1OCO     R        O  8 69REFFLD(ROREF/@@OCO *LIBL/ROPREF)
     A                                      DSPATR(HI)
     A                                      EDTCDE(K)
     A                                 10  4'Item'
     A                                      DSPATR(HI)
     A                                 10 34'Produced'
     A                                      DSPATR(HI)
     A                                 10 46'Net Prod'
     A                                      DSPATR(HI)
     A                                 10 56'Pkg Cost/Lb'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 69'Lab Cost/Lb'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 11  4'Code'
     A                                      DSPATR(HI)
     A                                 11 15'Produced Pounds'
     A                                      DSPATR(HI)
     A                                 11 34'Price/Lb'
     A                                      DSPATR(HI)
     A                                 11 46'Price/Lb'
     A                                      DSPATR(HI)
     A                                 11 56'Ing Cost/Lb'
     A                                      DSPATR(HI)
     A                                      COLOR(PNK)
     A                                 11 69'Oth Cost/Lb'
     A                                      DSPATR(HI)
     A                                      COLOR(PNK)
     A            C1ITCD    R        B 12  2REFFLD(ROREF/@@ITCD *LIBL/ROPREF)
     A                                      EDTCDE(M)
     A                                      DSPATR(HI)
     A*
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R RO709K1
     A*%%TS  SD  20080606  161632  ISLRAMS     REL-V5R4M0  5722-WDS
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A                                 23 10'F5=Refresh'
     A                                 23 21'F7=Report'
     A                                 23 32'F13=Fold/Unfold'
     A                                 23 49'F17=Print Opts'
     A                                 23 65'F20=Spool Files'
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *---------------------------------------------------------------
     A          R RO709ES                   SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE CONTROL FORMAT
      *---------------------------------------------------------------
     A          R RO709EC                   SFLCTL(RO709ES)
      *
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
