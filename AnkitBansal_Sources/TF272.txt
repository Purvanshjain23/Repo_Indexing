      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Triumph Foods
      * PROGRAM:     TF272--Yields: Edit with Error Listing
      * PROGRAMMER:  LeAnne Ramsey
      * CREATED:     12/03/08
      *
      * Function:    If any errors exist, the Yield logic will not be performed.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 03/02/09  LeAnne Ramsey
      *           Recompile only. Field 'Smoker Yield %' added to Resource Optimization Header.
      *
      * 02/17/10  LeAnne Ramsey
      *           Recompile only. Two new fields were added to the Resource Optimization Header:
      *                Cap&Blade Allocation % for Special Trim
      *                Cap&Blade Allocation % for Regular Trim
      *
      * 03/08/12  LeAnne Ramsey (E1984)
      *           Recompile only.
      *           Added 6 new Skirt Meat Allocation Flag fields to ROP100.
      *
      * 11/09/17  Danny Nguyen    - R12011-Kill Cut Data for STF
      *           File TFP050 was changed. Recompile only. No logic change.
      *
      /eject
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Ftfp099    if   e           k disk
      *  Function control
      *
      *
     Ftfl050a   if   e           k disk
      *  Kill/Cut data
      *
      *
     Frol100c   if   e           k disk
      *  Resource optimization header
      *
      *
     Fprint80   o    f   80        printer oflind(*inof)
      *
      /eject
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      *----------------------------------------------------------------
      * CONSTANTS
      *----------------------------------------------------------------
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      *----------------------------------------------------------------
      * ARRAYS AND TABLES
      *----------------------------------------------------------------
      *
      *----------------------------------------------------------------
      * STANDALONE FIELDS
      *----------------------------------------------------------------
      *
      * Standard fields
      *
     D dash            s             80    inz(*all'-')
     d rtime           s              6  0
      *
      *
      * Print fields
      *
     d errds           s             60
      *
      *
      * Parm fields
      *
     d xxerror         s              1
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *---------------------------------------------------------------
      *  Definition for external data area 'DAROCALC' for Meat Costing
      *---------------------------------------------------------------
     D
     D darocalc        ds                  dtaara(darocalc)
     D  dafl                   1      1
      *
      *---------------------------------------------------------------
      *  Local data area
      *---------------------------------------------------------------
      *
     d lda            uds                  dtaara(*lda)
      *
     D  ldwedt                29     36  0
      *
     D  ldcono                82     84  0
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * Mainline
      ****************************************************************
      *
      * Print headings
     C                   exsr      $h1hdr
      *
      * A Kill/Cut Data record must exist for the week.
      *
     C     ldwedt        chain     tfl050a                            92
     C                   if        *in92 = *on                                  If no rec
     C                   move      yes           xxerror
     C                   eval      errds = 'No Kill/Cut Data record exists for +
     C                                      this week.'
     C                   except    errmsg
     C                   endif                                                  If no rec
      *
      * If Margin Adjustment Close for the Week is "Final"
      * or Margin Adjustment Close function is running.
      *
     C     'MARGIN    '  chain     tfp099                             92
     C                   if        *in92 = *off                                 If hit
      *        Final
     C                   if        ldwedt <= fnledt
     C                   move      yes           xxerror
     C                   eval      errds = 'Margin Adjustment Close is already +
     C                                      FINAL for this week.'
     C                   except    errmsg
     C                   endif
      *        Running
     C                   if        fnsbfl = yes
     C                   move      yes           xxerror
     C                   eval      errds = 'A Margin Adjustment Close is +
     C                                      currently running.'
     C                   except    errmsg
     C                   endif
     C                   endif                                                  If hit
      *
      * If Meat Cost calc for the Week is "Final"
      *
     C     key01         chain     rol100c                            92
     C                   if        *in92 = *off and ohrcstcd = 'F'              If meat
     C                   move      yes           xxerror
     C                   eval      errds = 'The Meat Cost calc is already +
     C                                      FINAL for this week.'
     C                   except    errmsg
     C                   endif                                                  If meat
      *
      * If Meat Cost calc is running
      *
     C                   in        darocalc
     C                   if        darocalc <> *blank                           If running
     C                   move      yes           xxerror
     C                   eval      errds = 'The Meat Cost calc is currently +
     C                                      running.'
     C                   except    errmsg
     C                   endif                                                  If running
      *
      * If there were no errors or warnings, print "All OK" message.
      *
     C                   if        xxerror = no
     C                   except    noerrmsg
     C                   else
     C                   eval      errds = 'There were errors. No Weekly +
     C                                      Yield logic was executed.'
     C                   except    blank
     C                   except    errmsg
     C                   endif
      *
     C                   seton                                        lr
      /eject
      *-----------------------------------------------------------------------------------
      * Print headings
      *-----------------------------------------------------------------------------------
      *
     C     $h1hdr        begsr
      *
     C                   except    h1hdr
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Parm lists
      *
     C     *entry        plist
     C                   parm                    xxerror
      *
      * The "error" parm above should come in as NO. But, just in case, set it
      * to NO here.
     C                   move      no            xxerror
      *
      * Key lists
      *
     C     key01         klist
     C                   kfld                    ldwedt
     C                   kfld                    ldcono
      *
      *
      * Set up heading info.
      *
     C                   time                    rtime
      *
     C                   endsr
      /eject
      *-------------------------------------------------------------
      * Report heading lines
      *-------------------------------------------------------------
      *
     Oprint80   e            h1hdr          1 01
     O                       sdpgm               10
     O                                           46 'Weekly Yield Processing'
     O                                           70 'Date'
     O                       udate         y     80
      *
     O          e            h1hdr          1
     O                       sdusr               10
     O                                           41 'Error Listing'
     O                                           70 'Time'
     O                       rtime               80 '  :  :  '
      *
     O          e            h1hdr          2
     O                                           70 'Page'
     O                       page          z     80
      *
     O          e            h1hdr       0  1
     O                       dash                80
      *
      *-------------------------------------------------------------
      * Blank
      *-------------------------------------------------------------
      *
     O          e            blank          1
      *
      *-------------------------------------------------------------
      * There were no errors.
      *-------------------------------------------------------------
      *
     O          e            noerrmsg       2
     O                                           20 'There are no errors.'
      *-----------------------------------------------------------------------------
      * Weekly Revenue Closes were not Complete--and this is a FINAL
      *-----------------------------------------------------------------------------
      *
     O          e            errmsg         2
     O                       errds               60
      *
      /eject
