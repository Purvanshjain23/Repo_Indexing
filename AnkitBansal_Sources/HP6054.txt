      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Hog Production System
      * PROGRAM:     HP6054
      * TITLE:       Average Weekly Inventory Report
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     08/07/02
      *
      * FUNCTION:  Batch program to show the average weekly inventory for a specified
      *            date range. A work program builds 2 workfiles: detail and summary.
      *
      *            If the user generates the 'detail' version, the program prints the
      *            detail by farm with a total line for the consoldated farm site.
      *
      *            If the user generates the 'summary' version, the program prints the
      *            summary workfile--basically, the totals for each consolidated farm.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fhsp3054   if   e           k disk
      *    Workfile: Average weekly inventory report--detail
      *
      *
     Fhsp3055   if   e           k disk
      *    Workfile: Average weekly inventory report--summary
      *
      *
     Fqprint    o    f  115        printer oflind(*inof)
      *
      /EJECT
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Standard fields
      *
     D dash            s            115
     d rtime           s              6  0
      *
      * Control fields
      *
     D first           s              1    inz('Y')
     D count           s              3  0
     D start           s              3  0
     D end             s              3  0
      *
      *
      * Workfields
      *
     D wkalphfscd      s              5
     D wktext          s             30
      *
      *
      * Print fields
      *
     D t1text          s             30
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *
      *---------------------------------------------------------------
      * Local data area.
      *---------------------------------------------------------------
      *
     Dlda             uds                  dtaara(*lda)
     D  ldfmdy                 1      6  0
     D  ldtmdy                 8     13  0
     D  ldfcymd               15     22  0
     D  ldtcymd               24     31  0
      *
     D  lddsfl                33     33
     D  lddsds                35     44
     D  ldcnfscd              46     50  0
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * MAINLINE
      ****************************************************************
      *
      * Print headings
      *
     C                   except    h1hdr
      *
      * Read the summary workfile (ie: consolidated farm totals) as the primary file.
      *
     C     *loval        setll     hsp3055
      *
     C                   dou       *in90 = *on                                  Main do
     C                   read      hsp3055                                90
     C                   if        *in90 = *off                                 If not EOF
      *
     C                   if        first = yes
     C                   move      no            first
     C                   else
     C                   except    blank
     C                   endif
      *
      * If the user wants to see the 'detail', go retrieve/print all the
      * detail farm records for this consolidated farm. Otherwise, just
      * print this summary record.
      *
     C                   select
     C                   when      lddsfl = 'D'
     C                   exsr      $detail
     C                   when      lddsfl = 'S'
     C                   exsr      $summary
     C                   endsl
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Main do
      *
      *---------------------------------------------------------------
      * EOF processing
      *---------------------------------------------------------------
      * If the file was empty, print the standard 'no data...'
      * error message
      *
     C                   if        first = yes
     C                   except    nodata
     C                   endif
      *
     C                   seton                                        lr
      /EJECT
      *---------------------------------------------------------------
      * Print detail
      *---------------------------------------------------------------
      *
     C     $detail       begsr
      *
     C                   z-add     0             count
      *
      * Read each farm record in the detail file for the 'consolidated' farm
      * that you are processing.
      *
     C     wscnfscd      setll     hsp3054
      *
     C                   dou       *in91 = *on                                  Do detail
     C     wscnfscd      reade     hsp3054                                91
     C                   if        *in91 = *off                                 If not EOF
     C                   add       1             count
      *
     C                   if        *inof = *on
     C                   exsr      $h1hdr
     C                   endif
     C                   except    l1dtl
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Do detail
      *
      * If you processed more than a single farm for the 'consolidated' farm,
      *  Concatenate the farm number into the total line text
      *  Print a an underscore line
      *  Print a total line for the consolidated farm
      *
     C                   if        count > 1
      *
     C                   exsr      $alphfscd
      *
     C                   if        *inof = *on
     C                   exsr      $h1hdr
     C                   else
     C                   except    underscore
     C                   endif
      *
     C                   exsr      $summary
     C                   endif
      *
     C                   endsr
      /EJECT
      *--------------------------------------------------------------------------------------
      * Strip leading zeros from alpha farm site field
      *--------------------------------------------------------------------------------------
      *
     C     $alphfscd     begsr
      *
      * Move farm site number to an alpha field and strip the leading zeros.
      * Then concatenate it with text for the total line description field.
      *
     C                   move      wscnfscd      wkalphfscd
     C                   if        %subst(wkalphfscd:1:1) = '0'                 If position 1
     C                   eval      %subst(wkalphfscd:1:1) = *blank
     C                   if        %subst(wkalphfscd:2:1) = '0'                 If position 2
     C                   eval      %subst(wkalphfscd:2:1) = *blank
     C                   if        %subst(wkalphfscd:3:1) = '0'                 If position 3
     C                   eval      %subst(wkalphfscd:3:1) = *blank
     C                   if        %subst(wkalphfscd:4:1) = '0'                 If position 4
     C                   eval      %subst(wkalphfscd:4:1) = *blank
     C                   endif                                                  If position 4
     C                   endif                                                  If position 3
     C                   endif                                                  If position 2
     C                   endif                                                  If position 1
      *
      * Concatenate
     C                   eval      wktext = 'Consolidated Farm ' +
     C                                      %trim(wkalphfscd) + ' Total:'
      *
      * Now, right-justify the text you just concatenated.
      *
     C     ' '           checkr    wktext        end
     C                   eval      start = 30 - end + 1
     C                   eval      %subst(t1text:start) = wktext
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Print header
      *---------------------------------------------------------------
      *
     C     $h1hdr        begsr
      *
     C                   except    h1hdr
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Print summary
      *---------------------------------------------------------------
      *
     C     $summary      begsr
      *
     C                   if        *inof = *on
     C                   exsr      $h1hdr
     C                   endif
     C                   except    t1tot
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Set up a line of underscores and retrieve time for report heading.
      *
     C                   movel     *all'-'       dash
     C                   time                    rtime
      *
      *
      * Setup the user selections in the headings.
      *
      * Consolidated farm site
      *
     C                   if        ldcnfscd = 0
     C                   seton                                        89
     C                   endif
      *
     C                   if        lddsfl = 'S'
     C                   seton                                        88
     C                   endif
      *
     C                   endsr
      /EJECT
      *-------------------------------------------------------------
      * Report heading lines
      *-------------------------------------------------------------
      *
     Oqprint    e            h1hdr          1 01
     O                       sdpgm               10
     O                                           62 'HOG PRODUCTION SYSTEM'
     O                                          105 'DATE'
     O                       udate         y    115
      *
     O          e            h1hdr          1
     O                       sdusr               10
     O                                           62 'AVERAGE WEEKLY INVENTORY'
     O                                           69 ' REPORT'
     O                                          105 'TIME'
     O                       rtime              115 '  :  :  '
      *
     O          e            h1hdr          2
     O                                          105 'PAGE'
     O                       page          z    115
      *
      *
     O          e            h1hdr          1
     O                                           26 'From date ...............:'
     O                       ldfmdy              35 '  /  /  '
      *
      *
     O          e            h1hdr          1
     O                                           26 'To date .................:'
     O                       ldtmdy              35 '  /  /  '
      *
      *
     O          e            h1hdr          1
     O                                           26 'Detail or summary .......:'
     O                       lddsds              38
      *
      *
     O          e            h1hdr          2
     O                                           26 'Consolidated farm site ..:'
     O              N89      ldcnfscd      z     35
     O               89                          35 'ALL'
      *
      *
     O          e            h1hdr          1
     O                                           12 'CONSOLIDATED'
      *
     O                                           44 'TOTAL'
     O                                           53 'NUMBER'
     O                                           64 'AVERAGE'
     O                                           75 'FROM'
     O                                           85 'TO'
     O          e            h1hdr          1
     O                                            8 'FARM'
     O                                           19 'FARM'
     O                                           32 'PRODUCTION'
     O                                           44 'INVENTORY'
     O                                           51 'OF'
     O                                           64 'WEEKLY'
     O                                           75 'WEEK'
     O                                           85 'WEEK'
     O          e            h1hdr          1
     O                                            8 'SITE'
     O                                           19 'SITE'
     O                                           29 'PHASE'
     O                                           44 'HEAD'
     O                                           53 'WEEKS'
     O                                           64 'HEAD'
     O                                           75 'ENDING'
     O                                           85 'ENDING'
     O                                          104 'FARM SITE NAME'
      *
     O          e            h1hdr          1
     O                       dash               115
      *
      *-------------------------------------------------------------
      * Detail line
      *-------------------------------------------------------------
      *
     O          e            l1dtl       1
     O                       wdcnfscd      z      8
     O                       wdfscd        z     19
     O                       wdppcd              29
     O                       wdinhd        k     45
     O                       wdwkno        k     52
     O                       wdavhd        k     65
     O                       wdfmdy              75 '  /  /  '
     O                       wdtmdy              85 '  /  /  '
     O                       wdfsnm             115
      *
      *-------------------------------------------------------------
      * Summary version line
      *-------------------------------------------------------------
      *
     O          e            t1tot       1
     O               88      wscnfscd      z      8
     O              n88      t1text         b    30
     O                       wsinhd        k     45
     O                       wsavhd        k     65
     O               88      wsfmdy              75 '  /  /  '
     O               88      wstmdy              85 '  /  /  '
      *
      *-------------------------------------------------------------
      * Underscores
      *-------------------------------------------------------------
      *
     O          e            underscore  1
     O                                           44 '------------'
     O                                           64 '-------'
      *
      *-------------------------------------------------------------
      * Blank line
      *-------------------------------------------------------------
      *
     O          e            blank       1
      *
      *-------------------------------------------------------------
      * No data message line
      *-------------------------------------------------------------
      *
     O          e            nodata      1
     O                                           19 'NO DATA MEETS YOUR'
     O                                           38 'SELECTION CRITERIA'
