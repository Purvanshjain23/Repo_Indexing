     A*%%TS  SD  20040630  105810  ISLEANNER   REL-V5R2M0  5722-WDS
     A*****************************************************************
     A*
     A*  SYSTEM:        HOG PRODUCTION
     A*  DISPLAY FILE:  HPD5045 - SELECT BGF FARM SITE--ACTIVE/INACTIVE OPTIONS
     A*  CREATED BY:    LEANNE FEDOR
     A*  DATE:          05/04/04
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      PRINT
     A                                      CF04(04 'PROMPT')
     A                                      CA12(12 'EXIT')
     A N82                                  ROLLUP(25 'PAGE FORWARD')
     A*---------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP5045S1                  SFL
     A*%%TS  SD  20040504  133829  ISLRAMS     REL-V5R2M0  5722-WDS
     A            HDRRN1    R        H      REFFLD(@@RRN)
     A            D1FSCD    R        O  8  1REFFLD(@@FSCD)
     A                                      EDTCDE(M)
     A            D1FSNM    R        O  8  9REFFLD(FSFSNM)
     A            D1PTCD    R        O  8 41REFFLD(HSRREF/@@PTCD *LIBL/HSPREF)
     A            D1TFCD    R        O  8 53REFFLD(HSRREF/@@TFCD *LIBL/HSPREF)
     A            D1CELL    R        O  8 63REFFLD(HSRREF/@@CELL *LIBL/HSPREF)
     A                                      EDTCDE(M)
     A            D1AIST    R        O  8 72REFFLD(@@AIST)
     A*
     A*---------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP5045C1                  SFLCTL(HP5045S1)
     A*%%TS  SD  20040630  105810  ISLEANNER   REL-V5R2M0  5722-WDS
     A                                      SFLSIZ(0100)
     A                                      SFLPAG(0009)
     A                                      WINDOW(HP5045EC)
     A                                      SETOF(52)
     A                                      SETOF(53)
     A                                      SETOF(54)
     A                                      RTNCSRLOC(&RCD &FLD)
     A                                      OVERLAY
     A                                      SFLCSRRRN(&HDRRN)
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A            HDPAG1    R        H      REFFLD(@@PAG)
     A                                      SFLRCDNBR
     A            RCD           10A  H
     A            FLD           10A  H
     A            HDRRN     R        H      REFFLD(@@RRN)
     A            SDPGM     R        O  1  1REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 29'SELECT BGF FARM SITE'
     A                                      DSPATR(HI)
     A            C1DESC    R        O  2 24REFFLD(@@DS30)
     A                                      DSPATR(HI)
     A                                  3  1'Position Cursor, Press Enter'
     A                                  4 43'?'
     A                                      DSPATR(HI)
     A                                  4 55'?'
     A                                      DSPATR(HI)
     A N62                              4 72'?'
     A                                      DSPATR(HI)
     A                                  5  1'Farm'
     A                                      DSPATR(HI)
     A                                  5 38'Production'
     A                                      DSPATR(HI)
     A                                  5 52'Type of'
     A                                      DSPATR(HI)
     A                                  5 69'Active/'
     A                                      DSPATR(HI)
     A                                  6  1'Site'
     A                                      DSPATR(HI)
     A                                  6  9'Name'
     A                                      DSPATR(HI)
     A                                  6 41'Type'
     A                                      DSPATR(HI)
     A                                  6 54'Farm'
     A                                      DSPATR(HI)
     A                                  6 62'Cell'
     A                                      DSPATR(HI)
     A                                  6 68'Inactive'
     A                                      DSPATR(HI)
     A            C1FSCD    R        B  7  1REFFLD(@@FSCD)
     A                                      DSPATR(HI)
     A            C1FSNM    R        B  7  9REFFLD(FSFSNM)
     A                                      CHGINPDFT(UL LC FE)
     A                                      DSPATR(HI)
     A            C1PTCD    R        B  7 41REFFLD(HSRREF/@@PTCD *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A  52                                  DSPATR(PC)
     A            C1TFCD    R        B  7 53REFFLD(HSRREF/@@TFCD *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A  53                                  DSPATR(PC)
     A            C1CELL    R        B  7 63REFFLD(HSRREF/@@CELL *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A                                      EDTCDE(M)
     A N62        C1AIST    R        B  7 72REFFLD(HSRREF/@@AIST *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A  54                                  DSPATR(PC)
     A*
     A*---------------------------------------------------------------
     A* COMMAND LINE LINE
     A*---------------------------------------------------------------
     A          R HP5045K1
     A                                      WINDOW(HP5045EC)
     A                                      OVERLAY
     A  43                             17  1'Message program failed. Call IS.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 18  1'F4=Prompt'
     A                                 18 12'F12=Previous'
      *
     A*-------------------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
     A*-------------------------------------------------------------------------
     A          R HP5045ES                  SFL SFLMSGRCD(19)
      *
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
     A*
     A*-------------------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*-------------------------------------------------------------------------
     A*
     A          R HP5045EC                  SFLCTL(HP5045ES)
     A*%%TS  SD  19980327  105628  ISLRAMS     REL-V3R7M0  5716-PW1
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A                                      WINDOW(*DFT 20 75)
     A                                      WDWBORDER((*DSPATR RI) (*CHAR '    -
     A                                          '))
     A            MSGPGM                    SFLPGMQ(10)
     A*
     A*---------------------------------------------------------------
     A* ASSUME FORMAT -- REQUIRED FOR WINDOWS
     A*---------------------------------------------------------------
     A          R ASSUME
     A*%%TS  SD  20011025  160559  ISLRAMS     REL-V4R4M0  5769-PW1
     A                                      ASSUME
     A                                      OVERLAY
     A                                 24 79' '
