/***************************************************************** */
/*                                                                 */
/*  PROGRAM NUMBER:  PDUOUPC                                       */
/*  PROGRAM NAME:    Print INVENTORY IMPACT                        */
/*  PROGRAMMER:      PURVA DROGE                                   */
/*  CREATE DATE:     11/20/98                                      */
/*                                                                 */
/*  FUNCTION:        This CLP is submitted FROM THE ORDER          */
/*                   INQUIRY FOR SHIPPING DISPLAY FILE AND WILL    */
/*                   RUN AN OPEN QUERY FILE TO MINIMIZE RECORDS.   */
/*                                                                 */
/*                   USER OPTIONS ARE PASSED THROUGH THE PARMS.    */
/*                                                                 */
/*******************************************************************/
/*  Modification History                                           */
/*  RMC  3/06/02  ADDED 'REALIZATION ITEM ONLY' PARM  - &REALZ     */
/*                                                                 */
/*                                                                 */
/*******************************************************************/
             PGM        PARM(&COMP &ITEM &WHS &XFDATE &XTDATE  +
         &RPTTYP          &PDEV &HOLD &SAVE &REALZ &COPIES)
/*-----------------------------------------------------------------*/
/*                      PARAMETER                                  */
/*-----------------------------------------------------------------*/
/* INPUT PARAMETERS                                                */
             DCL        VAR(&COMP)  TYPE(*DEC)  LEN(3 0)
             DCL        VAR(&ITEM)  TYPE(*DEC)  LEN(7 0)
             DCL        VAR(&WHS)  TYPE(*CHAR) LEN(3)
             DCL        VAR(&XFDATE) TYPE(*CHAR) LEN(7)
             DCL        VAR(&XTDATE) TYPE(*CHAR) LEN(7)
             DCL        VAR(&RPTTYP)  TYPE(*CHAR) LEN(1)
             DCL        VAR(&REALZ)  TYPE(*CHAR) LEN(1)
             DCL        VAR(&PDEV)  TYPE(*CHAR) LEN(10)
             DCL        VAR(&HOLD)  TYPE(*CHAR) LEN(10)
             DCL        VAR(&SAVE)  TYPE(*CHAR) LEN(10)
             DCL        VAR(&COPIES) TYPE(*DEC) LEN(3)

/*-----------------------------------------------------------------*/
/* PROGRAM VARIABLES                                               */
             DCL        VAR(&FDATE) TYPE(*DEC) LEN(7 0)
             DCL        VAR(&TDATE) TYPE(*DEC) LEN(7 0)
             DCL        VAR(&QRYCOMP)  TYPE(*CHAR) LEN(3)
             DCL        VAR(&QRYFDATE) TYPE(*CHAR) LEN(7)
             DCL        VAR(&QRYTDATE) TYPE(*CHAR) LEN(7)
             DCL        VAR(&QRYWHSE)  TYPE(*CHAR) LEN(3)
             DCL        VAR(&QRYITEM)  TYPE(*CHAR) LEN(7)
             DCL        VAR(&A)        TYPE(*CHAR) LEN(25)
             DCL        VAR(&B)        TYPE(*CHAR) LEN(25)
             DCL        VAR(&C)        TYPE(*CHAR) LEN(25)
             DCL        VAR(&D)        TYPE(*CHAR) LEN(25)
             DCL        VAR(&F)        TYPE(*CHAR) LEN(200)
             DCL        VAR(&COMMAND) TYPE(*CHAR) LEN(512)

/*-----------------------------------------------------------------*/
/* MOVES INPUT TO QUERY FIELDS                                     */
             CHGVAR     VAR(&FDATE) VALUE(&XFDATE)
             CHGVAR     VAR(&TDATE) VALUE(&XTDATE)
             CHGVAR     VAR(&QRYCOMP)  VALUE(&COMP)
             CHGVAR     VAR(&QRYFDATE) VALUE(&FDATE)
             CHGVAR     VAR(&QRYTDATE) VALUE(&TDATE)
             CHGVAR     VAR(&QRYITEM)  VALUE(&ITEM)

/*-----------------------------------------------------------------*/
/* OPEN QUERY FILE CONCATS                                         */
             CHGVAR     VAR(&A) VALUE('L7AIC3 *EQ ' *CAT &QRYCOMP)
             CHGVAR     VAR(&B) VALUE('*AND L7LSDT *GE ' +
                                       *CAT &QRYFDATE)
             CHGVAR     VAR(&C) VALUE('*AND L7LSDT *LE ' +
                                       *CAT &QRYTDATE)
             IF         COND(&QRYITEM *NE '0000000') THEN(DO)
               CHGVAR   VAR(&D) VALUE('*AND L7HGCD *EQ ' +
                                       *CAT &QRYITEM)
             ENDDO
             CHGVAR     VAR(&F) VALUE(&A *BCAT &B *BCAT &C *BCAT &D)


/*-----------------------------------------------------------------*/
/* OPEN QUERY                                                      */
/*-----------------------------------------------------------------*/
             OVRDBF     FILE(PDL7CPL6) OVRSCOPE(*JOB) SHARE(*YES)
             OPNQRYF    FILE((PDL7CPL6)) QRYSLT(&F) KEYFLD(*FILE)

/*-----------------------------------------------------------------*/
/* CALL                                                            */
/*-----------------------------------------------------------------*/

             SBDPRTOVR  CMDVAR(&COMMAND) FILE(PDUMPFR$) OUTQ(&PDEV) +
                          COPIES(&COPIES) HOLD(&HOLD) SAVE(&SAVE)

             CALL       PGM(QCMDEXC) PARM(&COMMAND 512)
             CALL       PGM(PDUMPFR) PARM(' ' &COMP &WHS &FDATE +
                          &TDATE &RPTTYP &REALZ &ITEM)
/*-----------------------------------------------------------------*/
/*                      PROGRAM END                                */
/*-----------------------------------------------------------------*/
 ENDPGM:

             ENDPGM
