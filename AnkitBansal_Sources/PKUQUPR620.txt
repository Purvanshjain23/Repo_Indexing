/*******************************************************************/
/* MODIFIED:  RMC 5/02/02  CHANGE TLX400J TO TLX400P               */
/*            RMC 2/01/05  ADDED CODE FOR GENIFAX SOFTWARE         */
/* 05/22/2007 LJB DLY029   REMOVED GENIFAX CODE. SEE SAVED VERSION,*/
/*                         WHICH IS NAMED PKUQUPR522.              */
/*                          (SUSAN ALSO HAS A SAVED VERSION WITH   */
/*                           ADDITIONAL CHANGES IN IT. PKUQUPRSAV) */
/*                         COMPANY NUMBER IS ADDED AS A PARM.      */
/*                         CALL PMG9XFR AND PMG6PFR FROM THE OMS   */
/*                         SYSTEM. THE 1ST RTVS THE PRINTER PARMS  */
/*                         AND THE 2ND PRINTS THE FAX COVER SHEET  */
/*                         WITH THE CORRECT OVERLAY.               */
/* 06/20/2007 SLM DLY029   PASS COMPANY ALPHA AND MOVE TO *DEC     */
/*******************************************************************/
 PKUQUPR:    PGM        PARM(&PRTF &OUTQ &OUTQLIB &CONTACT +
                          &RECIPIENT &AREACODE &FAXNUMBER &DESC &COMPA)

            DCL  VAR(&PRTF)      TYPE(*CHAR) LEN(10)
            DCL  VAR(&OUTQ)      TYPE(*CHAR) LEN(10)
            DCL  VAR(&OUTQLIB)   TYPE(*CHAR) LEN(10)
            DCL  VAR(&CONTACT)   TYPE(*CHAR) LEN(25)
            DCL  VAR(&RECIPIENT) TYPE(*CHAR) LEN(25)
            DCL  VAR(&AREACODE)  TYPE(*CHAR) LEN(3)
            DCL  VAR(&FAXNUMBER) TYPE(*CHAR) LEN(25)
            DCL  VAR(&DESC)      TYPE(*CHAR) LEN(25)
            DCL  VAR(&COMPA)     TYPE(*CHAR) LEN(3)
            DCL  VAR(&COMP)      TYPE(*DEC) LEN(3 0)

            DCL  VAR(&FTX2)      TYPE(*CHAR) LEN(75)
            DCL  VAR(&ADDPHN)    TYPE(*CHAR) LEN(25)
            DCL  VAR(&ADDR)      TYPE(*CHAR) LEN(50)  /* EMAIL ADDR */

/*----------------------------------------------------------------- */
/* PARMS FOR THE RETRIEVE OVERLAY PARAMETERS PROGRAM                */
/*----------------------------------------------------------------- */
             DCL        VAR(&TYPE) TYPE(*CHAR) LEN(2)
             DCL        VAR(&DEPT) TYPE(*CHAR) LEN(6)
             DCL        VAR(&OVERLAY) TYPE(*CHAR) LEN(8)
             DCL        VAR(&FAXPGM) TYPE(*CHAR) LEN(10)
             DCL        VAR(&FAXFILE) TYPE(*CHAR) LEN(10)
             DCL        VAR(&ADDLOBJ) TYPE(*CHAR) LEN(10)
             DCL        VAR(&SENDPHONE) TYPE(*CHAR) LEN(10)
             DCL        VAR(&SENDFAX) TYPE(*CHAR) LEN(10)
             DCL        VAR(&OVOUTQ) TYPE(*CHAR) LEN(10)
             DCL        VAR(&HOLD) TYPE(*CHAR) LEN(4)
             DCL        VAR(&SAVE) TYPE(*CHAR) LEN(4)
             DCL        VAR(&DRAWER) TYPE(*DEC) LEN(3 0)
             DCL        VAR(&PRTDEVTYPE) TYPE(*CHAR) LEN(10)
             DCL        VAR(&LENGTH) TYPE(*DEC) LEN(3 0)
             DCL        VAR(&WIDTH) TYPE(*DEC) LEN(3 0)
             DCL        VAR(&CPI) TYPE(*DEC) LEN(2 0)
             DCL        VAR(&LPI) TYPE(*DEC) LEN(2 0)
             DCL        VAR(&OVERFLOW) TYPE(*DEC) LEN(3 0)
             DCL        VAR(&ROTATE) TYPE(*CHAR) LEN(5)
             DCL        VAR(&COMMENT1) TYPE(*CHAR) LEN(50)
             DCL        VAR(&COMMENT2) TYPE(*CHAR) LEN(50)
             DCL        VAR(&COMMENT3) TYPE(*CHAR) LEN(50)
             DCL        VAR(&COMMENT4) TYPE(*CHAR) LEN(50)
             DCL        VAR(&COMMENT5) TYPE(*CHAR) LEN(50)
             DCL        VAR(&RECSTS) TYPE(*CHAR) LEN(1)

/*----------------------------------------------------------------- */
/* CHANGE VARIABLES                                                 */
/*----------------------------------------------------------------- */

             CHGVAR     VAR(&COMP) VALUE(&COMPA) /* COMPANY NUMBER */
/*----------------------------------------------------------------- */
/* RETRIEVE THE FAX COVERSHEET PROGRAM AND FILE NAMES AND THE       */
/* PRINTER PARMS FROM THE NEW COMPANY DOCUMENT DEFAULTS FILE.       */
/* OVERRIDE THE PRINTER FILE AND PRINT THE COVERSHEET.              */
/*----------------------------------------------------------------- */
             CHGVAR     VAR(&TYPE) VALUE('FX') /* FAX */
             CHGVAR     VAR(&DEPT) VALUE('CARSUM') /* CARCASS SUMMRY */

 RETRIEVE:   CALL       PGM(PMG9XFR) PARM(' ' &COMP &TYPE &DEPT +
                          &OVERLAY &FAXPGM &FAXFILE &ADDLOBJ +
                          &SENDPHONE &SENDFAX &OVOUTQ &HOLD &SAVE +
                          &DRAWER &PRTDEVTYPE &LENGTH &WIDTH &LPI +
                          &CPI &OVERFLOW &ROTATE &COMMENT1 +
                          &COMMENT2 &COMMENT3 &COMMENT4 &COMMENT5 +
                          &RECSTS)

 OVERRIDE:   OVRPRTF    FILE(&FAXFILE) DEVTYPE(&PRTDEVTYPE) +
                          PAGESIZE(&LENGTH &WIDTH) LPI(&LPI) +
                          CPI(&CPI) OVRFLW(&OVERFLOW) +
                          DRAWER(&DRAWER) PAGRTT(&ROTATE) +
                          FRONTOVL(&OVERLAY) OUTQ(&OUTQ) +
                          HOLD(&HOLD) SAVE(&SAVE)

/* PRINT THE COVER SHEET FOR A/R STATEMENTS                         */
             CHGVAR     VAR(&ADDPHN) VALUE(&AREACODE *TCAT &FAXNUMBER)
 PRINTCVR:   CALL       PGM(&FAXPGM) PARM('' &COMP &TYPE &DEPT +
                          &OVERLAY &RECIPIENT &CONTACT &ADDPHN)

 /*----------------------------------------------------------------- */
 /* GET FAX SERVER EMAIL ADDRESS. THIS WILL BE THE EXTENSION TO      */
 /* SEABOARD'S SERVER, I.E.: @FAX.SEABOARDCORP.COM                   */
 /* THE CORRECT PHONE NUMBER FROM THE INPUT PARAMETERS WILL BE       */
 /* CONCATENATED TO IT AND THE COVER SHEET AND REPORT WILL THEN BE   */
 /* FAXED TO THAT PHONE NUMBER.                                      */
 /*----------------------------------------------------------------- */
             CALL       PGM(PPJXXFR) PARM(' ' 'FAXSERVER' &ADDR)
             CHGVAR     VAR(&FTX2) VALUE(&ADDPHN *TCAT &ADDR)

 /*----------------------------------------------------------------- */
 /* THE COVER SHEET ATTACHMENT MUST BE SENT AS A *PDF OR THE         */
 /* LOGO WILL NOT PRINT.                                             */
 /*----------------------------------------------------------------- */
             ESEND/ESNDMAIL RECIPIENT(&FTX2) SUBJECT(&RECIPIENT) +
                          MSG(*NONE) MSGTYPE(*TEXT) ATTLIST((* *PDF +
                          *N &FAXFILE) (* *PDF *N &PRTF))

             DLTSPLF    FILE(&PRTF) SELECT(*ALL)
             MONMSG     MSGID(CPF3309)
             DLTSPLF    FILE(&FAXFILE) SELECT(*ALL)
             MONMSG     MSGID(CPF3309)

 END:       ENDPGM
