     A*%%TS  SD  20220815  114528  ISELOUC     REL-V7R3M0  5770-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD720
     A*  TITLE:         VIEW FEED REFERENCE TICKET
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  DATE:          05/10/94
     A*
     A*****************************************************************
     A* P405   DN  04/03/20 - Bin Code (BNCD) was increased from 5A to
     A*                       6A. Shifted 'Hog Group' & 'Ration/Add' to
     A*                       the right 1 space.
     A*****************************************************************
     A* SDN736 RECOMPILED FOR TICKET NBR INCREASE (TKNO & RTNO 7.0 TO 9.0)
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A N90                                  CF05(05 'REFRESH')
     A                                      CA12(12 'PREVIOUS')
     A N82N90                               ROLLUP(25 'PAGE FORWARD')
     A*
     A*
     A*--------------------------------------------------------------*
     A* SUBFILE FORMAT ONE
     A*--------------------------------------------------------------*
     A          R HP720S1                   SFL
     A*%%TS  SD  20220815  113435  ISELOUC     REL-V7R3M0  5770-WDS
     A  85                                  SFLNXTCHG
     A            HDRRN1         4Y 0H
     A            DFTKNO    R        O 12  2REFFLD(FHTKNO)
     A                                      EDTCDE(M)
     A            DFMDY     R        O 12 13REFFLD(@@DT06)
     A                                      EDTCDE(Y)
     A            DFTRCD    R        O 12 22REFFLD(FHTRCD)
     A            DFTSCD    R        O 12 24REFFLD(FHTSCD)
     A            DFFSCD    R        O 12 27REFFLD(FDFSCD)
     A                                      EDTCDE(M)
     A            DFBNCD    R        O 12 34REFFLD(HSRREF/BNBNCD *LIBL/HSPREF)
P405 A**          DFHGCD    R        O 12 39REFFLD(HSRREF/HGHGCD *LIBL/HSPREF)
     A            DFHGCD    R        O 12 41REFFLD(HSRREF/HGHGCD *LIBL/HSPREF)
P405 A**          DFRNCD    R        O 12 47REFFLD(HSRREF/RNRNCD *LIBL/HSPREF)
     A            DFRNCD    R        O 12 49REFFLD(HSRREF/RNRNCD *LIBL/HSPREF)
     A            DFADCD    R        O 12 53REFFLD(HSRREF/RAADCD *LIBL/HSPREF)
     A            DFFDLB    R        O 12 59REFFLD(HSRREF/FDFDLB *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A            DFAM      R        O 12 70REFFLD(HSRREF/FDINAM *LIBL/HSPREF)
     A                                      EDTCDE(M)
     A*
     A*--------------------------------------------------------------*
     A* SUBFILE CONTROL FORMAT ONE
     A*--------------------------------------------------------------*
     A          R HP720C1                   SFLCTL(HP720S1)
     A*%%TS  SD  20220815  114528  ISELOUC     REL-V7R3M0  5770-WDS
     A                                      SFLSIZ(0050)
     A                                      SFLPAG(0010)
     A                                      RTNCSRLOC(&RCD &FLD)
     A                                      OVERLAY
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 26'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  2 24'VIEW FEED REFERENCE TICKET'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  4  2'Farm site ................:'
     A            C1FSCD    R        O  4 31REFFLD(FHFSCD)
     A                                      EDTCDE(M)
     A            C1FSNM    R        O  4 40REFFLD(FSREC/FSFSNM *LIBL/HSP018)
     A                                  5  2'Feed mill business office :'
     A            C1FMBO    R        O  5 31REFFLD(FHFMBO)
     A            C1BODS    R        O  5 40REFFLD(BOBODS)
     A                                  6  2'Reference ticket number ..:'
     A            C1RTNO         9Y 0O  6 31EDTCDE(M)
     A                                  7  2'Vendor invoice number ....:'
     A            C1VINO    R        O  7 31REFFLD(FHVINO)
     A                                  8 57'-------- TOTALS ------'
     A                                      DSPATR(HI)
     A            C1FDLB         7Y 0O  9 58EDTCDE(K)
     A            C1AM      R        O  9 69REFFLD(FDINAM)
     A                                      EDTCDE(M)
     A                                 10  5'Ticket'
     A                                      DSPATR(HI)
     A                                 10 14'Ticket'
     A                                      DSPATR(HI)
     A                                 10 22'T'
     A                                      DSPATR(HI)
     A                                 10 27'Farm'
     A                                      DSPATR(HI)
P405 A**                               10 38'Hog'
     A                                 10 41'Hog'
     A                                      DSPATR(HI)
     A                                 11  5'Number'
     A                                      DSPATR(HI)
     A                                 11 15'Date'
     A                                      DSPATR(HI)
     A                                 11 22'C'
     A                                      DSPATR(HI)
     A                                 11 24'St'
     A                                      DSPATR(HI)
     A                                 11 27'Site'
     A                                      DSPATR(HI)
     A                                 11 34'Bin'
     A                                      DSPATR(HI)
P405 A**                               11 38'Group'
     A                                 11 41'Group'
     A                                      DSPATR(HI)
P405 A**                               11 46'Ration/Add'
     A                                 11 49'Ration/Add'
     A                                      DSPATR(HI)
     A                                 11 61'Pounds'
     A                                      DSPATR(HI)
     A                                 11 74'Costs'
     A                                      DSPATR(HI)
     A*
     A*--------------------------------------------------------------*
     A* COMMAND KEY RECORD 1
     A*--------------------------------------------------------------*
     A          R HP720K1
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A N90                             23 11'F5=Refresh'
     A                                 23 23'F12=Previous'
      *--------------------------------------------------------------*
      * ERROR MESSAGE SUBFILE FORMAT
      *--------------------------------------------------------------*
     A          R HP720ES                   SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *                                                                *********
      *--------------------------------------------------------------*
      * ERROR MESSAGE SUBFILE CONTROL FORMAT
      *--------------------------------------------------------------*
     A          R HP720EC                   SFLCTL(HP720ES)
      *
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
