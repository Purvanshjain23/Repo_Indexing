/***************************************************************** */
/*                                                                 */
/*  ENVIRONMENT -----> PORK                                        */
/*  SYSTEM ----------> ACCOUNTS RECEIVABLE SYSTEM                  */
/*  PROGRAM NUMBER --> AR605CLP                                    */
/*  PROGRAM NAME ----> CUSTOMER STATEMENT                          */
/*  PROGRAMMER ------> LEANNE FEDOR                                */
/*  DATE ------------> 03/12/98                                    */
/*                                                                 */
/*  PROGRAM PURPOSE -> GENERATE A CUSTOMER STATEMENT.  THIS        */
/*                     NEW REPORT WAS REQUESTED BY DENNIS HUMMEL.  */
/*                                                                 */
/***************************************************************** */
/* MODIFICATIONS                                                   */
/* RMC 8/02/02 ------> ADDED PARM FOR STMT TYPE:N=NOT AGED,A=AGED  */
/*     8/13/02 ------> ADDED PARM FOR PRINT OR FAX STMT TO CONTROL */
/*                     PRTOVR THAT IS EXECUTED                     */
/* RMC 4/28/05 ------> REMOVED COMPANY PARM  -- ! NOT YET !        */
/* RMC 7/21/05 ------> POQZPFR - CHANGE TO NEW PGM WHEN CO IS REMVD*/
/* SAZ 8/26/05 ------> ADD PARM TO INDICATE PRINT CO ON STATEMENT  */
/*                     CREATE QRY TO SELECT ONE COMPANY OR ALL     */
/*                     COMPANIES.                                  */
/* SAZ 8/26/05 ------> ADD PARM &DRAWER, SO THAT MSL CAN PRINT     */
/*                     OUT OF DRAWER 3. THE NUMBER WILL BE PASSED  */
/*                     IN FROM THE JOB SCHEDULER JOB OR FROM THE   */
/*                     PGM THAT CALLS THIS FROM THE PROMPT.        */
/*                     OUTQ, COPIES, AND HOLD ARE NOW PASSED IN    */
/*                     ALSO.                                       */
/* RMC 12/16/5 ------> POQZPFR - CHANGE TO NEW PGM NOW.            */
/* LJB 07/07/2006      ADD COMPANY NUMBER PARM TO CALL TO AGE RPT. */
/*                     NEEDED TO PASS TO THE AGING PROGRAM TO      */
/*                     TEST FOR ZEROS FOR *ALL, FOR PPRKPFR        */
/* LJB 04/20/2010 P000198  CHANGE PRINTER FILE OVERRIDES TO =      */
/*                     HOLD(YES) AND SAVE(*YES) FOR FAX OR EMAIL   */
/*                     TO MULTIPLE RECIPIENTS                      */
/* LJB 05/04/2012 E000909  PASS COMPANY ORG AND A/R OUTQ. IF NOT   */
/*        DAILY'S THEN DUP SPLF TO A/R'S OUTPUT QUEUE. THAT OUTQ   */
/*        IS DEFINED IN SYSTEM VALUES: ARSTMTOUTQ                  */
/* LJB 05/10/2012 E002085  USE THE HOLD AND SAVE VALUE PASSED IN.  */
/*        COMMENT OUT THE HARD CODED VALUES OF *YES FOR BOTH.      */
/***************************************************************** */
             PGM        PARM(&COMPANY &CUSTNBR &AROUTQ &COMPORG +
                          &CUSTNM &CREDIT &CHARGE &STMTTP &PRTFAX +
                          &OUTQ &HOLD &SAVE &COPYN &PRTCO &DRAWER)

/* DEFINE THE VARIABLE REQUIRED IN THE COMMAND TO OVERRIDE THE     */
/* PRINT FILE TO A LASER PRINTER                                   */

             DCL        VAR(&CMD) TYPE(*CHAR) LEN(512)
             DCL        VAR(&QDATA) TYPE(*CHAR) LEN(150)

/* PARMS DEFINED AS SENT IN BY SYNON FUNCTION                      */

             DCL  VAR(&COMPANY) TYPE(*DEC)
             DCL  VAR(&CUSTNBR) TYPE(*DEC)
             DCL  VAR(&CUSTNM)  TYPE(*CHAR) LEN(30)
             DCL  VAR(&COMPORG) TYPE(*CHAR) LEN(1)
             DCL  VAR(&CREDIT)  TYPE(*CHAR) LEN(1)
             DCL  VAR(&CHARGE)  TYPE(*CHAR) LEN(1)
             DCL  VAR(&STMTTP)  TYPE(*CHAR) LEN(1)
             DCL  VAR(&PRTFAX)  TYPE(*CHAR) LEN(1)

             DCL  VAR(&COPYN)   TYPE(*DEC)
             DCL  VAR(&OUTQ)    TYPE(*CHAR) LEN(10)
             DCL  VAR(&AROUTQ)  TYPE(*CHAR) LEN(10)
             DCL  VAR(&SPLF)    TYPE(*CHAR) LEN(10)
             DCL  VAR(&HOLD)    TYPE(*CHAR) LEN(4)
             DCL  VAR(&SAVE)    TYPE(*CHAR) LEN(4)
             DCL  VAR(&PRTCO)    TYPE(*CHAR) LEN(1)

/* PARMS DEFINED WITH ACTUAL LENGTHS FOR PASSING WITH CALLED PROGRAMS */

             DCL  VAR(&COMP)   TYPE(*DEC) LEN(3 0)
             DCL  VAR(&CUST)   TYPE(*DEC) LEN(7 0)
             DCL  VAR(&CUSTA)  TYPE(*CHAR) LEN(7)
             DCL  VAR(&COPIES) TYPE(*DEC) LEN(3 0)
             DCL  VAR(&DRAWER) TYPE(*CHAR) LEN(1)

/* CHANGE INCOMING PARMS TO BE ACTUAL LENGTHS                         */

             CHGVAR    VAR(&COMP) VALUE(&COMPANY)
             CHGVAR    VAR(&CUST) VALUE(&CUSTNBR)
             CHGVAR    VAR(&CUSTA) VALUE(&CUST)
             CHGVAR    VAR(&COPIES) VALUE(&COPYN)
/********    CHGVAR    VAR(&HOLD) VALUE(*YES)               ********/
/********    CHGVAR    VAR(&SAVE) VALUE(*YES)               ********/

/*-----------------------------------------------------------------*/
/*  SET UP QUERY FOR SELECTED COMPANY                              */
/*-----------------------------------------------------------------*/
             CHGVAR    VAR(&QDATA) VALUE(' ')

/* ONLY SELECT RECORDS FOR THE USER'S SELECTED COMPANY             */

             IF         COND(&COMP *GT 0) THEN(DO)

             CHGVAR     VAR(%SST(&QDATA 1 10)) VALUE('BDAIC3 *EQ')
             CHGVAR     VAR(%SST(&QDATA 12 3)) VALUE(&COMP)

/* ONLY SELECT RECORDS FOR THE USER'S SELECTED CUSTOMER               */

             CHGVAR     VAR(%SST(&QDATA 16 15)) VALUE('*AND BDANC7 *EQ')
             CHGVAR     VAR(%SST(&QDATA 32 7)) VALUE(&CUST)

/* ALWAYS OMIT RECORDS WITH A ZERO BALANCE DUE                       */

             CHGVAR     VAR(%SST(&QDATA 40 17)) VALUE('*AND BDBGVA +
                          *NE 0')

/* ALWAYS OMIT RECORDS WITH AN ADJUSTMENT TYPE OF ARM OR ARC.        */

             CHGVAR     VAR(%SST(&QDATA 58 43)) VALUE('*AND BDUHCD +
                          *NE "ARC" *AND BDUHCD *NE "ARM"')

/* OMIT CHARGEBACKS IF THE USER DOES NOT WANT TO SEE THEM       */

             IF         COND(&CHARGE *EQ 'N') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 103 21)) VALUE(' *AND BDCEST +
                          *NE "CB"')
             ENDDO

             OVRDBF     FILE(ARBDCPL6) SHARE(*YES)
             OPNQRYF    FILE((ARBDCPL6)) QRYSLT(&QDATA) +
                          KEYFLD(*FILE) SEQONLY(*YES)
             ENDDO

/*------------------------------------------------------------------*/
/*  SET UP QUERY IF NO COMPANY SELECTED                             */
/*------------------------------------------------------------------*/
/*B:SZ050826             */
 /*ALL COMPANIES */
             IF         COND(&COMP *EQ 0) THEN(DO)

/* ONLY SELECT RECORDS FOR THE USER'S SELECTED CUSTOMER               */

             CHGVAR     VAR(%SST(&QDATA 1 10)) VALUE('BDANC7 *EQ')
             CHGVAR     VAR(%SST(&QDATA 12 7)) VALUE(&CUST)

/* ALWAYS OMIT RECORDS WITH A ZERO BALANCE DUE                       */

             CHGVAR     VAR(%SST(&QDATA 20 17)) VALUE('*AND BDBGVA +
                          *NE 0')

/* ALWAYS OMIT RECORDS WITH AN ADJUSTMENT TYPE OF ARM OR ARC.        */

             CHGVAR     VAR(%SST(&QDATA 38 43)) VALUE('*AND BDUHCD +
                          *NE "ARC" *AND BDUHCD *NE "ARM"')

/* OMIT CHARGEBACKS IF THE USER DOES NOT WANT TO SEE THEM       */

             IF         COND(&CHARGE *EQ 'N') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 81 21)) VALUE(' *AND BDCEST +
                          *NE "CB"')
             ENDDO

             OVRDBF     FILE(ARBDCPL6) SHARE(*YES)
             OPNQRYF    FILE((ARBDCPL6)) QRYSLT(&QDATA) +
                          KEYFLD((BDANC7) (BDCONB) (BDCEST)) +
                          SEQONLY(*YES)
             ENDDO
/*E:SZ050826             */
/*------------------------------------------------------------------*/
/* PRINT NOT AGED REPORT                                            */
/*------------------------------------------------------------------*/
 NOTAGED:    IF         COND(&STMTTP = 'N') THEN(DO)

/* USE THE CUSTOMIZED SEABOARD COMMAND TO OVERRIDE THE PRINT FILE.  */

 PRINTN:     IF         COND(&PRTFAX = 'Y') THEN(DO)
             SBDPRTOVR  CMDVAR(&CMD) FILE(QPRINT) OUTQ(&OUTQ) +
                          WIDTH(84) LANDSCAPE(*NO) DRAWER(&DRAWER) +
                          COPIES(&COPIES) HOLD(&HOLD) SAVE(&SAVE) +
                          USRDTA(&CUSTA)
             CALL       PGM(QCMDEXC) PARM(&CMD 512)
             ENDDO      /* END PRTFAX = Y */

             ELSE       CMD(OVRPRTF FILE(QPRINT) PAGESIZE(*N 84) +
                          PAGRTT(0) OUTQ(FAXQ) USRDTA(&CUSTA))

             CALL       PGM(AR605) PARM(&CUST &CUSTNM &CREDIT &PRTCO +
                          &COMP)

/* FOR ALL BUT DAILY'S, DUP THE SPOOLED FILE TO A/R OUTQ SO IT       */
/* PRINTS PRIOR TO FAXING/EMAILING.                 E000909          */
             IF         COND(&COMPORG *NE 'D') THEN(DO)
             IF         COND(&AROUTQ *NE '          ') THEN(DUPSPLF +
                          FILE(QPRINT) OUTQ(&AROUTQ) SPLNBR(*LAST) +
                          HOLD(*NO) SAVE(*NO) USRDTA(&CUSTA))
             ENDDO      /* END COMPANY ORG NOT DAILY'S */

             ENDDO      /* END STMTTYP = N */

             CLOF       OPNID(ARBDCPL6)
             MONMSG     MSGID(CPF0000)
             DLTOVR     FILE(*ALL)

/*------------------------------------------------------------------*/
/*  PRINT AGED REPORT                                               */
/*------------------------------------------------------------------*/
 AGED:       IF         COND(&STMTTP = 'A') THEN(DO)

/* THIS REPORT IS PRINTS LANDSCAPE ON A LASER PRINTER                */

 PRINTA:     IF         COND(&PRTFAX = 'Y') THEN(DO)

/*B:SZ050826 CHANGE PRT FILE                                         */
             SBDPRTOVR  CMDVAR(&CMD) FILE(PPRKPFR$) OUTQ(&OUTQ) +
                          WIDTH(144) LANDSCAPE(*YES) +
                          DRAWER(&DRAWER) COPIES(&COPIES) +
                          HOLD(&HOLD) SAVE(&SAVE) USRDTA(&CUSTA)
             CALL       PGM(QCMDEXC) PARM(&CMD 512)
             ENDDO      /* END OVERRIDE PPRKPFR */

             ELSE       CMD(OVRPRTF FILE(PPRKPFR$) PAGESIZE(*N 144) +
                          OUTQ(FAXQ) USRDTA(&CUSTA))

             OVRDBF     FILE(ARBDCPLZ) SHARE(*YES)
             OPNQRYF    FILE((ARBDCPLZ)) QRYSLT(&QDATA) +
                          KEYFLD(*FILE) SEQONLY(*YES)

/* 07/07/2006 ADD COMPANY NUMBER WHICH WILL BE PASSED TO THE         */
/* AGING PROGRAM TO TEST FOR ZEROS TO PROCESS ALL COMPANIES OR       */
/* TO PROCESS ONLY THE COMPANY PASSED IN.                            */
             CALL       PGM(PPRKPFR) PARM(' ' &CREDIT &PRTCO &COMP)

/* 05/04/2012 LJB E000909  FOR ALL BUT DAILY'S, DUP THE SPOOLED      */
/* FILE TO A/R OUTQ SO IT PRINTS PRIOR TO FAXING/EMAILING.           */
             IF         COND(&COMPORG *NE 'D') THEN(DO)
             IF         COND(&AROUTQ *NE '          ') THEN(DUPSPLF +
                          FILE(PPRKPFR$) OUTQ(&AROUTQ) +
                          SPLNBR(*LAST) HOLD(*NO) SAVE(*NO) +
                          USRDTA(&CUSTA))
             ENDDO      /* END COMPANY ORG NOT DAILY'S */

             CLOF       OPNID(ARBDCPLZ)
             MONMSG     MSGID(CPF0000)

             ENDDO      /* END STMTTYP = A */
             DLTOVR     FILE(*ALL)

 END:        ENDPGM
