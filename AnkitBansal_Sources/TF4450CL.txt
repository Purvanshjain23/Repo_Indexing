/***************************************************************** */
/*                                                                 */
/*  SYSTEM:          TRIUMPH FOODS                                 */
/*  PROGRAM NUMBER:  TF4450CL                                      */
/*  PROGRAM NAME:    SPECIFY PRODUCT SPLIT LISTING OPTIONS         */
/*  PROGRAMMER:      LEANNE RAMSEY                                 */
/*  CREATE DATE:     10/11/06                                      */
/*                                                                 */
/*******************************************************************/
/* MODIFICATIONS                                                   */
/*                                                                 */
/* 05/06/09  LeAnne Ramsey                                         */
/*           Changed print logic to match Meat Costing.            */
/*                                                                 */
/* 06/04/10  LeAnne Ramsey                                         */
/*           Changed from FROM/TO Dates to CutOff Effective Date.  */
/*******************************************************************/

             PGM

             DCL        VAR(&QDATA) TYPE(*CHAR) LEN(160)

             DCL        VAR(&OUTQ) TYPE(*CHAR) LEN(10)
             DCL        VAR(&HOLD) TYPE(*CHAR) LEN(4)
             DCL        VAR(&SAVE) TYPE(*CHAR) LEN(4)
             DCL        VAR(&COPY) TYPE(*DEC)  LEN(1)

             DCL        VAR(&LDCCYMD) TYPE(*DEC) LEN(8)

             DCL        VAR(&LDPRCD) TYPE(*DEC) LEN(7)
             DCL        VAR(&LDISTYCD) TYPE(*DEC) LEN(3)
             DCL        VAR(&LDTFCGCD) TYPE(*CHAR) LEN(2)
             DCL        VAR(&LDTFCLCD) TYPE(*CHAR) LEN(3)
             DCL        VAR(&LDAIST)   TYPE(*CHAR) LEN(1)

             CHGVAR     VAR(&LDCCYMD) VALUE(%SST(*LDA 15 8))
             CHGVAR     VAR(&LDPRCD) VALUE(%SST(*LDA 29 7))
             CHGVAR     VAR(&LDISTYCD) VALUE(%SST(*LDA 36 3))
             CHGVAR     VAR(&LDTFCGCD) VALUE(%SST(*LDA 39 2))
             CHGVAR     VAR(&LDTFCLCD) VALUE(%SST(*LDA 41 3))
             CHGVAR     VAR(&LDAIST)   VALUE(%SST(*LDA 44 1))

/*----------------------------------------------------------------*/
/* SET PARAMETERS FROM LDA FOR LISTING                            */
/*----------------------------------------------------------------*/

 OUTQ:       CHGVAR     VAR(&OUTQ) VALUE(%SST(*LDA 401 10))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(*JOB))

 HOLD:       CHGVAR     VAR(&HOLD) VALUE(%SST(*LDA 411 4))
             IF         COND(&HOLD *NE '*YES' *AND &HOLD *NE '*NO ') +
                          THEN(CHGVAR VAR(&HOLD) VALUE(*NO))

 SAVE:       CHGVAR     VAR(&SAVE) VALUE(%SST(*LDA 415 4))
             IF         COND(&SAVE *NE '*YES' *AND &SAVE *NE '*NO ') +
                          THEN(CHGVAR VAR(&SAVE) VALUE(*NO))

 COPY:       CHGVAR     VAR(&COPY) VALUE(%SST(*LDA 419 1))
             IF         COND(&COPY = 0) THEN(CHGVAR VAR(&COPY) +
                          VALUE(1))


/*---------------------------------------------------------------------*/
/* EXTRACT THE DATA FROM THE PRODUCT SPLIT FILE                        */
/*---------------------------------------------------------------------*/

             CHGVAR     VAR(&QDATA) VALUE(' ')

/* CUT OFF DATE                                                         */
             CHGVAR     VAR(%SST(&QDATA 1 12)) VALUE('PSEFDT *NE 0')

/* CUT OFF DATE                                                         */
             IF         COND(&LDCCYMD *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 14 15)) VALUE('*AND PSEFDT *LE')
             CHGVAR     VAR(%SST(&QDATA 30 8)) VALUE(&LDCCYMD)
             ENDDO

/* PRODUCT CODE                                                         */

             IF         COND(&LDPRCD *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 46 15)) VALUE('*AND PSPRCD *EQ')
             CHGVAR     VAR(%SST(&QDATA 62 7)) VALUE(&LDPRCD)
             ENDDO

/* ITEM STRUCTURE TYPE CODE                                            */

             IF         COND(&LDISTYCD *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 70 17)) VALUE('*AND +
                          PSISTYCD *EQ')
             CHGVAR     VAR(%SST(&QDATA 88 3)) VALUE(&LDISTYCD)
             ENDDO

/* TF CLASS GROUP CODE                                                 */

             IF         COND(&LDTFCGCD *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 92 19)) VALUE('*AND +
                          PSTFCGCD *EQ "')
             CHGVAR     VAR(%SST(&QDATA 111 2)) VALUE(&LDTFCGCD)
             CHGVAR     VAR(%SST(&QDATA 113 1)) VALUE('"')
             ENDDO

/* TF CLASSIFICATION CODE                                              */

             IF         COND(&LDTFCLCD *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 115 19)) VALUE('*AND +
                          PSTFCLCD *EQ "')
             CHGVAR     VAR(%SST(&QDATA 134 3)) VALUE(&LDTFCLCD)
             CHGVAR     VAR(%SST(&QDATA 137 1)) VALUE('"')
             ENDDO

/* ACTIVE/INACTIVE STATUS                                              */

             IF         COND(&LDAIST *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 139 17)) VALUE('*AND +
                          PSAIST *EQ "')
             CHGVAR     VAR(%SST(&QDATA 156 1)) VALUE(&LDAIST)
             CHGVAR     VAR(%SST(&QDATA 157 1)) VALUE('"')
             ENDDO

/* OVERRIDE PRINT FILE                                                */

             OVRPRTF    FILE(PRINT198) OUTQ(&OUTQ) COPIES(&COPY) +
                          HOLD(&HOLD) SAVE(&SAVE)

/* OVERRIDE PRODUCT SPLIT FILE                                          */

             OVRDBF     FILE(TFP045)  SHARE(*YES)
             OPNQRYF    FILE((TFP045)) OPTION(*INP) QRYSLT(&QDATA) +
                          KEYFLD((PSPRCD) (PSEFDT *DESCEND)) +
                          SEQONLY(*YES)
             CALL       PGM(TF645)
             CLOF       OPNID(TFP045)
             DLTOVR     FILE(TFP045)

/*---------------------------------------------------------------------*/
/* END OF PROCESSING                                                   */
/*---------------------------------------------------------------------*/

             DLTOVR     FILE(*ALL)

             ENDPGM

