/*********************************************************************/
/* PROGRAM : PUDIUPC                                                 */
/* DESC    : PRT MANFST DTL EDI RCP CL EXECUTE USER PROGRAM          */
/* DATE    : 07/25/2017                                              */
/* PROJECT : T11041 - 3PL EDI TRANSACTION                            */
/* BY      : DANNY NGUYEN                                            */
/* PURPOSE : Build WF EDI Whs Stk Txfr Diff (PUA1CPP) file in QTEMP  */
/*           & print Summary or Detail report for Warehouse Transfer */
/*           Orders (EDI 944) Received vs In-Transit.                */
/*           Call SEQUEL to print report as an excel & send via      */
/*           ESEND.                                                  */
/*********************************************************************/
/* MAINTENANCE LOG:                                                  */
/*-------------------------------------------------------------------*/
/* DATE     BY  CHANGE DESCRIPTION                                   */
/* -------- --- ---------------------------------------------------- */
/* 09/10/18 DN  R113315- Added PUA1CPL1 to override to QTEMP.        */
/* 07/13/18 DN  T11041 - Renamed PUA1CPPSUM to TRANSFRSUM. Added     */
/*                       TRANSFRDTL to print Detail report by        */
/*                       Order/Item level.                           */
/*                                                                   */
/*********************************************************************/

             PGM        PARM(&COMP &ORDER &ITEM &WHSFM &WHSTO &EMAIL +
                          &RPTTY &QTRCVGTSHP)

             DCL        VAR(&RTNCDE) TYPE(*CHAR) LEN(7)
             DCL        VAR(&COMP) TYPE(*DEC) LEN(3 0)
             DCL        VAR(&COMPA) TYPE(*CHAR) LEN(3)
             DCL        VAR(&ORDER) TYPE(*DEC) LEN(7 0)
             DCL        VAR(&ORDERA) TYPE(*CHAR) LEN(7)
             DCL        VAR(&ITEM) TYPE(*DEC) LEN(7 0)
             DCL        VAR(&ITEMA) TYPE(*CHAR) LEN(7)
             DCL        VAR(&WHSFM) TYPE(*CHAR) LEN(3)
             DCL        VAR(&WHSTO) TYPE(*CHAR) LEN(3)
             DCL        VAR(&RTNLIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&EMAIL) TYPE(*CHAR) LEN(50)
             DCL        VAR(&RPTTY) TYPE(*CHAR) LEN(1)
             DCL        VAR(&QTRCVGTSHP) TYPE(*CHAR) LEN(1)
             DCL        VAR(&EMLMSG) TYPE(*CHAR) LEN(110)
             DCL        VAR(&BYITEM) TYPE(*CHAR) LEN(7)

             CHGVAR     VAR(&COMPA) VALUE(&COMP)
             CHGVAR     VAR(&ORDERA) VALUE(&ORDER)
             CHGVAR     VAR(&ITEMA) VALUE(&ITEM)

/*-------------------------------------------------------------------*/
/* Build WF EDI Whs Stk Txfr Diff (PUA1CPP) file in QTEMP.           */
/*-------------------------------------------------------------------*/
             CRTDUPOBJ  OBJ(PUA1CPP) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)
             MONMSG     MSGID(CPF2130) EXEC(CLRPFM FILE(QTEMP/PUA1CPP))
             CRTDUPOBJ  OBJ(PUA1CPL0) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)
             MONMSG     MSGID(CPF2130) /* Object duplicated/not +
                          duplicated */
             CRTDUPOBJ  OBJ(PUA1CPL1) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)
             MONMSG     MSGID(CPF2130) /* Object duplicated/not +
                          duplicated */

             OVRDBF     FILE(PUA1CPP) TOFILE(QTEMP/PUA1CPP)
             OVRDBF     FILE(PUA1CPL0) TOFILE(QTEMP/PUA1CPL0)
             OVRDBF     FILE(PUA1CPL1) TOFILE(QTEMP/PUA1CPL1)

             CALL       PGM(PUFFXFR) PARM(&RTNCDE &COMP &ORDER +
                          &WHSFM &WHSTO &ITEM &RPTTY &QTRCVGTSHP)

/*-------------------------------------------------------------------*/
/* Create Excel Spreadsheet with SEQUEL Viewpoint and email to       */
/* receipient.                                                       */
/*-------------------------------------------------------------------*/

    /* Format Data to Blanks. */
         /* Item Code */
             IF         COND(&ITEMA = '0000000') THEN(DO)
             CHGVAR     VAR(&ITEMA) VALUE('*ALL')
             ENDDO

    /* Print Order Summary Receipt-EDI 944 */
             IF         COND(&RPTTY = 'S') THEN(DO)
             CHGVAR     VAR(&EMLMSG) VALUE('Manifest Order Summary +
                          Rcvd/In-Transit EDI: TRANSFRSUM Export to +
                          Excel')

    /* If TRANSFRSUM return library not PRKPLIB then set 'TEST' in Email Msg */
             RTVOBJD    OBJ(TRANSFRSUM) OBJTYPE(*USRSPC) +
                          RTNLIB(&RTNLIB)
             IF         COND(&RTNLIB *NE 'PRKPLIB') THEN(DO)
             CHGVAR     VAR(&EMLMSG) VALUE(&EMLMSG *BCAT '- TEST')
             ENDDO

    /* Call SEQUEL to Email Summary Report */
             EXECUTE    VIEW(TRANSFRSUM) PCFMT(*XLS) +
                          RECIPIENT(&EMAIL) EMLMSG(&EMLMSG) +
                          SETVAR((&COMP &COMPA) (&ORDER &ORDERA) +
                          (&ITEM &ITEMA) (&WHSFM &WHSFM) (&WHSTO +
                          &WHSTO))
             ENDDO

    /* Print Order Detail Receipt-EDI 944 */
             IF         COND(&RPTTY = 'D') THEN(DO)
             CHGVAR     VAR(&EMLMSG) VALUE('Manifest Order Items +
                          Detail Rcvd/In-Transit EDI: TRANSFRDTL +
                          Export to Excel')
         /* Show By Item # in Email Message. */
             IF         COND(&ITEMA *NE '*ALL') THEN(DO)
             CHGVAR     VAR(&EMLMSG) VALUE(&EMLMSG *BCAT '- For +
                          Item#' *BCAT &ITEMA)
             ENDDO

    /* If TRANSFRDTL return library not PRKPLIB then set 'TEST' in Email Msg */
             RTVOBJD    OBJ(TRANSFRDTL) OBJTYPE(*USRSPC) +
                          RTNLIB(&RTNLIB)
             IF         COND(&RTNLIB *NE 'PRKPLIB') THEN(DO)
             CHGVAR     VAR(&EMLMSG) VALUE(&EMLMSG *BCAT '- TEST')
             ENDDO

    /* Call SEQUEL to Email Detail Report */
             EXECUTE    VIEW(TRANSFRDTL) PCFMT(*XLS) +
                          RECIPIENT(&EMAIL) EMLMSG(&EMLMSG) +
                          SETVAR((&COMP &COMPA) (&ORDER &ORDERA) +
                          (&ITEM &ITEMA) (&WHSFM &WHSFM) (&WHSTO +
                          &WHSTO))
             ENDDO


             DLTOVR     FILE(*ALL)
             DLTF       FILE(QTEMP/PUA1CP*)

 ENDPGM:
             ENDPGM
