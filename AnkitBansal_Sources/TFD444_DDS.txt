     A*%%TS  SD  20080910  131720  ISLRAMS     REL-V5R4M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        TRIUMPH FOODS
     A*  DISPLAY FILE:  TFD444
     A*  TITLE:         SPECIFY UPDATE OF POAXCPP-ITEM STANDARD COSTS
     A*  PROGRAMMER:    LEANNE RAMSEY
     A*  DATE:          10/02/06
     A*****************************************************************
     A*  RMC 6/2/17 T10474 ADDED COMPANY OUTPUT AND DIFF FILE NAME FOR 440
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/TFPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A                                      CF04(04 'PROMPT')
     A                                      CF08(08 'SUBMIT')
     A                                      CF10(10 'VIEW')
     A                                      CF17(17 'PRINT OPTIONS')
     A                                      CF20(20 'WRKSPLF')
     A*
     A*--------------------------------------------------------------*
     A* RECORD FORMAT ONE
     A*--------------------------------------------------------------*
     A          R TF444R1
     A*%%TS  SD  20080910  131720  ISLRAMS     REL-V5R4M0  5722-WDS
     A                                      OVERLAY
     A                                      SETOF(31)
     A                                      SETOF(51)
     A                                      RTNCSRLOC(&RCD &FLD)
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 18'SPECIFY UPDATE OF ITEM STANDARD CO-
     A                                      STS'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USR)
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@JOB)
     A                                      DSPATR(HI)
     A                                  4  3'This function updates the Item Sta-
     A                                      ndard Costs'
     A N44                              4 49'(POAXCPP) with values'
     A  44                              4 49'(PUACCPP) with values'
     A                                  5  3'from records in the upload file TF-
     A                                      P244. Upload file TFP244 should be'
     A                                  6  3'rebuilt with data before submittin-
     A                                      g this function.'
     A                                  8  3'Company ..:'
     A            P1PARM         3A     8 20
     A                                 10  3'Upload values ..'
     A                                 10 20'?'
     A            D1UPCD    R        B 10 22REFFLD(@@FLAG)
     A N31                                  DSPATR(HI)
     A  31
     AO 51                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A            D1UPDS        20A  O 10 25COLOR(PNK)
     A*--------------------------------------------------------------*
     A* COMMAND KEY RECORD 1
     A*--------------------------------------------------------------*
     A          R TF444K1
     A*%%TS  SD  20071009  132442  ISLRAMS     REL-V5R4M0  5722-WDS
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A                                 23 11'F4=Prompt'
     A N99                             23 22'F8=Submit'
     A                                 23 33'F10=View'
     A N99                             23 43'F17=Print Options'
     A N99                             23 62'F20=Spool Files'
      *
      *--------------------------------------------------------------*
      * ERROR MESSAGE SUBFILE FORMAT
      *--------------------------------------------------------------*
     A          R TF444ES                   SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
     A*
     A*--------------------------------------------------------------*
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*--------------------------------------------------------------*
     A*
     A          R TF444EC                   SFLCTL(TF444ES)
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
