     A*%%TS  SD  20140306  103614  ISLRAMS     REL-V6R1M0  5761-WDS
     A*****************************************************************
     A* ENVIRONMENT:  PORK DIVISION
     A* SYSTEM:       AS/400
     A* FILE:         MPD402
     A* DESCRIPTION:  MP2: WORK WITH PURCHASE ORDERS
     A* PROGRAMMER:   LEANNE FEDOR
     A* DATE:         03/18/02
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/MP2REF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A                                      CF05(05 'REFRESH')
     A*
     A*
     A*---------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*---------------------------------------------------------------
     A          R MP402S1                   SFL
     A*%%TS  SD  20140305  162518  ISLRAMS     REL-V6R1M0  5761-WDS
     A  85                                  SFLNXTCHG
     A            HDRRN1         4Y 0H
     A            HDPOSN    R        H      REFFLD(@@POSN)
     A            D1OPT     R        B  9  2REFFLD(@@OPT)
     A                                      CHGINPDFT(UL)
     A  30                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A N30                                  DSPATR(HI)
     A            D1PONO    R        O  9  5REFFLD(@@PONO)
     A            D1JDVNNO  R        O  9 14REFFLD(@@JDVNNO)
     A                                      EDTCDE(M)
     A            D1JDVNNM      30A  O  9 24
     A            D1PCID    R        O  9 57REFFLD(@@PCID)
     A            D1ACONO   R        O  9 64REFFLD(@@ACONO)
     A                                      EDTCDE(Z)
     A            D1ERFL    R        O  9 72REFFLD(@@ERFL)
     A            D1INFL    R        O  9 78REFFLD(@@ERFL)
     A*
     A*---------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*---------------------------------------------------------------
     A          R MP402C1                   SFLCTL(MP402S1)
     A*%%TS  SD  20140306  103614  ISLRAMS     REL-V6R1M0  5761-WDS
     A                                      SFLSIZ(0050)
     A                                      SFLPAG(0012)
     A N82                                  ROLLUP(25 'PAGE FORWARD')
     A                                      SETOF(30)
     A                                      OVERLAY
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND(*MORE)
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 28'MP2 INTERFACE TO JDE'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USER)
     A                                      DSPATR(HI)
     A                                  2 26'WORK WITH PURCHASE ORDERS'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  4  2'2=Revise vendor'
     A                                  4 19'5=W/W receipts'
     A                                  4 35'7=W/W invoices'
     A                                  6  5'Purchase'
     A                                      DSPATR(HI)
     A                                  6 14'JDE Vendor'
     A                                      DSPATR(HI)
     A                                  6 55'Purchase'
     A                                      DSPATR(HI)
     A                                  6 64'Acct'
     A                                      DSPATR(HI)
     A                                  6 77'Inv'
     A                                      DSPATR(HI)
     A                                  7  2'Op'
     A                                      DSPATR(HI)
     A                                  7  8'Order'
     A                                      DSPATR(HI)
     A                                  7 16'Number'
     A                                      DSPATR(HI)
     A                                  7 24'JDE Vendor Name'
     A                                      DSPATR(HI)
     A                                  7 56'Center'
     A                                      DSPATR(HI)
     A                                  7 64'Comp'
     A                                      DSPATR(HI)
     A                                  7 70'Error'
     A                                      DSPATR(HI)
     A                                  7 76'Exist'
     A                                      DSPATR(HI)
     A            C1PONO    R        B  8  5REFFLD(@@PONO)
     A                                      DSPATR(HI)
     A            C1JDVNNO  R        B  8 14REFFLD(@@JDVNNO)
     A                                      DSPATR(HI)
     A            C1JDVNNM      30A  B  8 24DSPATR(HI)
     A            C1PCID    R        B  8 57REFFLD(@@PCID)
     A                                      DSPATR(HI)
     A            C1ACONO   R        B  8 64REFFLD(MPREC/@@ACONO *LIBL/MP2REF)
     A                                      EDTCDE(M)
     A                                      DSPATR(HI)
     A            C1ERFL    R        B  8 72REFFLD(@@ERFL)
     A                                      DSPATR(HI)
     A            C1INFL    R        B  8 78REFFLD(@@ERFL)
     A                                      DSPATR(HI)
     A*
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R MP402K1
     A*%%TS  SD  20011024  165925  ISLRAMS     REL-V4R4M0  5769-PW1
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A                                 23 11'F5=Refresh'
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *---------------------------------------------------------------
     A          R MP402ES                   SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE CONTROL FORMAT
      *---------------------------------------------------------------
     A          R MP402EC                   SFLCTL(MP402ES)
      *
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
