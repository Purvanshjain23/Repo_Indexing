     A*****************************************************************
     A* CCDSELE1:   Cost Center Selection Display File for Enterprise 1
     A* CREATED:    Brad Baden  SDN465  TFS# 16169   3/12/2020
     A*****************************************************************
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A*                                     REF(   )
     A          R CCR1
     A                                      WINDOW(CCC1)
     A                                  1  2'CCSELE1'
     A                                  1 21'SELECT COST CENTER'
     A                                      DSPATR(HI)
     A*
     A          R CCS1                      SFL
     A            DFSEL          1A  I  7  2VALUES('1' ' ')
     A            DFCC          12A  O  7  5
     A            DFNAME        30A  O  7 20
     A*
     A          R CCC1                      SFLCTL(CCS1)
     A                                      SFLSIZ(0150)
     A                                      SFLPAG(0010)
     A                                      WINDOW(3 14 19 61)
     A                                      BLINK
     A                                      OVERLAY
     A                                      CA03
     A                                      CA05
     A                                      PAGEDOWN
     A  40                                  SFLCLR
     A                                      SFLDSPCTL
     A  42                                  SFLDSP
     A  99                                  SFLEND(*MORE)
     A                                      SFLMODE(&SFMODE)
     A  60                                  SFLMSG('Roll up or down beyon first-
     A                                       or last record in subfile.' 60)
     A            SFLPS          4S 0H      SFLRCDNBR
     A            SFMODE         1A  H
     A                                  3  1'Type options, press Enter.'
     A                                      COLOR(BLU)
     A                                  4  3'1=Select'
     A                                      COLOR(BLU)
     A                                  4 14'Search for . .'
     A            DFSRCH        20A  I  4 30CHECK(LC)
     A                                  6  1'Sel'
     A                                      DSPATR(HI)
     A                                  6  5'Cost Center'
     A                                      DSPATR(HI)
     A*
     A          R CCK1
     A                                      WINDOW(CCC1)
     A                                      OVERLAY
     A                                 18  2'F3=Exit'
     A                                      COLOR(BLU)
     A*
     A          R CCR2
     A                                      ASSUME
     A                                  1  3' '
