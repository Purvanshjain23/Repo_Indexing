/***************************************************************** */
/*                                                                 */
/*  PROGRAM:         PWNPUPC                                       */
/*  PROGRAMMER:      Brad Badenn                                   */
/*  CREATE DATE:      6/15/2021                                    */
/*                                                                 */
/*  FUNCTION:        This CL will run a sequel to list the         */
/*                   payment differences between the PKA1CPP       */
/*                   and the PKA1CPPPS files.  The sequel will     */
/*                   produce a spreadsheet that is emailed to      */
/*                   the requester.                                */
/*                                                                 */
/*******************************************************************/
/*  Modification History                                           */
/*  x/xx/xxxx xxx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx  */
/*                                                                 */
/*******************************************************************/

             PGM        PARM(&COMPANY &EMAIL)

             DCL        VAR(&COMPANY) TYPE(*CHAR) LEN(3)
             DCL        VAR(&COMP)   TYPE(*DEC)  LEN(3 0)
             DCL        VAR(&SQLNAME) TYPE(*CHAR) LEN(10)
             DCL        VAR(&EMAIL) TYPE(*CHAR) LEN(50)
             DCL        VAR(&TEXT) TYPE(*CHAR) LEN(60)
             DCL        VAR(&LIBRARY) TYPE(*CHAR) LEN(10)
             DCL        VAR(&GRPPRFL) TYPE(*CHAR) LEN(10)
             DCL        VAR(&TESTLIB) TYPE(*CHAR) LEN(3)

/* Save current library list                                          */
             PSHLIBL

/**********************************************************************/
/*  Retrieve the user's Group Profile.  Set the library list          */
/*  based upon the combination of the group profile and company#.     */
/**********************************************************************/

             RTVUSRPRF  GRPPRF(&GRPPRFL)

             SELECT
             /* Set library list to a test library list               */
             WHEN       COND(&GRPPRFL = 'SBDPGMR ') THEN(DO)
             IF         COND(&COMPANY = '360') THEN(SETLIBL +
                          JOBD(PRKTSTGUP))
             IF         COND(&COMPANY = '440') THEN(SETLIBL +
                          JOBD(PRKTSTSTP))
             IF         COND(&COMPANY = '960') THEN(SETLIBL +
                          JOBD(PRKTSTTFP))
             IF         COND(&COMPANY = '961') THEN(SETLIBL +
                          JOBD(PRKTSTTFP))

             ADDLIBLE   LIB(PRKDEVQRY) POSITION(*LAST)

             ENDDO

             /* Set library list to a production library list         */
             OTHERWISE  CMD(DO)
             IF         COND(&COMPANY = '360') THEN(SETLIBL +
                          JOBD(PRKPRDGUP))
             IF         COND(&COMPANY = '440') THEN(SETLIBL +
                          JOBD(PRKPRDSTP))
             IF         COND(&COMPANY = '960') THEN(SETLIBL +
                          JOBD(TFSBDPROD))
             IF         COND(&COMPANY = '961') THEN(SETLIBL +
                          JOBD(TFSBDPROD))
             ENDDO

             ENDSELECT

 /* Move alpha company to numeric value                               */
             CHGVAR     VAR(&COMP) VALUE(&COMPANY)

 /* Retrieve the library that the Tattoo Header is in                 */
             RTVOBJD    OBJ(PKA1CPP) OBJTYPE(*FILE) RTNLIB(&LIBRARY)

 /* Strip out positions 5 - 7 of the library name                     */
             CHGVAR     VAR(&TESTLIB) VALUE(%SST(&LIBRARY 5 3))

 /* If library name contains 'TST', setup &TEXT with prefix of TEST:  */
             SELECT
             WHEN       COND(&TESTLIB *EQ 'TST') THEN(CHGVAR +
                          VAR(&TEXT) VALUE('TEST: Re-Payment Diff +
                          sequel for company' *BCAT &COMPANY))

 /* Otherwise, setup &TEXT with 'Re-Payment...'                       */
               OTHERWISE  CMD(CHGVAR VAR(&TEXT) VALUE('Re-Payment +
                            Difference sequel for company' *BCAT +
                            &COMPANY))
             ENDSELECT

 /* Select sequel name for EXECUTE statement                          */
             SELECT
             WHEN       COND(&COMP *EQ 360) THEN(CHGVAR +
                          VAR(&SQLNAME) VALUE('TATTDIF_GU'))

             WHEN       COND(&COMP *EQ 440) THEN(CHGVAR +
                          VAR(&SQLNAME) VALUE('TATTDIF_ST'))

             WHEN       COND(&COMP *EQ 960) THEN(CHGVAR +
                          VAR(&SQLNAME) VALUE('TATTDIF_TF'))

             WHEN       COND(&COMP *EQ 961) THEN(CHGVAR +
                          VAR(&SQLNAME) VALUE('TATTDIF_TF'))

             ENDSELECT

/* Execute the Sequel and email the results to the requested address  */
             EXECUTE    VIEW(*LIBL/&SQLNAME) PCFMT(*XLS) +
                          REPLACE(*YES) RECIPIENT(&EMAIL) +
                          EMLMSG(&TEXT) SETVAR((&COMP &COMPANY))

/* Restore original library list                                      */
             POPLIBL

 ENDPGM:     ENDPGM
