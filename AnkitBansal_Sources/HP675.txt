      *****************  RPG PROGRAM HEADING  ************************
      * SYSTEM:      HOG PRODUCTION
      * PROGRAM:     HP675
      * TITLE:       SCHEDULED HOG MOVEMENT LOG PRINT
      * PROGRAMMER:  SHARON ZURN
      * CREATED:     6/08/94
      *
      *  FUNCTION:   THIS PROGRAM PRINTS A LOG OF ALL SCHEDULED
      *              MOVEMENTS THAT MEETS THE USER'S SELECTION
      *              CRITERIA. HSP375 (WORKFILE) IS BUILT IN HP375.
      *
      *           THIS PROGRAM IS SUBMITTED BY HP475CL WHICH IS CALLED
      *           FROM HP475 THROUGH QCMDEXC.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 09/16/94  MARY PALMER
      *           ADDED CUSTOMER REPORT SELECTION TO SORT AND PRINT
      *           AS DESTINATION OPITON, AND ABILITY TO SELECT RUN
      *           FOR A SPECIFIC CUSTOMER NUMBER
      *
      * 04/25/95  LEANNE FEDOR
      *           CHANGED FARM SITE CODE FROM ALPHA TO NUMERIC.
      *
      * 08/16/95  LEANNE FEDOR
      *           CHANGED FOR MOVEMENT REWRITE.
      *
      * 08/31/95  LEANNE FEDOR
      *           MADE BUSINESS OFFICE AN OPTIONAL SELECTION.
      *
      * 09/20/95  LEANNE FEDOR
      *           ADDED A TOTAL LINE SHOWING 'SCHEDULED' HEAD PER
      *           CUSTOMER WHEN THE USER HAS ELECTED THE 'CUSTOMER'
      *           REPORT.
      *
      * 03/19/97  LEANNE FEDOR
      *           A NEW USER SELECTION OF 'UPDATE DATE' WAS ADDED.
      *           THIS DATE CORRESONDS TO THE NEW UPDATE DATE/TIME
      *           STAMP THAT WAS ADDED TO THE MOVEMENT EVENT FILE.
      *
      * 07/22/03  LeAnne Fedor
      *           Added Farm Site Code to detail line print. Prior to this change,
      *           only the Farm Site Name was printing.
      *
      * 06/07/04  LeAnne Fedor
      *           The user can now select Page Break on Farm (YES/NO) for the Origin and
      *           Destination reports. So, added conditional page break code.
      *
      * 12/09/11  LeAnne Ramsey (E1843)
      *           Added a new selection field: Transfers across State Line?
      *           (Options are Exclude/Include/Only.)
      *
      * 12/21/11  LeAnne Ramsey (E1843)
      *           Removed code commented out earlier.
      *
      * 10/16/13  LeAnne Ramsey (E2831)
      *           Recompile only. Added field 'MTech Reference'.
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     FHSP011    IF   E           K DISK
      *    PRODUCTION PHASE
      *
      *
     FHSP018    IF   E           K DISK
      *    FARM SITE
      *
      *
     FHSP375    IF   E             DISK
      *   MOVEMENT WORKFILE
      *   (RECORDS SELECTED AND KEYED IN OPEN QUERY)
      *
     FQPRINT    O    F  132        PRINTER OFLIND(*INOF)
      *
      /EJECT
      ****************************************************************
      * TABLE/ARRAY SPECIFICATIONS
      ****************************************************************
      ****************************************************************
      * INPUT SPECS
      ****************************************************************
      *
      *---------------------------------------------------------------
      *  DATE FORMATS
      *---------------------------------------------------------------
      *
      * DATE IN MMDDYY FORMAT
      *
     D                 DS
     D  WKMDY                  1      6  0
     D  WKMM                   1      2  0
     D  WKDD                   3      4  0
     D  WKYY                   5      6  0
      *
      * DATE IN CCYYMMDD FORMAT
      *
     D                 DS
     D  WKCYMD                 1      8  0
     D  WKCEN                  1      2  0
     D  WKYER                  3      4  0
     D  WKMON                  5      6  0
     D  WKDAY                  7      8  0
      *
      *---------------------------------------------------------------
      *  LDA -  WITH THE REQUESTED PRINT SELECTIONS
      *---------------------------------------------------------------
      *
     D                UDS
     D  LDFSBO                 1      5
     D  LDFYMD                 6     13  0
     D  LDTYMD                14     21  0
     D  LDPPCD                22     26
     D  LDFSCD                27     31  0
     D  LDMRCD                32     33
     D  LDCVNO                34     41  0
     D  LDUYMD                42     49  0
     D  LDPBFL                50     50
     D  LDASLFL               51     51
      *
     D  LDBODS                56     85
     D  LDFSNM                86    110
     D  LDCNAM               118    147
     D  LDMRDS               148    177
     D  LDPPDS               178    207
     D  LDFMDY               208    213  0
     D  LDTMDY               214    219  0
     D  LDUMDY               220    225  0
     D  LDASLDS              226    255
      *
      *---------------------------------------------------------------
      * STANDARD PROGRAM STATUS DATA STRUCTURE
      *---------------------------------------------------------------
      *    EXTERNALLY DEFINED AS UTPGFR (RECORD FORMAT: PGMDSR)
     D PGMDS         ESDS                  EXTNAME(UTPGFR)
      *
      *---------------------------------------------------------------
      *  NAMED CONSTANTS
      *---------------------------------------------------------------
      *
     D YES             C                   CONST('Y')
     D NO              C                   CONST('N')
      *
     D                 DS
     D  DUMMYD                 1      1
      *  THE PRECEDING DUMMY DATA STRUCTURE IS ONLY TO KEEP THE I-SPEC
      *  INFO FROM RUNNING OVER INTO THE 'CALC SPECS' ON THE COMPILE
      *  LISTING.  IT SERVES NO OTHER PURPOSE AND CAN BE DELETED
      *  AT ANY TIME!!!
      /EJECT
      ****************************************************************
      * MAIN LINE
      ****************************************************************
      *
     C     *IN90         DOUEQ     *ON                                          MAIN DO
     C                   READ      HSP375                                 90
     C     *IN90         IFEQ      *OFF                                         IF NOT EOF
      *
     C     FIRST         IFEQ      YES
     C                   EXSR      $FIRST
     C                   ENDIF
      *
      * CHECK FOR CONTROL BREAKS ON THE VARIOUS REPORTS:
      *
      * ORIGIN
     C                   SELECT
     C     LDMRCD        WHENEQ    'OR'
     C     WFORPP        IFNE      SVORPP
     C     WFORFS        ORNE      SVORFS
      *
     C                   if        wforpp <> svorpp or ldpbfl = yes
     C                   seton                                        of
     C                   else
     C                   except    blank
     C                   except    blank
     C                   endif
      *
     C                   MOVEL     WFORPP        WKPPCD
     C                   EXSR      $PPDS
     C                   MOVEL     WFORPP        SVORPP
     C                   MOVEL     WFORFS        SVORFS
     C                   ENDIF
      * DESTINATION
      *
     C     LDMRCD        WHENEQ    'DE'
     C     WFDNPP        IFNE      SVDNPP
     C     WFDNFS        ORNE      SVDNFS
      *
     C                   if        wfdnpp <> svdnpp or ldpbfl = yes
     C                   seton                                        of
     C                   else
     C                   except    blank
     C                   except    blank
     C                   endif
      *
     C                   MOVEL     WFDNPP        WKPPCD
     C                   EXSR      $PPDS
     C                   MOVEL     WFDNPP        SVDNPP
     C                   MOVEL     WFDNFS        SVDNFS
     C                   ENDIF
      * TRUCKER
     C     LDMRCD        WHENEQ    'TK'
     C     WFTICD        ANDNE     SVTICD
     C                   SETON                                        OF
     C                   MOVEL     WFTICD        SVTICD
      *
      * SCHEDULED DATE
      *
     C     LDMRCD        WHENEQ    'SC'
     C     WFORPP        ANDNE     SVORPP
     C                   SETON                                        OF
     C                   MOVEL     WFORPP        WKPPCD
     C                   EXSR      $PPDS
     C                   MOVEL     WFORPP        SVORPP
      *
      * CUSTOMER
     C     LDMRCD        WHENEQ    'CU'
     C     WFCVNO        ANDNE     SVCVNO
     C                   EXSR      $TOTAL
     C                   Z-ADD     WFCVNO        SVCVNO
     C                   Z-ADD     0             WKSCHD
     C                   ENDSL
      *
      * FLIP SCHEDULED DATE TO MDY FOR PRINTING
      *
     C                   Z-ADD     WFSCDT        WKCYMD
     C                   EXSR      $TOMDY
     C                   Z-ADD     WKMDY         DTSMDY
      *
      * RETRIEVE THE ORIGIN FARM SITE NAME
      *
     C     WFORFS        CHAIN     HSP018                             92
     C     *IN92         IFEQ      *OFF
     C                   MOVEL     FSFSNM        DTORNM
     C                   ELSE
     C                   MOVEL(P)  'UNKNOWN'     DTORNM
     C                   ENDIF
      *
      * MOVE EITHER THE DESTINATION FARM SITE NAME FOR TRANSFERS
      * OR THE CUSTOMER NAME FOR SALES MOVEMENTS TO PRINT FIELD
      *
     C                   SELECT
     C     WFMTCD        WHENEQ    'T'
     C                   SETOFF                                       61
     C                   MOVEL(P)  WFDNNM        DTDNNM
      *
     C     WFMTCD        WHENEQ    'S'
     C                   SETON                                        61
     C                   MOVEL(P)  WFDNNM        DTCVNM
     C                   ENDSL
      *
      * Concatenate the Bulding/Room into a single field for printing.
      *
     C                   eval      dtorblrm = %trimr(wforbl) + '/' +
     C                                        %triml(wforrm)
      *
     C                   eval      dtdnblrm = %trimr(wfdnbl) + '/' +
     C                                        %triml(wfdnrm)
      * IF OVERFLOW,
      *    PRINT THE REPORT HEADINGS
      *    PRINT THE PHASE HEADING FOR REPORTS SORTED BY:
      *            ORIGIN PHASE/SCHEDULED DATE
      *            ORIGIN PHASE/FARM
      *            DESTINATION PHASE/FARM
      *
     C     *INOF         IFEQ      *ON                                          IF OVERFLOW
     C                   EXCEPT    HRHDR
      *
     C     LDMRCD        IFEQ      'SC'
     C     LDMRCD        OREQ      'OR'
     C     LDMRCD        OREQ      'DE'
     C                   EXCEPT    HPHDR
     C                   ELSE
     C                   EXCEPT    BLANK
     C                   ENDIF
     C                   ENDIF                                                  IF OVERFLOW
      *
      * ACCUMULATE SCHEDULED HEAD FOR TOTAL LINE WHICH
      * PRINTS ONLY FOR THE 'CUSTOMER' REPORT
      *
     C                   ADD       WFSCHD        WKSCHD
      * PRINT DETAIL
     C                   EXCEPT    DTLINE
     C                   ENDIF                                                  IF NOT EOF
     C                   ENDDO                                                  MAIN DO
      *
      *----------------------------------------------------------------
      * EOF PROCESSING
      *----------------------------------------------------------------
      *  IF NO DETAIL RECORDS MATCHED THE SELECTION CRITERIA, THEN
      *  PRINT THE STANDARD 'NO DATA...' ERROR MESSAGE
      *
     C     FIRST         IFEQ      YES
     C                   EXCEPT    HRHDR
     C                   EXCEPT    NODATA
     C                   ELSE
      *
      * PRINT TOTAL LINE FOR 'CUSTOMER' REPORT
      *
     C     LDMRCD        IFEQ      'CU'
     C                   EXSR      $TOTAL
     C                   ENDIF
      *
     C                   ENDIF
      *
     C                   SETON                                        LR
      /EJECT
      *----------------------------------------------------------------
      * $TOTAL - PRINT TOTAL LINE
      *----------------------------------------------------------------
      *
      * A TOTAL LINE IS ONLY PRINTED FOR THE 'CUSTOMER' REPORT.
      *
     C     $TOTAL        BEGSR
      *
     C     *INOF         IFEQ      *ON
     C                   EXCEPT    HRHDR
     C                   ENDIF
     C                   EXCEPT    TOTAL
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $PPDS - RETRIEVE PRODUCTION PHASE DESCRIPTION
      *----------------------------------------------------------------
      *
     C     $PPDS         BEGSR
      *
     C     WKPPCD        CHAIN     HSP011                             92
     C     *IN92         IFEQ      *OFF                                         IF FOUND
     C                   MOVEL(P)  PPPPDS        HPPPDS
     C                   ELSE
     C                   MOVEL(P)  'UNKNOWN'     HPPPDS
     C                   ENDIF                                                  IF FOUND
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $TOMDY - FLIP DATE FROM YYMMDD TO MMDDYY
      *----------------------------------------------------------------
      *
     C     $TOMDY        BEGSR
      *
     C                   Z-ADD     WKMON         WKMM
     C                   Z-ADD     WKDAY         WKDD
     C                   Z-ADD     WKYER         WKYY
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $FIRST - FIRST TIME ROUTINE
      *----------------------------------------------------------------
      *
      * SAVE CONTROL BREAK FIELDS
      * RETRIEVE PRODUCTION PHASE DESCRIPTION
      * PRINT REPORT HEADINGS
      *
     C     $FIRST        BEGSR
      *
     C                   SELECT
     C     LDMRCD        WHENEQ    'OR'                                         ORIGIN
     C                   MOVEL     WFORPP        WKPPCD
     C                   EXSR      $PPDS
     C                   MOVEL     WFORPP        SVORPP
     C                   MOVEL     WFORFS        SVORFS
      *
     C     LDMRCD        WHENEQ    'DE'                                         DESTINATION
     C                   MOVEL     WFDNPP        WKPPCD
     C                   EXSR      $PPDS
     C                   MOVEL     WFDNPP        SVDNPP
     C                   MOVEL     WFDNFS        SVDNFS
      *
     C     LDMRCD        WHENEQ    'TK'                                         TRUCKER
     C                   MOVEL     WFTICD        SVTICD
      *
     C     LDMRCD        WHENEQ    'SC'                                         SCHEDULED DT
     C                   MOVEL     WFORPP        WKPPCD
     C                   EXSR      $PPDS
     C                   MOVEL     WFORPP        SVORPP
      *
     C     LDMRCD        WHENEQ    'CU'                                         CUSTOMER
     C                   Z-ADD     WFCVNO        SVCVNO
     C                   ENDSL
      *
     C                   MOVEL     NO            FIRST
     C                   SETON                                        OF
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * INITIALIZATION SUBROUTINE
      *----------------------------------------------------------------
      *
     C     *INZSR        BEGSR
      *
      * DEFINE PRINT FIELDS FOR HEADINGS
      *
     C     *LIKE         DEFINE    PPPPDS        HPPPDS
     C                   move      *blank        hrpbfl            3
     C                   MOVE      *BLANK        HRASLDS          47
      *
      * DEFINE PRINT FIELDS FOR DETAIL LINE
      *
     C     *LIKE         DEFINE    LDFMDY        DTSMDY                         SCHEDULED
     C     *LIKE         DEFINE    WFDNNM        DTCVNM                         CUSTOMER NAM
     C                   MOVE      *BLANK        DTDNNM           17
     C                   MOVE      *BLANK        DTORNM           13
     C                   MOVE      *BLANK        dtdnblrm         11
     C                   MOVE      *BLANK        dtorblrm         11
      *
      * DEFINE PRINT FIELD FOR TOTAL LINE
      *
     C                   Z-ADD     0             WKSCHD            9 0
      *
      * WORKFIELDS
      *
     C     *LIKE         DEFINE    PPPPCD        WKPPCD
      *
      * CONTROL BREAK FIELDS
      *
     C     *LIKE         DEFINE    WFDNFS        SVDNFS
     C     *LIKE         DEFINE    WFDNPP        SVDNPP
     C     *LIKE         DEFINE    WFORFS        SVORFS
     C     *LIKE         DEFINE    WFORPP        SVORPP
     C     *LIKE         DEFINE    WFTICD        SVTICD
     C     *LIKE         DEFINE    WFCVNO        SVCVNO
      *
      *
      * SET UP REPORT HEADINGS
      *
      * Page Break on Farm
      *
     C                   if        ldmrcd = 'DE' or ldmrcd = 'OR'
     C                   seton                                        93
      *
     C                   if        ldpbfl = yes
     C                   eval      hrpbfl = 'YES'
     C                   else
     C                   eval      hrpbfl = 'NO '
     C                   endif
      *
     C                   endif
      *
      * ALL BUSINESS OFFICES
      *
     C     LDFSBO        IFEQ      *BLANK
     C                   MOVEL(P)  'ALL'         LDFSBO
     C                   ENDIF
      *
      *
      * ALL SCHEDULED DATES
      *
     C     LDFYMD        IFEQ      0
     C                   SETON                                        85
     C                   ENDIF
      *
      *
      * ALL UPDATE DATES
      *
     C     LDUYMD        IFEQ      0
     C                   SETON                                        84
     C                   ENDIF
      *
      * ALL FARMS
     C     LDFSCD        IFEQ      0
     C                   SETON                                        81
     C                   ENDIF
      *
      * ALL PHASES
      *
     C     LDPPCD        IFEQ      *BLANK
     C                   MOVEL(P)  'ALL'         LDPPCD
     C                   ENDIF
      *
      * ALL CUSTOMERS
      *
     C     LDCVNO        IFEQ      0
     C                   SETON                                        89
     C                   ENDIF
      *
      *
      * SET INDICATORS TO CONTROL PRINTING OF CORRECT COLUMN
      * HEADINGS:
      *
      * REPORT INCLUDES ONLY SALES MOVEMENTS
      *
     C                   SELECT
     C     LDMRCD        WHENEQ    'CU'
     C                   SETON                                        87
      *
      * REPORT INCLUDES ONLY TRANSFER MOVEMENTS
      *
     C     LDMRCD        WHENEQ    'DE'
     C                   SETON                                        86
     C                   OTHER
      *
      * REPORT INCLUDES BOTH SALES AND TRANSFER MOVEMENTS
      *
     C                   SETON                                        88
     C                   ENDSL
      *
      * Transfers across a State line
      *
     C                   select
     C                   when      ldaslfl = 'I'
     C                   eval      hraslds = 'Transfers across a State Line +
     C                                        are Included.'
     C                   when      ldaslfl = 'E'
     C                   eval      hraslds = 'Transfers across a State Line +
     C                                        are Excluded.'
     C                   when      ldaslfl = 'O'
     C                   eval      hraslds = 'Only Transfers across a State +
     C                                        Line are printed.'
     C                   endsl
      *
      * RETRIEVE TIME FOR REPORT HEADING
      * SET UP A FIRST TIME FLAG
      * SET UP A DASHED LINE FOR UNDERLINING THE HEADING
      *
     C                   TIME                    RTIME             6 0
     C                   MOVEL     YES           FIRST             1
     C                   MOVEL     *ALL'-'       DASH            132
     C                   ENDSR
      /EJECT
      *-------------------------------------------------------------
      * REPORT HEADING LINES
      *-------------------------------------------------------------
      *
     OQPRINT    E            HRHDR          1 01
     O                       SDPGM               10
     O                                           78 'HOG PRODUCTION SYSTEM'
     O                                          122 'DATE'
     O                       UDATE         Y    132
      *
     O          E            HRHDR          1
     O                       SDUSR               10
     O                                           78 'SCHEDULED MOVEMENT LOG'
     O                                          122 'TIME'
     O                       RTIME              132 '  :  :  '
      *
     O          E            HRHDR          1
     O                                          122 'PAGE'
     O                       PAGE          Z    132
      *
     O          E            HRHDR          1
     O                                           22 'BUSINESS OFFICE ......'
     O                       LDFSBO              29
     O                       LDBODS              64
      *
     O          E            HRHDR          1
     O                                           22 'FROM SCHEDULED DATE ..'
     O              N85      LDFMDY        Y     32
     O               85                          27 'ALL'
      *
     O          E            HRHDR          1
     O                                           22 'TO SCHEDULED DATE ....'
     O              N85      LDTMDY        Y     32
     O               85                          27 'ALL'
      *
     O          E            HRHDR          1
     O                                           22 'UPDATE DATE ..........'
     O              N84      LDUMDY        Y     32
     O               84                          27 'ALL'
      *
     O          E            HRHDR          1
     O                                           22 'PRODUCTION PHASE .....'
     O                       LDPPCD              29
     O                       LDPPDS              64
      *
     O          E            HRHDR          1
     O                                           22 'FARM SITE ............'
     O              N81      LDFSCD        M     30
     O               81                          27 'ALL'
     O              N81      LDFSNM              59
      *
     O          E            HRHDR          1
     O                                           22 'CUSTOMER NUMBER ......'
     O               89                          27 'ALL'
     O              N89      LDCVNO        Z     32
     O                       LDCNAM              65
      *
     O          E            HRHDR          1
     O                                           22 'REPORT ...............'
     O                       LDMRDS              54
      *
     O          E   n87      HRHDR          1
     O                                           22 'ACROSS A STATE LINE ..'
     O                       hraslds             71
      *
     O          E            HRHDR          2
     O               93                          22 'PAGE BREAK ON FARM ...'
     O               93      HRPBFL              27
     O              n93                           1 ' '
      *-------------------------------------------------------------
      * COLUMN HEADING LINES
      *-------------------------------------------------------------
     O          E            HRHDR          1
     O                                            8 'MOVEMENT'
     O                                           18 'SCHEDULED'
     O                                           36 '----------------'
     O                                           44 'ORIGIN'
     O                                           62 '----------------'
      *
     O                                           80 '---------------'
     O                                           93 'DESTINATION'
     O                                          110 '---------------'
      *
      * FOR ALL REPORTS
     O              N87                         117 'MOVE'
     O               87                         118 'SALES'
     O                                          125 'SCHED'
      *
     O          E            HRHDR          1
     O                                            6 'NUMBER'
     O                                           18 'SHIP DATE'
     O                                           29 'FARM SITE'
     O                                           48 'BLDG/ROOM'
     O                                           56 'GROUP'
     O              n87                          62 'ST'
     O              n87                          67 'ST'
      *
      * BOTH SALES AND TRANSFERS
     O               88                          85 'FARM OR CUSTOMER'
     O               88                         100 'BLDG/ROOM'
     O               88                         110 'CUST/GRP'
      * ONLY TRANSFERS
     O               86                          78 'FARM SITE'
     O               86                         100 'BLDG/ROOM'
     O               86                         108 'GROUP'
      * ONLY SALES
     O               87                          82 'CUSTOMER NAME'
     O               87                         109 'NUMBER'
      *
     O                                          117 'CODE'
     O                                          125 'HEAD'
     O                                          132 'TRUCK'
      *
     O          E            HRHDR          1
     O                       DASH               132
      *
      *-------------------------------------------------------------
      * PHASE HEADING
      *-------------------------------------------------------------
      *
     O          E            HPHDR          2
     O                                            5 'PHASE'
     O                       HPPPDS              37
      *
      *-------------------------------------------------------------
      * DETAIL LINE
      *-------------------------------------------------------------
      *
     O          E            DTLINE         1
     O                       WFMVSN        Z      7
     O                       DTSMDY              17 '  /  /  '
      * ORIGIN INFO
     O                       WFORFS        z     23
     O                       DTORNM              38
     O                       dtorblrm            50
     O                       WFORHG              58
     O                       wforsacd            63
      *
      * DESTINATION INFO FOR A TRANSFER
      *
     O              N61      WFDNSACD            68
     O              N61      WFDNFS        z     72
     O              N61      DTDNNM              90
     O              N61      dtdnblrm           102
     O              N61      WFDNHG             110
      * FOR A SALE
     O               61      DTCVNM              94
     O               61      WFCVNO        Z    109
      *
     O                       WFCODE             118
     O                       WFSCHD        K    126
     O                       WFTICD             132
      *
      *-------------------------------------------------------------
      * TOTAL LINE FOR 'CUSTOMER' REPORT
      *-------------------------------------------------------------
      *
     O          E            TOTAL          1
     O                                          125 '_______'
      *
     O          E            TOTAL          2
     O                       WKSCHD        K    126
      *
      *-------------------------------------------------------------
      * PRINT A BLANK LINE
      *-------------------------------------------------------------
      *
     O          E            BLANK          1
      *
      *-------------------------------------------------------------
      * NO DATA MESSAGE LINE
      *-------------------------------------------------------------
     O          E            NODATA         1
     O                                           19 'NO DATA MEETS YOUR'
     O                                           38 'SELECTION CRITERIA'
