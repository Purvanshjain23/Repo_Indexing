      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Hog Production
      * PROGRAM:     HP235 - Write Feed Ticket Receiving Records to History
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     04/29/97
      *
      * FUNCTION: This program writes a History record for each Upload Receiving record
      *           and then deletes the Receiving record.
      *
      *           This program is kicked off from HP437 thru QCMDEXC.
      *
      *           Note: At the end of 2009 we added logic to delete old History records.
      *                 The file had grown to almost 2 million records!
      *                 So, we will now delete all records more than 90 days old.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      * 05/02/01  LeAnne Fedor
      *           Recompile only. Grossman ticket number expanded from
      *           6 to 7 positions.
      *
      * 05/07/01  LeAnne Fedor
      *           Per John Zimmerman, he will now flag some records with a value
      *           of '1' in the processing flag field. They should be included on
      *           listing of staging data but then dropped from any further
      *           processing. This will help the users monitor the Grossman system...
      *           but we are not going to move these records into HPS.
      *
      * 12/11/01  LeAnne Fedor
      *           A new 'record source' field was added.
      *
      * 01/07/02  LeAnne Fedor
      *           Changed 'ticket number' to alpha in the staging and history files.
      *
      * 08/12/02  LeAnne Fedor
      *           John Zimmerman is going to send us 'ration' and 'additive' in the
      *           6-character field that originally held 'item'.
      *
      * 10/26/09  LeAnne Ramsey
      *           David Weaver is rewriting the p.c. side.
      *           We no longer have/need our "staging" file; so, this code now processes
      *           the "receiving" file.
      *           Also, we added on-going deletion logic for the old History records.
      *           Since we are renaming the History file from HSP936-History Staging to
      *           HSP935-History Receiving, the "history" file will be empty after we
      *           install.
      *
      * 10/30/19  Brad baden    E15736 - Uplod Toll Mill Receipts
      *           Write new Ingredient Cost field to History file HSP935.
      *           Replace UDATE logic with System Date.
      *
      * 03/26/20  Danny Nguyen - P405 - Farm Number Increase
      *           Recompile only. Increased Bin Code (@@BNCD) in HSPREF file from 5A to 6A.
      *
      * 05/10/22 Eric L SDN736 Recompiled ticket nbr increase (TKNO & RTNO 7.0 TO 9.0)
      * 08/09/22 Eric L SDN736 WKISODATE populated using *MDY
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fhsp235    uf   e           k disk
      *  Uploaded feed tickets--receiving file
      *
      *
     Fhsp935    uf a e           k disk
      *  Uploaded feed tickets--history receiving file
      *
      /EJECT
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Workfields for date manipulation
      *
     d sysdate         s               d   inz(*sys)
     D wkisodate       s               D   datfmt(*iso)
     D wkdltdt         s              8  0
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * Mainline
      ****************************************************************
      *
      * Process each record in the Feed Ticket Upload Receiving file
      *
     C     *loval        setll     hsp235
      *
     C                   dou       *in90 = *on                                  Main do
     C                   read      hsp235                                 90
     C                   if        *in90 = *off                                 If not eof
     C                   z-add     u1mmno        h1mmno
     C                   move      u1tkno        h1tkno
     C                   z-add     u1tkdt        h1tkdt
     C                   movel     u1inv         h1inv
     C                   z-add     u1fscd        h1fscd
     C                   movel     u1bncd        h1bncd
     C                   movel     u1rncd        h1rncd
     C                   movel     u1adcd        h1adcd
     C                   z-add     u1lbs         h1lbs
     C                   z-add     u1ingr        h1ingr
      *
      * Write a History record and Delete the Receiving record
      *
     C                   write     h1rec
     C                   delete    u1rec
     C                   endif                                                  If not eof
     C                   enddo                                                  Main do
      *
     C                   seton                                        lr
      /EJECT
      *----------------------------------------------------------------
      * Delete old History records
      *----------------------------------------------------------------
      *
     C     $dlt935       begsr
      *
     C     wkdltdt       setgt     hsp935
      *
     C                   dou       *in91 = *on                                  Do delete
     C                   readp     hsp935                                 91
     C                   if        *in91 = *off and h1updt <= wkdltdt           If not eof
     C                   delete    h1rec
     C                   endif                                                  If not eof
     C                   enddo                                                  Do delete
      *
     C                   endsr
      /EJECT
      *----------------------------------------------------------------
      * Initialization
      *----------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Calc a date 90 days prior to the System Date. Then, delete all History records
      * older than this date.
      *
     C                   movel     sysdate       wkisodate
     C                   subdur    90:*d         wkisodate
     C     *iso          move      wkisodate     wkdltdt
     C                   exsr      $dlt935
      *
      * Use the System Date to set up timestamp values for the History recrod.
      *
     C                   move      sdusr         h1uscd
     C                   time                    h1uptm
     C     *mdy          move      udate         wkisodate
      **********         movel     sysdate       wkisodate
     C                   move      wkisodate     h1updt
      *
     C                   endsr
      /EJECT
