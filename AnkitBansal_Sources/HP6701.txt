      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Hog Production System
      * PROGRAM:     HP6701
      * TITLE:       Listing of Budget Templates
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     02/10/04
      *
      *
      *            This listing is submitted from HP4700-Work With Budget Templates.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
      *
     Fhsp011    if   e           k disk
      *    Production phase
      *
      *
     Fhsp101    if   e           k disk
      *    Type of farm
      *
      *
     Fhsp183    if   e           k disk
      *    Creation schedules
      *
      *
     Fhsp184    if   e           k disk
      *    Budget availability
      *
      *
     Fhsp185    if   e           k disk
      *    Budget template header (records selected in open query)
      *
      *
     Fqprint    o    f  132        printer oflind(*inof)
      *
      /EJECT
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Standard fields
      *
     D prtfl           s              1    inz('N')
     D dash            s            110    inz(*all'-')
     d rtime           s              6  0
      *
      *
      *
      * Print fields
      *
     D h1btds          s                   like(thbtds)
     D h1ppds          s                   like(ppppds)
     D h1tfds          s                   like(tftfds)
     D h1bads          s                   like(babads)
     D h1csds          s                   like(cscsds)
      *
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *---------------------------------------------------------------
      * Local data area.
      *---------------------------------------------------------------
      *
     Dlda             uds                  dtaara(*lda)
     D  ldbtcd                 2      7
     D  ldbacd                 8      8
     D  ldcscd                 9      9
     D  ldbtwk                10     14  0
     D  ldcell                15     16  0
     D  ldppcd                17     21
     D  ldtfcd                22     26
     D  ldsize                27     32
     D  ldaist                33     33
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      *******************************************************************************
      * MAINLINE
      *******************************************************************************
      *
      * Prepare to read the file and print headings the first time through.
      *
     C     *loval        setll     hsp185
     C                   except    h1hdr
      *
      * Process all records in the file (records already selected with
      * open query).
     C                   dou       *in90 = *on                                  Main do
     C                   read      hsp185                                 90
     C                   if        *in90 = *off                                 If not EOF
      * Print
     C                   if        *inof = *on
     C                   except    h1hdr
     C                   endif
     C                   except    l1dtl
     C                   movel     yes           prtfl
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Main do
      *
      *-----------------------------------------------------------------------------
      * EOF processing
      *-----------------------------------------------------------------------------
      *
      * If no records met the user's selections, print the standard 'no data...'
      * error message
      *
     C                   if        prtfl = no
     C                   except    nodata
     C                   endif
      *
     C                   seton                                        lr
      /eject
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Key lists
      *  None
      *
      * Set up heading info.
      *
     C                   time                    rtime
      *
      * Budget template
     C                   select
     C                   when      ldbtcd = *blank
     C                   seton                                        80
     C                   other
     C     ldbtcd        chain     hsp185                             92
     C                   if        *in92 = *off
     C                   eval      h1btds = thbtds
     C                   else
     C                   eval      h1btds = 'Unknown'
     C                   endif
     C                   endsl
      *
      * Budget availability code
      *
     C                   select
     C                   when      ldbacd = *blank
     C                   seton                                        81
     C                   other
     C     ldbacd        chain     hsp184                             92
     C                   if        *in92 = *off
     C                   eval      h1bads = babads
     C                   else
     C                   eval      h1bads = 'Unknown'
     C                   endif
     C                   endsl
      *
      * Creation schedule code
      *
     C                   select
     C                   when      ldcscd = *blank
     C                   seton                                        82
     C                   other
     C     ldcscd        chain     hsp183                             92
     C                   if        *in92 = *off
     C                   eval      h1csds = cscsds
     C                   else
     C                   eval      h1csds = 'Unknown'
     C                   endif
     C                   endsl
      *
      * Production phase
      *
     C                   select
     C                   when      ldppcd = *blank
     C                   seton                                        83
     C                   other
     C     ldppcd        chain     hsp011                             92
     C                   if        *in92 = *off
     C                   eval      h1ppds = ppppds
     C                   else
     C                   eval      h1ppds = 'Unknown'
     C                   endif
     C                   endsl
      *
      * Type of farm
     C                   select
     C                   when      ldtfcd = *blank
     C                   seton                                        84
     C                   other
     C     ldtfcd        chain     hsp101                             92
     C                   if        *in92 = *off
     C                   eval      h1tfds = tftfds
     C                   else
     C                   eval      h1tfds = 'Unknown'
     C                   endif
     C                   endsl
      *
      * Status
     C                   if        ldaist = *blank
     C                   seton                                        85
     C                   endif
      * Cell
     C                   if        ldcell = 0
     C                   seton                                        86
     C                   endif
      * Weeks
     C                   if        ldbtwk = 0
     C                   seton                                        87
     C                   endif
      *
      * Size
     C                   if        ldsize = *blank
     C                   seton                                        88
     C                   endif
      *
     C                   endsr
      /EJECT
      *-------------------------------------------------------------
      * Report heading lines
      *-------------------------------------------------------------
      *
     Oqprint    e            h1hdr          1 01
     O                       sdpgm               10
     O                                           72 'HOG PRODUCTION SYSTEM'
     O                                          100 'DATE'
     O                       udate         y    110
      *
     O          e            h1hdr          1
     O                       sdusr               10
     O                                           66 'LISTING OF BUDGET '
     O                                           75 'TEMPLATES'
     O                                          100 'TIME'
     O                       rtime              110 '  :  :  '
      *
     O          e            h1hdr          2
     O                                          100 'PAGE'
     O                       page          z    110
      *
      *
     O          e            h1hdr          1
     O                                           26 'Budget template code .....'
     O               80                          31 'All'
     O              n80      ldbtcd              33
     O              n80      h1btds              60
      *
      *
     O          e            h1hdr       0  1
     O                                           26 'Budget availability code .'
     O               81                          31 'All'
     O              n81      ldbacd              28
     O              n81      h1bads              55
      *
      *
     O          e            h1hdr       0  1
     O                                           26 'Creation schedule code ...'
     O               82                          31 'All'
     O              n82      ldcscd              28
     O              n82      h1csds              55
      *
      *
     O          e            h1hdr       0  1
     O                                           26 'Budget template weeks ....'
     O               87                          31 'All'
     O              n87      ldbtwk        z     33
      *
      *
     O          e            h1hdr       0  1
     O                                           26 'Production phase code ....'
     O               83                          31 'All'
     O              n83      ldppcd              32
     O              n83      h1ppds              65
      *
      *
     O          e            h1hdr       0  1
     O                                           26 'Type of farm code ........'
     O               84                          31 'All'
     O              n84      ldtfcd              32
     O              n84      h1tfds              60
      *
      *
     O          e            h1hdr       0  1
     O                                           26 'Cell .....................'
     O               86                          31 'All'
     O              n86      ldcell        z     30
      *
      *
     O          e            h1hdr       0  1
     O                                           26 'Size .....................'
     O               88                          31 'All'
     O              n88      ldsize              33
      *
      *
     O          e            h1hdr       0  2
     O                                           26 'Active/Inactive status ...'
     O               85                          31 'All'
     O              n85      ldaist              28
      *
      *-------------------------------------------------------------
      * Column headings
      *-------------------------------------------------------------
      *
     O          e            h1hdr       0  1
     O                                            6 'Budget'
     O                                           43 'Budget'
     O                                           59 'Creation'
     O                                           78 'Production'
     O                                           87 'Type of'
     O                                          110 'Active/'
      *
      *
     O          e            h1hdr       0  1
     O                                            8 'Template'
     O                                           49 'Availability'
     O                                           59 'Schedule'
     O                                           73 'Phase'
     O                                           84 'Farm'
     O                                          110 'Inactive'
      *
      *
     O          e            h1hdr       0  1
     O                                            4 'Code'
     O                                           21 'Description'
     O                                           41 'Code'
     O                                           55 'Code'
     O                                           66 'Weeks'
     O                                           72 'Code'
     O                                           84 'Code'
     O                                           93 'Cell'
     O                                           99 'Size'
     O                                          110 'Status'
      *
     O          e            h1hdr       0  2
     O                       dash               110
      *
      *-------------------------------------------------------------
      * Detail line
      *-------------------------------------------------------------
      *
      *
     O          e            l1dtl          1
     O                       thbtcd               6
     O                       thbtds              35
     O                       thbacd              43
     O                       thcscd              55
     O                       thbtwk        k     66
     O                       thppcd              73
     O                       thtfcd              85
     O                       thcell        k     93
     O                       thsize             101
     O                       thaist             107
      *
      *-------------------------------------------------------------
      * No data message line
      *-------------------------------------------------------------
      *
     O          e            nodata      1
     O                                           19 'NO DATA MEETS YOUR'
     O                                           38 'SELECTION CRITERIA'
