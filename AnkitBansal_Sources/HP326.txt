      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Hog Production System
      * PROGRAM:     Workfile: Process BGF-to-Nursery Transfers
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     11/14/01
      *
      * FUNCTION:    This function writes records to the Workfile: Nursery Groups with BGF
      *              Sources. One record is written for each unique combination of:
      *                         nursery group
      *                         source BGF farm
      *                         calendar year
      *                         calendar week
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 07/06/09  LeAnne Ramsey
      *           Recompile only. Added new field 'Continuous Flow Flag' to Hog Group file.
      *
      * 10/15/13  LeAnne Ramsey (E2831)
      *           Recompile only. Added field 'MTech Reference'.
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fhsl034d   if   e           k disk
      *    Hog group
      *
      *
     Fhsp018    if   e           k disk
      *    Farm site
      *
      *
     Fhsj075g   if   e           k disk
      *  Transfer detail + transfer header
      *  (Records selected in open query)
      *
      *
     Fhsp326    uf a e           k disk
      *  Workfile: Nursery groups with BGF sources
      *
      /EJECT
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      ****************************************************************
      * ARRAYS AND TABLES
      ****************************************************************
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Save fields
      *
     D svrcdt          s                   like(mhrcdt)
      *
      *
      * Workfields
      *
     D wksvdt          s                   like(mhrcdt)
      *
      *
      * Workfields for date manipulation
      *
     D wkcymdiso       s               d   datfmt(*iso)
     D wkyymmdd        s              6  0
      *
      *
      * Parm fields
      *
     D xxcocd          s              3  0 inz(350)
     D xxreturn        s              7a
      *
     D xxjulian        s              5  0
     D xxcyymmdd       s              7  0
      *
     D xxhpscdyr       s                   like(w2svyr)
     D xxhpscdwk       s                   like(w2svwk)
     D xxhpspicdt      s              5  0
      *
     D xxcdwk          s                   like(w2svwk)
     D xxday           s              1a
      *
     D xxacyr          s                   like(w2svyr)
     D xxacpe          s              2  0
     D xxacwk          s                   like(w2svwk)
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ***************************************************************************************
      * MAINLINE
      ***************************************************************************************
      *
      * Process all BGF-to-Nursery transfers from 1999 forward that have quality or
      * rejected head.
      * (Records selected with open query and file keyed by Received Date.)
      *
     C                   dou       *in90 = *on                                  Main do loop
     C                   read      hsj075g                                90
     C                   if        *in90 = *off and                             If not EOF
     C                             (mdqlhd <> 0 or mdrjhd <> 0)
      *
      * Determine year/week serviced
      *
     C                   if        mhrcdt <> svrcdt
     C                   exsr      $svyrwk
     C                   z-add     mhrcdt        svrcdt
     C                   endif
      *
      * Either update or write a record to the workfile for this combination of:
      *  destination nursery group/origin BGF farm/calendar year/calendar week
      *
     C     key01         chain     hsp326                             92
     C                   if        *in92 = *off
     C                   exsr      $upd326
     C                   else
     C                   exsr      $wrt326
     C                   endif
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Main do loop
      *
      *
      * EOF Processing
      *
     C                   seton                                        lr
      /eject
      *--------------------------------------------------------------
      * Update a record
      *--------------------------------------------------------------
      *
     C     $upd326       begsr
      *
     C                   add       mdqlhd        w2rchd
     C                   add       mdrjhd        w2rchd
     C                   update    w2rec
      *
     C                   endsr
      /eject
      *--------------------------------------------------------------
      * Write a record
      *--------------------------------------------------------------
      *
     C     $wrt326       begsr
      *
      * Retrieve destination hog group code
      *
     C     mddnsn        chain     hsl034d                            92
     C                   if        *in92 = *off
     C                   move      hghgcd        w2dnhgcd
     C                   else
     C                   move      *blank        w2dnhgcd
     C                   endif
      *
      * Retrieve destination farm site cell
      *
     C     mhdnfs        chain     hsp018                             92
     C                   if        *in92 = *off
     C                   z-add     fscell        w2dncell
     C                   else
     C                   z-add     0             w2dncell
     C                   endif
      *
      * Retrieve origin farm site cell
      *
     C     mhorfs        chain     hsp018                             92
     C                   if        *in92 = *off
     C                   z-add     fscell        w2orcell
     C                   else
     C                   z-add     0             w2orcell
     C                   endif
      *
     C                   z-add     mhorfs        w2orfscd
     C                   z-add     mhdnfs        w2dnfscd
     C                   z-add     mddnsn        w2dnhgsn
     C                   z-add     xxhpscdyr     w2svyr
     C                   z-add     xxhpscdwk     w2svwk
     C     mdrjhd        add       mdqlhd        w2rchd
      *
     C                   write     w2rec
      *
     C                   endsr
      /eject
      *--------------------------------------------------------------
      * Determine year/week serviced
      *--------------------------------------------------------------
      *
     C     $svyrwk       begsr
      *
      * First, determine the 'serviced' date as 131 days prior to the
      * received date of the movement into this nursery group.
      *
     C                   movel     mhrcdt        wkcymdiso
     C                   subdur    131:*days     wkcymdiso
     C                   movel     wkcymdiso     wksvdt
      *
      * Now, you need the HPS calendar year/week associated with this serviced
      * date.  We get this from the OMS calendar file by calling a Synon program.
      *
     C                   exsr      $synon
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      *  Use SYNON program to retrieve calendar year/week
      *---------------------------------------------------------------
      *
     C     $synon        begsr
      *
      * Get the date into the SYNON format.
      *
     C                   z-add     wksvdt        wkyymmdd
     C                   if        wksvdt >= 20000000
     C     wkyymmdd      add       1000000       xxcyymmdd
     C                   else
     C                   z-add     wkyymmdd      xxcyymmdd
     C                   endif
      *
     C                   call      'POF3XFR'
     C                   parm      *blank        xxreturn
     C                   parm                    xxcocd
     C                   parm                    xxcyymmdd
     C                   parm      0             xxjulian
     C                   parm      0             xxacyr
     C                   parm      0             xxacpe
     C                   parm      0             xxacwk
     C                   parm      *blank        xxday
     C                   parm      0             xxcdwk
     C                   parm      0             xxhpspicdt
     C                   parm      0             xxhpscdyr
     C                   parm      0             xxhpscdwk
      *
      *
     C                   endsr
      /EJECT
      *----------------------------------------------------------------
      * Initialization subroutine
      *----------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
     C     key01         klist
     C                   kfld                    mddnsn
     C                   kfld                    mhorfs
     C                   kfld                    xxhpscdyr
     C                   kfld                    xxhpscdwk
      *
     C                   endsr
      /EJECT
