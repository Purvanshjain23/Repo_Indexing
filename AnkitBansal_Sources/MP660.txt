      *****************  RPG PROGRAM HEADING  ************************
      *
      * ENVIRONMENT:  PORK DIVISION
      * SYSTEM:       AS/400
      * PROGRAM:      MP660
      * DESCRIPTION:  MP2: List of Invoices to be Posted to JDE
      * PROGRAMMER:   LeAnne Fedor
      * DATE:         05/25/02
      *
      * FUNCTION:  Batch program to list the invoices that will be posted into JDE.
      *
      *            This listing is submitted from MP460-Specify Edit/Post of Invoices into
      *            JDE.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 09/24/02  LeAnne Fedor
      *           Recompile only.
      *           All amount fields in the database were increased from 7,2 to 9,2.
      *
      * 03/12/08  LeAnne Ramsey
      *           We gave all functionality to High Plains Biodiesel. So,
      *           we now run over two different library lists. We added a Data Area
      *           holding Company Number. We will now print Company Number/Name at
      *           the top of the Report.
      *           Also, changed print file from QPRINT to PRINT198.
      *
      * 02/25/11  Barb Gutierrez
      *           Changed sort sequence of report from invoice number and po number to
      *           vendor name, po number, and invoice number per use request.  E001297.
      *
      * 03/11/14  LeAnne Ramsey (E2992)
      *           We will now us the new Accounting Company (in the lda) instead of
      *           the company in data area DAMP2CONO.
      *
      * 06/02/21  Danny Nguyen (S17068)
      *           Recompile only due to MPP104 file change.
      *
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fcaabrel1  if   e           k disk
      *    Company
      *
      *
     Fmpp104    if   e           k disk
      *    MP2: Invoice header
      *
      *
     Fmpl304b   if   e           k disk
      *    MP2: Workfile of Invoices to Post to JDE
      *
      *
     Fprint198  o    f  198        printer oflind(*inof)
      *
      /EJECT
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Standard fields
      *
     D prtfl           s              1    inz('N')
     D dash            s            161    inz(*all'-')
     d rtime           s              6  0
      *
      * Control fields
      *
     d svinno          s                   like(mjinno)
     d svpono          s                   like(mjpono)
     D first           s              1    inz('Y')
      *
      *
      * Parm fields
      *
     d xxepfl          s              1
      *
      *
      * Print fields
      *
     d h1conm          s                   like(abadtx)
      *
     d l1rtds          s              7
     d l1docno         s                   like(ihdocno)
     d l1batno         s                   like(ihbatno)
     d h1epds          s              4
      *
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *---------------------------------------------------------------
      * Local data area.
      *---------------------------------------------------------------
      *
     Dlda             uds                  dtaara(*lda)
      *
     D  ldcymd                 2      9  0
     D  ldmdy                 10     15  0
     D  lduser                16     25
     D  ldacono               86     88  0
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * MAINLINE
      ****************************************************************
      *
      * Prepare to read the file and print headings the first
      * time through. (The workfile will hold data for a single Accounting
      * Company.)
      *
     C     *loval        setll     mpl304b
      *
     C                   except    h1hdr
      *
      * Read and print records in the file.
      *
     C                   dou       *in90 = *on                                  Main do
     C                   read      mpl304b                                90
      *
     C                   if        *in90 = *off                                 If not EOF
      *
     C                   if        first = yes
     C                   move      no            first
     C                   move      mjinno        svinno
     C                   move      mjpono        svpono
     C                   endif
      *
     C                   if        *inof = *on
     C                   except    h1hdr
     C                   endif
      *
      * Control break:
      *     Print a blank line if Invoice Number or Purchase Order Number changes
      *
     C                   if        mjinno <> svinno or
     C                             mjpono <> svpono
     C                   except    blank
     C                   move      mjinno        svinno
     C                   move      mjpono        svpono
     C                   endif
      *
      * Set up detail line
      *
     C                   select
     C                   when      mjrtcd = 'I'
     C                   eval      l1rtds = 'Items'
     C                   when      mjrtcd = 'T'
     C                   eval      l1rtds = 'Tax'
     C                   when      mjrtcd = 'F'
     C                   eval      l1rtds = 'Freight'
     C                   endsl
      *
     C     mjposn        chain     mpp104                             92
     C                   if        *in92 = *off                                 If found
     C                   z-add     ihdocno       l1docno
     C                   z-add     ihbatno       l1batno
     C                   endif                                                  If found
      *
      * Print detail line
      *
     C                   if        *inof = *on
     C                   except    h1hdr
     C                   endif
      *
     C                   except    l1dtl
     C                   movel     yes           prtfl
     C                   endif                                                  If not EOF
     C                   enddo                                                  Main do
      *
      *---------------------------------------------------------------
      * EOF processing
      *---------------------------------------------------------------
      * If the file was empty, print the standard 'no data...'
      * error message
      *
     C                   if        prtfl = no
     C                   except    nodata
     C                   endif
      *
     C                   seton                                        lr
      /EJECT
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
     C     *entry        plist
     C                   parm                    xxepfl
      *
     C                   select
     C                   when      xxepfl = 'E'
     C                   move      'EDIT'        h1epds
     C                   when      xxepfl = 'P'
     C                   move      'POST'        h1epds
     C                   endsl
      *
      *
      * Set up a line of underscores and retrieve time for report heading.
      *
     C                   time                    rtime
      *
      * Retrieve Company Name
      *
     C     ldacono       chain     caabrel1                           92
     C                   if        *in92 = *off
     C                   eval      h1conm = abadtx
     C                   endif
      *
     C                   endsr
      /EJECT
      *-------------------------------------------------------------
      * Report heading lines
      *-------------------------------------------------------------
      *
     Oprint198  e            h1hdr          1 01
     O                       sdpgm               10
     O                                           92 'MP2 INTERFACE TO JDE'
     O                                          151 'DATE'
     O                       udate         y    161
      *
     O          e            h1hdr          1
     O                       sdusr               10
     O                                           90 'LIST OF INVOICES TO POST'
     O                                           97 ' TO JDE'
     O                                          151 'TIME'
     O                       rtime              161 '  :  :  '
      *
     O          e            h1hdr          2
     O                       h1epds              83
     O                                          151 'PAGE'
     O                       page          z    161
      *
     O          e            h1hdr          1
     O                                           21 'Accounting company:'
     O                       ldacono       z     30
     O                       h1conm              62
      *
     O          e            h1hdr          1
     O                                           21 'User ID:'
     O                       lduser              33
      *
      *
     O          e            h1hdr          2
     O                                           21 'Cut-off invoice date:'
     O                       ldmdy               30 '  /  /  '
      *
     O          e            h1hdr          1
     O                                           97 'DOCUMENT'
     O                                          107 'BATCH'
     O                                          119 'JDE VENDOR'
      *
     O          e            h1hdr          1
     O                                           14 'INVOICE NUMBER'
     O                                           29 'P.O. NBR'
     O                                           51 'COST CENTER'
     O                                           59 'OBJECT'
     O                                           69 'SUBSID'
     O                                           86 'AMOUNT'
     O                                           97 'NUMBER'
     O                                          107 'NUMBER'
     O                                          119 'NUMBER'
     O                                          136 'JDE VENDOR NAME'
      *
     O          e            h1hdr          1
     O                       dash               161
      *
      *-------------------------------------------------------------
      * Detail line
      *-------------------------------------------------------------
      *
     O          e            l1dtl       1
     O                       mjinno              20
     O                       mjpono              29
     O                       l1rtds              37
     O                       mjmcu               51
     O                       mjobj               59
     O                       mjsub               71
     O                       mjmpval       k     87
     O                       l1docno       zb    97
     O                       l1batno       zb   107
     O                       mjjdvnno      z    119
     O                       mjjdvnnm           161
      *
      *-------------------------------------------------------------
      * Blank line
      *-------------------------------------------------------------
      *
     O          e            blank       1
      *
      *-------------------------------------------------------------
      * No data message line
      *-------------------------------------------------------------
      *
     O          e            nodata      1
     O                                           22 'THERE ARE NO INVOICES '
     O                                           36 'TO POST TO JDE'
      /eject
