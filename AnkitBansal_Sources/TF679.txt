      *****************  RPG PROGRAM HEADING  ************************
     h option(*SRCSTMT:*NODEBUGIO)
      ****************************************************************
      *
      * SYSTEM:      Triumph Foods
      * PROGRAM:     TF679
      * TITLE:       Yields: Weekly Yield Report (Recap)
      * PROGRAMMER:  LeAnne Ramsey
      * CREATED:     12/11/08
      *
      * FUNCTION:  Batch program to print a Recap at the Item Structure Level.
      *            A "Primal" section is printed followed by a "ByProduct" section.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 01/29/09  LeAnne Ramsey
      *           Recompile only. Fields were added to Weekly Yield file.
      *
      * 02/06/09  LeAnne Ramsey
      *           Add 1) subtotal line for Primals (after Item Structure 600)
      *               2) 4 columns: SBF Produced Pounds per Head Cut
      *                             SBF Produced Pounds per Head Killed
      *                             TF Produced Pounds per Head Cut
      *                             TF Produced Pounds per Head Killed
      *           Also, exclude all OT items; we print OTs on the Detail report but
      *           do not include them in the totals. So, to make the Report Total
      *           on the Recap match the Report Totals on the companion Detail and
      *           Summary reports, we will exclude the OTs here.
      *
      * 02/24/09  LeAnne Ramsey
      *           Combined Primal Recap and ByProduct Recap into a single program/report.
      *
      * 03/10/09  LeAnne Ramsey
      *           Add 4 columns to the new ByProduct Section:
      *               SBF Produced Pounds per Head Cut
      *               SBF Produced Pounds per Head Killed
      *               TF Produced Pounds per Head Cut
      *               TF Produced Pounds per Head Killed
      *
      * 04/01/09  LeAnne Ramsey
      *           Marty H. and Brad Hamilton wanted to add 4 columns (the SBF/TF Per Head
      *           columns for the 740-Kill Offal lines to the ByProduct Reports. We did not
      *           have room. So, Damon Ginther approved moving Kill Offal to the PRIMAL reports.
      *           So, this Recap Report has been changed to print the 740-Kill Offal in the
      *           Primal Section instead of the ByProduct Section.
      *
      * 04/06/09  LeAnne Ramsey
      *           We will now move all the 750-Render BPs to the PRIMAL reports.
      *
      * 11/09/17  Danny Nguyen    - R12011-Kill Cut Data for STF
      *           File TFP050 was changed. Recompile only. No logic change.
      *
      * 02/08/22  Danny Nguyen   - DO2484 - WI479 STF Variance Reporting
      *           Recompile only due to TFP076 DBF change.
      *           No changes to report.
      *
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Ftfp050    if   e           k disk
      *    Kill/Cut data
      *
      *
     Fome8rel0  if   e           k disk
      *    Product type
      *
      *
     Ftfl076a   if   e           k disk
      *  Weekly yields  (records selected with open query)
      *
      *
     Fprint198  o    f  198        printer oflind(*inof)
      *
      /EJECT
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Standard fields
      *
     D dash            s            198    inz(*all'-')
     d rtime           s              6  0
      *
      *
      * Control fields
      *
     D firstp          s              1    inz('Y')
     D firstb          s              1    inz('Y')
     D svistycd        s                   like(ywistycd)
      *
      *
      * Workfields
      *
     D wkalph          s              3
     D wklb            s                   like(ywsstdplb)
     d wkpc            s             15  6
     d wksdiffpc       s                   like(ywcalcypc)
     d wktdiffpc       s                   like(ywcalcypc)
      *
      *
      * Parms
      *
     D xxorigin        s              1
     D xxwedt          s              8  0
     D xxistycd        s                   like(ywistycd)
     D xxisgrcd        s                   like(ywisgrcd)
     D xxtfclcd        s                   like(ywtfclcd)
     D xxtfcgcd        s                   like(ywtfcgcd)
     D xxspulb         s                   like(ywspulb)
     D xxspuslb        s                   like(ywspuslb)
     D xxtpulb         s                   like(ywtpulb)
     D xxtpuslb        s                   like(ywtpuslb)
     D xxspc           s              7  4
     D xxskllb         s              7  4
     D xxsctlb         s              7  4
     D xxtpc           s              7  4
     D xxtkllb         s              7  4
     D xxtctlb         s              7  4
      *
      *
      * Print fields
      *
     d h1demand        s             35
     D l1desc          s             14
     d t1desc          s             16
      *
      *
      * Array index
      *
     d x               s              1  0
      *
      ****************************************************************
      * ARRAYS AND TABLES
      ****************************************************************
      *
     D arspulb         s             11  2 dim(2)
     D arspuslb        s             11  2 dim(2)
     D arsstdplb       s             11  2 dim(2)
     D arsdifflb       s             11  2 dim(2)
     D arspc           s              7  4 dim(2)
     D arskllb         s              7  4 dim(2)
     D arsctlb         s              7  4 dim(2)
      *
     D artpulb         s             11  2 dim(2)
     D artpuslb        s             11  2 dim(2)
     D artstdplb       s             11  2 dim(2)
     D artdifflb       s             11  2 dim(2)
     D artpc           s              7  4 dim(2)
     D artkllb         s              7  4 dim(2)
     D artctlb         s              7  4 dim(2)
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *---------------------------------------------------------------
      * Local data area.
      *---------------------------------------------------------------
      *
     Dlda             uds                  dtaara(*lda)
     D  ldpfcd                 1      1
     D  ldyr                   2      5  0
     D  ldwk                   6      7  0
      *
     D  ldwedt                29     36  0
     D  ldwemdy               44     49  0
      *
     D  ldtfcgcd             139    140
     D  ldtfcgds             141    155
      *
     D  ldtfclcd             156    158
     D  ldtfclds             159    188
      *
     D  ldistycd             189    191  0
     D  ldistyds             192    221
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ***************************************************************************************
      * MAINLINE
      ***************************************************************************************
      *
      * Print Sections:
      *   1) Primal
      *   2) ByProduct
     C                   exsr      $primal
     C                   exsr      $byproduct
      *
      * EOF processing
     C                   seton                                        lr
      /EJECT
      *---------------------------------------------------------------------------------------------
      * Primal Section
      *---------------------------------------------------------------------------------------------
      *
     C     $primal       begsr
      *
     C                   seton                                        87
     C                   exsr      $h1hdr
      *
      * Process Weekly Yields records summing to the Item Structure Type.
      * Print 1 line for each Item Structure Type.
      * Always Exclude: 1) Other
      *                 2) ByProducts...except Picnic Cushions, Kill Offal, Render
      *
     C     *loval        setll     tfl076a
      *
     C                   dou       *in90 = *on                                  Do primals
     C                   read      tfl076a                                90
     C                   if        *in90 = *off                                 If not EOF
     C                             and ywtfcgcd <> 'OT'
     C                             and (ywtfcgcd <> 'BP' or
     C                                 (ywistycd = 600 and ywisgrcd = 630 and
     C                                  ywtfcgcd = 'BP' and ywtfclcd = 'BP5') or
     C                                 (ywistycd = 740 and ywtfclcd = 'OFF' and
     C                                  ywtfcgcd = 'BP') or
     C                                 (ywistycd = 750 and ywtfcgcd = 'BP'))
      * Control breaks
     C                   select
     C                   when      firstp = yes
     C                   move      no            firstp
     C                   z-add     ywistycd      svistycd
      *
     C                   when      ywistycd <> svistycd
     C                   exsr      $l1dtl
     C                   z-add     ywistycd      svistycd
     C                   endsl
      *
      * Call program to return calc'd values
      *
     C                   exsr      $tf814
      *
      * Accumulate values into arrays
      *
     C                   add       ywspulb       arspulb
     C                   add       ywspuslb      arspuslb
     C                   add       xxspc         arspc
     C                   add       xxskllb       arskllb
     C                   add       xxsctlb       arsctlb
      *
     C                   add       ywtpulb       artpulb
     C                   add       ywtpuslb      artpuslb
     C                   add       xxtpc         artpc
     C                   add       xxtkllb       artkllb
     C                   add       xxtctlb       artctlb
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Do primals
      *
      * At file end
     C                   if        firstp = yes
     C                   except    nodata
     C                   else
     C                   exsr      $l1dtl
     C                   evalr     t1desc = 'Total:'
     C                   exsr      $t1tot
      *
     C                   z-add     0             arspuslb
     C                   z-add     0             arspulb
     C                   z-add     0             arsstdplb
     C                   z-add     0             arsdifflb
     C                   z-add     0             arspc
     C                   z-add     0             arskllb
     C                   z-add     0             arsctlb
      *
     C                   z-add     0             artpuslb
     C                   z-add     0             artpulb
     C                   z-add     0             artstdplb
     C                   z-add     0             artdifflb
     C                   z-add     0             artpc
     C                   z-add     0             artkllb
     C                   z-add     0             artctlb
     C                   endif
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------------------------------------
      * ByProduct Section
      *---------------------------------------------------------------------------------------------
      *
     C     $byproduct    begsr
      *
     C                   setoff                                       87
     C                   seton                                        88
     C                   except    blank
     C                   exsr      $h2hdr
      *
      * Process all ByProduct Weekly Yield records summing to the Item Structure Type.
      * Print 1 line for each Item Structure Type.
      *
     C     *loval        setll     tfl076a
      *
     C                   dou       *in90 = *on                                  Do byproduct
     C                   read      tfl076a                                90
     C                   if        *in90 = *off and                             If not EOF
     C                             ywtfcgcd = 'BP'
      *
      * Exclude Picnic Cushions/Kill Offal/Render (they are in the Primal Section)
      *
     C                   if        (ywistycd = 600 and ywisgrcd = 630 and       If exclude
     C                              ywtfclcd = 'BP5') or
     C                             (ywistycd = 740 and ywtfclcd = 'OFF' and
     C                              ywtfcgcd = 'BP') or
     C                             (ywistycd = 750 and ywtfcgcd = 'BP')
     C                   else
      *
      * Control breaks
     C                   select
     C                   when      firstb = yes
     C                   move      no            firstb
     C                   z-add     ywistycd      svistycd
      *
     C                   when      ywistycd <> svistycd
     C                   exsr      $l1dtl
     C                   z-add     ywistycd      svistycd
     C                   endsl
      *
      * Call program to return calc'd values
      *
     C                   exsr      $tf814
      *
      * Detail processing; accumulate values into arrays
      *
     C                   add       ywspulb       arspulb
     C                   add       ywsstdplb     arsstdplb
     C     ywspulb       sub       ywsstdplb     wklb
     C                   add       wklb          arsdifflb
     C                   add       xxskllb       arskllb
     C                   add       xxsctlb       arsctlb
      *
     C                   add       ywtpulb       artpulb
     C                   add       ywtstdplb     artstdplb
     C     ywtpulb       sub       ywtstdplb     wklb
     C                   add       wklb          artdifflb
     C                   add       xxtkllb       artkllb
     C                   add       xxtctlb       artctlb
      *
     C                   endif                                                  If exclude
     C                   endif                                                  If not EOF
     C                   enddo                                                  Do byproduct
      *
      * End of file
     C                   if        firstb = yes
     C                   except    nodata
     C                   else
     C                   exsr      $l1dtl
     C                   evalr     t1desc = 'Total:'
     C                   exsr      $t1tot
     C                   endif
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Call program to return calc'd values
      *---------------------------------------------------------------
      *
     C     $tf814        begsr
      *
     C                   call      'TF814'
     C                   parm      ywwedt        xxwedt
     C                   parm      ywistycd      xxistycd
     C                   parm      ywisgrcd      xxisgrcd
     C                   parm      ywtfclcd      xxtfclcd
     C                   parm      ywtfcgcd      xxtfcgcd
     C                   parm      ywspulb       xxspulb
     C                   parm      ywspuslb      xxspuslb
     C                   parm      ywtpulb       xxtpulb
     C                   parm      ywtpuslb      xxtpuslb
     C                   parm      0             xxspc
     C                   parm      0             xxsctlb
     C                   parm      0             xxskllb
     C                   parm      0             xxtpc
     C                   parm      0             xxtctlb
     C                   parm      0             xxtkllb
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Calcs for ByProduct lines
      *---------------------------------------------------------------
      *
     C     $calc         begsr
      *
      * Calc the Difference in Pounds as a Percent of Standard Production Pounds
      *
      *         Seaboard
     C                   if        arsstdplb(x) <> 0
     C                   eval      wkpc = (arsdifflb(x) / arsstdplb(x)) * 100
     C                   if        wkpc < 999
     C                   z-add(h)  wkpc          wksdiffpc
     C                   endif
     C                   endif
      *
      *          Triumph
     C                   if        artstdplb(x) <> 0
     C                   eval      wkpc = (artdifflb(x) / artstdplb(x)) * 100
     C                   if        wkpc < 999
     C                   z-add(h)  wkpc          wktdiffpc
     C                   endif
     C                   endif
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Print detail line  (Item Structure Type)
      *---------------------------------------------------------------
      *
     C     $l1dtl        begsr
      *
      * Set array index
     C                   z-add     1             x
      *
      * If you are here from the "ByProduct" subroutine, do calcs.
      *
     C                   if        firstb = no
     C                   exsr      $calc
     C                   endif
      *
      * Retrieve Item Structure Type Description and concatenate it to the Code.
      *
     C                   move      svistycd      wkalph
      *
     C     svistycd      chain     ome8rel0                           92
     C                   if        *in92 = *off
     C                   eval      l1desc = %trim(wkalph) + '-'
     C                                      + %trim(e8dctx)
     C                   else
     C                   eval      l1desc = wkalph + '-Unassigned'
     C                   endif
      * Overflow
     C                   if        *inof = *on
     C                   exsr      $h1hdr
     C                   endif
      *
      * Print detail line
     C                   except    l1dtl
      *
      * Clear array entry
     C                   z-add     0             arspuslb (x)
     C                   z-add     0             arspulb  (x)
     C                   z-add     0             arsstdplb(x)
     C                   z-add     0             arsdifflb(x)
     C                   z-add     0             arspc    (x)
     C                   z-add     0             arskllb  (x)
     C                   z-add     0             arsctlb  (x)
      *
     C                   z-add     0             artpuslb (x)
     C                   z-add     0             artpulb  (x)
     C                   z-add     0             artstdplb(x)
     C                   z-add     0             artdifflb(x)
     C                   z-add     0             artpc    (x)
     C                   z-add     0             artkllb  (x)
     C                   z-add     0             artctlb  (x)
      *
      *
      * If you just printed the line for Item Structure Type 600, then
      * the users want you to print a SubTotal line.
      *
     C                   if        svistycd = 600
     C                   evalr     t1desc = 'SubTotal:'
     C                   exsr      $t1tot
     C                   endif
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Print Total lines
      *---------------------------------------------------------------
      *
     C     $t1tot        begsr
      *
      * Set array index
     C                   z-add     2             x
      *
      * If you are here from the "ByProduct" subroutine, do calcs.
      *
     C                   if        firstb = no
     C                   exsr      $calc
     C                   endif
      * Overflow?
     C                   if        *inof = *on
     C                   exsr      $h1hdr
     C                   endif
      * Print total line
     C                   except    t1tot
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Print report heading
      *---------------------------------------------------------------
      *
     C     $h1hdr        begsr
      *
     C                   except    h1hdr
      *
      * Column headings
     C                   exsr      $h2hdr
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Print column headings
      *---------------------------------------------------------------
      *
     C     $h2hdr        begsr
      *
     C                   except    h2hdr
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Parm lists
      *
     C     *entry        plist
     C                   parm                    xxorigin
      *
      * Retrieve time for report heading.
      *
     C                   time                    rtime
      *
      *
      * This report is generated:
      *  1) From Inventory Stock Closing
      *  2) As a Preliminary/Final from Margin Adjustment Close
      *  3) On-demand
      *
     C                   select
     C                   when      xxorigin = 'N'
     C                   eval      %subst(h1demand:4:28) =
     C                             'From Inventory Stock Closing'
      *
     C                   when      ldpfcd = 'D'
     C                   eval      %subst(h1demand:13:9) = 'On-Demand'
      *
     C                   when      ldpfcd = 'F'
     C                   eval      %subst(h1demand:4:29) =
     C                             'Final Margin Adjustment Close'
      *
     C                   when      ldpfcd = 'P'
     C                   eval      %subst(h1demand:1:35) =
     C                             'Preliminary Margin Adjustment Close'
     C                   endsl
      *
      * Set up Report Headings
      *
     C                   if        ldistycd = 0
     C                   seton                                        95
     C                   endif
      *
     C                   if        ldtfcgcd = *blank
     C                   seton                                        94
     C                   endif
      *
     C                   if        ldtfclcd = *blank
     C                   seton                                        93
     C                   endif
      *
      * Retrieve Kill/Cut Data
     C     ldwedt        chain     tfp050
      *
     C                   endsr
      /EJECT
      *-------------------------------------------------------------------------------------
      * Report heading lines
      *-------------------------------------------------------------------------------------
      *
     Oprint198  e            h1hdr          1 01
     O                       sdpgm               10
     O                                          100 'WEEKLY YIELD REPORT'
     O                                          106 '-RECAP'
     O                                          188 'DATE'
     O                       udate         y    198
      *
     O          e            h1hdr          1
     O                       sdusr               10
     O                       h1demand           111
     O                                          188 'TIME'
     O                       rtime              198 '  :  :  '
      *
     O          e            h1hdr          1
     O                                          188 'PAGE'
     O                       page          z    198
      *
      *
     O          e            h1hdr          1
     O                                           17 'Week-ending date:'
     O                       ldwemdy             26 '  /  /  '
      *
     O                                           60 'TF Class group code:'
     O               94                          65 'All'
     O              n94      ldtfcgcd            64
     O              n94      ldtfcgds            82
      *
     O                                          130 'Head'
     O                                          147 'Hot Carcass'
     O                                          159 'Head'
     O                                          176 'Cold Carcass'
      *
     O          e            h1hdr          1
     O                                           17 'Year/Week:'
     O                       ldyr          m     23
     O                       ldwk          m     27
      *
     O                                           60 'TF Classification code:'
     O               93                          65 'All'
     O              n93      ldtfclcd            65
     O              n93      ldtfclds            97
      *
      *
     O                                          130 'Killed'
     O                                          147 'Pounds'
     O                                          159 'Cut'
     O                                          176 'Pounds'
      *
      *
     O          e            h1hdr          1
     O                                           60 'Item structure type code:'
     O               95                          65 'All'
     O              n95      ldistycd            65
     O              n95      ldistyds            97
      *
     O                                          130 '--------'
     O                                          147 '--------------'
     O                                          159 '--------'
     O                                          176 '--------------'
      *
     O          e            h1hdr          1
     O                                          120 'Seaboard:'
     O                       kcsklhd       k    131
     O                       kcshclb       k    148
     O                       kcscthd       k    160
     O                       kcscclb       k    177
      *
     O          e            h1hdr          2
     O                                          120 'Triumph:'
     O                       kctklhd       k    131
     O                       kcthclb       k    148
     O                       kctcthd       k    160
     O                       kctcclb       k    177
      *
      *-------------------------------------------------------------------------------------
      * Column Headings
      *-------------------------------------------------------------------------------------
      *
      * Primal
     O          e    87      h2hdr       1  0
     O                                           14 '*** PRIMAL ***'
     O          e    87      h2hdr       0  1
     O                                           14 '*** PRIMAL ***'
     O          e    87      h2hdr       1  1
     O                                            4 'Item'
     O                                           40 'SBF'
     O                                           57 'SBF'
     O                                           68 'SBF'
     O                                           80 'SBF Prod'
     O                                           92 'SBF Prod'
     O                                          113 'TF'
     O                                          130 'TF'
     O                                          142 'TF'
     O                                          154 'TF Prod'
     O                                          166 'TF Prod'
      *
     O          e    87      h2hdr       0  1
     O                                            9 'Structure'
     O                                           40 'Produced'
     O                                           57 'Produced'
     O                                           68 'Yield'
     O                                           80 'Lbs Per'
     O                                           92 'Lbs Per'
     O                                          113 'Produced'
     O                                          130 'Produced'
     O                                          142 'Yield'
     O                                          154 'Lbs Per'
     O                                          166 'Lbs Per'
      *
     O          e    87      h2hdr       0  1
     O                                            4 'Type'
     O                                           40 'Start Pounds'
     O                                           57 'Pounds'
     O                                           68 'Percent'
     O                                           80 'Hd Cut'
     O                                           92 'Hd Kill'
     O                                          113 'Start Pounds'
     O                                          130 'Pounds'
     O                                          142 'Percent'
     O                                          154 'Hd Cut'
     O                                          166 'Hd Kill'
      *
      * ByProduct
     O          e    88      h2hdr       1  0
     O                                           17 '*** BYPRODUCT ***'
     O          e    88      h2hdr       0  1
     O                                           17 '*** BYPRODUCT ***'
     O          e    88      h2hdr       1  1
     O                                            4 'Item'
     O                                           40 'SBF Std'
     O                                           58 'SBF'
     O                                           76 'SBF Produced'
     O                                           88 'SBF Diff'
     O                                           99 'SBF Prod'
     O                                          110 'SBF Prod'
      *
     O                                          127 'TF Std'
     O                                          145 'TF'
     O                                          163 'TF Produced'
     O                                          175 'TF Diff'
     O                                          186 'TF Prod'
     O                                          197 'TF Prod'
      *
     O          e    88      h2hdr       0  1
     O                                            9 'Structure'
     O                                           40 'Production'
     O                                           58 'Produced'
     O                                           76 'Minus Std'
     O                                           88 'as % of'
     O                                           99 'Lbs Per'
     O                                          110 'Lbs Per'
      *
     O                                          127 'Production'
     O                                          145 'Produced'
     O                                          163 'Minus Std'
     O                                          175 'as % of'
     O                                          186 'Lbs Per'
     O                                          197 'Lbs Per'
      *
      *
     O          e    88      h2hdr       0  1
     O                                            4 'Type'
     O                                           40 'Pounds'
     O                                           58 'Pounds'
     O                                           76 'Prod Pounds'
     O                                           88 'Std Lbs'
     O                                           99 'Hd Cut'
     O                                          110 'Hd Kill'
      *
     O                                          127 'Pounds'
     O                                          145 'Pounds'
     O                                          163 'Prod Pounds'
     O                                          175 'Std Lbs'
     O                                          186 'Hd Cut'
     O                                          197 'Hd Kill'
      *
     O          e            h2hdr          0
     O                       dash               198
      *
      *-------------------------------------------------------------------------------------
      * Detail line
      *-------------------------------------------------------------------------------------
      *
      * Primal
     O          e    87      l1dtl       1  1
     O                       l1desc              14
     O                       arspuslb (x)  k     41
     O                       arspulb  (x)  k     58
     O                       arspc    (x)  k     69
     O                       arsctlb  (x)  k     81
     O                       arskllb  (x)  k     93
      *
     O                       artpuslb (x)  k    114
     O                       artpulb  (x)  k    131
     O                       artpc    (x)  k    143
     O                       artctlb  (x)  k    155
     O                       artkllb  (x)  k    167
      * Byproduct
      *
     O          e    88      l1dtl       1  1
     O                       l1desc              14
     O                       arsstdplb(x)  k     41
     O                       arspulb  (x)  k     59
     O                       arsdifflb(x)  k     77
     O                       wksdiffpc     kb    89
     O                       arsctlb  (x)  k    100
     O                       arskllb  (x)  k    111
      *
     O                       artstdplb(x)  k    128
     O                       artpulb  (x)  k    146
     O                       artdifflb(x)  k    164
     O                       wktdiffpc     kb   176
     O                       artctlb  (x)  k    187
     O                       artkllb  (x)  k    198
      *
      *-------------------------------------------------------------------------------------
      * Total line
      *-------------------------------------------------------------------------------------
      *
      * Primal
     O          e    87      t1tot       0  1
     O                                           40 '--------------'
     O                                           57 '--------------'
     O                                           68 '--------'
     O                                           80 '--------'
     O                                           92 '--------'
     O                                          113 '--------------'
     O                                          130 '--------------'
     O                                          142 '--------'
     O                                          154 '--------'
     O                                          166 '--------'
      * Primal
     O          e    87      t1tot          2
     O                       t1desc              25
     O                       arspuslb (x)  k     41
     O                       arspulb  (x)  k     58
     O                       arspc    (x)  k     69
     O                       arsctlb  (x)  k     81
     O                       arskllb  (x)  k     93
      *
     O                       artpuslb (x)  k    114
     O                       artpulb  (x)  k    131
     O                       artpc    (x)  k    143
     O                       artctlb  (x)  k    155
     O                       artkllb  (x)  k    167
      *
      * ByProduct
     O          e    88      t1tot       0  1
     O                                           40 '--------------'
     O                                           58 '--------------'
     O                                           76 '--------------'
     O                                           88 '--------'
     O                                           99 '--------'
     O                                          110 '--------'
      *
     O                                          127 '--------------'
     O                                          145 '--------------'
     O                                          163 '--------------'
     O                                          175 '--------'
     O                                          186 '--------'
     O                                          197 '--------'
      *
     O          e    88      t1tot          2
     O                       t1desc              25
     O                       arsstdplb(x)  k     41
     O                       arspulb  (x)  k     59
     O                       arsdifflb(x)  k     77
     O                       wksdiffpc     kb    89
     O                       arsctlb  (x)  k    100
     O                       arskllb  (x)  k    111
      *
     O                       artstdplb(x)  k    128
     O                       artpulb  (x)  k    146
     O                       artdifflb(x)  k    164
     O                       wktdiffpc     kb   176
     O                       artctlb  (x)  k    187
     O                       artkllb  (x)  k    198
      *
      *-------------------------------------------------------------
      * Blank line
      *-------------------------------------------------------------
      *
     O          e            blank       2
      *
      *-------------------------------------------------------------
      * No data message line
      *-------------------------------------------------------------
      *
     O          e            nodata      1
     O                                           19 'No data meets your'
     O                                           39 'selection criteria.'
