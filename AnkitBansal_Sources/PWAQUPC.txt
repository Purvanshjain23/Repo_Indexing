/*********************************************************************/
/*  PROGRAM:         PWALUPC                                         */
/*  PROGRAM DESC:    UPD M3 SUPPLR 4/EA CMP UP EXECUTE USER PROGRAM  */
/*  PROGRAMMER:      DANNY NGUYEN                                    */
/*  CREATE DATE:     12/27/2016                                      */
/*                                                                   */
/*  FUNCTION:        THIS CLP WILL UPDATE THE M3 SUPPLIER (PLB2REP)  */
/*                   FILE FOR EACH COMPANY (360/960/440) IN IT'S     */
/*                   ASSOCIATED LIBRARY BY DIVISION # & UPDATE THE   */
/*                   M3 SUPPLIER INTERFACE (PWAACPP) FILE.           */
/*********************************************************************/
/* MODIFICATION HISTORY                                              */
/*                                                                   */
/* MM/DD/YY DEV  EXPLANATION OF CHANGE                               */
/* -------- ---- ----------------------------------------------------*/
/*                                                                   */
/*********************************************************************/

             PGM        PARM(&RTNCDE)

             DCL        VAR(&RTNCDE) TYPE(*CHAR) LEN(7)
             DCL        VAR(&M3LIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&TOLIB360) TYPE(*CHAR) LEN(10)
             DCL        VAR(&TOLIB440) TYPE(*CHAR) LEN(10)
             DCL        VAR(&TOLIB960) TYPE(*CHAR) LEN(10)
             DCL        VAR(&DIV#360) TYPE(*CHAR) LEN(10)
             DCL        VAR(&DIV#440) TYPE(*CHAR) LEN(10)
             DCL        VAR(&DIV#960) TYPE(*CHAR) LEN(10)

/*-------------------------------------------------------------------*/
/* GET LIBRARIES                                                     */
/*-------------------------------------------------------------------*/
             CALL       PGM(PBSHXFR) PARM('' &M3LIB) /* SYSTEM VALUE +
                          M3ILIB - M3 SUPPLIER (PLB2REP) FILE LIB  */

             CALL       PGM(POMTXFR) PARM('' X'360F' 'HPELIB' +
                          &TOLIB360) /*  COMPANY VALUE - GUYMON LIB */
             CALL       PGM(POMTXFR) PARM('' X'440F' 'HPELIB' +
                          &TOLIB440) /*  COMPANY VALUE - SIOUX CITY LIB */
             CALL       PGM(POMTXFR) PARM('' X'960F' 'HPELIB' +
                          &TOLIB960) /*  COMPANY VALUE - ST. JOSEPH LIB */

/*-------------------------------------------------------------------*/
/* GET M3 DIVISION NUMBER FOR EACH COMPANY                           */
/*-------------------------------------------------------------------*/
             CALL       PGM(POMTXFR) PARM('' X'360F' 'M3HPEDIV' +
                          &DIV#360) /*  COMPANY VALUE - GUYMON DIV # */
             CHGVAR     VAR(%SST(&DIV#360 4 7)) VALUE('0000000')

             CALL       PGM(POMTXFR) PARM('' X'440F' 'M3HPEDIV' +
                          &DIV#440) /*  COMPANY VALUE - SIOUX CITY +
                          DIV # */
             CHGVAR     VAR(%SST(&DIV#440 4 7)) VALUE('0000000')

             CALL       PGM(POMTXFR) PARM('' X'960F' 'M3HPEDIV' +
                          &DIV#960) /*  COMPANY VALUE - ST. JOSEPH +
                          DIV # */
             CHGVAR     VAR(%SST(&DIV#960 4 7)) VALUE('0000000')

/*-------------------------------------------------------------------*/
/* SETUP M3 FILE LIBRARY & OVERRIDE PRIMARY READ INDEX               */
/*-------------------------------------------------------------------*/
             RMVLIBLE   LIB(&M3LIB)
             MONMSG     MSGID(CPF2110 CPF2104) /* LIB NOT FOUND */
             ADDLIBLE   LIB(&M3LIB)
             MONMSG     MSGID(CPF2103) /* LIB ALREADY EXIST */

    /* PWAACPL2 ACCESS PATH SEL M3I ERP PROCESS STS='0' (NOT PROCESSED) */
             OVRDBF     FILE(PWAACPL2) TOFILE(&M3LIB/PWAACPL2) /* READ */
             OVRDBF     FILE(PWAACPL0) TOFILE(&M3LIB/PWAACPL0) /* UPDATE */

/*-------------------------------------------------------------------*/
/* UPDATE M3 SUPPLIER (PLB2REP) FILE FOR EACH COMPANY ASSOCIATED LIB.*/
/* UPDATE M3 ERP PROCESS STS FROM 0 (UNPROCESSED) TO 1 (PROCESSED).  */
/*-------------------------------------------------------------------*/
UPD360:
             RMVLIBLE   LIB(&TOLIB360)
             MONMSG     MSGID(CPF2110 CPF2104) /* LIB NOT FOUND */
             ADDLIBLE   LIB(&TOLIB360)
             MONMSG     MSGID(CPF2103) /* LIB ALREADY EXIST */

    /* OVERRIDE ACCESS PATHS TO USE COMPANY 360 LIB */
             OVRDBF     FILE(PLB2REL1) TOFILE(&TOLIB360/PLB2REL1)
             OVRDBF     FILE(PLB2REL0) TOFILE(&TOLIB360/PLB2REL0)

             CALL       PGM(PWAKXFR) PARM(&RTNCDE &DIV#360)

             DLTOVR     FILE(PLB2REL1)
             DLTOVR     FILE(PLB2REL0)
             RMVLIBLE   LIB(&TOLIB360)

UPD440:
             RMVLIBLE   LIB(&TOLIB440)
             MONMSG     MSGID(CPF2110 CPF2104) /* LIB NOT FOUND */
             ADDLIBLE   LIB(&TOLIB440)
             MONMSG     MSGID(CPF2103) /* LIB ALREADY EXIST */

    /* OVERRIDE ACCESS PATHS TO USE COMPANY 440 LIB */
             OVRDBF     FILE(PLB2REL1) TOFILE(&TOLIB440/PLB2REL1)
             OVRDBF     FILE(PLB2REL0) TOFILE(&TOLIB440/PLB2REL0)

             CALL       PGM(PWAKXFR) PARM(&RTNCDE &DIV#440)

             DLTOVR     FILE(PLB2REL1)
             DLTOVR     FILE(PLB2REL0)
             RMVLIBLE   LIB(&TOLIB440)

UPD960:
             RMVLIBLE   LIB(&TOLIB960)
             MONMSG     MSGID(CPF2110 CPF2104) /* LIB NOT FOUND */
             ADDLIBLE   LIB(&TOLIB960)
             MONMSG     MSGID(CPF2103) /* LIB ALREADY EXIST */

    /* OVERRIDE ACCESS PATHS TO USE COMPANY 960 LIB */
             OVRDBF     FILE(PLB2REL1) TOFILE(&TOLIB960/PLB2REL1)
             OVRDBF     FILE(PLB2REL0) TOFILE(&TOLIB960/PLB2REL0)

             CALL       PGM(PWAKXFR) PARM(&RTNCDE &DIV#960)

             DLTOVR     FILE(*ALL)
             RMVLIBLE   LIB(&TOLIB960)
             RMVLIBLE   LIB(&M3LIB)

 ENDPGM:
             ENDPGM
