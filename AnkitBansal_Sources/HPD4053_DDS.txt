     A*%%TS  SD  20020805  134530  ISLRAMS     REL-V5R1M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD4053
     A*  TITLE:         SPECIFY OPTIONS FOR WEAN PIG PROJECTIONS LISTING
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  CREATED:       06/09/02
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A N99                                  CF04(04 'PROMPT')
     A  44N99                               CF08(08 'SUBMIT')
     A N99                                  CF17(17 'PRINT OPTIONS')
     A N99                                  CF20(20 'WRKSPLF')
     A*
     A*---------------------------------------------------------------
     A* RECORD FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP4053R1
     A*%%TS  SD  20020805  134530  ISLRAMS     REL-V5R1M0  5722-WDS
     A                                      SETOF(30)
     A                                      SETOF(31)
     A                                      SETOF(32)
     A                                      SETOF(33)
     A                                      SETOF(34)
     A                                      SETOF(35)
     A                                      SETOF(36)
     A                                      SETOF(37)
     A                                      SETOF(38)
     A                                      SETOF(39)
     A                                      SETOF(55)
     A                                      SETOF(57)
     A                                      SETOF(58)
     A                                      SETOF(59)
     A                                      OVERLAY
     A                                      RTNCSRLOC(&RCD &FLD)
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 25'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USR)
     A                                      DSPATR(HI)
     A                                  2 14'SPECIFY OPTIONS FOR WEAN PIG PROJE-
     A                                      CTION LISTING'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@JOB)
     A                                      DSPATR(HI)
     A                                  4  2'From breed year/week ..'
     A N99        D1SVYR    R        B  4 28REFFLD(@@CDYR)
     A                                      EDTCDE(M)
     A N30                                  DSPATR(HI)
     A  30                                  DSPATR(RI)
     A  30                                  DSPATR(PC)
     A N99        D1SVWK    R        B  4 35REFFLD(@@CDWK)
     A                                      EDTCDE(M)
     A N31                                  DSPATR(HI)
     A  31                                  DSPATR(RI)
     A  31                                  DSPATR(PC)
     A                                  5  8'-- OR --'
     A                                      DSPATR(HI)
     A                                  6  2'From wean year/week ...'
     A N99        D1WNYR    R        B  6 28REFFLD(@@CDYR)
     A                                      EDTCDE(M)
     A N32                                  DSPATR(HI)
     A  32                                  DSPATR(RI)
     A  32                                  DSPATR(PC)
     A N99        D1WNWK    R        B  6 35REFFLD(@@CDWK)
     A                                      EDTCDE(M)
     A N33                                  DSPATR(HI)
     A  33                                  DSPATR(RI)
     A  33                                  DSPATR(PC)
     A                                  8  2'Weeks to print ........'
     A N99        D1WEEKS   R        B  8 28REFFLD(HSRREF/@@NU20 *LIBL/HSPREF)
     A                                      EDTCDE(M)
     A N34                                  DSPATR(HI)
     A  34                                  DSPATR(RI)
     A  34                                  DSPATR(PC)
     A                                  8 38'(Maximum of 52)'
     A                                  9  2'Detail or Summary .....'
     A                                  9 26'?'
     A N99        D1DSFL    R        B  9 28REFFLD(HSRREF/@@FLAG *LIBL/HSPREF)
     A N35                                  DSPATR(HI)
     A  35
     AO 55                                  DSPATR(PC)
     A  35                                  DSPATR(RI)
     A            D1DSDS    R        O  9 38REFFLD(@@DS10)
     A                                 11  2'Cell ..................'
     A N99        D1CELL    R        B 11 28REFFLD(HSRREF/@@CELL *LIBL/HSPREF)
     A                                      EDTCDE(M)
     A N36                                  DSPATR(HI)
     A  36                                  DSPATR(RI)
     A  36                                  DSPATR(PC)
     A                                 11 65'(Blank for all)'
     A                                 12  2'Farm site .............'
     A                                 12 26'?'
     A N99        D1FSCD    R        B 12 28REFFLD(HSRREF/@@FSCD *LIBL/HSPREF)
     A                                      EDTCDE(M)
     A N37                                  DSPATR(HI)
     A  37
     AO 57                                  DSPATR(PC)
     A  37                                  DSPATR(RI)
     A            D1FSNM    R        O 12 38REFFLD(HSRREF/FSFSNM *LIBL/HSPREF)
     A                                 12 65'(Blank for all)'
     A                                 14  2'The Wean Pig Projection Listing ha-
     A                                      s 2 parts. You may print both parts-
     A                                       or'
     A                                      DSPATR(HI)
     A                                 15  2'just a single part.'
     A                                      DSPATR(HI)
     A                                 17  2'Print wean pig projections ..'
     A                                 17 32'?'
     A N99        D1WYNFL   R        B 17 34REFFLD(HSRREF/@@FLAG *LIBL/HSPREF)
     A N38                                  DSPATR(HI)
     A  38
     AO 58                                  DSPATR(PC)
     A  38                                  DSPATR(RI)
     A                                 18  2'Print finisher projections ..'
     A                                 18 32'?'
     A N99        D1FYNFL   R        B 18 34REFFLD(HSRREF/@@FLAG *LIBL/HSPREF)
     A N39                                  DSPATR(HI)
     A  39
     AO 59                                  DSPATR(PC)
     A  39                                  DSPATR(RI)
     A*
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R HP4053K1
     A*%%TS  SD  20020609  133603  ISLRAMS     REL-V5R1M0  5722-WDS
     A  43                             21  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A N99                             23 11'F4=Prompt'
     A  44N99                          23 22'F8=Submit'
     A N99                             23 33'F17=Print Options'
     A N99                             23 52'F20=Spool Files'
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *---------------------------------------------------------------
     A          R HP4053ES                  SFL SFLMSGRCD(24)
      *
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
     A*---------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*---------------------------------------------------------------
     A*
     A          R HP4053EC                  SFLCTL(HP4053ES)
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
