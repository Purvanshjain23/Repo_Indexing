      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      HOG PRODUCTION
      * PROGRAM:     HP426
      * TITLE:       WORK WITH USERS
      * PROGRAMMER:  LEANNE FEDOR
      * CREATED:     02/13/95
      *
      * FUNCTION: THIS IS A WORK WITH THAT DISPLAYS THE RECORDS
      *           IN THE USERS FILE.
      *
      *           IT HAS BOTH POSITIONERS AND SELECTORS.
      *           IT LOADS THE SUBFILE A PAGE AT A TIME.
      *           IT USES THE STANDARD MESSAGE HANDLING PROGRAM.
      *           IT HAS STANDARD SUBFILE OPTION EDITING.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 06/05/95  LEANNE FEDOR
      *           CHANGED LOGIC TO REFILL SUBFILE AFTER F4 INSTEAD OF
      *           EDITING SUBFILE OPTIONS.
      *
      * 03/27/96  LEANNE FEDOR
      *           RECOMPILE ONLY. A NEW FIELD 'DELETE MOVEMENTS FLAG'
      *           WAS ADDED TO THE AUTHORIZED USERS FILE.
      *
      * 04/30/96  LEANNE FEDOR
      *           RECOMPILE ONLY. A NEW FIELD 'MOVEMENT STATUS'
      *           WAS ADDED TO THE AUTHORIZED USERS FILE TO SUPPORT
      *           THE LOGIC FOR DELETING MOVEMENTS.
      *
      * 05/14/97  LEANNE FEDOR
      *           RECOMPILE ONLY.  OUTQUEUE WAS ADDED TO THE
      *           AUTHORIZED USERS FILE. THIS FIELD WILL NOW BE THE
      *           USER'S DEFAULT PRINTER IN HPS INSTEAD OF THE OUTQ
      *           ON THE USER'S PROFILE.
      *
      * 06/18/97  LEANNE FEDOR
      *           ADDED LOGIC TO ALLOW USER TO SELECT PRINT OPTIONS BY
      *           TAKING A FUNCTION KEY AND ENTERING SELECTIONS.
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     FHPD426    CF   E             WORKSTN
     F                                     SFILE(HP426S1:HDRRN1)
     F                                     INFDS(IOFEED)
      *
     FHSP001    IF   E           K DISK
     F                                     INFDS(DBFEED)
      *    USERS                      (KEY: AUUSCD)
      *
     FHSP002    IF   E           K DISK
      *    FUNCTIONS                 (KEY: FNFNCD)
      *
      ****************************************************************
      * TABLE AND ARRAY SPECIFICATIONS
      ****************************************************************
      *
      *---------------------------------------------------------------
      *  STANDARD MESSAGE ARRAYS
      *---------------------------------------------------------------
      *
      *---------------------------------------------------------------
      *  COMPILE TIME ARRAY FOR SBMJOB USED IN QCMDEXC
      *---------------------------------------------------------------
     D CMD             S              1    DIM(80) CTDATA PERRCD(80)
      *
      ****************************************************************
      * INPUT SPECIFICATIONS
      ****************************************************************
      *
      *---------------------------------------------------------------
      * STANDARD ERROR MESSAGE HANDLING DATA STRUCTURES
      *---------------------------------------------------------------
      *
      * FOR ERROR MESSAGE HANDLING, A PACKED INDEX (E) IS REQUIRED.
      * FOR PROGRAM READABILITY, DEFINE A CORRESPONDING ERROR COUNT
      * FIELD CALLED 'ERROR'
      *
     D                 DS                  INZ
     D  ERROR                  1      2P 0
     D  E                      1      2P 0
      *
      * THIS DATA STRUCTURE SUPPLIES THE NAME OF THE MESSAGE FILE TO
      * THE MESSAGE HANDLING CL PROGRAM.  THE FIELD NAME MSGFIL MUST BE
      * CONSTANT. THE VALUE IN QUOTES IS THE NAME OF THE SPECIFIC
      * MESSAGE FILE CONTAINING THE USER DEFINED MESSAGES.
      *
     D                 DS                  INZ
     D  MSGFIL                 1     10    INZ('HSMSGF    ')
      *
      * THE FOLLOWING 3 DATA STRUCTURES ARE USED TO SPEED MESSAGE
      * HANDLING SINCE IT IS FASTER TO CLEAR DATA STRUCTURES THAN
      * ARRAYS.  EACH IS ASSOCIATED WITH A STANDARD MESSAGE ARRAY.
      *
     D MGI             DS           140    INZ
     D  MGID                   1    140
     D                                     DIM(20)                              MSG ID ARRAY
      *
     D MGD             DS          1000    INZ
     D  MGDT                   1   1000
     D                                     DIM(20)                              MSG PARMS
      *
     D MGWK            DS            50    INZ
     D  MGW                    1     50
     D                                     DIM(50)                              MSG WORK PARMS
      *
      *---------------------------------------------------------------
      * STANDARD PROGRAM STATUS DATA STRUCTURE
      *---------------------------------------------------------------
      *    EXTERNALLY DEFINED AS UTPGFR (RECORD FORMAT: PGMDSR)
     D PGMDS         ESDS                  EXTNAME(UTPGFR)
      *
      *---------------------------------------------------------------
      * STANDARD WORKSTATION INFORMATION DATA STRUCTURE
      *---------------------------------------------------------------
      *    EXTERNALLY DEFINED AS UTWSFR (RECORD FORMAT: UTIDFRR)
     D IOFEED        E DS                  EXTNAME(UTWSFR)
      *
      *---------------------------------------------------------------
      * STANDARD DATABASE FILE INFORMATION DATA STRUCTURE
      *---------------------------------------------------------------
      *    EXTERNALLY DEFINED AS UTDBFR (RECORD FORMAT: FDBCKD)
     D DBFEED        E DS                  EXTNAME(UTDBFR)
      *
      *---------------------------------------------------------------
      * STANDARD SUBFILE OPTION CONVERSION
      *---------------------------------------------------------------
     D                 DS
     D  DFOPT                  1      2
     D  WKOPT1                 1      1
     D  WKOPT2                 2      2
      *
      *---------------------------------------------------------------
      *  NAMED CONSTANTS
      *---------------------------------------------------------------
      *
     D CREATE          C                   CONST('CREATE')
     D REVISE          C                   CONST('REVISE')
     D DELETE          C                   CONST('DELETE')
     D VIEW            C                   CONST('VIEW  ')
     D SELECT          C                   CONST('SELECT')
     D YES             C                   CONST('Y')
     D NO              C                   CONST('N')
     D SET1            C                   CONST('SET1  ')
     D EDIT1           C                   CONST('EDIT1 ')
     D SCRN1           C                   CONST('SCRN1 ')
     D EXIT            C                   CONST('EXIT  ')
     D UPDATE          C                   CONST('UPDATE')
      *
      *---------------------------------------------------------------
      *  LDA
      *---------------------------------------------------------------
      *
     D LDA            UDS
     D  LDRPFL                 1      1
     D  LDUSCD                 2     11
      *
     D  LDOUTU               401    410
     D  LDHOLD               411    411
     D  LDCOPY               412    412  0
     D  LDOUTQ               413    422
      /EJECT
      ****************************************************************
      * MAIN LINE
      ****************************************************************
      *
     C                   MOVEL     SET1          ROUTNE
      *
     C     *INLR         DOWEQ     *OFF                                         MAIN DO LOOP
      *
     C     ROUTNE        CASEQ     SET1          $SET1
     C     ROUTNE        CASEQ     SCRN1         $SCRN1
     C     ROUTNE        CASEQ     EDIT1         $EDIT1
      *
     C     ROUTNE        CASEQ     EXIT          $EXIT
     C                   ENDCS
     C                   ENDDO                                                  MAIN DO LOOP
      /EJECT
      *----------------------------------------------------------------
      * $SET1 - SUBROUTINE TO SET ENVIRONMENT FOR SCREEN 1
      *----------------------------------------------------------------
      *
     C     $SET1         BEGSR
      *
      *  FILL THE SUBFILE
      *
     C                   EXSR      $CLR1
     C                   EXSR      $FRSH1
      *
      *  SET ROUTINE TO DISPLAY SCREEN
      *
     C                   MOVEL     SCRN1         ROUTNE
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $SCRN1 - SUBROUTINE TO PERFORM OPERATOR I/O FOR SCREEN 1
      *----------------------------------------------------------------
      *
     C     $SCRN1        BEGSR
      *
      * WRITE THE ERROR MESSAGES FROM THE ERROR ARRAYS TO THE
      * ERROR MESSAGE SUBFILE
      *
     C                   EXSR      $WRMSG
      *
      * WRITE SCREEN 1 TO CRT
      *
     C                   WRITE     HP426K1
     C                   WRITE     HP426EC
     C                   EXFMT     HP426C1
      *
      * SAVE THE VALUE OF THE LOWEST SUBFILE RRN ON THE SCREEN FOR
      * LATER REPOSITIONING THE SUBFILE
      *
     C                   Z-ADD     IDFLOW        WKFLOW
      *
      * CLEAR THE MESSAGES FROM THE MESSAGE DATA STRUCTURES
      * AND PROGRAM MESSAGE QUEUE.
      *
     C                   EXSR      $CLMSG
      *
      * GET USER'S ENTRY AND SET THE NEXT ROUTINE
      *
     C                   SELECT
     C     *IN03         WHENEQ    *ON                                          F3-EXIT
     C                   MOVEL     EXIT          ROUTNE
      *
     C     *IN04         WHENEQ    *ON                                          F4-PROMPT
     C                   EXSR      $F4S1
     C                   EXSR      $CHKSV
      *
     C     *IN05         WHENEQ    *ON                                          F5-REFRESH
     C                   MOVEL     YES           RESET
     C                   MOVEL     SET1          ROUTNE
      *
     C     *IN06         WHENEQ    *ON                                          F6-CREATE
     C                   EXSR      $CREAT
     C                   Z-ADD     WKFLOW        HDPAG1
     C                   MOVEL     SCRN1         ROUTNE
      *
     C     *IN07         WHENEQ    *ON                                          F7-LISTING
     C                   EXSR      $LIST1
     C                   Z-ADD     WKFLOW        HDPAG1
     C                   MOVEL     SCRN1         ROUTNE
      *
     C     *IN17         WHENEQ    *ON                                          F17-PRINT OPT
     C                   MOVEL     SCRN1         ROUTNE
     C                   CALL      'HP451'
     C                   PARM                    SDPGM
     C     LDCOPY        PARM      LDCOPY        XXCOPY
     C                   PARM                    LDHOLD
     C                   PARM                    LDOUTQ
     C                   PARM                    XXNITE
     C                   PARM                    XXJOBQ
      *
     C     *IN20         WHENEQ    *ON                                          F20-WRKSPLF
     C                   CALL      'QEZOUTPT'                                     API
     C                   Z-ADD     WKFLOW        HDPAG1
     C                   MOVEL     SCRN1         ROUTNE
      *
      *
      * REPOSITION THE SUBFILE IF THE USER HAS CHANGED ANY OF THE
      * POSITIONERS OR SELECTORS IN THE CONTROL RECORD.
      *
     C     SVUSCD        WHENNE    C1USCD                                       IF POSITION
     C     SVSLCD        ORNE      C1SLCD
     C     SVRUFL        ORNE      C1RUFL
     C     SVMDFL        ORNE      C1MDFL
     C     SVRMFL        ORNE      C1RMFL
     C     SVRVFL        ORNE      C1RVFL
     C     SVAFFL        ORNE      C1AFFL
     C                   EXSR      $CHKSV
      *
     C     *IN25         WHENEQ    *ON                                          F25-ROLLUP
     C                   EXSR      $LOAD1
     C                   OTHER
      *
      * USER HAS PRESSED ENTER
      *
     C     NORECS        IFEQ      YES                                          IF EMPTY SFL
     C                   MOVEL     SET1          ROUTNE
     C                   ELSE
     C                   MOVEL     EDIT1         ROUTNE
     C                   ENDIF                                                  IF EMPTY SFL
     C                   ENDSL
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $CHKSV - SAVE THE CONTROL RECORD POSITIONERS/SELECTORS
      *----------------------------------------------------------------
      *
     C     $CHKSV        BEGSR
      *
     C                   MOVEL     NO            RESET
     C                   MOVEL     SET1          ROUTNE
      *
     C                   MOVEL     C1USCD        SVUSCD
     C                   MOVEL     C1SLCD        SVSLCD
     C                   MOVEL     C1RUFL        SVRUFL
     C                   MOVEL     C1MDFL        SVMDFL
     C                   MOVEL     C1RMFL        SVRMFL
     C                   MOVEL     C1RVFL        SVRVFL
     C                   MOVEL     C1AFFL        SVAFFL
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $EDIT1 - SUBROUTINE TO EDIT INPUT FIELDS FOR SCREEN 1
      *----------------------------------------------------------------
      *
     C     $EDIT1        BEGSR
      *
      * FIRST, EDIT ALL THE SELECTIONS ENTERED BY THE USER IN THE
      * SUBFILE OPTION FIELD TO SEE THAT VALID SELECTIONS WERE INPUT
      *
     C                   EXSR      $EDOP1
      *
      * IF THERE ARE NO ERRORS IN THE SUBFILE OPTION SELECTORS ENTERED
      * BY THE USER, THEN EXECUTE THE SUBROUTINE TO REREAD AND PROCESS
      * THE RECORDS SELECTED BY THE USER.  AFTER COMPLETING THE
      * SUBROUTINE, SET THE ROUTINE TO START OVER.
      *
     C     ERROR         IFEQ      0                                            IF NO ERROR
     C                   EXSR      $PROC1
      *
     C     RTNCD         IFEQ      '00'
     C                   MOVEL     SET1          ROUTNE
     C                   ELSE
     C                   Z-ADD     WKFLOW        HDPAG1
     C                   MOVEL     SCRN1         ROUTNE
     C                   ENDIF
      *
      * ELSE THERE WERE ERRORS IN THE SUBFILE OPTIONS ENTERED, SO SET
      * TO REDISPLAY SCREEN AND ERRORS TO USER
      *
     C                   ELSE
     C                   MOVEL     SCRN1         ROUTNE
     C                   ENDIF                                                  IF NO ERROR
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * $F4S1 - F4 PROMPT ON SCREEN 1                                    *
      *---------------------------------------------------------------
      *
     C     $F4S1         BEGSR
      *
     C                   SELECT
      *
      * SECURITY LOCATION
      *
     C     FLD           WHENEQ    'C1SLCD'
     C                   MOVE      *BLANK        XXSLCD
     C                   CALL      'HP588'
     C                   PARM                    XXSLCD
     C                   SETON                                        55
     C     XXSLCD        IFNE      *BLANK                                       IF SELECTED
     C                   MOVEL     XXSLCD        C1SLCD
     C                   ENDIF                                                  IF SELECTED
      *
      * RESTRICTED USER
      *
     C     FLD           WHENEQ    'C1RUFL'
     C                   MOVE      *BLANK        XXRUFL
     C                   CALL      'HP522'
     C                   PARM                    XXRUFL
     C                   SETON                                        53
     C     XXRUFL        IFNE      *BLANK                                       IF SELECTED
     C                   MOVEL     XXRUFL        C1RUFL
     C                   ENDIF                                                  IF SELECTED
      *
      * MODIFY DATA
      *
     C     FLD           WHENEQ    'C1MDFL'
     C                   MOVE      *BLANK        XXMDFL
     C                   CALL      'HP522'
     C                   PARM                    XXMDFL
     C                   SETON                                        58
     C     XXMDFL        IFNE      *BLANK                                       IF SELECTED
     C                   MOVEL     XXMDFL        C1MDFL
     C                   ENDIF                                                  IF SELECTED
      *
      * RESTRICTED MAINTENANCE
      *
     C     FLD           WHENEQ    'C1RMFL'
     C                   MOVE      *BLANK        XXRMFL
     C                   CALL      'HP522'
     C                   PARM                    XXRMFL
     C                   SETON                                        54
     C     XXRMFL        IFNE      *BLANK                                       IF SELECTED
     C                   MOVEL     XXRMFL        C1RMFL
     C                   ENDIF                                                  IF SELECTED
      *
      * RESTRICTED VIEW
      *
     C     FLD           WHENEQ    'C1RVFL'
     C                   MOVE      *BLANK        XXRVFL
     C                   CALL      'HP522'
     C                   PARM                    XXRVFL
     C                   SETON                                        57
     C     XXRVFL        IFNE      *BLANK                                       IF SELECTED
     C                   MOVEL     XXRVFL        C1RVFL
     C                   ENDIF                                                  IF SELECTED
      *
      * USER MUST BE AUTHORIZED TO FUNCTIONS FLAG
      *
     C     FLD           WHENEQ    'C1AFFL'
     C                   MOVE      *BLANK        XXAFFL
     C                   CALL      'HP522'
     C                   PARM                    XXAFFL
     C                   SETON                                        58
     C     XXAFFL        IFNE      *BLANK                                       IF SELECTED
     C                   MOVEL     XXAFFL        C1AFFL
     C                   ENDIF                                                  IF SELECTED
     C                   OTHER
      *
      * F4 NOT VALID FOR THIS FIELD SO ISSUE MESSAGE
      *
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR                                        013
     C                   MOVEL     'HS09011'     MGID(E)
     C                   ENDIF
     C                   ENDSL
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * $CLR1  - SUBROUTINE TO CLEAR THE SUBFILE FOR SCREEN 1
      *---------------------------------------------------------------
      *
     C     $CLR1         BEGSR
      *
     C                   Z-ADD     0             SVRRN1
      *
     C                   CLEAR                   HP426S1
     C                   SETON                                        83        SFLCLR
     C                   WRITE     HP426C1
     C                   SETOFF                                       818283
      *                                                    81=SFLDSP
      *                                                    82=SFLEND
      *                                                    83=SFLCLR
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * $FRSH1 - SUBROUTINE TO REFRESH/FILL THE SUBFILE FOR SCREEN 1
      *---------------------------------------------------------------
      *
     C     $FRSH1        BEGSR
      *
      * SET POSITION IN FILE
      *
     C     C1USCD        SETLL     HSP001
      *
      * THE FIELD 'RESET' IS A FLAG FIELD TO INDICATE WHETHER YOU ARE
      * ATTEMPTING TO REPOSITION THE USER CLOSE TO WHERE HE WAS IN THE
      * SUBFILE.
      * THE LOWEST SUBFILE RRN ON THE SCREEN FROM THE DSPF
      * FEEDBACK AREA (IDFLOW) WAS RETRIEVED EARLIER AND PUT IN WKFLOW.
      * ADD TO WKFLOW THE NUMBER OF LINES ON YOUR SUBFILE PAGE OR THE
      * NUMBER LESS 1 TO DETERMINE A SUBFILE RRN TO REPOSITION TO.
      *
     C     RESET         IFEQ      YES
     C     WKFLOW        ADD       WKRLIN        MAXRRN
     C                   ELSE
      *
      * DON'T TRY TO REPOSITION THE USER, JUST START THE SUBFILE OVER
      *
     C                   Z-ADD     WKPLIN        MAXRRN
     C                   ENDIF
      *
      * LOAD THE SUBFILE UNTIL YOU HAVE THE REQUIRED NUMBER OF RECORDS.
      * THE NUMBER WILL DEPEND ON WHETHER YOU ARE REPOSITIONING THE
      * SUBFILE OR NOT. STOP IF YOU GET TO THE END OF THE SUBFILE.
      *
     C     SVRRN1        DOUGE     MAXRRN
     C     *IN82         OREQ      *ON
     C                   EXSR      $LOAD1
     C                   ENDDO
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * $LOAD1- SUBROUTINE TO LOAD THE SUBFILE FOR SCREEN 1
      *---------------------------------------------------------------
      *
     C     $LOAD1        BEGSR
      *
     C                   Z-ADD     SVRRN1        HDRRN1
      *
      * SET THE COUNT OF RECORDS ON THE SUBFILE PAGE TO ZERO.
      *
     C                   Z-ADD     0             COUNT             2 0
      *
      * FILL THE SUBFILE UNTIL THE END OF THE DATABASE FILE OR YOU
      * HAVE FILLED ALL THE LINES ON THE SUBFILE PAGE
      *
     C                   SETOFF                                       82
     C     *IN82         DOUEQ     *ON                                           DO FILL
     C     COUNT         ORGE      WKPLIN
      *
     C                   READ      HSP001                                 82
     C     *IN82         IFEQ      *OFF                                          IF READ
      *
      * ONLY INCLUDE RECORDS THAT MATCH THE USER'S SELECTORS
      *
     C     C1SLCD        IFNE      *BLANK                                        IF LOCATION
     C     C1SLCD        ANDNE     AUSLCD
     C                   ELSE
      *
     C     C1RUFL        IFNE      *BLANK                                        IF USER
     C     C1RUFL        ANDNE     AURUFL
     C                   ELSE
      *
     C     C1MDFL        IFNE      *BLANK                                        IF MODIFY
     C     C1MDFL        ANDNE     AUMDFL
     C                   ELSE
      *
     C     C1RMFL        IFNE      *BLANK                                        IF MAINT
     C     C1RMFL        ANDNE     AURMFL
     C                   ELSE
      *
     C     C1RVFL        IFNE      *BLANK                                        IF VIEW
     C     C1RVFL        ANDNE     AURVFL
     C                   ELSE
      *
     C     C1AFFL        IFNE      *BLANK                                        IF AUTH
     C     C1AFFL        ANDNE     AUAFFL
     C                   ELSE
      *
     C                   ADD       1             COUNT
     C                   ADD       1             HDRRN1
     C                   MOVE      *BLANK        DFOPT
     C                   MOVEL     AUUSCD        DFUSCD
     C                   MOVEL(P)  AUUSNM        DFUSNM
     C                   MOVEL     AUSLCD        DFSLCD
     C                   MOVEL     AURUFL        DFRUFL
     C                   MOVEL     AUMDFL        DFMDFL
     C                   MOVEL     AURMFL        DFRMFL
     C                   MOVEL     AURVFL        DFRVFL
     C                   MOVEL     AUAFFL        DFAFFL
     C                   WRITE     HP426S1
     C                   SETON                                        81
      *
      * FOR POSITIONING, PLACE THE SUBFILE RELATIVE RECORD NUMBER INTO
      * THE SFLRCDNBR FIELD (HDPAG1)
      *
     C                   Z-ADD     HDRRN1        HDPAG1
      *
     C                   ENDIF                                                   IF AUTH
     C                   ENDIF                                                   IF VIEW
     C                   ENDIF                                                   IF MAINT
     C                   ENDIF                                                   IF MODIFY
     C                   ENDIF                                                   IF USER
     C                   ENDIF                                                   IF LOCATION
     C                   ENDIF                                                   IF READ
     C                   ENDDO                                                   DO FILL
      *
      * IF NO RECORDS WERE WRITTEN TO THE SUBFILE, THEN ISSUE
      * THE STANDARD MESSAGE 'NO DATA TO DISPLAY'.
      *
     C     HDRRN1        IFEQ      0
     C                   MOVEL     YES           NORECS
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS09002'     MGID(E)
     C                   ENDIF
     C                   ELSE
     C                   MOVEL     NO            NORECS
     C                   ENDIF
      *
     C                   Z-ADD     HDRRN1        SVRRN1
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * $CREAT - SUBROUTINE TO CALL THE MAINTENANCE PROGRAM WHEN THE
      *          USER TAKES F6=CREATE
      *---------------------------------------------------------------
      *
     C     $CREAT        BEGSR
      *
     C                   MOVEL     '00'          RTNCD
     C                   MOVEL     CREATE        MODE
     C                   MOVE      *BLANK        DFUSCD
     C                   CALL      'HP126'
     C                   PARM                    MODE
     C                   PARM                    DFUSCD
     C                   PARM                    RTNCD
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * $EDOP1 - SUBROUTINE TO READ CHANGED SUBFILE RECORDS AND EDIT
      *          FOR VALID SUBFILE OPTIONS ON SCREEN 1
      *---------------------------------------------------------------
      *
     C     $EDOP1        BEGSR
      *
     C                   Z-ADD     0             ERROR
      *
      * READ ALL THE CHANGED SUBFILE RECORDS EDITING FOR VALID OPTIONS
      *
     C                   SETOFF                                       80
     C     *IN80         DOUEQ     *ON                                          DO READC
     C                   READC     HP426S1                                80
     C     *IN80         IFEQ      *OFF                                         IF REC READ
      *
      * SET OFF ERROR INDICATOR FOR SUBFILE OPTION FIELD
      *
     C                   SETOFF                                       30
      *
      * CONVERT OPTION USER ENTERED TO STANDARD FORMAT. FOR EXAMPLE,
      * A ' 1', '01', AND '1 ' MUST BE STANDARDIZED TO ' 1'.
      *
     C     WKOPT2        IFEQ      *BLANK                                       IF 2 = BLANK
     C                   MOVE      WKOPT1        WKOPT2
     C                   MOVE      *BLANKS       WKOPT1
     C                   ENDIF                                                  IF 2 = BLANK
      *
     C     WKOPT1        IFEQ      *ZERO                                        IF 1 IS ZERO
     C                   MOVE      *BLANKS       WKOPT1
     C                   ENDIF                                                  IF 1 IS ZERO
      *
      * TEST FOR VALID SUBFILE OPTION ENTRY ON ALL CHANGED RECORDS
      *
     C     DFOPT         IFNE      ' 2'                                         IF BAD OPT
     C     DFOPT         ANDNE     ' 3'
     C     DFOPT         ANDNE     ' 4'
     C     DFOPT         ANDNE     ' 6'
     C     DFOPT         ANDNE     ' 7'
     C     DFOPT         ANDNE     ' 8'
     C     DFOPT         ANDNE     '  '
     C                   SETON                                        3085
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS09006'     MGID(E)
     C                   ENDIF
     C                   EXSR      $ERR1
     C                   ELSE
      *
      * PERFORM ADDITIONAL VALIDATIONS FOR
      *     OPTION 7=MAINTAIN AUTHORIZED FUNCTIONS
      *
     C     DFOPT         IFEQ      ' 7'                                         IF OPTION 7
     C     DFAFFL        ANDNE     YES
     C                   SETON                                        3085
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS00576'     MGID(E)
     C                   CLEAR                   MGW
     C                   MOVEA     DFOPT         MGW(1)
     C                   MOVEA     MGW           MGDT(E)
     C                   EXSR      $ERR1
     C                   ENDIF
     C                   ENDIF                                                  IF OPTION 7
      *
     C     ERROR         IFEQ      0
     C     DFOPT         ANDEQ     *BLANK
     C                   SETOFF                                       85
     C                   ELSE
     C                   SETON                                        85
     C                   ENDIF
     C                   ENDIF                                                  IF BAD OPT
      *
     C                   UPDATE    HP426S1
     C                   SETOFF                                       8530
      *
     C                   ENDIF                                                  IF REC READ
     C                   ENDDO                                                  DO READC
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * $PROC1 - SUBROUTINE TO READ SUBFILE RECORDS AND PROCESS
      *          USER SELECTIONS FOR SCREEN 1
      *---------------------------------------------------------------
      *
     C     $PROC1        BEGSR
      *
     C                   Z-ADD     0             ERROR
     C                   MOVEL     '00'          RTNCD
      *
      * READ ALL THE CHANGED SUBFILE RECORDS PROCESSING THE SUBFILE
      * SELECTION
      *
     C                   SETOFF                                       80
     C     *IN80         DOUEQ     *ON                                          DO PROCESS
     C                   READC     HP426S1                                80
     C     *IN80         IFEQ      *OFF                                         IF REC READ
      *
     C     RTNCD         IFEQ      '00'                                         IF ENTER
      *
      * IF USER SELECTED OPTION 2=REVISE,
      * SET THE FLAG 'RESET' TO YES SO THAT WHEN YOU RETURN TO THIS
      * WORK WITH YOU WILL REPOSITION TO THE SAME PLACE IN THE SUBFILE
      *
     C                   SELECT
     C     DFOPT         WHENEQ    ' 2'
     C                   MOVEL     YES           RESET
     C                   MOVEL     REVISE        MODE
     C                   CALL      'HP126'
     C                   PARM                    MODE
     C                   PARM                    DFUSCD
     C                   PARM                    RTNCD
      *
      * IF USER SELECTED OPTION 3=COPY AUTHORIZED FUNCTIONS
      * SET THE FLAG 'RESET' TO YES SO THAT WHEN YOU RETURN TO THIS
      * WORK WITH YOU WILL REPOSITION TO THE SAME PLACE IN THE SUBFILE
      *
     C     DFOPT         WHENEQ    ' 3'
     C                   MOVEL     YES           RESET
     C                   CALL      'HP123'
     C                   PARM                    DFUSCD
     C                   PARM                    RTNCD
      *
      * IF USER SELECTED OPTION 4=DELETE,
      * SET THE FLAG 'RESET' TO YES SO THAT WHEN YOU RETURN TO THIS
      * WORK WITH YOU WILL REPOSITION TO THE SAME PLACE IN THE SUBFILE
      *
     C     DFOPT         WHENEQ    ' 4'
     C                   MOVEL     YES           RESET
     C                   MOVEL     DELETE        MODE
     C                   CALL      'HP126'
     C                   PARM                    MODE
     C                   PARM                    DFUSCD
     C                   PARM                    RTNCD
      *
      * IF USER SELECTED OPTION 6=PRINT,
      * SE FLAG 'RESET' TO YES, SO THAT WHEN YOU RETURN TO THIS WORK
      * WITH YOU WILL REPOSITION TO THE SAME PLACE IN THE SUBFILE.
      *
     C     DFOPT         WHENEQ    ' 6'
     C                   MOVEL     YES           RESET
     C                   EXSR      $PRNT1
      *
      * IF USER SELECTED OPTION 7=MAINTAIN AUTHORIZED FUNCTIONS,
      * SET THE FLAG 'RESET' TO YES SO THAT WHEN YOU RETURN TO THIS
      * WORK WITH YOU WILL REPOSITION TO THE SAME PLACE IN THE SUBFILE
      *
     C     DFOPT         WHENEQ    ' 7'
     C                   MOVEL     YES           RESET
     C                   CALL      'HP128'
     C                   PARM                    DFUSCD
     C                   PARM                    DFMDFL
     C                   PARM                    RTNCD
      *
      * IF USER SELECTED OPTION 8=VIEW AUTHORIZED FUNCTIONS,
      * SET THE FLAG 'RESET' TO YES SO THAT WHEN YOU RETURN TO THIS
      * WORK WITH YOU WILL REPOSITION TO THE SAME PLACE IN THE SUBFILE
      *
     C     DFOPT         WHENEQ    ' 8'
     C                   MOVEL     YES           RESET
     C                   CALL      'HP728'
     C                   PARM                    DFUSCD
     C                   PARM                    RTNCD
     C                   ENDSL
      *
      * UPON RETURNING FROM THE CALLED PROGRAM,
      * CLEAR THE SUBFILE OPTION SELECTOR ENTERED BY THE USER AND
      * REWRITE THE SUBFILE RECORD
      *
     C                   MOVE      *BLANK        DFOPT
     C                   SETOFF                                       85
     C                   ELSE
     C                   SETON                                        85
     C                   ENDIF                                                  IF ENTER
      *
     C                   UPDATE    HP426S1
     C                   ENDIF                                                  IF REC READ
      *
     C                   ENDDO                                                  DO PROCESS
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * $LIST1 - PRINT A LISTING OF THE MASTER FILE USING CF07
      *---------------------------------------------------------------
      *
     C     $LIST1        BEGSR
      *
      *
      * SET UP THE LDA FIELDS
      *
     C                   MOVEL     'L'           LDRPFL
     C                   OUT       LDA
      *
      * MOVE THE 80-CHARACTER COMPILE TIME ARRAY 'CMD' TO AN
      * 80-CHARACTER FIELD FOR PASSING AS A PARM WITH QCMDEXC.
      *
     C                   MOVEA     XXJOBQ        CMD(51)
     C                   MOVEA     CMD           CMDSBM
     C                   CALL      'QCMDEXC'
     C                   PARM                    CMDSBM
     C                   PARM                    LENGTH
      *
      * ISSUE STANDARD REQUEST SUBMISSION MESSAGE
      *
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS09003'     MGID(E)
     C                   ENDIF
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * $PRNT1 - OPTION 6 TO PRINT AUTHORIZED FUNCTIONS FOR A USER
      *---------------------------------------------------------------
      *
     C     $PRNT1        BEGSR
      *
      *
      * MOVE DISPLAY FILE FIELDS INTO LDA
      *
     C                   MOVEL     'P'           LDRPFL
     C                   MOVEL     DFUSCD        LDUSCD
     C                   OUT       LDA
      *
      * MOVE THE 80-CHARACTER COMPILE TIME ARRAY 'CMD' TO AN
      * 80-CHARACTER FIELD FOR PASSING AS A PARM WITH QCMDEXC.
      *
     C                   MOVEA     XXJOBQ        CMD(51)
     C                   MOVEA     CMD           CMDSBM
     C                   CALL      'QCMDEXC'
     C                   PARM                    CMDSBM
     C                   PARM                    LENGTH
      *
      * ISSUE STANDARD REQUEST SUBMISSION MESSAGE
      *
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS09003'     MGID(E)
     C                   ENDIF
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * $ERR1  - SUBROUTINE TO SET THE SUBFILE TO REDISPLAY AT
      *          THE FIRST PAGE THAT HAS ERRORS FOR SCREEN 1
      *---------------------------------------------------------------
      *
     C     $ERR1         BEGSR
      *
     C     ERROR         IFEQ      1
     C                   Z-ADD     HDRRN1        HDPAG1
     C                   ENDIF
      *
     C                   ENDSR
      *
      *---------------------------------------------------------------
      * $EXIT - SUBROUTINE TO SET ON LR AND END JOB
      *---------------------------------------------------------------
      *
     C     $EXIT         BEGSR
     C                   SETON                                        LR
     C                   ENDSR
      *
      *---------------------------------------------------------------
      * $WRMSG - SUBROUTINE TO WRITE ERROR MESSAGES
      *---------------------------------------------------------------
      *
     C     $WRMSG        BEGSR
      *
     C                   CALL      'UT80045J'                           43
     C                   PARM                    MGID
     C                   PARM                    MGDT
     C                   PARM                    ERROR
     C                   PARM      SDPGM         PGM
     C                   PARM      MSGFIL        MSGFL
      *
      * IF CALL TO UT80045J FAILED, A MESSAGE HARDCODED IN THE DSPF
      * COMMAND KEY RECORD WILL BE SET ON. SO, REDISPLAY SCREEN TO USER
      *
     C     *IN43         IFEQ      *ON
     C                   MOVEL     SET1          ROUTNE
     C                   ENDIF
      *
     C                   ENDSR
      *
      *----------------------------------------------------------------
      * $CLMSG - SUBROUTINE TO CLEAR MESSAGES
      *----------------------------------------------------------------
      *
     C     $CLMSG        BEGSR
      *
     C                   CALL      'UT80045J'                           43
     C                   PARM      *BLANKS       MGI
     C                   PARM      *BLANKS       MGD
     C                   PARM      *ZEROS        ERROR
     C                   PARM      SDPGM         PGM
     C                   PARM      MSGFIL        MSGFL
      *
      * IF CALL TO UT80045J FAILED, A MESSAGE HARDCODED IN THE DSPF
      * COMMAND KEY RECORD WILL BE SET ON. SO, REDISPLAY SCREEN TO USER
      *
     C     *IN43         IFEQ      *ON
     C                   MOVEL     SET1          ROUTNE
     C                   ENDIF
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * *INZSR - INITIALIZATION SUBROUTINE
      *----------------------------------------------------------------
      *
     C     *INZSR        BEGSR
      *
      * PARM LISTS
      *   NONE
      *
      * GLOBAL DEFINES
      *
     C                   MOVE      *BLANK        MODE              6            PARM
     C                   MOVE      *BLANK        PGM              10            USED IN MSG
     C                   MOVE      *BLANK        MSGFL            10            USED IN MSG
     C                   MOVE      *BLANK        ROUTNE            6            ROUTINE
     C                   Z-ADD     0             MAXRRN            4 0          REPOSITION
     C                   MOVEL     NO            RESET             1            REPOSITION
     C                   MOVEL     NO            NORECS            1            NO RECORDS
     C     *LIKE         DEFINE    IDFLOW        WKFLOW
      *
      * SAVE FIELDS FOR POSITIONERS AND SELECTORS
      *
     C     *LIKE         DEFINE    C1USCD        SVUSCD
     C     *LIKE         DEFINE    C1SLCD        SVSLCD
     C     *LIKE         DEFINE    C1RUFL        SVRUFL
     C     *LIKE         DEFINE    C1MDFL        SVMDFL
     C     *LIKE         DEFINE    C1RMFL        SVRMFL
     C     *LIKE         DEFINE    C1RVFL        SVRVFL
     C     *LIKE         DEFINE    C1AFFL        SVAFFL
      *
      * DEFINE PARM FIELDS
      *
     C     *LIKE         DEFINE    C1SLCD        XXSLCD
     C     *LIKE         DEFINE    C1RUFL        XXRUFL
     C     *LIKE         DEFINE    C1RUFL        XXMDFL
     C     *LIKE         DEFINE    C1RMFL        XXRMFL
     C     *LIKE         DEFINE    C1RVFL        XXRVFL
     C     *LIKE         DEFINE    C1AFFL        XXAFFL
      *
      *  INITIALIZE RETURN CODE
      *
     C                   MOVEL     '00'          RTNCD             2            RETURN CODE
     C     *LIKE         DEFINE    HDRRN1        SVRRN1
      *
      *
      * DEFINE LDA
     C     *DTAARA       DEFINE    *LDA          LDA
      *
      * INITIALIZE LDA FIELDS
      *
     C                   MOVE      *BLANK        LDRPFL
     C                   MOVE      *BLANK        LDUSCD
      *
     C                   MOVE      *BLANK        LDHOLD
     C                   Z-ADD     0             LDCOPY
     C                   MOVEL     LDOUTU        LDOUTQ
     C                   MOVEL     '*JOBD'       XXJOBQ           10
     C                   MOVEL     NO            XXNITE            1
     C                   Z-ADD     0             XXCOPY            1 0
      *
      * GET THIS FUNCTION'S NUMBER OF COPIES, HOLD YES/NO
      *
     C     SDPGM         CHAIN     HSP002                             92
     C     *IN92         IFEQ      *OFF
     C                   Z-ADD     FNCOPY        LDCOPY
     C                   MOVEL     FNHOLD        LDHOLD
     C                   ENDIF
      *
     C     LDCOPY        IFEQ      0
     C                   Z-ADD     1             LDCOPY
     C                   ENDIF
      *
     C     LDHOLD        IFEQ      *BLANK
     C                   MOVEL     NO            LDHOLD
     C                   ENDIF
      *
      *  PARMS FOR USE WITH QCMDEXC.  'LENGTH' IS THE LENGTH OF THE
      *  COMMAND THAT WILL BE ISSUED WITH QCMDEXC.  QCMDEXC REQUIRES
      *  THE FIELD CONTAINING LENGTH TO BE DEFINED AS 15 WITH 5 DECIMAL
      *  POSITIONS.  THE FIELD 'CMDSBM' WILL HOLD THE VALUES OF THE
      *  THE SUBMIT JOB COMMAND WHEN PASSED AS A PARM ON THE CALL TO
      *  QCMDEXC.
      *
     C                   Z-ADD     80            LENGTH           15 5          QCMDEXC PARM
     C                   MOVE      *BLANK        CMDSBM           80            QCMDEXC PARM
      *
      * TO SUPPORT F5-REFRESH, LOGIC REQUIRES THE NUMBER OF LINES ON
      * THE SUBFILE PAGE AND ALSO THE NUMBER OF LINES LESS 1.  BY
      * SETTING THESE VALUES UP AS CONSTANT WORK FIELDS, THE PROCESSING
      * LOGIC CAN BE STANDARDIZED BETWEEN PROGRAMS.
      *
     C                   Z-ADD     11            WKPLIN            2 0          SFLPAG(0011)
     C     WKPLIN        SUB       1             WKRLIN            2 0          SFLPAG - 1
      *
      * THE FOLLOWING STANDARD CODE MUST BE INCLUDED TO MAKE THE
      * STANDARD ERROR MESSAGE HANDLING PROGRAM FUNCTION PROPERLY.
      * THIS CODE SETS AND CLEARS THE PROGRAM MESSAGE QUEUE.
      *
     C                   Z-ADD     20            MAXMSG            2 0
     C                   MOVEL     '*'           MSGPGM
     C                   EXSR      $CLMSG
      *
     C                   ENDSR
      /EJECT
      *****************************************************************
      * COMPILE TIME ARRAY 'CMD' USED IN QCMDEXC - LISTINGS
      *****************************************************************
** CMD TABLE 'CMD' SUBMITS LISTINGS
SBMJOB CMD(CALL PGM(HP426CL)) JOB(USERLISTS) JOBQ(__________)
