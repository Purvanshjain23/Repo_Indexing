     A*%%TS  SD  20050215  081947  ISLRAMS     REL-V5R2M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD7707
     A*  TITLE:         VIEW FARM BUDGET
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  DATE:          02/24/04
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A                                      CF04(04 'PROMPT')
     A                                      CF05(05 'REFRESH')
     A*
     A*
     A*---------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP7707S1                  SFL
     A*%%TS  SD  20050215  081947  ISLRAMS     REL-V5R2M0  5722-WDS
     A  85                                  SFLNXTCHG
     A            HDRRN1         4Y 0H
     A            HDBGIT    R        H      REFFLD(@@BGIT)
     A            D1BGIT         6A  O 13  2COLOR(PNK)
     A                                 13  9'Bdgt'
     A                                      DSPATR(HI)
     A            D1BGQT    R        O 13 14REFFLD(HSRREF/@@BGQT *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A            D1DESC        43A  O 13 30COLOR(PNK)
     A            D1ITTYCD  R        O 13 74REFFLD(HSRREF/@@ITTYCD *LIBL/HSPREF)
     A                                      COLOR(PNK)
     A                                 14  8'Avail'
     A                                      DSPATR(HI)
     A            D1AVQT    R        O 14 14REFFLD(HSRREF/@@BGQT *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A                                 14 30'amount:'
     A                                      DSPATR(HI)
     A            D1FBAM    R        O 14 38REFFLD(HSRREF/@@FBAM *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A                                 14 54'uom:'
     A                                      COLOR(WHT)
     A            D1UOM     R        O 14 59REFFLD(HSRREF/@@UOM *LIBL/HSPREF)
     A                                 14 69'uow:'
     A                                      COLOR(WHT)
     A            D1UOW     R        O 14 74REFFLD(HSRREF/@@UOW *LIBL/HSPREF)
     A*
     A*---------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP7707C1                  SFLCTL(HP7707S1)
     A*%%TS  SD  20050215  081947  ISLRAMS     REL-V5R2M0  5722-WDS
     A                                      SFLSIZ(0050)
     A                                      SFLPAG(0004)
     A N82                                  ROLLUP(25 'PAGE FORWARD')
     A                                      SETOF(50)
     A                                      SETOF(54)
     A                                      RTNCSRLOC(&RCD &FLD)
     A                                      OVERLAY
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 26'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  2 29'VIEW FARM BUDGET'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  4  2'Farm site .........:'
     A            C1FSCD    R        O  4 24REFFLD(HSRREF/@@FSCD *LIBL/HSPREF)
     A                                      EDTCDE(M)
     A            C1FSNM    R        O  4 31REFFLD(HSRREF/FSFSNM *LIBL/HSPREF)
     A                                  4 57'Budget number:'
     A            C1FBSN    R        O  4 72REFFLD(HSRREF/@@FBSN *LIBL/HSPREF)
     A                                      EDTCDE(Z)
     A                                  5  2'Budget template ...:'
     A            C1BTCD    R        O  5 23REFFLD(HSRREF/@@BTCD *LIBL/HSPREF)
     A            C1BTDS    R        O  5 31REFFLD(HSRREF/@@BTDS *LIBL/HSPREF)
     A                                  5 57'Budget status:'
     A            C1FBSDS   R        O  5 72REFFLD(HSRREF/@@FBSDS *LIBL/HSPREF)
     A                                  6  2'Budget availability:'
     A            C1BACD    R        O  6 23REFFLD(HSRREF/@@BACD *LIBL/HSPREF)
     A            C1BADS    R        O  6 31REFFLD(HSRREF/@@BADS *LIBL/HSPREF)
     A                                  6 57'From date ...:'
     A            C1FMDY    R        O  6 72REFFLD(HSRREF/@@DT06 *LIBL/HSPREF)
     A                                      EDTWRD('  /  /  ')
     A                                  7  2'Creation schedule .:'
     A            C1CSCD    R        O  7 23REFFLD(HSRREF/@@CSCD *LIBL/HSPREF)
     A            C1CSDS    R        O  7 31REFFLD(HSRREF/@@CSDS *LIBL/HSPREF)
     A                                  7 57'To date .....:'
     A            C1TMDY    R        O  7 72REFFLD(HSRREF/@@DT06 *LIBL/HSPREF)
     A                                      EDTWRD('  /  /  ')
     A                                  8  2'Weeks .............:'
     A            C1BTWK    R        O  8 23REFFLD(HSRREF/@@BTWK *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A                                  9  4'?'
     A                                      DSPATR(HI)
     A                                  9 76'?'
     A                                      DSPATR(HI)
     A                                 10  2'Budget'
     A                                      DSPATR(HI)
     A                                 10 74'Item'
     A                                      DSPATR(HI)
     A                                 11  2'Item'
     A                                      DSPATR(HI)
     A                                 11 17'Quantities'
     A                                      DSPATR(HI)
     A                                 11 30'Description'
     A                                      DSPATR(HI)
     A                                 11 74'Type'
     A                                      DSPATR(HI)
     A*
     A            C1BGIT         6A  B 12  2
     A  50                                  DSPATR(PC)
     A                                      DSPATR(HI)
     A            C1DESC        43A  B 12 30DSPATR(HI)
     A            C1ITTYCD  R        B 12 74REFFLD(HSRREF/@@ITTYCD *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A  54                                  DSPATR(PC)
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R HP7707K1
     A*%%TS  SD  20000921  074322  ISLRAMS     REL-V4R4M0  5769-PW1
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A                                 23 11'F4=Prompt'
     A                                 23 22'F5=Refresh'
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *---------------------------------------------------------------
     A          R HP7707ES                  SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE CONTROL FORMAT
      *---------------------------------------------------------------
     A          R HP7707EC                  SFLCTL(HP7707ES)
      *
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
