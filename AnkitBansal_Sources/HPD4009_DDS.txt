     A*%%TS  SD  20050126  144930  ISLRAMS     REL-V5R2M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD4009
     A*  TITLE:         WORK WITH SIRE LINES
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  DATE:          10/24/01
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A                                      CF05(05 'REFRESH')
     A                                      CF06(06 'CREATE')
     A                                      CF07(07 'LISTING')
     A                                      CF17(17 'PRINT OPTIONS')
     A                                      CF20(20 'WRKSPLF')
     A*
     A*
     A*---------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP4009S1                  SFL
     A*%%TS  SD  20050126  144930  ISLRAMS     REL-V5R2M0  5722-WDS
     A  85                                  SFLNXTCHG
     A            HDRRN1         4Y 0H
     A            D1OPT          2A  B  9  2CHGINPDFT(UL)
     A  30                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A N30                                  DSPATR(HI)
     A            D1SICD    R        O  9  9REFFLD(HSRREF/@@SICD *LIBL/HSPREF)
     A            D1SICOMP  R        O  9 19REFFLD(HSRREF/@@SICOMP *LIBL/HSPREF)
     A            D1PRIME   R        O  9 27REFFLD(HSRREF/@@PRIME *LIBL/HSPREF)
     A                                      EDTCDE(M)
     A            D1GG0     R        O  9 35REFFLD(HSRREF/@@GG5A *LIBL/HSPREF)
     A            D1GG1     R        O  9 47REFFLD(HSRREF/@@GG5A *LIBL/HSPREF)
     A            D1GG2     R        O  9 61REFFLD(HSRREF/@@GG5A *LIBL/HSPREF)
     A            D1AIST    R        O  9 76REFFLD(HSRREF/@@AIST *LIBL/HSPREF)
     A*
     A*---------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP4009C1                  SFLCTL(HP4009S1)
     A*%%TS  SD  20050126  144930  ISLRAMS     REL-V5R2M0  5722-WDS
     A                                      SFLSIZ(0050)
     A                                      SFLPAG(0012)
     A N82                                  ROLLUP(25 'PAGE FORWARD')
     A                                      SETOF(30)
     A                                      RTNCSRLOC(&RCD &FLD)
     A                                      OVERLAY
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 26'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  2 26'WORK WITH SIRE LINES'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  4  2'2=Revise'
     A                                  4 12'4=Delete'
     A                                  5 35'Cognos'
     A                                      DSPATR(HI)
     A                                  5 45'Alternate'
     A                                      DSPATR(HI)
     A                                  5 59'Alternate'
     A                                      DSPATR(HI)
     A                                  5 74'Active'
     A                                      DSPATR(HI)
     A                                  6  9'---Sire Line--'
     A                                      DSPATR(HI)
     A                                  6 26'Prime'
     A                                      DSPATR(HI)
     A                                  6 35'Report'
     A                                      DSPATR(HI)
     A                                  6 47'Report'
     A                                      DSPATR(HI)
     A                                  6 61'Report'
     A                                      DSPATR(HI)
     A                                  6 72'Inactive'
     A                                      DSPATR(HI)
     A                                  7  2'Opt'
     A                                      DSPATR(HI)
     A                                  7  9'Code'
     A                                      DSPATR(HI)
     A                                  7 16'Company'
     A                                      DSPATR(HI)
     A                                  7 27'Line'
     A                                      DSPATR(HI)
     A                                  7 34'Grouping'
     A                                      DSPATR(HI)
     A                                  7 45'Grouping 1'
     A                                      DSPATR(HI)
     A                                  7 59'Grouping 2'
     A                                      DSPATR(HI)
     A                                  7 74'Status'
     A                                      DSPATR(HI)
     A            C1SICD    R        B  8  9REFFLD(HSRREF/@@SICD *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A            C1SICOMP  R        B  8 19REFFLD(HSRREF/@@SICOMP *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A            C1PRIME   R        B  8 27REFFLD(HSRREF/@@PRIME *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A                                      EDTCDE(M)
     A            C1GG0     R        B  8 35REFFLD(@@GG5A)
     A                                      DSPATR(HI)
     A            C1GG1     R        B  8 47REFFLD(@@GG5A)
     A                                      DSPATR(HI)
     A            C1GG2     R        B  8 61REFFLD(@@GG5A)
     A                                      DSPATR(HI)
     A            C1AIST    R        B  8 76REFFLD(HSRREF/@@AIST *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A*
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R HP4009K1
     A*%%TS  SD  20011024  165925  ISLRAMS     REL-V4R4M0  5769-PW1
     A  43                             21  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A                                 23 11'F5=Refresh'
     A                                 23 23'F6=Create'
     A                                 23 34'F7=Listing'
     A                                 23 46'F17=Print Options'
     A                                 23 65'F20=Spool Files'
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *---------------------------------------------------------------
     A          R HP4009ES                  SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE CONTROL FORMAT
      *---------------------------------------------------------------
     A          R HP4009EC                  SFLCTL(HP4009ES)
      *
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
