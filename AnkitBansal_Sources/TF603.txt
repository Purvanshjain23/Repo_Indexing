      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Triumph Foods
      * PROGRAM:     TF603
      * TITLE:       Cash: Cash Distribution Balance Listing
      * PROGRAMMER:  LeAnne Ramsey
      * CREATED:     09/03/09
      *
      * FUNCTION:  Batch program to print the Cash Distribution Balance records.
      *
      *            We wrote this Listing/Download function for Tim Wood to use to help
      *            him reconcile his Cash Distribution at the end of each period.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 09/10/09  Alice Brownfield
      *           E00493-field name changes only - no logic changes
      *                  change to allocate AR Adjustments at gross vs net; i.e. give some
      *                  to Sales Expense then the NET to TF & SBF.
      /eject
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Ftfp011    if   e           k disk
      *    Cash distribution balance (records selected with open query)
      *
      *
     Ftfp303    if   e           k disk
      *    Workfile: Cash distribution balance summary
      *
      *
     Fprint198  o    f  198        printer oflind(*inof)
      *
      /EJECT
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Standard fields
      *
     D dash            s            198    inz(*all'-')
     d rtime           s              6  0
      *
      *
      * Control fields
      *
     D first           s              1    inz('Y')
     D count           s              2  0
     D svmonth         s              2  0
     D svwedt          s                   like(cbwedt)
      *
      *
      * Workfields for Date Manipulation
      *
     D wkisodate       s               d   datfmt(*iso)
     D wkmonth         s              2  0
      *
      *
      * Print fields
      *
     d l1wemdy         s              6  0
     d l2wemdy         s              6  0
      *
     d t1agpram        s                   like(cbagpram)
     d t1agclam        s                   like(cbagclam)
     d t1aaragam       s                   like(cbaaragam)
     d t1asxam         s                   like(cbasxam)
      *
     d t1snpram        s                   like(cbsnpram)
     d t1snclam        s                   like(cbsnclam)
     d t1saranam       s                   like(cbsaranam)
      *
     d t1tnpram        s                   like(cbtnpram)
     d t1tnclam        s                   like(cbtnclam)
     d t1taranam       s                   like(cbtaranam)
      *
      ****************************************************************
      * ARRAYS AND TABLES
      ****************************************************************
      *
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *---------------------------------------------------------------
      * Local data area.
      *---------------------------------------------------------------
      *
     Dlda             uds                  dtaara(*lda)
      *
     D  ldfmdy                 1      6  0
     D  ldtmdy                 7     12  0
     D  ldfcymd               13     20  0
     D  ldtcymd               21     28  0
      *
     D  ldrdfl                29     29
     D  ldrdds                30     37
      *
     D  lddsfl                38     38
     D  lddsds                39     48
      *
     D  ldiecd                49     49
     D  ldieds                50     75
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ***************************************************************************************
      * MAINLINE
      ***************************************************************************************
      *
     C                   exsr      $h1hdr
      *
      * Summary or Detail
     C                   select
     C                   when      lddsfl = 'S'
     C                   exsr      $summary
      *
     C                   when      lddsfl = 'D'
     C                   exsr      $detail
     C                   endsl
      *
      * EOF processing
      *
     C                   if        first = no
     C                   exsr      $t1tot
     C                   else
     C                   except    nodata
     C                   endif
      *
     C                   seton                                        lr
      /EJECT
      *---------------------------------------------------------------
      * Detail version
      *---------------------------------------------------------------
      *
     C     $detail       begsr
      *
     C     *loval        setll     tfp011
      *
     C                   dou       *in91 = *on                                  Do detail
     C                   read      tfp011                                 91
     C                   if        *in91 = *off                                 If not EOF
      *
      * If a WeekEnding Date had both an Exempt and a Non-Exempt record,
      * we will print a Total for that WeekEnding Date. So, we will have
      * break and count logic.
      *
     C                   select
     C                   when      first = yes
     C                   move      no            first
     C                   z-add     cbwedt        svwedt
      *
     C                   when      cbwedt <> svwedt
     C                   if        count > 1
     C                   exsr      $weektot
     C                   endif
     C                   z-add     cbwedt        svwedt
     C                   z-add     0             count
     C                   endsl
      *
      * Accumulate values for Report Total
      *
     C                   add       cbagpram      t1agpram
     C                   add       cbagclam      t1agclam
     C                   add       cbaaragam     t1aaragam
     C                   add       cbasxam       t1asxam
      *
     C                   add       cbsnpram      t1snpram
     C                   add       cbsnclam      t1snclam
     C                   add       cbsaranam     t1saranam
      *
     C                   add       cbtnpram      t1tnpram
     C                   add       cbtnclam      t1tnclam
     C                   add       cbtaranam     t1taranam
      *
     C                   exsr      $l1dtl
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Do detail
      *
     C                   if        count > 1
     C                   exsr      $weektot
     C                   endif
      *
     C                   endsr
      /EJECT
      *-------------------------------------------------------------------------------------
      * When a Week has both an Exempt and a Non-Exempt Record, we will print a Total Line
      *-------------------------------------------------------------------------------------
      *
      * The "total" for the week is the same as our "summary" record for that week (in the
      * workfile). So, retrieve that workfile record for the "saved" week.

     C     $weektot      begsr
      *
     C     svwedt        chain     tfp303                             92
     C                   if        *in92 = *off
     C                   exsr      $l2dtl
     C                   endif
      *
     C                   endsr
      /EJECT
      *--------------------------------------------------------------------------------------
      * Summary version
      *--------------------------------------------------------------------------------------
      *
      * Process each record in the workfile; the workfile holds 1 record for each
      * WeekEnding date; this record holds the sum of the Exempt and NonExempt records
      * from TFP011 for that WeekEnding Date.
      *
     C     $summary      begsr
      *
     C     *loval        setll     tfp303
      *
     C                   dou       *in91 = *on                                  Do summary
     C                   read      tfp303                                 91
     C                   if        *in91 = *off                                 If not EOF
      *
     C                   if        first = yes
     C                   move      no            first
     C                   endif
      *
      * Accumulate values for Report Total
      *
     C                   add       w1agpram      t1agpram
     C                   add       w1agclam      t1agclam
     C                   add       w1aaragam     t1aaragam
     C                   add       w1asxam       t1asxam
      *
     C                   add       w1snpram      t1snpram
     C                   add       w1snclam      t1snclam
     C                   add       w1saranam     t1saranam
      *
     C                   add       w1tnpram      t1tnpram
     C                   add       w1tnclam      t1tnclam
     C                   add       w1taranam     t1taranam
      * Print line
     C                   exsr      $l2dtl
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Do summary
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Extract Month from WeekEnding Date
      *---------------------------------------------------------------
      *
      * I wanted to put some white space into the listing but did not want to
      * count lines. So, I print a blank line when "month" changes.
      *
     C     $month        begsr
      *
     C                   extrct    wkisodate:*m  wkmonth
      *
     C                   if        wkmonth <> svmonth
     C                   except    blank
     C                   z-add     wkmonth       svmonth
     C                   endif
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Print detail line one
      *---------------------------------------------------------------
      *
     C     $l1dtl        begsr
      *
     C                   if        *inof = *on
     C                   exsr      $h1hdr
     C                   endif
      *
      * Flip the Week-ending Date into MMDDYY format.
      * And, check to see if "month" has changed.
      *
     C     *iso          test(d)                 cbwedt                 92
     C                   if        *in92 = *off                                 If OK
     C                   move      cbwedt        wkisodate
     C     *mdy          move      wkisodate     l1wemdy
     C                   exsr      $month
     C                   endif                                                  If OK
      *
     C                   add       1             count
     C                   except    l1dtl
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Print detail line two
      *---------------------------------------------------------------
      *
     C     $l2dtl        begsr
      *
     C                   if        *inof = *on
     C                   exsr      $h1hdr
     C                   endif
      *
      * If this is the Summary Version, flip the Week-ending Date into MMDDYY.
      * And, check to see if "month" has changed.
      *
     C                   if        lddsfl = 'S'
     C     *iso          test(d)                 w1wedt                 92
     C                   if        *in92 = *off                                 If OK
     C                   move      w1wedt        wkisodate
     C     *mdy          move      wkisodate     l2wemdy
     C                   exsr      $month
     C                   endif                                                  If OK
     C                   endif
      * Print line
     C                   except    l2dtl
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Print report heading
      *---------------------------------------------------------------
      *
     C     $h1hdr        begsr
      *
     C                   except    h1hdr
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Print the Total Line for the Listing
      *---------------------------------------------------------------
      *
     C     $t1tot        begsr
      *
     C                   if        *inof = *on
     C                   exsr      $h1hdr
     C                   endif
      *
     C                   except    t1tot
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Key lists
      *  None
      *
      * Retrieve time for report heading.
      *
     C                   time                    rtime
      *
      * If you are printing the Detail version, seton indicators to control printing.
      *
     C                   select
     C                   when      lddsfl = 'D'
     C                   seton                                        87
      *
     C                   when      lddsfl = 'S'
     C                   seton                                        88
     C                   endsl
      *
     C                   endsr
      /EJECT
      *-------------------------------------------------------------
      * Report heading lines
      *-------------------------------------------------------------
      *
     Oprint198  e            h1hdr          1 01
     O                       sdpgm               10
     O                                          105 'CASH DISTRIBUTION BALANCE'
     O                                          113 ' LISTING'
     O                                          188 'DATE'
     O                       udate         y    198
      *
     O          e            h1hdr          1
     O                       sdusr               10
     O                       lddsds             103
     O                                          188 'TIME'
     O                       rtime              198 '  :  :  '
      *
     O          e            h1hdr          1
     O                                          188 'PAGE'
     O                       page          z    198
      *
      *
     O          e            h1hdr          1
     O                                           23 'From WeekEnding date:'
     O                       ldfmdy              33 '  /  /  '
      *
     O          e            h1hdr          1
     O                                           23 'To WeekEnding date:'
     O                       ldtmdy              33 '  /  /  '
      *
     O          e            h1hdr          2
     O                                           23 'Exempt/Non-Exempt/Both:'
     O                       ldieds              51
      *
      *
      *-------------------------------------------------------------
      * Column headings
      *-------------------------------------------------------------
      *
     O          e            h1hdr          1
     O                                            8 'Week'
     O               87                          10 'E'
     O                                           25 'Agg Gross'
     O                                           40 'Agg Gross'
     O                                           55 'Agg A/R'
     O                                           65 'AggGross'
     O                                           81 'Agg Total'
      *
     O                                           97 'SBD Net'
     O                                          107 'SBD Net'
     O                                          123 'SBD Net'
     O                                          139 'SBD A/R'
      *
     O                                          155 'TF Net'
     O                                          165 'TF Net'
     O                                          181 'TF Net'
     O                                          197 'TF A/R'
      *
     O          e            h1hdr          1
     O                                            8 'Ending'
     O               87                          10 'X'
     O                                           25 'Prod Revenue'
     O                                           40 'Claim'
     O                                           55 'Adjustment'
     O                                           65 'SalesExp'
     O                                           81 'Sales Exp'
      *
     O                                           97 'Prod Revenue'
     O                                          107 'Prod Rev'
     O                                          123 'Claim'
     O                                          139 'Adjustment'
      *
     O                                          155 'Prod Revenue'
     O                                          165 'Prod Rev'
     O                                          181 'Claim'
     O                                          197 'Adjustment'
      *
     O          e            h1hdr          1
     O                                            8 'Date'
     O                                           25 'Amount'
     O                                           40 'Amount'
     O                                           55 'Amount'
     O                                           65 'Percent'
     O                                           81 'Amount'
      *
     O                                           97 'Amount'
     O                                          107 'Percent'
     O                                          123 'Amount'
     O                                          139 'Amount'
      *
     O                                          155 'Amount'
     O                                          165 'Percent'
     O                                          181 'Amount'
     O                                          197 'Amount'
      *
     O          e            h1hdr          0
     O                       dash               198
      *
      *-------------------------------------------------------------
      * Detail line for Cash Distribution Balance records
      *-------------------------------------------------------------
      *
     O          e            l1dtl       1  0
     O                       l1wemdy        b     8 '  /  /  '
     O                       cbrcexfl            10
     O                       cbagpram      k     26
     O                       cbagclam      k     41
     O                       cbaaragam     k     56
     O                       cbagsxpc            66 '   .    -'
     O                       cbasxam       k     82
      *
     O                       cbsnpram      k     98
     O                       cbsnprpc           108 '   .    -'
     O                       cbsnclam      k    124
     O                       cbsaranam     k    140
      *
     O                       cbtnpram      k    156
     O                       cbtnprpc           166 '   .    -'
     O                       cbtnclam      k    182
     O                       cbtaranam     k    198
      *
      *-------------------------------------------------------------
      * Summary line
      *-------------------------------------------------------------
      *
     O          e    87      l2dtl       1  1
     O                                           25 '--------------'
     O                                           40 '--------------'
     O                                           55 '--------------'
     O                                           81 '--------------'
      *
     O                                           97 '--------------'
     O                                          123 '--------------'
     O                                          139 '--------------'

     O                                          155 '--------------'
     O                                          181 '--------------'
     O                                          197 '--------------'
      *
     O          e            l2dtl       0  1
     O               88      l2wemdy        b     8 '  /  /  '
     O                       w1agpram      k     26
     O                       w1agclam      k     41
     O                       w1aaragam     k     56
     O                       w1asxam       k     82
      *
     O                       w1snpram      k     98
     O                       w1snclam      k    124
     O                       w1saranam     k    140
      *
     O                       w1tnpram      k    156
     O                       w1tnclam      k    182
     O                       w1taranam     k    198
      *
      *-------------------------------------------------------------
      * Total line
      *-------------------------------------------------------------
      *
     O          e            t1tot       2  0
     O                                            8 'TOTAL:'
     O                       t1agpram      k     26
     O                       t1agclam      k     41
     O                       t1aaragam     k     56
     O                       t1asxam       k     82
      *
     O                       t1snpram      k     98
     O                       t1snclam      k    124
     O                       t1saranam     k    140
      *
     O                       t1tnpram      k    156
     O                       t1tnclam      k    182
     O                       t1taranam     k    198
      *
      *-------------------------------------------------------------
      * Blank
      *-------------------------------------------------------------
      *
     O          e            blank       1
      *
      *-------------------------------------------------------------
      * No data message line
      *-------------------------------------------------------------
      *
     O          e            nodata      1
     O                                           19 'No data meets your'
     O                                           39 'selection criteria.'
      /eject
