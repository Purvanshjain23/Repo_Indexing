     A*%%TS  SD  19950214  085152  ISLRAMS     REL-V3R0M5  5763-PW1
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION
     A*  DISPLAY FILE:  HPD589 - SELECT USER
     A*  CREATED BY:    LEANNE FEDOR
     A*  DATE:          02/14/95
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      PRINT
     A                                      CA12(12 'PREVIOUS')
     A N82                                  ROLLUP(25 'PAGE FORWARD')
     A*--------------------------------------------------------------*
     A* REFERENCE WINDOW RECORD FORMAT 1
     A*--------------------------------------------------------------*
     A          R HP589R1
     A*%%TS  SD  19950214  085152  ISLRAMS     REL-V3R0M5  5763-PW1
     A                                      TEXT('Reference window')
     A                                      WINDOW(*DFT 16 63)
     A                                      WDWBORDER((*DSPATR RI) (*CHAR '    -
     A                                          '))
     A*--------------------------------------------------------------*
     A* RECORD FORMAT ONE
     A*--------------------------------------------------------------*
     A          R HP589R2
     A                                      TEXT('Written in RPG pgm to prevent-
     A                                       screen from clearing')
     A                                      CLRL(*NO)
     A                                      OVERLAY
     A*--------------------------------------------------------------*
     A* RECORD FORMAT TWO
     A*--------------------------------------------------------------*
     A* THIS FORMAT IS NOT WRITTEN IN THE RPG PGM.  IT PREVENTS THE
     A* CALLING_PROGRAM_SCREEN FROM BEING CLEARED.
     A*--------------------------------------------------------------*
     A          R HP589R3
     A                                      TEXT('Dummy format not written in R-
     A                                      PG pgm.')
     A                                      ASSUME
     A                                 11  1' '
     A*--------------------------------------------------------------*
     A* SUBFILE FORMAT ONE
     A*--------------------------------------------------------------*
     A          R HP589S1                   SFL
     A                                      TEXT('subfile record format one')
     A            HDRRN1         4S 0H
     A            DFUSCD    R        O  6  1REFFLD(AUUSCD)
     A            DFUSNM    R        O  6 13REFFLD(AUUSNM)
     A*--------------------------------------------------------------*
     A* SUBFILE CONTROL FORMAT ONE
     A*--------------------------------------------------------------*
     A          R HP589C1                   SFLCTL(HP589S1)
     A*%%TS  SD  19950214  085152  ISLRAMS     REL-V3R0M5  5763-PW1
     A                                      SFLSIZ(0050)
     A                                      SFLPAG(0008)
     A                                      WINDOW(HP589R1)
     A                                      TEXT('control format one')
     A                                      OVERLAY
     A                                      SFLCSRRRN(&HDRRN)
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND(*MORE)
     A                                      RMVWDW
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            HDRRN          5S 0H
     A                                  1 21'SELECT USER'
     A                                      DSPATR(HI)
     A                                  2  1'Position Cursor, Press Enter.'
     A                                  4  1'Code'
     A                                      DSPATR(HI)
     A                                  4 13'Name'
     A                                      DSPATR(HI)
     A            C1USCD    R        B  5  1REFFLD(AUUSCD)
     A                                      DSPATR(HI)
     A*----------------------------------------------------------------
     A* COMMAND LINE
     A*----------------------------------------------------------------
     A          R HP589K1
     A                                      TEXT('command line 1')
     A                                      OVERLAY
     A                                      WINDOW(HP589R1)
     A                                 15  1'F12=Previous'
