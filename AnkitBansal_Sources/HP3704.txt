      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Hog Production System
      * PROGRAM:     HP3704-Add/Remove Farm Links to Templates (with options)
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     02/06/04
      *
      * Function:    Depending on the incoming parm, this function:
      *                1) adds Farms to a Budget Template
      *                2) removes Farms for a Budget Template.
      *
      *              With both add/remove, the user may have specified 'which' Farms by
      *              populating optional selections for: production phase, production type,
      *              cell, type of farm.
      *
      *              This program is ultimately called from HP4700-Work With Budget Templates.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 10/15/13  LeAnne Ramsey (E2831)
      *           Recompile only. Added field 'MTech Reference'.
      /eject
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
      *
     Fhsp018    if   e           k disk
      *    Farm site
      *
      *
     Fhsp187    uf a e           k disk
      *    Budget template/farm links
      *
      /eject
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      *----------------------------------------------------------------
      * CONSTANTS
      *----------------------------------------------------------------
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      *----------------------------------------------------------------
      * STANDALONE FIELDS
      *----------------------------------------------------------------
      *
      * Standard fields
      *
     D rtncd           s              2
      *
      *
      * Control fields
      *
     D procfl          s              1
      *
      *
      * Parms
      *
     D xxarfl          s              1
     D xxcscd          s              1
     D xxbtcd          s              6
     D xxptcd          s                   like(fsptcd)
     D xxppcd          s                   like(fsppcd)
     D xxtfcd          s                   like(fstfcd)
     D xxcell          s                   like(fscell)
      *
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * Mainline
      ****************************************************************
      *
     C                   select
     C                   when      xxarfl = 'A'
     C                   exsr      $add
      *
     C                   when      xxarfl = 'R'
     C                   exsr      $remove
     C                   endsl
      *
     C                   seton                                        lr
      /eject
      *---------------------------------------------------------------
      * Add Farm links to a Budget Template
      *---------------------------------------------------------------
      *
     C     $add          begsr
      *
      * Read each Farm Site record. If the Farm is Active, process it.
      *
     C     *loval        setll     hsp018
      *
     C                   dou       *in90 = *on                                  Do add
     C                   read      hsp018                                 90
     C                   if        *in90 = *off and fsaist = 'A'                If not EOF
      *
      * We never add BGF farms for Rotational templates. Rotational templates
      * are just for GF and Nursery.
      *
     C                   if        xxcscd = 'R' and fsppcd = 'BGF  '            If skip
     C                   else
      *
      * Check to see if this Farm is already linked to the Budget Template.
      * If it is not already linked, continue.
      *
     C     key01         chain     hsp187                             92
     C                   if        *in92 = *on                                  If not linked
      *
     C                   select
     C                   when      xxppcd = *blank and
     C                             xxptcd = *blank and
     C                             xxtfcd = *blank and
     C                             xxcell = 0
     C                   exsr      $write
     C                   other
      *
     C                   move      yes           procfl
      *
     C                   if        xxppcd <> *blank and xxppcd <> fsppcd
     C                   move      no            procfl
     C                   endif
      *
     C                   if        xxptcd <> *blank and xxptcd <> fsptcd
     C                   move      no            procfl
     C                   endif
      *
     C                   if        xxtfcd <> *blank and xxtfcd <> fstfcd
     C                   move      no            procfl
     C                   endif
      *
     C                   if        xxcell <> 0 and xxcell <> fscell
     C                   move      no            procfl
     C                   endif
      *
     C                   if        procfl = yes
     C                   exsr      $write
     C                   endif
     C                   endsl
      *
     C                   endif                                                  If not linked
     C                   endif                                                  If skip
     C                   endif                                                  If not EOF
     C                   enddo                                                  Do add
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Write Farm Link record
      *---------------------------------------------------------------
      *
     C     $write        begsr
      *
     C                   move      xxbtcd        tlbtcd
     C                   z-add     fsfscd        tlfscd
     C                   write     tlrec
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Remove Farm Links from a Budget Template
      *---------------------------------------------------------------
      *
     C     $remove       begsr
      *
      * Process each Budget template/farm link record for the incoming Budget
      * Template code.
      *
     C     xxbtcd        setll     hsp187
      *
     C                   dou       *in90 = *on                                  Do remove
     C     xxbtcd        reade     hsp187                                 90
     C                   if        *in90 = *off                                 If not EOF
      *
      * If none of the optional selections are populated, delete the
      * record unconditionally. Otherwise, retrieve the Farm Site record
      * and make comparisons.
      *
     C                   select
     C                   when      xxppcd = *blank and
     C                             xxptcd = *blank and
     C                             xxtfcd = *blank and
     C                             xxcell = 0
     C                   delete    tlrec
     C                   other
      *
     C     tlfscd        chain     hsp018                             92
     C                   if        *in92 = *off                                 If farm hit
     C                   move      yes           procfl
      *
     C                   if        xxppcd <> *blank and xxppcd <> fsppcd
     C                   move      no            procfl
     C                   endif
      *
     C                   if        xxptcd <> *blank and xxptcd <> fsptcd
     C                   move      no            procfl
     C                   endif
      *
     C                   if        xxtfcd <> *blank and xxtfcd <> fstfcd
     C                   move      no            procfl
     C                   endif
      *
     C                   if        xxcell <> 0 and xxcell <> fscell
     C                   move      no            procfl
     C                   endif
      *
     C                   if        procfl = yes
     C                   delete    tlrec
     C                   endif
      *
     C                   endif                                                  If farm hit
     C                   endsl
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Do remove
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Parm lists
      *
     C     *entry        plist
     C                   parm                    xxbtcd
     C                   parm                    xxcscd
     C                   parm                    xxarfl
     C                   parm                    xxppcd
     C                   parm                    xxptcd
     C                   parm                    xxtfcd
     C                   parm                    xxcell
     C                   parm      *blank        rtncd
      *
      * Key lists
      *
     C     key01         klist
     C                   kfld                    xxbtcd
     C                   kfld                    fsfscd
      *
     C                   endsr
      /eject
