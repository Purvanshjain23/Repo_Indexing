/********************************************************************/
/*                                                                  */
/*  SYNOPSIS - PRINT LOAD SUMMARY REPORT FROM ORDER FILE.           */
/*                                                                  */
/*  COMPANY   - SEABOARD COPRORATION                                */
/*  SYSTEM    - ORDER MANAGEMENT                                    */
/*  USER NAME - SHEILA SCHERTZ                                      */
/*  DATE      - 08/16/95                                            */
/*                                                                  */
/*      PARAMETERS RECEIVED:                                        */
/*                                                                  */
/*          &OUTQ       - OUTPUT QUEUE                             */
/*          &COPEIS     - NUMBER OF COPIES                          */
/*          &HOLD       - HOLD PRINT FLAG                           */
/*          &SAVE       - SAVE PRINT FLAG                           */
/*          &COMPL      - COMPANY NUMBER                            */
/*          &FDATEL     - FROM DATE                                 */
/*          &TDATEL     - TO DATE                                   */
/*          &FDDTEL     - FROM DATE                                 */
/*          &TDDTEL     - TO DATE                                   */
/*          &LOADL      - LOAD NUMBER                               */
/*          &CARRIER    - CARRIER                                   */
/*          &FLAG       - EXCEPT FLAG                               */
/*          &LOADTYP    - LOAD TYPE                                 */
/*          &SHPMTH     - SHIPPING METHOD                           */
/*          &WHSE1      - WAREHOUSE 1                               */
/*          &WHSE2      - WAREHOUSE 2                               */
/*          &WHSE3      - WAREHOUSE 3                               */
/*          &WHSE4      - WAREHOUSE 4                               */
/*          &WHSE5      - WAREHOUSE 5                               */
/*          &WHSE6      - WAREHOUSE 6                               */
/*          &WHSE7      - WAREHOUSE 7                               */
/*          &WHSE8      - WAREHOUSE 8                               */
/*          &WHSE9      - WAREHOUSE 9                               */
/*          &WHSE10     - WAREHOUSE 10                              */
/*          &SOFLAG     - SELECT/OMIT FLAG                          */
/*          &RPTSEQ     - REPORT SEQ                                */
/********************************************************************/
/*  R CENTONZE 11/04/97   ADDED REPORT SEQ PARM.                    */
/*                        IF &RPTSEQ = 1, THEN CALL RPT IN SCHED    */
/*                         SHIP DATE/LOAD ID  SEQUENCE.             */
/*                        IF &RPTSEQ = 2, THEN CALL RPT IN CARRIER  */
/*                        /REQ DLV DATE/LOAD ID  SEQUENCE           */
/*                                                                  */
/*  P LYONS    11/20/97   REMOVED &PGM, &NIGHTQ FROM PARMS          */
/*                        ADDED SHIP METHOD PARM.                   */
/*                                                                  */
/*  S MASON    05/17/04   PKTR01 MULTI-COMPANY - ORDER HEADER       */
/*                        REMOVED &A SELECTION ON COMPANY           */
/*                        THIS WILL BE DONE IN THE PRINT REPORTS    */
/*                                                                  */
/*  L BUSER    06/08/07   FT38 - CHANGES, IF 2 DATES ENTERED:       */
/*   IF SCHEDULED SHIP DATE AND REQUESTED DELIVERY DATE ARE BOTH     */
/*   ENTERED, CONCATONATE AN "*AND" BETWEEN THE TWO DATE RANGES.     */
/*  PIO  05/24/2023 - REPLACE OPNQRYF WITH OPNSQLF IN OPBFCPL2 FILE  */
/*                    ADDED NEEDED OVRDBF AROUND THE COMMAND.        */
/*                    CHANGE *AND TO AND, *EQ TO =, *GE TO >=, *LE TO*/
/*                    <=, *GT TO > IN  &F                            */
/*  PIO  05/24/2023 - REPLACE OPNQRYF WITH OPNSQLF IN OPBFCPLF FILE  */
/*                    ADDED NEEDED OVRDBF AROUND THE COMMAND.        */
/*                    CHANGE *AND TO AND, *EQ TO =, *GE TO >=, *LE TO*/
/*                    <=, *GT TO > IN  &F                            */
/********************************************************************/
 OMNLPCLP:   PGM        PARM(&COMPL &FDATEL &TDATEL &FDDTEL &TDDTEL +
                          &LOADL &CARRIER &FLAG &LOADTYP &SHPMTH +
                          &WHSE1 &WHSE2 &WHSE3 &WHSE4 &WHSE5 &WHSE6 +
                          &WHSE7 &WHSE8 &WHSE9 &WHSE10 &SOFLAG +
                          &RPTSEQ &OUTQ &HOLD &SAVE &COPIESL +
                          &APFMDTL &APTHDTL)

        DCL    &COMP      *DEC    (3 0)           /*  COMPANY #     */
        DCL    &COMPL     *DEC                    /*  FOR PASSING   */
        DCL    &FDATE     *DEC    (7 0)           /*  FROM DATE     */
        DCL    &FDATEL    *DEC                    /*  FOR PASSING   */
        DCL    &TDATE     *DEC    (7 0)           /*  THROUGH DATE  */
        DCL    &TDATEL    *DEC                    /*  FOR PASSING   */
        DCL    &FDDTE     *DEC    (7 0)           /*  FROM DATE     */
        DCL    &FDDTEL    *DEC                    /*  FOR PASSING   */
        DCL    &TDDTE     *DEC    (7 0)           /*  THROUGH DATE  */
        DCL    &TDDTEL    *DEC                    /*  FOR PASSING   */
        DCL    &COPIES    *DEC    (3 0)           /*  # OF COPIES   */
        DCL    &COPIESL   *DEC                    /*  FOR PASSING   */
        DCL    &OUTQ      *CHAR   10              /*  OUTPUT QUEUE  */
        DCL    &HOLD       *CHAR   (4)            /* Hold Status    */
        DCL    &SAVE       *CHAR   (4)            /* Save Status    */
        DCL    &COMPA     *CHAR   3               /*  COMPANY ALPHA */
        DCL    &TDATEA    *CHAR   7               /*  TO   DT ALPHA */
        DCL    &FDATEA    *CHAR   7               /*  TO   DT ALPHA */
        DCL    &TDDTEA    *CHAR   7               /*  TO   DT ALPHA */
        DCL    &FDDTEA    *CHAR   7               /*  TO   DT ALPHA */
        DCL    &LOAD      *DEC    (7 0)           /*  LOAD #        */
        DCL    &LOADL     *DEC                    /*  FOR PASSING   */
        DCL    &CARRIER    *CHAR   (3)            /* CARRIER        */
        DCL    &FLAG       *CHAR   (1)            /* EXCEPT FLAG    */
        DCL    &LOADTYP    *CHAR   (1)            /* LOAD TYPE      */
        DCL    &SHPMTH     *CHAR   (2)            /* SHIPPING METH  */
        DCL    &SOFLAG     *CHAR   (1)           /* WAREHOUSE                 */
        DCL    &WHSE1      *CHAR   (3)           /* WAREHOUSE                 */
        DCL    &WHSE2      *CHAR   (3)           /* WAREHOUSE                 */
        DCL    &WHSE3      *CHAR   (3)           /* WAREHOUSE                 */
        DCL    &WHSE4      *CHAR   (3)           /* WAREHOUSE                 */
        DCL    &WHSE5      *CHAR   (3)           /* WAREHOUSE                 */
        DCL    &WHSE6      *CHAR   (3)           /* WAREHOUSE                 */
        DCL    &WHSE7      *CHAR   (3)           /* WAREHOUSE                 */
        DCL    &WHSE8      *CHAR   (3)           /* WAREHOUSE                 */
        DCL    &WHSE9      *CHAR   (3)           /* WAREHOUSE                 */
        DCL    &WHSE10     *CHAR   (3)           /* WAREHOUSE                 */
        DCL    &RPTSEQ     *CHAR   (1)           /* REPORT SEQ                */
        DCL    &APFMDTL    *DEC
        DCL    &APTHDTL    *DEC
        DCL    &APFRMDT    *DEC    (7 0)
        DCL    &APTHRDT    *DEC    (7 0)
        DCL    &COMMAND    *CHAR   (512)

/* SEND PROGRAM MESSAGE PARAMETERS  */

        DCL    &PGMO      *CHAR   10              /*  PRINTER FILE  */
        DCL     &B        *CHAR   25              /* QRY SCHD FROM  */
        DCL     &C        *CHAR   25              /* QRY SCHD TO    */
        DCL     &D        *CHAR   25              /* QRY DELV FROM  */
        DCL     &E        *CHAR   25              /* QRY DELV TO    */
        DCL     &F        *CHAR  120              /* QRY ALL        */

             CHGVAR    VAR(&COMP) VALUE(&COMPL) /* Change to length +
                          for print program */
             CHGVAR    VAR(&COMPA) VALUE(&COMP) /* Change to alpha +
                          for message data */
             CHGVAR    VAR(&FDATE) VALUE(&FDATEL) /* Change to length +
                          for print program */
             CHGVAR    VAR(&FDATEA) VALUE(&FDATE) /* Change to alpha +
                          for query data */
             CHGVAR    VAR(&TDATE) VALUE(&TDATEL) /* Change to length +
                          for print program */
             CHGVAR    VAR(&TDATEA) VALUE(&TDATE) /* Change to alpha +
                          for query data */
             CHGVAR    VAR(&FDDTE) VALUE(&FDDTEL) /* Change to length +
                          for print program */
             CHGVAR    VAR(&FDDTEA) VALUE(&FDDTE) /* Change to alpha +
                          for query data */
             CHGVAR    VAR(&TDDTE) VALUE(&TDDTEL) /* Change to length +
                          for print program */
             CHGVAR    VAR(&TDDTEA) VALUE(&TDDTE) /* Change to alpha +
                          for query data */
             CHGVAR    VAR(&LOAD) VALUE(&LOADL) /* Change to length +
                          for print program */
             CHGVAR    VAR(&COPIES) VALUE(&COPIESL) /* change to +
                          length for print program */
             CHGVAR    VAR(&APFRMDT) VALUE(&APFMDTL) /* change to +
                          length for print program */
             CHGVAR    VAR(&APTHRDT) VALUE(&APTHDTL) /* change to +
                          length for print program */

/********************************************************************/
/* SET PROGRAM PRINTER FILE NAME                                    */
/********************************************************************/
             IF         COND(&RPTSEQ *EQ '1') THEN(DO)
              CHGVAR     VAR(&PGMO) VALUE(PDWDPFR$)
             ENDDO

             IF         COND(&RPTSEQ *EQ '2') THEN(DO)
              CHGVAR     VAR(&PGMO) VALUE(PDEXPFR$)
             ENDDO

/********************************************************************/
/* Override printer file with user specified values                 */
/********************************************************************/

             SBDPRTOVR  CMDVAR(&COMMAND) FILE(&PGMO) OUTQ(&OUTQ) +
                          COPIES(&COPIES) HOLD(&HOLD) +
                          SAVE(&SAVE)

             CALL       PGM(QCMDEXC) PARM(&COMMAND 512)

/*********************************************************************/
/*   CREATE OPEN QUERY FILE TO BE USED FOR REPORT                    */
/*   IF SCHEDULED SHIP DATE AND REQUESTED DELIVERY DATE ARE BOTH     */
/*   ENTERED, CONCATONATE AN "*AND" BETWEEN THE TWO DATE RANGES.     */
/*********************************************************************/
             IF         COND(&FDATEA *NE '0000000') THEN(DO)
  /*PIO      CHGVAR     VAR(&B) VALUE('BEAODT *GE ' *CAT +  +
                         &FDATEA)                           */
  /*PIO*/    CHGVAR     VAR(&B) VALUE('BEAODT >=  ' *CAT +
                         &FDATEA)

  /*PIO      CHGVAR     VAR(&C) VALUE('*AND BEAODT *LE ' *CAT + +
                         &TDATEA)                              */
  /*PIO*/    CHGVAR     VAR(&C) VALUE(' AND BEAODT <=  ' *CAT +
                         &TDATEA)
             ENDDO

             IF         COND(&FDDTEA *NE '0000000') THEN(DO)
  /*PIO      CHGVAR     VAR(&D) VALUE('BEEIDT *GE ' *CAT +  +
                         &FDDTEA)                           */
  /*PIO*/    CHGVAR     VAR(&D) VALUE('BEEIDT >=  ' *CAT +
                         &FDDTEA)

  /*PIO      CHGVAR     VAR(&E) VALUE('*AND BEEIDT *LE ' *CAT + +
                         &TDDTEA)                              */
  /*PIO*/    CHGVAR     VAR(&E) VALUE(' AND BEEIDT <=  ' *CAT +
                         &TDDTEA)
             ENDDO

             IF         COND((&FDATEA *NE '0000000') *AND (&FDDTEA +
                          *NE '0000000')) THEN(CHGVAR VAR(&F) +
                          VALUE(&B *BCAT &C *BCAT '  AND ' *BCAT &D +
                          *BCAT &E))
             ELSE       CMD(CHGVAR VAR(&F) VALUE(&B *BCAT &C *BCAT +
                          &D *BCAT &E))

/*********************************************************************/
/*   SELECT QUERY   === &RPTSEQ = 1   (SCHED SHIP DATE/LOAD SEQ)     */
/*                                    (ACTUALLY, LOAD SEQ ASC  )     */
/*********************************************************************/
RPTSEQ1:     IF         COND(&RPTSEQ *EQ '1') THEN(DO)

   /*PIO     OVRDBF     FILE(OPBFCPL2) SHARE(*YES) +
             OPNQRYF    FILE(OPBFCPL2) +           +
                        QRYSLT(&F) +               +
                        KEYFLD(*FILE)              */

  /*PIO*/    OVRDBF     FILE(OPBFCPP) TOFILE(OPBFCPP) LVLCHK(*NO)
             OPNSQLF    SQL('Select * from OPBFCPP Where ' *BCAT &F +
                          *BCAT ' ORDER BY BEAODT ASC, BERMNB ASC +
                          ') OPNID(OPBFCPL2) OPTION(*ALL) +
                          SEQONLY(*NO) RCDFMT(@BFCPLG) +
                          ALWCPY(*IFRQD) /*PIO*/
  /*PIO*/    OVRDBF     FILE(OPBFCPL2) TOFILE(OPBFCPP) LVLCHK(*NO) +
                          OVRSCOPE(*JOB) SHARE(*YES) OPNSCOPE(*JOB)

/*********************************************************************/
/*   CALL LISTING PROGRAM                                            */
/*********************************************************************/
             CALL       PGM(PDWDPFR) PARM('' &FDATE &TDATE &FDDTE +
                          &TDDTE &COMP &LOAD &SHPMTH &CARRIER &FLAG +
                          &LOADTYP &WHSE1 &WHSE2 &WHSE3 &WHSE4 +
                          &WHSE5 &WHSE6 &WHSE7 &WHSE8 &WHSE9 +
                          &WHSE10 &SOFLAG &APFRMDT &APTHRDT)

             CLOF       OPNID(OPBFCPL2)
  /*PIO      DLTOVR     FILE(OPBFCPL2)     */
             DLTOVR     FILE(OPBFCPL2) LVL(*JOB) /*PIO*/
             ENDDO

/*********************************************************************/
/*   SELECT QUERY   === &RPTSEQ = 2   (CARRIER/REQ DLV DATE SEQ)    */
/*                  (ACTUALLY, CARRIER/LOAD ID, REQ DELIV DATE )     */
/*********************************************************************/
RPTSEQ2:     IF         COND(&RPTSEQ *EQ '2') THEN(DO)

   /*PIO     OVRDBF     FILE(OPBFCPLF) SHARE(*YES)  +
             OPNQRYF    FILE(OPBFCPLF) +            +
                        QRYSLT(&F) +                +
                        KEYFLD(*FILE)              */

   /*PIO*/   OVRDBF     FILE(OPBFCPP) TOFILE(OPBFCPP) LVLCHK(*NO)
             OPNSQLF    SQL('Select * from OPBFCPP Where ' *BCAT &F +
                          *BCAT 'ORDER BY BEBZNA ASC, BERMNB ASC, +
                          BEEIDT ASC') OPNID(OPBFCPLF) OPTION(*ALL) +
                          SEQONLY(*NO) RCDFMT(@BFCPRE) +
                          ALWCPY(*IFRQD) /*PIO*/
   /*PIO*/   OVRDBF     FILE(OPBFCPLF) TOFILE(OPBFCPP) LVLCHK(*NO) +
                          OVRSCOPE(*JOB) SHARE(*YES) OPNSCOPE(*JOB)


/*********************************************************************/
/*   CALL LISTING PROGRAM                                            */
/*********************************************************************/
             CALL       PGM(PDEXPFR) PARM('' &FDATE &TDATE &FDDTE +
                          &TDDTE &COMP &LOAD &SHPMTH &CARRIER &FLAG +
                          &LOADTYP &WHSE1 &WHSE2 &WHSE3 &WHSE4 +
                          &WHSE5 &WHSE6 &WHSE7 &WHSE8 &WHSE9 +
                          &WHSE10 &SOFLAG &APFRMDT &APTHRDT)

             CLOF       OPNID(OPBFCPLF)
    /*PIO    DLTOVR     FILE(OPBFCPLF)     */
             DLTOVR     FILE(OPBFCPLF) LVL(*JOB) /*PIO*/
             ENDDO
 END:        ENDPGM
