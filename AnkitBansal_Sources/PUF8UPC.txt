/***************************************************************** */
/*  PROGRAM:       PUF8UPC                                         */
/*  PROGRAM DESC:  EXC UPLOAD CSV & EXCPGMCL EXECUTE USER PROGRAM  */
/*  PROGRAMMER:    DANNY NGUYEN                                    */
/*  DATE:          02/13/2018                                      */
/*                                                                 */
/*  PURPOSE:       Conversion (or on demand) program to copy CSV   */
/*                 file & upload to a generic file on the AS400.   */
/*                 file on the AS400. Then it will execute a       */
/*                 program to run.                                 */
/*                 NOTE: CSV file will be saved in the following:  */
/*                       IFS directory '/home/CSVUPLOAD'.          */
/*                       The calling program is hard coded each    */
/*                       time.                                     */
/*                                                                 */
/* PARMS:                                                          */
/*                ˆ&CSVFILNME = CSV File Name. Is CASE SENSITIVE.  */
/*                ˆ&CPYTOFILE = Copy to File Name.                 */
/*******************************************************************/
/*  Modification History                                           */
/*                                                                 */
/*******************************************************************/
             PGM        PARM(&CSVFILNME &CPYTOFILE)

             DCL        VAR(&CSVFILNME) TYPE(*CHAR) LEN(10) /* CSV +
                          File Name */
             DCL        VAR(&CPYTOFILE) TYPE(*CHAR) LEN(10) /* AS400 +
                          File Name */

             DCL        VAR(&FROMSTMF) TYPE(*CHAR) LEN(30) +
                          VALUE('/home/csvupload/')
             DCL        VAR(&CSVDOTEXTN) TYPE(*CHAR) LEN(4) +
                          VALUE('.csv')
             DCL        VAR(&CSVFILNME2) TYPE(*CHAR) LEN(14)
             DCL        VAR(&CALLPGM) TYPE(*CHAR) LEN(10)

             DCL        VAR(&MSG) TYPE(*CHAR) LEN(512)
             DCL        VAR(&MSGID) TYPE(*CHAR) LEN(7)
             DCL        VAR(&MSGL) TYPE(*DEC) LEN(5 0)

/*-----------------------------------------------------------------*/
/* Validate Copy to Filename Exist.                                */
/*-----------------------------------------------------------------*/
             CHKOBJ     OBJ(&CPYTOFILE) OBJTYPE(*FILE)
             MONMSG     MSGID(CPF9801) EXEC(DO)
             SNDPGMMSG  MSG('Copy To File ' *CAT &CPYTOFILE *TCAT +
                          ' not found.')
             GOTO       CMDLBL(END)
             ENDDO

/*-----------------------------------------------------------------*/
/* Copy From Import File                                           */
/*-----------------------------------------------------------------*/
    /* Format csv file name with .csv extension. */
             CHGVAR     VAR(&CSVFILNME2) VALUE(&CSVFILNME *TCAT +
                          &CSVDOTEXTN)

    /* Format From Stream File Path. */
             CHGVAR     VAR(&FROMSTMF) VALUE(&FROMSTMF *TCAT +
                          &CSVFILNME2)

    /* Copy CSV File to AS400 File. */
             CPYFRMIMPF FROMSTMF(&FROMSTMF) TOFILE(*LIBL/&CPYTOFILE) +
                          MBROPT(*REPLACE) RCDDLM(*CRLF) +
                          RMVCOLNAM(*YES)
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(MSGTRAP))

/*-----------------------------------------------------------------*/
/* Call Program to Process.                                        */
/*-----------------------------------------------------------------*/
    /* Get Calling Program from System Values by Copy To Filename. */
         /*  CALL       PGM(PPJXXFR) PARM('' &CPYTOFILE &CALLPGM) */
         /*‚Change the calling program here. */
             CHGVAR     VAR(&CALLPGM) VALUE('PUY8XFR')

             IF         COND(&CALLPGM *EQ ' ') THEN(DO)
             SNDPGMMSG  MSG('Calling Program not found in System +
                           Values file. Job completed normally.')
             GOTO       CMDLBL(END)
             ENDDO
             ELSE       CMD(DO)
             CALL       PGM(&CALLPGM) PARM('')
             ENDDO

             GOTO       CMDLBL(END)

/*----------------------RECEIVE MESSAGES----------------------------*/
 MSGTRAP:    RCVMSG     MSGTYPE(*EXCP) MSGDTA(&MSG) MSGDTALEN(&MSGL) +
                          MSGID(&MSGID)
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(END))

             SNDPGMMSG  MSGID(&MSGID) MSGF(QCPFMSG) MSGDTA(%SST(&MSG +
                          1 &MSGL)) MSGTYPE(*ESCAPE)
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(END))

 END:        ENDPGM
