     A*%%TS  SD  20041223  102211  ISLRAMS     REL-V5R2M0  5722-WDS
     A*****************************************************************
     A*
     A*  SYSTEM:        TRIUMPH FOODS
     A*  DISPLAY FILE:  TFD796
     A*  TITLE:         VIEW BASE MARKETING FEES
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  CREATED:       12/16/04
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/TFPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CF03(03 'EXIT')
     A                                      CF05(05 'REFRESH')
     A*
     A*-----------------------------------------------------------------
     A* RECORD FORMAT ONE
     A*-----------------------------------------------------------------
     A          R TF796R1
     A*%%TS  SD  20041223  100848  ISLRAMS     REL-V5R2M0  5722-WDS
     A                                      OVERLAY
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 24'VIEW BASE MARKETING FEES'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USR)
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@JOB)
     A                                      DSPATR(HI)
     A                                  3  2'Year number .......:'
     A            D1YRNO    R        O  3 23REFFLD(TFREF/@@YRNO *LIBL/TFPREF)
     A                                      EDTCDE(Z)
     A                                      DSPATR(HI)
     A                                  4  2'Year beginning date:'
     A            D1YBMDY   R        O  4 23REFFLD(TFREF/@@DT06 *LIBL/TFPREF)
     A                                      EDTWRD('  /  /  ')
     A                                      DSPATR(HI)
     A                                  5  2'Year ending date ..:'
     A            D1YEMDY   R        O  5 23REFFLD(TFREF/@@DT06 *LIBL/TFPREF)
     A                                      EDTWRD('  /  /  ')
     A                                      DSPATR(HI)
     A                                  7  2'Current fee ..:'
     A            D1BMCFEE  R        O  7 18REFFLD(TFREF/@@BMCFEE)
     A                                      EDTCDE(K)
     A                                      DSPATR(HI)
     A                                  8  2'Deferred fee .:'
     A            D1BMDFEE  R        O  8 18REFFLD(TFREF/@@BMDFEE)
     A                                      EDTCDE(K)
     A                                      DSPATR(HI)
     A                                  9  2'Total fee ....:'
     A            D1BMTFEE  R        O  9 18REFFLD(TFREF/@@BMTFEE *LIBL/TFPREF)
     A                                      EDTCDE(K)
     A                                      DSPATR(HI)
     A*
     A*-----------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*-----------------------------------------------------------------
     A          R TF796K1
     A*%%TS  SD  20040706  113301  ISLRAMS     REL-V5R2M0  5722-WDS
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A                                 23 11'F5=Refresh'
      *
      *------------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *-----------------------------------------------------------------
     A          R TF796ES                   SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
      *------------------------------------------------------------------
      * ERROR MESSAGE SUBFILE CONTROL FORMAT
      *-----------------------------------------------------------------
     A          R TF796EC                   SFLCTL(TF796ES)
      *
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
