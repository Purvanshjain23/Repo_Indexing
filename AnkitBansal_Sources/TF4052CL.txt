/***************************************************************** */
/*                                                                 */
/*  SYSTEM:          TRIUMPH FOODS                                 */
/*  PROGRAM NUMBER:  TF4152CL                                      */
/*  PROGRAM NAME:    SPECIFY CASH DISTRIBUTION REPORT OPTIONS      */
/*  PROGRAMMER:      LEANNE RAMSEY                                 */
/*  CREATE DATE:     05/06/08                                      */
/*                                                                 */
/*******************************************************************/
/* MODIFIED:                                                       */
/*                                                                 */
/* 05/11/09  LeAnne Ramsey                                         */
/*           Make Print logic match Meat Costing.                  */
/*******************************************************************/

             PGM

             DCL        VAR(&QDATA) TYPE(*CHAR) LEN(66)

             DCL        VAR(&OUTQ) TYPE(*CHAR) LEN(10)
             DCL        VAR(&HOLD) TYPE(*CHAR) LEN(4)
             DCL        VAR(&SAVE) TYPE(*CHAR) LEN(4)
             DCL        VAR(&COPY) TYPE(*DEC)  LEN(1)

             DCL        VAR(&LDFDDT)  TYPE(*DEC) LEN(8)
             DCL        VAR(&LDTDDT)  TYPE(*DEC) LEN(8)

             CHGVAR     VAR(&LDFDDT)  VALUE(%SST(*LDA  7 8))
             CHGVAR     VAR(&LDTDDT)  VALUE(%SST(*LDA 21 8))

/*----------------------------------------------------------------*/
/* SET PARAMETERS FROM LDA FOR LISTINGS                           */
/*----------------------------------------------------------------*/

 OUTQ:       CHGVAR     VAR(&OUTQ) VALUE(%SST(*LDA 401 10))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(*JOB))

 HOLD:       CHGVAR     VAR(&HOLD) VALUE(%SST(*LDA 411 4))
             IF         COND(&HOLD *NE '*YES' *AND &HOLD *NE '*NO ') +
                          THEN(CHGVAR VAR(&HOLD) VALUE(*NO))

 SAVE:       CHGVAR     VAR(&SAVE) VALUE(%SST(*LDA 415 4))
             IF         COND(&SAVE *NE '*YES' *AND &SAVE *NE '*NO ') +
                          THEN(CHGVAR VAR(&SAVE) VALUE(*NO))

 COPY:       CHGVAR     VAR(&COPY) VALUE(%SST(*LDA 419 1))
             IF         COND(&COPY = 0) THEN(CHGVAR VAR(&COPY) +
                          VALUE(1))

/*---------------------------------------------------------------------*/
/* CREATE HEADER/SUMMARY WORKFILE                                      */
/*---------------------------------------------------------------------*/

             CRTDUPOBJ  OBJ(TFP3052) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)


/*---------------------------------------------------------------------*/
/* EXTRACT THE DATA FROM THE CASH DISTRIBUTION HEADER FILE             */
/*---------------------------------------------------------------------*/

             CHGVAR     VAR(&QDATA) VALUE(' ')

/* FROM DATE                                                            */
             CHGVAR     VAR(%SST(&QDATA 1 10)) VALUE('CHDDT *GE')
             CHGVAR     VAR(%SST(&QDATA 12 8)) VALUE(&LDFDDT)

/* TO DATE                                                              */
             CHGVAR     VAR(%SST(&QDATA 21 15)) VALUE('*AND CHDDT *LE')
             CHGVAR     VAR(%SST(&QDATA 37 8)) VALUE(&LDTDDT)

/* POSTED                                                               */
             CHGVAR     VAR(%SST(&QDATA 46 21)) VALUE('*AND CHCHSTCD +
                          *EQ "P"')

/* OVERRIDE CASH DISTRIBUTION HEADER FILE AND BUILD WORKFILE            */

             OVRDBF     FILE(TFL012A) SHARE(*YES)
             OPNQRYF    FILE((TFL012A)) OPTION(*INP) QRYSLT(&QDATA) +
                          KEYFLD(*FILE) SEQONLY(*YES)

             CALL       PGM(TF3052)

             CLOF       OPNID(TFL012A)
             DLTOVR     FILE(TFL012A)

/*---------------------------------------------------------------------*/
/* PRINT REPORT                                                        */
/*---------------------------------------------------------------------*/

             OVRPRTF    FILE(PRINT198) OUTQ(&OUTQ) COPIES(&COPY) +
                          HOLD(&HOLD) SAVE(&SAVE)

             CALL       PGM(TF6052)

/*---------------------------------------------------------------------*/
/* END OF PROCESSING                                                   */
/*---------------------------------------------------------------------*/

             DLTOVR     FILE(*ALL)

             ENDPGM

