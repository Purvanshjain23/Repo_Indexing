/***************************************************************** */
/*                                                                 */
/*  PROGRAM NUMBER:  HPBLDFIN                                      */
/*  PROGRAM NAME:    BUILD DATAMART FILES FOR GROW FINISH          */
/*                   AND NURSERY GROUPS                            */
/*  PROGRAMMER:      LEANNE FEDOR                                  */
/*  CREATE DATE:     08/14/00                                      */
/*                                                                 */
/*  FUNCTION:        THIS CL IS SUBMITTED FROM THE JOB SCHEDULER   */
/*                   ON BIGBYTE.                                   */
/*                                                                 */
/*  THE RUN TIME ON THE JOB GOT UP TO 3 OR 4 HOURS. WE CREATED     */
/*  THE DATA AREA 'DAFIN' IS USED TO CONTROL/DEFINE "CURRENT" AND  */
/*  "HISTORY" PROCESSING                                           */
/*                                                                 */
/*  TO CLEAR/BUILD ALL FILES SUBMIT THIS CL WITH A PARM VALUE      */
/*  OF H=HISTORY. TO DELETE/REBUILD ONLY 'CURRENT' RECORDS IN THE  */
/*  FILES, SUBMIT THE JOB WITH A BLANK FOR THE PARM VALUE.         */
/*                                                                 */
/*  THE DATA AREA CONTAINS THE BEGINNING/ENDING DATES              */
/*  FOR THE FIRST WEEK OF PROCESSING FOR BOTH SCENARIOS. USE OF    */
/*  THE DATA AREA ELIMINATES HARD-CODING OF DATES IN PROGRAMS AND  */
/*  ALLOWS 'CURRENT' TO BE REDIFINED AS TIME GOES BY.              */
/*                                                                 */
/*******************************************************************/
/*  MODIFIED:                                                      */

/* 12/05/01  LEANNE FEDOR                                          */
/*           REMOVED CALL TO HP315CL TO CREATE SOURCE INFO. WE WILL*/
/*           NOW GET THE INFO FROM FILES CREATED DURING THE        */
/*           SIRE LINE DERIVATION FUNCTION.                        */

/* 02/28/02  LEANNE FEDOR                                          */
/*           ADDED A NEW PROGRAM TO CALCULATE BEGINNING/ENDING     */
/*           DATES FOR 'CURRENT' PROCESSING.                       */

/* 05/29/02  LEANNE FEDOR                                          */
/*           MADE ADDITIONAL FILES DELETE/REBUILD FOR 'CURRENT'    */
/*           PROCESSING. PRIOR TO THIS, THESE FILES USED THE       */
/*           CLEAR/BUILD LOGIC.                                    */
/*                                                                 */
/* 08/22/02  BARB GUTIERREZ                                        */
/*           NO LONGER SENDING RECORDS TO NIBBLE.  WE'LL RUN ON    */
/*           BIGBYTE.                                              */
/*                                                                 */
/* 10/22/02  LEANNE FEDOR                                          */
/*           ADDED LOGIC FOR NEW FILE: REPORTED DAILY ACTIVITY     */
/*                                                                 */
/* 09/30/03  BARB GUTIERREZ                                        */
/*           REMOVED PROCESSING OF HPPF033                         */
/*                                                                 */
/* 10/11/04  LEANNE FEDOR                                          */
/*           ADDED LOGIC TO RESET/CLEAR THE NEW 'SUBMITTED FLAG'   */
/*           IN THE DATA AREA. THIS FLAG WILL KEEP THE USERS FROM  */
/*           STEPPING ON THEMSELVES WHEN THEY SUBMIT THE FUNCTION  */
/*           FROM THE NEW SUBMISSION SCREENS. THE FLAG LOGIC WAS   */
/*           NOT ADDED TO THE JOB SCHEDULER SUBMISSION.            */
/*                                                                 */
/* 11/01/04  LeAnne Fedor                                          */
/*           Added logic to write Missing Weeks records to the     */
/*           Farm/Building/Room file.                              */
/*                                                                 */
/* 09/21/07  LeAnne Ramsey                                         */
/*           Added a new program:                                 */
/*             HP295-Datamart FIN: Targets                         */
/*                                                                 */
/* 10/13/08  LEANNE RAMSEY                                         */
/*           WE ARE ANTICPATING THAT WE WILL BEGIN TO "PURGE" HPS. */
/*           THEREFORE, WE WILL NO LONGER WANT TO DO "HISTORY"     */
/*           RUNS SINCE WE COULD BE DELETING DATAMART DATA THAT    */
/*           WE CANNOT REBUILD---HAVING PURGED HPS.                */
/*           BUT, I HATE TO REMOVE ALL THE "HISTORY" LOGIC.        */
/*           SO, WE WILL, FOR NOW, SET THE INCOMING "PROC" PARM    */
/*           TO BLANK TO FORCE A "CURRENT" RUN.                    */
/*           WE WILL REMOVE THE "SEND" PARM--IT IS OBSOLETE.       */

/*           SEND PARM:       1) Y=YES, SEND TO NIBBLE FOR COGNOS      */
/*                            2) N=NO, COPY TO PRKDMFLIB FOR COGNOS    */
/*                            3) BLANK= BUILD ONLY; DON'T SEND OR COPY */
/*                                                                     */
/* 04/07/09  LeAnne Ramsey                                             */
/*           Removed all logic for HPPF088-Datamart FIN: Farm Deaths   */
/*           with Reasons. This file is no longer used and is being    */
/*           deleted.                                                  */
/*                                                                     */
/* 08/31/09  ALICE BROWNFIELD                                          */
/*           E00472 - Data Mart Build changes for DR Test              */
/*           Change CPYF from PRKFLIB to PRKDMFLIB to be CRTDUPOBJ     */
/*           ----  this works better for MIMIX                         */
/*                                                                     */
/* 10/02/09  LEANNE RAMSEY                                                    */
/*           The CPYF on HPPF075 was selecting only the "Full Week" records.  */
/*           So, when we went to CRTDUPOBJ, all records got duped.            */
/*           Stephanie Allen could not easily do anything in COGNOS to handle */
/*           this. So, we will go ahead and CRTDUPOBJ on the HPPF075 file.    */
/*           Then, we will immediately (using SQL) delete the Not-Full-Week   */
/*           records from the file in PRKDMFLIB.                              */
/*                                                                 */
/* 04/28/10  LeAnne Ramsey                                         */
/*           We now allocate/deallocate the files that are         */
/*           deleted/created in PRKDMFLIB.                         */
/*           Also, we now use ESEND instead of MPLUS/MPLUS.        */
/*           Note: Leave the MONMSG on the CRTDUPOBJ of the LFs    */
/*           monitoring for CPF0000 without setting the error flag.*/
/*                                                                 */
/* 10/14/16  Barb Gutierrez                                        */
/*           Added call to new program HP236CL.  This clp is used  */
/*           to calculate new fields Sold (MKT) days in phase,     */
/*           average sold (mkt) days in phase, and weighted open   */
/*           date.  This is only being done for GF groups and      */
/*           is only being calculated against live head.   E6136   */
/*                                                                 */
/* 07/01/20  Brad Baden   WHD# 66046  TFS# 16673                   */
/*           Send WRKOBJLCK report to user when any ALCOBJ fails.  */
/*                                                                 */
/***********************************************************************/

/*  THE PROC PARM WILL BE     1) BLANK FOR "CURRENT" PROCESSING, OR    */
/*                            2) 'H' FOR "HISTORY" PROCESSING          */
/*  NOTE: AS OF DECEMBER 2008, WE WILL OVERLAY THE INCOMING VALUE      */
/*  WITH A BLANK TO FORCE A "CURRENT" RUN.                             */


             PGM        PARM(&PROCFL)

             DCL        VAR(&PROCFL) TYPE(*CHAR) LEN(1)

             DCL        VAR(&CURBEGDT) TYPE(*DEC) LEN(8)
             DCL        VAR(&ERR)    TYPE(*CHAR) LEN(1) VALUE('N')
             DCL        VAR(&USER)     TYPE(*CHAR) LEN(10)
             DCL        VAR(&EMAIL)    TYPE(*CHAR) LEN(38)
             DCL        VAR(&TSTPRDFL) TYPE(*CHAR) LEN(1)
             DCL        VAR(&SUBJECT)  TYPE(*CHAR) LEN(60) +
                          VALUE('HPBLDFIN-Datamart FIN: Build +
                          Files....DIST LIST: prk cognos')

/* OVERRIDE INCOMING PARM TO BE "CURRENT" PROCESSING                 */

             CHGVAR     VAR(&PROCFL) VALUE(' ')

/*------------------------------------------------------------------*/
/* Set up the Email Recipient (either Test or Production)           */
/*------------------------------------------------------------------*/

/* Retrieve the Data Area that lets us know whether we are in TEST  */
/* or PRODUCTION. (Always default to the 'TEST' Distribution List.) */

             CHGVAR     VAR(&EMAIL) VALUE('prk cognos test')
             RTVDTAARA  DTAARA(DATSTPRD (1 1)) RTNVAR(&TSTPRDFL)
             IF         COND(&TSTPRDFL *EQ 'P') THEN(CHGVAR +
                          VAR(&EMAIL) VALUE('prk cognos'))

/*------------------------------------------------------------------*/
/* Retrieve the User for sending the AS400 messages                 */
/*------------------------------------------------------------------*/

             RTVJOBA    USER(&USER)

/*-----------------------------------------------------------------*/
/* Allocate all files that will be deleted/recreated in PRKDMFLIB  */
/*-----------------------------------------------------------------*/

/* Farm Site                                                       */
             ALCOBJ     OBJ((PRKDMFLIB/HPPF018 *FILE *EXCL))
             MONMSG     MSGID(CPF0000) EXEC(DO)
             CHGVAR     VAR(&ERR) VALUE('Y')

             /* WHD66046 - Send WRKOBJLCK report to &EMAIL           */
             WRKOBJLCK  OBJ(PRKDMFLIB/HPPF018) OBJTYPE(*FILE) +
                          OUTPUT(*PRINT)

             ESNDMAIL   RECIPIENT(&EMAIL) SUBJECT(&SUBJECT) MSG('No +
                          Datamart files were rebuilt because the +
                          program was unable to allocate file +
                          HPPF018. Check the physical (and logical +
                          files over it) to see who has the file(s) +
                          locked. The user has been sent an AS400 +
                          message to resubmit the job later. See +
                          attached report.') IMPORTANCE(*HIGH) +
                          ATTLIST((* *DFT *N QPDSPOLK * *LAST 1))

             DLTSPLF    FILE(QPDSPOLK)

             SNDMSG     MSG('***** Your build of NUR/FIN Data for COGNOS +
                          did not run because file HPPF018-Farm +
                          Sites was in use. No one can be using +
                          this file during the build. Resubmit your +
                          build later. *****') TOUSR(&USER)
                        ENDDO

/* Farm Buildings                                                         */
             ALCOBJ     OBJ((PRKDMFLIB/HPPF019 *FILE *EXCL))
             MONMSG     MSGID(CPF0000) EXEC(DO)
             CHGVAR     VAR(&ERR) VALUE('Y')

             /* WHD66046 - Send WRKOBJLCK report to &EMAIL           */
             WRKOBJLCK  OBJ(PRKDMFLIB/HPPF019) OBJTYPE(*FILE) +
                          OUTPUT(*PRINT)

             ESNDMAIL   RECIPIENT(&EMAIL) SUBJECT(&SUBJECT) MSG('No +
                          Datamart files were rebuilt because the +
                          program was unable to allocate file +
                          HPPF019. Check the physical (and logical +
                          files over it) to see who has the file(s) +
                          locked. The user has been sent an AS400 +
                          message to resubmit the job later. See +
                          attached report.') IMPORTANCE(*HIGH) +
                          ATTLIST((* *DFT *N QPDSPOLK * *LAST 1))

             DLTSPLF    FILE(QPDSPOLK)

             SNDMSG     MSG('***** Your build of NUR/FIN Data for COGNOS +
                          did not run because file HPPF019-Farm +
                          Buildings was in use. No one can be using +
                          this file during the build. Resubmit your +
                          build later. *****') TOUSR(&USER)
                        ENDDO

/* Hog Groups                                                             */
             ALCOBJ     OBJ((PRKDMFLIB/HPPF034 *FILE *EXCL))
             MONMSG     MSGID(CPF0000) EXEC(DO)
             CHGVAR     VAR(&ERR) VALUE('Y')

             /* WHD66046 - Send WRKOBJLCK report to &EMAIL           */
             WRKOBJLCK  OBJ(PRKDMFLIB/HPPF034) OBJTYPE(*FILE) +
                          OUTPUT(*PRINT)

             ESNDMAIL   RECIPIENT(&EMAIL) SUBJECT(&SUBJECT) MSG('No +
                          Datamart files were rebuilt because the +
                          program was unable to allocate file +
                          HPPF034. Check the physical (and logical +
                          files over it) to see who has the file(s) +
                          locked. The user has been sent an AS400 +
                          message to resubmit the job later. See +
                          attached report.') IMPORTANCE(*HIGH) +
                          ATTLIST((* *DFT *N QPDSPOLK * *LAST 1))

             DLTSPLF    FILE(QPDSPOLK)

             SNDMSG     MSG('***** Your build of NUR/FIN Data for +
                          COGNOS did not run because file +
                          HPPF034-Hog Groups was in use. No one +
                          can be using this file during the build. +
                          Resubmit your build later. *****') +
                          TOUSR(&USER)
                        ENDDO

/* Weekly Group Detail                                                        */
             ALCOBJ     OBJ((PRKDMFLIB/HPPF075 *FILE *EXCL))
             MONMSG     MSGID(CPF0000) EXEC(DO)
             CHGVAR     VAR(&ERR) VALUE('Y')

             /* WHD66046 - Send WRKOBJLCK report to &EMAIL           */
             WRKOBJLCK  OBJ(PRKDMFLIB/HPPF075) OBJTYPE(*FILE) +
                          OUTPUT(*PRINT)

             ESNDMAIL   RECIPIENT(&EMAIL) SUBJECT(&SUBJECT) MSG('No +
                          Datamart files were rebuilt because the +
                          program was unable to allocate file +
                          HPPF075. Check the physical (and logical +
                          files over it) to see who has the file(s) +
                          locked. The user has been sent an AS400 +
                          message to resubmit the job later. See +
                          attached report.') IMPORTANCE(*HIGH) +
                          ATTLIST((* *DFT *N QPDSPOLK * *LAST 1))

             DLTSPLF    FILE(QPDSPOLK)

             SNDMSG     MSG('***** Your build of NUR/FIN Data for +
                          COGNOS did not run because file +
                          HPPF075-Weekly Group Detail was in use. +
                          No one can be using this file during the +
                          build. Resubmit your build later. *****') +
                          TOUSR(&USER)
                        ENDDO

/* Reported Daily Activity                                                   */
             ALCOBJ     OBJ((PRKDMFLIB/HPPF103 *FILE *EXCL))
             MONMSG     MSGID(CPF0000) EXEC(DO)
             CHGVAR     VAR(&ERR) VALUE('Y')

             /* WHD66046 - Send WRKOBJLCK report to &EMAIL           */
             WRKOBJLCK  OBJ(PRKDMFLIB/HPPF103) OBJTYPE(*FILE) +
                          OUTPUT(*PRINT)

             ESNDMAIL   RECIPIENT(&EMAIL) SUBJECT(&SUBJECT) MSG('No +
                          Datamart files were rebuilt because the +
                          program was unable to allocate file +
                          HPPF103. Check the physical (and logical +
                          files over it) to see who has the file(s) +
                          locked. The user has been sent an AS400 +
                          message to resubmit the job later. See +
                          attached report.') IMPORTANCE(*HIGH) +
                          ATTLIST((* *DFT *N QPDSPOLK * *LAST 1))

             DLTSPLF    FILE(QPDSPOLK)

             SNDMSG     MSG('***** Your build of NUR/FIN Data for COGNOS +
                          did not run because file HPPF103-Reported Daily +
                          Activity was in use. No one can be using +
                          this file during the build. Resubmit your +
                          build later. *****') TOUSR(&USER)
                        ENDDO

/* Weekly Farm/Building/Room                                                */
             ALCOBJ     OBJ((PRKDMFLIB/HPPF175 *FILE *EXCL))
             MONMSG     MSGID(CPF0000) EXEC(DO)
             CHGVAR     VAR(&ERR) VALUE('Y')

             /* WHD66046 - Send WRKOBJLCK report to &EMAIL           */
             WRKOBJLCK  OBJ(PRKDMFLIB/HPPF175) OBJTYPE(*FILE) +
                          OUTPUT(*PRINT)

             ESNDMAIL   RECIPIENT(&EMAIL) SUBJECT(&SUBJECT) MSG('No +
                          Datamart files were rebuilt because the +
                          program was unable to allocate file +
                          HPPF175. Check the physical (and logical +
                          files over it) to see who has the file(s) +
                          locked. The user has been sent an AS400 +
                          message to resubmit the job later. See +
                          attached report.') IMPORTANCE(*HIGH) +
                          ATTLIST((* *DFT *N QPDSPOLK * *LAST 1))

             DLTSPLF    FILE(QPDSPOLK)

             SNDMSG     MSG('***** Your build of NUR/FIN Data for +
                          COGNOS did not run because file +
                          HPPF175-Weekly Farm/Building/Room was in +
                          use. No one can be using this file during +
                          the build. Resubmit your build later. +
                          *****') TOUSR(&USER)
                        ENDDO

/* Sire Lines                                                      */
             ALCOBJ     OBJ((PRKDMFLIB/HSP098 *FILE *EXCL))
             MONMSG     MSGID(CPF0000) EXEC(DO)
             CHGVAR     VAR(&ERR) VALUE('Y')

             /* WHD66046 - Send WRKOBJLCK report to &EMAIL           */
             WRKOBJLCK  OBJ(PRKDMFLIB/HSP098) OBJTYPE(*FILE) +
                          OUTPUT(*PRINT)

             ESNDMAIL   RECIPIENT(&EMAIL) SUBJECT(&SUBJECT) MSG('No +
                          Datamart files were rebuilt because the +
                          program was unable to allocate file +
                          HSP098. Check the physical (and logical +
                          files over it) to see who has the file(s) +
                          locked. The user has been sent an AS400 +
                          message to resubmit the job later. See +
                          attached report.') IMPORTANCE(*HIGH) +
                          ATTLIST((* *DFT *N QPDSPOLK * *LAST 1))

             DLTSPLF    FILE(QPDSPOLK)

             SNDMSG     MSG('***** Your build of NUR/FIN Data for COGNOS +
                          did not run because file HSP098-Sire +
                          Lines was in use. No one can be using +
                          this file during the build. Resubmit your +
                          build later. *****') TOUSR(&USER)
                        ENDDO

/* If you were unable to allocate any file, just get on out.         */

             IF         COND(&ERR = 'Y') THEN(GOTO CMDLBL(EXIT))

/*-----------------------------------------------------------------*/
/* CALCULATE 'CURRENT' BEGINNING/ENDING DATES AND UPDATE DATA AREA */
/*-----------------------------------------------------------------*/

             CALL       PGM(HP209) PARM(&CURBEGDT)


/*-----------------------------------------------------------------*/
/* ALWAYS CLEAR THE FOLLOWING DATAMART FILES...REGARDLESS OF       */
/* WHETHER THIS RUN IS A HISTORY RUN OR A CURRENT RUN              */
/*-----------------------------------------------------------------*/

             CLRPFM     FILE(*LIBL/HPPF018) /* Farms          */
             CLRPFM     FILE(*LIBL/HPPF019) /* Farm Buildings */

/*-----------------------------------------------------------------*/
/* CLEAR THE FOLLOWING DATAMART FILES FOR A 'HISTORY' RUN OR       */
/* DELETE RECORDS FROM THEM FOR A 'CURRENT' RUN.                   */
/*-----------------------------------------------------------------*/

             IF         COND(&PROCFL *EQ 'H') THEN(DO)
             CLRPFM     FILE(*LIBL/HPPF034) /* Group Header            */
             CLRPFM     FILE(*LIBL/HPPF075) /* Weekly Group Detail     */
             CLRPFM     FILE(*LIBL/HPPF175) /* Weekly Farm/Bldg/Room   */
             CLRPFM     FILE(*LIBL/HPPF103) /* Reported Daily Activity */
             ENDDO

             IF         COND(&PROCFL *EQ ' ') THEN(DO)
             CALL PGM(HP210)
             ENDDO


/*-----------------------------------------------------------------*/
/* BUILD THE FARM FILES                                            */
/*-----------------------------------------------------------------*/
             CALL       PGM(HP212) /* Farm Sites  */
             CALL       PGM(HP215) /* Farm Buildings */


/*-----------------------------------------------------------------*/
/* BUILD THE GROUP HEADER                                          */
/*-----------------------------------------------------------------*/

             CALL       PGM(HP213CL) PARM(&PROCFL &CURBEGDT)

/*---------------------------------------------------------------------*/
/* UPDATE THE GROUP HEADER                                             */
/*---------------------------------------------------------------------*/

/* In-before-out-flag */
             CALL       PGM(HP214CL) PARM(&PROCFL &CURBEGDT)

/* Update Closed/Disposed Groups */
             CALL       PGM(HP218CL) PARM(&PROCFL &CURBEGDT)

/* Update Closed GF Groups with Sold days in phase and avg sold days  */
/* in phase along with a weighted open date for live head only.       */
/* No deads or culls included. These are only 'MRKTS' sales to plant. */

             CALL       PGM(HP236CL) PARM(&PROCFL &CURBEGDT)

/*-----------------------------------------------------------------*/
/* BUILD THE WEEKLY DETAIL FILES FOR GROUPS AND FARMS              */
/*-----------------------------------------------------------------*/

             CALL       PGM(HP220) PARM(&PROCFL) /* WEEKLY GROUP DETAIL */
             CALL       PGM(HP221) PARM(&PROCFL) /* FARM/BUILDING/ROOM  */
             CALL       PGM(HP222) PARM(&PROCFL) /* COGNOS WORK-AROUND  */

/* Write Missing Week records to the Farm/Building/Room file */
/* (NOTE: You have to build a workfile of weeks first.)      */

             CRTDUPOBJL OBJ(HSP3021) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)
             CALL       PGM(HP3021) PARM(&PROCFL)

             CALL       PGM(HP2021)

/* Update the Group Header and Weekly Group Detail file with Target data */

             CALL       PGM(HP295) PARM(&PROCFL &CURBEGDT)

/*-----------------------------------------------------------------*/
/* BUILD THE REPORTED DAILY ACTIVITY FILE                          */
/*-----------------------------------------------------------------*/

             CALL       PGM(HP216CL) PARM(&PROCFL &CURBEGDT)


/*------------------------------------------------------------------------*/
/* IF YOU ARE PERFORMING "CURRENT" PROCESSING, THEN REORG THE FILES       */
/* BEFORE SENDING THEM TO NIBBLE.                                         */
/*------------------------------------------------------------------------*/

             IF         COND(&PROCFL *EQ ' ') THEN(DO)

             RGZPFM     *LIBL/HPPF034
             MONMSG     MSGID(CPF0000)

             RGZPFM     *LIBL/HPPF075
             MONMSG     MSGID(CPF0000)

             RGZPFM     *LIBL/HPPF103
             MONMSG     MSGID(CPF0000)

             RGZPFM     *LIBL/HPPF175
             MONMSG     MSGID(CPF0000)
             ENDDO

/*-----------------------------------------------------------------*/
/* DELETE DATAMART FILES in PRKDMFLIB                               */
/*-----------------------------------------------------------------*/

/* LOGICALS                                                          */
             DLTF       FILE(PRKDMFLIB/HPLF018*)
             MONMSG     MSGID(CPF0000)
             DLTF       FILE(PRKDMFLIB/HPLF019*)
             MONMSG     MSGID(CPF0000)
             DLTF       FILE(PRKDMFLIB/HPLF034*)
             MONMSG     MSGID(CPF0000)
             DLTF       FILE(PRKDMFLIB/HPLF075*)
             MONMSG     MSGID(CPF0000)
             DLTF       FILE(PRKDMFLIB/HPLF103*)
             MONMSG     MSGID(CPF0000)
             DLTF       FILE(PRKDMFLIB/HPLF175*)
             MONMSG     MSGID(CPF0000)
             DLTF       FILE(PRKDMFLIB/HSL098*)
             MONMSG     MSGID(CPF0000)

/* PHYSICALS                                                         */
             DLTF       FILE(PRKDMFLIB/HPPF018)
             MONMSG     MSGID(CPF0000)
             DLTF       FILE(PRKDMFLIB/HPPF019)
             MONMSG     MSGID(CPF0000)
             DLTF       FILE(PRKDMFLIB/HPPF034)
             MONMSG     MSGID(CPF0000)
             DLTF       FILE(PRKDMFLIB/HPPF075)
             MONMSG     MSGID(CPF0000)
             DLTF       FILE(PRKDMFLIB/HPPF103)
             MONMSG     MSGID(CPF0000)
             DLTF       FILE(PRKDMFLIB/HPPF175)
             MONMSG     MSGID(CPF0000)
             DLTF       FILE(PRKDMFLIB/HSP098)
             MONMSG     MSGID(CPF0000)

/*********************************************************************/
/* CREATE THE FILES INTO PRKDMFLIB                                  */
/*********************************************************************/

             CRTDUPOBJ  OBJ(HPPF018) FROMLIB(PRKFLIB) OBJTYPE(*FILE) +
                          TOLIB(PRKDMFLIB) DATA(*YES)
             MONMSG     MSGID(CPF2100) EXEC(CHGVAR VAR(&ERR) +
                          VALUE('Y'))

             CRTDUPOBJ  OBJ(HPPF019) FROMLIB(PRKFLIB) OBJTYPE(*FILE) +
                          TOLIB(PRKDMFLIB) DATA(*YES)
             MONMSG     MSGID(CPF2100) EXEC(CHGVAR VAR(&ERR) +
                          VALUE('Y'))

             CRTDUPOBJ  OBJ(HPPF034) FROMLIB(PRKFLIB) OBJTYPE(*FILE) +
                          TOLIB(PRKDMFLIB) DATA(*YES)
             MONMSG     MSGID(CPF2100) EXEC(CHGVAR VAR(&ERR) +
                          VALUE('Y'))

             CRTDUPOBJ  OBJ(HPPF075) FROMLIB(PRKFLIB) OBJTYPE(*FILE) +
                          TOLIB(PRKDMFLIB) DATA(*YES)
             MONMSG     MSGID(CPF2100) EXEC(CHGVAR VAR(&ERR) +
                          VALUE('Y'))
             DELETE     SQL('FROM PRKDMFLIB/HPPF075 WHERE WBFULLWK <> +
                          ''Y''')

             CRTDUPOBJ  OBJ(HPPF103) FROMLIB(PRKFLIB) OBJTYPE(*FILE) +
                          TOLIB(PRKDMFLIB) DATA(*YES)
             MONMSG     MSGID(CPF2100) EXEC(CHGVAR VAR(&ERR) +
                          VALUE('Y'))

             CRTDUPOBJ  OBJ(HPPF175) FROMLIB(PRKFLIB) OBJTYPE(*FILE) +
                          TOLIB(PRKDMFLIB) DATA(*YES)
             MONMSG     MSGID(CPF2100) EXEC(CHGVAR VAR(&ERR) +
                          VALUE('Y'))

             CRTDUPOBJ  OBJ(HSP098) FROMLIB(PRKFLIB) OBJTYPE(*FILE) +
                          TOLIB(PRKDMFLIB) DATA(*YES)
             MONMSG     MSGID(CPF2100) EXEC(CHGVAR VAR(&ERR) +
                          VALUE('Y'))

/* LOGICALS                                                          */
             CRTDUPOBJ  OBJ(HPLF018*) FROMLIB(PRKFLIB) +
                          OBJTYPE(*FILE) TOLIB(PRKDMFLIB) DATA(*NO)
             MONMSG     MSGID(CPF0000)

             CRTDUPOBJ  OBJ(HPLF019*) FROMLIB(PRKFLIB) +
                          OBJTYPE(*FILE) TOLIB(PRKDMFLIB) DATA(*NO)
             MONMSG     MSGID(CPF0000)

             CRTDUPOBJ  OBJ(HPLF034*) FROMLIB(PRKFLIB) +
                          OBJTYPE(*FILE) TOLIB(PRKDMFLIB) DATA(*NO)
             MONMSG     MSGID(CPF0000)

             CRTDUPOBJ  OBJ(HPLF075*) FROMLIB(PRKFLIB) +
                          OBJTYPE(*FILE) TOLIB(PRKDMFLIB) DATA(*NO)
             MONMSG     MSGID(CPF0000)

             CRTDUPOBJ  OBJ(HPLF103*) FROMLIB(PRKFLIB) +
                          OBJTYPE(*FILE) TOLIB(PRKDMFLIB) DATA(*NO)
             MONMSG     MSGID(CPF0000)

             CRTDUPOBJ  OBJ(HPLF175*) FROMLIB(PRKFLIB) +
                          OBJTYPE(*FILE) TOLIB(PRKDMFLIB) DATA(*NO)
             MONMSG     MSGID(CPF0000)

             CRTDUPOBJ  OBJ(HSL098*) FROMLIB(PRKFLIB) +
                          OBJTYPE(*FILE) TOLIB(PRKDMFLIB) DATA(*NO)
             MONMSG     MSGID(CPF0000)

/*---------------------------------------------------------------------*/
/* SEND COMPLETION MESSAGE TO I.S. FOLKS                               */
/*---------------------------------------------------------------------*/

             IF         COND(&ERR = 'Y') THEN(ESNDMAIL +
                          RECIPIENT(&EMAIL) SUBJECT(&SUBJECT) +
                          MSG('Some HPPFxxx or HPLFxxx files did +
                          not get recreated in PRKDMFLIB. If the +
                          preceeding DELETEs were successful, you +
                          may be missing files in PRKDMFLIB. +
                          Investigate and rerun the build.') +
                          IMPORTANCE(*HIGH))

             IF         COND(&ERR = 'N') THEN(ESNDMAIL +
                          RECIPIENT(&EMAIL) SUBJECT(&SUBJECT) +
                          MSG('All NUR/FIN files were successfully +
                          created in PRKDMFLIB; the build completed +
                          normally.') IMPORTANCE(*HIGH))

/*----------------------------------------------------------------------*/
/* RESET THE CONTROLLING DATA AREA TO INDICATE THAT THE JOB IS FINISHED */
/*----------------------------------------------------------------------*/

 EXIT:       CHGDTAARA  DTAARA(DAFIN (62 1)) VALUE(' ')

             DLCOBJ     OBJ((PRKDMFLIB/HPPF018 *FILE *EXCL))
             MONMSG     MSGID(CPF0000)
             DLCOBJ     OBJ((PRKDMFLIB/HPPF019 *FILE *EXCL))
             MONMSG     MSGID(CPF0000)
             DLCOBJ     OBJ((PRKDMFLIB/HPPF034 *FILE *EXCL))
             MONMSG     MSGID(CPF0000)
             DLCOBJ     OBJ((PRKDMFLIB/HPPF075 *FILE *EXCL))
             MONMSG     MSGID(CPF0000)
             DLCOBJ     OBJ((PRKDMFLIB/HPPF103 *FILE *EXCL))
             MONMSG     MSGID(CPF0000)
             DLCOBJ     OBJ((PRKDMFLIB/HPPF175 *FILE *EXCL))
             MONMSG     MSGID(CPF0000)
             DLCOBJ     OBJ((PRKDMFLIB/HSP098 *FILE *EXCL))
             MONMSG     MSGID(CPF0000)

             ENDPGM
