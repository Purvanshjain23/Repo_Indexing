     A*%%TS  SD  20080410  102026  ISLRAMS     REL-V5R4M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD179
     A*  TITLE:         DISPOSE HOG GROUPS
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  CREATED:       09/29/99
     A*
     A*****************************************************************
     A* INDICATORS:
     A* 90  THE SUBFILE RECORD HAS BEEN A) REMOVED FROM THE LIST OF
     A*     B) DISPOSED.  ALL FIELDS ARE NON-DISPLAY AND PROTECTED.
     A* 99  THE SUBFILE IS EMPTY. NO OPEN GROUPS EXIST FOR THE
     A*     SPECIFIED FARM AND CUT-OFF OPEN DATE.
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A N99                                  CA05(05 'REFRESH')
     A N99                                  CA10(10 'TRANSFERS')
     A N99                                  CA14(14 'DEATHS')
     A N99                                  CA15(15 'SALES')
     A N99                                  CF17(17 'PRINT OPTIONS')
     A N99                                  CF20(20 'WRKSPLF')
     A*
     A*---------------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*---------------------------------------------------------------------
     A          R HP179S1                   SFL
     A*%%TS  SD  19991027  102158  ISLRAMS     REL-V4R2M0  5769-PW1
     A  85                                  SFLNXTCHG
     A            HDRRN1         4Y 0H
     A            HDHGSN    R        H      REFFLD(@@HGSN)
     A            HDGSCD    R        H      REFFLD(@@GSCD)
     A            HDCRDT    R        H      REFFLD(@@DT08)
     A            HDOPDT    R        H      REFFLD(@@DT08)
     A            HDCYMDDSDTR        H      REFFLD(@@DT08)
     A            HDPROTECT R        H      REFFLD(@@FLAG)
     A            D1OPT     R        B 15  2REFFLD(@@OPT)
     A                                      CHGINPDFT(UL)
     A  90                                  DSPATR(ND)
     A  90                                  DSPATR(PR)
     A  30                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A N30N90                               DSPATR(HI)
     A N30N90                               DSPATR(UL)
     A            D1MDYDSDT R        O 15  6REFFLD(HSRREF/@@DT06 *LIBL/HSPREF)
     A                                      EDTWRD('  /  / 0')
     A  90                                  DSPATR(ND)
     A  31                                  DSPATR(RI)
     A            D1HD      R        O 15 15REFFLD(HSRREF/SGSHHD *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A  90                                  DSPATR(ND)
     A            D1BLCD    R        O 15 24REFFLD(@@BLCD)
     A  90                                  DSPATR(ND)
     A            D1RMCD    R        O 15 30REFFLD(@@RMCD)
     A  90                                  DSPATR(ND)
     A            D1HGCD    R        O 15 37REFFLD(HSRREF/@@HGCD)
     A  90                                  DSPATR(ND)
     A            D1OBFS    R        O 15 46REFFLD(HSRREF/@@FSCD *LIBL/HSPREF)
     A  90                                  DSPATR(ND)
     A                                      EDTCDE(Z)
     A            D1ONFS    R        O 15 52REFFLD(HSRREF/@@FSCD *LIBL/HSPREF)
     A  90                                  DSPATR(ND)
     A                                      EDTCDE(Z)
     A            D1GNCD    R        O 15 59REFFLD(HSRREF/@@GNCD *LIBL/HSPREF)
     A  90                                  DSPATR(ND)
     A            D1BSCD    R        O 15 66REFFLD(HSRREF/@@BSCD *LIBL/HSPREF)
     A  90                                  DSPATR(ND)
     A            D1SXCD    R        O 15 73REFFLD(HSRREF/@@SXCD *LIBL/HSPREF)
     A  90                                  DSPATR(ND)
     A            D1ADOI    R        O 15 75REFFLD(HGREC/HGADOI *LIBL/HSP034)
     A                                      EDTCDE(Z)
     A  90                                  DSPATR(ND)
     A*
     A*
     A*------------------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*------------------------------------------------------------------------
     A          R HP179C1                   SFLCTL(HP179S1)
     A*%%TS  SD  20080410  102026  ISLRAMS     REL-V5R4M0  5722-WDS
     A                                      SFLSIZ(0050)
     A                                      SFLPAG(0006)
     A                                      SETOF(30)
     A                                      SETOF(31)
     A                                      OVERLAY
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 25'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  2 24'DISPOSE OPEN HOG GROUPS'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  4  2'Business office ....:'
     A            C1FSBO    R        O  4 25REFFLD(@@BOCD)
     A            C1BODS    R        O  4 35REFFLD(BOBODS)
     A                                  5  2'Farm site ..........:'
     A            C1FSCD    R        O  5 25REFFLD(FSFSCD)
     A                                      EDTCDE(M)
     A            C1FSNM    R        O  5 35REFFLD(FSFSNM)
     A                                  6  2'Cut-off open date ..:'
     A            C1MDYOPDT R        O  6 25REFFLD(HSRREF/@@DT06 *LIBL/HSPREF)
     A                                      EDTWRD('  /  /  ')
     A                                  8  3'2=Revise group'
     A                                  8 30'5=View Group'
     A                                  8 52'6=Print Review with Costs'
     A                                  9  3'4=Remove group from list'
     A                                  9 30'7=View Movements'
     A                                  9 51'10=Print Review without Costs'
     A                                 10  3'8=Dispose group'
     A                                 10 29'12=View Adjustments'
     A                                 10 51'13=Print Movements'
     A                                 11  2'11=Maintain Adjustments'
     A                                 11 75'Avg'
     A                                      DSPATR(HI)
     A                                 12 75'Days'
     A                                      DSPATR(HI)
     A                                 13  6'Dispose'
     A                                      DSPATR(HI)
     A                                 13 14'Remaining'
     A                                      DSPATR(HI)
     A                                 13 37'Hog'
     A                                      DSPATR(HI)
     A                                 13 45'Origin Farms'
     A                                      DSPATR(HI)
     A                                 13 59'Gen'
     A                                      DSPATR(HI)
     A                                 13 66'Breed'
     A                                      DSPATR(HI)
     A                                 13 75'Old'
     A                                      DSPATR(HI)
     A                                 14  2'Opt'
     A                                      DSPATR(HI)
     A                                 14  8'Date'
     A                                      DSPATR(HI)
     A                                 14 17'Head'
     A                                      DSPATR(HI)
     A                                 14 24'Bldg'
     A                                      DSPATR(HI)
     A                                 14 30'Room'
     A                                      DSPATR(HI)
     A                                 14 37'Group'
     A                                      DSPATR(HI)
     A                                 14 48'BGF'
     A                                      DSPATR(HI)
     A                                 14 54'Nur'
     A                                      DSPATR(HI)
     A                                 14 59'Line'
     A                                      DSPATR(HI)
     A                                 14 66'Stock'
     A                                      DSPATR(HI)
     A                                 14 72'Sx'
     A                                      DSPATR(HI)
     A                                 14 76'In'
     A                                      DSPATR(HI)
     A*
     A*--------------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*--------------------------------------------------------------------
     A          R HP179K1
     A*%%TS  SD  20011031  084925  ISLRAMS     REL-V4R4M0  5769-PW1
     A  43                             21  2'Message program failed.  Call MIS -
     A                                      user support.'
     A                                      DSPATR(HI)
     A                                 22  2'F3=Exit'
     A N99                             22 11'F5=Refresh'
     A N99                             22 23'F10=W/W Transfers'
     A N99                             22 42'F14=Maintain Deaths'
     A N99                             22 63'F15=W/W Sales'
     A N99                             23 42'F17=Print Options'
     A N99                             23 63'F20=Spool File'
     A*
     A*-------------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE FORMAT
     A*-------------------------------------------------------------------
     A          R HP179ES                   SFL SFLMSGRCD(24)
     A*
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
     A*-------------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*-------------------------------------------------------------------
     A          R HP179EC                   SFLCTL(HP179ES)
     A*
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
