/********************************************************************/
/*                                                                  */
/*  SYNOPSIS - PRINT SALES HISTORY EXPENSE DETAIL                   */
/*                                                                  */
/*                                                                  */
/*  COMPANY   - SEABOARD COPRORATION                                */
/*  SYSTEM    - ORDER MANAGEMENT                                    */
/*  USER NAME - ROSE CENTONZE                                       */
/*  DATE      - 04/07/00                                            */
/*                                                                  */
/*      PARAMETERS RECEIVED:                                        */
/*                                                                  */
/*          &OUTQ       - OUTPUT QUEUE                              */
/*          &COPIESL    - NUMBER OF COPIES                          */
/*          &NIGHTQ     - NIGHT QUEUE                               */
/*          &HOLD       - HOLD PRINT FLAG                           */
/*          &SAVE       - SAVE PRINT FLAG                           */
/*          &PGM        - PROGRAM NAME                              */
/*          &COMPL      - COMPANY NUMBER - BLANK FOR ALL            */
/*          &FDATEL     - FROM DATE                                 */
/*          &TDATEL     - TO DATE                                   */
/*          &CUSTL      - CUSTOMER                                  */
/*          &EXPCD      - EXPENSE CODE                              */
/********************************************************************/

 POEXPCLP:   PGM        PARM(&PGM &COMPL &FDATEL &TDATEL &CUSTL +
                          &OUTQ &HOLD &SAVE &COPIESL &NIGHTQ +
                          &EXPCD)

        DCL    &COMP      *DEC    (3 0)           /*  COMPANY #     */
        DCL    &COMPL     *DEC                    /*  FOR PASSING   */
        DCL    &FDATE     *DEC    (7 0)          /*  FROM DATE      */
        DCL    &FDATEL    *DEC                   /*  FOR PASSING    */
        DCL    &TDATE     *DEC    (7 0)          /*  THROUGH DATE   */
        DCL    &TDATEL    *DEC                   /*  FOR PASSING    */
        DCL    &COPIES    *DEC    (3 0)           /*  # OF COPIES   */
        DCL    &COPIESL   *DEC                    /*  FOR PASSING   */
        DCL    &CUST      *DEC    (7 0)          /*  A/R CUSTOMER   */
        DCL    &CUSTL     *DEC                   /*  FOR PASSING    */
        DCL    &EXPCD     *CHAR   5              /*  EXPENSE CODE   */
        DCL    &OUTQ      *CHAR   10             /*  OUTPUT QUEUE   */
        DCL    &NIGHTQ    *CHAR   10             /*  NIGHT JOB QUEUE*/
        DCL    &SAVE      *CHAR   4              /*  SAVE SPOOL     */
        DCL    &HOLD      *CHAR   4              /*  HOLD SPOOL     */
        DCL    &PGM       *CHAR   10              /*  PGM NAME      */
        DCL    &COMPA     *CHAR   3               /*  COMPANY ALPHA */
        DCL    &FDATEA    *CHAR   7               /*  FROM DT ALPHA */
        DCL    &TDATEA    *CHAR   7               /*  TO   DT ALPHA */
        DCL    &CUSTA     *CHAR   7               /*  CUSTOMER      */
        DCL    &COMMAND    *CHAR   (512)

/* SEND PROGRAM MESSAGE PARAMETERS  */

        DCL    &USER      *CHAR   10              /*  USER ID       */
        DCL    &MSGRTN    *CHAR   10              /*  MSG QUEUE     */
        DCL    &PGMO      *CHAR   10              /*  PRINTER FILE  */
        DCL     &A        *CHAR   20              /* QRY FROM DATE  */
        DCL     &B        *CHAR   25              /* QRY TO DATE    */
        DCL     &C        *CHAR   25              /* QRY COMPANY    */
        DCL     &D        *CHAR   25              /* QRY EXPORT SLS */
        DCL     &E        *CHAR   25              /* QRY CUSTOMER   */
        DCL     &H        *CHAR  195              /* QRY ALL        */
        DCL     &DATA     *CHAR   10              /* DATA FILES     */
        DCL     &DATAH    *CHAR   10              /* HIST FILES     */

             CHGVAR    VAR(&COMP) VALUE(&COMPL) /* Change to length +
                          for print program */
             CHGVAR    VAR(&COMPA) VALUE(&COMP) /* Change to alpha +
                          for message data */
             CHGVAR    VAR(&FDATE) VALUE(&FDATEL) /* Change to length +
                          for print program */
             CHGVAR    VAR(&FDATEA) VALUE(&FDATE) /* Change to alpha +
                          for query data */
             CHGVAR    VAR(&TDATE) VALUE(&TDATEL) /* Change to length +
                          for print program */
             CHGVAR    VAR(&TDATEA) VALUE(&TDATE) /* Change to alpha +
                          for query data */
             CHGVAR    VAR(&CUST) VALUE(&CUSTL) /* Change to length +
                          for print program */
             CHGVAR    VAR(&CUSTA) VALUE(&CUST) /* Change to alpha +
                          for query data */
             CHGVAR    VAR(&COPIES) VALUE(&COPIESL) /* change to +
                          length for print program */

/********************************************************************/
/* APPEND $ TO THE PROGRAM NAME                                     */
/********************************************************************/


             CHGVAR     VAR(&PGMO) VALUE(&PGM *TCAT '$')

/*********************************************************************/
/*   CREATE OPEN QUERY FILE TO BE USED                               */
/*********************************************************************/

             CHGVAR     VAR(&A) VALUE('ALOBDT *GE ' *CAT +
                         &FDATEA)

             CHGVAR     VAR(&B) VALUE('*AND ALOBDT *LE ' *CAT +
                         &TDATEA)

             CHGVAR     VAR(&C) VALUE('*AND ALAIC3 *EQ ' *CAT +
                         &COMPA)


             IF         COND(&CUSTA *NE '0000000') THEN(DO)
             CHGVAR     VAR(&D) VALUE('*AND ALDUC7 *EQ ' *CAT +
                         &CUSTA)
             ENDDO

             IF         COND(&EXPCD *NE '     ') THEN(DO)
             CHGVAR     VAR(&E) VALUE('*AND ALBGAA *EQ "' *CAT +
                         &EXPCD *CAT '"')
             ENDDO


             CHGVAR     VAR(&H) VALUE(&A *BCAT &B *BCAT &C *BCAT &D +
                                      *BCAT &E)


             OVRDBF     FILE(POALCPL2) TOFILE(POALCPL2) +
                          SHARE(*YES)

             OPNQRYF    FILE((POALCPL2)) OPTION(*ALL) +
                          QRYSLT(&H) KEYFLD(*FILE)

/********************************************************************/
/* Override printer file with user specified values                 */
/********************************************************************/

/*  THIS USED TO SPECIFY DRAWER(2)                                */
             SBDPRTOVR  CMDVAR(&COMMAND) FILE(&PGMO) OUTQ(&OUTQ) +
                          COPIES(&COPIES) HOLD(&HOLD) SAVE(&SAVE)

             CALL       PGM(QCMDEXC) PARM(&COMMAND 512)
/*********************************************************************/
/*   CALL LISTING PROGRAM                                            */
/*********************************************************************/

             CALL       PGM(&PGM) PARM('' &FDATE &TDATE &COMP &EXPCD +
                          &CUST)

             CLOF       OPNID(POALCPL2)
             DLTOVR     FILE(POALCPL2)


 END:        ENDPGM
