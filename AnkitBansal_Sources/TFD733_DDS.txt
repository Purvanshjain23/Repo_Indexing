     A*%%TS  SD  20050317  095814  ISLRAMS     REL-V5R2M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        TRIUMPH FOODS
     A*  DISPLAY FILE:  TFD733
     A*  TITLE:         VIEW INVOICE
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  DATE:          03/10/05
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/TFPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A                                      CF04(04 'PROMPT')
     A                                      CF05(05 'REFRESH')
     A                                      CF13(13 'FOLD/DROP')
     A*
     A*-------------------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*-------------------------------------------------------------------------
     A          R TF733S1                   SFL
     A*%%TS  SD  20050317  095814  ISLRAMS     REL-V5R2M0  5722-WDS
     A  85                                  SFLNXTCHG
     A            HDRRN1    R        H      REFFLD(@@RRN)
     A            D1INFL    R        O 12  3REFFLD(TFREF/@@INFL *LIBL/TFPREF)
     A            D1SMCD    R        O 12  7REFFLD(TFREF/@@SMCD *LIBL/TFPREF)
     A            D1CCCD    R        O 12 12REFFLD(TFREF/@@CCCD *LIBL/TFPREF)
     A            D1ICDTMDY R        O 12 24REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  ')
     A            D1ICAM    R        O 12 33REFFLD(TFREF/@@ICAM *LIBL/TFPREF)
     A                                      EDTCDE(K)
     A            D1UNIT    R        O 12 49REFFLD(TFREF/@@UNIT *LIBL/TFPREF)
     A                                      EDTCDE(K)
     A            D1UCFL    R        O 12 67REFFLD(TFREF/@@UCFL *LIBL/TFPREF)
     A            D1ICSN    R        O 12 71REFFLD(TFREF/@@ICSN *LIBL/TFPREF)
     A                                      EDTCDE(M)
     A                                 13  5'GL Codes-Debit/Credit:'
     A                                      COLOR(PNK)
     A            D1GLDBCD  R        O 13 28REFFLD(TFREF/@@GLDBCD *LIBL/TFPREF)
     A                                      COLOR(PNK)
     A            D1GLCRCD  R        O 13 37REFFLD(TFREF/@@GLCRCD *LIBL/TFPREF)
     A                                      COLOR(PNK)
     A            D1COMTEXT      4A  O 13 48COLOR(PNK)
     A            D1ICCOM   R        O 13 53REFFLD(TFREF/@@ICCOM *LIBL/TFPREF)
     A                                      COLOR(PNK)
     A*
     A*-------------------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*-------------------------------------------------------------------------
     A          R TF733C1                   SFLCTL(TF733S1)
     A*%%TS  SD  20050317  094633  ISLRAMS     REL-V5R2M0  5722-WDS
     A                                      SFLSIZ(0050)
     A                                      SFLPAG(0005)
     A N82                                  ROLLUP(25 'PAGE FORWARD')
     A                                      SETOF(53)
     A                                      SETOF(54)
     A                                      SETOF(55)
     A                                      SETOF(58)
     A                                      RTNCSRLOC(&RCD &FLD)
     A                                      OVERLAY
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A  78                                  SFLFOLD(CF13)
     A  79                                  SFLDROP(CF13)
     A                                      SFLMODE(&FOLD)
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            FOLD           1A  H
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 33'VIEW INVOICE'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USR)
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@JOB)
     A                                      DSPATR(HI)
     A                                  3  2'Invoice number .:'
     A                                      DSPATR(HI)
     A            C1INSN    R        O  3 21REFFLD(TFREF/@@INSN *LIBL/TFPREF)
     A                                      EDTCDE(M)
     A                                  3 32'Frequency:'
     A                                      DSPATR(HI)
     A            C1CFQDS   R        O  3 43REFFLD(TFREF/@@CFQDS *LIBL/TFPREF)
     A                                  3 56'Amounts:'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  4  2'Invoice date ...:'
     A                                      DSPATR(HI)
     A            C1INDTMDY R        O  4 20REFFLD(TFREF/@@DT06 *LIBL/TFPREF)
     A                                      EDTWRD('  /  /  ')
     A                                  4 32'Status ..:'
     A                                      DSPATR(HI)
     A            C1STDS    R        O  4 43REFFLD(TFREF/@@STDS *LIBL/TFPREF)
     A                                  4 56'Invoice:'
     A                                      DSPATR(HI)
     A            C1INAM    R        O  4 65REFFLD(TFREF/@@INAM *LIBL/TFPREF)
     A                                      EDTCDE(K)
     A                                  5  2'Invoice due date:'
     A                                      DSPATR(HI)
     A            C1DUDTMDY R        O  5 20REFFLD(TFREF/@@DT06 *LIBL/TFPREF)
     A                                      EDTWRD('  /  /  ')
     A                                  5 56'Memo ..:'
     A                                      DSPATR(HI)
     A            C1MEMOAM  R        O  5 65REFFLD(TFREF/@@MEMOAM *LIBL/TFPREF)
     A                                      EDTCDE(K)
     A                                  7  3'?'
     A                                      DSPATR(HI)
     A                                  7  8'?'
     A                                      DSPATR(HI)
     A                                  7 14'?'
     A                                      DSPATR(HI)
     A                                  7 67'?'
     A                                      DSPATR(HI)
     A                                  8  2'Inc'
     A                                      DSPATR(HI)
     A                                  8 25'Invoice'
     A                                      DSPATR(HI)
     A                                  8 71'Invoice'
     A                                      DSPATR(HI)
     A                                  9  2'Inv'
     A                                      DSPATR(HI)
     A                                  9  7'Sys'
     A                                      DSPATR(HI)
     A                                  9 12'Charge'
     A                                      DSPATR(HI)
     A                                  9 25'Charge'
     A                                      DSPATR(HI)
     A                                  9 65'User'
     A                                      DSPATR(HI)
     A                                  9 72'Charge'
     A                                      DSPATR(HI)
     A                                 10  2'Tot'
     A                                      DSPATR(HI)
     A                                 10  7'Mod'
     A                                      DSPATR(HI)
     A                                 10 12'Code'
     A                                      DSPATR(HI)
     A                                 10 26'Date'
     A                                      DSPATR(HI)
     A                                 10 41'Amount'
     A                                      DSPATR(HI)
     A                                 10 58'Units'
     A                                      DSPATR(HI)
     A                                 10 65'Cont'
     A                                      DSPATR(HI)
     A                                 10 72'Number'
     A                                      DSPATR(HI)
     A            C1INFL    R        B 11  3REFFLD(TFREF/@@INFL *LIBL/TFPREF)
     A  58                                  DSPATR(PC)
     A                                      DSPATR(HI)
     A            C1SMCD    R        B 11  7REFFLD(TFREF/@@SMCD *LIBL/TFPREF)
     A  55                                  DSPATR(PC)
     A                                      DSPATR(HI)
     A            C1CCCD    R        B 11 12REFFLD(TFREF/@@CCCD *LIBL/TFPREF)
     A  53                                  DSPATR(PC)
     A                                      DSPATR(HI)
     A            C1ICDTMDY R        B 11 24REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  ')
     A                                      DSPATR(HI)
     A            C1UCFL    R        B 11 67REFFLD(TFREF/@@UCFL *LIBL/TFPREF)
     A  54                                  DSPATR(PC)
     A                                      DSPATR(HI)
     A            C1ICSN    R        B 11 71REFFLD(TFREF/@@ICSN *LIBL/TFPREF)
     A                                      DSPATR(HI)
     A                                      EDTCDE(M)
     A*
     A*-------------------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*-------------------------------------------------------------------------
     A          R TF733K1
     A*%%TS  SD  20050310  110614  ISLRAMS     REL-V5R2M0  5722-WDS
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A                                 23 11'F4=Prompt'
     A                                 23 22'F5=Refresh'
     A                                 23 34'F13=Fold/Unfold'
      *
     A*-------------------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
     A*-------------------------------------------------------------------------
     A          R TF733ES                   SFL SFLMSGRCD(24)
      *
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
     A*-------------------------------------------------------------------------
      * ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*-------------------------------------------------------------------------
     A          R TF733EC                   SFLCTL(TF733ES)
      *
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
