     *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Hog Production
      * PROGRAM:     HP7710
      * TITLE:       View Items
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     04/13/04
      *
      *
      * FUNCTION: This is a View that displays the records in the Item Master.
      *
      *           Restrictors:
      *               None
      *
      *           Positioners:
      *               None
      *
      *           Selectors:
      *               Item number
      *               Item description
      *               Active/inactive status
      *               Budget flag
      *               Budget item
      *               Item type code
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 07/19/04  LeAnne Fedor
      *           Per Alice, add a new fucntion key called F21=Print that
      *           will pass the selectors from this program into a pop-up
      *           window program.
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fhpd7710   cf   e             workstn
     F                                     sfile(hp7710s1:hdrrn1)
     F                                     infds(iofeed)
      *
      *
     Fhsp180    if   e           k disk
      *  Item type
      *
      *
     Fhsp181    if   e           k disk
      *  Items
      *
      /EJECT
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
     D set1            c                   'SET1  '
     D scrn1           c                   'SCRN1 '
     D exit            c                   'EXIT  '
      *
     D upper           c                   'ABCDEFGHIJKLMNOPQRSTUVWXYZ'
     D lower           c                   'abcdefghijklmnopqrstuvwxyz'
      *
      ****************************************************************
      * ARRAYS AND TABLES
      ****************************************************************
      *
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Standard fields
      *
     D mode            s              6
     D pgm             s             10
     D msgfl           s             10
     D maxmsg          s              2  0  inz(20)
     D routne          s              6
     D maxrrn          s              4  0
     D reset           s              1    inz('N')
     D norecs          s              1    inz('N')
     D count           s              2  0
     D wkflow          s                   like(idflow)
     D svrrn1          s                   like(hdrrn1)
     D rtncd           s              2
      *
      *
      * Workfields required for subfile paging--see initialization
      * subroutine.
      *
     D wkplin          s              2  0
     D wkrlin          s              2  0
      *
      *
      * Control fields
      *
     D procfl          s              1
      *
     D svitcd          s                   like(c1itcd)
     D svitds          s                   like(c1itds)
     D svbgit          s                   like(c1bgit)
     D svaist          s                   like(c1aist)
     D svittycd        s                   like(c1ittycd)
     D svbgfl          s                   like(c1bgfl)
     D svcritfl        s                   like(c1critfl)
      *
      *
      * Workfields
      *
     D wkdesc1         s                   like(c1bgit)
     D wkbgit          s                   like(c1bgit)
     D long1           s              2  0
      *
     D wkdesc2         s                   like(c1itcd)
     D wkitcd          s                   like(c1itcd)
     D long2           s              2  0
      *
     D wkdesc3         s                   like(c1itds)
     D wkitds          s                   like(c1itds)
     D long3           s              2  0
      *
      *
      * Parms
      *
     D xxitcd          s                   like(c1itcd)
     D xxbgit          s                   like(c1bgit)
      *
     D xxittycd        s                   like(c1ittycd)
     D xxittyds        s             30
      *
     D xxaist          s              1
     D xxbgfl          s                   like(c1bgfl)
     D xxcritfl        s                   like(c1critfl)
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * standard error message handling data structures
      *---------------------------------------------------------------
      *
      * For error message handling, a packed index (3) is required.
      * For program readability, define a corresponding error count
      * field called 'error'
      *
     D                 ds                  inz
     D  error                         2p 0
     D   e                            2p 0 overlay(error)
      *
      * This data structure supplies the name of the message file to
      * the message handling CL program.  The field name MSGFIL must be
      * constant. The value in quotes is the name of the specific
      * message file containing the user defined messages.
      *
     D                 ds                  inz
     D  msgfil                       10    inz('HSMSGF    ')
      *
      *---------------------------------------------------------------
      *  Standard message data structures
      *---------------------------------------------------------------
      *
      * The following 3 data structures are used to speed message
      * handling since it is faster to clear data structures than
      * arrays.  Each is associated with a standard message array.
      *
     D mgi             ds                  inz
     D  mgid                          7    dim(20)
      *
     D mgd             ds                  inz
     D  mgdt                         50    dim(20)
      *
     D mgwk            ds                  inz
     D  mgw                           1    dim(50)
      *
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *---------------------------------------------------------------
      * Standard workstation information data structure
      *---------------------------------------------------------------
      *    externally defined as UTWSFR (record format: UTIDFRR)
     D iofeed        e ds                  extname(utwsfr)
      *
      *
      *---------------------------------------------------------------
      * Standard database file information data structure
      *---------------------------------------------------------------
      *    externally defined as UTDBGR (record format: FDBCKD)
     D dbfeed        e ds                  extname(utdbfr)
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * MAINLINE
      ****************************************************************
      *
     C                   eval      routne = set1
      *
     C     *inlr         doweq     *off                                         Main do loop
     C     routne        caseq     set1          $set1
     C     routne        caseq     scrn1         $scrn1
     C     routne        caseq     exit          $exit
     C                   endcs
     C                   enddo                                                  Main do loop
      /EJECT
      *----------------------------------------------------------------
      * Set environment for screen 1
      *----------------------------------------------------------------
      *
     C     $set1         begsr
      *
      * Clear and fill the subfile
      *
     C                   exsr      $clr1
     C                   exsr      $frsh1
      *
      * Set routine to display screen
      *
     C                   eval      routne = scrn1
      *
     C                   endsr
      /eject
      *----------------------------------------------------------------
      * Perform operator I/O for screen 1
      *----------------------------------------------------------------
      *
     C     $scrn1        begsr
      *
      * Write the error messages from the error arrays to the
      * error message subfile
      *
     C                   exsr      $wrmsg
      *
      * Write screen 1 to crt
      *
     C                   write     hp7710k1
     C                   write     hp7710ec
     C                   exfmt     hp7710c1
      *
      * Save the value of the lowest subfile rrn on the screen for
      * later repositioning of the subfile
      *
     C                   eval      wkflow = idflow
      *
      * Clear the messages from the message data structures and
      * program message queue
      *
     C                   exsr      $clmsg
      *
      * Get user's entry and set next routine
      *
     C                   select
     C                   when      *in03 = *on                                  F3-exit
     C                   eval      routne = exit
      *
     C                   when      *in04 = *on                                  F4-prompt
     C                   exsr      $f4s1
     C                   if        error = 0
     C                   exsr      $chksv
     C                   else
     C                   eval      routne = scrn1
     C                   endif
      *
     C                   when      *in05 = *on                                  F5-refresh
     C                   movel     yes           reset
     C                   eval      routne = set1
      *
     C                   when      *in20 = *on                                  F20-wrksplf
     C                   call      'QEZOUTPT'                                       API
     C                   z-add     wkflow        hdpag1
     C                   eval      routne = scrn1
      *
     C                   when      *in21 = *on                                  F21-print
     C                   call      'HP4708'
     C                   parm      c1itcd        xxitcd
     C                   parm      c1bgit        xxbgit
     C                   parm      c1ittycd      xxittycd
     C                   parm      c1aist        xxaist
     C                   parm      c1bgfl        xxbgfl
     C                   parm      c1critfl      xxcritfl
     C                   eval      routne = scrn1
      *
      *
      * Reposition the subfile if the user has changed any of the positioners
      * or selectors in the control record.
      *
     C                   when      c1itcd <> svitcd or
     C                             c1itds <> svitds or
     C                             c1aist <> svaist or
     C                             c1bgfl <> svbgfl or
     C                             c1critfl <> svcritfl or
     C                             c1ittycd <> svittycd or
     C                             c1bgit <> svbgit
     C                   exsr      $chksv
      *
     C                   when      *in25 = *on                                  F25-ROLLUP
     C                   exsr      $load1
     C                   other
      *
      * User has pressed enter
      *
     C                   eval      routne = scrn1
     C                   endsl
      *
     C                   endsr
      /EJECT
      *----------------------------------------------------------------
      * Save the control record positioners/selectors
      *----------------------------------------------------------------
      *
     C     $chksv        begsr
      *
     C                   movel     no            reset
     C                   eval      routne = set1
      *
     C                   move      c1aist        svaist
     C                   move      c1bgfl        svbgfl
     C                   move      c1critfl      svcritfl
     C                   move      c1ittycd      svittycd
      *
      * Prepare to scan on Budget Item
      *  1) trim leading/trailing blanks
      *  2) find first non-blank position from right
      *
     C                   eval      c1bgit = %trim(c1bgit)
     C     ' '           checkr    c1bgit        long1
     C                   movel     c1bgit        svbgit
      *
      * Prepare to scan on Item Number
      *  1) trim leading/trailing blanks
      *  2) find first non-blank position from right
      *
     C                   eval      c1itcd = %trim(c1itcd)
     C     ' '           checkr    c1itcd        long2
     C                   movel     c1itcd        svitcd
      *
      * Prepare to scan on Item Description
      *  1) trim leading/trailing blanks
      *  2) find first non-blank position from right
      *
     C                   eval      c1itds = %trim(c1itds)
     C     ' '           checkr    c1itds        long3
     C                   movel     c1itds        svitds
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * F4 prompt on screen 1
      *---------------------------------------------------------------
      *
     C     $f4s1         begsr
      *
     C                   select
      * Budget item
     C                   when      fld = 'C1BGIT'
     C                   call      'HP5038'
     C                   parm      *blank        xxbgit
     C                   seton                                        51
     C                   if        xxbgit <> *blank
     C                   move      xxbgit        c1bgit
     C                   endif
      *
      * Item type
     C                   when      fld = 'C1ITTYCD'
     C                   call      'HP5036'
     C                   parm      *blank        xxaist
     C                   parm      *blank        xxittycd
     C                   parm      *blank        xxittyds
     C                   seton                                        52
     C                   if        xxittycd <> *blank
     C                   move      xxittycd      c1ittycd
     C                   endif
      *
      * Active/inactive status
      *
     C                   when      fld = 'C1AIST'
     C                   call      'HP512'
     C                   parm      *blank        xxaist
     C                   seton                                        53
     C                   if        xxaist <> *blank
     C                   movel     xxaist        c1aist
     C                   endif
      *
      * Budget Flag
     C                   when      fld = 'C1BGFL'
     C                   call      'HP522'
     C                   parm                    xxbgfl
     C                   seton                                        54
     C                   if        xxbgfl <> *blank
     C                   movel     xxbgfl        c1bgfl
     C                   endif
      *
      * Critical Flag
     C                   when      fld = 'C1CRITFL'
     C                   call      'HP522'
     C                   parm                    xxcritfl
     C                   seton                                        55
     C                   if        xxcritfl <> *blank
     C                   movel     xxcritfl      c1critfl
     C                   endif
     C                   other
      *
      * F4 not valid for this field...issue message
      *
     C                   if        error < maxmsg
     C                   add       1             error                                        013
     C                   movel     'HS09011'     mgid(e)
     C                   endif
     C                   endsl
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Clear subfile for screen 1
      *---------------------------------------------------------------
      *
     C     $clr1         begsr
      *
     C                   z-add     0             svrrn1
      *
     C                   clear                   hp7710s1
     C                   seton                                        83        sflclr
     C                   write     hp7710c1
     C                   setoff                                       818283
      *                                                    81=sfldsp
      *                                                    82=sflend
      *                                                    83=sflclr
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Refresh/fill the subfile for screen 1
      *---------------------------------------------------------------
      *
     C     $frsh1        begsr
      *
      * Set position in file
      *
     C     *loval        setll     hsp181
      *
      *
      * The field 'reset' is a flag field to indicate whether you are
      * attempting to reposition the user close to where he was in the
      * subfile.
      * The lowest subfile RRN on the screen from the DSPF feedback
      * area (IDFLOW) was retrieved earlier and put in WKFLOW.
      * Add to WKFLOW the number of lines on your subfile page or the
      * number less 1 to determine a subfile RRN to reposition to.
      *
      *
     C                   if        reset = yes
     C                   eval      maxrrn = wkflow + wkrlin
     C                   else
      *
      * Don't try to reposition the user, just start the subfile over
      *
     C                   eval      maxrrn = wkplin
     C                   endif
      *
      * Load the subfile until you have the required number of records.
      * The number will depend on whether uou are repositioning the
      * subfile or not. Stop if you get to the end of the subfile.
      *
     C                   dou       svrrn1 >= maxrrn or *in82 = *on
     C                   exsr      $load1
     C                   enddo
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Load subfile for screen 1
      *---------------------------------------------------------------
      *
     C     $load1        begsr
      *
     C                   z-add     svrrn1        hdrrn1
      *
      * Set the count of records on the subfile page to zero.
      *
     C                   z-add     0             count
      *
      * Fill the subfile until the end of the database file or
      * you have filled all the lines on the subfile page.
      *
     C                   dou       *in82 = *on or count >= wkplin               Do fill
     C                   read      hsp181                                 82
     C                   if        *in82 = *off                                 If read
     C                   move      yes           procfl
      *
     C                   if        imcritfl = *blank
     C                   move      no            d1critfl
     C                   else
     C                   move      imcritfl      d1critfl
     C                   endif
      *
      * Active/Inactive Status
      *
     C                   if        c1aist <> *blank and
     C                             c1aist <> imaist
     C                   move      no            procfl
     C                   endif
      *
      * Budget Flag
     C                   if        c1bgfl <> *blank and
     C                             c1bgfl <> imbgfl
     C                   move      no            procfl
     C                   endif
      *
      * Critical Flag
     C                   if        c1critfl <> *blank and
     C                             c1critfl <> d1critfl
     C                   move      no            procfl
     C                   endif
      *
      * Item type code
     C                   if        c1ittycd <> *blank and
     C                             c1ittycd <> imittycd
     C                   move      no            procfl
     C                   endif
      *
      * Item number
     C                   if        c1itcd <> *blank
     C     lower:upper   xlate     c1itcd        wkdesc2
     C     lower:upper   xlate     imitcd        wkitcd
     C     wkdesc2:long2 scan      wkitcd                                 92
     C                   if        *in92 = *off
     C                   move      no            procfl
     C                   endif
     C                   endif
      *
      *
      * Item description
     C                   if        c1itds <> *blank
     C     lower:upper   xlate     c1itds        wkdesc3
     C     lower:upper   xlate     imitds        wkitds
     C     wkdesc3:long3 scan      wkitds                                 92
     C                   if        *in92 = *off
     C                   move      no            procfl
     C                   endif
     C                   endif
      *
      *
      * Budget Item
     C                   if        c1bgit <> *blank
     C     lower:upper   xlate     c1bgit        wkdesc1
     C     lower:upper   xlate     imbgit        wkbgit
     C     wkdesc1:long1 scan      wkbgit                                 92
     C                   if        *in92 = *off
     C                   move      no            procfl
     C                   endif
     C                   endif
      *
      *
     C                   if        procfl = yes                                 If process
     C                   add       1             count
     C                   add       1             hdrrn1
     C                   move      imitcd        d1itcd
     C                   move      imitds        d1itds
     C                   move      imaist        d1aist
     C                   move      imbgfl        d1bgfl
     C                   move      imittycd      d1ittycd
     C                   move      imbgit        d1bgit
     C                   z-add     imcpuam       d1cpuam
     C                   write     hp7710s1
     C                   seton                                        81
      *
      * For positioning, place the subfile relative record number into
      * the sflrcdnbr field (hdpag1)
      *
     C                   eval      hdpag1 = hdrrn1
      *
     C                   endif                                                  If process
     C                   endif                                                  If read
     C                   enddo                                                  Do fill loop
      *
      * If no records were written to the subfile, then issue
      * the standard message 'No data to Display'.
      *
     C                   if        hdrrn1 = 0                                   If no recs
     C                   movel     yes           norecs
      *
     C                   if        error < maxmsg
     C                   add       1             error
     C                   eval      mgid(e) = 'HS09002'
     C                   endif
      *
     C                   else
     C                   movel     no            norecs
     C                   endif                                                  If no recs
      *
     C                   z-add     hdrrn1        svrrn1
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Seton last record and end job
      *---------------------------------------------------------------
      *
     C     $exit         begsr
     C                   seton                                        LR
     C                   endsr
      *
      /EJECT
      *---------------------------------------------------------------
      * Write error messages
      *---------------------------------------------------------------
      *
     C     $wrmsg        begsr
      *
     C                   call      'UT80045J'                           43
     C                   parm                    mgid
     C                   parm                    mgdt
     C                   parm                    error
     C                   parm      sdpgm         pgm
     C                   parm      msgfil        msgfl
      *
      * If call to UT80045J failed, a message hardcoded in the DSPF
      * command line format will be set on. So, redisplay screen.
      *
     C                   if        *in43 = *on
     C                   eval      routne = set1
     C                   endif
      *
     C                   endsr
      *
      *----------------------------------------------------------------
      * Clear messages
      *----------------------------------------------------------------
      *
     C     $clmsg        begsr
      *
     C                   call      'UT80045J'                           43
     C                   parm      *blank        mgi
     C                   parm      *blank        mgd
     C                   parm      *zero         error
     C                   parm      sdpgm         pgm
     C                   parm      msgfil        msgfl
      *
      * If call to UT80045J failed, a message hardcoded in the DSPF
      * command line format will be set on; so, redisplay screen.
      *
     C                   if        *in43 = *on
     C                   eval      routne = set1
     C                   endif
      *
     C                   endsr
      /EJECT
      *----------------------------------------------------------------
      * Initialization subroutine
      *----------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * To support F5-Refresh, logic requires the number of lines on
      * the subfile page and also the number of lines less 1.  By
      * setting these values up as constant work fields, the processing
      * logic can be standardized between programs.
      *
     C                   z-add     6             wkplin                         sflpag
     C     wkplin        sub       1             wkrlin                         sflpag - 1
      *
      *
      * The following standard code must be included to make the
      * standard error message handling program function properly.
      * This code sets and clears the program message queue.
      *
     C                   movel     '*'           msgpgm
     C                   exsr      $clmsg
      *
     C                   endsr
      /eject
