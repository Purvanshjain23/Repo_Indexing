     A*%%TS  SD  20140120  103813  ISLRAMS     REL-V6R1M0  5761-WDS
     A*****************************************************************
     A*
     A*  ENVIRONMENT:   PORK DIVISION
     A*  SYSTEM:        RESOURCE OPTIMIZATION
     A*  DISPLAY FILE:  ROD701
     A*  TITLE:         VIEW RESOURCE OPTIMIZATION HEADER
     A*  PROGRAMMER:    LEANNE RAMSEY
     A*  DATE:          01/16/06
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/ROPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CF03(03 'EXIT')
     A                                      CF05(05 'REFRESH')
     A*
     A*-----------------------------------------------------------------
     A* RECORD FORMAT ONE
     A*-----------------------------------------------------------------
     A          R RO701R1
     A*%%TS  SD  20140120  103813  ISLRAMS     REL-V6R1M0  5761-WDS
     A                                      OVERLAY
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 24'RESOURCE OPTIMIZATION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USR)
     A                                      DSPATR(HI)
     A                                  2 21'VIEW RESOURCE OPTIMIZATION HEADER'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@JOB)
     A                                      DSPATR(HI)
     A                                  4 11'Company:'
     A            D1CONO    R        O  4 20REFFLD(OHREC/OHCONO *LIBL/ROP100)
     A                                      DSPATR(HI)
     A            D1CONM    R        O  4 29REFFLD(@ABREAI/ABADTX *LIBL/CAABREP)
     A                                  4 62'Status:'
     A            D1RCSTCD  R        O  4 70REFFLD(OHREC/OHRCSTCD *LIBL/ROP100)
     A                                      DSPATR(HI)
     A                                  5  2'Week-ending date:'
     A            D1WEMDY   R        O  5 20REFFLD(ROREF/@@DT06 *LIBL/ROPREF)
     A                                      EDTWRD('  /  /  ')
     A                                      DSPATR(HI)
     A                                  7  2'BI credit value:'
     A            D1BICRVL  R        O  7 35REFFLD(OHREC/OHBICRVL *LIBL/ROP100)
     A                                      EDTCDE(K)
     A                                      DSPATR(HI)
     A                                  7 48'Sirloin Component Allotment %'
     A                                      DSPATR(UL)
     A                                      COLOR(BLU)
     A                                  8  2'Smoker yield % from BI picnic:'
     A            D1SMOKYPC R        O  8 35REFFLD(OHREC/OHBITRPC *LIBL/ROP100)
     A                                      EDTCDE(K)
     A                                      DSPATR(HI)
     A                                  8 48'Bone-in:'
     A            D1BICAPC  R        O  8 65REFFLD(OHREC/OHBICAPC *LIBL/ROP100)
     A                                      EDTCDE(K)
     A                                      DSPATR(HI)
     A                                  9  2'Sirloin trim 100% BI loin:'
     A            D1BITRPC  R        O  9 35REFFLD(OHREC/OHBITRPC *LIBL/ROP100)
     A                                      EDTCDE(K)
     A                                      DSPATR(HI)
     A                                  9 48'Boneless:'
     A            D1BLCAPC  R        O  9 65REFFLD(OHREC/OHBLCAPC *LIBL/ROP100)
     A                                      EDTCDE(K)
     A                                      DSPATR(HI)
     A                                 10  2'Sirloin trim allocation %:'
     A            D1ALTRPC  R        O 10 35REFFLD(OHREC/OHALTRPC *LIBL/ROP100)
     A                                      EDTCDE(K)
     A                                      DSPATR(HI)
     A                                 10 48'Sirloin ByProduct Yield %    '
     A                                      DSPATR(UL)
     A                                      COLOR(BLU)
     A                                 11 48'Bone-in:'
     A            D1BIBYPC  R        O 11 65REFFLD(OHREC/OHBIBYPC *LIBL/ROP100)
     A                                      EDTCDE(K)
     A                                      DSPATR(HI)
     A*
     A                                 12  2'Cap&Blade Allocation %       '
     A                                      DSPATR(UL)
     A                                      COLOR(BLU)
     A                                 12 48'Boneless:'
     A            D1BLBYPC  R        O 12 65REFFLD(OHREC/OHBLBYPC *LIBL/ROP100)
     A                                      DSPATR(HI)
     A                                      EDTCDE(K)
     A                                 13  2'Regular trim:'
     A            D1CBRTPC  R        O 13 19REFFLD(OHREC/OHCBRTPC *LIBL/ROP100)
     A                                      EDTCDE(K)
     A                                      DSPATR(HI)
     A                                 14  2'Special trim:'
     A            D1CBSTPC  R        O 14 19REFFLD(OHREC/OHCBSTPC *LIBL/ROP100)
     A                                      EDTCDE(K)
     A                                      DSPATR(HI)
     A*
     A                                 14 44'Skirt Meat Allocation   SBF   TF '
     A                                      DSPATR(UL)
     A                                      COLOR(BLU)
     A                                 15 50'Small commodity'
     A            D1SSCFL   R        O 15 69REFFLD(ROREF/@@SSCFL *LIBL/ROPREF)
     A                                      DSPATR(HI)
     A            D1TSCFL   R        O 15 74REFFLD(ROREF/@@TSCFL *LIBL/ROPREF)
     A                                      DSPATR(HI)
     A                                 16  2'Cap&Blade Yield %                 -
     A                                             '
     A                                      DSPATR(UL)
     A                                      COLOR(BLU)
     A                                 16 49'Medium commodity'
     A            D1SMCFL   R        O 16 69REFFLD(ROREF/@@SMCFL *LIBL/ROPREF)
     A                                      DSPATR(HI)
     A            D1TMCFL   R        O 16 74REFFLD(ROREF/@@TMCFL *LIBL/ROPREF)
     A                                      DSPATR(HI)
     A                                 17  6'Items:'
     A                                 17 14'See Listed Items'
     A                                      DSPATR(UL)
     A                                      COLOR(BLU)
     A                                 17 32'83356/89998'
     A                                      DSPATR(UL)
     A                                      COLOR(BLU)
     A*
     A                                 17 48'Converted Brisket'
     A            D1SCBFL   R        O 17 69REFFLD(ROREF/@@SCBFL *LIBL/ROPREF)
     A                                      DSPATR(HI)
     A            D1TCBFL   R        O 17 74REFFLD(ROREF/@@TCBFL *LIBL/ROPREF)
     A                                      DSPATR(HI)
     A                                 18  4'Cap Meat'
     A            D1CBCM1PC R        O 18 18REFFLD(OHREC/OHCBRTPC *LIBL/ROP100)
     A                                      EDTCDE(K)
     A                                      DSPATR(HI)
     A            D1CBCM2PC R        O 18 34REFFLD(OHREC/OHCBRTPC *LIBL/ROP100)
     A                                      EDTCDE(K)
     A                                      DSPATR(HI)
     A                                 19  2'Blade Meat'
     A            D1CBBM1PC R        O 19 18REFFLD(OHREC/OHCBRTPC *LIBL/ROP100)
     A                                      EDTCDE(K)
     A                                      DSPATR(HI)
     A            D1CBBM2PC R        O 19 34REFFLD(OHREC/OHCBRTPC *LIBL/ROP100)
     A                                      EDTCDE(K)
     A                                      DSPATR(HI)
     A                                 19 47'Cap&Blade Yield % Listed Items:'
     A                                      COLOR(BLU)
     A                                      DSPATR(UL)
     A                                 20  4'Reg Trim'
     A            D1CBRT1PC R        O 20 18REFFLD(OHREC/OHCBRTPC *LIBL/ROP100)
     A                                      EDTCDE(K)
     A                                      DSPATR(HI)
     A            D1CBRT2PC R        O 20 34REFFLD(OHREC/OHCBRTPC *LIBL/ROP100)
     A                                      EDTCDE(K)
     A                                      DSPATR(HI)
     A                                 20 47'951 953 955 83354 83357 89990'
     A                                 21  4'Cut Loss'
     A            D1CBCL1PC R        O 21 18REFFLD(OHREC/OHCBRTPC *LIBL/ROP100)
     A                                      EDTCDE(K)
     A                                      DSPATR(HI)
     A            D1CBCL2PC R        O 21 34REFFLD(OHREC/OHCBRTPC *LIBL/ROP100)
     A                                      EDTCDE(K)
     A                                      DSPATR(HI)
     A                                 21 47'89995 89996 89997 89999'
     A*-----------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*-----------------------------------------------------------------
     A          R RO701K1
     A*%%TS  SD  20040706  113301  ISLRAMS     REL-V5R2M0  5722-WDS
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A                                 23 11'F5=Refresh'
      *
      *------------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *-----------------------------------------------------------------
     A          R RO701ES                   SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
      *------------------------------------------------------------------
      * ERROR MESSAGE SUBFILE CONTROL FORMAT
      *-----------------------------------------------------------------
     A          R RO701EC                   SFLCTL(RO701ES)
      *
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
