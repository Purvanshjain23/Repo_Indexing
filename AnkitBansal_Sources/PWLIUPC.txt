/*********************************************************************/
/*                                                                   */
/*  PROGRAM NUMBER:  PWLIUPC                                         */
/*  PROGRAM NAME:    COPY TATTOO DIFF FILE TO NEW LAYOUT             */
/*  PROGRAMMER:      BRAD BADEN - H16853-BUY ORDER# FIELD INCREASE   */
/*  CREATE DATE:      4/05/2021                                      */
/*                                                                   */
/*  This program will be used to create and copy records in the      */
/*  PKA1CPPPS and PKDNCPPPS files.  The Buy Order Number in these    */
/*  files has changed from a 5-digit number to a 7-digit number.     */
/*                                                                   */
/*                                                                   */
/*********************************************************************/
/*  Modification History                                             */
/*                                                                   */
/*********************************************************************/
             PGM        PARM(&LIBRARY)

             DCL        VAR(&LIBRARY)  TYPE(*CHAR) LEN(10)

             DCL        VAR(&PSFILE)   TYPE(*CHAR) LEN(10)
             DCL        VAR(&PRODFILE) TYPE(*CHAR) LEN(10)
             DCL        VAR(&SAVEFILE) TYPE(*CHAR) LEN(10)

/* Process file PKA1CPPPS                                            */
             CHGVAR     VAR(&PRODFILE) VALUE('PKA1CPP')
             CHGVAR     VAR(&PSFILE)   VALUE('PKA1CPPPS')
             CHGVAR     VAR(&SAVEFILE) VALUE('PKA1CPPPST')

             CALLSUBR   SUBR(PROCESS)

/* Process file PKDNCPPPS                                            */
             CHGVAR     VAR(&PRODFILE) VALUE('PKDNCPP')
             CHGVAR     VAR(&PSFILE)   VALUE('PKDNCPPPS')
             CHGVAR     VAR(&SAVEFILE) VALUE('PKDNCPPPST')

             CALLSUBR   SUBR(PROCESS)

/*********************************************************************/
/** Subroutine PROCESS - Create new version of files                **/
/*********************************************************************/
             SUBR       SUBR(PROCESS)

/* Delete file in library PRKFSAV                                    */
             DLTF       FILE(PRKFSAV/&SAVEFILE)
             MONMSG     MSGID(CPF0000)

/* Copy file records to temporary file in PRKFSAV library            */
             CPYF       FROMFILE(&LIBRARY/&PSFILE) +
                          TOFILE(PRKFSAV/&SAVEFILE) MBROPT(*ADD) +
                          CRTFILE(*YES)

/* Delete production version of file                                 */
             DLTF       FILE(&LIBRARY/&PSFILE)

/* Create duplicate of Production file with "PS" on the end          */
             CRTDUPOBJ  OBJ(&PRODFILE) FROMLIB(&LIBRARY) +
                          OBJTYPE(*FILE) NEWOBJ(&PSFILE)

/* Copy saved records from PRKFSAV to production library             */
             CPYF       FROMFILE(PRKFSAV/&SAVEFILE) +
                          TOFILE(&LIBRARY/&PSFILE) MBROPT(*ADD) +
                          CRTFILE(*YES) FMTOPT(*MAP *DROP)

             ENDSUBR
/*********************************************************************/

             ENDPGM
