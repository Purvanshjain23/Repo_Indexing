/********************************************************************/
/*                                                                   */
/*  SYNOPSIS -  SUBMIT PRINT ACCRUAL YTD SUMMARY FOR BROKER          */
/*                                                                   */
/*  SYSTEM    - ORDER MANAGEMENT SYSTEM                              */
/*  DATE      - 06/25/2007                                           */
/*                                                                   */
/*      PARAMETERS:                                                  */
/*                                                                   */
/*     I    &APPLIC     - ACCRUAL TYPE (REQUIERED)                   */
/*     I    &OUTQ       - OUTPUT QUEUE                               */
/*     I    &HOLD       - HOLD STATUS                                */
/*     I    &SAVE       - SAVE STATUS                                */
/*     I    &NQSTS      - NIGHT QUEUE STATUS                         */
/*     I    &NQ         - NIGHT QUEUE                                */
/*     I    &COPYN      - NO. OF COPIES (PASSED)                     */
/*     I    &EMAILBRKR  - EMAIL BROKER Y/N                           */
/*     I    &BROKER     - BROKER CODE (OPTIONAL)                     */
/*     I    &BRKRTYPE   - BROKER TYPE (BROKER OR BROKER GROUP)       */
/*     I    &SLSPRSN    - SALESPERSON                                */
/*     I    &RECSTS     - RECORD STATUS                              */
/*     I    &EMAIL      - EMAIL ADDRESS                              */
/*     I    &ACRLCODE   - ACCRUAL CODE                               */
/*     I    &DTLSUM     - DETAIL OR SUMMARY RPT                      */
/*                                                                   */
/*********************************************************************/
 PMJIUPC:    PGM        PARM(&APPLIC &OUTQ &HOLD &SAVE &NQSYS &NQ +
                  &COPYN &DTLSUM &EMAILBRKR &BROKER &BRKRTYPE +
                          &SLSPRSN &RECSTS &EMAIL &ACCRCODE)
/* PARMS                                                             */
        DCL    &APPLIC     *CHAR   (10)          /* APPLICATION CODE */
        DCL    &OUTQ       *CHAR   (10)          /* Output Queue     */
        DCL    &HOLD       *CHAR   (4)           /* Hold Status      */
        DCL    &SAVE       *CHAR   (4)           /* Save Status      */
        DCL    &NQSYS      *CHAR   (1)           /* NQ RUN STATUS    */
        DCL    &NQ         *CHAR   (7)           /* NIGHT QUEUE      */
        DCL    &COPYN      *DEC                  /* # COPIES PASS    */
        DCL    &COPIES     *DEC    (2 0)         /* # COPIES         */
        DCL    &EMAILBRKR  *CHAR   (2)           /* EMAIL BRKR Y/N   */
        DCL    &BRKRTYPE   *CHAR   (1)           /* BROKER TYPE      */
        DCL    &SLSPRSN    *CHAR   (5)           /* SALESPERSON      */
        DCL    &RECSTS     *CHAR   (1)           /* RECORD STATUS    */
        DCL    &EMAIL      *CHAR   (50)          /* EMAIL ADDRESS    */
        DCL    &BROKER     *CHAR   (6)           /* BROKER CODE      */
        DCL    &ACCRCODE   *CHAR   (3)           /* ACCRUAL CODE     */
        DCL    &DTLSUM     *CHAR   (1)           /* DTL/SUMM         */
        DCL    &USRDATA    *CHAR   (10)          /* USER DATA        */
        DCL    &DATE       *CHAR   (6)           /* JOB DATE         */
        DCL    &NBR        *CHAR   (6)           /* JOB NUMBER       */
        DCL    &JOB        *CHAR   (10)          /* JOB              */
        DCL    &USER       *CHAR   (10)          /* USER PROFILE     */

/* ERROR MESSAGE FIELDS                                              */
        DCL    &MSG        *CHAR   (512)         /* message          */
        DCL    &MSGID      *CHAR   (7)           /* message id       */
        DCL    &MSGL       *DEC    (5 0)

/*********************************************************************/
/*   MONITOR FOR ALL MESSAGES AT PROGRAM LEVEL                       */
/*********************************************************************/
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(MSGTRAP))

/*********************************************************************/
/* MAKE THE USER DATA FIELD UNIQUE BY CONCATONATING THE BROKER       */
/* CODE WITH THE LAST 4 DIGITS OF THE JOB NUMBER                     */
/*********************************************************************/
             RTVJOBA    JOB(&JOB) USER(&USER) NBR(&NBR) DATE(&DATE)
             CHGVAR     VAR(&USRDATA) VALUE(&BROKER *TCAT (%SST(&NBR +
                          3 4)))

/********************************************************************/
/* IF THE REPORT(S) ARE BEING EMAILED THEN DEFAULT THE PRINTER FILE */
/* TO HOLD AND SAVE = *YES.                                         */
/********************************************************************/
             IF         COND(&EMAIL *NE '               ') THEN(DO)
             CHGVAR     VAR(&HOLD) VALUE(*YES)
             CHGVAR     VAR(&SAVE) VALUE(*YES)
             ENDDO

             CHGVAR     VAR(&COPIES) VALUE(&COPYN)
             OVRPRTF    FILE(PMJEPFR$) OUTQ(&OUTQ) COPIES(&COPIES) +
                          HOLD(&HOLD) SAVE(&SAVE) USRDTA(&USRDATA)

             CALL       PGM(PMJEPFR) PARM(' ' &BROKER &BRKRTYPE +
                          &SLSPRSN &RECSTS &ACCRCODE &DTLSUM)

             IF         COND(&EMAIL *NE '               ') THEN(DO)
             ESNDMAIL   RECIPIENT(&EMAIL) SUBJECT(&USRDATA) +
                          MSG('Broker Exhibits for ' *BCAT &BROKER +
                          *BCAT &DATE) MSGTYPE(*TEXT) ATTLIST((* +
                          *PDF *N PMJEPFR$ &NBR/&USER/&JOB *LAST))
             ENDDO

             GOTO       CMDLBL(END)

/*----------------------RECEIVE MESSAGES----------------------------*/
 MSGTRAP:    RCVMSG     MSGTYPE(*EXCP) MSGDTA(&MSG) MSGDTALEN(&MSGL) +
                          MSGID(&MSGID)
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(END))

             SNDPGMMSG  MSGID(&MSGID) MSGF(QCPFMSG) MSGDTA(%SST(&MSG +
                          1 &MSGL)) MSGTYPE(*ESCAPE)
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(END))

 END:        ENDPGM
