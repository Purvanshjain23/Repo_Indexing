      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Triumph Foods
      * PROGRAM:     TF665 - Eopfees: Unpaid Loads Listing
      * PROGRAMMER:  LeAnne Ramsey
      * CREATED:     04/03/09
      *
      * FUNCTION: This report is almost exactly like:
      *              TF223-Verfication for Variance-Unpaid Loads
      *              The users wanted us to make TF223 "on-demand".
      *              We could not make TF223 "on-demand" since it used frozen period files.
      *              So, we created this very similar "Unpaid Loads Listing" as requestd
      *              by Gary Martin. He and Tom Dye will use it to keep the loads
      *              "cleaned up" so that EOP will go more smoothly.
      *
      ****************************************************************************************
      * MODIFICATIONS:
      ****************************************************************************************
      * Date:     PROGRAMMER
      * ***************************************************************************************
      * 01/21/21  ISE            TSN593
      *           Replaced *loval with *start
      *
      /eject
      ****************************************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************************************
      *
     Fomfjcpmc  if   e           k disk
      *    Load header  (records keyed on CL)
      *
      *
     Fomhstpn8  if   e           k disk
      *    Sales history
      *
      *
     Fprint132  o    f  132        printer oflind(*inof)
      *
      /eject
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      ****************************************************************
      * ARRAYS AND TABLES
      ****************************************************************
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Standard fields
      *
     D dash97          s             97    inz(*all'-')
     d rtime           s              6  0
      *
      * Control fields
      *
     d first           s              1    inz('Y')
     d procfl          s              1    inz('Y')
     d svaic3          s                   like(fjaic3)
      *
      *
      * Date manipulation
      *
     D wkisodate       s               d   datfmt(*iso)
      *
      *
      * Print fields
      *
     d h1edds          s             10
      *
     d l1edfl          s              1
     d l1asdtmdy       s              6  0
     d l1absfam        s                   like(f4ulva)
      *
     d t1absfam        s                   like(f4ulva)
     d t1actfam        s                   like(f4ulva)
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *---------------------------------------------------------------
      * Local data area.
      *---------------------------------------------------------------
      *
     Dlda             uds                  dtaara(*lda)
      *
     D  lddt                  29     36  0
     D  ldsyndt               37     43  0
     D  ldmdy                 44     49  0
      *
     D  ldedfl               349    349
     D  ldcarcd              350    352
     D  ldcarnm              353    382
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      **********************************************************************************
      * Mainline
      **********************************************************************************
      *
      * Print headings.
     C                   exsr      $h1hdr
      *
      * Process Load Header records where:
      *       1) Actual Ship Date is on/before user's Date
      *       2) Are Prepaid (have a 'P' in the first position of Shipping Method)
      *       3) Carrier Payment Status is blank
      *       4) Load Status is I=Invoiced
      *
593  C*    *loval        setll     omfjcpmc
593  C     *start        setll     omfjcpmc
      *
     C                   dou       *in90 = *on                                  Main do
     C                   read      omfjcpmc                               90
     C                   if        *in90 = *off and                             If not EOF
     C                             fjf9dt <= ldsyndt and
     C                             %subst(fjq2cd:1:1) = 'P' and
     C                             fjrnst = ' ' and
     C                             fjpkst = 'I'
      *
     C                   move      yes           procfl
      *
      * Check user's selection of Export/Domestic/Both
      *
     C                   if        ldedfl = 'B' or
     C                             (ldedfl = 'E' and fju2st = yes) or
     C                             (ldedfl = 'D' and fju2st = no)
     C                   else
     C                   move      no            procfl
     C                   endif
      *
      * Check user's selection of Carrier Code
      *
     C                   if        ldcarcd = *blank or ldcarcd = fjbzna
     C                   else
     C                   move      no            procfl
     C                   endif
      *
      * If processing this Load
      *
     C                   if        procfl = yes                                 If process
      *
     C                   if        first = yes
     C                   move      no            first
     C                   z-add     fjaic3        svaic3
     C                   endif
      *
      * Accumulate Actual Freight Amount
      *
     C                   add       fjzbva        t1actfam
      *
      * Retrieve Absorbed Freight for this Load Header
      * Then, print the load header line
      *
     C                   exsr      $absfam
     C                   exsr      $l1dtl
      *
     C                   endif                                                  If process
     C                   endif                                                  If not EOF
     C                   enddo                                                  Main do
      *
      * EOF processing
     C                   if        first = yes
     C                   except    nodata
     C                   else
     C                   exsr      $t1tot
     C                   endif
      *
     C                   seton                                        lr
      /eject
      *-------------------------------------------------------------------
      * Retrieve Absorbed Freight for a Load from the Sales History file
      *-------------------------------------------------------------------
      *
     C     $absfam       begsr
      *
     C     fjrmnb        setll     omhstpn8
      *
     C                   dou       *in91 = *on                                  Do frt
     C     fjrmnb        reade     omhstpn8                               91
     C                   if        *in91 = *off                                 If not EOF
     C                   add       f4ulva        l1absfam
     C                   add       f4ulva        t1absfam
     C                   endif                                                  If not EOF
     C                   enddo                                                  Do frt
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Print record
      *---------------------------------------------------------------
      *
     C     $l1dtl        begsr
      *
      * Get Actual Ship Date into MMDDYY format for printing
      *
     C                   z-add     0             l1asdtmdy
     C                   if        fjf9dt <> 0                                  If date
     C     *cymd         test(d)                 fjf9dt                 92
     C                   if        *in92 = *off                                 If OK date
     C     *cymd         move      fjf9dt        wkisodate
     C     *mdy          move      wkisodate     l1asdtmdy
     C                   endif                                                  If OK date
     C                   endif                                                  If date
      *
      * Set up E=Export, D=Domestic based on Export Flag
      *
     C                   select
     C                   when      fju2st = yes
     C                   eval      l1edfl = 'E'
     C                   when      fju2st = no
     C                   eval      l1edfl = 'D'
     C                   endsl
      *
      * Print detail line
      *
     C                   if        *inof = *on
     C                   exsr      $h1hdr
     C                   endif
      *
     C                   if        fjaic3 <> svaic3
     C                   except    blank
     C                   z-add     fjaic3        svaic3
     C                   endif
      *
     C                   except    l1dtl
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Print report headings
      *---------------------------------------------------------------
      *
     C     $h1hdr        begsr
      *
     C                   except    h1hdr
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Print report total
      *---------------------------------------------------------------
      *
     C     $t1tot        begsr
      *
     C                   if        *inof = *on
     C                   exsr      $h1hdr
     C                   endif
      *
     C                   except    t1tot
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Set up User's selections in the Heading
      *
     C                   time                    rtime
      *
      * Export/Domestic
     C                   select
     C                   when      ldedfl = 'B'
     C                   eval      h1edds = 'Both'
      *
     C                   when      ldedfl = 'E'
     C                   eval      h1edds = 'Export'
      *
     C                   when      ldedfl = 'D'
     C                   eval      h1edds = 'Domestic'
     C                   endsl
      *
      * Carrier
     C                   if        ldcarcd = *blank
     C                   seton                                        88
     C                   endif
      *
     C                   endsr
      /eject
      *-------------------------------------------------------------
      * Report headings
      *-------------------------------------------------------------
      *
     Oprint132  e            h1hdr          1 01
     O                       sdpgm               10
     O                                           52 'UNPAID LOADS LISTING'
     O                                           87 'DATE'
     O                       udate         y     97
      *
     O          e            h1hdr          1
     O                       sdusr               10
     O                                           87 'TIME'
     O                       rtime               97 '  :  :  '
      *
      *
     O          e            h1hdr          2
     O                                           87 'PAGE'
     O                       page          z     97
      *
     O          e            h1hdr          1
     O                                           26 'Actual ship date on/before'
     O                                           27 ':'
     O                       ldmdy               37 '  /  /  '
      *
     O          e            h1hdr          1
     O                                           27 'Export/Domestic/Both:'
     O                       h1edds              39
      *
     O          e            h1hdr          2
     O                                           27 'Carrier Code:'
     O               88                          32 'All'
     O              n88      ldcarcd             32
     O              n88      ldcarnm             69
      *
      *
      *-------------------------------------------------------------
      * Column headings
      *-------------------------------------------------------------
      *
     O          e            h1hdr       0  1
     O                                           60 'Absorbed'
     O                                           77 'Actual'
      *
     O          e            h1hdr       0  1
     O                                            7 'Company'
     O                                           16 'Carrier'
     O                                           22 'Load'
     O                                           35 'Actual'
     O                                           45 'Export/'
     O                                           60 'Freight'
     O                                           77 'Freight'
      *
      *
     O          e            h1hdr       0  1
     O                                            6 'Number'
     O                                           14 'Code'
     O                                           20 'ID'
     O                                           35 'Ship Date'
     O                                           45 'Domestic'
     O                                           60 'Amount'
     O                                           77 'Amount'
      *
      *
     O          e            h1hdr          0
     O                       dash97
      *
      *-------------------------------------------------------------
      * Detail line
      *-------------------------------------------------------------
      *
     O          e            l1dtl       1
     O                       fjaic3        z      5
     O                       fjbzna              13
     O                       fjrmnb        z     24
     O                       l1asdtmdy      b    35 '  /  /  '
     O                       l1edfl         b    42
     O                       l1absfam      kb    61
     O                       fjzbva        kb    78
      *
      *-------------------------------------------------------------
      * Total line
      *-------------------------------------------------------------
      *
     O          e            t1tot       1
     O                                           60 '--------------'
     O                                           77 '--------------'
      *
     O          e            t1tot       1
     O                                           42 'Total:'
     O                       t1absfam      kb    61
     O                       t1actfam      kb    78
      *
      *-------------------------------------------------------------
      * Blank line
      *-------------------------------------------------------------
      *
     O          e            blank       1
      *
      *-------------------------------------------------------------
      * No data message line
      *-------------------------------------------------------------
      *
     O          e            nodata      1
     O                                           18 'No data meets your'
     O                                           38 'selection criteria.'
      /eject
