/***************************************************************** */
/*                                                                 */
/*  PROGRAM NUMBER:  HP474CL                                       */
/*  PROGRAM NAME:    FARM COST LISTINGS                            */
/*  PROGRAMMER:      LEANNE FEDOR                                  */
/*  CREATE DATE:     06/30/94                                      */
/*                                                                 */
/*  FUNCTION:        THIS CL IS SUBMITTED WITH QCMDEXC FROM        */
/*                   HP474-SPECIFY FARM COST LISTING OPTIONS.      */
/*                   USER OPTIONS ARE PASSED THROUGH THE LDA.      */
/*                   AN OPNQRY STATEMENT IS USED TO SELECT         */
/*                   RECORDS.                                      */
/*                                                                 */
/*******************************************************************/
/* MODIFICATIONS                                                   */
/*                                                                 */
/* 07/25/94  R. CENTONZE                                           */
/*           ADDED OVRPRTF PARMS HOLD, OUTQ, COPY.                 */
/*                                                                 */
/* 11/07/94  LEANNE FEDOR                                          */
/*           RECOMPILE ONLY. A FIELD WAS ADDED TO HSP028-FARM COST */
/*                                                                 */
/*  04/23/96  LEANNE FEDOR                                         */
/*            ADDED CUSTOMIZED PRINTER OVERRIDE COMMAND TO FORCE   */
/*            REPORT TO PRINT ON LASER PRINTER.                    */
/*                                                                 */
/*  06/19/97  LEANNE FEDOR                                         */
/*            CHANGED OUT QUEUE LOGIC.                             */
/*******************************************************************/

             PGM

/* DEFINE THE VARIABLE REQUIRED IN THE COMMAND TO OVERRIDE THE     */
/* PRINT FILE TO A LASER PRINTER                                   */

             DCL        VAR(&CMD) TYPE(*CHAR) LEN(512)


             DCL        VAR(&QDATA) TYPE(*CHAR) LEN(100)

             DCL        VAR(&LDFSBO) TYPE(*CHAR) LEN(5)
             DCL        VAR(&LDSQ) TYPE(*CHAR) LEN(1)
             DCL        VAR(&LDEFDT) TYPE(*DEC) LEN(8)
             DCL        VAR(&LDFSCD) TYPE(*DEC) LEN(5 0)

             DCL        VAR(&HOLD) TYPE(*CHAR) LEN(10)
             DCL        VAR(&COPYS) TYPE(*DEC) LEN(2)
             DCL        VAR(&OUTQ) TYPE(*CHAR) LEN(10)

             CHGVAR     VAR(&LDFSBO) VALUE(%SST(*LDA 1 5))
             CHGVAR     VAR(&LDSQ) VALUE(%SST(*LDA 6 1))
             CHGVAR     VAR(&LDEFDT) VALUE(%SST(*LDA 7 8))
             CHGVAR     VAR(&LDFSCD) VALUE(%SST(*LDA 15 5))

/*  SELECT FARM COST RECORDS FOR THE SELECTED OFFICE */

             CHGVAR     VAR(%SST(&QDATA 1 12)) VALUE('FCFSBO *EQ "')
             CHGVAR     VAR(%SST(&QDATA 13 5)) VALUE(&LDFSBO)
             CHGVAR     VAR(%SST(&QDATA 18 1)) VALUE('"')


/*  IF EFFECTIVE DATE IS NOT ZERO, THEN ADD A DATE SELECTION     */

             IF         COND(&LDEFDT *NE 00000000) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 20 15)) VALUE('*AND FCEFDT +
                          *EQ')
             CHGVAR     VAR(%SST(&QDATA 36 8)) VALUE(&LDEFDT)
             ENDDO


/*  IF FARM SITE IS NOT BLANK, THEN ADD A FARM SELECTION     */

             IF         COND(&LDFSCD *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 45 15)) VALUE('*AND FCFSCD +
                          *EQ')
             CHGVAR     VAR(%SST(&QDATA 62 5)) VALUE(&LDFSCD)
             ENDDO

/* SET PARAMETERS FROM LDA                         */

 HOLD:       IF         COND(%SST(*LDA 411 1) = 'Y') THEN(CHGVAR +
                          VAR(&HOLD) VALUE('*YES      '))
             ELSE       CMD(CHGVAR VAR(&HOLD) VALUE('*NO       '))

COPYS:       CHGVAR     VAR(&COPYS) VALUE(%SST(*LDA 412 1))
             IF         COND(&COPYS = 0) THEN(CHGVAR VAR(&COPYS) +
                          VALUE(1))

OUTQ:        CHGVAR     VAR(&OUTQ) VALUE(%SST(*LDA 413 10))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(%SST(*LDA 401 10)))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE('*JOB      '))

/*------------------------------------------------------------------*/
/* OVERRIDE PRINT FILE                                              */
/*------------------------------------------------------------------*/

/* USE THE CUSTOMIZED SEABOARD COMMAND TO OVERRIDE THE PRINT FILE.  */
/* THIS COMMAND ALLOWS THE REPORT TO BE GENERATED ON A STANDARD LINE*/
/* PRINTER OR A LASER PRINTER. TO USE THIS OVERRIDE YOU MUST DECLARE*/
/* A COMMAND VARIABLE AND FOLLOW THE OVERRIDE WITH A CALL TO        */
/* QCMDEXC.                                                         */

/* THIS REPORT CAN PRINT AT 10CPI IN 80 CHARACTERS                  */

             SBDPRTOVR  CMDVAR(&CMD) FILE(QPRINT) OUTQ(&OUTQ) +
                          WIDTH(80) COPIES(&COPYS) HOLD(&HOLD)

             CALL       PGM(QCMDEXC) PARM(&CMD 512)

/*-------------------------------------------------------------------*/
/*  CALL THE APPROPRIATE LISTING BASED ON SEQUENCE                   */
/*-------------------------------------------------------------------*/

/*  LISTING BY FARM                                                */

             IF         COND(&LDSQ *EQ 'F') THEN(DO)
             OVRDBF     FILE(HSL028B) SHARE(*YES)
             OPNQRYF    FILE((HSL028B)) QRYSLT(&QDATA) KEYFLD(*FILE) +
                          SEQONLY(*YES)
             CALL       PGM(HP673)
             CLOF       OPNID(HSL028B)
             DLTOVR     FILE(HSL028B QPRINT)
             ENDDO

/*  LISTING BY EFFECTIVE DATE                                      */

             IF         COND(&LDSQ *EQ 'E') THEN(DO)
             OVRDBF     FILE(HSL028A) SHARE(*YES)
             OPNQRYF    FILE((HSL028A)) QRYSLT(&QDATA) KEYFLD(*FILE) +
                          SEQONLY(*YES)
             CALL       PGM(HP674)
             CLOF       OPNID(HSL028A)
             DLTOVR     FILE(HSL028A QPRINT)
             ENDDO

             ENDPGM

