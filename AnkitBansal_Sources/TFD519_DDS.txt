     A*%%TS  SD  20051004  162635  ISLRAMS     REL-V5R3M0  5722-WDS
     A*****************************************************************
     A*
     A*  SYSTEM:        TRIUMPH FOODS
     A*  DISPLAY FILE:  TFD519 - SELECT REPORT TYPE
     A*  CREATED BY:    LEANNE FEDOR
     A*  DATE:          10/04/05
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/TFPREF)
     A                                      PRINT
     A                                      CA12(12 'PREVIOUS')
     A N82                                  ROLLUP(25 'PAGE FORWARD')
     A*---------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*---------------------------------------------------------------
     A          R TF519S1                   SFL
     A            HDRRN1    R        H      REFFLD(@@RRN)
     A            D1RTCD    R        O  5  1REFFLD(@@RTCD)
     A            D1RTDS    R        O  5  8REFFLD(@@RTDS)
     A*
     A*---------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*---------------------------------------------------------------
     A          R TF519C1                   SFLCTL(TF519S1)
     A*%%TS  SD  20051004  162601  ISLRAMS     REL-V5R3M0  5722-WDS
     A                                      SFLSIZ(0005)
     A                                      SFLPAG(0004)
     A                                      WINDOW(TF519EC)
     A                                      OVERLAY
     A                                      SFLCSRRRN(&HDRRN)
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A            HDPAG1    R        H      REFFLD(@@PAG)
     A                                      SFLRCDNBR
     A            HDRRN     R        H      REFFLD(@@RRN)
     A            SDPGM     R        O  1  1REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 13'SELECT REPORT TYPE'
     A                                      DSPATR(HI)
     A                                  2  1'Position Cursor, Press Enter'
     A                                  4  1'Code'
     A                                      DSPATR(HI)
     A                                  4  8'Description'
     A                                      DSPATR(HI)
     A*---------------------------------------------------------------
     A* COMMAND LINE LINE
     A*---------------------------------------------------------------
     A          R TF519K1
     A                                      WINDOW(TF519EC)
     A                                      OVERLAY
     A  43                              9  1'Message program failed. Call IS.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 10  1'F12=Previous'
      *
     A*-------------------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
     A*-------------------------------------------------------------------------
     A          R TF519ES                   SFL SFLMSGRCD(10)
      *
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
     A*
     A*-------------------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*-------------------------------------------------------------------------
     A*
     A          R TF519EC                   SFLCTL(TF519ES)
     A*%%TS  SD  19980327  105628  ISLRAMS     REL-V3R7M0  5716-PW1
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A                                      WINDOW(*DFT 12 35)
     A                                      WDWBORDER((*DSPATR RI) (*CHAR '    -
     A                                          '))
     A            MSGPGM                    SFLPGMQ(10)
     A*
     A*---------------------------------------------------------------
     A* ASSUME FORMAT -- REQUIRED FOR WINDOWS
     A*---------------------------------------------------------------
     A          R ASSUME
     A*%%TS  SD  19980327  102611  ISLRAMS     REL-V3R7M0  5716-PW1
     A                                      ASSUME
     A                                      OVERLAY
     A                                 24 79' '
