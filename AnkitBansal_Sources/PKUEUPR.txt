/***************************************************************** */
/*                                                                 */
/*  PROGRAM NUMBER:  PKUEUPR                                       */
/*  PROGRAM NAME:    PRINT PROCUREMENT PERFORMANCE RPTS-DTL,SUM    */
/*  PROGRAMMER:      ROSE CENTONZE                                 */
/*  CREATE DATE:     07/17/95                                      */
/*                                                                 */
/*  FUNCTION:        THIS CL IS SUBMITTED WITH EXECUTE MESSAGE     */
/*                   FROM PROMPT PRINT PRCRMNT PRFMNCE RPT.        */
/*                   USER OPTIONS ARE PASSED THROUGH THE PARMS.    */
/*                                                                 */
/*******************************************************************/
/*  Modification History                                           */
/*   B. Baden      07/17/17 E11200 - Number of Copies is Zero.     */
/*                               Change incoming parameter order   */
/*                               to pass in an alpha COPIES value. */
/*   J. High       05/16/17 E10339 - Hog Procurement Performance   */
/*                               Report Modifications              */
/*                               Replace Parm &SCODE with &SCODE18 */
/*                               &SCODE18 = 6 Source Code Concat   */
/*   R. CENTONZE   04/02/97 -- ADD SOURCE CODE AS A PARM/SELECTOR  */
/*                                                                 */
/*   J. TREMPER    02/08/1999 -- ADDED REPORT SEQUEUNCE AS A       */
/*                 PARM/SELECTOR                                   */
/*                                                                 */
/*   P. DROGE      05/25/1999 -- ADDED REPORT SEQUEUNCE FOR        */
/*                 KILL DATE                                       */
/*                                                                 */
/*   P. DROGE      06/15/1999 -- ADDED OUTFILE CREATE PROCESSING   */
/*                                                                 */
/*   P. DROGE      02/02/2000 -- ADDED REPORT SEQUENCE 4 FOR       */
/*                               REPORT BY LEAN% AND SOURCE CODE.  */
/*                                                                 */
/*******************************************************************/
             PGM        PARM(&CO &SRCE &PAYM &SCODE18 &PROD &BUYR +
                          &OMS &XFROM &XTO &MKT &PRC &DTLSUM &SEQ +
                          &OUTF &FLDR &PDEV &HOLD &SAVE &XCOPIES)
/* PRINT OPTION PARMS                                             */
             DCL        VAR(&RTN) TYPE(*CHAR) LEN(7)
             DCL        VAR(&PDEV) TYPE(*CHAR) LEN(10)
             DCL        VAR(&HOLD) TYPE(*CHAR) LEN(4)
             DCL        VAR(&SAVE) TYPE(*CHAR) LEN(4)

             DCL        VAR(&XCOPIES) TYPE(*CHAR) LEN(2)
             DCL        VAR(&PGMO) TYPE(*CHAR) LEN(8)

/* SYSTEM NAME PARM                                              */
             DCL        VAR(&OMS) TYPE(*CHAR) LEN(6)
             DCL        VAR(&LPP) TYPE(*CHAR) LEN(40)

/* FUNCTION SPECIFIC PARMS                                        */
             DCL        VAR(&XCO) TYPE(*CHAR) LEN(3)
             DCL        VAR(&SRCE) TYPE(*CHAR) LEN(1)
             DCL        VAR(&PAYM) TYPE(*CHAR) LEN(1)
             DCL        VAR(&SCODE18) TYPE(*CHAR) LEN(18)
             DCL        VAR(&PROD) TYPE(*CHAR) LEN(6)
             DCL        VAR(&BUYR) TYPE(*CHAR) LEN(3)
             DCL        VAR(&XFROM) TYPE(*CHAR) LEN(7)
             DCL        VAR(&XTO) TYPE(*CHAR) LEN(7)
             DCL        VAR(&DTLSUM) TYPE(*CHAR) LEN(1)
             DCL        VAR(&MKT) TYPE(*CHAR) LEN(3)
             DCL        VAR(&PRC) TYPE(*CHAR) LEN(7)
             DCL        VAR(&SEQ) TYPE(*CHAR) LEN(1)
             DCL        VAR(&OUTF) TYPE(*CHAR) LEN(6)
             DCL        VAR(&FLDR) TYPE(*CHAR) LEN(6)

/* NUMERIC VARIABLES                                             */
             DCL        VAR(&COPIES) TYPE(*CHAR) LEN(2)
             DCL        VAR(&CO) TYPE(*DEC) LEN(3 0)
             DCL        VAR(&FROM) TYPE(*DEC) LEN(7)
             DCL        VAR(&TO) TYPE(*DEC) LEN(7)

/* SBDPRTOVR VARIABLE                                          */
             DCL        VAR(&CMD) TYPE(*CHAR) LEN(512)

             /* */
/* CHANGE ALPHA PARMS TO NUMERIC VARIABLES                       */
         /*  CHGVAR     VAR(&XCOPIES) VALUE(&COPIES)   */
         /*  CHGVAR     VAR(&CO) VALUE(&XCO)           */
             CHGVAR     VAR(&LPP) VALUE(&OMS)
             CHGVAR     VAR(&FROM) VALUE(&XFROM)
             CHGVAR     VAR(&TO) VALUE(&XTO)
/*******************************************************************/
/*     PRINT REPORTS                                              */
/*******************************************************************/

             ADDLIBLE   LIB(SEQUEL) POSITION(*LAST)
             MONMSG     MSGID(CPF0000)

/* CREATE PRINTER FILE OVERRIDE CODE                            */
             IF         COND(&SEQ *EQ '1') THEN(DO)
             CHGVAR     VAR(&PGMO) VALUE(PKT4PFR$)
             ENDDO
             IF         COND(&SEQ *EQ '2') THEN(DO)
             CHGVAR     VAR(&PGMO) VALUE(PKQKPFR$)
             ENDDO
             IF         COND(&SEQ *EQ '3') THEN(DO)
             CHGVAR     VAR(&PGMO) VALUE(PKRHPFR$)
             ENDDO
             IF         COND(&SEQ *EQ '4') THEN(DO)
             CHGVAR     VAR(&PGMO) VALUE(PKUKPFR$)
             ENDDO
/* IF OUTFILE IS NOT BLANK, CREATE THE FILE BY KILL DATE */
             IF         COND(&OUTF *NE '          ') THEN(DO)
             CHGVAR     VAR(&PGMO) VALUE(PKRHPFR$)
             CHGVAR     VAR(&SEQ) VALUE('3')
             ENDDO

             SBDPRTOVR  CMDVAR(&CMD) FILE(&PGMO) OUTQ(&PDEV) +
                          COPIES(&XCOPIES) HOLD(&HOLD) SAVE(&SAVE)
             CALL       PGM(QCMDEXC) PARM(&CMD 512)

SEQUENCE:    IF         COND(&SEQ *EQ '1') THEN(DO)
BOTH:        IF         COND(&DTLSUM *EQ 'B') THEN(DO)

/* PRINT SUMMARY AND DETAIL                                    */
             CALL       PGM(PKT4PFR) PARM(' ' &CO &SRCE &PAYM +
                          &SCODE18 &PROD &BUYR &FROM &TO &LPP &MKT +
                          &PRC 'D')
             CALL       PGM(PKT4PFR) PARM(' ' &CO &SRCE &PAYM +
                          &SCODE18 &PROD &BUYR &FROM &TO &LPP &MKT +
                          &PRC 'S')
             ENDDO
             ELSE (DO)
/* PRINT SUMMARY OR DETAIL                                     */
             CALL       PGM(PKT4PFR) PARM(' ' &CO &SRCE &PAYM +
                          &SCODE18 &PROD &BUYR &FROM &TO &LPP &MKT +
                          &PRC &DTLSUM)
             ENDDO
             ENDDO
/*********************************************************************/
             IF         COND(&SEQ *EQ '2') THEN(DO)
             IF         COND(&DTLSUM *EQ 'B') THEN(DO)

/* PRINT SUMMARY AND DETAIL                                    */
             CALL       PGM(PKQKPFR) PARM(' ' &CO &SRCE &PAYM +
                          &SCODE18 &PROD &BUYR &FROM &TO &LPP &MKT +
                          &PRC 'D')
             CALL       PGM(PKQKPFR) PARM(' ' &CO &SRCE &PAYM +
                          &SCODE18 &PROD &BUYR &FROM &TO &LPP &MKT +
                          &PRC 'S')
             ENDDO
             ELSE (DO)
/* PRINT SUMMARY OR DETAIL                                     */
             CALL       PGM(PKQKPFR) PARM(' ' &CO &SRCE &PAYM +
                          &SCODE18 &PROD &BUYR &FROM &TO &LPP &MKT +
                          &PRC &DTLSUM)
             ENDDO
             ENDDO
/*******************************************************************/
             IF         COND(&SEQ *EQ '3') THEN(DO)
             IF         COND(&DTLSUM *EQ 'B') THEN(DO)

/* PRINT SUMMARY AND DETAIL                                    */
             CALL       PGM(PKRHPFR) PARM(' ' &CO &FROM &SRCE &PAYM +
                          &SCODE18 &PROD &BUYR &FROM &TO &LPP &MKT +
                          &PRC 'D' &OUTF)
             CALL       PGM(PKRHPFR) PARM(' ' &CO &FROM &SRCE &PAYM +
                          &SCODE18 &PROD &BUYR &FROM &TO &LPP &MKT +
                          &PRC 'S' &OUTF)
             ENDDO
             ELSE (DO)
/* PRINT SUMMARY OR DETAIL                                     */

             IF         COND(&OUTF *NE '      ') THEN(DO)
             CLRPFM     FILE(PKEPCPP)
             ENDDO
             CALL       PGM(PKRHPFR) PARM(' ' &CO &FROM &SRCE &PAYM +
                          &SCODE18 &PROD &BUYR &FROM &TO &LPP &MKT +
                          &PRC &DTLSUM &OUTF)

             IF         COND(&OUTF *NE '      ') THEN(DO)
             EXECUTE    VIEW(PRFMRPT) TOFLR(&FLDR) TODOC(&OUTF) +
                          PCFMT(*DELIMITED) REPLACE(*YES)
             ENDDO

             ENDDO
             ENDDO
/*********************************************************************/
             IF         COND(&SEQ *EQ '4') THEN(DO)
             IF         COND(&DTLSUM *EQ 'B') THEN(DO)

/* PRINT SUMMARY AND DETAIL                                    */
             CALL       PGM(PKUKPFR) PARM(' ' &CO &SRCE &PAYM +
                          &SCODE18 &PROD &BUYR &FROM &TO &LPP &MKT +
                          &PRC 'D')
             CALL       PGM(PKUKPFR) PARM(' ' &CO &SRCE &PAYM +
                          &SCODE18 &PROD &BUYR &FROM &TO &LPP &MKT +
                          &PRC 'S')
             ENDDO
             ELSE (DO)
/* PRINT SUMMARY OR DETAIL                                     */
             CALL       PGM(PKUKPFR) PARM(' ' &CO &SRCE &PAYM +
                          &SCODE18 &PROD &BUYR &FROM &TO &LPP &MKT +
                          &PRC &DTLSUM)
             ENDDO
             ENDDO
/*******************************************************************/
             DLTOVR *ALL
             ENDPGM
