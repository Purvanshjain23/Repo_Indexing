      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      HOG PRODUCTION
      * PROGRAM:     HP384
      * TITLE:       BUILD WORKFILE FOR SALES MOVEMENT/CHECK REPORT
      * PROGRAMMER:  LEANNE FEDOR
      * CREATED:     9/07/94
      *
      *  FUNCTION:   THIS BATCH PROGRAM IS CALLED FROM HP484CL.
      *              IT BUILDS WORKFILE HSP384 THAT IS USED IN THE
      *              REPORT PROGRAM HP684.
      *
      *              THE USER'S SUBMISSION CRITERIA ARE PASSED IN THE
      *              LDA FROM HP484.  AN OPEN QUERY IS USED IN THE
      *              CL TO MAKE AN INITIAL SELECTION OF DATA.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      * 09/27/94  LEANNE FEDOR
      *           RECOMPILE ONLY. REMOVED THE FOLLOWING UNUSED
      *           DATABASE FIELDS FROM THE CHECK FILES:
      *             HSP063-CHECK HEADER        CHCHBO CHHRDT
      *             HSP064-CHECK DETAIL        CDCHBO CDCSAM
      *             HSP065-CHECK ADJUSTMENTS   CJCHBO
      *
      * 01/13/95  LEANNE FEDOR
      *           RECOMPILE ONLY. SEX ADDED TO HOG GROUP FILE.
      *
      * 03/13/95  LEANNE FEDOR
      *           RECOMPILE ONLY. ORIGIN BGF FARM SITE AND ORIGIN
      *           NURSERY FARM SITE ADDED TO HOG GROUP FILE.
      *
      * 04/24/95  LEANNE FEDOR
      *           CHANGED FARM SITE CODE FROM ALPHA TO NUMERIC.
      *
      * 07/03/95  LEANNE FEDOR
      *           CHANGED TO USE NEW FILES FOR MOVEMENT REWRITE.
      *
      * 10/11/95  LEANNE FEDOR
      *           CHANGED LOGIC TO WRITE A WORKFILE RECORD FOR EACH
      *           CHECK DETAIL RECORD INSTEAD OF EACH MOVEMENT.
      *
      * 02/06/96  LEANNE FEDOR
      *           RECOMPILE ONLY. NEW FIELD 'BOL PRINTED COUNT' ADDED
      *           TO SALES MOVEMENT HEADER FILE.
      *
      * 05/07/96  LEANNE FEDOR
      *           DAVE O'SULLIVAN REQUESTED THAT 'BREED STOCK' BE
      *           A SELECTION FOR THE REPORT. LOGIC WAS ADDED IN THIS
      *           PROGRAM TO ONLY PROCESS RECORDS IF THE BREED STOCK
      *           MATCHED THE USER'S SELECTION.
      *
      * 06/03/97  LEANNE FEDOR
      *           RECOMPILE ONLY. NEW FIELDS 'PURGE FLAG' AND 'DATE OF
      *           FIRST PURGE' ADDED TO HOG GROUP FILE.
      *
      * 02/16/98  LeAnne Fedor
      *           Recompile only. Added the following fields to the
      *           database for sales movement detail and check detail:
      *                     condemned head and pounds
      *                     yard dead head and pounds
      *
      * 01/11/99  LeAnne Fedor
      *           Recompile only. New field 'scheduled kill date' added
      *           to sales movement header file.
      *
      * 10/17/00  LeAnne Fedor
      *           Recompile only. New field 'load type' added to the sales
      *           movement head file.
      *
      * 06/28/01  LeAnne Fedor
      *           Recompile only.
      *           Manager and supervisor fields removed from Hog Group file.
      *
      * 07/06/09  LeAnne Ramsey
      *           Recompile only. Added new field 'Continuous Flow Flag' to Hog Group file.
      /eject
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     FHSL034D   IF   E           K DISK
      *    HOG GROUP
      *
      *
     FHSL064C   IF   E           K DISK
      *    CHECK DETAIL   (records selected in OPNQRY)
      *
      *
     FHSP084    IF   E           K DISK
      *  SALE MOVEMENT HEADER  (records selected in OPNQRY)
      *
      *
     FHSP384    O    E           K DISK
      *    WORKFILE
      *
      /EJECT
      ****************************************************************
      * Definition Specifications
      ****************************************************************
      *
      *---------------------------------------------------------------
      *  LDA
      *---------------------------------------------------------------
      *
     D                UDS
     D  LDFSBO                 1      5
     D  LDPTCD                64     68
     D  LDPPCD                79     83
     D  LDHMCD               114    118
     D  LDFSCD               149    153  0
     D  LDHGSN               217    223  0
     D  LDBSCD               231    235
     D  LDBSDS               236    255
      *
      *
      *---------------------------------------------------------------
      * STANDARD PROGRAM STATUS DATA STRUCTURE
      *---------------------------------------------------------------
      *    EXTERNALLY DEFINED AS UTPGFR (RECORD FORMAT: PGMDSR)
     D PGMDS         ESDS                  EXTNAME(UTPGFR)
      *
      *---------------------------------------------------------------
      *  NAMED CONSTANTS
      *---------------------------------------------------------------
      *
     D YES             C                   CONST('Y')
     D NO              C                   CONST('N')
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * MAIN LINE
      ****************************************************************
      *
      * READ EACH SALES MOVEMENT HEADER RECORD SELECTED IN THE OPEN QUERY
      *
     C     *LOVAL        SETLL     HSP084
      *
     C     *IN90         DOUEQ     *ON                                          MAIN DO
     C                   READ      HSP084                                 90
     C     *IN90         IFEQ      *OFF                                         IF NOT EOF
      *
      * WRITE A WORKFILE RECORD FOR EACH CHECK DETAIL
      * RECORD FOR THIS MOVEMENT.
      * (NOTE: IF THE USER ELECTED TO SEE ONLY ONE GROUP,
      * AN OPEN QUERY HAS BEEN RUN OVER THIS CHECK DETAIL FILE)
      *
     C     SHMVSN        SETLL     HSL064C
     C     *IN91         DOUEQ     *ON                                          DO CHECKS
     C     SHMVSN        READE     HSL064C                                91
     C     *IN91         IFEQ      *OFF                                         IF CHECK
      *
      * Retrieve 'group' info and compare to user's selections
      *
     C                   exsr      $hgcd
      *
      * Process record
      *
     C     PROCFL        IFEQ      YES                                          IF PROCESS
     C                   MOVEL     HGPTCD        WFPTCD
     C                   MOVEL     HGPPCD        WFPPCD
     C                   Z-ADD     SHRCDT        WFRCDT
     C                   Z-ADD     SHCVNO        WFCVNO
     C                   Z-ADD     SHMVSN        WFMVSN
     C                   MOVEL     SHFSBO        WFFSBO
     C                   MOVEL     SHSTCD        WFSTCD
     C                   Z-ADD     SHFSCD        WFFSCD
      *
     C                   MOVEL     CDTPCD        WFTPCD
     C                   Z-ADD     CDCHNO        WFCHNO
     C                   Z-ADD     CDCHDT        WFCHDT
     C                   Z-ADD     CDLNNO        WFLNNO
      *
     C                   WRITE     WFREC
     C                   ENDIF                                                  IF CHECK
     C                   ENDIF                                                  IF PROCESS
     C                   ENDDO                                                  DO CHECKS
      *
     C                   ENDIF                                                  IF NOT EOF
     C                   ENDDO                                                  MAIN DO
      *
     C                   SETON                                        LR
      *
      /EJECT
      *---------------------------------------------------------------
      * Retrieve group info to compare to user's selections
      *---------------------------------------------------------------
      *
     C     $hgcd         begsr
      *
     C     cdhgsn        chain     hsl034d                            92
     C                   select
     C                   when      *in92 = *on
     C                   move      no            procfl
      *
     C                   when      (ldbscd <> *blank and hgbscd <> ldbscd) or
     C                             (ldptcd <> *blank and hgptcd <> ldptcd) or
     C                             (ldppcd <> *blank and hgppcd <> ldppcd)
     C                   move      no            procfl
     C                   other
      *
     C                   move      yes           procfl
     C                   endsl
      *
     C                   endsr
      /EJECT
      *----------------------------------------------------------------
      * *INZSR - INITIALIZATION SUBROUTINE
      *----------------------------------------------------------------
      *
     C     *INZSR        BEGSR
      *
     C                   MOVEL     YES           PROCFL            1
      *
     C                   ENDSR
      /EJECT
