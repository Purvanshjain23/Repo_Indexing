/***************************************************************** */
/*                                                                 */
/*  SYSTEM:          TRIUMPH FOODS                                 */
/*  PROGRAM NUMBER:  TF432CL                                       */
/*  PROGRAM NAME:    WORK WITH INVOICE CHARGES                     */
/*  PROGRAMMER:      LEANNE FEDOR                                  */
/*  CREATE DATE:     03/15/05                                      */
/*                                                                 */
/*  PRINTS LISTING(S) FROM WORK WITH.                              */
/*                                                                 */
/*******************************************************************/
/* MODIFIED:                                                       */
/*                                                                 */
/* 05/06/09  LEANNE RAMSEY                                         */
/*           CHANGED PRINT LOGIC TO MATCH MEAT COSTING.            */
/*******************************************************************/

             PGM

             DCL        VAR(&QDATA) TYPE(*CHAR) LEN(225)

             DCL        VAR(&OUTQ) TYPE(*CHAR) LEN(10)
             DCL        VAR(&HOLD) TYPE(*CHAR) LEN(4)
             DCL        VAR(&SAVE) TYPE(*CHAR) LEN(4)
             DCL        VAR(&COPY) TYPE(*DEC)  LEN(1)

             DCL        VAR(&LDFCYMD) TYPE(*DEC) LEN(8)
             DCL        VAR(&LDTCYMD) TYPE(*DEC) LEN(8)
             DCL        VAR(&LDINSN)  TYPE(*DEC) LEN(7)
             DCL        VAR(&LDICSN)  TYPE(*DEC) LEN(7)

             DCL        VAR(&LDYNFL)  TYPE(*CHAR) LEN(1)
             DCL        VAR(&LDINSTCD)  TYPE(*CHAR) LEN(1)
             DCL        VAR(&LDINFL)  TYPE(*CHAR) LEN(1)
             DCL        VAR(&LDSMCD)  TYPE(*CHAR) LEN(2)
             DCL        VAR(&LDCCCD)  TYPE(*CHAR) LEN(10)

             CHGVAR     VAR(&LDYNFL) VALUE(%SST(*LDA 1 1))
             CHGVAR     VAR(&LDFCYMD) VALUE(%SST(*LDA 8 8))
             CHGVAR     VAR(&LDTCYMD) VALUE(%SST(*LDA 22 8))
             CHGVAR     VAR(&LDINSTCD) VALUE(%SST(*LDA 30 1))
             CHGVAR     VAR(&LDSMCD) VALUE(%SST(*LDA 33 2))
             CHGVAR     VAR(&LDINFL) VALUE(%SST(*LDA 35 1))
             CHGVAR     VAR(&LDCCCD) VALUE(%SST(*LDA 36 10))
             CHGVAR     VAR(&LDINSN) VALUE(%SST(*LDA 46 7))
             CHGVAR     VAR(&LDICSN) VALUE(%SST(*LDA 53 7))

/*----------------------------------------------------------------*/
/* SET PARAMETERS FROM LDA FOR LISTINGS                           */
/*----------------------------------------------------------------*/

 OUTQ:       CHGVAR     VAR(&OUTQ) VALUE(%SST(*LDA 401 10))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(*JOB))

 HOLD:       CHGVAR     VAR(&HOLD) VALUE(%SST(*LDA 411 4))
             IF         COND(&HOLD *NE '*YES' *AND &HOLD *NE '*NO ') +
                          THEN(CHGVAR VAR(&HOLD) VALUE(*NO))

 COPY:       CHGVAR     VAR(&COPY) VALUE(%SST(*LDA 419 1))
             IF         COND(&COPY = 0) THEN(CHGVAR VAR(&COPY) +
                          VALUE(1))

 SAVE:       CHGVAR     VAR(&SAVE) VALUE(%SST(*LDA 415 4))
             IF         COND(&SAVE *NE '*YES' *AND &SAVE *NE '*NO ') +
                          THEN(CHGVAR VAR(&SAVE) VALUE(*NO))


/*---------------------------------------------------------------------*/
/* EXTRACT THE DATA FROM THE MARGIN ADJUSTMENT SUMMARY FILE            */
/*---------------------------------------------------------------------*/

             CHGVAR     VAR(&QDATA) VALUE(' ')

             CHGVAR     VAR(%SST(&QDATA 1 12)) VALUE('ICICSN *NE 0')

/* IF 'INCLUDE POSTED CHARGES' IS NO                                */

             IF         COND(&LDYNFL *EQ 'N') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 14 21)) VALUE('*AND ICINSTCD +
                          *NE "P"')
             ENDDO

/* IF FROM DATE                                                     */
             IF         COND(&LDFCYMD *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 36 15)) VALUE('*AND ICICDT *GE')
             CHGVAR     VAR(%SST(&QDATA 52 8)) VALUE(&LDFCYMD)
             ENDDO

/* IF TO DATE                                                       */
             IF         COND(&LDTCYMD *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 61 15)) VALUE('*AND ICICDT *LE')
             CHGVAR     VAR(%SST(&QDATA 77 8)) VALUE(&LDTCYMD)
             ENDDO

/* IF INVOICE STATUS CODE                                              */

             IF         COND(&LDINSTCD *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 86 19)) VALUE('*AND ICINSTCD *EQ "')
             CHGVAR     VAR(%SST(&QDATA 105 1)) VALUE(&LDINSTCD)
             CHGVAR     VAR(%SST(&QDATA 106 1)) VALUE('"')
             ENDDO

/* IF SYSTEM MODULE CODE                                               */

             IF         COND(&LDSMCD *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 108 17)) VALUE('*AND ICSMCD *EQ "')
             CHGVAR     VAR(%SST(&QDATA 125 2)) VALUE(&LDSMCD)
             CHGVAR     VAR(%SST(&QDATA 127 1)) VALUE('"')
             ENDDO

/* IF INCLUDE IN INVOICE TOTAL FLAG                                    */

             IF         COND(&LDINFL *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 130 17)) VALUE('*AND ICINFL *EQ "')
             CHGVAR     VAR(%SST(&QDATA 147 1)) VALUE(&LDINFL)
             CHGVAR     VAR(%SST(&QDATA 148 1)) VALUE('"')
             ENDDO

/* IF CHARGE CODE                                                      */

             IF         COND(&LDCCCD *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 150 17)) VALUE('*AND ICCCCD *EQ "')
             CHGVAR     VAR(%SST(&QDATA 167 10)) VALUE(&LDCCCD)
             CHGVAR     VAR(%SST(&QDATA 177 1)) VALUE('"')
             ENDDO

/* IF INVOICE NUMBER                                                */
             IF         COND(&LDINSN *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 179 15)) VALUE('*AND ICINSN *EQ')
             CHGVAR     VAR(%SST(&QDATA 195 7)) VALUE(&LDINSN)
             ENDDO

/* IF CHARGE NUMBER                                                 */
             IF         COND(&LDICSN *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 203 15)) VALUE('*AND ICICSN *EQ')
             CHGVAR     VAR(%SST(&QDATA 219 7)) VALUE(&LDICSN)
             ENDDO


/* OVERRIDE INVOICE CHARGES FILE                              */

             OVRDBF     FILE(TFL021H) SHARE(*YES)
             OPNQRYF    FILE((TFL021H)) OPTION(*INP) QRYSLT(&QDATA) +
                          KEYFLD(*FILE) SEQONLY(*YES)


/*---------------------------------------------------------------------*/
/* OVERRIDE PRINT FILE                                                 */
/*---------------------------------------------------------------------*/

             OVRPRTF    FILE(PRINT132) OUTQ(&OUTQ) COPIES(&COPY) +
                          HOLD(&HOLD) SAVE(&SAVE)

 /* CALL PRINT PROGRAM                                                    */

             CALL       PGM(TF632)

             CLOF       OPNID(TFL021H)
             DLTOVR     FILE(TFL021H)
             DLTOVR     FILE(*ALL)

             ENDPGM

