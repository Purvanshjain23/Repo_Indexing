     A*%%TS  SD  20091007  132522  ISLRAMS     REL-V5R4M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        TRIUMPH FOODS
     A*  DISPLAY FILE:  TFD461
     A*  TITLE:         SPECIFY AR ADJUSTMENT REPORT BY GL POST ACCOUNT
     A*  PROGRAMMER:    LEANNE RAMSEY
     A*  CREATED:       10/06/09
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/TFPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A                                      CF04(04 'PROMPT')
     A                                      CF08(08 'SUBMIT')
     A                                      CF17(17 'PRINT OPTIONS')
     A                                      CF20(20 'WRKSPLF')
     A*
     A*---------------------------------------------------------------
     A* RECORD FORMAT ONE
     A*---------------------------------------------------------------
     A          R TF461R1
     A*%%TS  SD  20091007  132522  ISLRAMS     REL-V5R4M0  5722-WDS
     A                                      SETOF(30)
     A                                      SETOF(31)
     A                                      SETOF(32)
     A                                      SETOF(34)
     A                                      SETOF(35)
     A                                      SETOF(54)
     A                                      SETOF(55)
     A                                      OVERLAY
     A                                      RTNCSRLOC(&RCD &FLD)
     A            RCD       R        H      REFFLD(@@RCD)
     A            FLD       R        H      REFFLD(@@FLD)
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 18'SPECIFY OPTIONS FOR AR ADJUSTMENT -
     A                                      REPORT'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USR)
     A                                      DSPATR(HI)
     A                                  2 28'BY G/L POST ACCOUNT'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@JOB)
     A                                      DSPATR(HI)
     A                                  4 39'Week-ending date'
     A                                      DSPATR(UL)
     A                                      COLOR(BLU)
     A                                  4 57'Year'
     A                                      DSPATR(UL)
     A                                      COLOR(BLU)
     A                                  4 62'Week'
     A                                      DSPATR(UL)
     A                                      COLOR(BLU)
     A                                  4 67'Period'
     A                                      DSPATR(UL)
     A                                      COLOR(BLU)
     A                                  5  2'From date .............'
     A            D1FMDY    R        B  5 28REFFLD(TFREF/@@DT06 *LIBL/TFPREF)
     A                                      EDTWRD('  /  /  -')
     A  30                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A N30                                  DSPATR(HI)
     A            D1FWEMDY  R        O  5 43REFFLD(TFREF/@@DT06 *LIBL/TFPREF)
     A                                      EDTWRD('  /  /  ')
     A            D1FYR     R        O  5 57REFFLD(TFREF/@@YR *LIBL/TFPREF)
     A                                      EDTCDE(Z)
     A            D1FWK     R        O  5 63REFFLD(TFREF/@@WK *LIBL/TFPREF)
     A                                      EDTCDE(Z)
     A            D1FPE     R        O  5 69REFFLD(TFREF/@@PE *LIBL/TFPREF)
     A                                      EDTCDE(Z)
     A                                  6  2'To date ...............'
     A            D1TMDY    R        B  6 28REFFLD(TFREF/@@DT06 *LIBL/TFPREF)
     A                                      EDTWRD('  /  /  -')
     A  31                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A N31                                  DSPATR(HI)
     A            D1TWEMDY  R        O  6 43REFFLD(TFREF/@@DT06 *LIBL/TFPREF)
     A                                      EDTWRD('  /  /  ')
     A            D1TYR     R        O  6 57REFFLD(TFREF/@@YR *LIBL/TFPREF)
     A                                      EDTCDE(Z)
     A            D1TWK     R        O  6 63REFFLD(TFREF/@@WK *LIBL/TFPREF)
     A                                      EDTCDE(Z)
     A            D1TPE     R        O  6 69REFFLD(TFREF/@@PE *LIBL/TFPREF)
     A                                      EDTCDE(Z)
     A                                  8  2'Detail/summary/both ...'
     A                                  8 26'?'
     A            D1DSFL         1A  B  8 28
     A N35                                  DSPATR(HI)
     A  35
     AO 55                                  DSPATR(PC)
     A  35                                  DSPATR(RI)
     A            D1DSDS        23A  O  8 38
     A                                 10  2'G/L Post method code ..'
     A                                 10 26'?'
     A            D1PMCD    R        B 10 28REFFLD(TFREF/@@PMCD *LIBL/TFPREF)
     A N34                                  DSPATR(HI)
     A  34
     AO 54                                  DSPATR(PC)
     A  34                                  DSPATR(RI)
     A            D1PMDS    R        O 10 38REFFLD(TFREF/@@PMDS *LIBL/TFPREF)
     A                                 10 69'(Blank=All)'
     A                                 11  2'Post company ..........'
     A            D1PCONO   R        B 11 28REFFLD(TFREF/@@CONO *LIBL/TFPREF)
     A                                      EDTCDE(K)
     A  32                                  DSPATR(PC)
     A  32                                  DSPATR(RI)
     A N32                                  DSPATR(HI)
     A                                 11 69'(Blank=All)'
     A                                 13  2'Post account number:'
     A                                      DSPATR(UL)
     A                                 14  8'Cost center .....'
     A            D1PCC     R        B 14 28REFFLD(TFREF/@@CC *LIBL/TFPREF)
     A                                      DSPATR(HI)
     A                                 14 69'(Blank=All)'
     A                                 15  8'Object ..........'
     A            D1POBJ    R        B 15 28REFFLD(TFREF/@@OBJ *LIBL/TFPREF)
     A                                      DSPATR(HI)
     A                                 15 69'(Blank=All)'
     A                                 16  8'Subsidiary ......'
     A            D1PSUB    R        B 16 28REFFLD(TFREF/@@SUB *LIBL/TFPREF)
     A                                      DSPATR(HI)
     A                                 16 69'(Blank=All)'
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R TF461K1
     A*%%TS  SD  20080124  092007  ISLRAMS     REL-V5R4M0  5722-WDS
     A  43                             21  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A                                 23 11'F4=Prompt'
     A                                 23 22'F8=Submit'
     A                                 23 33'F17=Print Options'
     A                                 23 52'F20=Spool Files'
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *---------------------------------------------------------------
     A          R TF461ES                   SFL SFLMSGRCD(24)
      *
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
     A*---------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*---------------------------------------------------------------
     A*
     A          R TF461EC                   SFLCTL(TF461ES)
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
