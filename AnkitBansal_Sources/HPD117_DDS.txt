     A*%%TS  SD  20011227  103235  ISLRAMS     REL-V4R4M0  5769-PW1
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION
     A*  DISPLAY FILE:  HPD117
     A*  TITLE:         REVISE RATION
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  DATE:          12/02/94
     A*
     A*  INDICATOR 60 IS ON WHEN IN REVISE MODE
     A*  INDICATOR 61 IS ON WHEN IN CREATE MODE
     A*  INDICATOR 62 IS ON WHEN IN DELETE MODE
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A N62                                  CF04(04 'PROMPT')
     A  60N62                               CF05(05 'REFRESH')
     A  60N62                               CF11(11 'DELETE')
     A                                      CA12(12 'PREVIOUS')
     A*
     A*---------------------------------------------------------------
     A* RECORD FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP117R1
     A*%%TS  SD  20011227  103009  ISLRAMS     REL-V4R4M0  5769-PW1
     A                                      SETOF(31)
     A                                      SETOF(32)
     A                                      SETOF(33)
     A                                      SETOF(34)
     A                                      SETOF(35)
     A                                      SETOF(36)
     A                                      SETOF(37)
     A                                      SETOF(38)
     A                                      SETOF(54)
     A                                      SETOF(57)
     A                                      SETOF(58)
     A                                      OVERLAY
     A                                      RTNCSRLOC(&RCD &FLD)
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 28'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A            DFMODE    R        O  2  2REFFLD(@@MODE)
     A                                      DSPATR(HI)
     A                                  2 32'REVISE RATION'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  4  2'Ration code .........'
     A            DFRNCD    R        B  4 26REFFLD(RNRNCD)
     A  61N31                               DSPATR(HI)
     A  31                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A  60
     AO 62                                  DSPATR(PR)
     A  61                                  DSPATR(UL)
     A                                  5  2'Ration description ..'
     A            DFRNDS    R        B  5 26REFFLD(RNRNDS)
     A N33N62                               DSPATR(HI)
     A  33                                  DSPATR(PC)
     A  33                                  DSPATR(RI)
     A  62                                  DSPATR(PR)
     A N62                                  DSPATR(UL)
     A                                  6  2'Production phase ....'
     A                                  6 24'?'
     A            DFPPCD    R        B  6 26REFFLD(RNPPCD)
     A N34N62                               DSPATR(HI)
     A  34
     AO 54                                  DSPATR(PC)
     A  34                                  DSPATR(RI)
     A  62                                  DSPATR(PR)
     A N62                                  DSPATR(UL)
     A            DFPPDS    R        O  6 35REFFLD(PPPPDS)
     A                                  7  2'Ration type .........'
     A                                  7 24'?'
     A            DFRTCD    R        B  7 26REFFLD(RNRTCD)
     A N37N62                               DSPATR(HI)
     A  37
     AO 57                                  DSPATR(PC)
     A  37                                  DSPATR(RI)
     A  62                                  DSPATR(PR)
     A N62                                  DSPATR(UL)
     A            DFRTDS    R        O  7 35REFFLD(RTRTDS)
     A                                  8  2'Batch pounds ........'
     A            DFRBLB    R        B  8 26REFFLD(RNRBLB)
     A                                      EDTCDE(K)
     A N35N62                               DSPATR(HI)
     A  35                                  DSPATR(PC)
     A  35                                  DSPATR(RI)
     A  62                                  DSPATR(PR)
     A N62                                  DSPATR(UL)
     A                                  9  2'Maximum age (days) ..'
     A            DFMAXDAY  R        B  9 26REFFLD(HSRREF/@@MAXDAY *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A N32N62                               DSPATR(HI)
     A  32                                  DSPATR(PC)
     A  32                                  DSPATR(RI)
     A  62                                  DSPATR(PR)
     A N62                                  DSPATR(UL)
     A                                 10  2'Lysine percent ......'
     A            DFLYPR    R        B 10 26REFFLD(RNLYPR)
     A                                      EDTCDE(K)
     A N36N62                               DSPATR(HI)
     A  36                                  DSPATR(PC)
     A  36                                  DSPATR(RI)
     A  62                                  DSPATR(PR)
     A N62                                  DSPATR(UL)
     A                                 11  2'Status ..............'
     A                                 11 24'?'
     A            DFRNST    R        B 11 26REFFLD(RNRNST)
     A N38N62                               DSPATR(HI)
     A  58
     AO 38                                  DSPATR(PC)
     A  38                                  DSPATR(RI)
     A  62                                  DSPATR(PR)
     A N62                                  DSPATR(UL)
     A*
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R HP117K1
     A*%%TS  SD  19940406  125922  ISLRAMS     REL-V2R2M0  5738-PW1
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A                                 23 11'F4=Prompt'
     A N62N61                          23 22'F5=Refresh'
     A N62N61                          23 34'F11=Delete'
     A                                 23 46'F12=Previous'
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *---------------------------------------------------------------
     A          R HP117ES                   SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE CONTROL FORMAT
      *---------------------------------------------------------------
     A          R HP117EC                   SFLCTL(HP117ES)
      *
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
