      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Triumph Foods
      * PROGRAM:     TF632
      * TITLE:       Listing of Invoice Charges
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     03/14/05
      *
      * FUNCTION:  Batch program to print a record(s) in the Invoice Charges file.
      *
      *            This listing is submitted from W/W Invoice Charges when the user takes:
      *                1) F7=Listing
      *
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Ftfp005    if   e           k disk
      *    Charge codes
      *
      *
     Ftfp006    if   e           k disk
      *    System modules
      *
      *
     Ftfp020    if   e           k disk
      *    Invoice header
      *
      *
     Ftfl021h   if   e           k disk
      *    Invoice charges (records selected by open query)
      *
      *
     Ftfp097    if   e           k disk
      *    Status codes
      *
      *
     Ftfp098    if   e           k disk
      *    Charge frequency codes
      *
      *
     Fprint132  o    f  132        printer oflind(*inof)
      *
      /EJECT
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Standard fields
      *
     D dash            s            132    inz(*all'-')
     d rtime           s              6  0
      *
      *
      * Control fields
      *
     D first           s              1    inz('Y')
     D procfl          s              1    inz('N')
      *
      *
      * Workfields for date manipulation
      *
     D wkisodate       s               d   datfmt(*iso)
      *
      *
      *
      * Print fields
      *
     D h1ccds          s                   like(ccccds)
     D h1ucds          s              3
     D h1cfqds         s                   like(cfcfqds)
     D h1inds          s              3
     D h1smds          s                   like(smsmds)
     D h1stds          s                   like(ststds)
     D h1ynds          s              3
      *
     D l1indtmdy       s              6  0
     D l1podtmdy       s              6  0
     D l1icdtmdy       s              6  0
     D l1ucfl          s                   like(ccucfl)
     D l1cfqcd         s                   like(cccfqcd)
     D l1comtext       s              4
      *
      ****************************************************************
      * ARRAYS AND TABLES
      ****************************************************************
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *
      *---------------------------------------------------------------
      * Local data area.
      *---------------------------------------------------------------
      *
     Dlda             uds                  dtaara(*lda)
     D  ldynfl                 1      1
     D  ldfmdy                 2      7  0
     D  ldfcymd                8     15  0
     D  ldtmdy                16     21  0
     D  ldtcymd               22     29  0
     D  ldinstcd              30     30
     D  ldcfqcd               31     31
     D  lducfl                32     32
     D  ldsmcd                33     34
     D  ldinfl                35     35
     D  ldcccd                36     45
     D  ldinsn                46     52  0
     D  ldicsn                53     59  0
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ***************************************************************************************
      * MAINLINE
      ***************************************************************************************
      *
      * Print headings
     C                   exsr      $h1hdr
      *
      * Read each record selected by the open query.
      *
     C     *loval        setll     tfl021h
      *
     C                   dou       *in90 = *on                                  Main do
     C                   read      tfl021h                                90
     C                   if        *in90 = *off                                 If not EOF
     C                   move      yes           procfl
      *
      * Retrieve Charge Code info
      *
     C     iccccd        chain     tfp005                             92
     C                   if        *in92 = *off
     C                   move      ccucfl        l1ucfl
     C                   move      cccfqcd       l1cfqcd
     C                   else
     C                   move      *blank        l1ucfl
     C                   move      *blank        l1cfqcd
     C                   endif
      *
      * Check for User Selections that could not be performed with the
      * Open Query in the CL.
      *
     C                   if        ldcfqcd <> *blank or
     C                             lducfl  <> *blank
     C                   exsr      $select
     C                   endif
      *
      * Process the record if it meets the user's selections.
      *
     C                   if        procfl = yes                                 If process
     C                   move      no            first
      *
      * Flip Invoice Charge Date from CCYYMMDD to MMDDYY
      *
     C                   if        icicdt <> 0
     C     *iso          test(d)                 icicdt                 92
     C                   if        *in92 = *off                                 If OK date
     C                   move      icicdt        wkisodate
     C     *mdy          move      wkisodate     l1icdtmdy
     C                   endif                                                  If OK date
     C                   endif
      *
      *
      * If there is a "comment" move the text "COM:" to a print field.
      * (I'm just trying to make the report look better; so, tried this
      * instead of hardcoding the 'text' in the detail line.)
      *
     C                   if        iciccom <> *blank
     C                   eval      l1comtext = 'Com:'
     C                   else
     C                   move      *blank        l1comtext
     C                   endif
      *
      *
      * Retrieve Invoice Info
      *
     C                   if        icinsn <> 0
     C                   exsr      $invoice
     C                   endif
      *
      * Print record
     C                   exsr      $l1dtl
     C                   endif                                                  If process
     C                   endif                                                  If not EOF
     C                   enddo                                                  Main do
      *
      *---------------------------------------------------------------
      * EOF processing
      *---------------------------------------------------------------
      * If the file was empty, print the standard 'no data...' message
      *
     C                   if        first = yes
     C                   except    nodata
     C                   endif
      *
     C                   seton                                        lr
      /EJECT
      *---------------------------------------------------------------
      * Print detail line
      *---------------------------------------------------------------
      *
     C     $l1dtl        begsr
      *
     C                   if        *inof = *on
     C                   exsr      $h1hdr
     C                   endif
      *
     C                   except    l1dtl
      *
      * If there was a "Comment" then print a blank line.
      *
     C                   if        l1comtext <> *blank
     C                   except    blank
     C                   endif
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Print report heading
      *---------------------------------------------------------------
      *
     C     $h1hdr        begsr
      *
     C                   except    h1hdr
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Make comparisons to User's selections
      *---------------------------------------------------------------
      *
     C     $select       begsr
      *
      * User-controlled
     C                   if        lducfl <> *blank and
     C                             lducfl <> l1ucfl
     C                   move      no            procfl
     C                   endif
      * Frequency
     C                   if        ldcfqcd <> *blank and
     C                             ldcfqcd <> l1cfqcd
     C                   move      no            procfl
     C                   endif
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Retrieve Invoice Info
      *---------------------------------------------------------------
      *
     C     $invoice      begsr
      *
     C     icinsn        chain     tfp020                             92
     C                   if        *in92 = *off
      *
      * Flip Invoice Date from CCYYMMDD to MMDDYY
      *
     C                   if        ihindt <> 0
     C     *iso          test(d)                 ihindt                 92
     C                   if        *in92 = *off                                 If OK date
     C                   move      ihindt        wkisodate
     C     *mdy          move      wkisodate     l1indtmdy
     C                   endif                                                  If OK date
     C                   endif
      *
      * Flip Posted Date from CCYYMMDD to MMDDYY for display
      *
     C                   if        ihpodt <> 0
     C     *iso          test(d)                 ihpodt                 92
     C                   if        *in92 = *off                                 If OK date
     C                   move      ihpodt        wkisodate
     C     *mdy          move      wkisodate     l1podtmdy
     C                   endif                                                  If OK date
     C                   endif
     C                   endif
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Retrieve time for report heading.
      *
     C                   time                    rtime
      *
      * Set up heading selections:
      *
      * Include Posted Charges flag
      *
     C                   select
     C                   when      ldynfl = yes
     C                   eval      h1ynds = 'Yes'
     C                   other
     C                   eval      h1ynds = 'No '
     C                   endsl
      *
      * From date
     C                   if        ldfmdy = 0
     C                   seton                                        88
     C                   endif
      *
      * To date
     C                   if        ldtmdy = 0
     C                   seton                                        89
     C                   endif
      *
      * Invoice number
     C                   if        ldinsn = 0
     C                   seton                                        82
     C                   endif
      *
      * Invoice charge number
      *
     C                   if        ldicsn = 0
     C                   seton                                        83
     C                   endif
      *
      * Invoice Status
     C                   if        ldinstcd = *blank
     C                   seton                                        81
     C                   else
      *
     C     ldinstcd      chain     tfp097                             92
     C                   if        *in92 = *off
     C                   move      ststds        h1stds
     C                   endif
     C                   endif
      *
      * Charge Code
     C                   if        ldcccd = *blank
     C                   seton                                        80
     C                   else
      *
     C     ldcccd        chain     tfp005                             92
     C                   if        *in92 = *off
     C                   move      ccccds        h1ccds
     C                   endif
     C                   endif
      *
      *
      * Charge Frequency Code
      *
     C                   if        ldcfqcd = *blank
     C                   seton                                        84
     C                   else
      *
     C     ldcfqcd       chain     tfp098                             92
     C                   if        *in92 = *off
     C                   move      cfcfqds       h1cfqds
     C                   endif
     C                   endif
      *
      * User Controlled flag
      *
     C                   if        lducfl = *blank
     C                   seton                                        85
     C                   else
      *
     C                   select
     C                   when      lducfl = yes
     C                   eval      h1ucds = 'Yes'
     C                   other
     C                   eval      h1ucds = 'No '
     C                   endsl
     C                   endif
      *
      * Include on Invoice Total Flag
      *
     C                   if        ldinfl = *blank
     C                   seton                                        86
     C                   else
      *
     C                   select
     C                   when      ldinfl = yes
     C                   eval      h1inds = 'Yes'
     C                   other
     C                   eval      h1inds = 'No '
     C                   endsl
     C                   endif
      *
      * System Module Code
      *
     C                   if        ldsmcd = *blank
     C                   seton                                        87
     C                   else
      *
     C     ldsmcd        chain     tfp006                             92
     C                   if        *in92 = *off
     C                   move      smsmds        h1smds
     C                   endif
     C                   endif
      *
     C                   endsr
      /EJECT
      *-------------------------------------------------------------
      * Report heading lines
      *-------------------------------------------------------------
      *
     Oprint132  e            h1hdr          1 01
     O                       sdpgm               10
     O                                           80 'LISTING OF INVOICE CHARGES'
     O                                          122 'DATE'
     O                       udate         y    132
      *
     O          e            h1hdr          1
     O                       sdusr               10
     O                                          122 'TIME'
     O                       rtime              132 '  :  :  '
      *
      *
     O          e            h1hdr          1
     O                                          122 'PAGE'
     O                       page          z    132
      *
     O          e            h1hdr          1
     O                                           26 'Include posted charges ...'
     O                       h1ynds              31
      *
     O                                           69 'Charge status code .....'
     O                                           75 '.....:'
     O               81                          80 'All'
     O              n81      ldinstcd            78
     O              n81      h1stds              99
      *
      *
     O          e            h1hdr          1
     O                                           26 'From invoice charge date..'
     O              n88      ldfmdy              36 '  /  /  '
     O               88                          31 'All'
      *
     O                                           69 'Charge frequency code ..'
     O                                           75 '.....:'
     O               84                          80 'All'
     O              n84      ldcfqcd             78
     O              n84      h1cfqds             99
      *
     O          e            h1hdr          1
     O                                           26 'To invoice charge date ...'
     O              n89      ldtmdy              36 '  /  /  '
     O               89                          31 'All'
      *
     O                                           69 'User-controlled flag ...'
     O                                           75 '.....:'
     O               85                          80 'All'
     O              n85      lducfl              78
     O              n85      h1ucds              92
      *
     O          e            h1hdr          1
     O                                           26 'Invoice number ...........'
     O               82                          31 'All'
     O              n82      ldinsn        z     35
      *
     O                                           69 'System module code .....'
     O                                           75 '.....:'
     O               87                          80 'All'
     O              n87      ldsmcd              79
     O              n87      h1smds             109
      *
     O          e            h1hdr          1
     O                                           26 'Invoice charge number ....'
     O               83                          31 'All'
     O              n83      ldicsn        z     35
      *
     O                                           69 'Include in invoice total'
     O                                           75 ' flag:'
     O               86                          80 'All'
     O              n86      ldinfl              78
     O              n86      h1inds              92
      *
     O          e            h1hdr          2
     O                                           69 'Charge code ............'
     O                                           75 '.....:'
     O               80                          80 'All'
     O              n80      ldcccd              87
     O              n80      h1ccds             114
      *
      *
      *-------------------------------------------------------------
      * Column headings
      *-------------------------------------------------------------
      *
     O          e            h1hdr       0  1
     O                                           57 'Include in'
     O                                          132 'User'
      *
     O          e            h1hdr       0  1
     O                                            7 'Invoice'
     O                                           16 'Invoice'
     O                                           26 'Posted'
     O                                           35 'Status'
     O                                           45 'Charge'
     O                                           56 'Invoice'
     O                                           65 'System'
     O                                           73 'Charge'
     O                                           86 'Charge'
     O                                          102 'Charge'
     O                                          126 'Freq'
     O                                          132 'Cont'
      *
     O          e            h1hdr       0  1
     O                                            7 'Number'
     O                                           15 'Date'
     O                                           25 'Date'
     O                                           34 'Code'
     O                                           45 'Number'
     O                                           55 'Total'
     O                                           65 'Module'
     O                                           71 'Code'
     O                                           85 'Date'
     O                                          102 'Amount'
     O                                          118 'Units'
     O                                          126 'Code'
     O                                          132 'Flag'
      *
     O          e            h1hdr          1
     O                       dash               132
      *
      *
      *-------------------------------------------------------------
      * Detail line
      *-------------------------------------------------------------
      *
     O          e            l1dtl          1
     O                       icinsn        z      7
     O                       l1indtmdy      b    17 '  /  /  '
     O                       l1podtmdy      b    27 '  /  /  '
     O                       icinstcd            33
     O                       icicsn        z     45
     O                       icinfl              53
     O                       icsmcd              63
     O                       iccccd              77
     O                       l1icdtmdy      b    87 '  /  /  '
     O                       icicam        k    103
     O                       icunit        k    119
     O                       l1cfqcd        b   124
     O                       l1ucfl         b   131
      *
     O          e            l1dtl          1
     O                       l1comtext          106
     O                       iciccom            132
      *
      *-------------------------------------------------------------
      * Blank line
      *-------------------------------------------------------------
      *
     O          e            blank       1
      *
      *
      *-------------------------------------------------------------
      * No data message line
      *-------------------------------------------------------------
      *
     O          e            nodata      1
     O                                           19 'No data meets your'
     O                                           39 'selection criteria.'
