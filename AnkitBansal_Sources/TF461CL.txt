/***************************************************************** */
/*                                                                 */
/*  SYSTEM:          TRIUMPH FOODS                                 */
/*  PROGRAM NUMBER:  TF461CL                                       */
/*  PROGRAM NAME:    AR ADJUSTMENT REPORT BY POST G/L ACCOUNT      */
/*  PROGRAMMER:      LEANNE RAMSEY                                 */
/*  CREATE DATE:     10/06/09                                      */
/*                                                                 */
/*  FUNCTION:        THESE DETAIL/SUMMARY REPORTS ARE GENERATED    */
/*                   ON-DEMAND ONLY.                               */
/*                                                                 */
/*******************************************************************/

             PGM

             DCL        VAR(&QDATA) TYPE(*CHAR) LEN(170)

             DCL        VAR(&OUTQ) TYPE(*CHAR) LEN(10)
             DCL        VAR(&HOLD) TYPE(*CHAR) LEN(4)
             DCL        VAR(&SAVE) TYPE(*CHAR) LEN(4)
             DCL        VAR(&COPY) TYPE(*DEC)  LEN(1)

             DCL        VAR(&LDFWEDT)   TYPE(*DEC)  LEN(8)
             DCL        VAR(&LDTWEDT)   TYPE(*DEC)  LEN(8)
             DCL        VAR(&LDPMCD)    TYPE(*CHAR) LEN(1)
             DCL        VAR(&LDPCONO)   TYPE(*DEC)  LEN(3)
             DCL        VAR(&LDPCC)     TYPE(*CHAR) LEN(12)
             DCL        VAR(&LDPOBJ)    TYPE(*CHAR) LEN(6)
             DCL        VAR(&LDPSUB)    TYPE(*CHAR) LEN(8)
             DCL        VAR(&LDDSFL)    TYPE(*CHAR) LEN(1)

             CHGVAR     VAR(&LDDSFL)    VALUE(%SST(*LDA 140 1))
             CHGVAR     VAR(&LDFWEDT)   VALUE(%SST(*LDA 52 8))
             CHGVAR     VAR(&LDTWEDT)   VALUE(%SST(*LDA 60 8))
             CHGVAR     VAR(&LDPCONO)   VALUE(%SST(*LDA 71 3))
             CHGVAR     VAR(&LDPCC)     VALUE(%SST(*LDA 77 12))
             CHGVAR     VAR(&LDPOBJ)    VALUE(%SST(*LDA 89 6))
             CHGVAR     VAR(&LDPSUB)    VALUE(%SST(*LDA 95 8))
             CHGVAR     VAR(&LDPMCD)    VALUE(%SST(*LDA 141 1))

/*----------------------------------------------------------------*/
/* RETRIEVE/SET PRINT DEFAULTS                                    */
/*----------------------------------------------------------------*/

 OUTQ:       CHGVAR     VAR(&OUTQ) VALUE(%SST(*LDA 401 10))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(*JOB))

 HOLD:       CHGVAR     VAR(&HOLD) VALUE(%SST(*LDA 411 4))
             IF         COND(&HOLD *NE '*YES' *AND &HOLD *NE '*NO ') +
                          THEN(CHGVAR VAR(&HOLD) VALUE(*NO))

 SAVE:       CHGVAR     VAR(&SAVE) VALUE(%SST(*LDA 415 4))
             IF         COND(&SAVE *NE '*YES' *AND &SAVE *NE '*NO ') +
                          THEN(CHGVAR VAR(&SAVE) VALUE(*NO))

 COPY:       CHGVAR     VAR(&COPY) VALUE(%SST(*LDA 419 1))
             IF         COND(&COPY = 0) THEN(CHGVAR VAR(&COPY) +
                          VALUE(1))

/*--------------------------------------------------------------------------*/
/*  Extract AR Adjustment G/L Post records                                  */
/*--------------------------------------------------------------------------*/

             CHGVAR     VAR(&QDATA) VALUE(' ')

             CHGVAR     VAR(%SST(&QDATA 1 11)) VALUE('APRWEDT *GE')
             CHGVAR     VAR(%SST(&QDATA 13 8)) VALUE(&LDFWEDT)
             CHGVAR     VAR(%SST(&QDATA 22 16)) VALUE('*AND APRWEDT +
                          *LE')
             CHGVAR     VAR(%SST(&QDATA 39 8)) VALUE(&LDTWEDT)

/* IF COMPANY                                                       */
             IF         COND(&LDPCONO *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 48 16)) VALUE('*AND APPCONO *EQ')
             CHGVAR     VAR(%SST(&QDATA 65 3)) VALUE(&LDPCONO)
             ENDDO

/* IF COST CENTER                                                   */

             IF         COND(&LDPCC *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 69 16)) VALUE('*AND APPCC *EQ "')
             CHGVAR     VAR(%SST(&QDATA 85 12)) VALUE(&LDPCC)
             CHGVAR     VAR(%SST(&QDATA 97 1)) VALUE('"')
             ENDDO

/* IF OBJECT                                                        */
             IF         COND(&LDPOBJ *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 99 17)) VALUE('*AND APPOBJ *EQ "')
             CHGVAR     VAR(%SST(&QDATA 116 6)) VALUE(&LDPOBJ)
             CHGVAR     VAR(%SST(&QDATA 122 1)) VALUE('"')
             ENDDO

/* IF SUBSIDIARY                                                    */
             IF         COND(&LDPSUB *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 124 17)) VALUE('*AND APPSUB *EQ "')
             CHGVAR     VAR(%SST(&QDATA 141 8)) VALUE(&LDPSUB)
             CHGVAR     VAR(%SST(&QDATA 149 1)) VALUE('"')
             ENDDO

/* IF POST METHOD                                                   */
             IF         COND(&LDPMCD *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 151 17)) VALUE('*AND APPMCD *EQ "')
             CHGVAR     VAR(%SST(&QDATA 168 1)) VALUE(&LDPMCD)
             CHGVAR     VAR(%SST(&QDATA 169 1)) VALUE('"')
             ENDDO

             OVRDBF     FILE(TFP025) SHARE(*YES)
             OPNQRYF    FILE((TFP025)) QRYSLT(&QDATA) KEYFLD(*FILE) +
                          SEQONLY(*YES)

/*--------------------------------------------------------------------------*/
/*  Run report                                                              */
/*--------------------------------------------------------------------------*/

             OVRPRTF    FILE(PRINT198) OUTQ(&OUTQ) COPIES(&COPY) +
                          HOLD(&HOLD) SAVE(&SAVE)

/* Detail */
             IF         COND(&LDDSFL *EQ 'D' *OR &LDDSFL *EQ 'B') +
                          THEN(DO)
             CALL       PGM(TF661) PARM('D')
             ENDDO

/* Summary */
             IF         COND(&LDDSFL *EQ 'S' *OR &LDDSFL *EQ 'B') +
                          THEN(DO)
             CALL       PGM(TF661) PARM('S')
             ENDDO

             CLOF       OPNID(TFP025)
             DLTOVR     FILE(*ALL)

             ENDPGM

