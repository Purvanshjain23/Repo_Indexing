     A*%%TS  SD  20090929  082912  ISLRAMS     REL-V5R4M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION
     A*  DISPLAY FILE:  HPD1067
     A*  TITLE:         CREATE MULTIPLE SCHEDULED SALES MOVEMENTS
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  DATE:          11/24/99
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A                                      CF04(04 'PROMPT')
     A                                      CF05(05 'REFRESH')
     A                                      CF09(09 'ACCEPT')
     A                                      CF13(13 'FOLD/DROP')
     A*---------------------------------------------------------------
     A* SUBFILE FORMAT 1
     A*---------------------------------------------------------------
     A*
     A          R HP1067S1                  SFL
     A*%%TS  SD  20090929  082912  ISLRAMS     REL-V5R4M0  5722-WDS
     A  85                                  SFLNXTCHG
     A            HDRRN1         4Y 0H
     A            HDHGSN    R        H      REFFLD(HGHGSN)
     A            HDSCDT    R        H      REFFLD(@@DT08)
     A            HDSPLIT   R        H      REFFLD(@@FLAG)
     A            D1MDY     R        B  9  3REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A  37                                  DSPATR(PC)
     A  37                                  DSPATR(RI)
     A N37                                  DSPATR(HI)
     A N37                                  DSPATR(UL)
     A            D1FSCD    R        B  9 14REFFLD(@@FSCD)
     A  31
     AO 51                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A N31                                  DSPATR(HI)
     A N31                                  DSPATR(UL)
     A            D1BLCD    R        B  9 22REFFLD(@@BLCD)
     A  35
     AO 55                                  DSPATR(PC)
     A  35                                  DSPATR(RI)
     A N35                                  DSPATR(HI)
     A N35                                  DSPATR(UL)
     A            D1RMCD    R        B  9 29REFFLD(@@RMCD)
     A  30
     AO 50                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A N30                                  DSPATR(HI)
     A N30                                  DSPATR(UL)
     A            D1HGCD    R        B  9 36REFFLD(HGHGCD)
     A  34
     AO 54                                  DSPATR(PC)
     A  34                                  DSPATR(RI)
     A N34                                  DSPATR(HI)
     A N34                                  DSPATR(UL)
     A            D1SCHD    R        B  9 46REFFLD(MDSCHD)
     A                                      EDTCDE(K)
     A  38                                  DSPATR(PC)
     A  38                                  DSPATR(RI)
     A N38                                  DSPATR(HI)
     A N38                                  DSPATR(UL)
     A            D1LTCD    R        B  9 57REFFLD(HSRREF/@@LTCD *LIBL/HSPREF)
     A  33
     AO 53                                  DSPATR(PC)
     A  33                                  DSPATR(RI)
     A N33                                  DSPATR(HI)
     A N33                                  DSPATR(UL)
     A            D1GSCD    R        O  9 65REFFLD(HGGSCD)
     A            D1MVSN    R        O  9 72REFFLD(@@MVSN)
     A                                      EDTCDE(Z)
     A            D1FSNM    R        O 10 14REFFLD(FSFSNM)
     A                                      COLOR(BLU)
     A            D1FSBO    R        H      REFFLD(FSFSBO)
     A*
     A*----------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT  1
     A*----------------------------------------------------------------
     A*
     A          R HP1067C1                  SFLCTL(HP1067S1)
     A*%%TS  SD  20090820  164824  ISLRAMS     REL-V5R4M0  5722-WDS
     A                                      SFLSIZ(0100)
     A                                      SFLPAG(0006)
     A N82                                  ROLLUP(25 'PAGE FORWARD')
     A                                      SFLCSRRRN(&HDRR1)
     A                                      OVERLAY
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND(*MORE)
     A  78                                  SFLFOLD(CF13)
     A  79                                  SFLDROP(CF13)
     A                                      SFLMODE(&FOLD)
     A                                      SETOF(30)
     A                                      SETOF(31)
     A                                      SETOF(32)
     A                                      SETOF(33)
     A                                      SETOF(34)
     A                                      SETOF(35)
     A                                      SETOF(37)
     A                                      SETOF(38)
     A                                      SETOF(50)
     A                                      SETOF(51)
     A                                      SETOF(52)
     A                                      SETOF(53)
     A                                      SETOF(54)
     A                                      SETOF(55)
     A                                      RTNCSRLOC(&RCD &FLD)
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            HDRR1          5S 0H
     A            FOLD           1A  H
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 31'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A            D1MODE    R        O  2  2REFFLD(@@MODE)
     A                                      DSPATR(HI)
     A                                  2 24'Edit (Maintain) Uploaded Cull Sale-
     A                                      s'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  3  2'Business Office.....'
     A                                      COLOR(BLU)
     A            SDBOBO         5A  O  3 23
     A                                      DSPATR(HI)
     A            SDBODS        30A  O  3 30
     A                                      COLOR(BLU)
     A                                  4  2'Customer number ..'
     A                                  4 21'?'
     A            C1CVNO    R        B  4 23REFFLD(SHREC/SHCVNO *LIBL/HSP084)
     A  32
     AO 52                                  DSPATR(PC)
     A  32                                  DSPATR(RI)
     A N32                                  DSPATR(HI)
     A N32                                  DSPATR(UL)
     A            C1CNAM    R        O  4 34REFFLD(@@DS30)
     A                                  6 15'?'
     A                                  6 23'?'
     A                                  6 30'?'
     A                                  6 38'?'
     A                                  6 57'?'
     A                                  7  2'Scheduled'
     A                                  7 47'Sched'
     A                                  7 56'Load'
     A                                  7 63'Group'
     A                                  7 72'Movement'
     A                                  8  2'Ship Date'
     A                                  8 14'Farm'
     A                                  8 22'Bldg'
     A                                  8 29'Room'
     A                                  8 36'Group'
     A                                  8 48'Head'
     A                                  8 56'Type'
     A                                  8 63'Status'
     A                                  8 73'Number'
     A*----------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*----------------------------------------------------------------
     A          R HP1067K1
     A*%%TS  SD  19991111  095829  ISLRAMS     REL-V4R2M0  5769-PW1
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A                                 23 11'F4=Prompt'
     A                                 23 22'F5=Refresh'
     A                                 23 34'F9=Accept'
     A                                 23 59'F13=Fold/Unfold'
      *----------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *----------------------------------------------------------------
     A          R HP1067ES                  SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
     A*----------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*----------------------------------------------------------------
     A*
     A          R HP1067EC                  SFLCTL(HP1067ES)
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
