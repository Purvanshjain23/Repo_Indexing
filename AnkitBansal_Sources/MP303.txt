      *****************  RPG PROGRAM HEADING  ************************
      *
      * ENVIRONMENT:  PORK DIVISION
      * SYSTEM:       AS/400
      * PROGRAM:      MP303
      * DESCRIPTION:  MP2: Build Workfile for JDE Posting of Receipts
      * PROGRAMMER:   LeAnne Fedor
      * DATE:         03/19/02
      *
      *
      * FUNCION:     This program reads each record in the P.O. Detail file. It writes each
      *              record that is A) error-free and B) not posted to a workfile that
      *              will be used for posting into JDE.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      * 08/09/04  Alice
      *           Changed to use MPL103B to exclude POSTED to GL
      *           PO Detail records.  The original code was using
      *           the Physcial (MPP103) and reading the entire file
      *           every time and searching for P1PDPOFK = "N".
      *
      * 03/30/11  Barb Gutierrez
      *           Recompile only due to field size changes in database.  E001398
      *
      * 03/05/14  LeAnne Ramsey (E2992)
      *           Added new Accounting Company field to the workfile...which now has
      *           Accounting Company as the primary key.
      *
      * 03/26/19  Brad baden
      *           Recompile only due to field size changes in database.
      *
      * 05/24/21  Danny Nguyen   (S17068)
      *           DBFC on MPP103 & MPP303 files. PDITEMRT field length changed
      *           from 9.4 to 11.4.
      *           Recompile only.
      *
      /eject
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fmpp102    if   e           k disk
      *  MP2: Purchase order header
      *
      *
     Fmpl103b   if   e           k disk    rename(pdrec:pdrec1) prefix(p1)
      *  MP2: Purchase order detail
      *
      *
     Fmpp303    o    e           k disk
      *  MP2: Workfile of Receipts to Post to JDE
      *
      /eject
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Parm fields
      *
     D xxacono         s                   like(pdacono)
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * MAINLINE
      ****************************************************************
      *
      * Process each record in the Purchase Order Detail file for the
      * incoming Accounting Company. If the record is error-free and has not
      * yet been posted to JDE, write it to the workfile. If the incoming
      * Company is zero, this has been called from the Job Scheduler job and
      * we want to process all companies.
      *
     C     xxacono       setll     mpl103b
      *
     C                   dou       *in90 = *on                                  Main do
      *
     C                   if        xxacono = 0
     C                   read      mpl103b                                90
     C                   else
     C     xxacono       reade     mpl103b                                90
     C                   endif
      *
     C                   if        *in90 = *off and                             If not EOF
     C                             p1pderfl = no
      *
      * Clear record format
      *
     C                   clear                   pdrec
      *
     C                   z-add     p1pdacono     pdacono
     C                   move      p1pdcono      pdcono
     C                   z-add     p1pdposn      pdposn
     C                   move      p1pdttcd      pdttcd
     C                   z-add     p1pdtrdt      pdtrdt
     C                   z-add     p1pdrcno      pdrcno
     C                   z-add     p1pditemrt    pditemrt
     C                   z-add     p1pdserno     pdserno
     C                   z-add     p1pdseqno     pdseqno
     C                   z-add     p1pdtrqt      pdtrqt
     C                   move      p1pdmcu       pdmcu
     C                   move      p1pdobj       pdobj
     C                   move      p1pdsub       pdsub
      *
      * Retrieve vendor number and purchase order number from P.O. Header record.
      *
     C     p1pdposn      chain     mpp102                             92
     C                   if        *in92 = *off                                 If found
     C                   move      phpono        pdpono
     C                   z-add     phjdvnno      pdjdvnno
     C                   move      phjdvnnm      pdjdvnnm
     C                   endif                                                  If found
      *
     C                   write     pdrec
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Main do
      *
     C                   seton                                        LR
      /eject
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
     C     *entry        plist
     C                   parm                    xxacono
      *
     C                   endsr
      /eject
