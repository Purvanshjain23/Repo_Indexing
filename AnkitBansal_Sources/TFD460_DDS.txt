     A*%%TS  SD  20091015  153648  ISLRAMS     REL-V5R4M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        TRIUMPH FOODS
     A*  DISPLAY FILE:  TFD460
     A*  TITLE:         SPECIFY LISTING OF ADJUSTMENTS FROM THE A/R SYSTEM
     A*  PROGRAMMER:    LEANNE RAMSEY
     A*  CREATED:       05/29/08
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/TFPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A                                      CF04(04 'PROMPT')
     A                                      CF08(08 'SUBMIT')
     A                                      CF17(17 'PRINT OPTIONS')
     A                                      CF20(20 'WRKSPLF')
     A*
     A*---------------------------------------------------------------
     A* RECORD FORMAT ONE
     A*---------------------------------------------------------------
     A          R TF460R1
     A*%%TS  SD  20091015  153648  ISLRAMS     REL-V5R4M0  5722-WDS
     A                                      SETOF(30)
     A                                      SETOF(31)
     A                                      SETOF(32)
     A                                      SETOF(33)
     A                                      SETOF(34)
     A                                      SETOF(35)
     A                                      SETOF(36)
     A                                      SETOF(53)
     A                                      SETOF(54)
     A                                      SETOF(55)
     A                                      SETOF(56)
     A                                      OVERLAY
     A                                      RTNCSRLOC(&RCD &FLD)
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 18'SPECIFY OPTIONS FOR LISTING ADJUST-
     A                                      MENTS'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USR)
     A                                      DSPATR(HI)
     A                                  2 20'FROM THE ACCOUNTS RECEIVABLE SYSTE-
     A                                      M'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@JOB)
     A                                      DSPATR(HI)
     A                                  4  2'From payment/deposit date ..'
     A            D1FMDY    R        B  4 33REFFLD(TFREF/@@DT06 *LIBL/TFPREF)
     A                                      EDTWRD('  /  /  -')
     A  30                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A N30                                  DSPATR(HI)
     A                                  5  2'To payment/deposit date ....'
     A            D1TMDY    R        B  5 33REFFLD(TFREF/@@DT06 *LIBL/TFPREF)
     A                                      EDTWRD('  /  /  -')
     A  31                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A N31                                  DSPATR(HI)
     A                                  6  2'Exempt/Non-Exempt/Both .....'
     A                                  6 31'?'
     A            D1IECD    R        B  6 33REFFLD(TFREF/@@IECD *LIBL/TFPREF)
     A N33                                  DSPATR(HI)
     A  33
     AO 53                                  DSPATR(PC)
     A  33                                  DSPATR(RI)
     A            D1IEDS    R        O  6 46REFFLD(TFREF/@@IEDS *LIBL/TFPREF)
     A                                  8  2'Company .......................'
     A            D1CONO    R        B  8 36REFFLD(TFREF/@@CONO *LIBL/TFPREF)
     A                                      EDTCDE(K)
     A  32                                  DSPATR(PC)
     A  32                                  DSPATR(RI)
     A N32                                  DSPATR(HI)
     A                                  8 70'(Blank=All)'
     A                                 10  2'Adjustment type ...............'
     A            D1AJTY    R        B 10 36REFFLD(TFREF/@@AJTY *LIBL/TFPREF)
     A                                      DSPATR(HI)
     A                                 10 70'(Blank=All)'
     A                                 11  2'Exclude/Only print ............'
     A                                 11 34'?'
     A            D1AJACT   R        B 11 36REFFLD(TFREF/@@IECD *LIBL/TFPREF)
     A N34                                  DSPATR(HI)
     A  34
     AO 54                                  DSPATR(PC)
     A  34                                  DSPATR(RI)
     A            D1AJACTDS     30A  O 11 46
     A                                 13  2'ITC-DTFs Include/Exclude/Only .'
     A                                 13 34'?'
     A            D1DTFFL   R        B 13 36REFFLD(TFREF/@@IECD *LIBL/TFPREF)
     A N36                                  DSPATR(HI)
     A  36
     AO 56                                  DSPATR(PC)
     A  36                                  DSPATR(RI)
     A            D1DTFDS        7A  O 13 46
     A                                 15  2'A/R customer number ...........'
     A                                 15 34'?'
     A            D1CUNO    R        B 15 36REFFLD(TFREF/@@ARCUNO *LIBL/TFPREF)
     A                                      EDTCDE(M)
     A  35
     AO 55                                  DSPATR(PC)
     A  35                                  DSPATR(RI)
     A N35                                  DSPATR(HI)
     A            D1CUNM        23A  O 15 46
     A                                 15 70'(Blank=All)'
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R TF460K1
     A*%%TS  SD  20080124  092007  ISLRAMS     REL-V5R4M0  5722-WDS
     A  43                             21  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A                                 23 11'F4=Prompt'
     A                                 23 22'F8=Submit'
     A                                 23 33'F17=Print Options'
     A                                 23 52'F20=Spool Files'
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *---------------------------------------------------------------
     A          R TF460ES                   SFL SFLMSGRCD(24)
      *
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
     A*---------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*---------------------------------------------------------------
     A*
     A          R TF460EC                   SFLCTL(TF460ES)
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
