     A*%%TS  SD  20041012  164154  ISLRAMS     REL-V5R2M0  5722-WDS
     A*****************************************************************
     A*
     A*  SYSTEM:        HOG PRODUCTION
     A*  DISPLAY FILE:  HPD5050 - SELECT SCHEDULED SALES MOVEMENT
     A*  CREATED BY:    LEANNE FEDOR
     A*  DATE:          10/12/04
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      PRINT
     A                                      CF04(04 'PROMPT')
     A                                      CA12(12 'PREVIOUS')
     A N82                                  ROLLUP(25 'PAGE FORWARD')
     A*---------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP5050S1                  SFL
     A*%%TS  SD  20041012  164154  ISLRAMS     REL-V5R2M0  5722-WDS
     A            HDRRN1    R        H      REFFLD(@@RRN)
     A            D1SCMDY   R        O  9  1REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  ')
     A            D1MVSN    R        O  9 12REFFLD(@@MVSN)
     A                                      EDTCDE(M)
     A            D1STCD    R        O  9 22REFFLD(HSRREF/@@STCD *LIBL/HSPREF)
     A            D1TOCD    R        O  9 29REFFLD(@@TOCD)
     A            D1TICD    R        O  9 37REFFLD(HSRREF/@@TICD *LIBL/HSPREF)
     A            D1CVNO    R        O  9 45REFFLD(HSRREF/SHCVNO *LIBL/HSPREF)
     A                                      EDTCDE(M)
     A            D1CELL    R        O  9 57REFFLD(HSRREF/@@CELL *LIBL/HSPREF)
     A                                      EDTCDE(Z)
     A            D1FSCD    R        O  9 62REFFLD(@@FSCD)
     A                                      EDTCDE(Z)
     A            D1PTCT    R        O  9 71REFFLD(HSRREF/@@PTCT *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A*
     A*---------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP5050C1                  SFLCTL(HP5050S1)
     A*%%TS  SD  20041012  164154  ISLRAMS     REL-V5R2M0  5722-WDS
     A                                      SFLSIZ(0100)
     A                                      SFLPAG(0009)
     A                                      WINDOW(HP5050EC)
     A                                      SETOF(50)
     A                                      SETOF(51)
     A                                      SETOF(52)
     A                                      SETOF(53)
     A                                      SETOF(54)
     A                                      RTNCSRLOC(&RCD &FLD)
     A                                      OVERLAY
     A                                      SFLCSRRRN(&HDRRN)
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A            HDPAG1    R        H      REFFLD(@@PAG)
     A                                      SFLRCDNBR
     A            RCD           10A  H
     A            FLD           10A  H
     A            HDRRN     R        H      REFFLD(@@RRN)
     A            SDPGM     R        O  1  1REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 24'SELECT SCHEDULED SALES MOVEMENT'
     A                                      DSPATR(HI)
     A                                  3  1'Position Cursor, Press Enter'
     A                                  5 24'?'
     A                                      DSPATR(HI)
     A                                  5 31'?'
     A                                      DSPATR(HI)
     A                                  5 39'?'
     A                                      DSPATR(HI)
     A                                  5 48'?'
     A                                      DSPATR(HI)
     A                                  5 64'?'
     A                                      DSPATR(HI)
     A                                  6  1'Scheduled'
     A                                      DSPATR(HI)
     A                                  6 12'Movement'
     A                                      DSPATR(HI)
     A                                  6 22'Sales'
     A                                      DSPATR(HI)
     A                                  6 29'Tran'
     A                                      DSPATR(HI)
     A                                  6 45'Customer'
     A                                      DSPATR(HI)
     A                                  6 63'Farm'
     A                                      DSPATR(HI)
     A                                  6 70'Print'
     A                                      DSPATR(HI)
     A                                  7  1'Ship Date'
     A                                      DSPATR(HI)
     A                                  7 12'Number'
     A                                      DSPATR(HI)
     A                                  7 22'Type'
     A                                      DSPATR(HI)
     A                                  7 29'Office'
     A                                      DSPATR(HI)
     A                                  7 37'Truckr'
     A                                      DSPATR(HI)
     A                                  7 47'Number'
     A                                      DSPATR(HI)
     A                                  7 56'Cell'
     A                                      DSPATR(HI)
     A                                  7 63'Site'
     A                                      DSPATR(HI)
     A                                  7 70'Count'
     A                                      DSPATR(HI)
     A*
     A            C1SCMDY   R        B  8  1REFFLD(@@DT06)
     A                                      DSPATR(HI)
     A                                      EDTWRD('  /  /  -')
     A            C1STCD    R        B  8 22REFFLD(HSRREF/@@STCD *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A  50                                  DSPATR(PC)
     A            C1TOCD    R        B  8 29REFFLD(HSRREF/@@TOCD *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A  51                                  DSPATR(PC)
     A            C1TICD    R        B  8 37REFFLD(HSRREF/@@TICD *LIBL/HSPREF)
     A  53                                  DSPATR(PC)
     A                                      DSPATR(HI)
     A            C1CVNO    R        B  8 45REFFLD(HSRREF/SHCVNO *LIBL/HSPREF)
     A                                      EDTCDE(M)
     A                                      DSPATR(HI)
     A  52                                  DSPATR(PC)
     A            C1CELL    R        B  8 57REFFLD(HSRREF/@@CELL *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A            C1FSCD    R        B  8 62REFFLD(@@FSCD)
     A  54                                  DSPATR(PC)
     A                                      DSPATR(HI)
     A                                      EDTCDE(M)
     A*---------------------------------------------------------------
     A* COMMAND LINE LINE
     A*---------------------------------------------------------------
     A          R HP5050K1
     A                                      WINDOW(HP5050EC)
     A                                      OVERLAY
     A  43                             18  1'Message program failed. Call IS.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 19  1'F4=Prompt'
     A                                 19 12'F12=Previous'
      *
     A*-------------------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
     A*-------------------------------------------------------------------------
     A          R HP5050ES                  SFL SFLMSGRCD(19)
      *
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
     A*
     A*-------------------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*-------------------------------------------------------------------------
     A*
     A          R HP5050EC                  SFLCTL(HP5050ES)
     A*%%TS  SD  19980327  105628  ISLRAMS     REL-V3R7M0  5716-PW1
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A                                      WINDOW(*DFT 20 75)
     A                                      WDWBORDER((*DSPATR RI) (*CHAR '    -
     A                                          '))
     A            MSGPGM                    SFLPGMQ(10)
     A*
     A*---------------------------------------------------------------
     A* ASSUME FORMAT -- REQUIRED FOR WINDOWS
     A*---------------------------------------------------------------
     A          R ASSUME
     A*%%TS  SD  20011025  160559  ISLRAMS     REL-V4R4M0  5769-PW1
     A                                      ASSUME
     A                                      OVERLAY
     A                                 24 79' '
