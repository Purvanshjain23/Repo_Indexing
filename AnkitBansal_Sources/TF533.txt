      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Resource Optimization
      * PROGRAM:     TF533 - Select A/R Customer
      * PROGRAMMER:  LeAnne Ramsey
      * CREATED:     08/27/09
      *
      *
      * FUNCTION: This is a select function window display of the
      *           records in the Synon Customer master file.
      *
      *           A record is selected by positioning the cursor on the line
      *           and pressing enter.
      *
      *           Restrictors:
      *              none
      *
      *           Positioners:
      *              carrier code
      *
      *           Selectors:
      *              customer name
      *              status
      *
      * NOTE:    This is a pop-up window program.  Please note that in window
      *          programs the record format containing the window definition
      *          must be written first.  So, we write the error message subfile
      *          control record first followed by the command line record format.
      *          This is opposite of the non-window programs.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 09/10/09  LeAnne Ramsey
      *           Changed to select A/R Customers instead of ShipTo Customers.
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Ftfd533    cf   e             workstn sfile(tf533s1:hdrrn1)
     F                                     infds(iofeed)
      *
     Fcabbrel0  if   e           k disk
      *  Customer
      *
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
     D set1            c                   'SET1  '
     D edit1           c                   'EDIT1 '
     D scrn1           c                   'SCRN1 '
     D exit            c                   'EXIT  '
      *
      *
     D upper           c                   'ABCDEFGHIJKLMNOPQRSTUVWXYZ'
     D lower           c                   'abcdefghijklmnopqrstuvwxyz'
      *
      ****************************************************************
      * ARRAYS AND TABLES
      ****************************************************************
      *
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Standard fields
      *
     D pgm             s             10
     D msgfl           s             10
     D maxmsg          s              2  0  inz(20)
     D routne          s              6
     D maxrrn          s              4  0
     D norecs          s              1    inz('N')
     D reset           s              1    inz('N')
     D count           s              2  0
      *
      *
      * Workfields required for subfile paging--see initialization
      * subroutine.
      *
     D wkplin          s              2  0
     D wkrlin          s              2  0
     D wkflow          s                   like(idflow)
      *
      *
      * Control fields
      *
     D length          s              2  0
     D procfl          s              1
      *
     D svcuno          s                   like(c1cuno)
     D svcunm          s                   like(c1cunm)
     D svstat          s                   like(c1stat)
      *
      *
      * Workfields
      *
     D wkdesc          s                   like(c1cunm)
     D wkcunm          s                   like(c1cunm)
      *
      *
      * Parm fields
      *
     D xxcuno          s                   like(c1cuno)
     D xxcunm          s                   like(c1cunm)
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * standard error message handling data structures
      *---------------------------------------------------------------
      *
      * For error message handling, a packed index (3) is required.
      * For program readability, define a corresponding error count
      * field called 'error'
      *
     D                 ds                  inz
     D  error                         2p 0
     D   e                            2p 0 overlay(error)
      *
      * This data structure supplies the name of the message file to
      * the message handling CL program.  The field name MSGFIL must be
      * constant. The value in quotes is the name of the specific
      * message file containing the user defined messages.
      *
     D                 ds                  inz
     D  msgfil                       10    inz('HSMSGF    ')
      *                                          14
      *---------------------------------------------------------------
      *  Standard message data structures
      *---------------------------------------------------------------
      *
      * The following 3 data structures are used to speed message
      * handling since it is faster to clear data structures than
      * arrays.  Each is associated with a standard message array.
      *
     D mgi             ds                  inz
     D  mgid                          7    dim(20)
      *
     D mgd             ds                  inz
     D  mgdt                         50    dim(20)
      *
     D mgwk            ds                  inz
     D  mgw                           1    dim(50)
      *
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *---------------------------------------------------------------
      * Standard workstation information data structure
      *---------------------------------------------------------------
      *    externally defined as UTWSFR (record format: UTIDFRR)
     D iofeed        e ds                  extname(utwsfr)
      *
      *
      *---------------------------------------------------------------
      * Standard database file information data structure
      *---------------------------------------------------------------
      *    externally defined as UTDBGR (record format: FDBCKD)
     D dbfeed        e ds                  extname(utdbfr)
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * MAINLINE
      ****************************************************************
      *
     C                   eval      routne = set1
      *
     C     *inlr         doweq     *off
     C     routne        caseq     set1          $set1
     C     routne        caseq     scrn1         $scrn1
     C     routne        caseq     exit          $exit
     C                   endcs
     C                   enddo
      /EJECT
      *---------------------------------------------------------------
      * Set environment for screen 1
      *---------------------------------------------------------------
      *
     C     $set1         begsr
      *
      * Clear/fill the subfile
      *
     C                   exsr      $clr1
     C                   exsr      $frsh1
      *
      * Set routine to display screen
      *
     C                   eval      routne = scrn1
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Perform operator I/O for screen 1
      *---------------------------------------------------------------
      *
     C     $scrn1        begsr
      *
      * Write the error messages from the error arrays to the
      * error message subfile
      *
     C                   exsr      $wrmsg
      *
      * Write screen 1 to crt
      *
     C                   write     tf533ec
     C                   write     tf533k1
     C                   exfmt     tf533c1
      *
      * Clear the messages from the message data structures and
      * program message queue
      *
     C                   exsr      $clmsg
      *
      * Get user's entry and set next routine
      *
     C                   select
     C                   when      *in12 = *on                                  F12-previous
     C                   eval      routne = exit
      *
      * Reposition the subfile if the user has changed any control record values
      *
     C                   when      svcuno <> c1cuno  or
     C                             svcunm <> c1cunm  or
     C                             svstat <> c1stat
     C                   exsr      $chksv
      *
     C                   when      *in25 = *on                                  F25-rollup
     C                   exsr      $load1
     C                   eval      routne = scrn1
     C                   other
      *
      * User has pressed enter
      *
     C                   if        norecs = yes                                 If empty sfl
     C                   eval      routne = set1
     C                   else
     C                   exsr      $proc1
     C                   endif                                                  If empty sfl
     C                   endsl
      *
     C                   endsr
      /EJECT
      *----------------------------------------------------------------
      * Process user's selection
      *----------------------------------------------------------------
      *
     C     $proc1        begsr
      *
      * If cursor location returned is the rrn of a record in the
      * subfile, chain to the subfile record and load the data into
      * the parms to return to the calling program.
      *
     C                   if        hdrrn <> 0
     C                   eval      hdrrn1 = hdrrn
     C     hdrrn1        chain     tf533s1                            92
     C                   z-add     d1cuno        xxcuno
     C                   move      d1cunm        xxcunm
     C                   endif
      *
      * If the user selected a record, set the routine to exit.  If no record
      * was selected, redisplay the screen.
      *
     C                   if        xxcuno = 0
     c                   eval      routne = set1
     C                   else
     c                   eval      routne = exit
     C                   endif
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Clear subfile for screen 1
      *---------------------------------------------------------------
      *
     C     $clr1         begsr
      *
     C                   clear                   tf533s1
     C                   seton                                        83        sflclr
     C                   write     tf533c1
     C                   setoff                                       818283
      *                                                    81=sfldsp
      *                                                    82=sflend
      *                                                    83=sflclr
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Refresh/fill the subfile for screen 1
      *---------------------------------------------------------------
      *
     C     $frsh1        begsr
      *
      * Set position in file
      *
     C     c1cuno        setll     cabbrel0
      *
      * The lowest subfile RRN on the screen from the DSPF feedback area (IDFLOW)
      * was retrieved earlier and put in WKFLOW. Add to WKFLOW the number of lines
      * on your subfile page or the number less 1 to determine a subfile RRN to
      * reposition to. RESET is a flag field to indicate whether you are attempting
      * to reposition the user close to where he was in the subfile. If RESET is
      * 'NO' don't reposition--just start over.
      *
     C                   if        reset = yes
     C     wkflow        add       wkrlin        maxrrn
     C                   else
     C                   z-add     wkplin        maxrrn
     C                   endif
      *
      * Load the subfile until you have the required number of records.
      * The number will depend on whether uou are repositioning the
      * subfile or not. Stop if you get to the end of the subfile.
      *
     C                   dou       hdrrn1 >= maxrrn or *in82 = *on
     C                   exsr      $load1
     C                   enddo
      *
     C                   endsr
      /EJECT
      *----------------------------------------------------------------
      * Save the control record positioners/selectors
      *----------------------------------------------------------------
      *                                                                        z
     C     $chksv        begsr
      *
     C                   eval      routne = set1
     C                   eval      reset = no
      *
     C                   move      c1stat        svstat
     C                   z-add     c1cuno        svcuno
      *
      * Prepare to scan on description
      *  1) trim leading/trailing blanks
      *  2) find first non-blank position from right
      *
     C                   eval      c1cunm = %trim(c1cunm)
     C     ' '           checkr    c1cunm        length
     C                   movel     c1cunm        svcunm
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Load subfile for screen 1
      *---------------------------------------------------------------
      *
     C     $load1        begsr
      *
      * Set the count of records on the subfile page to zero.
      *
     C                   z-add     0             count
      *
      * Fill the subfile until the end of the database file or
      * you have filled all the lines on the subfile page.
      *
     C                   setoff                                       82
     C                   dou       *in82 = *on or count >= wkplin               Do fill
      *
      * We want to only load "A/R Customer" records into your subfile.
      * When the ShipTo Customer Number equals the A/R Customer Number,
      * then you have an "A/R Customer" record. So, process it.
      *
     C                   read      cabbrel0                               82
     C                   if        *in82 = *off and                             If A/R Customer
     C                             bbbkc7 = bbanc7
      *
     C                   move      yes           procfl
      *
      * Only include records that match the user's selectors
      *
     C                   if        c1cunm <> *blank
     C     lower:upper   xlate     c1cunm        wkdesc
     C     lower:upper   xlate     bbaytx        wkcunm
     C     wkdesc:length scan      wkcunm                                 92
     C                   if        *in92 = *off
     C                   move      no            procfl
     C                   endif
     C                   endif
      *
     C                   if        c1stat   <> *blank and
     C                             c1stat   <> bbvsst
     C                   move      no            procfl
     C                   endif
      *
     C                   if        procfl = yes                                 If process
     C                   add       1             count
     C                   add       1             hdrrn1
     C                   z-add     bbbkc7        d1cuno
     C                   move      bbaytx        d1cunm
     C                   move      bbvsst        d1stat
     C                   move      bbajna        d1city
     C                   move      bbbgcd        d1state
     C                   write     tf533s1
     C                   seton                                        81
      *
      * For positioning, place the subfile relative record number into the
      * sflrcdnbr field (HDPAG1)
      *
     C                   z-add     hdrrn1        hdpag1
     C                   endif                                                  If process
     C                   endif                                                  If A/R Customer
     C                   enddo                                                  Do fill
      *
      * If no records were written to the subfile, then issue
      * the standard message 'No data to Display'.
      *
     C                   if        hdrrn1 = 0                                   If no recs
     C                   eval      norecs = yes
      *
     C                   if        error < maxmsg
     C                   add       1             error
     C                   eval      mgid(e) = 'HS09002'
     C                   endif
      *
     C                   else
     C                   eval      norecs = no
     C                   endif                                                  If no recs
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * End job
      *---------------------------------------------------------------
      *
     C     $exit         begsr
     C                   seton                                        LR
     C                   endsr
      *
      *---------------------------------------------------------------
      * Write error messages
      *---------------------------------------------------------------
      *
     C     $wrmsg        begsr
      *
     C                   call      'UT80045J'                           43
     C                   parm                    mgid
     C                   parm                    mgdt
     C                   parm                    error
     C                   parm      sdpgm         pgm
     C                   parm      msgfil        msgfl
      *
      * If call to UT80045J failed, a message hardcoded in the DSPF
      * command line format will be set on. So, redisplay screen.
      *
     C                   if        *in43 = *on
     C                   eval      routne = set1
     C                   endif
      *
     C                   endsr
      *
      *----------------------------------------------------------------
      * Clear messages
      *----------------------------------------------------------------
      *
     C     $clmsg        begsr
      *
     C                   call      'UT80045J'                           43
     C                   parm      *blank        mgi
     C                   parm      *blank        mgd
     C                   parm      *zero         error
     C                   parm      sdpgm         pgm
     C                   parm      msgfil        msgfl
      *
      * If call to UT80045J failed, a message hardcoded in the DSPF
      * command line format will be set on; so, redisplay screen.
      *
     C                   if        *in43 = *on
     C                   eval      routne = set1
     C                   endif
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Key lists
      *     None
      *
      * Parm lists
      *
     C     *entry        plist
     C                   parm                    xxcuno
     C                   parm                    xxcunm
      *
      * Always default "status" to A=Active.
      *
     C                   move      'A'           c1stat
     C                   move      'A'           svstat
      *
      * To support subfile paging, logic requires the number of lines on
      * the subfile page.
      *
     C                   z-add     9             wkplin                         sflpag
     C     wkplin        sub       1             wkrlin
      *
      *
      * The following standard code must be included to make the
      * standard error message handling program function properly.
      * This code sets and clears the program message queue.
      *
     C                   movel     '*'           msgpgm
     C                   exsr      $clmsg
      *
      * Execute required window format
      *
     C                   write     assume
      *
     C                   endsr
      /EJECT
