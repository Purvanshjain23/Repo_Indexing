/**************************************************************************/
/*                                                                        */
/*  PROGRAM NUMBER:  HP3085CL                                             */
/*  PROGRAM NAME:    FEED REPORT FOR CREATED GROUPS                       */
/*  PROGRAMMER:      LEANNE FEDOR                                         */
/*  CREATE DATE:     02/01/06                                             */
/*                                                                        */
/*  FUNCTION:        THIS CL IS SUBMITTED WITH QCMDEXC FROM:              */
/*                       THE EOP FUNCTION AND                             */
/*                       AN ON-DEMAND FUNCTION FOR PRELIMINARY EOP EDITS  */
/*                                                                        */
/*                   OPTIONS ARE PASSED THROUGH THE LDA.                  */
/*                                                                        */
/**************************************************************************/
/*  MODIFIED:                                                             */
/* 02/29/16    Rose Centonze    E5290                              */
/*             Added company code parm and add to opnqry select    */
/*                                                                 */
/**************************************************************************/

             PGM        PARM(&PPCOCD)

             DCL        VAR(&PPCOCD) TYPE(*CHAR) LEN(3)            /* E5290 */

/* DEFINE THE VARIABLE REQUIRED IN THE COMMAND TO OVERRIDE THE     */
/* PRINT FILE TO A LASER PRINTER                                   */

             DCL        VAR(&CMD) TYPE(*CHAR) LEN(512)
             DCL        VAR(&QDATA) TYPE(*CHAR) LEN(60) /* E5290-WAS 50*/

             DCL        VAR(&LDTCYMD) TYPE(*DEC) LEN(8)

             DCL        VAR(&HOLD) TYPE(*CHAR) LEN(4)
             DCL        VAR(&SAVE) TYPE(*CHAR) LEN(4)
             DCL        VAR(&COPYS) TYPE(*DEC) LEN(2)
             DCL        VAR(&OUTQ) TYPE(*CHAR) LEN(10)

             CHGVAR     VAR(&LDTCYMD) VALUE(%SST(*LDA 9 8))


/*-------------------------------------------------------------------*/
/* SET UP PRINT OVERRIDES                                            */
/*-------------------------------------------------------------------*/

 HOLD:       IF         COND(%SST(*LDA 411 1) = 'Y') THEN(CHGVAR +
                          VAR(&HOLD) VALUE(*YES))
             ELSE       CMD(CHGVAR VAR(&HOLD) VALUE(*NO))

 COPYS:      CHGVAR     VAR(&COPYS) VALUE(%SST(*LDA 412 1))
             IF         COND(&COPYS = 0) THEN(CHGVAR VAR(&COPYS) +
                          VALUE(1))

OUTQ:        CHGVAR     VAR(&OUTQ) VALUE(%SST(*LDA 413 10))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(%SST(*LDA 401 10)))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(*JOB))


/* AS OF MARCH 2004, USERS ASKED US TO ALWAYS PUT OUTPUT ON SAVE/HOLD */
/* FOR THE EOP RUNS                                                   */

             CHGVAR     VAR(&SAVE) VALUE(*YES)
             CHGVAR     VAR(&HOLD) VALUE(*YES)


/*------------------------------------------------------------------*/
/* OVERRIDE PRINT FILE                                              */
/*------------------------------------------------------------------*/

/* USE THE CUSTOMIZED SEABOARD COMMAND TO OVERRIDE THE PRINT FILE.  */
/* THIS COMMAND ALLOWS THE REPORT TO BE GENERATED ON A STANDARD LINE*/
/* PRINTER OR A LASER PRINTER. TO USE THIS OVERRIDE YOU MUST DECLARE*/
/* A COMMAND VARIABLE AND FOLLOW THE OVERRIDE WITH A CALL TO        */
/* QCMDEXC.                                                         */

             SBDPRTOVR  CMDVAR(&CMD) FILE(QPRINT) OUTQ(&OUTQ) +
                          WIDTH(132) LANDSCAPE(*PRTF) DRAWER(*PRTF) +
                          COPIES(&COPYS) HOLD(&HOLD) SAVE(&SAVE)

             CALL       PGM(QCMDEXC) PARM(&CMD 512)


/*-------------------------------------------------------------------*/
/*  SETUP OPNQRY STATEMENT TO SELECT CREATED HOG GROUPS              */
/*-------------------------------------------------------------------*/

             CHGVAR     VAR(%SST(&QDATA 1 15)) VALUE('HGGSCD +
                          *EQ "CR"')
             CHGVAR     VAR(%SST(&QDATA 17 15)) VALUE('*AND HGCRDT +
                          *LE')
             CHGVAR     VAR(%SST(&QDATA 33 8)) VALUE(&LDTCYMD)

/*   SELECT COMPANY IN PARM          E5290                     */

             CHGVAR     VAR(%SST(&QDATA  42 15)) VALUE('*AND HGCOCD +
                          *EQ')
             CHGVAR     VAR(%SST(&QDATA  58 3)) VALUE(&PPCOCD)


             OVRDBF     FILE(HSP034)  SHARE(*YES)
             OPNQRYF    FILE((HSP034)) QRYSLT(&QDATA) +
                          KEYFLD((HGCRDT) (HGFSCD) (HGBLCD) +
                          (HGRMCD)) SEQONLY(*YES)

/*-------------------------------------------------------------------*/
/* PRINT THE REPORT                                                  */
/*-------------------------------------------------------------------*/

             CALL       PGM(HP6085)

             CLOF       OPNID(HSP034)
             DLTOVR     FILE(*ALL)

             ENDPGM

