      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Hog Production System
      * PROGRAM:     HP6705
      * TITLE:       Farm Budget Listing
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     02/25/04
      *
      *            This listing is submitted from HP4702-Work With Farm Budgets.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 10/13/04  LeAnne Fedor
      *           Recompile only. Cost Per Unit Amount was changed from
      *           9,2 to 9,4.
      *
      * 11/22/04  LeAnne Fedor
      *           Added 2 new columns:
      *                     Used Amount
      *                     Variance Amount (ie: Budget Amount - Used Amount)
      *           Added totals for:
      *                     Budgeted Amount
      *                     Used Amount
      *                     Variance Amount
      *
      * 10/16/13  LeAnne Ramsey (E2831)
      *           Recompile only. Added field 'MTech Reference'.
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fhsp018    if   e           k disk
      *    Farm site
      *
      *
     Fhsl181d   if   e           k disk
      *    Items
      *
      *
     Fhsp183    if   e           k disk
      *    Creation schedule
      *
      *
     Fhsp185    if   e           k disk
      *    Budget template header
      *
      *
     Fhsp188    if   e           k disk
      *    Farm budget header
      *
      *
     Fhsp189    if   e           k disk
      *    Farm budget detail
      *
      *
     Fhsp191    if   e           k disk
      *    Farm budget status
      *
      *
     Fhsp3719   if   e           k disk
      *    Workfile
      *
      *
     Fqprint    o    f  198        printer oflind(*inof)
      *
      /EJECT
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Standard fields
      *
     D prtfl           s              1    inz('N')
     D dash            s            166    inz(*all'-')
     d rtime           s              6  0
      *
      *
      * Workfields for date manipulation
      *
     D wkcymdiso       s               d   datfmt(*iso)
      *
      *
      * Print fields
      *
     D h1fscd          s                   like(fsfscd)
     D h1fsnm          s                   like(fsfsnm)
     D h1btcd          s                   like(thbtcd)
     D h1btds          s                   like(thbtds)
     D h1btwk          s                   like(thbtwk)
     D h1fbscd         s                   like(sbfbscd)
     D h1fbsds         s                   like(sbfbsds)
     D h1cscd          s                   like(cscscd)
     D h1csds          s                   like(cscsds)
     D h1fmdy          s              6  0
     D h1tmdy          s              6  0
      *
     D l1desc          s                   like(imitds)
     D l1uom           s                   like(imuom)
     D l1uow           s                   like(imuow)
     D l1ittycd        s                   like(imittycd)
     D l1apqt          s                   like(jdbgqt)
     D l1usqt          s                   like(jdbgqt)
     D l1avqt          s                   like(jdbgqt)
     D l1tcam          s                   like(w1tcam)
     D l1varam         s                   like(w1tcam)
      *
     D t1fbam          s                   like(w1tcam)
     D t1tcam          s                   like(w1tcam)
     D t1varam         s                   like(w1tcam)
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *---------------------------------------------------------------
      * Local data area.
      *---------------------------------------------------------------
      *
     Dlda             uds                  dtaara(*lda)
     D  ldreport               1      1
     D  ldfbsn                 2      8  0
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      *******************************************************************************
      * MAINLINE
      *******************************************************************************
      *
      * Prepare to read the file and print headings the first time through.
      *
     C     ldfbsn        setll     hsp189
     C                   except    h1hdr
      *
      * Read/print all records in the Farm Budget Detail file.
      *
     C                   dou       *in90 = *on                                  Main do
     C     ldfbsn        reade     hsp189                                 90
     C                   if        *in90 = *off                                 If not EOF
      *
      * Retrieve Budget Item info from Item Master
      *
     C     jdbgit        chain     hsl181d                            92
     C                   if        *in92 = *off                                 If hit
     C                   move      imuom         l1uom
     C                   move      imuow         l1uow
     C                   move      imittycd      l1ittycd
      *
     C                   select
     C                   when      imitcd = imbgit
     C                   eval      l1desc = imitds
     C                   other
     C                   eval      l1desc = imbgit
     C                   endsl
     C                   endif                                                  If hit
      *
      * Retrieve all Request Detail records for this farm budget/budget item
      * from the workfile.
     C                   exsr      $requests
      *
      * Calculate Available Quantity as: Budgeted Quantity less Approved Quantity
      *
     C     jdbgqt        sub       l1apqt        l1avqt
      *
      *
      * If the budget is active:
      *   1) Calculate Variance Amount (Budgeted Amount less Used Amount)
      *   2) Accumulate dollar amounts for Total Line
      *
     C                   if        *in97 = *on
     C     jdfbam        sub       l1tcam        l1varam
     C                   add       jdfbam        t1fbam
     C                   add       l1tcam        t1tcam
     C                   add       l1varam       t1varam
     C                   endif
      *
      * Print detail line
      *
     C                   if        *inof = *on
     C                   except    h1hdr
     C                   endif
     C                   except    l1dtl
     C                   movel     yes           prtfl
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Main do
      *
      *---------------------
      * EOF processing
      *---------------------
      *
      * If no records met the user's selections, print the 'no data...' message,
      * otherwise, print total line
      *
     C                   select
     C                   when      prtfl = no
     C                   except    nodata
      *
     C                   when      *in97 = *on
     C                   if        *inof = *on
     C                   except    h1hdr
     C                   endif
     C                   except    t1tot
     C                   endsl
      *
     C                   seton                                        lr
      /eject
      *-------------------------------------------------------------------------------------
      * Retrieve values for this Farm Budget/Budget Item from the workfile
      *-------------------------------------------------------------------------------------
      *
      * Retrieve Approved and Used Values
      *
     C     $requests     begsr
      *
     C     key01         chain     hsp3719                            92
     C                   if        *in92 = *off                                 If hit
     C                   z-add     w1apqt        l1apqt
     C                   z-add     w1usqt        l1usqt
     C                   z-add     w1tcam        l1tcam
     C                   endif                                                  If hit
      *
     C                   endsr
      /eject
      *-------------------------------------------------------------------------------------
      * Initialization subroutine
      *-------------------------------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Key lists
      *
     C     key01         klist
     C                   kfld                    ldfbsn
     C                   kfld                    jdbgit
      *
      * Set up heading info.
      *
     C                   time                    rtime
      *
      * Retrieve Farm Budget Header info
      *
     C     ldfbsn        chain     hsp188                             92
     C                   if        *in92 = *off                                 If farm header
     C                   z-add     jhfscd        h1fscd
     C                   move      jhbtcd        h1btcd
     C                   z-add     jhbtwk        h1btwk
     C                   move      jhfbscd       h1fbscd
      *
      * Flip the dates from CCYYMMDD to MMDDYY
      *
     C     *iso          test(d)                 jhbfdt                 92
     C                   if        *in92 = *off                                 If OK date
     C                   move      jhbfdt        wkcymdiso
     C     *mdy          move      wkcymdiso     h1fmdy
     C                   endif                                                  If OK date
      *
     C     *iso          test(d)                 jhbtdt                 92
     C                   if        *in92 = *off                                 If OK date
     C                   move      jhbtdt        wkcymdiso
     C     *mdy          move      wkcymdiso     h1tmdy
     C                   endif                                                  If OK date
      *
      * Retrieve Template description
      *
     C     jhbtcd        chain     hsp185                             92
     C                   if        *in92 = *off
     C                   move      thbtds        h1btds
      *
     C     thcscd        chain     hsp183                             92
     C                   if        *in92 = *off
     C                   move      cscscd        h1cscd
     C                   move      cscsds        h1csds
     C                   endif
      *
     C                   endif
      *
      * Retrieve Farm Budget Status description
      *
     C     jhfbscd       chain     hsp191                             92
     C                   if        *in92 = *off
     C                   move      sbfbsds       h1fbsds
     C                   endif
      *
      * Retrieve Farm Site name
      *
     C     jhfscd        chain     hsp018                             92
     C                   if        *in92 = *off
     C                   move      fsfsnm        h1fsnm
     C                   endif
      *
      * We do not have Budget Amount until a budget goes Active. So, set
      * the indicator on to control printing of the Budget Amount column.
      *
     C                   if        jhfbscd <> 'I'
     C                   seton                                        97
     C                   endif
      *
     C                   endif                                                  If farm header
      *
     C                   endsr
      /EJECT
      *-------------------------------------------------------------
      * Report heading lines
      *-------------------------------------------------------------
      *
     Oqprint    e            h1hdr          1 01
     O                       sdpgm               10
     O                                           90 'HOG PRODUCTION SYSTEM'
     O                                          156 'DATE'
     O                       udate         y    166
      *
     O          e            h1hdr          1
     O                       sdusr               10
     O                                           89 'FARM BUDGET LISTING'
     O                                          156 'TIME'
     O                       rtime              166 '  :  :  '
      *
     O          e            h1hdr          2
     O                                          156 'PAGE'
     O                       page          z    166
      *
      *
     O          e            h1hdr          1
     O                                           23 'Farm site ............:'
     O                       h1fscd        z     32
     O                       h1fsnm              59
      *
      *
     O          e            h1hdr          1
     O                                           23 'Template .............:'
     O                       h1btcd              33
     O                       h1btds              59
      *
     O          e            h1hdr          1
     O                                           23 'Creation schedule ....:'
     O                       h1cscd              32
     O                       h1csds              54
      *
     O          e            h1hdr          1
     O                                           23 'Weeks ................:'
     O                       h1btwk        z     32
      *
      *
     O          e            h1hdr          1
     O                                           23 'Budget number ........:'
     O                       ldfbsn        z     32
      *
      *
     O          e            h1hdr          1
     O                                           23 'Budget status ........:'
     O                       h1fbscd             32
     O                       h1fbsds             42
      *
      *
     O          e            h1hdr          1
     O                                           23 'From date ............:'
     O                       h1fmdy              32 '  /  /  '
      *
      *
     O          e            h1hdr          2
     O                                           23 'To date ..............:'
     O                       h1tmdy              32 '  /  /  '
      *
      *
      *-------------------------------------------------------------
      * Column headings
      *-------------------------------------------------------------
      *
     O          e            h1hdr       0  1
     O                                           34 'Item'
      *
     O          e            h1hdr       0  1
     O                                           34 'Type'
     O                                           69 'Budget'
     O                                           85 'Used'
     O                                          101 'Approved'
     O                                          117 'Available'
     O               97                         133 'Budget'
     O               97                         149 'Used'
     O               97                         165 'Variance'
      *
     O          e            h1hdr       0  1
     O                                           23 'Budget Item/Description'
     O                                           34 'Code'
     O                                           41 'UOM'
     O                                           51 'UOW'
     O                                           69 'Quantity'
     O                                           85 'Quantity'
     O                                          101 'Quantity'
     O                                          117 'Quantity'
     O               97                         133 'Amount'
     O               97                         149 'Amount'
     O               97                         165 'Amount'
      *
     O          e            h1hdr       0  2
     O                       dash               166
      *
      *-------------------------------------------------------------
      * Detail line
      *-------------------------------------------------------------
      *
     O          e            l1dtl          1
     O                       jdbgit              25
     O                       l1ittycd       b    36
     O                       l1uom          b    46
     O                       l1uow          b    53
     O                       jdbgqt        k     70
     O                       l1usqt        kb    86
     O                       l1apqt        kb   102
     O                       l1avqt        kb   118
     O               97      jdfbam        k    134
     O               97      l1tcam        kb   150
     O               97      l1varam       kb   166
      *
     O          e            l1dtl          2
     O                       l1desc         b    56
      *
      *-------------------------------------------------------------
      * Total line
      *-------------------------------------------------------------
      *
     O          e            t1tot          1
     O                                          133 '--------------'
     O                                          149 '--------------'
     O                                          165 '--------------'
      *
     O          e            t1tot          1
     O                                          118 'Totals:'
     O                       t1fbam        kb   134
     O                       t1tcam        kb   150
     O                       t1varam       kb   166
      *
      *-------------------------------------------------------------
      * No data message line
      *-------------------------------------------------------------
      *
     O          e            nodata      1
     O                                           19 'NO DATA MEETS YOUR'
     O                                           38 'SELECTION CRITERIA'
