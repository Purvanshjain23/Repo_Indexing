     A*%%TS  SD  20030120  102739  ISLRAMS     REL-V5R1M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD485
     A*  TITLE:         SPECIFY SCHEDULED SALES MOVEMENTS TO CREATE
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  DATE:          11/24/99
     A*
     A*****************************************************************
     A* MODIFICATIONS:
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A                                      CF04(04 'PROMPT')
     A                                      CA12(12 'PREVIOUS')
     A*
     A*---------------------------------------------------------------
     A* RECORD FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP485R1
     A*%%TS  SD  20030120  102739  ISLRAMS     REL-V5R1M0  5722-WDS
     A                                      SETOF(30)
     A                                      SETOF(31)
     A                                      SETOF(32)
     A                                      SETOF(33)
     A                                      SETOF(34)
     A                                      SETOF(35)
     A                                      SETOF(50)
     A                                      SETOF(51)
     A                                      SETOF(52)
     A                                      OVERLAY
     A                                      RTNCSRLOC(&RCD &FLD)
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 30'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A            DFVERSION     18A  O  2  2DSPATR(HI)
     A                                  2 23'SPECIFY SCHEDULED SALES MOVEMENTS -
     A                                      TO CREATE'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  5  2'Do you want multiple movements per-
     A                                       screen ......'
     A                                  5 52'?'
     A            DFMMFL    R        B  5 54REFFLD(HSRREF/@@FLAG *LIBL/HSPREF)
     A  30
     AO 50                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A N30                                  DSPATR(HI)
     A N30                                  DSPATR(UL)
     A                                  7  2'If you selected multiple movements-
     A                                       per screen,'
     A                                  8  2'do you want the system to generate-
     A                                       movements '
     A                                  9  2'for you to review and modify on-li-
     A                                      ne ...........'
     A                                  9 52'?'
     A            DFSGFL    R        B  9 54REFFLD(HSRREF/@@FLAG *LIBL/HSPREF)
     A  31
     AO 51                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A N31                                  DSPATR(HI)
     A N31                                  DSPATR(UL)
     A                                 11  2'If you requested the system to gen-
     A                                      erate movements,'
     A                                 12  2'please provide the following infor-
     A                                      mation:'
     A                                 14 12'Farm .........................'
     A                                 14 43'?'
     A            DFFSCD    R        B 14 45REFFLD(HSRREF/@@FSCD *LIBL/HSPREF)
     A                                      EDTCDE(M)
     A  32
     AO 52                                  DSPATR(PC)
     A  32                                  DSPATR(RI)
     A N32                                  DSPATR(HI)
     A N32                                  DSPATR(UL)
     A            DFFSNM    R        O 14 55REFFLD(FSREC/FSFSNM *LIBL/HSP018)
     A                                 15 12'Scheduled ship date ..........'
     A            DFMDYSCDT R        B 15 45REFFLD(HSRREF/@@DT06 *LIBL/HSPREF)
     A                                      EDTWRD('  /  /  ')
     A  33                                  DSPATR(PC)
     A  33                                  DSPATR(RI)
     A N33                                  DSPATR(HI)
     A N33                                  DSPATR(UL)
     A                                 16 12'Number of head per movement ..'
     A            DFHDMV    R        B 16 45REFFLD(@@NU30)
     A                                      EDTCDE(K)
     A  34                                  DSPATR(PC)
     A  34                                  DSPATR(RI)
     A N34                                  DSPATR(HI)
     A N34                                  DSPATR(UL)
     A                                 17 12'Maximum number of movements ..'
     A            DFNBMV    R        B 17 45REFFLD(@@NU30)
     A                                      EDTCDE(K)
     A  35                                  DSPATR(PC)
     A  35                                  DSPATR(RI)
     A N35                                  DSPATR(HI)
     A N35                                  DSPATR(UL)
     A*
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R HP485K1
     A*%%TS  SD  19991124  140630  ISLRAMS     REL-V4R2M0  5769-PW1
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A                                 23 11'F4=Prompt'
     A                                 23 22'F12-Previous'
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *---------------------------------------------------------------
     A          R HP485ES                   SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE CONTROL FORMAT
      *---------------------------------------------------------------
     A          R HP485EC                   SFLCTL(HP485ES)
      *
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
