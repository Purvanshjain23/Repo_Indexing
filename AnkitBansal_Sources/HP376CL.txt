/***************************************************************** */
/*                                                                 */
/*  PROGRAM NUMBER:  HP376CL                                       */
/*  PROGRAM NAME:    CLOSE HOG GROUPS                              */
/*  PROGRAMMER:      LEANNE FEDOR                                  */
/*  CREATE DATE:     04/24/03                                      */
/*                                                                 */
/*  FUNCTION:        THIS CL IS CALLED FROM HP477CL-CLOSING THE    */
/*                   PERIOD.                                       */
/*                                                                 */
/*******************************************************************/
/* MODIFIED:                                                       */
/* 07/16/03  LEANNE FEDOR                                          */
/*           DISPOSED GROUP EDITS ARE NOW RUN AT THE BEGINNING OF  */
/*           THE PERIOD END CL. THIS SPEEDS UP THE PROCESS. SO,    */
/*           BY THE TIME YOU GET TO HERE, THE DISPOSED GROUPS WILL */
/*           BE ERROR-FREE. SO, WE NO LONGER NEEDED THE INCOMING   */
/*           'ERROR' PARM. IT WAS REMOVED.                         */
/*           WE ALSO NO LONGER NEED THE 'ERROR' LISTING THAT WAS   */
/*           ORIGINALLY PRINTED HERE (HP677)...THE NEW, IMPROVED   */
/*           LISTING (HP372) IS PRINTED EARLY IN THE EOP FUNCTION. */
/*                                                                 */
/* 02/26/16    Rose Centonze    E5290                              */
/*             Added company code parm and add to opnqry select    */
/*                                                                 */
/*******************************************************************/

             PGM        PARM(&PPCOCD)


             DCL        VAR(&PPCOCD) TYPE(*CHAR) LEN(3)            /* E5290 */

             DCL        VAR(&QDATA) TYPE(*CHAR) LEN(60) /* E5290- WAS 40 */

             DCL        VAR(&LDEPDT) TYPE(*DEC) LEN(8)

             DCL        VAR(&HOLD) TYPE(*CHAR) LEN(4)
             DCL        VAR(&SAVE) TYPE(*CHAR) LEN(4)
             DCL        VAR(&COPYS) TYPE(*DEC) LEN(2)
             DCL        VAR(&OUTQ) TYPE(*CHAR) LEN(10)

             CHGVAR     VAR(&LDEPDT) VALUE(%SST(*LDA 9 8))


/*-----------------------------------------------------------------*/
/*  CREATE WORKFILES                                               */
/*-----------------------------------------------------------------*/

             CRTDUPOBJL OBJ(HSP371) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)
             CRTDUPOBJL OBJ(HSL371A) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)

             CRTDUPOBJL OBJ(HSP373) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)


/*-----------------------------------------------------------------*/
/*  PERFORM OPEN QUERY AND BUILD WORKFILES                         */
/*-----------------------------------------------------------------*/

/*  SELECT DISPOSED HOG GROUPS UP TO THE ENDING PERIOD DATE   */

             CHGVAR     VAR(%SST(&QDATA 1 10)) VALUE('HGDSDT *LE')
             CHGVAR     VAR(%SST(&QDATA 12 8)) VALUE(&LDEPDT)
             CHGVAR     VAR(%SST(&QDATA 21 20)) VALUE('*AND HGGSCD +
                          *EQ "DS"')

/*   SELECT COMPANY IN PARM          E5290                     */

             CHGVAR     VAR(%SST(&QDATA 42 15)) VALUE('*AND HGCOCD +
                          *EQ')
             CHGVAR     VAR(%SST(&QDATA 58 3)) VALUE(&PPCOCD)

             OVRDBF     FILE(HSL034D) SHARE(*YES)
             OPNQRYF    FILE((HSL034D)) QRYSLT(&QDATA) KEYFLD(*FILE) +
                          SEQONLY(*YES)

             CALL       PGM(HP371)

             CLOF       OPNID(HSL034D)
             DLTOVR     FILE(HSL034D)


/*-----------------------------------------------------------------*/
/* SET PRINT PARAMETERS FROM LDA                                   */
/*-----------------------------------------------------------------*/

/* AS OF MARCH 2004, USERS ASKED US TO ALWAYS PUT OUTPUT ON SAVE/HOLD */

             CHGVAR     VAR(&SAVE) VALUE(*YES)
             CHGVAR     VAR(&HOLD) VALUE(*YES)

/* SET PARAMETERS FROM LDA                                           */

 COPYS:      CHGVAR     VAR(&COPYS) VALUE(%SST(*LDA 412 1))
             IF         COND(&COPYS = 0) THEN(CHGVAR VAR(&COPYS) +
                          VALUE(1))

 OUTQ:       CHGVAR     VAR(&OUTQ) VALUE(%SST(*LDA 413 10))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(%SST(*LDA 401 10)))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(*JOB))

 OVRPRTF:    OVRPRTF    FILE(QPRINT) OUTQ(&OUTQ) COPIES(&COPYS) +
                          HOLD(&HOLD) SAVE(&SAVE) SHARE(*YES)


/*-----------------------------------------------------------------*/
/* PRINT REPORTS                                                   */
/*-----------------------------------------------------------------*/

/*  CALL PROGRAM TO PRINT CLOSED GROUP REPORT            */
/*  AS OF MAY 2003 WE HAVE COMMENTED OUT THIS REPORT.    */
/*  WE WILL REINSTATE IT IF THE USERS REQUEST; OTHERWISE */
/*  WE CAN EVENTUALLY DELETE IT...IF THEY DON'T WANT IT. */

/*           CALL       PGM(HP671)               */
             DLTOVR     FILE(QPRINT)

/*  WRITE THE WORKFILE DATA TO THE DATABASE REGARDLESS OF WHETHER */
/*  IS A FINAL RUN OR AN EDIT RUN. IF IT IS AN 'EDIT' RUN, THE    */
/*  GROUPS WILL BE 'UNCLOSED' AFTER THE PERIOD END PROGRAMS RUN.  */

             CALL       PGM(HP373)

 EXIT:
             ENDPGM

