      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Hog Production
      * PROGRAM:     HP7019
      * TITLE:       View BGF Weekly Wean Projections--All Farms/Weeks
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     07/06/04
      *
      *
      * FUNCTION: This is a View that displays the records in the BGF Weekly
      *           Wean Projections file. All 'dates' on the screen are 'calendar' dates--
      *           not 'accounting' dates.
      *
      *           Restrictors:
      *                None
      *
      *           Positioners:
      *               Report year    (descending)
      *               Report week    (descending)
      *
      *           Selectors:
      *               Report week-ending date
      *               Cell
      *               Farm site
      *               Predictions year
      *               Predictions week
      *               Predictions week-ending date
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 10/16/13  LeAnne Ramsey (E2831)
      *           Recompile only. Added field 'MTech Reference'.
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fhpd7019   cf   e             workstn infds(iofeed)
     F                                     sfile(hp7019s1:hdrrn1)
      *
      *
     Fhsp018    if   e           k disk
      *    Farm site
      *
      *
     Fhsl086b   if   e           k disk    infds(dbfeed)
      *    BGF weekly wean projections
      *
      /EJECT
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
     D set1            c                   'SET1  '
     D scrn1           c                   'SCRN1 '
     D exit            c                   'EXIT  '
      *
      ****************************************************************
      * ARRAYS AND TABLES
      ****************************************************************
      *
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Standard fields
      *
     D mode            s              6
     D pgm             s             10
     D msgfl           s             10
     D maxmsg          s              2  0  inz(20)
     D routne          s              6
     D maxrrn          s              4  0
     D reset           s              1    inz('N')
     D norecs          s              1    inz('N')
     D count           s              2  0
     D wkflow          s                   like(idflow)
     D rtncd           s              2
     D svrrn1          s                   like(hdrrn1)
      *
      *
      * Workfields required for subfile paging--see initialization
      * subroutine.
      *
     D wkplin          s              2  0
     D wkrlin          s              2  0
      *
      *
      * Control fields and save fields for restrictors, positioners and selectors
      *
      *
     D svfscd          s                   like(c1fscd)
     D svcell          s                   like(c1cell)
      *
     D svrpcdyr        s                   like(c1rpcdyr)
     D svrpcdwk        s                   like(c1rpcdwk)
     D svrpwemdy       s                   like(c1rpwemdy)
      *
     D svpwcdyr        s                   like(c1pwcdyr)
     D svpwcdwk        s                   like(c1pwcdwk)
     D svpwwemdy       s                   like(c1pwwemdy)
      *
      *
      * Work fields for date manipulation
      *
     D wkisodate       s               D   datfmt(*iso)
     D wkmdydate       s               D   datfmt(*mdy)
     D wkrpwedt        s                   like(pwrpwedt)
     D wkpwwedt        s                   like(pwpwwedt)
      *
      *
      * Parms
      *
     D xxfsbo          s                   like(fsfsbo)
     D xxfscd          s                   like(c1fscd)
     D xxfsnm          s                   like(fsfsnm)
     D xxaist          s                   like(fsaist)
      *
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * standard error message handling data structures
      *---------------------------------------------------------------
      *
      * For error message handling, a packed index (3) is required.
      * For program readability, define a corresponding error count
      * field called 'error'
      *
     D                 ds                  inz
     D  error                         2p 0
     D   e                            2p 0 overlay(error)
      *
      * This data structure supplies the name of the message file to
      * the message handling CL program.  The field name MSGFIL must be
      * constant. The value in quotes is the name of the specific
      * message file containing the user defined messages.
      *
     D                 ds                  inz
     D  msgfil                       10    inz('HSMSGF    ')
      *
      *---------------------------------------------------------------
      *  Standard message data structures
      *---------------------------------------------------------------
      *
      * The following 3 data structures are used to speed message
      * handling since it is faster to clear data structures than
      * arrays.  Each is associated with a standard message array.
      *
     D mgi             ds                  inz
     D  mgid                          7    dim(20)
      *
     D mgd             ds                  inz
     D  mgdt                         50    dim(20)
      *
     D mgwk            ds                  inz
     D  mgw                           1    dim(50)
      *
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *---------------------------------------------------------------
      * Standard workstation information data structure
      *---------------------------------------------------------------
      *    externally defined as UTWSFR (record format: UTIDFRR)
     D iofeed        e ds                  extname(utwsfr)
      *
      *
      *---------------------------------------------------------------
      * Standard database file information data structure
      *---------------------------------------------------------------
      *    externally defined as UTDBGR (record format: FDBCKD)
     D dbfeed        e ds                  extname(utdbfr)
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * MAINLINE
      ****************************************************************
      *
     C                   eval      routne = set1
      *
     C     *inlr         doweq     *off                                         Main do loop
     C     routne        caseq     set1          $set1
     C     routne        caseq     scrn1         $scrn1
     C     routne        caseq     exit          $exit
     C                   endcs
     C                   enddo                                                  Main do loop
      /EJECT
      *----------------------------------------------------------------
      * Set environment for screen 1
      *----------------------------------------------------------------
      *
     C     $set1         begsr
      *
      * Clear and fill the subfile
      *
     C                   exsr      $clr1
     C                   exsr      $frsh1
      *
      * Set routine to display screen
      *
     C                   eval      routne = scrn1
      *
     C                   endsr
      /eject
      *----------------------------------------------------------------
      * Perform operator I/O for screen 1
      *----------------------------------------------------------------
      *
     C     $scrn1        begsr
      *
      *
      * Write the error messages from the error arrays to the
      * error message subfile
      *
     C                   exsr      $wrmsg
      *
      * Write screen 1 to crt
      *
     C                   write     hp7019k1
     C                   write     hp7019ec
     C                   exfmt     hp7019c1
      *
      * Save the value of the lowest subfile rrn on the screen for
      * later repositioning of the subfile
      *
     C                   eval      wkflow = idflow
      *
      * Clear the messages from the message data structures and
      * program message queue
      *
     C                   exsr      $clmsg
      *
      * Get user's entry and set next routine
      *
     C                   select
     C                   when      *in03 = *on                                  F3-exit
     C                   eval      routne = exit
      *
     C                   when      *in04 = *on                                  F4-prompt
     C                   exsr      $f4s1
     C                   exsr      $chksv
      *
     C                   when      *in05 = *on                                  F5-refresh
     C                   movel     yes           reset
     C                   eval      routne = set1
      *
      * Reposition the subfile if the user has changed any of the positioners
      * or selectors in the control record.
      *
     C                   when      c1rpcdyr <> svrpcdyr or
     C                             c1rpcdwk <> svrpcdwk or
     C                             c1rpwemdy <> svrpwemdy or
     C                             c1fscd <> svfscd or
     C                             c1cell <> svcell or
     C                             c1pwcdyr <> svpwcdyr or
     C                             c1pwcdwk <> svpwcdwk or
     C                             c1pwwemdy <> svpwwemdy
     C                   exsr      $chksv
     C                   eval      routne = set1
      *
     C                   when      *in25 = *on                                  F25-ROLLUP
     C                   exsr      $load1
     C                   other
      *
      * User has pressed enter
      *
     C                   eval      routne = set1
     C                   move      yes           reset
     C                   endsl
      *
     C                   endsr
      /EJECT
      *----------------------------------------------------------------
      * Save the control record positioners/selectors
      *----------------------------------------------------------------
      *
     C     $chksv        begsr
      *
     C                   movel     no            reset
     C                   eval      routne = set1
      *
     C                   z-add     c1fscd        svfscd
     C                   z-add     c1cell        svcell
      *
     C                   z-add     c1rpcdyr      svrpcdyr
     C                   z-add     c1rpcdwk      svrpcdwk
     C                   z-add     c1rpwemdy     svrpwemdy
      *
     C                   z-add     c1pwcdyr      svpwcdyr
     C                   z-add     c1pwcdwk      svpwcdwk
     C                   z-add     c1pwwemdy     svpwwemdy
      *
      *
      * Flip Report Week-Ending Date selector from mmddyy to ccyymmdd format
      *
     C     *mdy          test(d)                 c1rpwemdy              92
     C                   if        *in92 = *on                                  If bad date
     C                   z-add     0             wkrpwedt
     C                   else
     C     *mdy          move      c1rpwemdy     wkisodate
     C                   move      wkisodate     wkrpwedt
     C                   endif                                                  If bad date
      *
      *
      * Flip Projection Week-ending Date selector from mmddyy to ccyymmdd format
      *
     C     *mdy          test(d)                 c1pwwemdy              92
     C                   if        *in92 = *on                                  If bad date
     C                   z-add     0             wkpwwedt
     C                   else
     C     *mdy          move      c1pwwemdy     wkisodate
     C                   move      wkisodate     wkpwwedt
     C                   endif                                                  If bad date
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * F4 prompt on screen 1
      *---------------------------------------------------------------
      *
     C     $f4s1         begsr
      *
      * Farm site
     C                   select
     C                   when      fld = 'C1FSCD'
     C                   call      'HP5045'
     C                   parm      *blank        xxaist
     C                   parm      0             xxfscd
     C                   parm      *blank        xxfsnm
     C                   seton                                        52
     C                   if        xxfscd <> 0
     C                   z-add     xxfscd        c1fscd
     C                   endif
     C                   other
      *
      * F4 not valid for this field...issue message
      *
     C                   if        error < maxmsg
     C                   add       1             error                                        013
     C                   movel     'HS09011'     mgid(e)
     C                   endif
     C                   endsl
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Clear subfile for screen 1
      *---------------------------------------------------------------
      *
     C     $clr1         begsr
      *
     C                   z-add     0             svrrn1
      *
     C                   clear                   hp7019s1
     C                   seton                                        83        sflclr
     C                   write     hp7019c1
     C                   setoff                                       818283
      *                                                    81=sfldsp
      *                                                    82=sflend
      *                                                    83=sflclr
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Refresh/fill the subfile for screen 1
      *---------------------------------------------------------------
      *
     C     $frsh1        begsr
      *
      * Set position in file
      *
     C                   select
     C                   when      c1pwcdyr = 0
     C     xxfsbo        setll     hsl086b
     C                   other
     C     key01         setll     hsl086b
     C                   endsl
      *
      *
      * The field 'reset' is a flag field to indicate whether you are
      * attempting to reposition the user close to where he was in the
      * subfile.
      * The lowest subfile RRN on the screen from the DSPF feedback
      * area (IDFLOW) was retrieved earlier and put in WKFLOW.
      * Add to WKFLOW the number of lines on your subfile page or the
      * number less 1 to determine a subfile RRN to reposition to.
      *
      *
     C                   if        reset = yes
     C                   eval      maxrrn = wkflow + wkrlin
     C                   else
      *
      * Don't try to reposition the user, just start the subfile over
      *
     C                   eval      maxrrn = wkplin
     C                   endif
      *
      * Load the subfile until you have the required number of records.
      * The number will depend on whether uou are repositioning the
      * subfile or not. Stop if you get to the end of the subfile.
      *
     C                   dou       svrrn1 >= maxrrn or *in82 = *on
     C                   exsr      $load1
     C                   enddo
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Load subfile for screen 1
      *---------------------------------------------------------------
      *
     C     $load1        begsr
      *
     C                   z-add     svrrn1        hdrrn1
      *
      * Set the count of records on the subfile page to zero.
      *
     C                   z-add     0             count
      *
      * Fill the subfile until the end of the database file or
      * you have filled all the lines on the subfile page.
      *
     C                   setoff                                       82
     C                   dou       *in82 = *on or count >= wkplin               Do fill
     C                   read      hsl086b                                82
     C                   if        *in82 = *off                                 If read
      *
      *
      * Retrieve cell associated with farm site.
      *
     C     pwfscd        chain     hsp018                             92
     C                   if        *in92 = *off                                 If found
     C                   z-add     fscell        d1cell
     C                   else
     C                   z-add     0             d1cell
     C                   endif                                                  If found
      *
      *
      * Only include records that match the user's selectors
      *
     C                   if        c1fscd <> 0 and                              If farm
     C                             c1fscd <> pwfscd
     C                   else
      *
     C                   if        c1rpcdyr <> 0 and                            If report year
     C                             c1rpcdyr <> pwrpcdyr
     C                   else
      *
     C                   if        c1rpcdwk <> 0 and                            If report week
     C                             c1rpcdwk <> pwrpcdwk
     C                   else
      *
     C                   if        c1rpwemdy <> 0 and                           If report date
     C                             wkrpwedt  <> pwrpwedt
     C                   else
      *
     C                   if        c1cell <> 0 and                              If cell
     C                             c1cell <> fscell
     C                   else
      *
     C                   if        c1pwwemdy <> 0 and                           If project date
     C                             wkpwwedt  <> pwpwwedt
     C                   else
      *
      *
     C                   add       1             count
     C                   add       1             hdrrn1
     C                   z-add     pwrpcdyr      d1rpcdyr
     C                   z-add     pwrpcdwk      d1rpcdwk
     C                   z-add     pwfscd        d1fscd
     C                   z-add     pwpwcdyr      d1pwcdyr
     C                   z-add     pwpwcdwk      d1pwcdwk
     C                   z-add     pwtopwhd      d1topwhd
     C                   z-add     pwpwsohd      d1pwsohd
     C                   z-add     pwsocuhd      d1socuhd
     C                   z-add     pwbocuhd      d1bocuhd
      *
      *
      * Flip report week-ending date from CCYYMMDD to MMDDYY.
      *
     C                   if        pwrpwedt <> 0                                If report date
     C     *iso          move      pwrpwedt      wkmdydate
     C                   move      wkmdydate     d1rpwemdy
     C                   endif                                                  If report date
      *
      * Flip projection week-ending date from CCYYMMDD to MMDDYY.
      *
     C                   if        pwpwwedt <> 0                                If project date
     C     *iso          move      pwpwwedt      wkmdydate
     C                   move      wkmdydate     d1pwwemdy
     C                   endif                                                  If project date
      *
     C                   write     hp7019s1
     C                   seton                                        81
      *
      * For positioning, place the subfile relative record number into
      * the sflrcdnbr field (hdpag1)
      *
     C                   eval      hdpag1 = hdrrn1
      *
     C                   endif                                                  If project date
     C                   endif                                                  If cell
     C                   endif                                                  If report date
     C                   endif                                                  If report week
     C                   endif                                                  If report year
     C                   endif                                                  If farm
     C                   endif                                                  If read
     C                   enddo                                                  Do fill
      *
      *
      * If no records were written to the subfile, then issue
      * the standard message 'No data to Display'.
      *
     C                   if        hdrrn1 = 0                                   If no recs
     C                   movel     yes           norecs
      *
     C                   if        error < maxmsg
     C                   add       1             error
     C                   eval      mgid(e) = 'HS09002'
     C                   endif
      *
     C                   else
     C                   movel     no            norecs
     C                   endif                                                  If no recs
      *
     C                   z-add     hdrrn1        svrrn1
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Seton last record and end job
      *---------------------------------------------------------------
      *
     C     $exit         begsr
     C                   seton                                        LR
     C                   endsr
      *
      /EJECT
      *---------------------------------------------------------------
      * Write error messages
      *---------------------------------------------------------------
      *
     C     $wrmsg        begsr
      *
     C                   call      'UT80045J'                           43
     C                   parm                    mgid
     C                   parm                    mgdt
     C                   parm                    error
     C                   parm      sdpgm         pgm
     C                   parm      msgfil        msgfl
      *
      * If call to UT80045J failed, a message hardcoded in the DSPF
      * command line format will be set on. So, redisplay screen.
      *
     C                   if        *in43 = *on
     C                   eval      routne = set1
     C                   endif
      *
     C                   endsr
      *
      *----------------------------------------------------------------
      * Clear messages
      *----------------------------------------------------------------
      *
     C     $clmsg        begsr
      *
     C                   call      'UT80045J'                           43
     C                   parm      *blank        mgi
     C                   parm      *blank        mgd
     C                   parm      *zero         error
     C                   parm      sdpgm         pgm
     C                   parm      msgfil        msgfl
      *
      * If call to UT80045J failed, a message hardcoded in the DSPF
      * command line format will be set on; so, redisplay screen.
      *
     C                   if        *in43 = *on
     C                   eval      routne = set1
     C                   endif
      *
     C                   endsr
      /EJECT
      *----------------------------------------------------------------
      * Initialization subroutine
      *----------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Parm lists
      *
     C     *entry        plist
     C                   parm                    xxfsbo
      *
      * Key lists
      *
     C     key01         klist
     C                   kfld                    xxfsbo
     C                   kfld                    c1pwcdyr
     C                   kfld                    c1pwcdwk
      *
      * To support F5-Refresh, logic requires the number of lines on
      * the subfile page and also the number of lines less 1.  By
      * setting these values up as constant work fields, the processing
      * logic can be standardized between programs.
      *
     C                   z-add     14            wkplin                         sflpag
     C     wkplin        sub       1             wkrlin                         sflpag - 1
      *
      *
      * The following standard code must be included to make the
      * standard error message handling program function properly.
      * This code sets and clears the program message queue.
      *
     C                   movel     '*'           msgpgm
     C                   exsr      $clmsg
      *
     C                   endsr
      /eject
