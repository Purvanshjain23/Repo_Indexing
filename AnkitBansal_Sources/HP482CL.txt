/***************************************************************** */
/*  PROGRAM NUMBER --> HP482CL                                     */
/*  PROGRAM NAME ----> CLOSED HOG GROUP PERFORMANCE                */
/*  PROGRAMMER ------> SHARON ZURN               07/29/94          */
/*                                                                 */
/*  PROGRAM PURPOSE -> THIS CL CALLS THE PROGRAM TO PRINT A        */
/*                     CLOSED HOG GROUP PERFORMANCE REPORT.        */
/*                     VALUES ARE PASSED THRU THE LDA FROM         */
/*                     SPECIFY HP482. THEY INCLUDE BUSINESS OFFICE,*/
/*                     FROM DATE, TO DATE, PROD PHASE, AND FARM    */
/*                     SITE, AND PRODUCTION TYPE.                  */
/*                                                                 */
/***************************************************************** */
/*  MODIFIED BY -----> MARY PALMER   OMIT BGF GROUPS FROM OPNQRY   */
/*                     10/05/94                                    */
/*                                                                 */
/*  MODIFIED BY -----> MARY PALMER   MAKE BUSINESS OFFICE OPTIONAL */
/*                     10/19/94      INCLUDE ALL IF BLANK          */
/*                                                                 */
/*  MODIFIED BY -----> MARY PALMER   FOR RANKING, BUILD WORKFILES  */
/*                     10/20/94      HSP388 & HSP289 AND RUN PRINT */
/*                                   PROGRAM HP688.  USE OPNQRYS TO*/
/*                                   ON WORKFILE HSP388 TO SEQUENCE*/
/*                                   FOR NO RANKING, RUN HP682.    */
/*                                                                 */
/*  12/05/94  LEANNE FEDOR                                         */
/*            CHANGED WORKFILE LOGIC. WE WILL NOW CREATE WORKFILES */
/*            IN QTEMP INSTEAD OF CLEARING THEM IN PRODUCTION.     */
/*                                                                 */
/*  01/23/95  LEANNE FEDOR                                         */
/*            ADDED A THIRD WORKFILE. AND, CHANGED LOGIC TO USE    */
/*            THE WORKFILES FOR BOTH PRINT PROGRAMS                */
/*                                                                 */
/*  03/13/95  LEANNE FEDOR                                         */
/*            RECOMPILE ONLY.  ORIGIN BGF FARM SITE AND ORIGIN     */
/*            NURSERY FARM SITE ADDED TO HOG GROUP FILE            */
/*                                                                 */
/*  04/25/95  LEANNE FEDOR                                         */
/*            CHANGED FARM SITE FROM ALPHA TO NUMERIC.             */
/*                                                                 */
/*  11/01/95  LEANNE FEDOR                                         */
/*            ADDED SELECTION LOGIC FOR THE NEW FIELD 'GROUP       */
/*            TYPE'.                                               */
/*                                                                 */
/*  04/12/96  LEANNE FEDOR                                         */
/*            ADDED A NEW SELECTION ON 'BREED STOCK'.  ALSO ADDED  */
/*            CUSTOMIZED PRINTER OVERRIDE COMMAND TO HANDLE        */
/*            PRINTING OF REPORT ON LEGAL PAPER IN LASER PRINTER.  */
/*                                                                 */
/* 05/21/96  LEANNE FEDOR                                          */
/*           IN PREPARATION FOR HAVING TWO BOXES (CISC AND RISC),  */
/*           REPLACED STANDARD CREATE DUPLICATE OBJECT COMMAND     */
/*           WITH A COMMAND CUSTOMIZED BY JOHN ERICSON. THIS       */
/*           COMMAND WILL RETRIEVE/USE THE FILE FROM THE APPRO-    */
/*           PRIATE LIBRARY INSTEAD OF USING HARDCODED 'PRKFLIB'.  */
/*           THE CUSTOMIZED COMMAND IS CRTDUPOBJL FOR 'CREATE      */
/*           DUPLICATE OBJECT FROM LIBRARY'.                       */
/*                                                                 */
/* 08/09/96  LEANNE FEDOR                                          */
/*           RECOMPILE ONLY. FIELDS ADDED TO WORKFILES.            */
/*                                                                 */
/* 08/14/96  LEANNE FEDOR                                          */
/*           RECOMPILE ONLY. FIELD ADDED TO WORKFILES.             */
/*                                                                 */
/* 06/03/97 LEANNE FEDOR                                           */
/*          RECOMPILE ONLY.  NEW FIELDS 'PURGE FLAG' AND 'DATE OF  */
/*          FIRST PURGE' ADDED TO HOG GROUP FILE.                  */
/*                                                                 */
/* 06/19/97 LEANNE FEDOR                                           */
/*          CHANGED OUT QUEUE LOGIC.                               */
/*******************************************************************/

             PGM

/* DEFINE THE VARIABLE REQUIRED IN THE COMMAND TO OVERRIDE THE     */
/* PRINT FILE TO A LASER PRINTER                                   */

             DCL        VAR(&CMD) TYPE(*CHAR) LEN(512)

             DCL        VAR(&QDATA)  TYPE(*CHAR) LEN(250)
             DCL        VAR(&LDFSBO) TYPE(*CHAR) LEN(5)
             DCL        VAR(&LDFROM) TYPE(*DEC)  LEN(8)
             DCL        VAR(&LDTO)   TYPE(*DEC)  LEN(8)
             DCL        VAR(&LDPPCD) TYPE(*CHAR) LEN(5)
             DCL        VAR(&LDPTCD) TYPE(*CHAR) LEN(5)
             DCL        VAR(&LDFSCD) TYPE(*DEC) LEN(5)
             DCL        VAR(&LDGTCD) TYPE(*CHAR) LEN(1)
             DCL        VAR(&LDOPTN) TYPE(*CHAR) LEN(1)
             DCL        VAR(&LDBSCD) TYPE(*CHAR) LEN(5)

             DCL        VAR(&HOLD) TYPE(*CHAR) LEN(10)
             DCL        VAR(&COPYS) TYPE(*DEC) LEN(2)
             DCL        VAR(&OUTQ) TYPE(*CHAR) LEN(10)

             CHGVAR     VAR(&LDFSBO) VALUE(%SST(*LDA 1 5))
             CHGVAR     VAR(&LDFROM) VALUE(%SST(*LDA 6 8))
             CHGVAR     VAR(&LDTO)   VALUE(%SST(*LDA 14 8))
             CHGVAR     VAR(&LDPPCD) VALUE(%SST(*LDA 27 5))
             CHGVAR     VAR(&LDPTCD) VALUE(%SST(*LDA 22 5))
             CHGVAR     VAR(&LDFSCD) VALUE(%SST(*LDA 32 5))
             CHGVAR     VAR(&LDOPTN) VALUE(%SST(*LDA 37 1))
             CHGVAR     VAR(&LDGTCD) VALUE(%SST(*LDA 52 1))
             CHGVAR     VAR(&LDBSCD) VALUE(%SST(*LDA 53 5))


/* ALWAYS SELECT ONLY CLOSED HOG GROUPS                           */

             CHGVAR     VAR(%SST(&QDATA 1 12)) VALUE('HGGSCD *EQ "')
             CHGVAR     VAR(%SST(&QDATA 13 2)) VALUE('CL')
             CHGVAR     VAR(%SST(&QDATA 15 1)) VALUE('"')

/* ALWAYS SELECT ONLY GROUPS WITH A CLOSE DATE IN THE SELECTED RANGE */

             CHGVAR     VAR(%SST(&QDATA 17 15)) VALUE('*AND HGCLDT +
                          *GE')
             CHGVAR     VAR(%SST(&QDATA 33 8)) VALUE(&LDFROM)
             CHGVAR     VAR(%SST(&QDATA 42 15)) VALUE('*AND HGCLDT +
                          *LE')
             CHGVAR     VAR(%SST(&QDATA 58 8)) VALUE(&LDTO)

/*  IF HAVE A BUSINESS OFFICE, RESTRICT TO SELECTED OFFICE       */

             IF         COND(&LDFSBO *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 67 17)) VALUE('*AND HGFSBO +
                          *EQ "')
             CHGVAR     VAR(%SST(&QDATA 84 5)) VALUE(&LDFSBO)
             CHGVAR     VAR(%SST(&QDATA 89 1)) VALUE('"')
             ENDDO

/*  IF PRODUCTION TYPE IS NOT BLANK, THEN ADD A TYPE SELECTION */

             IF         COND(&LDPTCD *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 91 17)) VALUE('*AND HGPTCD +
                          *EQ "')
             CHGVAR     VAR(%SST(&QDATA 108 5)) VALUE(&LDPTCD)
             CHGVAR     VAR(%SST(&QDATA 113 1)) VALUE('"')
             ENDDO

/*  IF PRODUCTION PHASE IS NOT BLANK, THEN ADD A PHASE SELECTION */

             IF         COND(&LDPPCD *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 115 17)) VALUE('*AND HGPPCD +
                          *EQ "')
             CHGVAR     VAR(%SST(&QDATA 132 5)) VALUE(&LDPPCD)
             CHGVAR     VAR(%SST(&QDATA 137 1)) VALUE('"')
             ENDDO

/*  IF FARM SITE IS NOT A BLANK, THEN ADD A FARM SITE SELECTION  */

             IF         COND(&LDFSCD *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 139 4)) VALUE('*AND')
             CHGVAR     VAR(%SST(&QDATA 144 10)) VALUE('HGFSCD *EQ')
             CHGVAR     VAR(%SST(&QDATA 155 5)) VALUE(&LDFSCD)
             ENDDO

/*  IF GROUP TYPE IS NOT BLANK, THEN ADD A GROUP TYPE SELECTION  */

             IF         COND(&LDGTCD *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 161 17)) VALUE('*AND HGGTCD +
                          *EQ "')
             CHGVAR     VAR(%SST(&QDATA 178 1)) VALUE(&LDGTCD)
             CHGVAR     VAR(%SST(&QDATA 179 1)) VALUE('"')
             ENDDO

/*  IF THE USER SELECTED A BREED STOCK                           */

             IF         COND(&LDBSCD *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 181 17)) VALUE('*AND HGBSCD +
                          *EQ "')
             CHGVAR     VAR(%SST(&QDATA 198 5)) VALUE(&LDBSCD)
             CHGVAR     VAR(%SST(&QDATA 203 1)) VALUE('"')
             ENDDO

/*  ALWAYS OMIT "BGF" PRODUCTION PHASE GROUPS                    */

             CHGVAR     VAR(%SST(&QDATA 205 23)) VALUE('*AND HGPPCD +
                          *NE "BGF  "')

/* RUN QUERY OVER THE HOG GROUP FILE                                 */

             OVRDBF     FILE(HSP034) SHARE(*YES)
             OPNQRYF    FILE((HSP034)) QRYSLT(&QDATA) +
                          KEYFLD((HGPPCD) (HGFSCD) (HGCLDT) +
                          (HGHGCD)) SEQONLY(*YES)

/*------------------------------------------------------------------*/
/*  ALWAYS CREATE THREE WORKFILES                                   */
/*------------------------------------------------------------------*/

             CRTDUPOBJL OBJ(HSP387) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)

             CRTDUPOBJL OBJ(HSP388) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)

             CRTDUPOBJL OBJ(HSP389) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)

             CALL       PGM(HP388)

/*------------------------------------------------------------------*/
/* SETUP THE REPORT PRINT PARAMETERS                                */
/*------------------------------------------------------------------*/

 HOLD:       IF         COND(%SST(*LDA 411 1) = 'Y') THEN(CHGVAR +
                          VAR(&HOLD) VALUE(*YES))
             ELSE       CMD(CHGVAR VAR(&HOLD) VALUE(*NO))

 COPYS:      CHGVAR     VAR(&COPYS) VALUE(%SST(*LDA 412 1))
             IF         COND(&COPYS = 0) THEN(CHGVAR VAR(&COPYS) +
                          VALUE(1))

 OUTQ:       CHGVAR     VAR(&OUTQ) VALUE(%SST(*LDA 413 10))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(%SST(*LDA 401 10)))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(*JOB))


/*------------------------------------------------------------------*/
/* OVERRIDE PRINT FILE                                              */
/*------------------------------------------------------------------*/

/* USE THE CUSTOMIZED SEABOARD COMMAND TO OVERRIDE THE PRINT FILE.  */
/* THIS COMMAND ALLOWS THE REPORT TO BE GENERATED ON A STANDARD LINE*/
/* PRINTER OR A LASER PRINTER. TO USE THIS OVERRIDE YOU MUST DECLARE*/
/* A COMMAND VARIABLE AND FOLLOW THE OVERRIDE WITH A CALL TO        */
/* QCMDEXC.                                                         */

/* THIS REPORT IS CONDENSED PRINT AT 15 CPI; SO, ON A LASER PRINTER */
/* FORCE IT TO DRAWER '2' FOR LEGAL PAPER AND FORCE 'LANDSCAPE'     */

             SBDPRTOVR  CMDVAR(&CMD) FILE(QPRINT) OUTQ(&OUTQ) +
                          WIDTH(198) LANDSCAPE(*YES) DRAWER(2) +
                          COPIES(&COPYS) HOLD(&HOLD)

             CALL       PGM(QCMDEXC) PARM(&CMD 512)

/*------------------------------------------------------------------*/
/*  IF NO RANKING WAS SELECTED                                      */
/*------------------------------------------------------------------*/

             IF         COND(&LDOPTN *EQ 'N') THEN(DO)
             OVRDBF     FILE(HSP387) SHARE(*YES)
             OPNQRYF    FILE((HSP387)) KEYFLD((WGPPCD) (WGFSCD) +
                          (WGDSDT) (WGHGCD)) SEQONLY(*YES)
             CALL PGM(HP682)
             GOTO       CMDLBL(EXIT)
             ENDDO

/*------------------------------------------------------------------*/
/*  IF RANKING WAS SELECTED                                         */
/*------------------------------------------------------------------*/

   IF       COND(&LDOPTN *NE 'N')  THEN(DO)

/*  IF AVERAGE DAILY GAIN WAS SELECTED                    */

             IF         COND(&LDOPTN *EQ 'A') THEN(DO)
             OVRDBF     FILE(HSP387) SHARE(*YES)
             OPNQRYF    FILE((HSP387)) KEYFLD((WGPPCD) (WGAGLB +
                          *DESCEND)) SEQONLY(*YES)
             ENDDO

/*  IF BACKFAT INCHES WAS SELECTED                        */

             IF         COND(&LDOPTN *EQ 'B') THEN(DO)
             OVRDBF     FILE(HSP387) SHARE(*YES)
             OPNQRYF    FILE((HSP387)) KEYFLD((WGPPCD) (WGBFIN))     +
                          SEQONLY(*YES)
             ENDDO

/*  IF CARCASS YIELD WAS SELECTED                         */

             IF         COND(&LDOPTN *EQ 'C') THEN(DO)
             OVRDBF     FILE(HSP387) SHARE(*YES)
             OPNQRYF    FILE((HSP387)) KEYFLD((WGPPCD) (WGCRYD +
                          *DESCEND)) SEQONLY(*YES)
             ENDDO

/*  IF DEATH LOSS PERCENT WAS SELECTED                    */

             IF         COND(&LDOPTN *EQ 'D') THEN(DO)
             OVRDBF     FILE(HSP387) SHARE(*YES)
             OPNQRYF    FILE((HSP387)) KEYFLD((WGPPCD) (WGDDPR)) +
                          SEQONLY(*YES)
             ENDDO

/*  IF FEED CONVERSION WAS SELECTED                       */

             IF         COND(&LDOPTN *EQ 'F') THEN(DO)
             OVRDBF     FILE(HSP387) SHARE(*YES)
             OPNQRYF    FILE((HSP387)) KEYFLD((WGPPCD) (WGFDCV)) +
                          SEQONLY(*YES)
             ENDDO

/*  IF LEAN PERCENT WAS SELECTED                          */

             IF         COND(&LDOPTN *EQ 'L') THEN(DO)
             OVRDBF     FILE(HSP387) SHARE(*YES)
             OPNQRYF    FILE((HSP387)) KEYFLD((WGPPCD) (WGLEAN +
                          *DESCEND)) SEQONLY(*YES)
             ENDDO

/*  IF MARGIN PER CWT WAS SELECTED                          */

             IF         COND(&LDOPTN *EQ 'M') THEN(DO)
             OVRDBF     FILE(HSP387) SHARE(*YES)
             OPNQRYF    FILE((HSP387)) KEYFLD((WGPPCD) (WGM$CW +
                          *DESCEND)) SEQONLY(*YES)
             ENDDO

             CALL PGM(HP688)
             ENDDO

EXIT:        CLOF       OPNID(HSP034)
             CLOF       OPNID(HSP387)
             DLTOVR     FILE(*ALL)
             ENDPGM

