      *****************  RPG PROGRAM HEADING  ************************
     h option(*SRCSTMT:*NODEBUGIO)
      *
      * ENVIRONMENT: Pork Division
      * SYSTEM:      Margin Management
      * PROGRAM:     SD203 - Signal Demand: Customer
      * PROGRAMMER:  Alice
      * CREATED:     12/27/07
      *
      * FUNCTION: This program builds the Customer Master file.
      *
      **********************************************************************************************
      * MODIFICATIONS:
      **********************************************************************************************
      * Date      Programmer
      *
      * 04/09/09  LeAnne Ramsey
      *           Since we keep having Datamart issues during Disaster Recovery testing,
      *           we changed this program to use the real Customer Master instead of the
      *           Datamart Customer Master.
      /eject
      **********************************************************************************************
      * FILE SPECIFICATIONS
      **********************************************************************************************
      *
     Fcaajrel1  if   e           k disk
      *  Type Codes
      *
      *
     Fcabbrep   if   e           k disk
      *   Customer
      *
      *
     Fcaeorel3  if   e           k disk
      *  Company/Customer
      *
      *
     Fpdjxrel1  if   e           k disk
      *  Customer Group Master
      *
      *
     Fsdp203    o    e           k disk
      *    Signal Demand: Customer
      *
      /EJECT
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      *---------------------------------------------------------------
      *  Named Constants
      *---------------------------------------------------------------
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      ****************************************************************
      * Data Structures
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ***************************************************************************************
      * Mainline
      ***************************************************************************************
      *
      * Read each record in the OMS Customer file.
      *
     C                   dou       *in90 = *on                                  Main do loop
     C                   read      cabbrep                                90
     C                   if        *in90 = *off                                 If not EOF
      *
      * Do not process customers that:
      *   1) are not set up in the company/customer file
      *
     C     bbbkc7        chain     caeorel3                           92
     C                   if        *in92 = *off                                 If process
      *
      *
      * Set up Datamart fields that are populated from:
      *  1) OMS Company/customer file
      *  2) OMS Customer file
      *
     C                   exsr      $compcust
     C                   exsr      $directmap
      *
      * Write record
     C                   write     curec
     C                   clear                   curec
      *
     C                   endif                                                  If process
     C                   endif                                                  If not EOF
     C                   enddo                                                  Main do loop
      *
     C                   seton                                        lr
      /EJECT
      *----------------------------------------------------------------
      * Populate fields that are valued from the Company/customer file
      *----------------------------------------------------------------
      *
     C     $compcust     begsr
      *
      * Populate the customer group code, then retrieve its description from the
      * Customer Group Master.  Concatenate the code/description into a single field.
      *
     C                   move      eot9cd        cucugrcd
     C     eot9cd        chain     pdjxrel1                           92
     C                   if        *in92 = *off
     C                   eval      cucugrds =  %trim(cucugrcd) + '-' +
     C                                         %trim(jxndna)
     C                   endif
      *
      *
      * Populate the Customer Type code, then retrieve its description
      * from the Customer Type Master.  Concatenate the code/description
      * into a single field.
      *
     C                   move      eobocd        cucutycd
     C     eobocd        chain     caajrel1                           92
     C                   if        *in92 = *off
     C                   eval      cucutyds =  %trim(cucutycd) + '-' +
     C                                         %trim(ajauna)
     C                   endif
      *
      * Distributor Y/N Flag
      * Brokerage flag
      * Sales route
     C                   move      eoq6st        cudistfl
     C                   move      eollst        cubkfl
     C                   move      eor4cd        cusrcd
      *
     C                   endsr
      /EJECT
      *----------------------------------------------------------------
      * Populate fields that are a direct map from the customer master
      *----------------------------------------------------------------
      *
     C     $directmap    begsr
      *
      * Customer number/name/status
      *
     C                   z-add     bbbkc7        cucuno
     C                   move      bbaytx        cucuname
     C                   move      bbvsst        custatus
      *
      * City/state/country/zip
     C                   move      bbajna        cucity
     C                   move      bbbgcd        custate
     C                   move      bbkxcd        cucntry
     C                   move      bbbttx        cuzip
      *
      * A/R customer
      * Parent customer
     C                   z-add     bbanc7        cuarcuno
     C                   z-add     bbbtc7        cupacuno
      *
      * Ship to customer flag
      *
     C                   move      bbrjst        cushcufl
      *
      * Determine A/R and Parent Flags
      *
     C                   if        cucuno = cuarcuno
     C                   move      yes           cuarcufl
     C                   else
     C                   move      no            cuarcufl
     C                   endif
      *
     C                   if        cucuno = cupacuno
     C                   move      yes           cupacufl
     C                   else
     C                   move      no            cupacufl
     C                   endif
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Initialization
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
     C                   endsr
      /EJECT
