     A*%%TS  SD  20031230  065426  ISLRAMS     REL-V5R2M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD4094
     A*  TITLE:         SPECIFY EXPENSE CODE/REPORT CODE CHANGE
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  CREATED:       12/29/03
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A N99                                  CF04(04 'PROMPT')
     A  44N99                               CF08(08 'SUBMIT')
     A N99                                  CF20(20 'WRKSPLF')
     A*
     A*---------------------------------------------------------------
     A* RECORD FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP4094R1
     A*%%TS  SD  20031230  065426  ISLRAMS     REL-V5R2M0  5722-WDS
     A                                      SETOF(31)
     A                                      SETOF(51)
     A                                      SETOF(32)
     A                                      SETOF(52)
     A                                      OVERLAY
     A                                      RTNCSRLOC(&RCD &FLD)
     A            RCD       R        H      REFFLD(@@RCD)
     A            FLD       R        H      REFFLD(@@FLD)
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 25'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USR)
     A                                      DSPATR(HI)
     A                                  2 18'SPECIFY EXPENSE CODE/REPORT CODE C-
     A                                      HANGE'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@JOB)
     A                                      DSPATR(HI)
     A                                  4  3'This function is for I.S. personne-
     A                                      l only.'
     A                                  6  3'This function WILL change historic-
     A                                      al data. It will also associate the'
     A                                  7  3'Expense Code with the new Report C-
     A                                      ode in file HSP052-Expense Codes.'
     A                                  8  3'Before making the Report Code chan-
     A                                      ge, YOU must decide whether the req-
     A                                      uested'
     A                                  9  3'change is viable/OK/allowed---this-
     A                                       program WILL NOT make that decisio-
     A                                      n.'
     A                                 11  3'To give us a bit of a safety net, -
     A                                      the function has been written to ON-
     A                                      LY'
     A                                 12  3'allow modification from/to Report -
     A                                      Codes that are User-Controlled.'
     A                                 14  2'Expense code .......'
     A                                 14 23'?'
     A            D1EXCD    R        B 14 25REFFLD(HSRREF/@@EXCD *LIBL/HSPREF)
     A N31                                  DSPATR(HI)
     A  51
     AO 31                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A            D1EXDS    R        O 14 32REFFLD(EXREC/EXEXDS *LIBL/HSP052)
     A                                 15  2'Expense subclass ..:'
     A            D1ESCD    R        O 15 25REFFLD(HSRREF/EXESCD *LIBL/HSPREF)
     A                                 16  2'Expense class .....:'
     A            D1ECCD    R        O 16 25REFFLD(HSRREF/EXECCD *LIBL/HSPREF)
     A                                 17  2'Report category ...:'
     A            D1OLDCACD R        O 17 25REFFLD(HSRREF/@@CACD *LIBL/HSPREF)
     A                                 18  2'Report code .......:'
     A            D1OLDRPCD R        O 18 25REFFLD(HSRREF/EXRPCD *LIBL/HSPREF)
     A                                 20  2'New report code ....'
     A                                 20 23'?'
     A            D1RPCD    R        B 20 25REFFLD(HSRREF/@@EXCD *LIBL/HSPREF)
     A N32                                  DSPATR(HI)
     A  52
     AO 32                                  DSPATR(PC)
     A  32                                  DSPATR(RI)
     A            D1RSDS    R        O 20 32REFFLD(HSRREF/RPRSDS *LIBL/HSPREF)
     A                                 21  2'Report category ....'
     A            D1CACD    R        O 21 25REFFLD(HSRREF/@@CACD *LIBL/HSPREF)
     A*
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R HP4094K1
     A*%%TS  SD  19980601  163600  ISLRAMS     REL-V3R7M0  5716-PW1
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A                                 23 11'F4=Prompt'
     A  44                             23 22'F8=Submit'
     A                                 23 60'F20=Spool Files'
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *---------------------------------------------------------------
     A          R HP4094ES                  SFL SFLMSGRCD(24)
      *
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
     A*---------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*---------------------------------------------------------------
     A*
     A          R HP4094EC                  SFLCTL(HP4094ES)
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
