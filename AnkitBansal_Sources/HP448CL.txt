/***************************************************************** */
/*                                                                  */
/*  PROGRAM NUMBER --> HP448CL                                      */
/*  PROGRAM NAME ----> ANNUAL SALES FOR TAX REPORT                  */
/*  PROGRAMMER ------> LEANNE FEDOR                                 */
/*  DATE ------------> 03/15/96                                     */
/*                                                                  */
/*  PROGRAM PURPOSE -> THIS CL GENERATES A WORKFILE AND THEN        */
/*                     EXECUTES THE PRINT PROGRAM.                  */
/*                     VALUES ARE PASSED THRU THE LDA FROM HP448.   */
/*                                                                  */
/*******************************************************************/
/*  MODIFICATIONS:                                                 */
/*                                                                 */
/* 05/21/96  LEANNE FEDOR                                          */
/*           IN PREPARATION FOR HAVING TWO BOXES (CISC AND RISC),  */
/*           REPLACED STANDARD CREATE DUPLICATE OBJECT COMMAND     */
/*           WITH A COMMAND CUSTOMIZED BY JOHN ERICSON. THIS       */
/*           COMMAND WILL RETRIEVE/USE THE FILE FROM THE APPRO-    */
/*           PRIATE LIBRARY INSTEAD OF USING HARDCODED 'PRKFLIB'.  */
/*           THE CUSTOMIZED COMMAND IS CRTDUPOBJL FOR 'CREATE      */
/*           DUPLICATE OBJECT FROM LIBRARY'.                       */
/*                                                                  */
/* 06/18/97  LEANNE FEDOR                                          */
/*           CHANGED OUTQ LOGIC                                    */
/*******************************************************************/

             PGM

/* DEFINE THE VARIABLE REQUIRED IN THE COMMAND TO OVERRIDE THE     */
/* PRINT FILE TO A LASER PRINTER                                   */

             DCL        VAR(&CMD) TYPE(*CHAR) LEN(512)

             DCL        VAR(&QDATA) TYPE(*CHAR) LEN(200)

             DCL        VAR(&LDFSBO) TYPE(*CHAR) LEN(5)
             DCL        VAR(&LDFYMD) TYPE(*DEC) LEN(8)
             DCL        VAR(&LDTYMD) TYPE(*DEC) LEN(8)
             DCL        VAR(&HOLD) TYPE(*CHAR) LEN(10)
             DCL        VAR(&COPYS) TYPE(*DEC) LEN(2)
             DCL        VAR(&OUTQ) TYPE(*CHAR) LEN(10)

             CHGVAR     VAR(&LDFSBO) VALUE(%SST(*LDA 1 5))
             CHGVAR     VAR(&LDFYMD) VALUE(%SST(*LDA 6 8))
             CHGVAR     VAR(&LDTYMD) VALUE(%SST(*LDA 14 8))


/*-------------------------------------------------------------------*/
/* SET UP QUERY OVER THE SALES HEADER                                */
/*-------------------------------------------------------------------*/

/*  SELECT ALL SALES RECORDS IN THE DATE RANGE USING RECEIVED DATE   */

             CHGVAR     VAR(%SST(&QDATA 1 10)) VALUE('SHRCDT *GE')
             CHGVAR     VAR(%SST(&QDATA 12 8)) VALUE(&LDFYMD)
             CHGVAR     VAR(%SST(&QDATA 21 15)) VALUE('*AND SHRCDT +
                          *LE')
             CHGVAR     VAR(%SST(&QDATA 37 8)) VALUE(&LDTYMD)

/*  IF BUSINESS OFFICE                                               */

             IF         COND(&LDFSBO *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 46 17)) VALUE('*AND SHFSBO +
                          *EQ "')
             CHGVAR     VAR(%SST(&QDATA 63 5)) VALUE(&LDFSBO)
             CHGVAR     VAR(%SST(&QDATA 68 1)) VALUE('"')
             ENDDO


/* RUN QUERY OVER THE SALES HEADER                              */

             OVRDBF     FILE(HSP084) SHARE(*YES)
             OPNQRYF    FILE((HSP084)) QRYSLT(&QDATA) +
                          KEYFLD((SHFSBO) (SHCVNO)) SEQONLY(*YES)

/*-------------------------------------------------------------------*/
/* CREATE WORKFILE                                                   */
/*-------------------------------------------------------------------*/

             CRTDUPOBJL OBJ(HSP347) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)

             CRTDUPOBJL OBJ(HSP348) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)

/* GENERATE WORKFILE                                                 */

             CALL HP348

/*-------------------------------------------------------------------*/
/* SET UP PRINT PARAMETERS FROM LDA                                  */
/*-------------------------------------------------------------------*/

 HOLD:       IF         COND(%SST(*LDA 411 1) = 'Y') THEN(CHGVAR +
                          VAR(&HOLD) VALUE(*YES))
             ELSE       CMD(CHGVAR VAR(&HOLD) VALUE(*NO))

 COPYS:      CHGVAR     VAR(&COPYS) VALUE(%SST(*LDA 412 1))
             IF         COND(&COPYS = 0) THEN(CHGVAR VAR(&COPYS) +
                          VALUE(1))

 OUTQ:       CHGVAR     VAR(&OUTQ) VALUE(%SST(*LDA 413 10))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(%SST(*LDA 401 10)))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(*JOB))


/* USE THE CUSTOMIZED SEABOARD COMMAND TO OVERRIDE THE PRINT FILE.  */
/* THIS COMMAND ALLOWS THE REPORT TO BE GENERATED ON A STANDARD LINE*/
/* PRINTER OR A LASER PRINTER. TO USE THIS OVERRIDE YOU MUST DECLARE*/
/* A COMMAND VARIABLE AND FOLLOW THE OVERRIDE WITH A CALL TO        */
/* QCMDEXC.                                                         */

/* THIS REPORT CAN FIT IN 80 COLUMNS AT 10 CPI                      */

             SBDPRTOVR  CMDVAR(&CMD) FILE(QPRINT) OUTQ(&OUTQ) +
                          WIDTH(80) COPIES(&COPYS) HOLD(&HOLD)

             CALL       PGM(QCMDEXC) PARM(&CMD 512)

/*  RUN REPORT                                                      */

             CALL       PGM(HP648)

             CLOF       OPNID(HSP084)
             DLTOVR     FILE(*ALL)

             ENDPGM

