     A*%%TS  SD  20021202  154655  ISLRAMS     REL-V5R1M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION
     A*  DISPLAY FILE:  HPD598 - SELECT CREATED OR OPEN HOG GROUP
     A*                          ON A FARM
     A*  CREATED BY:    LEANNE FEDOR
     A*  DATE:          05/12/95
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA12(12 'PREVIOUS')
     A N82                                  ROLLUP(25 'PAGE FORWARD')
     A*--------------------------------------------------------------*
     A* REFERENCE WINDOW RECORD FORMAT 1
     A*--------------------------------------------------------------*
     A          R HP598R1
     A*%%TS  SD  19950512  144332  ISLRAMS     REL-V3R0M5  5763-PW1
     A                                      TEXT('Reference window')
     A                                      WINDOW(*DFT 20 70)
     A                                      WDWBORDER((*DSPATR RI) (*CHAR '    -
     A                                          '))
     A*--------------------------------------------------------------*
     A* RECORD FORMAT ONE
     A*--------------------------------------------------------------*
     A          R HP598R2
     A                                      TEXT('Written in RPG pgm to prevent-
     A                                       screen from clearing')
     A                                      CLRL(*NO)
     A                                      OVERLAY
     A*--------------------------------------------------------------*
     A* RECORD FORMAT TWO
     A*--------------------------------------------------------------*
     A* THIS FORMAT IS NOT WRITTEN IN THE RPG PGM.  IT PREVENTS THE
     A* CALLING_PROGRAM_SCREEN FROM BEING CLEARED.
     A*--------------------------------------------------------------*
     A          R HP598R3
     A                                      TEXT('Dummy format not written in R-
     A                                      PG pgm.')
     A                                      ASSUME
     A                                 11  1' '
     A*--------------------------------------------------------------*
     A* SUBFILE FORMAT ONE
     A*--------------------------------------------------------------
     A          R HP598S1                   SFL
     A*%%TS  SD  20021202  154655  ISLRAMS     REL-V5R1M0  5722-WDS
     A                                      TEXT('subfile record format one')
     A            HDRRN1         4S 0H
     A            DFBLCD    R        O  9  1REFFLD(HSRREF/HGBLCD *LIBL/HSPREF)
     A            DFRMCD    R        O  9  8REFFLD(HSRREF/HGRMCD *LIBL/HSPREF)
     A            DFHGCD    R        O  9 15REFFLD(HGHGCD)
     A            DFGSCD    R        O  9 24REFFLD(HGGSCD)
     A            DFPTCD    R        O  9 28REFFLD(HSRREF/@@PTCD *LIBL/HSPREF)
     A            DFPPCD    R        O  9 35REFFLD(HSRREF/@@PPCD *LIBL/HSPREF)
     A            DFGTCD    R        O  9 45REFFLD(HSRREF/@@GTCD *LIBL/HSPREF)
     A            DFOPDTMDY R        O  9 51REFFLD(HSRREF/@@DT06 *LIBL/HSPREF)
     A                                      EDTWRD('  /  /  ')
     A            DFCRDTMDY R        O  9 61REFFLD(HSRREF/@@DT06 *LIBL/HSPREF)
     A                                      EDTWRD('  /  /  ')
     A*--------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*--------------------------------------------------------------
     A          R HP598C1                   SFLCTL(HP598S1)
     A*%%TS  SD  20021202  154655  ISLRAMS     REL-V5R1M0  5722-WDS
     A                                      SFLSIZ(0050)
     A                                      SFLPAG(0008)
     A                                      WINDOW(HP598R1)
     A                                      TEXT('control format one')
     A                                      OVERLAY
     A                                      SFLCSRRRN(&HDRRN)
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND(*MORE)
     A                                      RMVWDW
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            HDRRN          5S 0H
     A                                  1  9'SELECT CREATED OR OPEN HOG GROUP O-
     A                                      N A FARM'
     A                                      DSPATR(HI)
     A                                  2  1'Position Cursor, Press Enter'
     A                                  4  1'Farm:'
     A            C1FSCD    R     Y  O  4  8REFFLD(FSFSCD)
     A                                      EDTCDE(M)
     A            C1FSNM    R        O  4 15REFFLD(FSREC/FSFSNM *LIBL/HSP018)
     A                                  6 28'-Production-'
     A                                      DSPATR(HI)
     A                                  6 43'Group'
     A                                      DSPATR(HI)
     A                                  6 53'Open'
     A                                      DSPATR(HI)
     A                                  6 62'Create'
     A                                      DSPATR(HI)
     A                                  7  1'Bldg'
     A                                      DSPATR(HI)
     A                                  7  8'Room'
     A                                      DSPATR(HI)
     A                                  7 15'Group'
     A                                      DSPATR(HI)
     A                                  7 24'St'
     A                                      DSPATR(HI)
     A                                  7 28'Type'
     A                                      DSPATR(HI)
     A                                  7 35'Phase'
     A                                      DSPATR(HI)
     A                                  7 44'Type'
     A                                      DSPATR(HI)
     A                                  7 53'Date'
     A                                      DSPATR(HI)
     A                                  7 63'Date'
     A                                      DSPATR(HI)
     A            C1BLCD    R        B  8  1REFFLD(@@BLCD)
     A                                      DSPATR(HI)
     A            C1RMCD    R        B  8  8REFFLD(HSRREF/@@RMCD *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A            C1GSCD    R        B  8 24REFFLD(HGGSCD)
     A                                      DSPATR(HI)
     A          R HP598K1
     A*%%TS  SD  19940506  091739  ISRCENT     REL-V2R2M0  5738-PW1
     A                                      OVERLAY
     A                                      WINDOW(HP598R1)
     A                                 19  1'F12=Previous'
