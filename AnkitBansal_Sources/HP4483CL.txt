/***************************************************************** */
/*                                                                 */
/*  PROGRAM NUMBER:  HP4483CL                                      */
/*  PROGRAM NAME:    QUARTERLY PREDICTION VALUES LISTING           */
/*  PROGRAMMER:      LEANNE FEDOR                                  */
/*  CREATE DATE:     01/31/02                                      */
/*                                                                 */
/*  FUNCTION:        THIS CL IS SUBMITTED WITH QCMDEXC FROM        */
/*                   HP4483-SPECIFY QUARTERLY PREDICTION VALUES    */
/*                   LISTING OPTIONS.                              */
/*                                                                 */
/*                   OPTIONS ARE PASSED THROUGH THE LDA.           */
/*                                                                 */
/*******************************************************************/
/*  MODIFIED:                                                      */
/*******************************************************************/

             PGM

             DCL        VAR(&CMD) TYPE(*CHAR) LEN(512)
             DCL        VAR(&QDATA) TYPE(*CHAR) LEN(90)

             DCL        VAR(&LDCDYR) TYPE(*DEC) LEN(4)
             DCL        VAR(&LDCDQR) TYPE(*DEC) LEN(1)
             DCL        VAR(&LDCELL) TYPE(*DEC) LEN(2)
             DCL        VAR(&LDGTCD) TYPE(*CHAR) LEN(1)

             DCL        VAR(&HOLD) TYPE(*CHAR) LEN(10)
             DCL        VAR(&COPYS) TYPE(*DEC) LEN(2)
             DCL        VAR(&OUTQ) TYPE(*CHAR) LEN(10)

             CHGVAR     VAR(&LDCDYR) VALUE(%SST(*LDA 1 4))
             CHGVAR     VAR(&LDCDQR) VALUE(%SST(*LDA 5 1))
             CHGVAR     VAR(&LDCELL) VALUE(%SST(*LDA 6 2))
             CHGVAR     VAR(&LDGTCD) VALUE(%SST(*LDA 8 1))


/*-------------------------------------------------------------------*/
/* SET UP/RUN OPEN QUERY TO SELECT THE RECORDS                       */
/*-------------------------------------------------------------------*/

/* SET UP A DEFAULT STARTING POINT                                      */

             CHGVAR     VAR(%SST(&QDATA 1 12)) VALUE('QPCDYR *NE 0')

/*  CONDITIONALLY SELECT RECORDS FOR THE SPECIFIED YEAR                 */

             IF         COND(&LDCDYR *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 14 15)) VALUE('*AND QPCDYR *EQ')
             CHGVAR     VAR(%SST(&QDATA 30 4)) VALUE(&LDCDYR)
             ENDDO


/*  CONDITIONALLY SELECT RECORDS FOR THE SPECIFIED QUARTER        */

             IF         COND(&LDCDQR *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 35 15)) VALUE('*AND QPCDQR +
                          *EQ')
             CHGVAR     VAR(%SST(&QDATA 51 1)) VALUE(&LDCDQR)
             ENDDO


/*  CONDITIONALLY SELECT RECORDS FOR THE SPECIFIED CELL  */

             IF         COND(&LDCELL *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 53 15)) VALUE('*AND QPCELL +
                          *EQ')
             CHGVAR     VAR(%SST(&QDATA 69 2)) VALUE(&LDCELL)
             ENDDO


/*  CONDITIONALLY SELECT RECORDS FOR THE SPECIFIED GROUP TYPE */

             IF         COND(&LDGTCD *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 72 17)) VALUE('*AND QPGTCD *EQ "')
             CHGVAR     VAR(%SST(&QDATA 89 1)) VALUE(&LDGTCD)
             CHGVAR     VAR(%SST(&QDATA 90 1)) VALUE('"')
             ENDDO

             OVRDBF     FILE(HSP083) SHARE(*YES)
             OPNQRYF    FILE((HSP083)) QRYSLT(&QDATA) KEYFLD(*FILE) +
                          SEQONLY(*YES)

/*------------------------------------------------------------------*/
/*  SET UP PRINT FILE OVERRIDES                                      */
/*------------------------------------------------------------------*/

/* SET PARAMETERS FROM LDA                                           */

 HOLD:       IF         COND(%SST(*LDA 411 1) = 'Y') THEN(CHGVAR +
                          VAR(&HOLD) VALUE(*YES))
             ELSE       CMD(CHGVAR VAR(&HOLD) VALUE(*NO))

 COPYS:      CHGVAR     VAR(&COPYS) VALUE(%SST(*LDA 412 1))
             IF         COND(&COPYS = 0) THEN(CHGVAR VAR(&COPYS) +
                          VALUE(1))

 OUTQ:       CHGVAR     VAR(&OUTQ) VALUE(%SST(*LDA 413 10))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(%SST(*LDA 401 10)))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(*JOB))

/*------------------------------------------------------------------*/
/* OVERRIDE PRINT FILE                                              */
/*------------------------------------------------------------------*/

/* USE THE CUSTOMIZED SEABOARD COMMAND TO OVERRIDE THE PRINT FILE.  */
/* THIS COMMAND ALLOWS THE REPORT TO BE GENERATED ON A STANDARD LINE*/
/* PRINTER OR A LASER PRINTER. TO USE THIS OVERRIDE YOU MUST DECLARE*/
/* A COMMAND VARIABLE AND FOLLOW THE OVERRIDE WITH A CALL TO        */
/* QCMDEXC.                                                         */


             SBDPRTOVR  CMDVAR(&CMD) FILE(QPRINT) OUTQ(&OUTQ) +
                          WIDTH(137) LANDSCAPE(*YES) DRAWER(*PRTF) +
                          COPIES(&COPYS) HOLD(&HOLD)

             CALL       PGM(QCMDEXC) PARM(&CMD 512)


/*------------------------------------------------------------------*/
/*  CALL PRINT PROGRAM                                               */
/*------------------------------------------------------------------*/

             CALL       PGM(HP6083)

             CLOF       OPNID(HSP083)
             DLTOVR     FILE(HSP083)


             ENDPGM
