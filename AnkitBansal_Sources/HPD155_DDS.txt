     A*%%TS  SD  20010710  094017  ISLRAMS     REL-V4R4M0  5769-PW1
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD155
     A*  TITLE:         MAINTAIN BINS
     A*  PROGRAMMER:    ROSE CENTONZE
     A*  CREATED:       04/07/94
     A*
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A N90                                  CF05(05 'REFRESH')
     A N90                                  CF09(09 'ACCEPT')
     A                                      CA12(12 'PREVIOUS')
     A*
     A*-------------------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*-------------------------------------------------------------------------
     A          R HP155S1                   SFL
     A*%%TS  SD  20010710  094017  ISLRAMS     REL-V4R4M0  5769-PW1
     A  85                                  SFLNXTCHG
     A            HDRRN1    R        H      REFFLD(@@RRN)
     A            HDBNCD    R        H      REFFLD(@@BNCD)
     A            D1BNCD    R        B  8  3REFFLD(@@BNCD)
     A  31                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A N31N90                               DSPATR(HI)
     A            D1BICD    R        B  8 14REFFLD(HSRREF/@@BICD *LIBL/HSPREF)
     A  32                                  DSPATR(PC)
     A  32                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A N32N90                               DSPATR(HI)
     A*
     A*-------------------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*-------------------------------------------------------------------------
     A          R HP155C1                   SFLCTL(HP155S1)
     A*%%TS  SD  20010710  094017  ISLRAMS     REL-V4R4M0  5769-PW1
     A                                      SFLSIZ(0050)
     A                                      SFLPAG(0013)
     A N90                                  ROLLUP(25 'PAGE FORWARD')
     A                                      SFLCSRRRN(&HDRR1)
     A                                      SETOF(31)
     A                                      SETOF(32)
     A                                      OVERLAY
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A            HDPAG1    R        H      REFFLD(@@PAG)
     A                                      SFLRCDNBR
     A            HDRR1     R        H      REFFLD(@@RRN)
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 28'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USR)
     A                                      DSPATR(HI)
     A            C1MODE    R        O  2  2REFFLD(@@MODE)
     A                                      DSPATR(HI)
     A                                  2 32'MAINTAIN BINS'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@JOB)
     A                                      DSPATR(HI)
     A                                  4  3'Farm site ..:'
     A            C1FSCD    R        O  4 18REFFLD(FSFSCD)
     A                                      EDTCDE(M)
     A            C1FSNM    R        O  4 26REFFLD(FSFSNM)
     A                                  6  3'Bin'
     A                                  6 14'Bin Set'
     A                                  7  3'Code'
     A                                  7 15'Code'
     A*
     A*-------------------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*-------------------------------------------------------------------------
     A          R HP155K1
     A*%%TS  SD  20010418  140705  ISLRAMS     REL-V4R4M0  5769-PW1
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A N90                             23 11'F5=Refresh'
     A N90                             23 23'F9=Accept'
     A                                 23 34'F12=Previous'
      *
     A*-------------------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
     A*-------------------------------------------------------------------------
     A          R HP155ES                   SFL SFLMSGRCD(24)
      *
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
     A*-------------------------------------------------------------------------
      * ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*-------------------------------------------------------------------------
     A          R HP155EC                   SFLCTL(HP155ES)
      *
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
