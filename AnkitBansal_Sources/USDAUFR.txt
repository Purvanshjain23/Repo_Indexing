      *
      *  USE THIS SOURCE FOR CALCULATION OF TERMS TYPE 2
      *  SEE PARAMETER LIST FOR REQUIRED INPUTS AND RETURNED OUTPUTS
      *
     I            DS
     I                                        1   70UXWKDT
     I                                        1   10UC
     I                                        2   30UYY
     I                                        4   50UMM
     I                                        6   70UDD
     C           *LIKE     DEFN #IA9DT    UXA9DT
     C           *LIKE     DEFN #IAJDY    UXAJDY
     C           *LIKE     DEFN #ICUNB    UXCUNB
     C           *LIKE     DEFN #IALDY    UXALDY
     C           *LIKE     DEFN #IAPDY    UXAPDY
     C           *LIKE     DEFN #ICTNB    UXCTNB
     C           *LIKE     DEFN #IAMDY    UXAMDY
     C           *LIKE     DEFN #IAQDY    UXAQDY
     C                     MOVE #IA9DT    UXA9DT           TRANS DATE
     C                     MOVE #IAJDY    UXAJDY           CUTOFF DAY
     C                     MOVE #ICUNB    UXCUNB           ADD MTHS BEFORE
     C                     MOVE #IALDY    UXALDY           DISC DAY BEFORE
     C                     MOVE #IAPDY    UXAPDY           NET DAY BEFORE
     C                     MOVE #ICTNB    UXCTNB           ADD MTHS AFTER
     C                     MOVE #IAMDY    UXAMDY           DISC DAY AFTER
     C                     MOVE #IAQDY    UXAQDY           NET AFTER AFTER
     C                     EXSR UTERM2                     CALCULATE TERMS
     C           *LIKE     DEFN #OASDT    UXASDT
     C           *LIKE     DEFN #OATDT    UXATDT
     C                     MOVE UXASDT    #OASDT           DISCOUNT DATE
     C                     MOVE UXATDT    #OATDT           DUE DATE
     C           UTERM2    BEGSR
      *===================================================================
      *  CALCULATE TERMS TYPE 2
      *===================================================================
      *
      * DATE FORMAT IS CYYMMDD
     C                     MOVE UXA9DT    UXWKDT
     C*
      * CALCULATE DUE DATE
     C           UDD       IFLE UXAJDY                     CUTOFF DAY
     C           UMM       ADD  UXCUNB    UMM              ADD MTHS BEFORE
     C                     MOVE UXAPDY    UDD              NET DAY BEFORE
     C                     ELSE
     C           UMM       ADD  UXCTNB    UMM              ADD MTHS AFTER
     C                     MOVE UXAQDY    UDD              NET DAY AFTER
     C                     END
     C           UMM       IFGT 12
     C           UMM       SUB  12        UMM
     C           UYY       ADD  1         UYY
     C           UYY       IFEQ 0
     C                     MOVE 1         UC               NEXT CENTURY
     C                     END
     C                     END
     C                     MOVE UXWKDT    UXATDT           DUE DATE
     C* SAVE CENTURY, YEAR, AND MONTH FOR DISCOUNT DATE CALCULATION
     C                     Z-ADDUC        UCH     10       SAVE CENTURY
     C                     Z-ADDUYY       UYYH    20       SAVE YEAR
     C                     Z-ADDUMM       UMMH    20       SAVE MONTH
     C*
      * CALCULATE DISCOUNT DATE AS REQUIRED
     C                     MOVELUXA9DT    UXWKDT
     C                     Z-ADDUCH       UC               SAVED CENTURY
     C                     Z-ADDUYYH      UYY              SAVED YEAR
     C                     Z-ADDUMMH      UMM              SAVED MONTH
     C*
     C           UDD       IFLE UXAJDY                     CUTOFF DAY
     C           UXALDY    IFNE *ZEROS                     DISC DAY BEFORE
     C                     MOVE UXALDY    UDD              DISC DAY BEFORE
     C                     MOVELUXWKDT    UXASDT           DISCOUNT DATE
     C                     ELSE
     C                     MOVE UXA9DT    UXASDT
     C                     END
     C                     ELSE
     C           UXAMDY    IFNE *ZEROS                     DISC DAY AFTER
     C                     MOVE UXAMDY    UDD              DISC DAY AFTER
     C                     MOVELUXWKDT    UXASDT           DISCOUNT DATE
     C                     ELSE
     C                     MOVE UXA9DT    UXASDT
     C                     END
     C                     END
     C*
      *===================================================================
      *
     C                     ENDSR
