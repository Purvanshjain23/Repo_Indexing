      *****************  RPG PROGRAM HEADING  ************************
      *
      * ENVIRONMENT: Pork Division
      * SYSTEM:      HPS---Datamart
      * PROGRAM:     HP296-Datamart ALL: Farm Production
      * PROGRAMMER:  LeAnne Ramsey
      * CREATED:     03/15/10
      *
      * FUNCTION:    Stephanie Allen needs limited production data for the Period at the
      *              Farm level for the "utility" cubes. So, we have added a new Datamart file:
      *                HPPA174-Datamart ALL: Farm Production
      *
      *              When this program was written, we were only collecting:
      *                Gilt&Sow Ending Inventory Head for the BGF farms
      *                Produced Head for the NurFin Farms
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 10/18/10  LeAnne Ramsey  (E1079)
      *           Recompile only. Sire Line Company is now 2-characters instead
      *           of one.
      *
      *  05/12/16 Barb Gutierrez   E5752
      *          - Added company control file HSP0071 and changed some logic to
      *            accomodate multiple companies being processed.
      *          - Delete records by company and period end date instead of just
      *            period end date.
      *          - Added update of company number in HPPA174.
      *          - Read farm master by company passed in.
      *
      * 10/14/16  Barb Gutierrez
      *           Recompile only. Added fields Sold days in phase, Avg Sold days in phase,
      *           and Wgt open date, to HPPA034.         E6136
      *
      * 01/12/23  Eric Louchez - 111690 - Increment HGCD from 7 A to 9A.
      *           Recompile only.
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fhsp0071   IF   E           K DISK
      *   Company Control File
      *
     Fhpla018a  if   e           k disk
      *   Datamart ALL: Farm site
      *
      *
     Fhpla034b  if   e           k disk
      *   Datamart ALL: Group header
      *
      *
     Fhplb092e  if   e           k disk
      *   Datamart BGF: Weekly production
      *
      *
     Fhppa174   uf a e           k disk
      *   Datamart All: Farm production
      *
      /EJECT
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      *
      ****************************************************************
      * ARRAYS AND TABLES
      ****************************************************************
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
     d svccly          s                   like(ccccly)
     d svcclp          s                   like(cccclp)
     d svlepdt         s                   like(cclepdt)
     d svlbpdt         s                   like(cclbpdt)
      *
      * Workfields for date manipulation
      *
     D wkisoepdt       s               d   datfmt(*iso)
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *---------------------------------------------------------------
      *  Definition for external data area 'DALPER' which holds the last
      *  period that was closed in HPS.
      *---------------------------------------------------------------
      *
      *   Remove use of dalper.  e5752
     D*dalper          uds
     D* daccyy                 1      4  0
     D* daper                  5      6  0
     D* dabpdt                 7     14  0
     D* daepdt                15     22  0
     D* dappfl                24     24
      *
      *---------------------------------------------------------------
      * Local data area
      *---------------------------------------------------------------
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ***************************************************************************************
      * MAINLINE
      ***************************************************************************************
      *
      * Read each record in the Datamart ALL: Farm file for the company passed in.
      *
     C     xxcocd        setll     hpla018a
     C                   dou       *in90 = *on                                  Main do loop
     C     xxcocd        reade     hpla018a                               90
     C                   if        *in90 = *off                                 If not EOF
      *
      * Populate fields
     C                   move      fscocd        w1cocd
     C                   z-add     fsfscd        w1fscd
     C                   move      fsppcd        w1ppcd
      *  use year and period from company control file instead of lda.  e5752
     C                   z-add     svccly        w1acyr
     C                   z-add     svcclp        w1acpe
     C                   z-add     svlepdt       w180epdt
     C                   move      wkisoepdt     w1epdt
      *
      * BGF or Nur/Fin
     C                   select
     C                   when      fsppcd = 'BGF  '
     C                   exsr      $bgf
     C                   other
     C                   exsr      $nurfin
     C                   endsl
      *
      * Write record
     C                   write     w1rec
     C                   clear                   w1rec
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Main do loop
      *
      * EOF Processing
     C                   seton                                        lr
      /EJECT
      *----------------------------------------------------------------
      * Process BGF Farms
      *----------------------------------------------------------------
      *
     C     $bgf          begsr
      *
      * For BGF Farms, we are summing Gilt&Sow Ending Inventory Head.
      * The records in the BGF file are weekly records. So, read/sum all the
      * weekly records for the Farm/Acct Year/Acct Period.
      *
     C     key01         setll     hplb092e
      *
     C                   dou       *in91 = *on                                  Do BGF farm
     C     key01         reade     hplb092e                               91
     C                   if        *in91 = *off                                 If not EOF
     C                   z-add     bpengshd      w1engshd
     C                   endif                                                  If not EOF
     C                   enddo                                                  Do BGF farm
      *
     C                   endsr
      /eject
      *----------------------------------------------------------------
      * Process NurFin Farms
      *----------------------------------------------------------------
      *
     C     $nurfin       begsr
      *
      * The NurFin data in this file has a record for each Group for the Period.
      * So, read/sum records for this Period End Date/Farm.
      * The Cognos users want the "Produced Head" field to be the sum of:
      *    Transferred Out Head-Different Phase
      *    Sold Head-Carcass
      *    Sold Head-Live
      *    Feeder Pig Sale Head
      *
     C     key02         setll     hpla034b
      *
     C                   dou       *in91 = *on                                  Do NurFin Farm
     C     key02         reade     hpla034b                               91
     C                   if        *in91 = *off                                 If not EOF
     C                   add       hgdtohd       w1prhd
     C                   add       hgcarhd       w1prhd
     C                   add       hglivhd       w1prhd
     C                   add       hgfpshd       w1prhd
     C                   endif                                                  If not EOF
     C                   enddo                                                  Do NurFin Farm
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      *  Pass in company.  E5752
      *
     C     *entry        plist
     C                   parm                    xxcocd            3
      *
      * Key lists
      *
     C     key01         klist
     C                   kfld                    w1fscd
     C                   kfld                    w1acyr
     C                   kfld                    w1acpe
      *
     C     key02         klist
     C                   kfld                    xxcocd
     C                   kfld                    w1epdt
     C                   kfld                    w1fscd
      *
     C     key03         klist
     C                   kfld                    xxcocd
     C                   kfld                    svlepdt
      *
      * Read the company control file for the company being passed in to retrieve
      * the last HPS closed year/period.      E5752
      *
     C     xxcocd        chain     hsp0071                            92
     C                   if        *in92 = *off                                 If exists
     C                   move      ccccly        svccly                         last close year
     C                   move      cccclp        svcclp                         last close period
     C                   move      cclbpdt       svlbpdt                        last close beg date
     C                   move      cclepdt       svlepdt                        last close end date
     C                   endif                                                  If exists
      *
      * You will need the Period End date in "date format" for the Datamart file.
      *
     C     *iso          move      svlepdt       wkisoepdt
      *
      * Always delete all existing records for the Period from the Datamart file.
      *  should only be deleting the records for the company selected in the
      *  open query.  (hpbldCD$)
      *
      *  changed to use key03 instead of just svlepdt to delete records.
      *
     C     key03         setll     hppa174
     C                   dou       *in91 = *on                                  Do deletes
     C     key03         reade     hppa174                                91
     C                   if        *in91 = *off
     C                   delete    w1rec
     C                   endif
     C                   enddo                                                  Do deletes
      *
     C                   endsr
      *
