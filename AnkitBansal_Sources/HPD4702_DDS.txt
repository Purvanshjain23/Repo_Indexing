     A*%%TS  SD  20040714  090823  ISLRAMS     REL-V5R2M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD4702
     A*  TITLE:         WORK WITH FARM BUDGETS
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  CREATED:       02/23/04
     A*
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A                                      CF04(04 'PROMPT')
     A                                      CF05(05 'REFRESH')
     A N87                                  CF06(06 'CREATE')
     A                                      CF07(07 'LISTING')
     A                                      CF13(13 'FOLD/DROP')
     A                                      CF17(17 'PRINT OPTIONS')
     A                                      CF20(20 'WRKSPLF')
     A*
     A*-------------------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*-------------------------------------------------------------------------
     A          R HP4702S1                  SFL
     A*%%TS  SD  20040714  090823  ISLRAMS     REL-V5R2M0  5722-WDS
     A  85                                  SFLNXTCHG
     A            HDRRN1    R        H      REFFLD(@@RRN)
     A            D1OPT     R        B 12  2REFFLD(@@OPT)
     A                                      CHGINPDFT(UL)
     A  30                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A N30                                  DSPATR(HI)
     A            D1FSCD    R        O 12  6REFFLD(@@FSCD)
     A                                      EDTCDE(M)
     A            D1BTCD    R        O 12 14REFFLD(HSRREF/@@BTCD *LIBL/HSPREF)
     A            D1BACD    R        O 12 25REFFLD(HSRREF/@@BACD *LIBL/HSPREF)
     A            D1CSCD    R        O 12 32REFFLD(HSRREF/@@CSCD *LIBL/HSPREF)
     A            D1BTWK    R        O 12 37REFFLD(HSRREF/@@BTWK *LIBL/HSPREF)
     A                                      EDTCDE(M)
     A            D1FMDY    R        O 12 45REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  ')
     A            D1TMDY    R        O 12 55REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  ')
     A            D1FBSN    R        O 12 65REFFLD(HSRREF/@@FBSN *LIBL/HSPREF)
     A                                      EDTCDE(M)
     A            D1FBSCD   R        O 12 76REFFLD(HSRREF/@@FBSCD *LIBL/HSPREF)
     A                                 13  6'Farm name:'
     A                                      COLOR(PNK)
     A            D1FSNM    R        O 13 17REFFLD(FSFSNM)
     A                                 13 44'Temp desc:'
     A                                      COLOR(PNK)
     A            D1BTDS    R        O 13 55REFFLD(HSRREF/@@BTDS *LIBL/HSPREF)
     A*
     A*-------------------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*-------------------------------------------------------------------------
     A          R HP4702C1                  SFLCTL(HP4702S1)
     A*%%TS  SD  20040714  085547  ISLRAMS     REL-V5R2M0  5722-WDS
     A                                      SFLSIZ(0050)
     A                                      SFLPAG(0004)
     A N82                                  ROLLUP(25 'PAGE FORWARD')
     A                                      SETOF(30)
     A                                      SETOF(31)
     A                                      SETOF(51)
     A                                      SETOF(52)
     A                                      SETOF(53)
     A                                      SETOF(54)
     A                                      SETOF(55)
     A                                      SETOF(56)
     A                                      RTNCSRLOC(&RCD &FLD)
     A                                      OVERLAY
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A  78                                  SFLFOLD(CF13)
     A  79                                  SFLDROP(CF13)
     A                                      SFLMODE(&FOLD)
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            FOLD           1A  H
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 28'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USR)
     A                                      DSPATR(HI)
     A            C1VERSION R        O  2  2REFFLD(HSRREF/@@DS20 *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A                                  2 27'WORK WITH FARM BUDGETS'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@JOB)
     A                                      DSPATR(HI)
     A                                  4  2'Include closed budgets ...'
     A                                  4 29'?'
     A            C1YNFL    R        B  4 31REFFLD(HSRREF/@@FLAG *LIBL/HSPREF)
     A  31
     AO 51                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A N31                                  DSPATR(HI)
     A N87                              6  2'2=Change Dates'
     A N87                              6 18'4=Delete'
     A                                  6 28'5=View'
     A                                  6 36'6=Print'
     A N87                              6 45'9=Customize Items'
     A N87                              6 64'13=Close'
     A                                  8  8'?'
     A                                      DSPATR(HI)
     A                                  8 16'?'
     A                                      DSPATR(HI)
     A                                  8 25'?'
     A                                      DSPATR(HI)
     A                                  8 32'?'
     A                                      DSPATR(HI)
     A                                  8 76'?'
     A                                      DSPATR(HI)
     A                                  9  7'Farm'
     A                                      DSPATR(HI)
     A                                  9 14'Budget'
     A                                      DSPATR(HI)
     A                                  9 23'Budget'
     A                                      DSPATR(HI)
     A                                  9 31'Crt'
     A                                      DSPATR(HI)
     A                                  9 47'From'
     A                                      DSPATR(HI)
     A                                  9 58'To'
     A                                      DSPATR(HI)
     A                                  9 66'Budget'
     A                                      DSPATR(HI)
     A                                  9 74'Budget'
     A                                      DSPATR(HI)
     A                                 10  2'Opt'
     A                                      DSPATR(HI)
     A                                 10  7'Site'
     A                                      DSPATR(HI)
     A                                 10 13'Template'
     A                                      DSPATR(HI)
     A                                 10 23'Avail'
     A                                      DSPATR(HI)
     A                                 10 31'Schd'
     A                                      DSPATR(HI)
     A                                 10 37'Weeks'
     A                                      DSPATR(HI)
     A                                 10 47'Date'
     A                                      DSPATR(HI)
     A                                 10 57'Date'
     A                                      DSPATR(HI)
     A                                 10 66'Number'
     A                                      DSPATR(HI)
     A                                 10 74'Status'
     A                                      DSPATR(HI)
     A            C1FSCD    R        B 11  6REFFLD(@@FSCD)
     A                                      DSPATR(HI)
     A                                      EDTCDE(M)
     A N31N51N52
     A N53N54N55
     A N56                                  DSPATR(PC)
     A            C1BTCD    R        B 11 14REFFLD(HSRREF/@@BTCD *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A  53                                  DSPATR(PC)
     A            C1BACD    R        B 11 25REFFLD(HSRREF/@@BACD *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A  54                                  DSPATR(PC)
     A            C1CSCD    R        B 11 32REFFLD(HSRREF/@@CSCD *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A  55                                  DSPATR(PC)
     A            C1BTWK    R        B 11 37REFFLD(HSRREF/@@BTWK *LIBL/HSPREF)
     A                                      EDTCDE(M)
     A                                      DSPATR(HI)
     A            C1FMDY    R        B 11 45REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  ')
     A                                      DSPATR(HI)
     A            C1TMDY    R        B 11 55REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  ')
     A                                      DSPATR(HI)
     A            C1FBSN    R        B 11 65REFFLD(HSRREF/@@FBSN *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A                                      EDTCDE(M)
     A N31N51N52
     A N53N54N55
     A N56                                  DSPATR(PC)
     A            C1FBSCD   R        B 11 76REFFLD(HSRREF/@@FBSCD *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A  56                                  DSPATR(PC)
     A*
     A*-------------------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*-------------------------------------------------------------------------
     A          R HP4702K1
     A*%%TS  SD  20040223  175559  ISLRAMS     REL-V5R2M0  5722-WDS
     A  43                             21  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 22  2'F3=Exit'
     A                                 22 11'F4=Prompt'
     A                                 22 22'F5=Refresh'
     A N87                             22 34'F6=Create'
     A                                 22 45'F7=Listing'
     A                                 22 57'F13=Fold/Unfold'
     A                                 23 34'F17-Print Options'
     A                                 23 57'F20=Spool Files'
      *
     A*-------------------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
     A*-------------------------------------------------------------------------
     A          R HP4702ES                  SFL SFLMSGRCD(24)
      *
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
     A*-------------------------------------------------------------------------
      * ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*-------------------------------------------------------------------------
     A          R HP4702EC                  SFLCTL(HP4702ES)
      *
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
