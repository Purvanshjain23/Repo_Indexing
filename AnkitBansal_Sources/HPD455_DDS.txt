     A*%%TS  SD  20010905  163028  ISLRAMS     REL-V4R4M0  5769-PW1
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD455
     A*  TITLE:         SPECIFY DEATHS LISTING OPTIONS
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  DATE:          04/03/95
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A N99                                  CF04(04 'PROMPT')
     A N99 44                               CF08(08 'SUBMIT')
     A                                      CA12(12 'PREVIOUS')
     A N99                                  CF17(17 'PRINT OPTIONS')
     A N99                                  CF20(20 'WRKSPLF')
     A*
     A*---------------------------------------------------------------
     A* RECORD FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP455R1
     A*%%TS  SD  20010905  163028  ISLRAMS     REL-V4R4M0  5769-PW1
     A                                      SETOF(31)
     A                                      SETOF(32)
     A                                      SETOF(33)
     A                                      SETOF(34)
     A                                      SETOF(35)
     A                                      SETOF(36)
     A                                      SETOF(37)
     A                                      SETOF(38)
     A                                      SETOF(42)
     A                                      SETOF(51)
     A                                      SETOF(54)
     A                                      SETOF(55)
     A                                      SETOF(56)
     A                                      SETOF(57)
     A                                      SETOF(58)
     A                                      OVERLAY
     A                                      RTNCSRLOC(&RCD &FLD)
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 27'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  2 23'SPECIFY DEATH REPORT OPTIONS'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  4  2'Business office ...'
     A                                  4 22'?'
     A            DFFSBO    R        B  4 24REFFLD(BOBOBO)
     A N31                                  DSPATR(HI)
     A  31
     AO 51                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A            DFBODS    R        O  4 34REFFLD(BOBODS)
     A                                  4 65'(Blank for all) '
     A                                  6  2'From date .........'
     A            DFFMDY    R     Y  B  6 24REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A N32                                  DSPATR(HI)
     A  32                                  DSPATR(PC)
     A  32                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A                                  7  2'To date ...........'
     A            DFTMDY    R     Y  B  7 24REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A N33                                  DSPATR(HI)
     A  33                                  DSPATR(PC)
     A  33                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A                                  8  2'Production type ...'
     A                                  8 22'?'
     A            DFPTCD    R        B  8 24REFFLD(PTPTCD)
     A N34                                  DSPATR(HI)
     A  34
     AO 54                                  DSPATR(PC)
     A  34                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A            DFPTDS    R        O  8 34REFFLD(PTPTDS)
     A                                  8 65'(Blank for all) '
     A                                  9  2'Production phase ..'
     A                                  9 22'?'
     A            DFPPCD    R        B  9 24REFFLD(PPPPCD)
     A N35                                  DSPATR(HI)
     A  35
     AO 55                                  DSPATR(PC)
     A  35                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A            DFPPDS    R        O  9 34REFFLD(PPPPDS)
     A                                  9 65'(Blank for all) '
     A                                 10  2'Group type ........'
     A                                 10 22'?'
     A            DFGTCD    R        B 10 24REFFLD(HSRREF/@@GTCD *LIBL/HSPREF)
     A N38                                  DSPATR(HI)
     A  38
     AO 58                                  DSPATR(PC)
     A  38                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A            DFGTDS    R        O 10 34REFFLD(HSRREF/GTGTDS *LIBL/HSPREF)
     A                                 10 65'(Blank for all) '
     A                                 11  2'Farm site .........'
     A                                 11 22'?'
     A            DFFSCD    R        B 11 24REFFLD(FSFSCD)
     A                                      EDTCDE(M)
     A N36                                  DSPATR(HI)
     A  36
     AO 56                                  DSPATR(PC)
     A  36                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A            DFFSNM    R        O 11 34REFFLD(FSFSNM)
     A                                 11 65'(Blank for all) '
     A                                 12  2'Hog group .........'
     A                                 12 22'?'
     A            DFHGCD    R        B 12 24REFFLD(HSRREF/HGHGCD *LIBL/HSPREF)
     A N37                                  DSPATR(HI)
     A  37                                  DSPATR(RI)
     A  37
     AO 57                                  DSPATR(PC)
     A  99                                  DSPATR(PR)
     A                                 12 65'(Blank for all) '
     A                                 14  2'Report ............'
     A                                 14 22'?'
     A            DFRPFL    R        B 14 24REFFLD(@@FLAG)
     A N42                                  DSPATR(HI)
     A  42
     AO 52                                  DSPATR(PC)
     A  42                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A            DFRPDS    R        O 14 34REFFLD(@@DS30)
     A*
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R HP455K1
     A*%%TS  SD  19950531  161912  ISLRAMS     REL-V3R0M5  5763-PW1
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A N99                             23 11'F4=Prompt'
     A  44N99                          23 22'F8=Submit'
     A    N99                          23 33'F12-Previous'
     A N99                             23 47'F17=Print Options'
     A    N99                          23 65'F20=Spool Files'
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *---------------------------------------------------------------
     A          R HP455ES                   SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
     A*                                                                *********
     A*---------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*---------------------------------------------------------------
     A*
     A          R HP455EC                   SFLCTL(HP455ES)
     A*%%TS  SD  19940705  110806  ISRCENT     REL-V2R2M0  5738-PW1
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
