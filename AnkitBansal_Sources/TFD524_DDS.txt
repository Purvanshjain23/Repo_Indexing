     A*%%TS  SD  20090406  130517  ISLRAMS     REL-V5R4M0  5722-WDS
     A*****************************************************************
     A*
     A*  SYSTEM:        TF FEES & PAYMENTS
     A*  DISPLAY FILE:  TFD524- SELECT CARRIER
     A*  CREATED BY:    LEANNE RAMSEY
     A*  DATE:          04/06/09
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/TFPREF)
     A                                      PRINT
     A                                      CA12(12 'PREVIOUS')
     A N82                                  ROLLUP(25 'PAGE FORWARD')
     A*---------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*---------------------------------------------------------------
     A          R TF524S1                   SFL
     A*%%TS  SD  20090406  130517  ISLRAMS     REL-V5R4M0  5722-WDS
     A            HDRRN1    R        H      REFFLD(@@RRN)
     A            D1CARCD        3A  O  8  2
     A            D1CARNM       30A  O  8 11
     A            D1STAT         1A  O  8 44
     A*
     A*---------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*---------------------------------------------------------------
     A          R TF524C1                   SFLCTL(TF524S1)
     A*%%TS  SD  20090406  130517  ISLRAMS     REL-V5R4M0  5722-WDS
     A                                      SFLSIZ(0100)
     A                                      SFLPAG(0009)
     A                                      WINDOW(TF524EC)
     A                                      OVERLAY
     A                                      SFLCSRRRN(&HDRRN)
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A            HDPAG1    R        H      REFFLD(@@PAG)
     A                                      SFLRCDNBR
     A            HDRRN     R        H      REFFLD(@@RRN)
     A            SDPGM     R        O  1  1REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 17'SELECT CARRIER'
     A                                      DSPATR(HI)
     A                                  3  2'Position Cursor, Press Enter'
     A                                  5  2'Carrier'
     A                                      DSPATR(HI)
     A                                  6  2'Code'
     A                                      DSPATR(HI)
     A                                  6 11'Name'
     A                                      DSPATR(HI)
     A                                  6 42'Status'
     A                                      DSPATR(HI)
     A            C1CARCD        3A  B  7  2DSPATR(HI)
     A            C1CARNM       30A  B  7 11CHGINPDFT(UL LC FE)
     A                                      DSPATR(HI)
     A            C1STAT         1A  B  7 44DSPATR(HI)
     A*---------------------------------------------------------------
     A* COMMAND LINE LINE
     A*---------------------------------------------------------------
     A          R TF524K1
     A*%%TS  SD  20070122  101312  ISLRAMS     REL-V5R3M0  5722-WDS
     A                                      WINDOW(TF524EC)
     A                                      OVERLAY
     A  43                             17  1'Message program failed. Call IS.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 18  2'F12=Previous'
      *
     A*-------------------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
     A*-------------------------------------------------------------------------
     A          R TF524ES                   SFL SFLMSGRCD(19)
      *
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
     A*
     A*-------------------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*-------------------------------------------------------------------------
     A*
     A          R TF524EC                   SFLCTL(TF524ES)
     A*%%TS  SD  19980327  105628  ISLRAMS     REL-V3R7M0  5716-PW1
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A                                      WINDOW(*DFT 20 50)
     A                                      WDWBORDER((*DSPATR RI) (*CHAR '    -
     A                                          '))
     A            MSGPGM                    SFLPGMQ(10)
     A*
     A*---------------------------------------------------------------
     A* ASSUME FORMAT -- REQUIRED FOR WINDOWS
     A*---------------------------------------------------------------
     A          R ASSUME
     A*%%TS  SD  20011025  160559  ISLRAMS     REL-V4R4M0  5769-PW1
     A                                      ASSUME
     A                                      OVERLAY
     A                                 24 79' '
