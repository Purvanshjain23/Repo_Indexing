      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      HPS
      * PROGRAM:     HP337 - Renumber Feed Tickets
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     07/30/02
      *
      * We periodically renumber feed tickets at John Zimmerman's request.
      * We have renumbered 4 times as of August, 2007.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 08/29/07  LeAnne Ramsey
      *           Change starting ticket number to '1' per John Zimmerman
      *           for this runumbering run.
      *
      * 04/23/09  LeAnne Ramsey
      *           Recompile only. Costed Time/User added to Feed Ticket Header.
      *
      * 05/12/09  LeAnne Ramsey
      *           Recompile only. PickUp Ticket Type added to Feed Ticket Header.
      *
      * 09/21/09  LeAnne Ramsey
      *           Recompile only. Added new field 'Upload Date' to Feed Ticket Header.
      *
      * 03/26/20  Danny Nguyen - P405 - Farm Number Increase
      *           Recompile only. Increased Bin Code (@@BNCD) in HSPREF file from 5A to 6A.
      *
      * 05/10/22 Eric L SDN736 Recompiled ticket nbr increase (TKNO & RTNO 7.0 TO 9.0)
      /eject
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fhsl037f   uf   e           k disk    rename(fhrec:fhrecf)
      *  Feed ticket header (delivery tickets only)
      *
      *
     Fhsl037p   uf   e           k disk    rename(fhrec:fhrecp)
      *  Feed ticket header (pickup and adjustment tickets only)
      *
      *
     Fhsp038    uf   e           k disk
      *  Feed ticket detail
      *
      *
     Fhsp059    uf   e           k disk
      *  Feed pickup ticket
      *
      *
     Fhspmap37  o    e           k disk
      *  Feed ticket mapping file from ticket renumbering
      *
      *
     Fhspbld37  o    e           k disk    rename(fhrec:fhrecbld)
      *  Feed ticket header build file
      *
      *
     Fhspbld38  o    e           k disk    rename(fdrec:fdrecbld)
      *  Feed ticket detail build file
      *
      *
     Fhspbld59  o    e           k disk    rename(fprec:fprecbld)
      *  Feed pickup ticket build file
      *
      /eject
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      *
      ****************************************************************
      * ARRAYS AND TABLES
      ****************************************************************
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Workfields
      *
      *
     D nextno          s                   like(fhtkno)
      *
     D oldtkno         s                   like(fhtkno)
     D oldrtno         s                   like(fhrtno)
      *
     D newtkno         s                   like(fhtkno)
     D newrtno         s                   like(fhrtno)
      *
     D wkfmbo          s                   like(fhfmbo)
      *
     D wkcrupdt        s              8  0
     D wkisodate       s               D   datfmt(*iso)
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * MAINLINE
      ****************************************************************
      *
      * Read each delivery ticket in the Feed Ticket Header file.
      *
     C                   dou       *in90 = *on                                  Main do
     C                   read      hsl037f                                90
     C                   if        *in90 = *off                                 If not EOF
      *
      * Save feed mill business office
      *
     C                   move      fhfmbo        wkfmbo

      * Delivery ticket
     C                   exsr      $delivery
      *
      *
      * Tickets that have this delivery ticket as their 'reference ticket'
      *
     C                   exsr      $reference
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Main do
      *
      *
     C                   seton                                        lr
      /eject
      *---------------------------------------------------------------
      * Process the delivery ticket
      *---------------------------------------------------------------
      *
     C     $delivery     begsr
      *
     C                   z-add     fhtkno        oldtkno
     C                   z-add     fhrtno        oldrtno
      *
     C                   add       1             nextno
     C                   z-add     nextno        newtkno
     C                   z-add     nextno        newrtno
      *
      * Write old/new ticket numbers to mapping file
      *
     C                   exsr      $map
      *
      * Write delivery ticket header with new numbers to the build file
      *
     C                   z-add     newtkno       fhtkno
     C                   z-add     newrtno       fhrtno
     C                   write     fhrecbld
      *
      * Process the Feed Detail Records for this delivery ticket header
      *
     C                   exsr      $detail
      *
      * Delete the delivery ticket from the production feed ticket header file
      *
     C                   delete    fhrecf
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Map old/new values
      *---------------------------------------------------------------
      *
     C     $map          begsr
      *
     C                   move      wkfmbo        tmfmbo
     C                   move      fhtrcd        tmtrcd
      *
     C                   z-add     oldtkno       tmoldtkno
     C                   z-add     newtkno       tmnewtkno
      *
     C                   z-add     wkcrupdt      tmcrdt
      *
     C                   write     tmrec
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Process feed ticket detail records
      *---------------------------------------------------------------
      *
     C     $reference    begsr
      *
      * Process each Feed Ticket header record that has the specified Reference
      * ticket number.
      *
     C     key02         setll     hsl037p
      *
     C                   dou       *in93 = *on                                  Do loop
     C     key02         reade     hsl037p                                93
     C                   if        *in93 = *off                                 If not EOF
      *
     C                   z-add     fhtkno        oldtkno
      *
      * Get a new ticket number for this pickup or adjustment ticket
      *
     C                   add       1             nextno
     C                   z-add     nextno        newtkno
      *
      * Write old/new ticket numbers to mapping file
      *
     C                   exsr      $map
      *
      * Write ticket header with new numbers to the build file
      *
     C                   z-add     newtkno       fhtkno
     C                   z-add     newrtno       fhrtno
     C                   write     fhrecbld
      *
      * Process the Feed Detail Records using the 'old' number for retrieval
      *
     C                   exsr      $detail
      *
      * If this is a pickup ticket, it may have records in the Feed Pick-up file.
      *
     C                   if        fhtrcd = 'P'
     C                   exsr      $pickup
     C                   endif
      *
      * Delete ticket header from the production feed ticket header file
      *
     C                   delete    fhrecp
     C                   endif                                                  If not EOF
      *
     C                   enddo                                                  Do loop
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Process feed ticket detail records
      *---------------------------------------------------------------
      *
     C     $detail       begsr
      *
      * Read each Feed Ticket Detail record:
      *    1) write the record to the build file with its new numbers
      *    2) delete the record out of the production file
      *
     C     key01         setll     hsp038
      *
     C                   dou       *in91 = *on                                  Do detail
     C     key01         reade     hsp038                                 91
     C                   if        *in91 = *off                                 If not EOF
     C                   z-add     newtkno       fdtkno
     C                   z-add     newrtno       fdrtno
     C                   write     fdrecbld
     C                   delete    fdrec
     C                   endif                                                  If not EOF
     C                   enddo                                                  Do detail
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Pickup tickets where to/from farms are not the same farm
      *---------------------------------------------------------------
      *
     C     $pickup       begsr
      *
     C     key01         setll     hsp059
      *
     C                   dou       *in91 = *on                                  Do pickups
     C     key01         reade     hsp059                                 91
     C                   if        *in91 = *off                                 If not EOF
     C                   z-add     newtkno       fptkno
     C                   write     fprecbld
     C                   delete    fprec
     C                   endif                                                  If not EOF
     C                   enddo                                                  Do pickups
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      *
     C     key01         klist
     C                   kfld                    wkfmbo
     C                   kfld                    oldtkno
     C                   kfld                    fhtrcd
      *
      *
     C     key02         klist
     C                   kfld                    wkfmbo
     C                   kfld                    oldrtno
      *
      * Set this number to whatever number John Zimmerman has requested.
      * Please note that a block of numbers (see data area DAFDSN) are
      * not for user consumption!
      *
     C                   z-add     0             nextno
      *
      * Set up system date for record stamp in mapping file
      *
     C     *mdy          move      udate         wkisodate
     C                   move      wkisodate     wkcrupdt
      *
     C                   endsr
      /eject
