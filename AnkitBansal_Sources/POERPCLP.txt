/********************************************************************/
/*                                                                  */
/*  SYNOPSIS - PRINT SALES REPORTS                                  */
/*                                                                  */
/*                                                                  */
/*  COMPANY   - SEABOARD COPRORATION                                */
/*  SYSTEM    - ORDER MANAGEMENT                                    */
/*  USER NAME - SHEILA SCHERTZ                                      */
/*  DATE      - 05/14/96                                            */
/*                                                                  */
/*      PARAMETERS RECEIVED:                                        */
/*                                                                  */
/*          &OUTQ       - OUTPUT QUEUE                              */
/*          &COPIESL    - NUMBER OF COPIES                          */
/*          &NIGHTQ     - NIGHT QUEUE                               */
/*          &HOLD       - HOLD PRINT FLAG                           */
/*          &SAVE       - SAVE PRINT FLAG                           */
/*          &PGM        - PROGRAM NAME                              */
/*          &COMPL      - COMPANY NUMBER - BLANK FOR ALL            */
/*          &FDATEL     - FROM DATE                                 */
/*          &TDATEL     - TO DATE                                   */
/*          &CUSTL      - CUSTOMER                                  */
/*          &SHPCL      - SHIP TO CUSTOMER                          */
/*          &SLSPR      - SALESPERSON                               */
/*          &DTLSUM     - DETAIL OR SUMMARY                         */
/*          &EXPSLS     - EXPORT SALES                              */
/*          &SELRPT     - SELECT REPORT                             */
/*          &FLAG1      - INCLUDE ALL PRODUCTS                      */
/*          &FLAG2      - INCLUDE CM/DM                             */
/*                                                                  */
/*                                                                  */
/* SLM 11/11/04 PASS IN WAREHOUSE TYPE AND WHSE CODE PARMS          */
/*                                                                  */
/********************************************************************/

/********************************************************************/
/* SLM 11/11/04 PASS IN WAREHOUSE TYPE AND WHSE CODE PARMS        */
/********************************************************************/
 PDLAPCLP:   PGM        PARM(&PGM &COMPL &FDATEL &TDATEL &CUSTL +
                          &OUTQ &HOLD &SAVE &COPIESL &NIGHTQ +
                          &DTLSUM &EXPSLS &SELRPT &SHPCL &SLSPR +
                          &FLAG1 &FLAG2 &STATE &WHSTYP &WHSCOD)

        DCL    &COMP      *DEC    (3 0)           /*  COMPANY #     */
        DCL    &COMPL     *DEC                    /*  FOR PASSING   */
        DCL    &FDATE     *DEC    (7 0)          /*  FROM DATE      */
        DCL    &FDATEL    *DEC                   /*  FOR PASSING    */
        DCL    &TDATE     *DEC    (7 0)          /*  THROUGH DATE   */
        DCL    &TDATEL    *DEC                   /*  FOR PASSING    */
        DCL    &COPIES    *DEC    (3 0)           /*  # OF COPIES   */
        DCL    &COPIESL   *DEC                    /*  FOR PASSING   */
        DCL    &CUST      *DEC    (7 0)          /*  A/R CUSTOMER   */
        DCL    &CUSTL     *DEC                   /*  FOR PASSING    */
        DCL    &SHPCS     *DEC    (7 0)          /*  SHIP TO CUST   */
        DCL    &SHPCL     *DEC                   /*  FOR PASSING    */
        DCL    &SLSPR     *CHAR   3              /*  SALESPERSON    */
        DCL    &OUTQ      *CHAR   10             /*  OUTPUT QUEUE   */
        DCL    &NIGHTQ    *CHAR   10             /*  NIGHT JOB QUEUE*/
        DCL    &SAVE      *CHAR   4              /*  SAVE SPOOL     */
        DCL    &HOLD      *CHAR   4              /*  HOLD SPOOL     */
        DCL    &PGM       *CHAR   10              /*  PGM NAME      */
        DCL    &COMPA     *CHAR   3               /*  COMPANY ALPHA */
        DCL    &FDATEA    *CHAR   7               /*  FROM DT ALPHA */
        DCL    &TDATEA    *CHAR   7               /*  TO   DT ALPHA */
        DCL    &CUSTA     *CHAR   7               /*  CUSTOMER      */
        DCL    &SHPCA     *CHAR   7               /*  SHIP TO CUST  */
        DCL    &DTLSUM    *CHAR   1               /*  DETAIL/SUMMARY*/
        DCL    &EXPSLS    *CHAR   1               /*  EXPORT SALES  */
        DCL    &SELRPT    *CHAR   1               /*  SELECT REPORT */
        DCL    &FLAG1     *CHAR   1               /*  INCL. ALL PROD*/
        DCL    &FLAG2     *CHAR   1               /*  INCL. CM/DM   */
        DCL    &STATE     *CHAR   2
        DCL    &COMMAND    *CHAR   (512)
        DCL    &WHSTYP     *CHAR   (2)           /* WAREHOUSE TYPE            */
        DCL    &WHSCOD     *CHAR   (3)           /* WAREHOUSE CODE            */

/* SEND PROGRAM MESSAGE PARAMETERS  */

        DCL    &USER      *CHAR   10              /*  USER ID       */
        DCL    &MSGRTN    *CHAR   10              /*  MSG QUEUE     */
        DCL    &PGMO      *CHAR   10              /*  PRINTER FILE  */
        DCL     &A        *CHAR   20              /* QRY FROM DATE  */
        DCL     &B        *CHAR   25              /* QRY TO DATE    */
        DCL     &C        *CHAR   25              /* QRY COMPANY    */
        DCL     &D        *CHAR   25              /* QRY EXPORT SLS */
        DCL     &E        *CHAR   25              /* QRY CUSTOMER   */
        DCL     &F        *CHAR   25              /* QRY SHIP CUST  */
        DCL     &G        *CHAR   25              /* QRY SLSPERSON  */
        DCL     &H        *CHAR  195              /* QRY ALL        */
        DCL     &I        *CHAR   25              /* QRY STATE      */
        DCL     &DATA     *CHAR   10              /* DATA FILES     */
        DCL     &DATAH    *CHAR   10              /* HIST FILES     */

             CHGVAR    VAR(&COMP) VALUE(&COMPL) /* Change to length +
                          for print program */
             CHGVAR    VAR(&COMPA) VALUE(&COMP) /* Change to alpha +
                          for message data */
             CHGVAR    VAR(&FDATE) VALUE(&FDATEL) /* Change to length +
                          for print program */
             CHGVAR    VAR(&FDATEA) VALUE(&FDATE) /* Change to alpha +
                          for query data */
             CHGVAR    VAR(&TDATE) VALUE(&TDATEL) /* Change to length +
                          for print program */
             CHGVAR    VAR(&TDATEA) VALUE(&TDATE) /* Change to alpha +
                          for query data */
             CHGVAR    VAR(&CUST) VALUE(&CUSTL) /* Change to length +
                          for print program */
             CHGVAR    VAR(&CUSTA) VALUE(&CUST) /* Change to alpha +
                          for query data */
             CHGVAR    VAR(&SHPCS) VALUE(&SHPCL) /* Change to length +
                          for print program */
             CHGVAR    VAR(&SHPCA) VALUE(&SHPCS) /* Change to alpha +
                          for query data */
             CHGVAR    VAR(&COPIES) VALUE(&COPIESL) /* change to +
                          length for print program */

/********************************************************************/
/* APPEND $ TO THE PROGRAM NAME                                     */
/********************************************************************/

             IF         COND(&SELRPT *EQ '2') THEN(DO)
             CHGVAR     VAR(&PGM)  VALUE('PDOGPFR')
             ENDDO

             CHGVAR     VAR(&PGMO) VALUE(&PGM *TCAT '$')

/*********************************************************************/
/*   CREATE OPEN QUERY FILE TO BE USED FOR AREA/BRK WORK FILE BUILD  */
/*********************************************************************/

             CHGVAR     VAR(&A) VALUE('F4GNDT *GE ' *CAT +
                         &FDATEA)

             CHGVAR     VAR(&B) VALUE('*AND F4GNDT *LE ' *CAT +
                         &TDATEA)

             CHGVAR     VAR(&C) VALUE('*AND F4AIC3 *EQ ' *CAT +
                         &COMPA)

             IF         COND(&EXPSLS *EQ 'N') THEN(DO)
             CHGVAR     VAR(&D) VALUE('*AND F4U2ST *NE ' *CAT +
                         '"Y"')
             ENDDO
             IF         COND(&EXPSLS *EQ 'E') THEN(DO)
             CHGVAR     VAR(&D) VALUE('*AND F4U2ST *EQ ' *CAT +
                         '"Y"')
             ENDDO

             IF         COND(&CUSTA *NE '0000000') THEN(DO)
             CHGVAR     VAR(&E) VALUE('*AND F4ANC7 *EQ ' *CAT +
                         &CUSTA)
             ENDDO

             IF         COND(&SHPCA *NE '0000000') THEN(DO)
             CHGVAR     VAR(&F) VALUE('*AND F4BKC7 *EQ ' *CAT +
                         &SHPCA)
             ENDDO

             IF         COND(&SLSPR *NE '   ') THEN(DO)
             CHGVAR     VAR(&G) VALUE('*AND F4R4CD *EQ "' *CAT +
                         &SLSPR *CAT '"')
             ENDDO
             IF         COND(&STATE *NE '  ') THEN(DO)
             CHGVAR     VAR(&I) VALUE('*AND F4DBCD *EQ "' *CAT +
                         &STATE *CAT '"')
             ENDDO

             CHGVAR     VAR(&H) VALUE(&A *BCAT &B *BCAT &C *BCAT &D +
                                      *BCAT &E *BCAT &F *BCAT &G +
                                      *BCAT &I)

             IF         COND(&SELRPT *EQ '1') THEN(DO)
             OVRDBF     FILE(OMHSTPMC) TOFILE(OMHSTPMC) +
                          SHARE(*YES)

             OPNQRYF    FILE((OMHSTPMC)) OPTION(*ALL) +
                          QRYSLT(&H) KEYFLD(*FILE)
             ENDDO

    /*       IF         COND(&SELRPT *EQ '2') THEN(DO)    +
             OVRDBF     FILE(OMHSTPL8) TOFILE(OMHSTPL8) +
                          SHARE(*YES)                     +
                                                           +
             OPNQRYF    FILE((OMHSTPL8)) OPTION(*ALL) +
                          QRYSLT(&H) KEYFLD(*FILE)          +
             ENDDO                                         */

/********************************************************************/
/* Override printer file with user specified values                 */
/********************************************************************/

/*  THIS USED TO SPECIFY DRAWER(2)                                */
             SBDPRTOVR  CMDVAR(&COMMAND) FILE(&PGMO) OUTQ(&OUTQ) +
                          COPIES(&COPIES) HOLD(&HOLD) SAVE(&SAVE)

             CALL       PGM(QCMDEXC) PARM(&COMMAND 512)
/*********************************************************************/
/*   CALL LISTING PROGRAM                                            */
/*********************************************************************/

             CALL       PGM(&PGM) PARM('' &FDATE &TDATE +
                          &FLAG1 &FLAG2 &SELRPT &EXPSLS &COMP &CUST +
                          &SHPCS &SLSPR &DTLSUM &STATE)

             IF         COND(&SELRPT *EQ '1') THEN(DO)
             CLOF       OPNID(OMHSTPMC)
             DLTOVR     FILE(OMHSTPMC)
             ENDDO

 /*          IF         COND(&SELRPT *EQ '2') THEN(DO)       +
             CLOF       OPNID(OMHSTPL8)                      +
             DLTOVR     FILE(OMHSTPL8)                       +
             ENDDO                                           */

 END:        ENDPGM
