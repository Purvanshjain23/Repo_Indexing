     A*%%TS  SD  20000927  104746  ISLRAMS     REL-V4R4M0  5769-PW1
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD7006
     A*  TITLE:         VIEW FARMS FOR A BGF TARGET TABLE
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  DATE:          09/20/00
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A                                      CF04(04 'PROMPT')
     A                                      CF05(05 'REFRESH')
     A*
     A*
     A*---------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP7006S1                  SFL
     A*%%TS  SD  20000927  104746  ISLRAMS     REL-V4R4M0  5769-PW1
     A  85                                  SFLNXTCHG
     A            HDRRN1         4Y 0H
     A            D1FSCD    R        O 10  3REFFLD(@@FSCD)
     A                                      EDTCDE(M)
     A            D1FSNM    R        O 10 11REFFLD(FSREC/FSFSNM *LIBL/HSP018)
     A            D1MDYEFDT R        O 10 40REFFLD(HSRREF/@@DT06 *LIBL/HSPREF)
     A                                      EDTWRD('  /  /  ')
     A            D1CELL    R        O 10 53REFFLD(FSREC/FSCELL *LIBL/HSP018)
     A                                      EDTCDE(M)
     A            D1FSBO    R        O 10 60REFFLD(HSRREF/@@BOCD *LIBL/HSPREF)
     A            D1PTCD    R        O 10 72REFFLD(HSRREF/@@PTCD *LIBL/HSPREF)
     A*
     A*
     A*---------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP7006C1                  SFLCTL(HP7006S1)
     A*%%TS  SD  20000927  104746  ISLRAMS     REL-V4R4M0  5769-PW1
     A                                      SFLSIZ(0050)
     A                                      SFLPAG(0012)
     A N82                                  ROLLUP(25 'PAGE FORWARD')
     A                                      SETOF(31)
     A                                      SETOF(51)
     A                                      SETOF(52)
     A                                      SETOF(53)
     A                                      SETOF(54)
     A                                      RTNCSRLOC(&RCD &FLD)
     A                                      OVERLAY
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 26'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  2 20'VIEW FARMS FOR A BGF TARGET TABLE'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  4  2'Target table ...............:'
     A            C1TTBCD   R        O  4 34REFFLD(HSRREF/@@TTBCD *LIBL/HSPREF)
     A            C1TTBDS   R        O  4 41REFFLD(HSRREF/@@TTBDS *LIBL/HSPREF)
     A                                  5  2'Show current entries only ...'
     A                                  5 32'?'
     A            C1YNFL    R        B  5 34REFFLD(HSRREF/@@FLAG *LIBL/HSPREF)
     A  31
     AO 51                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A N31                                  DSPATR(HI)
     A                                  6 61'?'
     A                                      DSPATR(HI)
     A                                  6 74'?'
     A                                      DSPATR(HI)
     A                                  7 40'Effective'
     A                                      DSPATR(HI)
     A                                  7 58'Business'
     A                                      DSPATR(HI)
     A                                  7 69'Production'
     A                                      DSPATR(HI)
     A                                  8  4'Farm'
     A                                      DSPATR(HI)
     A                                  8 11'Name'
     A                                      DSPATR(HI)
     A                                  8 42'Date'
     A                                      DSPATR(HI)
     A                                  8 52'Cell'
     A                                      DSPATR(HI)
     A                                  8 59'Office'
     A                                      DSPATR(HI)
     A                                  8 72'Type'
     A                                      DSPATR(HI)
     A            C1FSCD    R        B  9  3REFFLD(@@FSCD)
     A                                      DSPATR(HI)
     A                                      EDTCDE(M)
     A            C1CELL    R        B  9 53REFFLD(HSRREF/@@CELL *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A  52                                  DSPATR(PC)
     A            C1FSBO    R        B  9 60REFFLD(HSRREF/@@BOCD *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A  54                                  DSPATR(PC)
     A            C1PTCD    R        B  9 72REFFLD(HSRREF/@@PTCD *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A  55                                  DSPATR(PC)
     A*
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R HP7006K1
     A*%%TS  SD  20000921  074322  ISLRAMS     REL-V4R4M0  5769-PW1
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A                                 23 11'F4=Prompt'
     A                                 23 22'F5=Refresh'
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *---------------------------------------------------------------
     A          R HP7006ES                  SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE CONTROL FORMAT
      *---------------------------------------------------------------
     A          R HP7006EC                  SFLCTL(HP7006ES)
      *
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
