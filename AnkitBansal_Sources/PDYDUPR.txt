/*********************************************************************/
/*     Program: PDYDUPR                                              */
/*       Title: Pork A/R Nightly Aging Updates & Various Reports     */
/*        Date: 04/24/98                                             */
/* >>> NOTE TO PROGRAMMER:                                           */
/* >>>>> THIS PROGRAM IS CALLED ONCE FOR EACH COMPANY NUMBER THAT    */
/*       IS PASSED IN. DO NOT ADD CODE TO PROCESS EACH COMPANY.      */
/* >>>>>                                                             */
/*********************************************************************/
/* Modifications:                                                    */
/*   Date     Pgmr     Description                                   */
/* -------- ---------- --------------------------------------------  */
/* 03/12/98 ISLRAMS    Removed the parameters Sales Type and Sales   */
/*                     Type Description on the calls to AR600CLP     */
/*                                                                   */
/* 04/24/98 ISBLAWS    Removed the copy of the report "Print Nightly */
/*                     Aging by Invoice" that goes to the printer    */
/*                     PRKPRT02.                                     */
/*                                                                   */
/* 04/30/98 ISJTREM    ADDED WAREHOUSE PARAMETERS SO THAT THE PRINT  */
/*                     CLAIMS REPORT COULD BE CALLED.                */
/*                                                                   */
/* 07/06/98 ISBLAWS    Removed the clearing of A/R Order Value       */
/*                     Variance file.                                */
/*                                                                   */
/* 09/16/98 ISBLAWS    Added Export Orders by Customer Report to end */
/*                     of job.                                       */
/* 02/21/02 ISRCENT    ADDED DETAIL/SUMMARY PARM AND INCLUDE         */
/*                     CHARGEBACKS PARM.                             */
/* 04/04/02 ISRCENT    Change cutoff date to yesterday's date        */
/* scan:RC0404                                                       */
/* 07/22/02 ISRCENT    For chargeback report, call AR600 instead of  */
/* scan:RC0722         PDAZPFR                                       */
/*                                                                   */
/* 11/04/02 ISRCENT    ADDED PARENT CUSTOMER PARM AFTER SALES ROUTE  */
/* 01/13/04 ISRCENT    ADDED 5 INVOICE SUFFIX PARMS TO AR600CLP      */
/* 08/30/05 ISRCENT    ARS011- WHEN &COMP IS 360, CALL THE AGING WITH */
/*                     CO 000 TO RUN A COMBINED AGING & CB           */
/* 10/03/05 ISRCENT    RUN EXPORT ORDS FOR CO 360 ONLY               */
/*                     RUN CLAIMS CM/DM FOR 360 AND 362 - WHEN 362 IS READY */
/* 10/03/05 ISRCENT    DO CHANGES FOR 960 BEING READY, MOVE TO TEST  */
/* 12/19/05 ISRCENT    FOR NOW, DEFAULT AGING PRINT TO COMPANY 360   */
/*                     CHANGE VARIABLE &BOTH TO 360, CHG BACK TO 000  */
/*                     WHEN READY                                     */
/* 01/11/06 ISRCENT    READY NOW! SO CHG &BOTH TO 000             */
/*                                                                    */
/* 03/06/06 ISLRAMS    ADDED ADDITIONAL CALLS TO AR600CL.             */
/*                     IN ADDITION TO THE EXISTING CALLS, WE WILL     */
/*                     IMMEDIATELY REPEAT THE CALL FOR COMPANY 360    */
/*                     AND THEN COMPANY 960 OVERRIDING THE PRINT FILE */
/*                     TO ONLY PRINT THE LAST PAGE WHICH WE WILL      */
/*                     SEND TO THE DUP OUT QUEUE.                     */
/*                                                                    */
/* 03/08/06 ISLRAMS    PER AMB, THE USER JUST WANTS 1 COPY OF         */
/*                     EVERYTHING. SO, CHANGED THE HARDCODING OF      */
/*                     "COPIES" FROM 2 TO 1.                          */
/*                                                                    */
/* 09/21/06 ISMASO     ADD THE NEW REPORT FOR A/R CLAIM EXP PENDING   */
/* 09/25/06 ISMASO     PER AMB, VARIOUS AREAS WILL NO LONGER NEED     */
/*                     THE LAST PAGE OF THE REPORT.                   */
/* 10/03/06 ISMASO     Move the CLAIM after the A/R NIGHTLY AGING BY  */
/*                     INVOICE, then move the CLAIM PENDING after the */
/*                     test of 360 go to end.                         */
/*                     Change the 360 go to CLAIMS TO go to end       */
/*                     Change the 2ND 360 to go to end because of the */
/*                     new change go to end.                          */
/*                                                                    */
/*                     The cl should be review before going           */
/*                     live with another plant... or DAILYS           */
/*                                                                    */
/* 12/06/06 ISLBUSE    RECEIVE A/R AGE CODE FROM CALLING PROGRAM AND  */
/*   PK0039            PASS TO AR600CLP.                              */
/* 02/27/07 ISLBUSE    REMOVED CODE THAT SENT THE PROGRAM TO "END"    */
/*   L70227A           IF COMPANY WAS NOT 360. REMOVED HARDCODING,    */
/* (03/01/2007 LJB)    ADD NEW PARM - COUNT OF TIMES THIS IS CALLED.  */
/*                     REARRANGED PROGRAMS: RUN ALL PROGRAMS THAT     */
/*                     PROCESS BY COMPANY NUMBER FIRST. RUN ALL       */
/*                     PROGRAMS THAT RUN ONLY ONCE AFTER THE TEST     */
/*                     FOR THE COUNTER = 2.                           */
/* 10/26/07 PKD        ADDED DUPSPLF FOR DAILY'S                     */
/* 10/08/08 RMC        FOR &ORG = HPB DONT CHG &LAST TO "Y", WE WANT  */
/*                     TO PRINT THE WHOLE REPORT                     */
/* 06/26/09 LJB        ADDED NEW CUSTOMER WATCH FLAG TO CALL TO       */
/*   E00431            AR600CLP - THIS WILL BE BLANKS HERE.           */
/* 11/03/10 SLM        * OUT THE PRT CLAIM EXPENSE PENDING            */
/*   E001124                                                          */
/* 06/08/16 JBB        ADD INCLUDE REBILL FLAG TO PARMS LIST OF       */
/*   E5262             CALLED PROGRAMS.  ADD SECTIONS TO PRINT        */
/*                     REPORTS FOR REBILLS ONLY, SIMILAR TO           */
/*                     CHARGEBACKS ONLY.                              */
/* 03/08/17 RMC        FOR &ORG = S, CHANGE COUNTER TO 3 TO ALLOW RUN */
/*                     FOR 440, SO USE &COUNTCOMP;  GET 960 DUPOUTQ FROM */
/*                     A COMPANY VALUE SO IT CAN RUN IN TEST          */
/* 06/23/17 LJB        FOR &ORG = S, DON'T PRINT THE FULL VERSION OF  */
/*   E010921           THE REPORTS (2ND SECTION)                      */
/*                     THIS INCLUDES THE EXPORT ORDER BY CUST         */
/*                     PER CLAUDIA ELLIS                              */
/* 12/14/17 RMC        FOR &ORG ='H'PB HOLD ALL REPORTS, PER CLAUDIA   */
/*     S12208                                                        */
/*********************************************************************/
 PDYDUPR:    PGM        PARM(&COMP &OUTQ &AGECODE &COUNT)

   /*-------------------*/
   /* Declare Variables */
   /*-------------------*/

             DCL  VAR(&COMP)     TYPE(*DEC)
             DCL  VAR(&RETURN)   TYPE(*CHAR) LEN(7)
             DCL  VAR(&FROMDATE) TYPE(*CHAR) LEN(7)
             DCL  VAR(&TODATE)   TYPE(*CHAR) LEN(7)
             DCL  VAR(&OUTQ)     TYPE(*CHAR) LEN(10)
             DCL  VAR(&HOLD)     TYPE(*CHAR) LEN(4)   VALUE('*NO ')
             DCL  VAR(&SAVE)     TYPE(*CHAR) LEN(4)   VALUE('*YES')
             DCL  VAR(&COPY)     TYPE(*DEC)  LEN(2 0)
             DCL  VAR(&LAST)     TYPE(*CHAR) LEN(1)   VALUE(' ')
             DCL  VAR(&COMPN)    TYPE(*DEC)  LEN(3 0)
             DCL  VAR(&COPIES)   TYPE(*DEC)
             DCL  VAR(&DATE)     TYPE(*CHAR) LEN(6)
             DCL  VAR(&CYMD)     TYPE(*CHAR) LEN(7)
             DCL  VAR(&YESTR)    TYPE(*DEC) LEN(7 0)
             DCL  VAR(&CUTOF)    TYPE(*DEC) LEN(7 0)
             DCL  VAR(&ASOFD)    TYPE(*DEC)
             DCL  VAR(&ARCUST)   TYPE(*DEC)
             DCL  VAR(&ARCSTN)   TYPE(*CHAR) LEN(30)
             DCL  VAR(&PARENT)   TYPE(*DEC)         /* PARENT */
             DCL  VAR(&PARNAM)   TYPE(*CHAR) LEN(30) /* PARENT NAME */
             DCL  VAR(&ONEDAY)   TYPE(*DEC) LEN(8 0) VALUE(-1)
             DCL  VAR(&PRTZR)    TYPE(*CHAR) LEN(1)   VALUE('N')
             DCL  VAR(&RPTTYP)   TYPE(*CHAR) LEN(1)   VALUE('1')
             DCL  VAR(&SLSRT)    TYPE(*CHAR) LEN(3)
             DCL  VAR(&SLSRTN)   TYPE(*CHAR) LEN(30)
             DCL  VAR(&INCLCB)   TYPE(*CHAR) LEN(1)
             DCL  VAR(&INCLRB)   TYPE(*CHAR) LEN(1)
             DCL  VAR(&NIGHTQ)   TYPE(*CHAR) LEN(10)
             DCL  VAR(&FDATE)    TYPE(*DEC)  LEN(7 0)
             DCL  VAR(&DATEN)    TYPE(*DEC)
             DCL  VAR(&ITEM)     TYPE(*DEC)
             DCL  VAR(&REASCD)   TYPE(*CHAR) LEN(3)   VALUE('   ')
             DCL  VAR(&DTLSUM)   TYPE(*CHAR) LEN(1) VALUE('D')
             DCL  VAR(&SELRPT)   TYPE(*CHAR) LEN(1)   VALUE('2')
             DCL  VAR(&MEMOS)    TYPE(*CHAR) LEN(1)   VALUE('B')
             DCL  VAR(&WHSE1)    TYPE(*CHAR) LEN(3)   VALUE('   ')
             DCL  VAR(&WHSE2)    TYPE(*CHAR) LEN(3)   VALUE('   ')
             DCL  VAR(&WHSE3)    TYPE(*CHAR) LEN(3)   VALUE('   ')
             DCL  VAR(&WHSE4)    TYPE(*CHAR) LEN(3)   VALUE('   ')
             DCL  VAR(&WHSE5)    TYPE(*CHAR) LEN(3)   VALUE('   ')
             DCL  VAR(&WHSE6)    TYPE(*CHAR) LEN(3)   VALUE('   ')
             DCL  VAR(&WHSE7)    TYPE(*CHAR) LEN(3)   VALUE('   ')
             DCL  VAR(&WHSE8)    TYPE(*CHAR) LEN(3)   VALUE('   ')
             DCL  VAR(&WHSE9)    TYPE(*CHAR) LEN(3)   VALUE('   ')
             DCL  VAR(&WHSE10)   TYPE(*CHAR) LEN(3)   VALUE('   ')
             DCL  VAR(&SOFLAG)   TYPE(*CHAR) LEN(1)   VALUE(' ')
             DCL  VAR(&INVS1)    TYPE(*CHAR) LEN(2)   VALUE('  ')
             DCL  VAR(&INVS2)    TYPE(*CHAR) LEN(2)   VALUE('  ')
             DCL  VAR(&INVS3)    TYPE(*CHAR) LEN(2)   VALUE('  ')
             DCL  VAR(&INVS4)    TYPE(*CHAR) LEN(2)   VALUE('  ')
             DCL  VAR(&INVS5)    TYPE(*CHAR) LEN(2)   VALUE('  ')
             DCL  VAR(&AGECODE)  TYPE(*CHAR) LEN(7)   /* PK0039 */
             DCL  VAR(&BOTH)     TYPE(*DEC) VALUE(0000000000.00000)
                                                 /* ALL COMPS*/
             DCL  VAR(&COUNT)    TYPE(*DEC)  LEN(5 0) /* L70227A */
             DCL  VAR(&COUNTCOMP)    TYPE(*DEC)  LEN(5 0)
             DCL  VAR(&TFPRTDUP)    TYPE(*CHAR) LEN(10)
             DCL  VAR(&SPPRTDUP)    TYPE(*CHAR) LEN(10)
             DCL  VAR(&PLT) TYPE(*CHAR) LEN(1)
             DCL  VAR(&ORG) TYPE(*CHAR) LEN(1)
             DCL  VAR(&WATCH)    TYPE(*CHAR) LEN(2)   VALUE('  ')
                                                      /* E00431 */
/*-------------------------------------------------------------------*/
/* Initialize Variables                                              */
/*-------------------------------------------------------------------*/
             CHGVAR     VAR(&COMPN) VALUE(&COMP)
             CHGVAR     VAR(&COPIES) VALUE(1)
             CHGVAR     VAR(&ARCUST) VALUE(0)
             CHGVAR     VAR(&ITEM) VALUE(0000000000.00000)
             CHGVAR     VAR(&FDATE) VALUE(0000000)
             CHGVAR     VAR(&DTLSUM) VALUE('D')
             CHGVAR     VAR(&SELRPT) VALUE('2')
             CHGVAR     VAR(&MEMOS) VALUE('B')
             CHGVAR     VAR(&HOLD) VALUE('*NO ')
             CHGVAR     VAR(&SAVE) VALUE('*YES')
             CHGVAR     VAR(&ONEDAY) VALUE(-1)
             CHGVAR     VAR(&PRTZR) VALUE('N')
             CHGVAR     VAR(&RPTTYP) VALUE('1')
/*-------------------------------------------------------------------*/
/* GET ORGANIZATION FOR CHECKING LATER                              */
/*-------------------------------------------------------------------*/
             CALL       PGM(PMOVXFR) PARM(' ' &COMPN &PLT &ORG)

             IF COND(&ORG *EQ 'S') THEN(CHGVAR VAR(&COUNTCOMP) VALUE(3))
             IF COND(&ORG *EQ 'D') THEN(CHGVAR VAR(&COUNTCOMP) VALUE(2))
             IF COND(&ORG *EQ 'H') THEN(CHGVAR VAR(&COUNTCOMP) VALUE(2))
             IF COND(&ORG *EQ 'S') THEN(CHGVAR VAR(&HOLD) VALUE('*NO '))
S12208:      IF COND(&ORG *EQ 'H') THEN(CHGVAR VAR(&HOLD) VALUE('*YES'))
             IF COND(&COMPN *EQ 960) THEN(DO)
               CALL POMTXFR ('' &COMPN 'DFTDUPOUT' &TFPRTDUP)
               ENDDO
             IF COND(&COMPN *EQ 440) THEN(DO)
               CALL POMTXFR ('' &COMPN 'DFTDUPOUT' &SPPRTDUP)
               ENDDO

/**+ + + + + + + + + + + + + + + + + + + + + + + + + + + + + + + + +**/
/** PUT ANY PROGRAMS THAT NEED TO RUN FOR ALL COMPANIES IN THIS      */
/**   SECTION. PROGRAMS THAT ONLY RUN ONCE NEED TO GO AT THE         */
/**   BOTTOM AFTER THE TEST FOR THE COUNTER.                         */
/**+ + + + + + + + + + + + + + + + + + + + + + + + + + + + + + + + +**/

/*-------------------------------------------------------------------*/
/* UPDATE A/R SUM AGING                                              */
/*-------------------------------------------------------------------*/
             CALL PGM(PDXNXFR) PARM(&RETURN &COMPN)

/*-------------------------------------------------------------------*/
/* CALC PREVIOUS WORK DATE                                           */
/* PRINT CLAIMS BASED ON PASSED IN REPORT TYPE (6 REPORTS)           */
/* ** NOTE - ONLY RPT 2 IS BEING SELECTED **                         */
/*-------------------------------------------------------------------*/
/* CLC PREVIOUS WORK DATE XF - /UT FUNCTIONS *                       */
 CLAIMS:     CALL       PGM(PDC7XFR) PARM('' &FDATE)
             CHGVAR     VAR(&DATEN) VALUE(&FDATE)

             CALL       PGM(PDXGPCLP) PARM(&COMP &ARCUST &DATEN +
                          &DATEN &ITEM &REASCD &SLSRT &SELRPT +
                          &MEMOS &OUTQ &HOLD &SAVE &COPIES &NIGHTQ +
                          &WHSE1 &WHSE2 &WHSE3 &WHSE4 &WHSE5 &WHSE6 +
                          &WHSE7 &WHSE8 &WHSE9 &WHSE10 &SOFLAG)

/*-------------------------------------------------------------------*/
/* CLC INC/DEC DATE SHR XF - CALENDAR *                              */
/*-------------------------------------------------------------------*/
             RTVSYSVAL  SYSVAL(QDATE) RTNVAR(&DATE)
             CVTDAT     DATE(&DATE) TOVAR(&CYMD) FROMFMT(*MDY) +
                          TOFMT(*CYMD) TOSEP(*NONE)
BRC0404:     CHGVAR     VAR(&CUTOF) VALUE(&CYMD)

/* CLC INC/DEC DATE SHR XF - CALENDAR *                              */
             CALL       PGM(PDNKXFR) PARM('' &YESTR &COMPN &CUTOF +
                          &ONEDAY)
             CHGVAR     VAR(&ASOFD) VALUE(&YESTR)

/*-------------------------------------------------------------------*/
/* PRINT NIGHTLY AGING BY INVOICE                                    */
/* PRINT NIGHTLY AGING BY CUSTOMER FOR THE PASSED IN COMPANY         */
/* GENERATE THE REPORT FOR THE PASSED IN COMPANY.                    */
/* OVERRIDE THE PRINT FILE TO PRINT ONLY THE "LAST" PAGE.            */
/* NOTE:  &COMP PARM HAS THE PASSED IN COMPANY NUMBER AS 15.5        */
             /* E5262 JBB 06/08/16 - SET INCLUDE REBILL FLAG */
/*-------------------------------------------------------------------*/
             CHGVAR     VAR(&INCLCB) VALUE('N')
             CHGVAR     VAR(&INCLRB) VALUE('N')

NOTHPB:      IF         COND(&ORG *NE 'H') THEN(DO)
             CHGVAR     VAR(&LAST) VALUE('Y')
             ENDDO

             CALL       PGM(AR600CLP) PARM(&COMP &ASOFD &ARCUST +
                          &ARCSTN &PARENT &PARNAM &PRTZR &RPTTYP +
                          &SLSRT &SLSRTN &DTLSUM &INCLCB &INCLRB +
                          &INVS1 &INVS2 &INVS3 &INVS4 &INVS5 &OUTQ +
                          &HOLD &SAVE &COPIES &NIGHTQ &LAST +
                          &AGECODE &WATCH)

             IF         COND(&COMPN *EQ 960) THEN(DO)
             DUPSPLF    FILE(PRINT198) OUTQ(&TFPRTDUP) SPLNBR(*LAST) +
                          HOLD(*NO)
             MONMSG     MSGID(CPF0000)
             ENDDO
             IF         COND(&COMPN *EQ 440) THEN(DO)
             DUPSPLF    FILE(PRINT198) OUTQ(&SPPRTDUP) SPLNBR(*LAST) +
                          HOLD(*NO)
             MONMSG     MSGID(CPF0000)
             ENDDO

   /* IF ORGANIZATION IS DAILY'S, DUP THE REPORT TO DAILY'S OUTQS */
             IF         COND(&ORG *EQ 'D') THEN(DO)
             DUPSPLF    FILE(PRINT198) OUTQ(DMPRTDUP) SPLNBR(*LAST) +
                          HOLD(*NO)
             DUPSPLF    FILE(PRINT198) OUTQ(DSPRTDUP) SPLNBR(*LAST) +
                          HOLD(*NO)
             ENDDO
/*-------------------------------------------------------------------*/
/* PRINT CHARGEBACKS FROM AGING REPORT -- CHARGEBACKS ONLY           */
/*-------------------------------------------------------------------*/
             CHGVAR     VAR(&INCLCB) VALUE('O')

             /* E5262 JBB 06/08/16 - SET INCLUDE REBILL FLAG */
             CHGVAR     VAR(&INCLRB) VALUE('N')

             CALL       PGM(AR600CLP) PARM(&COMP &ASOFD &ARCUST +
                          &ARCSTN &PARENT &PARNAM &PRTZR &RPTTYP +
                          &SLSRT &SLSRTN &DTLSUM &INCLCB &INCLRB +
                          &INVS1 &INVS2 &INVS3 &INVS4 &INVS5 &OUTQ +
                          &HOLD &SAVE &COPIES &NIGHTQ &LAST +
                          &AGECODE &WATCH)

             IF         COND(&COMPN *EQ 960) THEN(DO)
             DUPSPLF    FILE(PRINT198) OUTQ(&TFPRTDUP) SPLNBR(*LAST) +
                          HOLD(*NO)
             MONMSG     MSGID(CPF0000)
             ENDDO
             IF         COND(&COMPN *EQ 440) THEN(DO)
             DUPSPLF    FILE(PRINT198) OUTQ(&SPPRTDUP) SPLNBR(*LAST) +
                          HOLD(*NO)
             MONMSG     MSGID(CPF0000)
             ENDDO

   /* IF ORGANIZATION IS DAILY'S, DUP THE REPORT TO DAILY'S OUTQS */
             IF         COND(&ORG *EQ 'D') THEN(DO)
             DUPSPLF    FILE(PRINT198) OUTQ(DMPRTDUP) SPLNBR(*LAST) +
                          HOLD(*NO)
             DUPSPLF    FILE(PRINT198) OUTQ(DSPRTDUP) SPLNBR(*LAST) +
                          HOLD(*NO)
             ENDDO
/*-------------------------------------------------------------------*/
/* E5262 JBB 06/08/16 - SET INCLUDE REBILL FLAG                      */
/* PRINT REBILLS FROM AGING REPORT -- REBILLS ONLY                   */
/*-------------------------------------------------------------------*/
             CHGVAR     VAR(&INCLRB) VALUE('O')
             CHGVAR     VAR(&INCLCB) VALUE('N')

             CALL       PGM(AR600CLP) PARM(&COMP &ASOFD &ARCUST +
                          &ARCSTN &PARENT &PARNAM &PRTZR &RPTTYP +
                          &SLSRT &SLSRTN &DTLSUM &INCLCB &INCLRB +
                          &INVS1 &INVS2 &INVS3 &INVS4 &INVS5 &OUTQ +
                          &HOLD &SAVE &COPIES &NIGHTQ &LAST +
                          &AGECODE &WATCH)

             IF         COND(&COMPN *EQ 960) THEN(DO)
             DUPSPLF    FILE(PRINT198) OUTQ(&TFPRTDUP) SPLNBR(*LAST) +
                          HOLD(*NO)
             MONMSG     MSGID(CPF0000)
             ENDDO
             IF         COND(&COMPN *EQ 440) THEN(DO)
             DUPSPLF    FILE(PRINT198) OUTQ(&SPPRTDUP) SPLNBR(*LAST) +
                          HOLD(*NO)
             MONMSG     MSGID(CPF0000)
             ENDDO

   /* IF ORGANIZATION IS DAILY'S, DUP THE REPORT TO DAILY'S OUTQS */
             IF         COND(&ORG *EQ 'D') THEN(DO)
             DUPSPLF    FILE(PRINT198) OUTQ(DMPRTDUP) SPLNBR(*LAST) +
                          HOLD(*NO)
             DUPSPLF    FILE(PRINT198) OUTQ(DSPRTDUP) SPLNBR(*LAST) +
                          HOLD(*NO)
             ENDDO

/**+ + + + + + + + + + + + + + + + + + + + + + + + + + + + + + + + +**/
/** PUT ANY PROGRAMS THAT ONLY RUN ONCE IN THIS SECTION.             */
/**   PROGRAMS THAT RUN FOR EACH COMPANY NEED TO GO IN THE TOP       */
/**+ + + + + + + + + + + + + + + + + + + + + + + + + + + + + + + + +**/
/*-------------------------------------------------------------------*/
/* PRINT NIGHTLY AGING BY CUSTOMER FOR ALL COMPANIES                 */
/* PK0039 - ADD A/R AGING CODE TO AR600CLP                           */
/* NOTE:  &BOTH PARM HAS ALL ZEROS IN IT, PRINT ENTIRE REPORT        */
/* E5262 JBB 06/08/16 - SET INCLUDE REBILL FLAG                      */
/* E010921 DON'T PRINT THE FULL VERSIONS OF THE REPORTS FOR          */
/* ORGANIZATION = SEABOARD, PER CLAUDIA ELLIS                        */
/*-------------------------------------------------------------------*/
 /*****  STRCNTEQ2:  IF         COND(&COUNT *EQ 2) THEN(DO)        */
 STRCNTEQ2:  IF         COND(&COUNT *EQ &COUNTCOMP) THEN(DO)

             CHGVAR     VAR(&LAST) VALUE(' ')
 ERC0404:    CHGVAR     VAR(&INCLCB) VALUE('N')

             CHGVAR     VAR(&INCLRB) VALUE('N')
/* E010921 */
             IF         COND(&ORG *EQ 'S') THEN(GOTO CMDLBL(END))

             CALL       PGM(AR600CLP) PARM(&BOTH &ASOFD &ARCUST +
                          &ARCSTN &PARENT &PARNAM &PRTZR &RPTTYP +
                          &SLSRT &SLSRTN &DTLSUM &INCLCB &INCLRB +
                          &INVS1 &INVS2 &INVS3 &INVS4 &INVS5 &OUTQ +
                          &HOLD &SAVE &COPIES &NIGHTQ &LAST +
                          &AGECODE &WATCH)

/*-------------------------------------------------------------------*/
/* PRINT CHARGEBACKS FROM AGING REPORT -- CHARGEBACKS ONLY           */
/* FOR ALL COMPANIES                                                 */
/* E5262 JBB 06/08/16 - SET INCLUDE REBILL FLAG                      */
/*-------------------------------------------------------------------*/
             CHGVAR     VAR(&INCLCB) VALUE('O')
             CHGVAR     VAR(&INCLRB) VALUE('N')

 ERC0722:    CALL       PGM(AR600CLP) PARM(&BOTH &ASOFD &ARCUST +
                          &ARCSTN &PARENT &PARNAM &PRTZR &RPTTYP +
                          &SLSRT &SLSRTN &DTLSUM &INCLCB &INCLRB +
                          &INVS1 &INVS2 &INVS3 &INVS4 &INVS5 &OUTQ +
                          &HOLD &SAVE &COPIES &NIGHTQ &LAST +
                          &AGECODE &WATCH)

/*-------------------------------------------------------------------*/
/* PRINT REBILLS FROM AGING REPORT -- REBILLS ONLY                   */
/* FOR ALL COMPANIES                                                 */
/*-------------------------------------------------------------------*/
             CHGVAR     VAR(&INCLRB) VALUE('O')
             CHGVAR     VAR(&INCLCB) VALUE('N')

 ERB0722:    CALL       PGM(AR600CLP) PARM(&BOTH &ASOFD &ARCUST +
                          &ARCSTN &PARENT &PARNAM &PRTZR &RPTTYP +
                          &SLSRT &SLSRTN &DTLSUM &INCLCB &INCLRB +
                          &INVS1 &INVS2 &INVS3 &INVS4 &INVS5 &OUTQ +
                          &HOLD &SAVE &COPIES &NIGHTQ &LAST +
                          &AGECODE &WATCH)

/*-------------------------------------------------------------------*/
/* PRINT CLAIMS PENDING RPT                                          */
/* ADD NEW REPORT THAT WILL BE SENT VIA EMAIL                        */
/*-------------------------------------------------------------------*/
 CLAIMSPND:
/*******     CALL       PGM(PLA3XFR) PARM(' ' 'E')              ******/

/*-------------------------------------------------------------------*/
/* Export Orders by Customer Report                                  */
/* Hold the report. They don't want to print the whole thing. E010921*/
/*-------------------------------------------------------------------*/
             CHGVAR VAR(&RETURN)   VALUE(' ')
             CHGVAR VAR(&FROMDATE) VALUE('0000000')
             CHGVAR VAR(&TODATE)   VALUE('0000000')
             CHGVAR VAR(&HOLD)     VALUE('*YES')
             CHGVAR VAR(&SAVE)     VALUE('*YES')
             CHGVAR VAR(&COPY)     VALUE(1)

             CALL       PGM(PDS7XFR) PARM(&RETURN &FROMDATE &TODATE +
                          &OUTQ &HOLD &SAVE &COPY)
 ENDCNTEQ2:  ENDDO

 END:        ENDPGM
