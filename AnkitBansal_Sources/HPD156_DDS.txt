     A*%%TS  SD  19941208  161831  ISLRAMS     REL-V3R0M5  5763-PW1
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD156
     A*  TITLE:         MAINTAIN RATION CALORIES
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  CREATED:       12/05/94
     A*
     A*  INDICATOR 60 IS ON WHEN IN REVISE MODE
     A*  INDICATOR 61 IS ON WHEN IN CREATE MODE
     A*  INDICATOR 62 IS ON WHEN USER HAS DELETED OR CREATED A RECORD
     A*               IN THE SUBFILE
     A*  INDICATOR 90 IS ON WHEN THE BUSINESS OFFICE THE USER HAS
     A*               SELECTED FROM THE SPECIFY HAS BEEN DELETED
     A*               BY ANOTHER USER
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A N60                                  CF04(04 'PROMPT')
     A N90                                  CF05(05 'REFRESH')
     A  60N90                               CF06(06 'CREATE')
     A                                      CA12(12 'PREVIOUS')
     A N82N90                               ROLLUP(25 'PAGE FORWARD')
     A*
     A*---------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP156S1                   SFL
     A*%%TS  SD  19941208  155452  ISLRAMS     REL-V3R0M5  5763-PW1
     A                                      SETOF(30)
     A                                      SETOF(31)
     A                                      SETOF(32)
     A                                      SETOF(62)
     A  85                                  SFLNXTCHG
     A            HDRRN1         4Y 0H
     A            DFOPT          2A  B 10  2CHGINPDFT(UL)
     A  61
     AO 62
     AO 90                                  DSPATR(PR)
     A  61
     AO 90                                  DSPATR(ND)
     A  30                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A N61N62N90
     A N30                                  DSPATR(HI)
     A N61N62N90
     A N30                                  DSPATR(UL)
     A            DFRNCD    R        B 10  8REFFLD(CPRNCD)
     A  60
     AO 62
     AO 90                                  DSPATR(PR)
     A  61 90                               DSPATR(ND)
     A  31                                  DSPATR(RI)
     A  31
     AO 51                                  DSPATR(PC)
     A N60N62N90
     A N31                                  DSPATR(HI)
     A N60N62N90
     A N31                                  DSPATR(UL)
     A            DFCLRT    R        B 10 20REFFLD(CPCLRT)
     A                                      EDTCDE(K)
     A  62
     AO 90                                  DSPATR(PR)
     A  32                                  DSPATR(RI)
     A  32                                  DSPATR(PC)
     A N32N62N90                            DSPATR(HI)
     A N32N62N90                            DSPATR(UL)
     A            DFRNDS    R        O 10 33REFFLD(RNRNDS)
     A            DFACT          8A  O 10 59
     A*
     A*---------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP156C1                   SFLCTL(HP156S1)
     A*%%TS  SD  19941208  161831  ISLRAMS     REL-V3R0M5  5763-PW1
     A                                      SFLSIZ(0100)
     A                                      SFLPAG(0012)
     A                                      SFLCSRRRN(&HDRR1)
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A                                      OVERLAY
     A                                      RTNCSRLOC(&RCD &FLD)
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            HDRR1          5S 0H
     A            RCD           10A  H
     A            FLD           10A  H
     A                                  1  2'HP156'
     A                                      DSPATR(HI)
     A                                  1 25'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A            DFMODE    R        O  2  2REFFLD(@@MODE)
     A                                      DSPATR(HI)
     A                                  2 24'MAINTAIN RATION CALORIES'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  4  2'Effective date ..:'
     A            DFMDY     R        O  4 22REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A N61                              6  2'4=Delete'
     A  61                              7 10'?'
     A                                      DSPATR(HI)
     A                                  8  8'Ration'
     A                                      DSPATR(HI)
     A                                  8 17'Calories per'
     A                                      DSPATR(HI)
     A N61                              9  2'Opt'
     A                                      DSPATR(HI)
     A                                  9  8'Code'
     A                                      DSPATR(HI)
     A                                  9 21'Pound'
     A                                      DSPATR(HI)
     A                                  9 33'Description'
     A                                      DSPATR(HI)
     A*
     A*----------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*----------------------------------------------------------------
     A          R HP156K1
     A*%%TS  SD  19940321  103222  ISLRAMS     REL-V2R2M0  5738-PW1
     A                                      SETOF(60)
     A                                      SETOF(61)
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A                                      DSPATR(HI)
     A                                 23  2'F3=Exit'
     A N60N90                          23 11'F4=Prompt'
     A N90                             23 22'F5=Refresh'
     A  60N90                          23 34'F6=Create'
     A                                 23 45'F12=Previous'
     A*
     A*----------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE FORMAT
     A*----------------------------------------------------------------
     A          R HP156ES                   SFL SFLMSGRCD(24)
     A*
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
     A*
     A*----------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*----------------------------------------------------------------
     A          R HP156EC                   SFLCTL(HP156ES)
     A*
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
