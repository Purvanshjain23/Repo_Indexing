      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Hog Production System
      * PROGRAM:     HP248 - Download: Market Selector--BGF Farm Sire Lines
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     03/06/02
      *
      * FUNCTION:    This program builds a file of HPS BGF Farm sire lines for each
      *              service year/week in the Market Selector download file.
      *              This file will be FTP'd to the Pork Intranet Site on a nightly
      *              basis.
      *
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 01/27/05  LeAnne Fedor
      *           Recompile only. Two new fields (sire line company, prime line)
      *           added to Sire Line file.
      *
      * 09/20/06  LeAnne Fedor
      *           Recompile only. New field "Origin BGF Pod" was added to the
      *           Market Selector Group file.
      *
      * 10/18/10  LeAnne Ramsey  (E1079)
      *           Recompile only. Sire Line Company is now 2-characters instead
      *           of one in file HSP098-Sire Lines.
      /eject
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fhsp098    if   e           k disk
      *  Sire lines
      *
      *
     Fhsp325    if   e           k disk
      *  Workfile: Weekly sire line dose percents
      *
      *
     Fhsp246    if   e           k disk
      *   Download: Market selector
      *
      *
     Fhsp248    uf a e           k disk
      *   Download: Market selector--BGF farm sire lines
      *
      /eject
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Workfields
      *
     D wkgg0           s                   like(sigg0)
      *
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * MAINLINE
      ****************************************************************
      *
      * Process each BGF Farm record in the Market Selector file.
      * (Only BGF Farm records will have a 'service year'; records with a service
      *  year of zero are 'group' records...not BGF farm records.)
      *
     C     *loval        setll     hsp246
      *
     C                   dou       *in90 = *on                                  Do BGF farms
     C                   read      hsp246                                 90
     C                   if        *in90 = *off and d3svyr <> 0                 If not EOF
     C                   exsr      $sireline
     C                   endif                                                  If not EOF
     C                   enddo                                                  Do BGF farms
      *
      * Now, reread the sireline records that you have just written
      * and calculate the dose 'rate' from the dose 'percent'.
      *
     C                   exsr      $rate
      *
     C                   seton                                        lr
      /eject
      *-----------------------------------------------------------------------
      * Retrieve sire line data for the BGF farm/service year/service week
      *-----------------------------------------------------------------------
      *
     C     $sireline     begsr
      *
     C     key02         setll     hsp325
      *
     C                   dou       *in91 = *on                                  Do BGF farm
     C     key02         reade     hsp325                                 91
     C                   if        *in91 = *off                                 If not EOF
     C                   exsr      $cognos
     C                   exsr      $doses
     C                   endif                                                  If not EOF
     C                   enddo                                                  Do BGF farm
      *
     C                   endsr
      /eject
      *--------------------------------------------------------------------------
      * Retrieve the 'Cognos report grouping code' associated with the sire line
      *--------------------------------------------------------------------------
      *
     C     $cognos       begsr
      *
     C     w1sicd        chain     hsp098                             92
     C                   if        *in92 = *off                                 If hit
     C                   move      sigg0         wkgg0
     C                   else
     C                   move      w1sicd        wkgg0
     C                   endif                                                  If hit
      *
     C                   endsr
      /eject
      *--------------------------------------------------------------------------
      * Write or update the 'doses' record for the Cognos Report Grouping
      *--------------------------------------------------------------------------
      *
     C     $doses        begsr
      *
     C     key01         chain     hsp248                             92
     C                   if        *in92 = *off                                 If hit
     C                   add       w1dopr        d5dopr
     C                   update    d5rec
     C                   else
      *
     C                   move      wkgg0         d5gg0
     C                   z-add     w1dopr        d5dopr
      *
     C                   move      d3ds          d5ds
     C                   move      d3ppcd        d5ppcd
     C                   z-add     d3svyr        d5svyr
     C                   z-add     d3svwk        d5svwk
     C                   z-add     d3fscd        d5fscd
     C                   z-add     d3cell        d5cell
      *
     C                   write     d5rec
     C                   endif                                                  If hit
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Calculate the 'rate' that corresponds to the 'percent'
      *---------------------------------------------------------------
      *
      * Brad wanted the 'rate' (5,4) instead of the 'percent' (5,2).
      * So, just divide each 'percent' by 100 to get the rate for each record.
      *
     C     $rate         begsr
      *
     C     *loval        setll     hsp248
      *
     C                   dou       *in91 = *on                                  Do rates
     C                   read      hsp248                                 91
     C                   if        *in91 = *off                                 If not EOF
     C     d5dopr        div       100           d5dort
     C                   update    d5rec
     C                   endif                                                  If not EOF
     C                   enddo                                                  Do rates
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * *inzsr - initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Parm lists
      *    None
      *
      *
      * Key lists
      *
     C     key01         klist
     C                   kfld                    d3ds
     C                   kfld                    wkgg0
      *
      *
     C     key02         klist
     C                   kfld                    d3fscd
     C                   kfld                    d3svyr
     C                   kfld                    d3svwk
      *
     C                   endsr
      /eject
