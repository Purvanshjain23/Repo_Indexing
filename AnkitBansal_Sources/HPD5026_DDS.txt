     A*%%TS  SD  20021223  114430  ISLRAMS     REL-V5R1M0  5722-WDS
     A*****************************************************************
     A*
     A*  SYSTEM:        HOG PRODUCTION
     A*  DISPLAY FILE:  HPD5026 - SELECT TRANSPORTATION OFFICE
     A*  CREATED BY:    LEANNE FEDOR
     A*  DATE:          12/23/02
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      PRINT
     A                                      CA12(12 'PREVIOUS')
     A N82                                  ROLLUP(25 'PAGE FORWARD')
     A*---------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP5026S1                  SFL
     A*%%TS  SD  20021223  113402  ISLRAMS     REL-V5R1M0  5722-WDS
     A            HDRRN1    R        H      REFFLD(@@RRN)
     A            D1TOCD    R        O  6  2REFFLD(@@TOCD)
     A            D1TONM    R        O  6 10REFFLD(HSRREF/@@TONM *LIBL/HSPREF)
     A            D1AIST    R        O  6 40REFFLD(HSRREF/@@AIST *LIBL/HSPREF)
     A*
     A*---------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP5026C1                  SFLCTL(HP5026S1)
     A*%%TS  SD  20021223  114430  ISLRAMS     REL-V5R1M0  5722-WDS
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0008)
     A                                      WINDOW(HP5026EC)
     A                                      OVERLAY
     A                                      SFLCSRRRN(&HDRRN)
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A            HDPAG1    R        H      REFFLD(@@PAG)
     A                                      SFLRCDNBR
     A            HDRRN     R        H      REFFLD(@@RRN)
     A            SDPGM     R        O  1  1REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 13'SELECT TRANSPORTATION OFFICE'
     A                                      DSPATR(HI)
     A            C1DESC    R        O  2 13REFFLD(@@DS30)
     A                                      DSPATR(HI)
     A                                  3  2'Position Cursor, Press Enter'
     A                                  5  2'Code'
     A                                      DSPATR(HI)
     A                                  5 10'Name'
     A                                      DSPATR(HI)
     A                                  5 38'Status'
     A                                      DSPATR(HI)
     A*---------------------------------------------------------------
     A* COMMAND LINE LINE
     A*---------------------------------------------------------------
     A          R HP5026K1
     A                                      WINDOW(HP5026EC)
     A                                      OVERLAY
     A  43                             15  1'Message program failed. Call IS.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 16  1'F12=Previous'
      *
     A*-------------------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
     A*-------------------------------------------------------------------------
     A          R HP5026ES                  SFL SFLMSGRCD(17)
      *
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
     A*
     A*-------------------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*-------------------------------------------------------------------------
     A*
     A          R HP5026EC                  SFLCTL(HP5026ES)
     A*%%TS  SD  19980327  105628  ISLRAMS     REL-V3R7M0  5716-PW1
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A                                      WINDOW(*DFT 18 45)
     A                                      WDWBORDER((*DSPATR RI) (*CHAR '    -
     A                                          '))
     A            MSGPGM                    SFLPGMQ(10)
     A*
     A*---------------------------------------------------------------
     A* ASSUME FORMAT -- REQUIRED FOR WINDOWS
     A*---------------------------------------------------------------
     A          R ASSUME
     A*%%TS  SD  20011025  160559  ISLRAMS     REL-V4R4M0  5769-PW1
     A                                      ASSUME
     A                                      OVERLAY
     A                                 24 79' '
