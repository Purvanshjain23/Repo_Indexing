     A*%%TS  SD  20020416  111807  ISLRAMS     REL-V5R1M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD4079
     A*  TITLE:         SPECIFY OPTIONS FOR ROOM INVENTORY LISTING
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  CREATED:       04/11/02
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A N99                                  CF04(04 'PROMPT')
     A  44N99                               CF08(08 'SUBMIT')
     A N99                                  CF17(17 'PRINT OPTIONS')
     A N99                                  CF20(20 'WRKSPLF')
     A*
     A*---------------------------------------------------------------
     A* RECORD FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP4079R1
     A*%%TS  SD  20020416  111807  ISLRAMS     REL-V5R1M0  5722-WDS
     A                                      SETOF(31)
     A                                      SETOF(32)
     A                                      SETOF(33)
     A                                      SETOF(34)
     A                                      SETOF(51)
     A                                      SETOF(52)
     A                                      SETOF(53)
     A                                      OVERLAY
     A                                      RTNCSRLOC(&RCD &FLD)
     A            RCD       R        H      REFFLD(@@RCD)
     A            FLD       R        H      REFFLD(@@FLD)
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 25'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USR)
     A                                      DSPATR(HI)
     A                                  2 17'SPECIFY OPTIONS FOR ROOM INVENTORY-
     A                                       LISTING'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@JOB)
     A                                      DSPATR(HI)
     A                                  4  2'Production phase ...'
     A N99                              4 23'?'
     A N99        D1PPCD    R        B  4 25REFFLD(HSRREF/@@PPCD *LIBL/HSPREF)
     A N31                                  DSPATR(HI)
     A  51
     AO 31                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A            D1PPDS    R        O  4 33REFFLD(PPREC/PPPPDS *LIBL/HSP011)
     A                                  4 65'(Blank for All)'
     A                                  5  2'Cell ...............'
     A N99        D1CELL    R        B  5 25REFFLD(HSRREF/@@CELL *LIBL/HSPREF)
     A                                      EDTCDE(M)
     A N34                                  DSPATR(HI)
     A  34                                  DSPATR(PC)
     A  34                                  DSPATR(RI)
     A                                  5 65'(Blank for All)'
     A                                  6  2'Farm site ..........'
     A N99                              6 23'?'
     A N99        D1FSCD    R        B  6 25REFFLD(@@FSCD)
     A                                      EDTCDE(M)
     A N32                                  DSPATR(HI)
     A  52
     AO 32                                  DSPATR(PC)
     A  32                                  DSPATR(RI)
     A            D1FSNM    R        O  6 33REFFLD(FSFSNM)
     A                                  6 65'(Blank for All)'
     A                                  7  2'Print empty rooms ..'
     A                                  7 23'?'
     A            D1EMPFL   R        B  7 25REFFLD(HSRREF/@@FLAG *LIBL/HSPREF)
     A N33                                  DSPATR(HI)
     A  53
     AO 33                                  DSPATR(PC)
     A  33                                  DSPATR(RI)
     A            D1EMPDS   R        O  7 33REFFLD(@@DS30)
     A*
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R HP4079K1
     A*%%TS  SD  19980601  163600  ISLRAMS     REL-V3R7M0  5716-PW1
     A  43                             21  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A N99                             23 11'F4=Prompt'
     A  44N99                          23 22'F8=Submit Listing'
     A N99                             23 41'F17=Print Options'
     A N99                             23 60'F20=Spool Files'
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *---------------------------------------------------------------
     A          R HP4079ES                  SFL SFLMSGRCD(24)
      *
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
     A*---------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*---------------------------------------------------------------
     A*
     A          R HP4079EC                  SFLCTL(HP4079ES)
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
