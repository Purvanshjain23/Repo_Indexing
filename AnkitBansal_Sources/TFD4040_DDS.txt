     A*%%TS  SD  20090417  140712  ISLRAMS     REL-V5R4M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        TRIUMPH FOODS
     A*  DISPLAY FILE:  TFD4040
     A*  TITLE:         SPECIFY CASH DISTRIBUTION BALANCE ADJUSTMENTS LISTING
     A*  PROGRAMMER:    LEANNE RAMSEY
     A*  DATE:          11/06/06
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/TFPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CF03(03 'EXIT')
     A                                      CF04(04 'PROMPT')
     A                                      CF08(08 'SUBMIT')
     A                                      CF17(17 'PRINT OPTIONS')
     A                                      CF20(20 'SPOOL FILES')
     A*
     A*----------------------------------------------------------------
     A* RECORD FORMAT ONE
     A*----------------------------------------------------------------
     A          R TF4040R1
     A*%%TS  SD  20090417  140712  ISLRAMS     REL-V5R4M0  5722-WDS
     A                                      WINDOW(TF4040EC)
     A                                      TEXT('INPUT SCREEN')
     A                                      SETOF(30)
     A                                      SETOF(31)
     A                                      SETOF(32)
     A                                      SETOF(33)
     A                                      SETOF(52)
     A                                      OVERLAY
     A                                      RTNCSRLOC(&RCD &FLD)
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  1REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 21'SPECIFY CASH DISTRIBUTION BALANCE'
     A                                      DSPATR(HI)
     A                                  2 24'ADJUSTMENTS LISTING OPTIONS'
     A                                      DSPATR(HI)
     A                                  4  1'Extract on W=WeekEnding Date or A=-
     A                                      Adjustment Date ..'
     A            D1DTFL         1A  B  4 54
     A  33                                  DSPATR(PC)
     A  33                                  DSPATR(RI)
     A N33                                  DSPATR(HI)
     A  87                              6 37'WeekEnding Date'
     A                                      DSPATR(UL)
     A                                      COLOR(BLU)
     A                                  7  1'From date ...........'
     A            D1FMDY    R        B  7 25REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A  30                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A N30                                  DSPATR(HI)
     A  87        D1FWEMDY  R        O  7 39REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A                                      COLOR(BLU)
     A                                  7 53'(Blank for all)'
     A                                  8  1'To date .............'
     A            D1TMDY    R        B  8 25REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A  31                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A N31                                  DSPATR(HI)
     A  87        D1TWEMDY  R        O  8 39REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A                                      COLOR(BLU)
     A                                  8 53'(Blank for all)'
     A                                 10  1'Record exempt flag ..'
     A                                 10 23'?'
     A            D1RCEXFL  R        B 10 25REFFLD(TFREF/@@RCEXFL *LIBL/TFPREF)
     A  32
     AO 52                                  DSPATR(PC)
     A  32                                  DSPATR(RI)
     A N32                                  DSPATR(HI)
     A                                 10 53'(Blank for all)'
     A*
     A*----------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*----------------------------------------------------------------
     A          R TF4040K1
     A*%%TS  SD  20090417  111146  ISLRAMS     REL-V5R4M0  5722-WDS
     A                                      WINDOW(TF4040EC)
     A                                      TEXT('COMMAND KEYS')
     A                                      OVERLAY
     A  43                             11  1'Message program failed. Call IS.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 12  1'F3=Exit'
     A                                 12  9'F4=Prompt'
     A                                 12 19'F8=Submit'
     A                                 12 30'F17=Print Options'
     A                                 12 48'F20=Spool Files'
      *
      *----------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *----------------------------------------------------------------
     A          R TF4040ES
     A                                      TEXT('Error message subfile')
     A                                      SFL SFLMSGRCD(13)
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
     A*
     A*----------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT WITH WINDOW DEFINITION
     A*----------------------------------------------------------------
     A          R TF4040EC                  SFLCTL(TF4040ES)
     A*%%TS  SD  20000628  103649  ISLRAMS     REL-V4R2M0  5769-PW1
     A                                      TEXT('Error message subfile')
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A                                      WINDOW(*DFT 14 70)
     A                                      WDWBORDER((*DSPATR RI) (*CHAR '    -
     A                                          '))
     A            MSGPGM                    SFLPGMQ(10)
     A*
     A*----------------------------------------------------------------
     A* ASSUME FORMAT - REQUIRED FOR WINDOWS
     A*----------------------------------------------------------------
     A*
     A          R ASSUME
     A*%%TS  SD  20000627  095625  ISLRAMS     REL-V4R2M0  5769-PW1
     A                                      TEXT('ASSUME FORMAT FOR WINDOWS')
     A                                      ASSUME
     A                                      OVERLAY
     A                                 24 79' '
