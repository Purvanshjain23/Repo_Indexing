/***************************************************************** */
/*                                                                 */
/*  SYSTEM:          TRIUMPH FOODS                                 */
/*  PROGRAM NUMBER:  TF422CL                                       */
/*  PROGRAM NAME:    BYPRODUCT ADJUSTMENT REPORTS                  */
/*  PROGRAMMER:      LEANNE RAMSEY                                 */
/*  CREATE DATE:     05/01/07                                      */
/*                                                                 */
/*******************************************************************/
/* MODIFICATIONS:                                                  */
/*                                                                 */
/* 05/07/09  LEANNE RAMSEY                                         */
/*           CHANGED PRINT LOGIC TO MATCH MEAT COSTING.            */
/*******************************************************************/

             PGM

             DCL        VAR(&QDATA) TYPE(*CHAR) LEN(50)

             DCL        VAR(&OUTQ) TYPE(*CHAR) LEN(10)
             DCL        VAR(&HOLD) TYPE(*CHAR) LEN(4)
             DCL        VAR(&SAVE) TYPE(*CHAR) LEN(4)
             DCL        VAR(&COPY) TYPE(*DEC)  LEN(1)

             DCL        VAR(&LDWEDT)   TYPE(*DEC) LEN(8)
             DCL        VAR(&LDRPT)    TYPE(*CHAR) LEN(1)
             DCL        VAR(&LDTFCLCD) TYPE(*CHAR) LEN(3)

             CHGVAR     VAR(&LDWEDT)   VALUE(%SST(*LDA  29 8))
             CHGVAR     VAR(&LDRPT)    VALUE(%SST(*LDA 113 1))
             CHGVAR     VAR(&LDTFCLCD) VALUE(%SST(*LDA 156 3))

/*----------------------------------------------------------------*/
/* SET PARAMETERS FROM LDA FOR LISTINGS                           */
/*----------------------------------------------------------------*/

 OUTQ:       CHGVAR     VAR(&OUTQ) VALUE(%SST(*LDA 401 10))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(*JOB))

 HOLD:       CHGVAR     VAR(&HOLD) VALUE(%SST(*LDA 411 4))
             IF         COND(&HOLD *NE '*YES' *AND &HOLD *NE '*NO ') +
                          THEN(CHGVAR VAR(&HOLD) VALUE(*NO))

 COPY:       CHGVAR     VAR(&COPY) VALUE(%SST(*LDA 419 1))
             IF         COND(&COPY = 0) THEN(CHGVAR VAR(&COPY) +
                          VALUE(1))

 SAVE:       CHGVAR     VAR(&SAVE) VALUE(%SST(*LDA 415 4))
             IF         COND(&SAVE *NE '*YES' *AND &SAVE *NE '*NO ') +
                          THEN(CHGVAR VAR(&SAVE) VALUE(*NO))

             OVRPRTF    FILE(PRINT198) OUTQ(&OUTQ) COPIES(&COPY) +
                          HOLD(&HOLD) SAVE(&SAVE)

/*---------------------------------------------------------------------*/
/* BYPRODUCT MIX                                                       */
/*---------------------------------------------------------------------*/

             IF         COND(&LDRPT *EQ 'M' *OR &LDRPT *EQ 'B') THEN(DO)

/* Extract data for MIX                                                */

             CHGVAR     VAR(&QDATA) VALUE(' ')
             CHGVAR     VAR(%SST(&QDATA 1 11)) VALUE('YDWEDT *EQ ')
             CHGVAR     VAR(%SST(&QDATA 12 8)) VALUE(&LDWEDT)

/* If TF Classification code                                        */

             IF         COND(&LDTFCLCD *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 21 19)) VALUE('*AND YDTFCLCD *EQ "')
             CHGVAR     VAR(%SST(&QDATA 40 3)) VALUE(&LDTFCLCD)
             CHGVAR     VAR(%SST(&QDATA 43 1)) VALUE('"')
             ENDDO

             OVRDBF     FILE(ROL122G) SHARE(*YES)
             OPNQRYF    FILE((ROL122G)) OPTION(*INP) QRYSLT(&QDATA) +
                          KEYFLD(*FILE) SEQONLY(*YES)

/* Print Mix Report                                                     */

             CALL       PGM(TF622)

             CLOF       OPNID(ROL122G)
             DLTOVR     FILE(ROL122G)
             ENDDO

/*---------------------------------------------------------------------*/
/* BYPRODUCT VOLUME                                                    */
/*---------------------------------------------------------------------*/

             IF         COND(&LDRPT *EQ 'V' *OR &LDRPT *EQ 'B') THEN(DO)
             CALL       PGM(TF618)
             ENDDO

             DLTOVR     FILE(*ALL)

             ENDPGM

