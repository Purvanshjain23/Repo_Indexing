      *****************  RPG PROGRAM HEADING  ***********************
      *
      * SYSTEM:      HOG PRODUCTION
      * PROGRAM:     HP157
      * TITLE:       MAINTAIN GROUP DEATHS
      * PROGRAMMER:  LEANNE FEDOR
      * CREATED:     05/31/95
      *
      * FUNCTION: THIS PROGRAM USES A SUBFILE TO MAINTAIN DEATHS OF
      *           HOGS BY GROUPS FOR A FARM SITE.
      *
      *           THE PROGRAM WILL CREATE, DELETE AND UPDATE RECORDS
      *
      *           THIS PROGRAM IS CALLED FROM HP440-WORK WITH FARMS.
      *
      *           RESTRICTORS:   FARM SITE
      *           POSITIONERS:   DEAD DATE
      *           SELECTORS:     MOVEMENT NUMBER
      *                          HOG GROUP
      *                          DESTROY FLAG
      *                          DEATH REASON CODE
      *
      *           IT DOES HAVE F4-PROMPT LOGIC.
      *           IT LOADS THE SUBFILE A PAGE AT A TIME.
      *           IT USES THE STANDARD MESSAGE HANDLING PROGRAM.
      *           IT HAS STANDARD SUBFILE OPTION EDITING.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 09/26/95  LEANNE FEDOR
      *           CHANGED SCREEN LAYOUT. DESTROY YES/NO FIELD WILL NOW
      *           BE THE LAST INPUT FIELD ON THE ROW. IF THE USER DOES
      *           NOT ENTER A Y OR N, THE PROGRAM WILL DEFAULT AN 'N'.
      *
      * 10/14/95  LEANNE FEDOR
      *           CHANGED LOGIC TO ALLOW CHANGES TO AN EXISTING
      *           MOVEMENT WHEN THE GROUP STATUS IS 'CR' AS WELL AS
      *           'OP'. THIS LOGIC WAS REQUIRED TO ADDRESS THE
      *           SCENARIO WHERE:
      *            1) USER CREATED A GROUP
      *            2) USER OPEN A GROUP BY TRANSFERING PIGS INTO THE GROUP
      *            3) USERS CREATED A DAILY DEAD MOVEMENT
      *            4) USERS DECIDED GROUP WAS CREATED ACCIDENTALLY AND
      *               WANTED TO GET RID OF GROUP
      *            5) USERS DELETED THE TRANSFER MOVEMENT IN THUS RESETTING
      *               THE GROUP STATUS TO 'CR'EATE.
      *            6) THIS PROGRAM WOULD NOT LET THE USER'S DELETE THE
      *               DAILY DEAD MOVEMENT BECAUSE THE GROUP STATUS WAS NOT
      *               'OPEN'
      *
      * 11/02/99  LeAnne Fedor
      *           Reworked.  1) Building/room were added to subfile line with logic
      *           that attempts to default in a group or vice/versa a building/room
      *           depending on which values the user keyed. 2) Moved date to control
      *           record when in CREATE mode so that the user did not have to key
      *           a date on each subfile record.  3) We now allow 'reason code' to be
      *           an optional entry.  4) Removed reason code description from the
      *           subfile to make room for building/room.
      *
      * 11/11/99  LeAnne Fedor
      *           Changed the function to only create/update when user takes F9=accept.
      *           This allows user to view defaulted info prior to creating movements.
      *           To be consistent with other programs, the 'toggle' from create to
      *           revise mode will now be F10.
      *
      * 10/16/00  LeAnne Fedor
      *           When working with Daily Deads for the Datamart, I discovered that the
      *           'destroy flag' value in the file was not being populated with the
      *           subfile value. I changed the code to populate.
      *
      *           Also, in anticipation of Anne Marie Dushay's launching of her new
      *           crusade to gather death info by date and reason code, we have changed
      *           the entry logic. Now, in CREATE mode, we dup the building/room/group
      *           from the previous subfile line entry when all three are left blank by
      *           the user.
      *
      * 01/25/01  LeAnne Fedor
      *           Added a function key to go to the screen/program to maintain farm deaths
      *           with reasons (a new file required by Anne Marie Dushay).
      *
      * 07/17/02  LeAnne Fedor
      *           Added production phase as a parm on the calls to the group
      *           select programs.
      *
      * 12/01/02  LeAnne Fedor
      *           Removed Production Phase and Production Type from the Killed/Dead file.
      *
      * 01/16/06  LeAnne Fedor
      *           Recompile only.
      *           We have removed "Bin Set" from HSP020-Building/Rooms and put it in our new
      *           HSP113-Rooms/Bin Sets file.
      *
      * 04/07/09  LeAnne Ramsey
      *           File HSP088-Farm Deaths with Reasons is being deleted. So,
      *           we removed: 7=Revise Farm Reasons.
      *
      * 04/20/09  LeAnne Ramsey
      *           Removed call to HP1057; we no longer have the Farm Deaths Reasons file.
      *
      * 07/02/09  LeAnne Ramsey
      *           Recompile only. Added new field 'Continuous Flow Flag' to Hog Group file.
      *
      * 10/15/13  LeAnne Ramsey (E2831)
      *           Recompile only. Added field 'MTech Reference'.
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     FHPD157    CF   E             WORKSTN
     F                                     SFILE(HP157S1:HDRRN1)
     F                                     INFDS(IOFEED)
      *
     FHSP018    IF   E           K DISK
      *    FARM SITE
      *
      *
     Fhsp019    if   e           k disk
      *    Farm buildings
      *
      *
     Fhsp020    if   e           k disk
      *    Building rooms
      *
      *
     FHSP034    IF   E           K DISK
      *    HOG GROUP
      *
      *
     Fhsl034p   if   e           k disk
     F                                     rename(hgrec:hgrecp)
      *  Hog group
      *
      *
     FHSP041    IF   E           K DISK
      *  DEATH/DISEASE REASON
      *
      *
     FHSP053    IF   E           K DISK
      *    MOVEMENT STATUS CODES
      *
      *
     FHSP058    UF A E           K DISK
      *  MOVEMENT EVENT
      *
      *
     FHSJ068A   IF   E           K DISK
     F                                     RENAME(KDREC:KDRECA)
     F                                     INFDS(DBFEED)
      *  KILLED/DEAD HOGS + HOG GROUP
      *
      *
     FHSP068    UF A E           K DISK
      *  KILLED/DEAD HOGS
      *
      /EJECT
      ****************************************************************
      * INPUT SPECIFICATIONS
      ****************************************************************
      *
      *---------------------------------------------------------------
      * STANDARD ERROR MESSAGE HANDLING DATA STRUCTURES
      *---------------------------------------------------------------
      *
      * FOR ERROR MESSAGE HANDLING, A PACKED INDEX (E) IS REQUIRED.
      * FOR PROGRAM READABILITY, DEFINE A CORRESPONDING ERROR COUNT
      * FIELD CALLED 'ERROR'. ALSO DEFINE CURSOR LOCATION FIELDS.
      *
     D                 DS                  INZ
     D  ERROR                  1      2P 0
     D  E                      1      2P 0
      *
      * THIS DATA STRUCTURE SUPPLIES THE NAME OF THE MESSAGE FILE TO
      * THE MESSAGE HANDLING CL PROGRAM.  THE FIELD NAME MSGFIL MUST BE
      * CONSTANT. THE VALUE IN QUOTES IS THE NAME OF THE SPECIFIC
      * MESSAGE FILE CONTAINING THE USER DEFINED MESSAGES.
      *
     D                 DS                  INZ
     D  MSGFIL                 1     10    INZ('HSMSGF    ')
      *
      * THE FOLLOWING 3 DATA STRUCTURES ARE USED TO SPEED MESSAGE
      * HANDLING SINCE IT IS FASTER TO CLEAR DATA STRUCTURES THAN
      * ARRAYS.  EACH IS ASSOCIATED WITH A STANDARD MESSAGE ARRAY.
      *
     D MGI             DS           140    INZ
     D  MGID                   1    140
     D                                     DIM(20)                              MSG ID ARRAY
      *
     D MGD             DS          1000    INZ
     D  MGDT                   1   1000
     D                                     DIM(20)                              MSG PARMS
      *
     D MGWK            DS            50    INZ
     D  MGW                    1     50
     D                                     DIM(50)                              MSG WORK PARMS
      *
      *---------------------------------------------------------------
      * STANDARD PROGRAM STATUS DATA STRUCTURE
      *---------------------------------------------------------------
      *    EXTERNALLY DEFINED AS UTPGFR (RECORD FORMAT: PGMDSR)
     D PGMDS         ESDS                  EXTNAME(UTPGFR)
      *
      *---------------------------------------------------------------
      * STANDARD WORKSTATION INFORMATION DATA STRUCTURE
      *---------------------------------------------------------------
      *    EXTERNALLY DEFINED AS UTWSFR (RECORD FORMAT: UTIDFRR)
     D IOFEED        E DS                  EXTNAME(UTWSFR)
      *
      *---------------------------------------------------------------
      * STANDARD DATABASE FILE INFORMATION DATA STRUCTURE
      *---------------------------------------------------------------
      *    EXTERNALLY DEFINED AS UTDBFR (RECORD FORMAT: FDBCKD)
     D DBFEED        E DS                  EXTNAME(UTDBFR)
      *
      *---------------------------------------------------------------
      * STANDARD SUBFILE OPTION CONVERSION
      *---------------------------------------------------------------
     D                 DS
     D  DFOPT                  1      2
     D  WKOPT1                 1      1
     D  WKOPT2                 2      2
      *
      *---------------------------------------------------------------
      *  DATE FORMATS
      *---------------------------------------------------------------
      *
      * DATE IN MMDDYY FORMAT
      *
     D                 DS
     D  WKMDY                  1      6  0
     D  WKMM                   1      2  0
     D  WKDD                   3      4  0
     D  WKYY                   5      6  0
      *
      * DATE IN CCYYMMDD FORMAT
      *
     D                 DS
     D  WKCYMD                 1      8  0
     D  WKCEN                  1      2  0
     D  WKYER                  3      4  0
     D  WKMON                  5      6  0
     D  WKDAY                  7      8  0
      *
      *---------------------------------------------------------------
      *  DATA AREA FOR NEXT MOVEMENT NUMBER
      *---------------------------------------------------------------
      *
     D                 DS
     D  DAMVSN                 1      7  0
      *
      *---------------------------------------------------------------
      *  DEFINITION FOR ACCOUNTING PERIOD DATA AREA
      *---------------------------------------------------------------
     D                 DS
     D  DAAPER                 1     24
     D  DACCYY                 1      4
     D  DAPER                  5      6
     D  DABPDT                 7     14  0
     D  DAEPDT                15     22  0
     D  DAPGFL                23     23
     D  DAPPFL                24     24
      *
      *---------------------------------------------------------------
      *  NAMED CONSTANTS
      *---------------------------------------------------------------
      *
     D CREATE          C                   CONST('CREATE')
     D REVISE          C                   CONST('REVISE')
     D DELETE          C                   CONST('DELETE')
     D VIEW            C                   CONST('VIEW  ')
     D SELECT          C                   CONST('SELECT')
     D YES             C                   CONST('Y')
     D NO              C                   CONST('N')
     D SET1            C                   CONST('SET1  ')
     D EDIT1           C                   CONST('EDIT1 ')
     D SCRN1           C                   CONST('SCRN1 ')
     D EXIT            C                   CONST('EXIT  ')
     D UPDATE          C                   CONST('UPDATE')
      *
      *
      * Workfields for date manipulation
      *
     D wkmmddyy        s               D   datfmt(*mdy)
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * MAINLINE
      ****************************************************************
      *
     C                   MOVEL     SET1          ROUTNE
      *
     C     *INLR         DOWEQ     *OFF                                         MAIN DO LOOP
      *
     C     ROUTNE        CASEQ     SET1          $SET1
     C     ROUTNE        CASEQ     SCRN1         $SCRN1
     C     ROUTNE        CASEQ     EDIT1         $EDIT1
      *
     C     ROUTNE        CASEQ     EXIT          $EXIT
     C                   ENDCS
     C                   ENDDO                                                  MAIN DO LOOP
      /EJECT
      *----------------------------------------------------------------
      * $SET1 - SUBROUTINE TO SET ENVIRONMENT FOR SCREEN 1
      *----------------------------------------------------------------
      *
     C     $SET1         BEGSR
      *
      * BRING IN THE DATA AREA THAT CONTAINS THE CURRENT ACCOUNTING
      * PERIOD FOR EDITING AGAINST THE KILLED/DEAD DATE. (WE BRING
      * THIS IN IN THIS SUBROUTINE BECAUSE THE EDITS CHECK TO SEE IF
      * THE PERIOD END PROGRAMS ARE CURRENTLY RUNNING.)
      *
     C                   IN        DAAPER
      *
      * VERIFY THAT THE FARM SITE THAT THE USER SELECTED STILL EXISTS.
      *
     C     C1FSCD        CHAIN     HSP018                             90
     C     *IN90         IFEQ      *ON                                          IF NOT FOUND
     C                   MOVEL     YES           NOTFLG
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS00121'     MGID(E)
     C                   CLEAR                   MGW
     C                   MOVE      C1FSCD        XXALPHFSCD
     C                   MOVEA     XXALPHFSCD    MGW(1)
     C                   MOVEA     MGW           MGDT(E)
     C                   ENDIF
     C                   ENDIF                                                  IF NOT FOUND
      *
      *  CLEAR/FILL THE SUBFILE
      *
     C                   EXSR      $CLR1
     C                   EXSR      $FRSH1
      *
      *  SET ROUTINE TO DISPLAY SCREEN
      *
     C                   MOVEL     SCRN1         ROUTNE
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $SCRN1 - SUBROUTINE TO PERFORM OPERATOR I/O FOR SCREEN 1
      *----------------------------------------------------------------
      *
     C     $SCRN1        BEGSR
      *
      * RESET INDICATORS IN CONTROL RECORD
      *
     C                   EXSR      $C1IND
      *
      * WRITE THE ERROR MESSAGES FROM THE ERROR ARRAYS TO THE
      * ERROR MESSAGE SUBFILE
      *
     C                   EXSR      $WRMSG
      *
      *  WRITE SCREEN 1 TO CRT
      *
     C                   WRITE     HP157K1
     C                   WRITE     HP157EC
     C                   EXFMT     HP157C1
      *
      * SET THE 'RESET' FLAG TO NO.  THIS FLAG IS USED TO DETERMINE
      * WHETHER TO REPOSITION THE USER TO A PARTICULAR PLACE IN THE
      * SUBFILE (RESET=YES) OR TO PLACE THE USER AT THE BEGINNNING OF
      * THE SUBFILE (RESET=NO)
      *
     C                   MOVEL     NO            RESET
      *
      * RETRIEVE THE LOWEST SUBFILE RRN ON THE SCREEN FROM THE DSPF
      * FEEDBACK AREA (IDFLOW) AND MOVE IT TO A WORK FIELD.  THIS VALUE
      * WILL BE USED LATER TO CONTROL THE REPOSITIONING OF THE SUBFILE.
      *
     C                   Z-ADD     IDFLOW        WKFLOW
      *
      * CLEAR ALL MESSAGES
      *
     C                   EXSR      $CLMSG
      *
      * GET USER'S ENTRY AND RESET STANDARD RESPONSE
      *
     C                   SELECT
     C     *IN03         WHENEQ    *ON                                          F3-EXIT
     C                   MOVEL     '03'          RTNCD
     C                   MOVEL     EXIT          ROUTNE
      *
     C     *IN04         WHENEQ    *ON
     C                   EXSR      $F4S1
      *
      * USER PROMPTED IN CONTROL RECORD SO CHECK TO SEE IF YOU
      * NEED TO REFILL THE SUBFILE TO MATCH HIS NEW SELECTORS.
      *
     C     FLD           IFEQ      'C1HGCD'
     C     FLD           OREQ      'C1BLCD'
     C     FLD           OREQ      'C1RMCD'
     C     FLD           OREQ      'C1DSFL'
     C     FLD           OREQ      'C1DRCD'
     C     FLD           OREQ      'C1MSCD'
     C                   EXSR      $CHKSV
     C                   ELSE
      *
      * USER PROMPTED IN SUBFILE, JUST REDISPLAY SCREEN.
      *
     C                   MOVEL     SCRN1         ROUTNE
     C                   ENDIF
      *
     C     *IN05         WHENEQ    *ON                                          F5-REFRESH
     C     MODE          IFEQ      REVISE
     C                   MOVEL     YES           RESET
     C                   ENDIF
     C                   MOVEL     SET1          ROUTNE
      *
     C     *IN09         WHENEQ    *ON                                          F9-ACCEPT
     C                   exsr      $edit1
     C                   if        error = 0                                    If no error
     C                   exsr      $proc1
      *
     C                   if        mode = revise and
     C                             error < maxmsg and
     C                             entryfl = yes
     C                   add       1             error
     C                   movel     'HS09046'     mgid(e)
     C                   movel     set1          routne
     C                   endif
     C                   endif                                                  If no error
      *
     C     *IN10         WHENEQ    *ON                                          F10-TOGGLE
     C     MODE          IFEQ      CREATE
     C                   MOVEL     REVISE        MODE
     C                   MOVEL     REVISE        DFMODE
     C                   ELSE
     C                   MOVEL     CREATE        MODE
     C                   MOVEL     CREATE        DFMODE
     C                   ENDIF
     C                   MOVEL     SET1          ROUTNE
      *
     C     *IN12         WHENEQ    *ON                                          F12-PREVIOUS
     C                   MOVEL     '12'          RTNCD
     C                   MOVEL     EXIT          ROUTNE
      *
      *
      * USER HAS CHANGED A POSITIONER OR SELECTOR IN THE
      * CONTROL RECORD
      *
     C     SVMDY         WHENNE    C1MDY
     C     SVBLCD        ORNE      C1BLCD
     C     SVRMCD        ORNE      C1RMCD
     C     SVHGCD        ORNE      C1HGCD
     C     SVDSFL        ORNE      C1DSFL
     C     SVDRCD        ORNE      C1DRCD
     C     SVMSCD        ORNE      C1MSCD
     C     SVMVSN        ORNE      C1MVSN
     C                   EXSR      $CHKSV
      *
     C     *IN25         WHENEQ    *ON                                          F25-ROLLUP
     C                   EXSR      $LOAD1
     C                   MOVEL     SCRN1         ROUTNE
     C                   OTHER
      *
      * USER PRESSED 'ENTER'
      *
     C                   if        mode = revise and hdrrn1 = 0                 IF NOTHING
     C                   else
     C                   exsr      $edit1
      *
      * If a) there were no errors and b) there were subfile entries, issue
      * a message telling the user to accept his changes.
      *
     C                   if        error = 0 and entryfl = yes
     C                   z-add     1             error
     C                   movel     'HS09026'     mgid(e)
     C                   endif
     C                   ENDIF                                                  IF NOTHING
      *
     C                   movel     scrn1         routne
      *
     C                   ENDSL
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $CHKSV - CHECK TO SEE IF CONTROL RECORD FIELDS ARE CHANGED
      *----------------------------------------------------------------
      *
     C     $CHKSV        BEGSR
      *
      * IF THE USER HAS ENTERED A DIFFERENT DEAD DATE,
      * FLIP THE DEAD DATE THE USER ENTERED AS A POSITIONER
      * FROM MMDDYY TO CCYYMMDD FOR KEYING PURPOSES
      *
     C     C1MDY         IFNE      SVMDY                                        IF DIFF DATE
     C                   Z-ADD     0             WKKDDT
     C     C1MDY         IFNE      0                                            If a date
     C                   Z-ADD     C1MDY         PFRM8
     C                   EXSR      $DATE
     C     PRTRN         IFEQ      *BLANK                                       If good date
     C                   Z-ADD     PTO8          WKKDDT
     C                   ENDIF                                                  If good date
     C                   ENDIF                                                  If a date
     C                   ENDIF                                                  IF DIFF DATE
      *
     C                   MOVEL     NO            RESET
     C                   MOVEL     SET1          ROUTNE
     C                   Z-ADD     C1MDY         SVMDY
     C                   MOVEL     C1BLCD        SVBLCD
     C                   MOVEL     C1RMCD        SVRMCD
     C                   MOVEL     C1HGCD        SVHGCD
     C                   MOVEL     C1DSFL        SVDSFL
     C                   MOVEL     C1DRCD        SVDRCD
     C                   MOVEL     C1MSCD        SVMSCD
     C                   Z-ADD     C1MVSN        SVMVSN
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $EDIT1 - SUBROUTINE TO EDIT INPUT FIELDS FOR SCREEN 1
      *----------------------------------------------------------------
      *
     C     $EDIT1        BEGSR
      *
      * SET OFF ANY PROMPT INDICATORS THAT MAY STILL BE ON
      *
     C                   EXSR      $F4OFF
     C                   move      no            entryfl
      *
      * In REVISE mode, edit subfile options.
      * In CREATE mode, edit killed dead date in control record.
      *
     C                   select
     C                   when      mode = revise
     C                   exsr      $edop1
     C                   when      mode = create
     C                   exsr      $c1kddtmdy
     C                   endsl
      *
      * If there are no errors in the control record or subfile option,
      * edit the subfile records.
      *
     C     ERROR         IFEQ      0
     C                   EXSR      $EDS1
     C                   ENDIF
      *
     C                   movel     scrn1         routne
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * $CLR1  - SUBROUTINE TO CLEAR THE SUBFILE ON SCREEN 1
      *---------------------------------------------------------------
      *
     C     $CLR1         BEGSR
      *
     C                   Z-ADD     0             SVRRN1
      *
     C                   CLEAR                   HP157S1
     C                   SETON                                        83        SFLCLR
     C                   WRITE     HP157C1
     C                   SETOFF                                       818283
      *                                                    81=SFLDSP
      *                                                    82=SFLEND
      *                                                    83=SFLCLR
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * $FRSH1 - SUBROUTINE TO REFRESH/FILL THE SUBFILE FOR SCREEN 1
      *---------------------------------------------------------------
      *
     C     $FRSH1        BEGSR
      *
      *
      * SET INDICATORS TO CONTROL DSPF DISPLAY
      *
     C     MODE          IFEQ      CREATE
     C                   SETOFF                                       60
     C                   SETON                                        61
     C                   Z-ADD     0             C1MDY
     C                   MOVE      *BLANK        C1HGCD
     C                   MOVE      *BLANK        C1DSFL
     C                   MOVE      *BLANK        C1DRCD
     C                   Z-ADD     0             C1MVSN
     C                   Z-ADD     0             SVMDY
     C                   MOVE      *BLANK        SVBLCD
     C                   MOVE      *BLANK        SVRMCD
     C                   MOVE      *BLANK        SVHGCD
     C                   MOVE      *BLANK        SVDSFL
     C                   MOVE      *BLANK        SVDRCD
     C                   MOVE      *BLANK        SVMSCD
     C                   Z-ADD     0             SVMVSN
     C                   ELSE
      * REVISE MODE
     C                   SETOFF                                       61
     C                   SETON                                        60
     C                   ENDIF
      *
      * POSITION ON FARM SITE AND DEAD DATE
      *
     C     C1MDY         IFEQ      0
     C     C1FSCD        SETLL     HSJ068A
     C                   ELSE
     C     key03         SETLL     HSJ068A
     C                   ENDIF
      *
      * THE FIELD 'RESET' IS A FLAG FIELD TO INDICATE WHETHER YOU ARE
      * ATTEMPTING TO REPOSITION THE USER CLOSE TO WHERE HE WAS IN THE
      * SUBFILE.
      * THE LOWEST SUBFILE RRN ON THE SCREEN FROM THE DSPF
      * FEEDBACK AREA (IDFLOW) WAS RETRIEVED EARLIER AND PUT IN WKFLOW.
      * ADD TO WKFLOW THE NUMBER OF LINES ON YOUR SUBFILE PAGE OR THE
      * NUMBER LESS 1 TO DETERMINE A SUBFILE RRN TO REPOSITION TO.
      * IF RESET IS 'NO', DON'T REPOSITION THE USER; JUST START OVER
      *
     C     RESET         IFEQ      YES
     C     WKFLOW        ADD       WKRLIN        MAXRRN
     C                   ELSE
     C                   Z-ADD     WKPLIN        MAXRRN
     C                   ENDIF
      *
      * LOAD THE SUBFILE UNTIL YOU HAVE THE REQUIRED NUMBER OF RECORDS
      * BY COMPARING THE HIDDEN RRN IN THE SUBFILE TO DESIRED MAXIMUM.
      * THE MAXIMUM WILL DEPEND ON WHETHER YOU ARE REPOSITIONING THE
      * SUBFILE OR NOT. STOP IF YOU GET TO THE END OF THE SUBFILE.
      *
     C     SVRRN1        DOUGE     MAXRRN
     C     *IN82         OREQ      *ON
     C                   EXSR      $LOAD1
     C                   ENDDO
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * $LOAD1- SUBROUTINE TO LOAD THE SUBFILE FOR SCREEN 1
      *---------------------------------------------------------------
      *
     C     $LOAD1        BEGSR
      *
     C                   Z-ADD     SVRRN1        HDRRN1
      *
      * SET THE COUNT OF RECORDS ON THE SUBFILE PAGE TO ZERO.
      * COUNT IS USED TO COUNT THE RECORDS WRITTEN TO THE SUBFILE.
      *
     C                   Z-ADD     0             COUNT
      *
      * FILL THE SUBFILE UNTIL THE END OF THE DATABASE FILE OR YOU
      * HAVE FILLED ALL THE LINES ON THE SUBFILE PAGE
      *
     C                   SETOFF                                       82
     C     *IN82         DOUEQ     *ON                                          DO FILL LOOP
     C     COUNT         ORGE      WKPLIN
      *
      * CLEAR SUBFILE FIELDS
      *
     C                   Z-ADD     0             HDKDDT
     C                   Z-ADD     0             HDHGSN
     C                   MOVE      *BLANK        HDPFL
     C                   MOVE      *BLANK        HDNDFL
      *
     C                   Z-ADD     0             DFMDY
     C                   MOVE      *BLANK        DFBLCD
     C                   MOVE      *BLANK        DFRMCD
     C                   MOVE      *BLANK        DFHGCD
     C                   Z-ADD     0             DFKDHD
     C                   Z-ADD     0             DFKDLB
     C                   MOVE      *BLANK        DFDSFL
     C                   MOVE      *BLANK        DFDRCD
      *
      *
      * IN CREATE MODE, WRITE A BLANK SUBFILE RECORD
      *
     C                   SELECT
     C     MODE          WHENEQ    CREATE                                       WH CREATE
     C                   ADD       1             COUNT
     C                   ADD       1             HDRRN1
     C                   WRITE     HP157S1
     C                   SETON                                        81
     C                   Z-ADD     HDRRN1        HDPAG1
      *
      * IN REVISE MODE, READ DEAD HOG RECORDS FOR THE FARM SITE
      * ONLY INCLUDE RECORDS THAT MATCH THE USER'S SELECTORS
      *
     C     MODE          WHENEQ    REVISE                                       WH REVISE
     C     C1FSCD        READE     HSJ068A                                82
     C     *IN82         IFEQ      *OFF                                          IF FOUND
      *
     C     C1BLCD        IFNE      *BLANK                                        IF BUILDING
     C     C1BLCD        ANDNE     HGBLCD
     C                   ELSE
      *
     C     C1RMCD        IFNE      *BLANK                                        IF ROOM
     C     C1RMCD        ANDNE     HGRMCD
     C                   ELSE
      *
     C     C1HGCD        IFNE      *BLANK                                        IF GROUP
     C     C1HGCD        ANDNE     HGHGCD
     C                   ELSE
      *
     C     C1DSFL        IFNE      *BLANK                                        IF DESTROY
     C     C1DSFL        ANDNE     KDDSFL
     C                   ELSE
      *
     C     C1DRCD        IFNE      *BLANK                                        IF REASON
     C     C1DRCD        ANDNE     KDDRCD
     C                   ELSE
      *
     C     C1MSCD        IFNE      *BLANK                                        IF STATUS
     C     C1MSCD        ANDNE     KDMSCD
     C                   ELSE
      *
     C     C1MVSN        IFNE      0                                             IF MOVEMENT
     C     C1MVSN        ANDNE     KDMVSN
     C                   ELSE
      *
     C                   Z-ADD     KDHGSN        HDHGSN
     C                   Z-ADD     KDKDDT        HDKDDT
      *
     C                   MOVEL     HGHGCD        DFHGCD
     C                   MOVEL     HGBLCD        DFBLCD
     C                   MOVEL     HGRMCD        DFRMCD
     C                   Z-ADD     KDKDHD        DFKDHD
     C                   Z-ADD     KDKDLB        DFKDLB
     C                   MOVEL     KDDSFL        DFDSFL
     C                   MOVEL     KDDRCD        DFDRCD
     C                   MOVEL     KDMSCD        DFMSCD
     C                   Z-ADD     KDMVSN        DFMVSN
      *
      * FLIP DEAD DATE TO MMDDYY
      *
     C     *iso          move      kdkddt        wkmmddyy
     C                   move      wkmmddyy      dfmdy
      *
      * IF THE GROUP STATUS IS VOIDED, DISPOSED OR CLOSED SET THE
      * FLAG IN THE SUBFILE RECORD THAT WILL ULTIMATELY
      * PROTECT THE FIELDS FROM USER INPUT
      *
     C     HGGSCD        IFEQ      'OP'
     C     HGGSCD        OREQ      'CR'
     C                   ELSE
     C                   MOVEL     YES           HDPFL
     C                   ENDIF
      *
      * PROTECT ALL FIELDS IF THIS MOVEMENT HAS A POSTED STATUS
      *
     C     DFMSCD        CHAIN     HSP053                             91
     C     *IN91         IFEQ      *OFF
     C     MSPOFL        ANDEQ     YES
     C                   MOVEL     YES           HDPFL
     C                   ENDIF
      *
     C                   EXSR      $SETOF
     C                   EXSR      $SFIND
      *
     C                   ADD       1             COUNT
     C                   ADD       1             HDRRN1
     C                   WRITE     HP157S1
     C                   SETON                                        81
     C                   Z-ADD     HDRRN1        HDPAG1
     C                   ENDIF                                                   IF MOVEMENT
     C                   ENDIF                                                   IF STATUS
     C                   ENDIF                                                   IF REASON
     C                   ENDIF                                                   IF DESTROY
     C                   ENDIF                                                   IF GROUP
     C                   ENDIF                                                   IF ROOM
     C                   ENDIF                                                   IF BUILDING
     C                   ENDIF                                                   IF FOUND
     C                   ENDSL
      *
     C                   ENDDO                                                  DO FILL LOOP
      *
      * IF NO RECORDS WERE WRITTEN TO THE SUBFILE, THEN ISSUE
      * THE STANDARD ERROR MESSAGE OF NO DATA FOUND. ALSO, SET ON
      * THE INDICATOR THAT WILL KEEP THE POSITIONER/SELECTOR FIELDS
      * FROM DISPLAYING.
      *
     C     HDRRN1        IFEQ      0
     C     ERROR         ANDLT     MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS09002'     MGID(E)
     C                   ENDIF
      *
     C                   Z-ADD     HDRRN1        SVRRN1
      *
     C                   ENDSR
      /EJECT
      *------------------------------------------------------------------------
      * $C1KDDTMDY - VALIDATE KILLED/DEAD DATE IN CONTROL RECORD IN CREATE MODE
      *------------------------------------------------------------------------
      *
     C     $C1KDDTMDY    BEGSR
      *
     C                   Z-ADD     C1KDDTMDY     PFRM8
     C                   EXSR      $DATE
     C     PRTRN         IFNE      *BLANK                                       IF INVALID
     C                   SETON                                        32
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVE      'HS09004'     MGID(E)
     C                   ENDIF
     C                   ELSE
      *
      * IF THE DEAD DATE IS VALID,
      *     CHECK THAT IT IS NOT GREATER THAN TODAY'S DATE
      *     CHECK THAT IT IS NOT IN A CLOSED PERIOD
      *     IF IT IS IN THE CURRENT PERIOD,
      *         CHECK THAT THE PERIOD CLOSE PROGRAMS ARE NOT RUNNING
      *
     C                   SELECT
     C     PTO8          WHENGT    WKUDAT                                       WH FUTURE
     C                   SETON                                        32
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS00426'     MGID(E)
     C                   ENDIF
      *
     C     PTO8          WHENLT    DABPDT                                       WH CLOSED
     C                   SETON                                        32
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS09007'     MGID(E)
     C                   ENDIF
      *
     C     PTO8          WHENGE    DABPDT                                       WH RUNNING
     C     PTO8          ANDLE     DAEPDT
     C     DAPPFL        ANDNE     *BLANK
     C                   SETON                                        32
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS09016'     MGID(E)
     C                   ENDIF
     C                   ENDSL
     C                   ENDIF                                                  IF INVALID
      *
      * IF THERE ARE NO ERRORS ON THE DATE, MOVE IT TO THE
      * HIDDEN SUBFILE FIELD.
      *
     C     *IN32         IFEQ      *OFF
     C                   Z-ADD     PTO8          svkddt
     C                   ELSE
     C                   Z-ADD     0             svkddt
     C                   ENDIF
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * $EDOP1 - SUBROUTINE TO READ CHANGED RECORDS IN SUBFILE AND
      *          EDIT FOR VALID SUBFILE OPTIONS ON SCREEN 1
      *---------------------------------------------------------------
      *
     C     $EDOP1        BEGSR
      *
      * READ ALL THE CHANGED SUBFILE RECORDS EDITING FOR VALID SUBFILE
      * OPTIONS
      *
     C     *IN80         DOUEQ     *ON                                          DO READ C
     C                   READC     HP157S1                                80
     C     *IN80         IFEQ      *OFF                                         IF REC READ
     C     HDPFL         ANDNE     YES
     C     HDNDFL        ANDNE     YES
      *
      * SET OFF ERROR INDICATOR FOR SUBFILE OPTION
      *
     C                   SETON                                        85
     C                   EXSR      $SETOF
      *
      * CONVERT OPTION USER ENTERED TO STANDARD FORMAT. FOR EXAMPLE,
      * A ' 1', '01', AND '1 ' MUST BE STANDARDIZED TO ' 1'.
      *
     C     WKOPT2        IFEQ      *BLANK                                       IF 2 = BLANK
     C                   MOVE      WKOPT1        WKOPT2
     C                   MOVE      *BLANK        WKOPT1
     C                   ENDIF                                                  IF 2 = BLANK
      *
     C     WKOPT1        IFEQ      *ZEROS                                       IF 1 IS ZERO
     C                   MOVE      *BLANK        WKOPT1
     C                   ENDIF                                                  IF 1 IS ZERO
      *
     C                   if        dfopt <> *blank
     C                   move      yes           entryfl
     C                   endif
      *
      * TEST FOR VALID SUBFILE OPTION ENTRY ON ALL CHANGED RECORDS
      *
     C     DFOPT         IFNE      ' 4'                                         IF INVALID
     C     DFOPT         ANDNE     '  '
     C                   SETON                                        30
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS09006'     MGID(E)
     C                   ENDIF
     C                   EXSR      $ERR1
     C                   ENDIF                                                  IF INVALID
      *
     C                   EXSR      $SFIND
     C                   UPDATE    HP157S1
      *
     C                   ENDIF                                                  IF REC READ
     C                   ENDDO                                                  DO READ C
      * RESET MDT
     C                   SETOFF                                       85
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * $EDS1  - Edit Changed Subfile Records on Screen 1
      *---------------------------------------------------------------
      *
     C     $EDS1         BEGSR
      *
     C                   move      *blank        svsflblcd
     C                   move      *blank        svsflrmcd
     C                   move      *blank        svsflhgcd
      *
      * READ ALL THE CHANGED SUBFILE RECORDS PROCESSING THE SUBFILE
      * SELECTION.
      *
     C                   SETOFF                                       80
     C     *IN80         DOUEQ     *ON                                          DO PROCESS
     C                   READC     HP157S1                                80
     C     *IN80         IFEQ      *OFF                                         IF REC READ
      *
     C     HDPFL         IFNE      YES                                          IF NOT PROTECTED
     C     HDNDFL        ANDNE     YES
      *
      *
      * SET OFF ERROR INDICATORS
      *
     C                   EXSR      $SETOF
      *
      * You will not edit records to be deleted; so, set the modified
      * data tag on here so that they will be processed when the user
      * accepts his changes.
      *
     C                   if        dfopt = ' 4'
     C                   seton                                        85
     C                   else
     C                   setoff                                       85
     C                   endif
      *
      * PERFORM EDIT SUBROUTINE
      *
     C                   if        (mode = revise and dfopt <> ' 4') or         If edit
     C                             (mode = create and
     C                             (dfblcd <> *blank or
     C                              dfrmcd <> *blank or
     C                              dfhgcd <> *blank or
     C                              dfkdhd <> 0 or
     C                              dfkdlb <> 0 or
     C                              dfdsfl <> *blank or
     C                              dfdrcd <> *blank))
      *
      * SET ON MDT
     C                   SETON                                        85
      *
     C                   MOVE      *BLANK        HDPFL
     C                   MOVE      *BLANK        HDNDFL
     C                   move      yes           entryfl
      *
      *
      *  1) When REVISING, validate kill date
      *  2) When CREATING, populate bldg/room/group with the values from
      *     the previous subfile record...when bldg/room/group are blank.
      *
     C                   select
     C                   when      mode = revise
     C                   exsr      $dfkddt
      *
     C                   when      mode = create
     C                   if        dfblcd = *blank and dfrmcd = *blank and      If 3 blank
     C                             dfhgcd = *blank
     C                   move      svsflblcd     dfblcd
     C                   move      svsflrmcd     dfrmcd
     C                   move      svsflhgcd     dfhgcd
     C                   else
     C                   move      dfblcd        svsflblcd
     C                   move      dfrmcd        svsflrmcd
     C                   move      dfhgcd        svsflhgcd
     C                   endif                                                   If 3 blank
     C                   endsl
      *
      * The user must at least enter a building or a group. If neither are
      * entered, issue message; otherwise, validate and attempt to retrieve/
      * default in the missing values.
      *
     C                   if        dfblcd = *blank and
     C                             dfhgcd = *blank
     C                   seton                                        3338
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS09132'     mgid(e)
     C                   exsr      $err1
     C                   endif
     C                   else
      * Validate:
      *  1) Building
      *  2) Room
      *  3) Hog group
      *
     C                   exsr      $blcd
      *
      * Edit room if a) it is not blank and b) it was not edited previously
      * in the logic that attempted to default a group for a building.
      * (The check on indicator 39 keeps you from issuing a second invalid
      * room message.)
     C                   if        dfrmcd <> *blank and *in39 = *off
     C                   exsr      $rmcd
     C                   endif
      *
     C                   exsr      $hgcd
     C                   endif                                                  If no bldg/room/grou
      *
      *
      * Validate:
      * 1) dead head
      * 2) dead pounds
      * 3) reason code
      * 4) destroyed flag
     C                   exsr      $kdhd
     C                   exsr      $kdlb
     C                   exsr      $drcd
     C                   exsr      $dsfl
     C                   endif                                                  If edit
     C                   ENDIF                                                  IF NOT PROTECTED
      *
     C                   EXSR      $SFIND
     C                   UPDATE    HP157S1
      *
      * SET OFF THE ERROR INDICATORS
      *
     C                   EXSR      $SETOF
     C                   ENDIF                                                  IF REC READ
     C                   ENDDO                                                  DO PROCESS
      * SET MDT
     C                   SETOFF                                       85
      *
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * $PROC1 - Process Subfile Records on Screen 1
      *---------------------------------------------------------------
      *
     C     $PROC1        BEGSR
      *
      * READ ALL THE CHANGED SUBFILE RECORDS PROCESSING THE SUBFILE
      * SELECTION.
      *
     C                   SETOFF                                       80
     C     *IN80         DOUEQ     *ON                                          DO PROCESS
     C                   READC     HP157S1                                80
     C     *IN80         IFEQ      *OFF                                         IF REC READ
      *
     C     HDPFL         IFNE      YES                                          IF NOT PROTECTED
     C     HDNDFL        ANDNE     YES
      *
     C                   EXSR      $UPDAT
     C                   ENDIF                                                  IF NOT PROTECTED
      *
     C                   SETOFF                                       85
     C                   UPDATE    HP157S1
      *
     C                   ENDIF                                                  IF REC READ
     C                   ENDDO                                                  DO PROCESS
      *
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $UPDAT - UPDATE DATABASE
      *----------------------------------------------------------------
      *
     C     $UPDAT        BEGSR
      *
      *----------------------------------------------------------------
      * CREATING A NEW RECORD:
      *----------------------------------------------------------------
      *   1) ASSIGN A MOVEMENT NUMBER BY RETRIEVING THE LAST NUMBER
      *      FROM THE DATA AREA AND INCREMENTING IT.
      *
      *   2) WRITE THE NEW KILLED/DEAD RECORD
      *
      *   3) CREATE A MOVEMENT EVENT RECORD
      *
     C                   SELECT
     C     MODE          WHENEQ    CREATE                                       WH CREATE
     C     DFHGCD        IFNE      *blank
      *
     C                   EXSR      $SETUP
      *
     C     *LOCK         IN        DAMVSN                                                       3
     C                   ADD       1             DAMVSN
     C                   OUT       DAMVSN
      *
     C                   Z-ADD     DAMVSN        KDMVSN
     C                   Z-ADD     DAMVSN        DFMVSN
     C                   MOVEL     'EN'          DFMSCD
     C                   MOVEL     YES           HDPFL
     C                   MOVEL     'EN'          KDMSCD
     C                   WRITE     KDREC
      *
     C                   EXSR      $CRTME
     C                   ENDIF
      *
      *----------------------------------------------------------------
      * REVISING A RECORD
      *----------------------------------------------------------------
      *
      * RETRIEVE RECORD AND LOCK BEFORE UPDATE/DELETE
      *
      *
     C     MODE          WHENEQ    REVISE                                       WH REVISE
      *
     C     DFMVSN        CHAIN     HSP068                             91
     C     *IN91         IFEQ      *OFF                                         IF FOUND
      *
      * WHEN DELETING A RECORD:
      *   1) DELETE THE KILLED/DEAD RECORD
      *   2) DELETE THE MOVEMENT EVENT RECORD
      *
     C     DFOPT         IFEQ      ' 4'                                         IF DELETING
     C                   DELETE    KDREC
      *
     C                   EXSR      $DLTME
     C                   MOVE      *BLANK        DFOPT
     C                   Z-ADD     0             HDKDDT
     C                   Z-ADD     0             HDHGSN
     C                   MOVEL     YES           HDPFL
     C                   MOVEL     YES           HDNDFL
     C                   ELSE
      *
      * WHEN CHANGING A RECORD:
      *   1) UPDATE THE KILLED/DEAD RECORD
      *   2) DELETE THE OLD MOVEMENT EVENT RECORD
      *   3) WRITE A NEW MOVEMENT EVENT RECORD
      *
     C                   EXSR      $SETUP
     C                   UPDATE    KDREC
     C                   EXSR      $DLTME
     C                   EXSR      $CRTME
     C                   ENDIF                                                  IF DELETING
     C                   ENDIF                                                  IF FOUND
     C                   ENDSL
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $CRTME - CREATE A MOVEMENT EVENT RECORD
      *----------------------------------------------------------------
      *
     C     $CRTME        BEGSR
      *
      * SET UP UPDATE TIME STAMP
      *
     C                   Z-ADD     WKUDAT        MEUPDT
     C                   TIME                    MEUPTM
     C                   MOVEL     SDUSR         MEUSCD
     C                   MOVEL     SDPGM         MEPGM
      *
     C                   Z-ADD     DFMVSN        MEMVSN
     C                   MOVEL     'D'           MEMTCD
     C                   Z-ADD     HDHGSN        MEHGSN
      *
     C                   select
     C                   when      mode = revise
     C                   Z-ADD     HDKDDT        MEMEDT
     C                   when      mode = create
     C                   z-add     svkddt        memedt
     C                   endsl
      *
     C                   MOVEL     'O'           MEODFL
     C                   Z-ADD     0             MECVNO
     C                   Z-ADD     0             MEHSN
     C                   Z-ADD     0             MELNNO
     C                   WRITE     MEREC
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $DLTME - DELETE AN EXISTING MOVEMENT EVENT RECORD
      *----------------------------------------------------------------
      *
     C     $DLTME        BEGSR
      *
     C     DFMVSN        CHAIN     HSP058                             91
     C     *IN91         IFEQ      *OFF
     C                   DELETE    MEREC
     C                   ENDIF
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $SETUP - SUBROUTINE TO SETUP DATABASE FIELDS FROM SCREEN FIELDS
      *----------------------------------------------------------------
      *
     C     $SETUP        BEGSR
      *
      * MOVE VALUES FROM HIDDEN SUBFILE FIELDS
      *
     C                   select
     C                   when      mode = revise
     C                   Z-ADD     HDKDDT        KDKDDT
     C                   when      mode = create
     C                   Z-ADD     svkddt        KDKDDT
     C                   endsl
      *
     C                   Z-ADD     HDHGSN        KDHGSN
      *
      * MOVE VALUES FROM SCREEN FIELDS
      *
     C                   Z-ADD     C1FSCD        KDFSCD
     C                   MOVEL     XXFSBO        KDFSBO
     C                   Z-ADD     DFKDHD        KDKDHD
     C                   Z-ADD     DFKDLB        KDKDLB
     C                   MOVEL     DFDRCD        KDDRCD
     C                   MOVEL     DFDSFL        KDDSFL
      *
      * SET UP DEFAULT VALUES
      *
     C                   Z-ADD     0             KDEPDT
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $SETOF - SUBROUTINE TO SET OFF ERROR INDICATORS FOR SUBFILE
      *----------------------------------------------------------------
      *
     C     $SETOF        BEGSR
      *
     C                   SETOFF                                       303133
     C                   SETOFF                                       343536
     C                   SETOFF                                       373839
     C                   SETOFF                                       535657
     C                   SETOFF                                       5859
     C                   SETOFF                                       6364
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * $hgcd - Validate hog group
      *---------------------------------------------------------------
      *
     C     $hgcd         BEGSR
      *
      *
      * Validate group:
      *   1) group must be valid with an open status
      *   2) group must belong to farm and business office selected
      *   3) group must be in the building/room
      *   4) pigs must have been delivered to the group on or before the
      *      dead date.
      *
     C                   select
     C                   when      dfhgcd = *blank
     C                   SETON                                        33
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS09001'     MGID(E)
     C                   EXSR      $ERR1
     C                   ENDIF
     C                   other
      *
     C     DFHGCD        CHAIN     HSP034                             91
     C     *IN91         IFEQ      *ON                                          IF BAD GROUP
     C                   SETON                                        33
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS00261'     MGID(E)
     C                   CLEAR                   MGW
     C                   MOVEA     DFHGCD        MGW(1)
     C                   MOVEA     MGW           MGDT(E)
     C                   EXSR      $ERR1
     C                   ENDIF
     C                   ELSE
      *
      * CHECK FOR MATCH IN GROUP BUSINESS OFFICE AND SELECTED B.O.
      *
     C     HGFSBO        IFNE      XXFSBO                                       IF DIFF B/O
     C                   SETON                                        33
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS00459'     MGID(E)
     C                   CLEAR                   MGW
     C                   MOVEA     DFHGCD        MGW(1)
     C                   MOVEA     HGFSBO        MGW(8)
     C                   MOVEA     XXFSBO        MGW(13)
     C                   MOVEA     MGW           MGDT(E)
     C                   EXSR      $ERR1
     C                   ENDIF
     C                   ENDIF                                                  IF DIFF B/O
      *
      * THE GROUP'S FARM SITE MUST MATCH THE SELECTED FARM SITE.
      *
     C     *IN33         IFEQ      *OFF                                         IF DIFF FARM
     C     HGFSCD        ANDNE     C1FSCD
     C     C1FSCD        ANDNE     0
     C                   SETON                                        33
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS00468'     MGID(E)
     C                   CLEAR                   MGW
     C                   MOVEA     DFHGCD        MGW(1)
     C                   MOVE      HGFSCD        XXALPHFSCD
     C                   MOVEA     XXALPHFSCD    MGW(8)
     C                   MOVE      C1FSCD        XXALPHFSCD
     C                   MOVEA     XXALPHFSCD    MGW(13)
     C                   MOVEA     MGW           MGDT(E)
     C                   EXSR      $ERR1
     C                   ENDIF
     C                   ENDIF                                                  IF DIFF FARM
      *
     C     HGGSCD        IFNE      'OP'                                         IF NOT OPEN
     C                   SETON                                        33
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS00586'     MGID(E)
     C                   CLEAR                   MGW
     C                   MOVEA     DFHGCD        MGW(1)
     C                   MOVEA     HGGSCD        MGW(8)
     C                   MOVEA     MGW           MGDT(E)
     C                   EXSR      $ERR1
     C                   ENDIF
     C                   ENDIF                                                  IF NOT OPEN
      *
      * IF THE DEATH DATE AND GROUP ARE BOTH VALID, CHECK
      * THAT PIGS WERE TRANSFERRED INTO THE GROUP (THE GROUP
      * WAS OPENED) ON OR BEFORE THE DEAD DATE.
      *
     C                   if        *in31 = *off and *in33 = *off and            IF OK DT/GRP
     C                             ((mode = revise and hdkddt < hgopdt) or
     C                              (mode = create and svkddt < hgopdt))
     C                   SETON                                        33
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS00682'     MGID(E)
     C                   CLEAR                   MGW
     C                   MOVEA     DFHGCD        MGW(1)
      *
      * FLIP THE OPENED DATE
      *
     C     *iso          move      hgopdt        wkmmddyy
     C                   move      wkmmddyy      wkmdy
     C                   movel     wkmdy         xxmdy
      *
     C                   MOVEA     XXMDY         MGW(8)
     C                   MOVEL     DFMDY         XXMDY
     C                   MOVEA     XXMDY         MGW(14)
     C                   MOVEA     MGW           MGDT(E)
     C                   EXSR      $ERR1
     C                   ENDIF
     C                   ENDIF                                                  IF OK DT/GRP
      *
      * Group building must match subfile building---if there is a building
      * on the subfile.
     C                   select
     C                   when      dfblcd = *blank
     C                   movel     hgblcd        dfblcd
     C                   other
     C                   if        dfblcd <> hgblcd                             If mismatch building
     C                   seton                                        33
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS00481'     mgid(e)
     C                   clear                   mgw
     C                   movea     dfhgcd        mgw(1)
     C                   movea     hgblcd        mgw(8)
     C                   movea     dfblcd        mgw(13)
     C                   movea     mgw           mgdt(e)
     C                   exsr      $err1
     C                   endif
     C                   endif                                                  If mismatch building
     C                   endsl
      *
      *
      * Group room must match subfile room---if there is a room
      * on the subfile.
     C                   select
     C                   when      dfrmcd = *blank
     C                   movel     hgrmcd        dfrmcd
     C                   other
     C                   if        dfrmcd <> hgrmcd                             If mismatch room
     C                   seton                                        33
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS00482'     mgid(e)
     C                   clear                   mgw
     C                   movea     dfhgcd        mgw(1)
     C                   movea     hgrmcd        mgw(8)
     C                   movea     dfrmcd        mgw(13)
     C                   movea     mgw           mgdt(e)
     C                   exsr      $err1
     C                   endif
     C                   endif                                                  If mismatch room
     C                   endsl
      *
     C                   ENDIF                                                  IF BAD GROUP
     C                   endsl
      *
      * IF THERE ARE NO ERRORS ON HOG GROUP, SET UP HIDDEN
      * SUBFILE FIELD VALUES THAT WILL BE USED IN DATABASE UPDATES.
      *
     C     *IN33         IFEQ      *OFF
     C                   Z-ADD     HGHGSN        HDHGSN
     C                   ELSE
     C                   Z-ADD     0             HDHGSN
     C                   ENDIF
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * $group - Attempt to find a default group in a building
      *---------------------------------------------------------------
      *
      * If only one group is open in that building or building/room, default
      * it in. But, if multiple groups are open, issue a message to the
      * user to select a group.
      *
     C     $group        begsr
      *
      * Key with either a) farm/building if room is blank or b) farm/
      * building/room if room is populated.
      *
     C                   if        dfrmcd = *blank
     C     key01         chain     hsl034p                            92
     C                   else
     C     key02         chain     hsl034p                            92
     C                   endif
      *
     C                   if        *in92 = *off                                 If open group
      *
      * Check to see if there is another open group.
      * Key with either a) farm/building if room is blank or b) farm/
      * building/room if room is populated.
      *
     C                   if        dfrmcd = *blank
     C     key01         reade     hsl034p                                92
     C                   else
     C     key02         reade     hsl034p                                92
     C                   endif
      *
     C                   if        *in92 = *off                                 If mult group
     C                   seton                                        33
     C                   if        error < maxmsg
     C                   add       1             error
     C                   clear                   mgw
     C                   movea     dfblcd        mgw(1)
      *
     C                   select
     C                   when      dfrmcd = *blank
     C                   movel     'HS00721'     mgid(e)
     C                   other
     C                   movel     'HS00765'     mgid(e)
     C                   movea     dfrmcd        mgw(6)
     C                   endsl
      *
     C                   movea     mgw           mgdt(e)
     C                   exsr      $err1
     C                   endif
     C                   else
      *
      * Only one group is open; default in its values.
      *
     C                   movel     hghgcd        dfhgcd
     C                   movel     hgrmcd        dfrmcd
     C                   endif                                                  If mult group
     C                   else
      *
      * No groups are open in this building/room
      *
     C                   seton                                        33
     C                   if        error < maxmsg
     C                   add       1             error
     C                   clear                   mgw
     C                   move      c1fscd        xxalphfscd
     C                   movea     xxalphfscd    mgw(1)
     C                   movea     dfblcd        mgw(6)
      *
     C                   select
     C                   when      dfrmcd = *blank
     C                   movel     'HS00722'     mgid(e)
     C                   other
     C                   movel     'HS00766'     mgid(e)
     C                   movea     dfrmcd        mgw(11)
     C                   endsl
      *
     C                   movea     mgw           mgdt(e)
     C                   exsr      $err1
     C                   endif
     C                   endif                                                  If open group
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * $DFKDDT - VALIDATE KILLED/DEAD DATE IN SUBFILE IN REVISE MODE
      *---------------------------------------------------------------
      *
     C     $DFKDDT       BEGSR
      *
     C                   z-ADD     DFMDY         PFRM8
     C                   EXSR      $DATE
     C     PRTRN         IFNE      *BLANK                                       IF INVALID
     C                   SETON                                        31
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVE      'HS09004'     MGID(E)
     C                   EXSR      $ERR1
     C                   ENDIF
     C                   ELSE
      *
      * IF THE DEAD DATE IS VALID,
      *     CHECK THAT IT IS NOT GREATER THAN TODAY'S DATE
      *     CHECK THAT IT IS NOT IN A CLOSED PERIOD
      *     IF IT IS IN THE CURRENT PERIOD,
      *         CHECK THAT THE PERIOD CLOSE PROGRAMS ARE NOT RUNNING
      *
     C                   SELECT
     C     PTO8          WHENGT    WKUDAT                                       WH FUTURE
     C                   SETON                                        31
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS00426'     MGID(E)
     C                   EXSR      $ERR1
     C                   ENDIF
      *
     C     PTO8          WHENLT    DABPDT                                       WH CLOSED
     C                   SETON                                        31
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS09007'     MGID(E)
     C                   ENDIF
      *
     C     PTO8          WHENGE    DABPDT                                       WH RUNNING
     C     PTO8          ANDLE     DAEPDT
     C     DAPPFL        ANDNE     *BLANK
     C                   SETON                                        31
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS09016'     MGID(E)
     C                   ENDIF
     C                   ENDSL
     C                   ENDIF                                                  IF INVALID
      *
      * IF THERE ARE NO ERRORS ON THE DATE, MOVE IT TO THE
      * HIDDEN SUBFILE FIELD.
      *
     C     *IN31         IFEQ      *OFF
     C                   Z-ADD     PTO8          HDKDDT
     C                   ELSE
     C                   Z-ADD     0             HDKDDT
     C                   ENDIF
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $blcd - validate building
      *----------------------------------------------------------------
      *
     C     $blcd         begsr
      *
      * If building is blank and hog group is entered, default building and
      * room from hog group.
      *
     C                   if        dfblcd = *blank and dfhgcd <> *blank
     C     dfhgcd        chain     hsp034                             92
     C                   if        *in92 = *off
     C                   move      hgblcd        dfblcd
     C                   move      hgrmcd        dfrmcd
     C                   endif
     C                   endif
      *
      * Validations:
      *  1) building is required
      *  2) building must exist on the specified farm site
      *
     C                   select
     C                   when      dfblcd = *blank
     C                   seton                                        38
     C                   if        error < maxmsg
     C                   add       1             error
     C                   exsr      $err1
     C                   eval      mgid(e) = 'HS09001'
     C                   endif
     C                   other
      *
     C     key01         chain     hsp019                             92
     C                   if        *in92 = *on                                  If not found
     C                   seton                                        38
     C                   if        error < maxmsg
     C                   add       1             error
     C                   exsr      $err1
     C                   eval      mgid(e) = 'HS00116'
     C                   clear                   mgw
     C                   movea     dfblcd        mgw(1)
     C                   move      c1fscd        xxalphfscd
     C                   movea     xxalphfscd    mgw(6)
     C                   movea     mgw           mgdt(e)
     C                   endif
     C                   endif                                                  If not found
     C                   endsl
      *
      * Attempt to default in a group when:
      *   1) building is valid and group is blank and
      *   2) room is blank or valid
      *
     C                   if        *in38 = *off and dfhgcd = *blank             If valid building
      *
     C                   if        dfrmcd <> *blank
     C                   exsr      $rmcd
     C                   endif
      *                                                                         If room OK/blank
     C                   if        *in39 = *off
     C                   exsr      $group                                       If room OK/blank
     C                   endif
     C                   endif                                                  If valid building
      *
     C                   endsr
      /EJECT
      *----------------------------------------------------------------
      * $rmcd - validate room
      *----------------------------------------------------------------
      *
      * Validations:
      *  1) required
      *  2) must exist in the building on the farm site
      *  3) must have an active status
      *
     C     $rmcd         begsr
      *
     C                   select
     C                   when      dfrmcd = *blank
     C                   seton                                        39
     C                   if        error < maxmsg
     C                   add       1             error
     C                   exsr      $err1
     C                   eval      mgid(e) = 'HS09001'
     C                   endif
     C                   other
      *
      * Check that the room exists in the building on the farm site
     *
     C     key02         chain     hsp020                             92
     C                   if        *in92 = *on                                  If no hit
     C                   seton                                        39
     C                   if        error < maxmsg
     C                   add       1             error
     C                   exsr      $err1
     C                   eval      mgid(e) = 'HS00117'
     C                   clear                   mgw
     C                   movea     dfrmcd        mgw(1)
     C                   movea     dfblcd        mgw(6)
     C                   move      c1fscd        xxalphfscd
     C                   movea     xxalphfscd    mgw(11)
     C                   movea     mgw           mgdt(e)
     C                   endif
     C                   else
     *
      * Room must have an 'active' status
     *
     C                   if        brrmst <> 'A'                                If not active
     C                   seton                                        39
     C                   if        error < maxmsg
     C                   add       1             error
     C                   exsr      $err1
     C                   eval      mgid(e) = 'HS00726'
     C                   clear                   mgw
     C                   movea     dfrmcd        mgw(1)
     C                   movea     mgw           mgdt(e)
     C                   endif
     C                   endif                                                  If not active
     C                   endif                                                  If no hit
      *
     C                   endsl
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * $kdhd - Validate head
      *---------------------------------------------------------------
      *
     C     $kdhd         begsr
      *
     C                   SELECT
     C     DFKDHD        WHENEQ    0
     C                   SETON                                        34
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS09001'     MGID(E)
     C                   EXSR      $ERR1
     C                   ENDIF
      *
     C     DFKDHD        WHENLT    0
     C                   SETON                                        34
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS09000'     MGID(E)
     C                   EXSR      $ERR1
     C                   ENDIF
     C                   ENDSL
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * $kdlb - Validate pounds
      *---------------------------------------------------------------
      *
     C     $kdlb         begsr
      *
     C                   SELECT
     C     DFKDLB        WHENEQ    0
     C                   SETON                                        35
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS09001'     MGID(E)
     C                   EXSR      $ERR1
     C                   ENDIF
      *
     C     DFKDLB        WHENLT    0
     C                   SETON                                        35
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS09000'     MGID(E)
     C                   EXSR      $ERR1
     C                   ENDIF
     C                   ENDSL
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * $drcd - Validate death reason code
      *---------------------------------------------------------------
      *
     C     $drcd         begsr
      *
     C                   SELECT
     C     DFDRCD        WHENEQ    *BLANK
     C                   MOVE      '8    '       DFDRCD
     C                   OTHER
      *
     C     DFDRCD        CHAIN     HSP041                             91
     C     *IN91         IFEQ      *ON                                          IF BAD
     C                   SETON                                        37
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS00221'     MGID(E)
     C                   CLEAR                   MGW
     C                   MOVEA     DFDRCD        MGW(1)
     C                   MOVEA     MGW           MGDT(E)
     C                   EXSR      $ERR1
     C                   ENDIF
     C                   ENDIF                                                  IF BAD
     C                   ENDSL
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * $dsfl - Validate destroyed flag
      *---------------------------------------------------------------
      *
     C     $dsfl         begsr
      *
      * DESTROYED FLAG MUST BE YES OR NO OR BLANK.
      * DEFAULT A 'NO' IF THE USER LEFT THE DESTROY FLAG BLANK.
      * (YES MEANS THE HOG HAD TO BE DESTROYED; NO MEANS THE
      * (HOG DIED ON ITS OWN.)
      *
     C                   select
     C                   when      dfdsfl = *blank
     C                   move      no            dfdsfl
     C                   other
      *
     C                   if        dfdsfl <> yes and dfdsfl <> no               If bad
     C                   SETON                                        36
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS00587'     MGID(E)
     C                   EXSR      $ERR1
     C                   ENDIF
     C                   endif                                                  If bad
     C                   endsl
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * $F4S1 - F4 PROMPT ON SCREEN 1
      *---------------------------------------------------------------
      *
     C     $F4S1         BEGSR
      *
      *
      * SET OFF PROMPT INDICATORS IN ALL SUBFILE RECORDS
      *
     C                   EXSR      $F4OFF
      *
      *
     C                   SELECT
      *
      *---------------------------------------------------------------
      * CONTROL RECORD PROMPTS
      *---------------------------------------------------------------
      *
      * Date and Movement are not promptible
      *
     C                   WHEN      FLD = 'C1MDY' or FLD = 'C1MVSN' or
     C                             FLD = 'C1KDDTMDY'
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS09011'     MGID(E)
     C                   ENDIF
      *
      * BUILDING
      *
     C     FLD           WHENEQ    'C1BLCD'
     C                   call      'HP523'
     C                   parm                    c1fscd
     C                   parm                    xxblcd
     C                   parm                    xxblds
     C                   SETON                                        55
     C     XXBLCD        IFNE      *BLANK                                       IF SELECTED
     C                   MOVEL     XXBLCD        C1BLCD
     C                   ENDIF                                                  IF SELECTED
      *
      * Room
      *
     C     FLD           WHENEQ    'C1RMCD'
     C                   call      'HP524'
     C                   parm                    c1fscd
     C                   parm                    c1blcd
     C                   parm                    xxrmcd
     C                   SETON                                        49
     C     XXRMCD        IFNE      *BLANK                                       IF SELECTED
     C                   MOVEL     XXRMCD        C1RMCD
     C                   ENDIF                                                  IF SELECTED
      *
      *
      * HOG GROUP
      *
     C     FLD           WHENEQ    'C1HGCD'
     C                   CALL      'HP5950'
     C                   PARM                    c1fscd
     C                   PARM                    c1fsnm
     C                   PARM      c1blcd        XXBLCD
     C                   PARM      c1rmcd        XXRMCD
     C                   PARM      *blank        XXHGCD
     C                   PARM      *blank        XXGSCD
     C                   PARM      *blank        XXPTCD
     C                   PARM      *blank        XXPPCD
     C                   SETON                                        50
     C     XXHGCD        IFNE      *BLANK                                       IF SELECTED
     C                   MOVEL     XXHGCD        C1HGCD
     C                   ENDIF                                                  IF SELECTED
      *
      * DESTROYED FLAG
      *
     C     FLD           WHENEQ    'C1DSFL'
     C                   CALL      'HP522'
     C                   PARM      *blank        XXDSFL
     C                   SETON                                        51
     C     XXDSFL        IFNE      *BLANK                                       IF SELECTED
     C                   MOVEL     XXDSFL        C1DSFL
     C                   ENDIF                                                  IF SELECTED
      *
      * DEATH REASON
      *
     C     FLD           WHENEQ    'C1DRCD'
     C                   CALL      'HP510'
     C                   PARM      *blank        XXDRCD
     C                   PARM      *blank        XXDRDS
     C                   SETON                                        52
     C     XXDRCD        IFNE      *BLANK                                       IF SELECTED
     C                   MOVEL     XXDRCD        C1DRCD
     C                   ENDIF                                                  IF SELECTED
      *
      * MOVEMENT STATUS
      *
     C     FLD           WHENEQ    'C1MSCD'
     C                   CALL      'HP542'
     C                   PARM      'D'           XXMTCD
     C                   PARM      *blank        XXMSCD
     C                   PARM      *blank        XXMSDS
     C                   SETON                                        54
     C     XXMSCD        IFNE      *BLANK                                       IF SELECTED
     C                   MOVEL     XXMSCD        C1MSCD
     C                   ENDIF                                                  IF SELECTED
      *
      *------------------------------------------------------------------------------------
      * PROMPTS FOR SUBFILE RECORD FIELDS
      *------------------------------------------------------------------------------------
      *
      * Building
     C                   when      fld = 'DFBLCD'
     C                   exsr      $rrn1
     C                   if        *in97 = *off                                 If hit
     C                   if        hdpfl = yes                                  If protected
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS09011'     mgid(e)
     C                   endif
     C                   else
     C                   call      'HP523'
     C                   parm                    c1fscd
     C                   parm      *blank        xxblcd
     C                   parm      *blank        xxblds
     C                   if        xxblcd <> *blank
     C                   movel     xxblcd        dfblcd
     C                   endif
     C                   endif                                                  If protected
     C                   exsr      $setof
     C                   exsr      $sfind
     C                   seton                                        8558
     C                   update    hp157s1
     C                   setoff                                       8558
      *
      * RESET THE SUBFILE RELATIVE RECORD NUMBER TO WHAT IT WAS BEFORE
      * THE USER TOOK F4 (IT WILL BE OUT-OF-SYNCH IF THE USER HAS
      * SELECTED A RECORD THUS CAUSING THE SUBFILE RECORD TO BE
      * RETRIEVED AND UPDATED.)  BUT, SET THE PAGE OF THE SUBFILE TO
      * BE DISPLAYED TO THE PAGE ON WHICH THE USER TOOK F4.
      *
     C                   z-add     svrrn1        hdrrn1
     C                   z-add     hdrr1         hdpag1
     C                   endif                                                  If hit
      *
      *
      *------------------------------------------------------------------------
      * Room
      *------------------------------------------------------------------------
      *
     C                   when      fld = 'DFRMCD'
     C                   exsr      $rrn1
     C                   if        *in97 = *off                                 If hit
     C                   if        hdpfl = yes                                  If protected
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS09011'     mgid(e)
     C                   endif
     C                   else
     C                   call      'HP524'
     C                   parm                    c1fscd
     C                   parm                    dfblcd
     C                   parm      *blank        xxrmcd
     C                   if        xxrmcd <> *blank
     C                   movel     xxrmcd        dfrmcd
     C                   endif
     C                   endif                                                  If protected
     C                   exsr      $setof
     C                   exsr      $sfind
     C                   seton                                        8559
     C                   update    hp157s1
     C                   setoff                                       8559
      *
      * RESET THE SUBFILE RELATIVE RECORD NUMBER TO WHAT IT WAS BEFORE
      * THE USER TOOK F4 (IT WILL BE OUT-OF-SYNCH IF THE USER HAS
      * SELECTED A RECORD THUS CAUSING THE SUBFILE RECORD TO BE
      * RETRIEVED AND UPDATED.)  BUT, SET THE PAGE OF THE SUBFILE TO
      * BE DISPLAYED TO THE PAGE ON WHICH THE USER TOOK F4.
      *
     C                   z-add     svrrn1        hdrrn1
     C                   z-add     hdrr1         hdpag1
     C                   endif                                                  If hit
      *
      *------------------------------------------------------------------------
      * HOG GROUP
      *------------------------------------------------------------------------
      *
     C     FLD           WHENEQ    'DFHGCD'
     C                   EXSR      $RRN1
     C     *IN97         IFEQ      *OFF                                         IF HIT
     C     HDPFL         IFEQ      YES                                          IF PROTECTED
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS09011'     MGID(E)
     C                   ENDIF
     C                   ELSE
      *
     C                   CALL      'HP5950'
     C                   PARM                    c1fscd
     C                   PARM                    c1fsnm
     C                   PARM      DFBLCD        XXBLCD
     C                   PARM      DFRMCD        XXRMCD
     C                   PARM      *BLANK        XXHGCD
     C                   PARM      *BLANK        XXGSCD
     C                   PARM      *BLANK        XXPTCD
     C                   PARM      *BLANK        XXPPCD
     C     XXHGCD        IFNE      *BLANK                                       IF SELECTED
     C                   MOVEL     XXHGCD        DFHGCD
     C                   MOVEL     XXBLCD        DFBLCD
     C                   MOVEL     XXRMCD        DFRMCD
     C                   ENDIF                                                  IF SELECTED
     C                   ENDIF                                                  IF PROTECTED
      *
     C                   EXSR      $SETOF
     C                   EXSR      $SFIND
     C                   SETON                                        8553
     C                   UPDATE    HP157S1
     C                   SETOFF                                       8553
      *
      * RESET THE SUBFILE RELATIVE RECORD NUMBER TO WHAT IT WAS BEFORE
      * THE USER TOOK F4 (IT WILL BE OUT-OF-SYNCH IF THE USER HAS
      * SELECTED A RECORD THUS CAUSING THE SUBFILE RECORD TO BE
      * RETRIEVED AND UPDATED.)  BUT, SET THE PAGE OF THE SUBFILE TO
      * BE DISPLAYED TO THE PAGE ON WHICH THE USER TOOK F4.
      *
     C                   Z-ADD     SVRRN1        HDRRN1
     C                   Z-ADD     HDRR1         HDPAG1
     C                   ENDIF                                                  IF HIT
      *
      *
      * DESTROYED FLAG
      *
     C     FLD           WHENEQ    'DFDSFL'
     C                   EXSR      $RRN1
     C     *IN97         IFEQ      *OFF                                         IF DSFL HIT
     C     HDPFL         IFEQ      YES                                          IF PROTECTED
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS09011'     MGID(E)
     C                   ENDIF
     C                   ELSE
      *
     C                   CALL      'HP522'
     C                   PARM      *blank        XXDSFL
     C     XXDSFL        IFNE      *BLANK                                       IF SELECTED
     C                   MOVEL     XXDSFL        DFDSFL
     C                   ENDIF                                                  IF SELECTED
     C                   ENDIF                                                  IF PROTECTED
      *
     C                   EXSR      $SETOF
     C                   EXSR      $SFIND
     C                   SETON                                        8556
     C                   UPDATE    HP157S1
     C                   SETOFF                                       8556
      *
      * RESET THE SUBFILE RELATIVE RECORD NUMBER TO WHAT IT WAS BEFORE
      * THE USER TOOK F4 (IT WILL BE OUT-OF-SYNCH IF THE USER HAS
      * SELECTED A RECORD THUS CAUSING THE SUBFILE RECORD TO BE
      * RETRIEVED AND UPDATED.)  BUT, SET THE PAGE OF THE SUBFILE TO
      * BE DISPLAYED TO THE PAGE ON WHICH THE USER TOOK F4.
      *
     C                   Z-ADD     SVRRN1        HDRRN1
     C                   Z-ADD     HDRR1         HDPAG1
     C                   ENDIF                                                  IF DSFL HIT
      *
      *
      * DEATH REASON
      *
     C     FLD           WHENEQ    'DFDRCD'
     C                   EXSR      $RRN1
     C     *IN97         IFEQ      *OFF                                         IF DRCD HIT
      *
     C     HDPFL         IFEQ      YES                                          IF PROTECTED
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS09011'     MGID(E)
     C                   ENDIF
     C                   ELSE
      *
     C                   CALL      'HP510'
     C                   PARM      *blank        XXDRCD
     C                   PARM      *blank        XXDRDS
     C     XXDRCD        IFNE      *BLANK                                       IF SELECTED
     C                   MOVEL     XXDRCD        DFDRCD
     C                   ENDIF                                                  IF SELECTED
     C                   ENDIF                                                  IF PROTECTED
      *
     C                   EXSR      $SETOF
     C                   EXSR      $SFIND
     C                   SETON                                        8557
     C                   UPDATE    HP157S1
     C                   SETOFF                                       8557
      *
      * RESET THE SUBFILE RELATIVE RECORD NUMBER TO WHAT IT WAS BEFORE
      * THE USER TOOK F4 (IT WILL BE OUT-OF-SYNCH IF THE USER HAS
      * SELECTED A RECORD THUS CAUSING THE SUBFILE RECORD TO BE
      * RETRIEVED AND UPDATED.)  BUT, SET THE PAGE OF THE SUBFILE TO
      * BE DISPLAYED TO THE PAGE ON WHICH THE USER TOOK F4.
      *
     C                   Z-ADD     SVRRN1        HDRRN1
     C                   Z-ADD     HDRR1         HDPAG1
     C                   ENDIF
     C                   OTHER
      *
      * F4 NOT VALID FOR THIS FIELD SO ISSUE MESSAGE.
      * RESET THE SUBFILE RELATIVE RECORD NUMBER TO WHAT IT WAS BEFORE
      * THE USER TOOK F4 (IT WILL BE OUT-OF-SYNCH IF THE USER HAS
      * SELECTED A RECORD THUS CAUSING THE SUBFILE RECORD TO BE
      * RETRIEVED AND UPDATED.)  BUT, SET THE PAGE OF THE SUBFILE TO
      * BE DISPLAYED TO THE PAGE ON WHICH THE USER TOOK F4.
      *
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS09011'     MGID(E)
     C                   ENDIF
      *
     C                   EXSR      $RRN1
     C     *IN97         IFEQ      *OFF                                         IF HIT
     C                   EXSR      $SETOF
     C                   EXSR      $SFIND
     C                   SETON                                        85
     C                   UPDATE    HP157S1
     C                   SETOFF                                       85
      *
      * RESET THE SUBFILE RELATIVE RECORD NUMBER TO WHAT IT WAS BEFORE
      * THE USER TOOK F4 (IT WILL BE OUT-OF-SYNCH IF THE USER HAS
      * SELECTED A RECORD THUS CAUSING THE SUBFILE RECORD TO BE
      * RETRIEVED AND UPDATED.)  BUT, SET THE PAGE OF THE SUBFILE TO
      * BE DISPLAYED TO THE PAGE ON WHICH THE USER TOOK F4.
      *
     C                   Z-ADD     SVRRN1        HDRRN1
     C                   Z-ADD     HDRR1         HDPAG1
     C                   ENDIF                                                  IF HIT
      *
     C                   ENDSL
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $F4OFF - SET OFF THE PROMPT SUBFILE  RECORD INDICATORS
      *----------------------------------------------------------------
      *
     C     $F4OFF        BEGSR
      *
      *
     C                   SETOFF                                       80
     C     *IN80         DOUEQ     *ON
     C                   READC     HP157S1                                80
     C     *IN80         IFEQ      *OFF
     C                   SETON                                        85
     C                   EXSR      $SETOF
     C                   EXSR      $SFIND
     C                   UPDATE    HP157S1
     C                   ENDIF
     C                   ENDDO
      * Set MDT
     C                   SETOFF                                       85
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $C1IND - RESET INDICATORS FOR CONTROL RECORD
      *----------------------------------------------------------------
      *
     C     $C1IND        BEGSR
      *
      * IF THE FARM WAS DELETED OUT FROM UNDER THE USER, THEN SET
      * *IN90 BACK ON TO CONTROL AVAILABLE FUNCTION KEYS AND INPUT
      * FIELDS.
      *
     C     NOTFLG        IFEQ      YES                                          IF NOT FOUND
     C                   SETON                                        90
     C                   ENDIF                                                  IF NOT FOUND
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $SFIND - RESET INDICATORS FOR SUBFILE RECORD
      *----------------------------------------------------------------
      *
     C     $SFIND        BEGSR
      *
      * IF THE FARM WAS DELETED OUT FROM UNDER THE USER, THEN SET
      * *IN90 BACK ON TO CONTROL AVAILABLE FUNCTION KEYS AND INPUT
      * FIELDS.
      *
     C     NOTFLG        IFEQ      YES                                          IF NOT FOUND
     C                   SETON                                        90
     C                   ENDIF                                                  IF NOT FOUND
      *
      *
      * IF PROTECT SUBFILE FIELDS
      *
     C     HDPFL         IFEQ      YES
     C                   SETON                                        64
     C                   ENDIF
      *
      *
      * IF SUBFILE FIELDS SHOULD BE NON-DISPLAY
      *
     C     HDNDFL        IFEQ      YES
     C                   SETON                                        6364
     C                   ENDIF
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $DATE -  MANIPULATE DATES WITH DATE UTILITY
      *----------------------------------------------------------------
      *
     C     $DATE         BEGSR
      *
     C                   Z-ADD     0             PTO8
     C                   MOVEL     'MDY '        PFRFMT
     C                   MOVEL     'CYMD'        PTOFMT
     C                   Z-ADD     0             PDAYS
     C                   MOVE      *BLANK        PCODE
     C                   MOVE      *BLANK        PRTRN
     C                   CALL      'UT80060R'
     C                   PARM                    PFRM8             8 0
     C                   PARM                    PTO8              8 0
     C                   PARM                    PFRFMT            4
     C                   PARM                    PTOFMT            4
     C                   PARM                    PDAYS             4 0
     C                   PARM                    PCODE             1
     C                   PARM                    PRTRN             1
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * $ERR1  - SUBROUTINE TO SET THE SUBFILE TO REDISPLAY AT
      *          THE FIRST SCREEN THAT HAS ERRORS ON SCREEN 1
      *---------------------------------------------------------------
      *
     C     $ERR1         BEGSR
      *
     C     ERROR         IFEQ      1
     C                   Z-ADD     HDRRN1        HDPAG1
     C                   ENDIF
      *
     C                   ENDSR
      *
      *---------------------------------------------------------------
      * $EXIT - SUBROUTINE TO SET ON LR AND END JOB
      *---------------------------------------------------------------
      *
     C     $EXIT         BEGSR
     C                   SETON                                        LR
     C                   ENDSR
      *
      *---------------------------------------------------------------
      * $WRMSG - SUBROUTINE TO WRITE ERROR MESSAGES
      *---------------------------------------------------------------
      *
     C     $WRMSG        BEGSR
      *
     C                   CALL      'UT80045J'                           43
     C                   PARM                    MGID
     C                   PARM                    MGDT
     C                   PARM                    ERROR
     C                   PARM      SDPGM         PGM
     C                   PARM      MSGFIL        MSGFL
      *
      * IF CALL TO UT80045J FAILED, A MESSAGE HARDCODED IN THE DSPF
      * COMMAND KEY RECORD WILL BE SET ON. SO, REDISPLAY SCREEN TO USER
      *
     C     *IN43         IFEQ      *ON
     C                   MOVEL     SET1          ROUTNE
     C                   ENDIF
      *
     C                   ENDSR
      *
      *----------------------------------------------------------------
      * $CLMSG - SUBROUTINE TO CLEAR MESSAGES
      *----------------------------------------------------------------
      *
     C     $CLMSG        BEGSR
      *
     C                   CALL      'UT80045J'                           43
     C                   PARM      *BLANK        MGI
     C                   PARM      *BLANK        MGD
     C                   PARM      *ZEROS        ERROR
     C                   PARM      SDPGM         PGM
     C                   PARM      MSGFIL        MSGFL
      *
      * IF CALL TO UT80045J FAILED, A MESSAGE HARDCODED IN THE DSPF
      * COMMAND KEY RECORD WILL BE SET ON. SO, REDISPLAY SCREEN TO USER
      *
     C     *IN43         IFEQ      *ON
     C                   MOVEL     SET1          ROUTNE
     C                   ENDIF
      *
     C                   ENDSR
      /EJECT
      ****************************************************************
      * $RRN1  - ROUTINE TO FIND/RETRIEVE THE SUBFILE RECORD TO UPDATE
      *          WHEN RETURNING FROM A SELECT PROGRAM ON SCREEN 1
      ****************************************************************
      *
      * HDRR1 IS THE HIDDEN FIELD ASSOCIATED WITH THE KEYWORD SFLCSRRRN
      * ON THE CONTROL RECORD IN THE DSPF.
      * HDRRN1 IS THE VALUE ASSOCIATED WITH THE KEYWORD 'KSFILE' IN
      * THE F-SPECS FOR THE WORKSTATION.
      *
     C     $RRN1         BEGSR
      *
     C                   SETOFF                                       97
      *
     C     HDRR1         IFNE      0
     C                   Z-ADD     HDRR1         HDRRN1
     C     HDRRN1        CHAIN     HP157S1                            97
     C                   ENDIF
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * INITIALIZATION SUBROUTINE
      *---------------------------------------------------------------
      *
     C     *INZSR        BEGSR
      *
      * PARM LISTS
      *
     C     *ENTRY        PLIST
     C                   PARM                    XXFSBO
     C                   PARM                    C1FSCD
     C                   PARM                    C1FSNM
     C                   PARM                    RTNCD
      *
      * Key lists
      *
      *
     C     key01         klist
     C                   kfld                    c1fscd
     C                   kfld                    dfblcd
      *
     C     key02         klist
     C                   kfld                    c1fscd
     C                   kfld                    dfblcd
     C                   kfld                    dfrmcd
      *
     C     key03         KLIST
     C                   KFLD                    C1FSCD
     C                   KFLD                    WKKDDT
      *
      *
      * GLOBAL DEFINES
      *
     C                   MOVE      *BLANK        PGM              10            USED IN MSG
     C                   MOVE      *BLANK        MSGFL            10            USED IN MSG
     C                   MOVE      *BLANK        ROUTNE            6            ROUTINE
     C                   Z-ADD     0             MAXRRN            4 0          REPOSITION
     C                   MOVEL     NO            RESET             1            REPOSITION
     C                   MOVEL     NO            NOTFLG            1
     C     *LIKE         DEFINE    DFMODE        MODE                           SFL POSITION
     C     *LIKE         DEFINE    IDFLOW        WKFLOW                         SFL POSITION
     C                   MOVEL     '00'          RTNCD             2            RETURN CODE
     C     *LIKE         DEFINE    HDRRN1        SVRRN1                         F4-PROMPT
     C                   Z-ADD     0             COUNT             2 0
      *
     C                   MOVEL     NO            ENTRYFL           1            SUBFILE ENTRY
      *
      * SAVE FIELDS FOR CONTROLLING REPOSITIONING ON
      * POSITIONERS AND SELECTORS
      *
     C     *LIKE         DEFINE    C1HGCD        SVHGCD
     C     *LIKE         DEFINE    C1BLCD        SVBLCD
     C     *LIKE         DEFINE    C1RMCD        SVRMCD
     C     *LIKE         DEFINE    C1DSFL        SVDSFL
     C     *LIKE         DEFINE    C1DRCD        SVDRCD
     C     *LIKE         DEFINE    C1MDY         SVMDY
     C     *LIKE         DEFINE    C1MVSN        SVMVSN
     C     *LIKE         DEFINE    C1MSCD        SVMSCD
     C     *LIKE         DEFINE    KDKDDT        WKKDDT
     C     *LIKE         DEFINE    kdkddt        svkddt
      *
     C     *LIKE         DEFINE    C1HGCD        SVSFLHGCD
     C     *LIKE         DEFINE    C1BLCD        SVSFLBLCD
     C     *LIKE         DEFINE    C1RMCD        SVSFLRMCD
      *
      *
      * PARM FIELDS USED IN CALLS TO/FROM OTHER PROGRAMS
      *
     C     *LIKE         DEFINE    HGFSBO        XXFSBO
     C     *LIKE         DEFINE    HGBLCD        XXBLCD
     C                   MOVE      *BLANK        XXBLDS           20
     C     *LIKE         DEFINE    HGRMCD        XXRMCD
     C                   MOVE      *BLANK        XXALPHFSCD        5
     C     *LIKE         DEFINE    HGHGCD        XXHGCD
     C     *LIKE         DEFINE    HGGSCD        XXGSCD
     C     *LIKE         DEFINE    HGPTCD        XXPTCD
     C     *LIKE         DEFINE    HGPPCD        XXPPCD
     C     *LIKE         DEFINE    KDDSFL        XXDSFL
     C     *LIKE         DEFINE    DRDRCD        XXDRCD
     C     *LIKE         DEFINE    DRDRDS        XXDRDS
     C     *LIKE         DEFINE    C1MSCD        XXMSCD
     C                   MOVE      *BLANK        XXMSDS           20
     C                   MOVE      *BLANK        XXMTCD            1
     C                   MOVE      *BLANK        XXMDY             6
      *
      * TO SUPPORT F5-REFRESH, LOGIC REQUIRES THE NUMBER OF LINES ON
      * THE SUBFILE PAGE AND ALSO THE NUMBER OF LINES LESS 1.  BY
      * SETTING THESE VALUES UP AS CONSTANT WORK FIELDS, THE PROCESSING
      * LOGIC CAN BE STANDARDIZED BETWEEN PROGRAMS.
      *
     C                   Z-ADD     10            WKPLIN            2 0          SFLPAG
     C     WKPLIN        SUB       1             WKRLIN            2 0          SFLPAG - 1
      *
      * DETERMINE THE INITIAL MODE--REVISE IF THERE ARE
      * DEAD HOGS FOR THE FARM; CREATE MODE IF THERE ARE NOT.
      *
     C     C1FSCD        CHAIN     HSJ068A                            91
     C     *IN91         IFEQ      *OFF
     C                   MOVEL     REVISE        MODE
     C                   MOVEL     REVISE        DFMODE
     C                   ELSE
     C                   MOVEL     CREATE        MODE
     C                   MOVEL     CREATE        DFMODE
     C                   ENDIF
      *
      * THE SYSTEM DATE IS REQUIRED IN CCYYMMDD FORMAT FOR
      * EDITING. SO, FLIP THE DATE.
      *
     C                   Z-ADD     UDATE         PFRM8
     C                   EXSR      $DATE
     C     PRTRN         IFEQ      *BLANK                                       IF VALID
     C                   Z-ADD     PTO8          WKUDAT            8 0
     C                   ELSE
     C                   Z-ADD     0             WKUDAT
     C                   ENDIF                                                  IF VALID
      *
      * DEFINE THE DATA AREAS THAT CONTAIN THE MOVEMENT
      * NUMBER  AND THE CURRENT ACCOUNTING PERIOD.
      *
     C     *DTAARA       DEFINE                  DAMVSN
     C     *DTAARA       DEFINE                  DAAPER
      *
      * THE FOLLOWING STANDARD CODE MUST BE INCLUDED TO MAKE THE
      * STANDARD ERROR MESSAGE HANDLING PROGRAM FUNCTION PROPERLY.
      * THIS CODE SETS AND CLEARS THE PROGRAM MESSAGE QUEUE.
      *
     C                   Z-ADD     20            MAXMSG            2 0
     C                   MOVEL     '*'           MSGPGM                         SET QUEUE
     C                   EXSR      $CLMSG                                       CLEAR MSG
      *
     C                   ENDSR
      /EJECT
