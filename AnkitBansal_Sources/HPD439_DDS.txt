     A*%%TS  SD  20020429  131012  ISLRAMS     REL-V5R1M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD439
     A*  TITLE:         WORK WITH TRANSFERS
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  DATE:          04/10/95
     A*
     A*****************************************************************
     A* RMC 2/19/2016 E5190  ADD BUSINESS OFFICE RESTRICTOR
     A* DN  07/17/2021 W80211 RENAMED F17='PRINT BOL' TO F17=
     A*                'PRT/EMAIL BOL'.
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A N99                                  CF04(04 'PROMPT')
     A N99                                  CF05(05 'REFRESH')
     A N99                                  CF06(06 'CREATE')
     A N99                                  CF17(17 'BOL')
     A N99                                  CF20(20 'WRKSPLF')
     A*
     A*----------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*----------------------------------------------------------------
     A          R HP439S1                   SFL
     A*%%TS  SD  19951004  144005  ISLRAMS     REL-V3R0M5  5763-PW1
     A  85                                  SFLNXTCHG
     A            HDRRN1         4Y 0H
     A            DFOPT          2A  B 12  2CHGINPDFT(UL)
     A  30                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A N30                                  DSPATR(HI)
     A  99                                  DSPATR(PR)
     A  99                                  DSPATR(ND)
     A            DFMVSN    R        O 12  6REFFLD(MHMVSN)
     A                                      EDTCDE(4)
     A            DFMSCD    R        O 12 15REFFLD(MHMSCD)
     A            DFSMDY    R     Y  O 12 18REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  ')
     A            DFSCHD    R     Y  O 12 27REFFLD(SGSCHD)
     A                                      EDTCDE(K)
     A            DFORHG    R        O 12 35REFFLD(HGHGCD)
     A            DFORFS    R        O 12 43REFFLD(FSFSCD)
     A                                      EDTCDE(Z)
     A            DFORBL    R        O 12 49REFFLD(HSRREF/HGBLCD *LIBL/HSPREF)
     A            DFORPP    R        O 12 55REFFLD(HGPPCD)
     A            DFDNHG    R        O 12 62REFFLD(HGHGCD)
     A            DFDNFS    R        O 12 70REFFLD(HGFSCD)
     A                                      EDTCDE(Z)
     A            DFDNBL    R        O 12 76REFFLD(HSRREF/HGRMCD *LIBL/HSPREF)
     A*
     A*----------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*----------------------------------------------------------------
     A          R HP439C1                   SFLCTL(HP439S1)
     A*%%TS  SD  20020429  113527  ISLRAMS     REL-V5R1M0  5722-WDS
     A                                      SFLSIZ(0050)
     A                                      SFLPAG(0010)
     A N82N99                               ROLLUP(25 'PAGE FORWARD')
     A                                      SETOF(37)
     A                                      SETOF(31)
     A                                      SETOF(51)
     A                                      SETOF(52)
     A                                      SETOF(53)
     A                                      SETOF(54)
     A                                      SETOF(55)
     A                                      SETOF(56)
     A                                      SETOF(57)
     A                                      RTNCSRLOC(&RCD &FLD)
     A                                      OVERLAY
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 26'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  2 27'WORK WITH TRANSFERS'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  4  2'Business office .. ?'
     A            C1FSBO    R        B  4 23REFFLD(HGFSBO)
     A  31
     AO 51                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A N31                                  DSPATR(HI)
     A  99                                  DSPATR(PR)
     A            C1BODS    R        O  4 30REFFLD(BOBODS)
     A                                  5  2'Show current movements only ..'
     A                                  5 33'?'
     A                                      DSPATR(HI)
     A            C1CURFL   R        B  5 35REFFLD(HSRREF/@@FLAG *LIBL/HSPREF)
     A  57
     AO 37                                  DSPATR(PC)
     A  37                                  DSPATR(RI)
     A N37                                  DSPATR(HI)
     A  99                                  DSPATR(PR)
     A N99                              7  2'2=Revise'
     A N99                              7 12'5=View'
     A N99                              8 16'?'
     A                                      DSPATR(HI)
     A N99                              8 37'?'
     A                                      DSPATR(HI)
     A N99                              8 45'?'
     A                                      DSPATR(HI)
     A N99                              8 57'?'
     A                                      DSPATR(HI)
     A N99                              8 64'?'
     A                                      DSPATR(HI)
     A N99                              8 72'?'
     A                                      DSPATR(HI)
     A                                  9 20'Ship'
     A                                      DSPATR(HI)
     A                                  9 28'Sched'
     A                                      DSPATR(HI)
     A                                  9 35'-------- ORIGIN ---------'
     A                                      DSPATR(HI)
     A                                  9 62'--- DESTINATION ---'
     A                                      DSPATR(HI)
     A                                 10  2'Opt'
     A                                      DSPATR(HI)
     A                                 10  6'Movemnt'
     A                                      DSPATR(HI)
     A                                 10 15'St'
     A                                      DSPATR(HI)
     A                                 10 20'Date'
     A                                      DSPATR(HI)
     A                                 10 29'Head'
     A                                      DSPATR(HI)
     A                                 10 35'Group'
     A                                      DSPATR(HI)
     A                                 10 44'Farm'
     A                                      DSPATR(HI)
     A                                 10 49'Bldg'
     A                                      DSPATR(HI)
     A                                 10 55'Phase'
     A                                      DSPATR(HI)
     A                                 10 62'Group'
     A                                      DSPATR(HI)
     A                                 10 71'Farm'
     A                                      DSPATR(HI)
     A                                 10 76'Bldg'
     A                                      DSPATR(HI)
     A            C1MVSN    R        B 11  6REFFLD(MHMVSN)
     A                                      EDTCDE(4)
     A                                      DSPATR(HI)
     A N37N57                               DSPATR(PC)
     A  99                                  DSPATR(PR)
     A  99                                  DSPATR(ND)
     A            C1MSCD    R        B 11 15REFFLD(MHMSCD)
     A                                      DSPATR(HI)
     A  51                                  DSPATR(PC)
     A  99                                  DSPATR(PR)
     A  99                                  DSPATR(ND)
     A            C1SMDY    R     Y  B 11 18REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  ')
     A                                      DSPATR(HI)
     A            C1ORHG    R        B 11 35REFFLD(HGHGCD)
     A                                      DSPATR(HI)
     A  52                                  DSPATR(PC)
     A  99                                  DSPATR(PR)
     A  99                                  DSPATR(ND)
     A            C1ORFS    R        B 11 43REFFLD(HGFSCD)
     A                                      EDTCDE(Z)
     A                                      DSPATR(HI)
     A  53                                  DSPATR(PC)
     A  99                                  DSPATR(PR)
     A  99                                  DSPATR(ND)
     A            C1ORBL    R        B 11 49REFFLD(HSRREF/HGBLCD *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A  99                                  DSPATR(PR)
     A  99                                  DSPATR(ND)
     A            C1ORPP    R        B 11 55REFFLD(HGPPCD)
     A                                      DSPATR(HI)
     A  54                                  DSPATR(PC)
     A  99                                  DSPATR(PR)
     A  99                                  DSPATR(ND)
     A            C1DNHG    R        B 11 62REFFLD(HGHGCD)
     A                                      DSPATR(HI)
     A  55                                  DSPATR(PC)
     A  99                                  DSPATR(PR)
     A  99                                  DSPATR(ND)
     A            C1DNFS    R        B 11 70REFFLD(HGFSCD)
     A                                      EDTCDE(Z)
     A                                      DSPATR(HI)
     A  56                                  DSPATR(PC)
     A  99                                  DSPATR(PR)
     A  99                                  DSPATR(ND)
     A            C1DNBL    R        B 11 76REFFLD(HSRREF/HGRMCD *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A  99                                  DSPATR(PR)
     A  99                                  DSPATR(ND)
     A*
     A*----------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*----------------------------------------------------------------
     A          R HP439K1
     A*%%TS  SD  19960213  095527  ISLRAMS     REL-V3R1M0  5763-PW1
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A                                 23 11'F4=Prompt'
     A                                 23 22'F5=Refresh'
     A                                 23 34'F6=Create'
80211A**                               23 45'F17=Print BOL'
  |  A                                 23 45'F17=Prt/Email BOL'
  |  A**                               23 60'F20=Spool Files'
80211A                                 23 64'F20=Spool Files'
      *----------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *----------------------------------------------------------------
     A          R HP439ES                   SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *                                                                *********
      *----------------------------------------------------------------
      * ERROR MESSAGE SUBFILE CONTROL FORMAT
      *----------------------------------------------------------------
     A          R HP439EC                   SFLCTL(HP439ES)
      *
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
