     A*%%TS  SD  20220805  155352  ISELOUC     REL-V7R3M0  5770-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD430
     A*  TITLE:         WORK WITH UPLOADED FEED TICKETS
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  DATE:          04/29/97
     A*
     A*****************************************************************
     A* SDN736 RECOMPILED FOR TICKET NBR INCREASE (TKNO & RTNO 7.0 TO 9.0)
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A                                      CF04(04 'PROMPT')
     A                                      CF05(05 'REFRESH')
     A                                      CF20(20 'WRKSPLF')
     A*
     A*
     A*---------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP430S1                   SFL
     A*%%TS  SD  20220805  155352  ISELOUC     REL-V7R3M0  5770-WDS
     A  85                                  SFLNXTCHG
     A            HDRRN1         4Y 0H
     A            DFOPT          2A  B  9  2CHGINPDFT(UL)
     A  30                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A N30                                  DSPATR(HI)
     A            DFTKNO    R        O  9  7REFFLD(FDTKNO)
     A                                      EDTCDE(M)
     A            DFMDY     R        O  9 20REFFLD(@@DT06)
     A                                      EDTCDE(Y)
     A            DFFMBO    R        O  9 30REFFLD(@@FMBO)
     A            DFFSCD    R        O  9 37REFFLD(@@FSCD)
     A                                      EDTCDE(M)
     A            DFVINO    R        O  9 46REFFLD(@@IJD)
     A            DFERFL    R        O  9 61REFFLD(@@FLAG)
     A*
     A*---------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP430C1                   SFLCTL(HP430S1)
     A*%%TS  SD  20220805  155352  ISELOUC     REL-V7R3M0  5770-WDS
     A                                      SFLSIZ(0050)
     A                                      SFLPAG(0013)
     A N82                                  ROLLUP(25 'PAGE FORWARD')
     A                                      SETOF(51)
     A                                      SETOF(52)
     A                                      SETOF(55)
     A                                      RTNCSRLOC(&RCD &FLD)
     A                                      OVERLAY
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 26'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  2 21'WORK WITH UPLOADED FEED TICKETS'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  4  2'2=Revise'
     A                                  5 30'?'
     A                                      DSPATR(HI)
     A                                  5 39'?'
     A                                      DSPATR(HI)
     A                                  5 61'?'
     A                                      DSPATR(HI)
     A                                  6  8'Ticket'
     A                                      DSPATR(HI)
     A                                  6 21'Ticket'
     A                                      DSPATR(HI)
     A                                  6 28'Business'
     A                                      DSPATR(HI)
     A                                  6 38'Farm'
     A                                      DSPATR(HI)
     A                                  6 46'Vendor'
     A                                      DSPATR(HI)
     A                                  6 59'Error'
     A                                      DSPATR(HI)
     A                                  7  2'Opt'
     A                                      DSPATR(HI)
     A                                  7  8'Number'
     A                                      DSPATR(HI)
     A                                  7 22'Date'
     A                                      DSPATR(HI)
     A                                  7 29'Office'
     A                                      DSPATR(HI)
     A                                  7 38'Site'
     A                                      DSPATR(HI)
     A                                  7 46'Invoice'
     A                                      DSPATR(HI)
     A                                  7 59'Flag'
     A                                      DSPATR(HI)
     A            C1TKNO    R        B  8  7REFFLD(@@TKNO)
     A                                      DSPATR(HI)
     A            C1FMBO    R        B  8 30REFFLD(@@FMBO)
     A                                      DSPATR(HI)
     A  51                                  DSPATR(PC)
     A            C1FSCD    R        B  8 37REFFLD(@@FSCD)
     A                                      EDTCDE(M)
     A                                      DSPATR(HI)
     A  52                                  DSPATR(PC)
     A            C1VINO    R        B  8 46REFFLD(@@IJD)
     A                                      DSPATR(HI)
     A            C1ERFL    R        B  8 61REFFLD(@@FLAG)
     A                                      DSPATR(HI)
     A  55                                  DSPATR(PC)
     A*
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R HP430K1
     A*%%TS  SD  19940404  112055  ISLRAMS     REL-V2R2M0  5738-PW1
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A                                 23 11'F4=Prompt'
     A                                 23 22'F5=Refresh'
     A                                 23 34'F20=Spool Files'
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *---------------------------------------------------------------
     A          R HP430ES                   SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE CONTROL FORMAT
      *---------------------------------------------------------------
     A          R HP430EC                   SFLCTL(HP430ES)
      *
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
