     A*%%TS  SD  20090114  110610  ISLRAMS     REL-V5R4M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        TRIUMPH FOODS
     A*  DISPLAY FILE:  TFD4500
     A*  TITLE:         SPECIFY KILL/CUT DATA LISTING
     A*  PROGRAMMER:    LEANNE RAMSEY
     A*  DATE:          03/27/06
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/TFPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CF03(03 'EXIT')
     A                                      CF08(08 'SUBMIT')
     A                                      CF17(17 'PRINT OPTIONS')
     A                                      CF20(20 'SPOOL FILES')
     A*
     A*----------------------------------------------------------------
     A* RECORD FORMAT ONE
     A*----------------------------------------------------------------
     A          R TF4500R1
     A*%%TS  SD  20090114  110610  ISLRAMS     REL-V5R4M0  5722-WDS
     A                                      WINDOW(TF4500EC)
     A                                      TEXT('INPUT SCREEN')
     A                                      SETOF(30)
     A                                      SETOF(31)
     A                                      OVERLAY
     A            SDPGM     R        O  1  1REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 14'SPECIFY KILL/CUT DATA LISTING OPTI-
     A                                      ONS'
     A                                      DSPATR(HI)
     A                                  3 49'Week-ending'
     A                                      COLOR(BLU)
     A                                  4 49'    Date   '
     A                                      DSPATR(UL)
     A                                      COLOR(BLU)
     A                                  5  1'From date .......'
     A            D1FMDY    R        B  5 21REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A  30                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A N30                                  DSPATR(HI)
     A                                  5 32'(Blank for all)'
     A            D1FWEMDY  R        O  5 50REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A                                      COLOR(BLU)
     A                                  6  1'To date .........'
     A            D1TMDY    R        B  6 21REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A  31                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A N31                                  DSPATR(HI)
     A                                  6 32'(Blank for all)'
     A            D1TWEMDY  R        O  6 50REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A                                      COLOR(BLU)
     A*
     A*----------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*----------------------------------------------------------------
     A          R TF4500K1
     A*%%TS  SD  20070327  135012  ISLRAMS     REL-V5R4M0  5722-WDS
     A                                      WINDOW(TF4500EC)
     A                                      TEXT('COMMAND KEYS')
     A                                      OVERLAY
     A  43                              9  2'Message program failed. Call IS.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 10  1'F3=Exit'
     A                                 10 10'F8=Submit'
     A                                 10 21'F17=Print Options'
     A                                 10 40'F20=Spool Files'
      *
      *----------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *----------------------------------------------------------------
     A          R TF4500ES
     A                                      TEXT('Error message subfile')
     A                                      SFL SFLMSGRCD(11)
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
     A*
     A*----------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT WITH WINDOW DEFINITION
     A*----------------------------------------------------------------
     A          R TF4500EC                  SFLCTL(TF4500ES)
     A*%%TS  SD  20000628  103649  ISLRAMS     REL-V4R2M0  5769-PW1
     A                                      TEXT('Error message subfile')
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A                                      WINDOW(*DFT 12 70)
     A                                      WDWBORDER((*DSPATR RI) (*CHAR '    -
     A                                          '))
     A            MSGPGM                    SFLPGMQ(10)
     A*
     A*----------------------------------------------------------------
     A* ASSUME FORMAT - REQUIRED FOR WINDOWS
     A*----------------------------------------------------------------
     A*
     A          R ASSUME
     A*%%TS  SD  20000627  095625  ISLRAMS     REL-V4R2M0  5769-PW1
     A                                      TEXT('ASSUME FORMAT FOR WINDOWS')
     A                                      ASSUME
     A                                      OVERLAY
     A                                 24 79' '
