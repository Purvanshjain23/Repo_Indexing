/***************************************************************** */
/*                                                                 */
/*  PROGRAM NUMBER:  HP372CL                                       */
/*  PROGRAM NAME:    PRELIMINARY EDIT LISTING FOR DISPOSED GROUPS  */
/*  PROGRAMMER:      LEANNE FEDOR                                  */
/*  CREATE DATE:     07/14/03                                      */
/*                                                                 */
/*  FUNCTION:        THIS GIVES A PRELIMINARY EDIT LISTING SHOWING */
/*                   REASONS THAT DISPOSED GROUPS CANNOT BE CLOSED.*/
/*                                                                 */
/*******************************************************************/
/* MODIFICATIONS:                                                  */
/*                                                                 */
/* 02/29/16    Rose Centonze    E5290                              */
/*             Added company code parm and add to opnqry select    */
/*                                                                 */
/*******************************************************************/

             PGM        PARM(&ERROR &PPCOCD)

             DCL        VAR(&PPCOCD) TYPE(*CHAR) LEN(3)            /* E5290 */

             DCL        VAR(&QDATA) TYPE(*CHAR) LEN(60) /* E5290-WAS 40 */
             DCL        VAR(&ERROR) TYPE(*CHAR) LEN(1)

             DCL        VAR(&LDEPDT) TYPE(*DEC) LEN(8)

             DCL        VAR(&HOLD) TYPE(*CHAR) LEN(4)
             DCL        VAR(&SAVE) TYPE(*CHAR) LEN(4)
             DCL        VAR(&COPYS) TYPE(*DEC) LEN(2)
             DCL        VAR(&OUTQ) TYPE(*CHAR) LEN(10)

             CHGVAR     VAR(&LDEPDT) VALUE(%SST(*LDA 9 8))


/*-----------------------------------------------------------------*/
/* SET PRINT PARAMETERS FROM LDA                                   */
/*-----------------------------------------------------------------*/

/* AS OF MARCH 2004, USERS ASKED US TO ALWAYS PUT OUTPUT ON SAVE/HOLD */

             CHGVAR     VAR(&SAVE) VALUE(*YES)
             CHGVAR     VAR(&HOLD) VALUE(*YES)

/* SET PARAMETERS FROM LDA                                           */

 COPYS:      CHGVAR     VAR(&COPYS) VALUE(%SST(*LDA 412 1))
             IF         COND(&COPYS = 0) THEN(CHGVAR VAR(&COPYS) +
                          VALUE(1))

 OUTQ:       CHGVAR     VAR(&OUTQ) VALUE(%SST(*LDA 413 10))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(%SST(*LDA 401 10)))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(*JOB))

 OVRPRTF:    OVRPRTF    FILE(QPRINT) OUTQ(&OUTQ) COPIES(&COPYS) +
                          HOLD(&HOLD) SAVE(&SAVE) SHARE(*YES)


/*-------------------------------------------------------------------*/
/*  SETUP OPNQRY STATEMENT TO SELECT DISPOSED HOG GROUPS             */
/*-------------------------------------------------------------------*/

             CHGVAR     VAR(%SST(&QDATA 1 10)) VALUE('HGDSDT *LE')
             CHGVAR     VAR(%SST(&QDATA 12 8)) VALUE(&LDEPDT)
             CHGVAR     VAR(%SST(&QDATA 21 20)) VALUE('*AND HGGSCD +
                          *EQ "DS"')

/*   SELECT COMPANY IN PARM          E5290                     */

             CHGVAR     VAR(%SST(&QDATA 42 15)) VALUE('*AND HGCOCD +
                          *EQ')
             CHGVAR     VAR(%SST(&QDATA 58 3)) VALUE(&PPCOCD)

             OVRDBF     FILE(HSL034E) SHARE(*YES)
             OPNQRYF    FILE((HSL034E)) QRYSLT(&QDATA) KEYFLD(*FILE) +
                          SEQONLY(*YES)

/*-------------------------------------------------------------------*/
/* GENERATE EDIT LISTING                                             */
/*-------------------------------------------------------------------*/

             CALL       PGM(HP372) PARM(&ERROR)

             CLOF       OPNID(HSL034E)
             DLTOVR     FILE(HSL034E)

             DLTOVR     FILE(QPRINT)

             ENDPGM

