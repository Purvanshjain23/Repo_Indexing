     A*%%TS  SD  20010822  093225  ISLRAMS     REL-V4R4M0  5769-PW1
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD431
     A*  TITLE:         WORK WITH EXPENSE EDIT FILE
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  DATE:          01/05/98
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A N99                                  CF04(04 'PROMPT')
     A N99                                  CF05(05 'REFRESH')
     A N99                                  CF20(20 'WRKSPLF')
     A*
     A*
     A*---------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP431S1                   SFL
     A*%%TS  SD  20010822  093225  ISLRAMS     REL-V4R4M0  5769-PW1
     A  85                                  SFLNXTCHG
     A            HDRRN1         4Y 0H
     A            DFOPT          2A  B 10  2CHGINPDFT(UL)
     A  30                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A N30                                  DSPATR(HI)
     A  99                                  DSPATR(PR)
     A            DFEXCD    R        O 10  9REFFLD(EEEXCD)
     A            DFCJD     R        O 10 19REFFLD(HSRREF/@@CJD *LIBL/HSPREF)
     A            DFAM      R        O 10 35REFFLD(HSRREF/EEAM *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A            DFOBJ     R        O 10 62REFFLD(HSRREF/@@OBJ *LIBL/HSPREF)
     A            DFERFL    R        O 10 75REFFLD(GHERFL)
     A*
     A*---------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP431C1                   SFLCTL(HP431S1)
     A*%%TS  SD  20010822  093225  ISLRAMS     REL-V4R4M0  5769-PW1
     A                                      SFLSIZ(0050)
     A                                      SFLPAG(0011)
     A N82N99                               ROLLUP(25 'PAGE FORWARD')
     A                                      SETOF(51)
     A                                      SETOF(53)
     A                                      RTNCSRLOC(&RCD &FLD)
     A                                      OVERLAY
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 26'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  2 23'WORK WITH EXPENSE EDIT FILE'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  4  2'2=Revise'
     A                                  4 12'4=Delete'
     A                                  6 11'?'
     A                                      DSPATR(HI)
     A                                  6 75'?'
     A                                      DSPATR(HI)
     A                                  7  8'Expense'
     A                                      DSPATR(HI)
     A                                  7 23'Cost'
     A                                      DSPATR(HI)
     A                                  7 73'Error'
     A                                      DSPATR(HI)
     A                                  8  2'Opt'
     A                                      DSPATR(HI)
     A                                  8  9'Code'
     A                                      DSPATR(HI)
     A                                  8 22'Center'
     A                                      DSPATR(HI)
     A                                  8 49'Amount'
     A                                      DSPATR(HI)
     A                                  8 62'Account'
     A                                      DSPATR(HI)
     A                                  8 74'Flag'
     A                                      DSPATR(HI)
     A            C1EXCD    R        B  9  9REFFLD(EEEXCD)
     A  51                                  DSPATR(PC)
     A                                      DSPATR(HI)
     A  99                                  DSPATR(PR)
     A            C1CJD     R        B  9 19REFFLD(HSRREF/@@CJD *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A                                      CHECK(RB)
     A            C1OBJ     R        B  9 62REFFLD(HSRREF/@@OBJ *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A            C1ERFL    R        B  9 75REFFLD(GHERFL)
     A                                      DSPATR(HI)
     A  53                                  DSPATR(PC)
     A  99                                  DSPATR(PR)
     A*
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R HP431K1
     A*%%TS  SD  20010822  093225  ISLRAMS     REL-V4R4M0  5769-PW1
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A                                 23 11'F4=Prompt'
     A                                 23 22'F5=Refresh'
     A                                 23 34'F20=Spool Files'
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *---------------------------------------------------------------
     A          R HP431ES                   SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE CONTROL FORMAT
      *---------------------------------------------------------------
     A          R HP431EC                   SFLCTL(HP431ES)
      *
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
