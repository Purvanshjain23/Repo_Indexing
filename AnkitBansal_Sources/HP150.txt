      *****************  RPG PROGRAM HEADING  ************************
      * SYSTEM:      HOG PRODUCTION
      * PROGRAM:     HP150
      * TITLE:       REVISE FARM SITE
      * PROGRAMMER:  ROSE CENTONZE
      * CREATED:     03/18/94
      *
      * FUNCTION: USES AN EDIT RECORD FORMAT TO ADD/CHANGE/DELETE
      *           FARM SITE INFORMATION.
      *
      *           THIS PROGRAM IS CALLED FROM HP450 - WORK WITH
      *           FARM SITES.
      *
      *           THE FIELD 'MODE' CONTAINS THE VALUE PASSED INTO
      *           THIS PROGRAM FROM THE CALLING PROGRAM.  THE FIELD
      *           'DFMODE' IS THE SCREEN FIELD THAT DISPLAYS THE
      *           VALUE OF 'MODE'.  MODE WILL BE CREATE, REVISE OR
      *           DELETE.           ONCE IN THE PROGRAM, MODE AND
      *           DFMODE WILL NEVER CHANGE.  HOWEVER, THE ACTION
      *           BEING PERFORMED CAN CHANGE DEPENDING ON THE
      *           FUNCTION KEYS THE USER TAKES.
      *
      *           THE USER MAY DELETE AN EXISTING RECORD BY TAKING
      *           F11=DELETE FROM THIS PROGRAM OR OPTION 4=DELETE
      *           FROM WORK WITH FARM SITE, IF THE USER IS
      *           DELETING WITH 4=DELETE, NO PANEL WILL BE DISPLAYED
      *           FROM THIS PROGRAM (HP150) UNLESS THERE IS AN ERROR
      *           ON THE ATTEMPTED DELETION.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 04/18/95  LEANNE FEDOR
      *           FARM SITE CHANGED FROM ALPHA TO NUMERIC FIELD.
      *           NEW FIELD, CELL, ADDED TO FARM SITE FILE. TO GET
      *           EVERYTHING TO FIT ON THE SCREEN, I REMOVED THE SIXTH
      *           LINE OF THE J.D. EDWARDS ADDRESS FROM THE DISPLAY; I
      *           LEFT THE LOGIC IN THE PROGRAM IN CASE WE NEED TO PUT
      *           THAT LINE BACK ON THE SCREEN LATER.
      *
      * 05/01/95  LEANNE FEDOR
      *           REMOVED DELETION INTEGRITY CHECK AGAINST FEED TICKET
      *           HEADER. THE DELETION CHECK WILL NOW BE PERORMED ONLY
      *           AGAINST THE DETAIL RECORDS. THIS CHANGE RESULTS FROM
      *           THE CHANGE TO PICKUP TICKET LOGIC THAT ALLOWS A
      *           PICKUP TICKET TO BE DELIVERED TO A DIFFERENT FARM--
      *           THUS THE FEED TICKET HEADER NO LONGER ACCURATELY
      *           REPRESENTS ALL POSSIBLE FARMS ASSOCIATED WITH THAT
      *           TICKET.
      *
      * 07/03/95  LEANNE FEDOR
      *           CHANGED DELETION INTEGRITY CHECK FOR MOVEMENTS FOR
      *           MOVEMENT REWRITE.
      *
      * 05/08/96  LEANNE FEDOR
      *           MADE CHANGES TO FACILITATE NEW JDE RELEASE CONVERSION
      *           CHANGED SEARCH TYPE TO 3A; CALL NEW GENERIC
      *           PROGRAM HPJDEV TO VALIDATE INSTEAD OF HPJDEA SINCE
      *           NO VENDOR ADDRESS IS REQUIRED.
      *
      * 05/15/96  LEANNE FEDOR
      *           REMOVED DSPF FIELDS FOR J.D. EDWARDS ADDRESS FOR
      *           FARM SITE.
      *
      * 12/09/97  LEANNE FEDOR
      *           ADDED EDIT THAT COST CENTER MUST BE UNIQUE IN
      *           FARM SITE FILE.
      *
      * 12/19/97  LEANNE FEDOR
      *           TEMPORARILY REMOVE CHECK FOR 'ACTIVE' STATUS ON
      *           MANAGERS AND SUPERVISORS.  TOM KRAUSE IS TRYING
      *           TO ENTER COST CENTERS AND DOES NOT KNOW THE ACTIVE
      *           MANAGERS AND SUPERVISORS TO ENTER.
      *
      * 03/09/98  LEANNE FEDOR
      *           This program had to be recompiled to address the
      *           increased field sizes of feed pounds/dollars in the
      *           feed ticket file. BUT, I also reinstated the manager
      *           edit that was 'temporarily' removed on 12/19/97.
      *
      * 07/09/01  LeAnne Fedor
      *           All 3 managers (cell, production, multisite) are now output only.
      *           A separate function accessed through Work with Managers has been
      *           written for Anne Marie Dushay; she will maintain the managers...not
      *           Rindy!  When a new farm is created, we will default all managers to
      *           a value of 999-unassigned.
      *
      * 07/10/01  LeAnne Fedor
      *           Removed reference to Bin Set file; we have deleted the file.
      *
      * 08/14/01  LeAnne Fedor
      *           Per Alice B., remove the edit that 'cost center' must be unique
      *           per farm in the Farm Site file. We know that this will cause
      *           problems with the Expense Ticket download from JDE....but we
      *           will address those problems later.
      *
      * 09/04/01  LeAnne Fedor
      *           Recompile only...'hog capacity' removed from Farm Buildings file.
      *           'Square feet' fields added to Farm Site file.
      *
      * 01/03/02  LeAnne Fedor
      *           Recompile only. New fields added to Manager file.
      *
      * 01/22/02  LeAnne Fedor
      *           Redesigned screen to handle new fields: pod, group type, consolidated
      *           farm site number.
      *           Recompile only. New fields added to Manager file.
      *
      * 06/27/02  LeAnne Fedor
      *           Added Type of Farm and County to Farm Site File and to screen.
      *
      * 07/24/02  LeAnne Fedor
      *           Added edits for BGF farms:
      *              Cannot have a group type of wean-to-finish
      *              Cannot have a farm type of wean-to-finish
      *
      * 01/09/03  LeAnne Fedor
      *           Added 'transportation office' to screen (removed output only 'business
      *           office code/description) to make room.
      *
      * 07/14/03  LeAnne Fedor
      *           Removed deletion integrity check against HSP047-EOP Farm Site file; this
      *           file no longer exists in the system.
      *
      * 03/21/05  LeAnne Fedor
      *           Recompile only. Field size changed in Farm Cost file.
      *
      * 06/06/05  LeAnne Fedor
      *           Recompile only. Received Date was added as a key field on
      *           HSL084C.
      *
      * 01/16/06  LeAnne Fedor
      *           Recompile only.
      *           Field 'Bin Set' removed from HSP020-Building/Rooms.
      *           'Bin Set' is now in new file HSP113-Rooms/Bin Sets.
      *
      * 07/06/09  LeAnne Ramsey
      *           Recompile only. Added new field 'Continuous Flow Flag' to Hog Group file.
      *
      * 10/15/13  LeAnne Ramsey (E2831)
      *           Recompile only. Added field 'MTech Reference'.
      *
      * 02/29/16  Susan Mason   (E5050)
      *           Allow the user to enter into the MTech Refer
      *           ence. Field from the HSP018
      *
      * 03/25/20  Danny Nguyen - P405 - Farm Number Increase
      *           Recompile only. Increased Bin Code (@@BNCD) in HSPREF file from 5A to 6A.
      *
      * 07/14/20  Danny Nguyen - P408 - Added Premsie ID for input on screen & file HSP0181
      *           to host the Premise ID.
      *           When maintaining the Farm Site (HSP018) record, allow to
      *           create/update/delete the Farm Site Addt'l Info (HSP0181) record.
      *
      * 05/10/22 Eric L SDN736 Recompiled ticket nbr increase (TKNO & RTNO 7.0 TO 9.0)
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     FHPD150    CF   E             WORKSTN INFDS(IOFEED)
      *
      *
     FHSP009    IF   E           K DISK
      *    BUSINESS OFFICE
      *
      *
     FHSP010    IF   E           K DISK
      *    PRODUCTION TYPE
      *
      *
     FHSP011    IF   E           K DISK
      *    PRODUCTION PHASE
      *
      *
     FHSP012    IF   E           K DISK
      *    COMPANY RELATIONSHIP
      *
      *
     FHSP013    IF   E           K DISK
      *    FEED MILL DELIVERY ZONES
      *
      *
     FHSP018    UF A E           K DISK
      *    FARM SITE
      *
      *
P408 FHSP0181   UF A E           K DISK
P408  *    FARM SITE ADDITION INFORMATION
      *
      *
     FHSP019    IF   E           K DISK
      *    FARM BUILDINGS
      *
      *
     FHSP020    IF   E           K DISK
      *    BUILDINGS ROOMS
      *
      *
     FHSP023    IF   E           K DISK
      *    INSTALLED EQUIPMENT
      *
      *
     FHSP025    IF   E           K DISK
      *    Group type
      *
      *
     FHSP028    IF   E           K DISK
      *    FARM COST
      *
      *
     FHSL034A   IF   E           K DISK
      *    HOG GROUP
      *
      *
     FHSP036    IF   E           K DISK
      *    BINS
      *
     FHSL038N   IF   E           K DISK
      *    FEED TICKET DETAIL
      *
      *
     FHSL048F   IF   E           K DISK
      *    EXPENSE TICKET
      *
      *
     FHSL068C   IF   E           K DISK
      *    KILLED/DEAD HOGS
      *
      *
     FHSL069D   IF   E           K DISK
      *    INVENTORY ADJUSTMENTS
      *
      *
     FHSL074C   IF   E           K DISK
      *  TRANSFER HEADER
      *
      *
     FHSL074D   IF   E           K DISK    RENAME(MHREC:MHRECD)
      *  TRANSFER HEADER
      *
      *
     FHSL084C   IF   E           K DISK
      *  SALE HEADER
      *
      *
     Fhsp101    if   e           k disk
      *  Type of farm
      *
      *
     Fhsp109    if   e           k disk
      *    Transportation office
      *
      /EJECT
      ****************************************************************
      * INPUT SPECIFICATIONS
      ****************************************************************
      *
      *---------------------------------------------------------------
      * STANDARD ERROR MESSAGE HANDLING DATA STRUCTURES
      *---------------------------------------------------------------
      *
      * FOR ERROR MESSAGE HANDLING, A PACKED INDEX (E) IS REQUIRED.
      * FOR PROGRAM READABILITY, DEFINE A CORRESPONDING ERROR COUNT
      * FIELD CALLED 'ERROR'. ALSO, DEFINE CURSOR LOCATION FIELDS.
      *
     D                 DS                  INZ
     D  ERROR                  1      2P 0
     D  E                      1      2P 0
      *
      * THIS DATA STRUCTURE SUPPLIES THE NAME OF THE MESSAGE FILE TO
      * THE MESSAGE HANDLING CL PROGRAM.  THE FIELD NAME MSGFIL MUST BE
      * CONSTANT. THE VALUE IN QUOTES IS THE NAME OF THE SPECIFIC
      * MESSAGE FILE CONTAINING THE USER DEFINED MESSAGES.
      *
     D                 DS                  INZ
     D  MSGFIL                 1     10    INZ('HSMSGF    ')
      *
      * THE FOLLOWING 3 DATA STRUCTURES ARE USED TO SPEED MESSAGE
      * HANDLING SINCE IT IS FASTER TO CLEAR DATA STRUCTURES THAN
      * ARRAYS.  EACH IS ASSOCIATED WITH A STANDARD MESSAGE ARRAY.
      *
     D MGI             DS           140    INZ
     D  MGID                   1    140
     D                                     DIM(20)                              MSG ID ARRAY
      *
     D MGD             DS          1000    INZ
     D  MGDT                   1   1000
     D                                     DIM(20)                              MSG PARMS
      *
     D MGWK            DS            50    INZ
     D  MGW                    1     50
     D                                     DIM(50)                              MSG WORK PARMS
      *
      *---------------------------------------------------------------
      * STANDARD PROGRAM STATUS DATA STRUCTURE
      *---------------------------------------------------------------
      *    EXTERNALLY DEFINED AS UTPGFR (RECORD FORMAT: PGMDSR)
     D PGMDS         ESDS                  EXTNAME(UTPGFR)
      *
      *---------------------------------------------------------------
      * STANDARD WORKSTATION INFORMATION DATA STRUCTURE
      *---------------------------------------------------------------
      *    EXTERNALLY DEFINED AS UTWSFR (RECORD FORMAT: UTIDFRR)
     D IOFEED        E DS                  EXTNAME(UTWSFR)
      *
      *---------------------------------------------------------------
      *  NAMED CONSTANTS
      *---------------------------------------------------------------
      *
     D CREATE          C                   CONST('CREATE')
     D REVISE          C                   CONST('REVISE')
     D DELETE          C                   CONST('DELETE')
     D VIEW            C                   CONST('VIEW  ')
     D SELECT          C                   CONST('SELECT')
     D YES             C                   CONST('Y')
     D NO              C                   CONST('N')
     D SET1            C                   CONST('SET1  ')
     D EDIT1           C                   CONST('EDIT1 ')
     D SCRN1           C                   CONST('SCRN1 ')
     D EXIT            C                   CONST('EXIT  ')
     D UPDATE          C                   CONST('UPDATE')
      *
      *
      /EJECT
      ****************************************************************
      * MAIN LINE
      ****************************************************************
      *
     C                   MOVEL     SET1          ROUTNE
      *
     C     *INLR         DOWEQ     *OFF                                         MAIN DO LOOP
      *
     C     ROUTNE        CASEQ     SET1          $SET1
     C     ROUTNE        CASEQ     SCRN1         $SCRN1
     C     ROUTNE        CASEQ     EDIT1         $EDIT1
      *
     C     ROUTNE        CASEQ     UPDATE        $UPDAT
      *
     C     ROUTNE        CASEQ     EXIT          $EXIT
     C                   ENDCS
     C                   ENDDO                                                  MAIN DO LOOP
      *
      /EJECT
      *----------------------------------------------------------------
      * $SET1 - SUBROUTINE TO SET ENVIRONMENT FOR SCREEN 1
      *----------------------------------------------------------------
      *
     C     $SET1         BEGSR
      *
      *  CLEAR FIELDS IN THE DISPLAY FILE RECORD
      *
     C                   MOVE      *BLANK        DFTOCD
     C                   MOVE      *BLANK        DFPTCD
     C                   MOVE      *BLANK        DFPTDS
     C                   MOVE      *BLANK        DFPPCD
     C                   MOVE      *BLANK        DFPPDS
     C                   MOVE      *BLANK        DFTONM
     C                   Z-ADD     0             DFPAJD
     C                   MOVE      *BLANK        DFPJNM
     C                   MOVE      *BLANK        DFCJD
     C                   MOVE      *BLANK        DFCCDS
     C                   MOVE      *BLANK        DFCRCD
     C                   MOVE      *BLANK        DFCRDS
     C                   MOVE      *BLANK        DFGTCD
     C                   MOVE      *BLANK        DFGTDS
      ** P408 - Added Premise ID.
P408 C                   MOVE      *BLANKS       DFPRM
     C                   Z-ADD     0             DFCELL
     C                   Z-ADD     0             DFPOD
     C                   Z-ADD     0             DFCNFSCD
     C                   MOVE      *BLANK        DFCNFSNM
     C                   MOVE      *BLANK        DFFMBO
     C                   MOVE      *BLANK        DFFMDS
     C                   MOVE      *BLANK        DFDZCD
     C                   MOVE      *BLANK        DFDZDS
     C                   Z-ADD     0             DFFAJD
     C                   MOVE      *BLANK        DFFJNM
     C                   eval      dfsacd = *blank
     C                   eval      dfsanm = *blank
      *
      * IF INCOMING MODE IS REVISE OR DELETE, THEN RETRIEVE DATABASE
      * RECORD USING THE FARM SITE CODE THAT WAS PASSED IN.
      * DO NOT LOCK THE RECORD.  TYPICALLY, IN DELETE MODE THESE
      * RETRIEVED VALUES WILL NOT BE DISPLAYED.  HOWEVER, IF THERE
      * IS AN ERROR ON THE DELETE ACTION, THE PANEL WILL BE DISPLAYED.
      * P4085 - Get Premised ID from FARM SITE ADD/INFO (HSP0181) File.
      *
     C     MODE          IFEQ      REVISE                                       IF R OR D
     C     MODE          OREQ      DELETE
     C     DFFSCD        CHAIN(N)  HSP018                             90
     C     *IN90         IFEQ      *ON                                          IF NOT FOUND
     C                   SETON                                        62
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS00132'     MGID(E)
     C                   CLEAR                   MGW
     C                   MOVE      DFFSCD        XXALPHFSCD
     C                   MOVEA     XXALPHFSCD    MGW(1)
     C                   MOVEA     MGW           MGDT(E)
     C                   ENDIF
     C                   ELSE
      *
     C                   MOVEL     FSSACD        DFSACD
     C                   MOVEL     FSCNTY        DFCNTY
     C                   MOVEL     FSFSNM        DFFSNM
     C                   MOVEL     FSTOCD        DFTOCD
     C                   MOVEL     FSPTCD        DFPTCD
     C                   MOVEL     FSPPCD        DFPPCD
     C                   Z-ADD     FSPAJD        DFPAJD
     C                   Z-ADD     FSFAJD        DFFAJD
     C                   MOVEL     FSCRCD        DFCRCD
     C                   MOVEL     FSGTCD        DFGTCD
     C                   MOVEL     FSTFCD        DFTFCD
     C                   Z-ADD     FSCELL        DFCELL
     C                   Z-ADD     FSPOD         DFPOD
     C                   Z-ADD     FSCNFSCD      DFCNFSCD
     C                   MOVEL     FSCJD         DFCJD
     C                   MOVEL     FSFMBO        DFFMBO
     C                   MOVEL     FSDZCD        DFDZCD
     C                   MOVEL     FSMTECHREF    DFMTEC
      *
      * P408 - Set Premise ID on Screen.
P408 C     DFFSCD        CHAIN(N)  HSP0181
  |  C                   IF        %Found
  |  C                   MOVEL     FIPRCD        DFPRM
  |  C                   ELSE
  |  C                   MOVEL     *BLANK        DFPRM
P408 C                   ENDIF
      *
      * RETRIEVE DESCRIPTIONS AND NAMES FOR FARM SITE FIELDS
      *
     C     DFTOCD        CHAIN     HSP109                             91
     C     *IN91         IFEQ      *OFF
     C                   MOVEL(P)  TOTONM        DFTONM
     C                   ELSE
     C                   MOVEL     'UNKNOWN'     DFTONM
     C                   ENDIF
      *
     C     DFPTCD        CHAIN     HSP010                             91
     C     *IN91         IFEQ      *OFF
     C                   MOVEL(P)  PTPTDS        DFPTDS
     C                   ELSE
     C                   MOVEL     'UNKNOWN'     DFPTDS
     C                   ENDIF
      *
     C     DFPPCD        CHAIN     HSP011                             91
     C     *IN91         IFEQ      *OFF
     C                   MOVEL(P)  PPPPDS        DFPPDS
     C                   ELSE
     C                   MOVEL(P)  'UNKNOWN'     DFPPDS
     C                   ENDIF
      *
     C     DFCRCD        CHAIN     HSP012                             91
     C     *IN91         IFEQ      *OFF
     C                   MOVEL(P)  CRCRDS        DFCRDS
     C                   ELSE
     C                   MOVEL(P)  'UNKNOWN'     DFCRDS
     C                   ENDIF
      *
     C     DFGTCD        CHAIN     HSP025                             91
     C     *IN91         IFEQ      *OFF
     C                   MOVEL(P)  GTGTDS        DFGTDS
     C                   ELSE
     C                   MOVEL(P)  'UNKNOWN'     DFGTDS
     C                   ENDIF
      *
     C     DFTFCD        CHAIN     HSP101                             91
     C     *IN91         IFEQ      *OFF
     C                   MOVEL(P)  TFTFDS        DFTFDS
     C                   ELSE
     C                   MOVEL     'UNKNOWN'     DFTFDS
     C                   ENDIF
      *
     C     DFCNFSCD      CHAIN     HSP018                             91
     C     *IN91         IFEQ      *OFF
     C                   MOVEL(P)  FSFSNM        DFCNFSNM
     C                   ELSE
     C                   MOVEL(P)  'UNKNOWN'     DFCNFSNM
     C                   ENDIF
      *
     C     DFFMBO        CHAIN     HSP009                             91
     C     *IN91         IFEQ      *OFF
     C                   MOVEL(P)  BOBODS        DFFMDS
     C                   ELSE
     C                   MOVEL(P)  'UNKNOWN'     DFFMDS
     C                   ENDIF
      *
     C     KL013         CHAIN     HSP013                             91
     C     *IN91         IFEQ      *OFF
     C                   MOVEL(P)  DZDZDS        DFDZDS
     C                   ELSE
     C                   MOVEL(P)  'UNKNOWN'     DFDZDS
     C                   ENDIF
      *
      * RETRIEVE FARM SITE JDE NUMBER AND NAME USING A
      * SEARCH TYPE OF 'F' FOR FACTILTY.
      *
     C                   MOVE      *BLANK        DFFJNM
     C     DFFAJD        IFNE      0
     C                   Z-ADD     DFFAJD        INAJD
     C                   MOVEL(P)  'F'           INAT1
     C                   EXSR      $JDEV
     C     JDEFL         IFEQ      YES
     C                   SETON                                        45
     C                   ELSE
     C                   MOVEL     OUALPH        DFFJNM
     C                   ENDIF
     C                   ENDIF
      *
      * RETRIEVE PRODUCER JDE NUMBER USING A SEARCH TYPE OF
      * 'P'
      *
     C                   MOVE      *BLANK        DFPJNM
     C     DFPAJD        IFNE      0
     C                   Z-ADD     DFPAJD        INAJD
     C                   MOVEL(P)  'P'           INAT1
     C                   EXSR      $JDEV
     C     JDEFL         IFEQ      YES
     C                   SETON                                        46
     C                   ELSE
     C                   MOVEL     OUALPH        DFPJNM
     C                   ENDIF
     C                   ENDIF
      *
      * RETRIEVE COST CENTER JDE DESCRIPTION FIELD
      *
     C                   MOVE      *BLANK        DFCCDS
     C     DFCJD         IFNE      *BLANK
     C                   MOVEL     DFCJD         INCJD
     C                   EXSR      $JDEC
     C     JDEFL         IFEQ      YES
     C                   SETON                                        44
     C                   ELSE
     C                   MOVEL     OUCCDS        DFCCDS
     C                   ENDIF
     C                   ENDIF
      *
      *
      * Retrieve JDE state name
      *
     C                   exsr      $sanm
      *
      * PROTECT FARM SITE, ADDRESS NUMBER, PRODUCTION TYPE, PRODUCTION PHASE
      * IF ANY HOG GROUPS EXIST ON THE FARM
      *
     C     DFFSCD        CHAIN     HSL034A                            90
     C     *IN90         IFEQ      *OFF                                         IF GROUP OK
     C                   SETON                                        63
     C                   ENDIF
      *
     C                   ENDIF                                                  IF NOT FOUND
     C                   ENDIF                                                  IF R OR D
      *
      * IF THE RECORD WAS NOT FOUND, SET TO DISPLAY THE SCREEN
      * WITH THE ERROR MESSAGE. OTHERWISE, IF THE USER IS DELETING,
      * GO TO THE UPDATE ROUTINE AND DON'T DISPLAY THE SCREEN.
      * IF CREATING OR REVISING, DISPLAY THE SCREEN.
      *
     C     ERROR         IFEQ      0                                            IF ERRORS
     C     ACTION        IFEQ      DELETE
     C                   MOVEL     UPDATE        ROUTNE
     C                   ELSE
     C                   MOVEL     SCRN1         ROUTNE
     C                   ENDIF
      * ERRORS EXIST
     C                   ELSE
     C                   MOVEL     SCRN1         ROUTNE
     C                   ENDIF                                                  IF ERRORS
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $SCRN1 - SUBROUTINE TO PERFORM OPERATOR I/O FOR SCREEN 1
      *----------------------------------------------------------------
      *
     C     $SCRN1        BEGSR
      *
      *  WRITE MESSAGES FROM ARRAYS TO MESSAGE SUBFILE
      *
     C                   EXSR      $WRMSG
      *
      *  SET DSPF INDICATORS TO CONTROL INPUT/OUT FIELDS AND AVAILABLE
      *  FUNCTION KEYS.
      *
     C                   EXSR      $IND
      *
      * WRITE SCREEN 1 TO CRT
      *
     C                   WRITE     HP150K1
     C                   WRITE     HP150EC
     C                   EXFMT     HP150R1
      *
      * CLEAR MESSAGES
      *
     C                   EXSR      $CLMSG
      *
      * RESET ACTION TO BE EQUAL TO THE INCOMING MODE.
      * THIS IS REQUIRED IN CASE THE USER TOOK F11=DELETE,
      * GOT AN ERROR ON THE DELETION, AND NOW NEEDS TO BE ABLE TO
      * CHANGE THE RECORD AND TAKE AN ACTION OTHER THAN DELETE
      *
     C                   MOVEL     MODE          ACTION
      *
      *
      * GET USER'S INPUT AND SET ROUTINE
      *
     C                   SELECT
      *
     C     *IN03         WHENEQ    *ON                                          F3=EXIT
     C                   MOVEL     '03'          RTNCD
     C                   MOVEL     EXIT          ROUTNE
      *
     C     *IN04         WHENEQ    *ON                                          F4-PROMPT
     C     MODE          ANDNE     DELETE
     C                   EXSR      $F4S1
     C                   MOVEL     SCRN1         ROUTNE
      *
     C     *IN05         WHENEQ    *ON                                          F5-REFRESH
     C                   MOVEL     SET1          ROUTNE
      *
     C     *IN11         WHENEQ    *ON                                          F11-DELETE
     C     MODE          OREQ      DELETE
     C                   MOVEL     DELETE        ACTION
     C                   MOVEL     DELETE        DFMODE
     C                   MOVEL     UPDATE        ROUTNE
     C                   OTHER
      *
      * USER HAS PRESSED ENTER
      *
     C                   MOVEL     EDIT1         ROUTNE
     C                   ENDSL
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * Set indicators to control screen capabilityR SCREEN 1
      *----------------------------------------------------------------
      *
     C     $IND          BEGSR
      *
     C                   SELECT
     C     MODE          WHENEQ    REVISE
     C                   SETON                                        60
     C     MODE          WHENEQ    CREATE
     C                   SETON                                        61
     C     MODE          WHENEQ    DELETE
     C                   SETON                                        62
     C                   ENDSL
      *
     C                   if        version = 'PROD'
     C                   seton                                        64
     C                   endif
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $EDIT1 - SUBROUTINE TO EDIT INPUT FIELDS FOR SCREEN 1
      *----------------------------------------------------------------
      *
     C     $EDIT1        BEGSR
      *
      * *IN62 WILL BE ON IF THE RECORD THE USER IS TRYING TO ACCESS
      * HAS BEEN DELETED BY ANOTHER USER.  SO, ONLY PERFORM THE
      * EDITS FOR THE FIELDS ON THE SCREEN IF *IN62 IS OFF
      *
     C     *IN62         IFEQ      *OFF                                         IF HERE
      *
      * IF YOU ARE CREATING A NEW RECORD,
      *   VALIDATE FARM SITE CODE
      *
     C     ACTION        IFEQ      CREATE                                       IF CREATE
     C                   EXSR      $FSCD
     C                   ENDIF                                                  IF CREATE
      *
      * ALWAYS VALIDATE FARM NAME
      *
     C     DFFSNM        IFEQ      *BLANK
     C                   SETON                                        32
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS09001'     MGID(E)
     C                   ENDIF
     C                   ENDIF
      *
      *
      * Validate:
      *   County code
      *   State code
     C                   EXSR      $CNTY
     C                   EXSR      $SACD
      *
      * Validate:
      *   Transportation office
      *   Production type
      *   Production phase
      *   Group type
      *   Type of farm
      *
     C                   EXSR      $TOCD
     C                   EXSR      $PTCD
     C                   EXSR      $PPCD
     C                   EXSR      $GTCD
     C                   EXSR      $TFCD
      *
      * Validate:
      *   Cell
      *   Pod
      *   Consolidated farm
      *
     C                   EXSR      $CELL
     C                   EXSR      $POD
     C                   EXSR      $CNFSCD
      *
      * Validate:
      *    G/L cost center
      *    Company relationship
      *
     C                   EXSR      $CJD
     C                   EXSR      $CRCD
      *
      * Validate:
      *    Feed mill business office
      *    Delivery zone
      *
     C                   EXSR      $FMBO
     C                   EXSR      $DZCD
      *
      * Validate:
      *    Farm site JDE address code
      *    Producer JDE address code
      *
     C                   EXSR      $FAJD
     C                   EXSR      $PAJD
     C                   ENDIF                                                  IF HERE
      *
      * IF THERE ARE NO ERRORS, ATTEMPT THE WRITE/UPDATE; OTHERWISE,
      * REDISPLAY THE SCREEN.  IF A RECORD HAS BEEN DELETED OUT FROM
      * UNDER THE USER (*IN62 IS ON), THEN THE ERROR MESSAGE WILL BE
      * ISSUED IN THE $UPDAT SUBROUTINE.  SO, ALWAYS PERFORM THIS CODE.
      *
     C     ERROR         IFEQ      0
     C                   EXSR      $UPDAT
     C                   ELSE
     C                   MOVEL     SCRN1         ROUTNE
     C                   ENDIF
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * VALIDATE FARM SITE
      *----------------------------------------------------------------
      *
     C     $FSCD         BEGSR
      *
     C     DFFSCD        IFEQ      0                                            IF BLANK
     C                   SETON                                        31
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVE      'HS09001'     MGID(E)
     C                   ENDIF
     C                   ELSE
      *
     C     DFFSCD        CHAIN(N)  HSP018                             91
     C     *IN91         IFEQ      *OFF                                         IF FOUND
     C                   SETON                                        31
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS00130'     MGID(E)
     C                   CLEAR                   MGW
     C                   MOVE      DFFSCD        XXALPHFSCD
     C                   MOVEA     XXALPHFSCD    MGW(1)
     C                   MOVEA     MGW           MGDT(E)
     C                   ENDIF
     C                   ENDIF                                                  IF FOUND
     C                   ENDIF                                                  IF BLANK
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * Edit Transporation Office code
      *---------------------------------------------------------------
      *
      * Validations:
      *  1) must be a valid transporation office
      *  2) must have an active status
      *
     C     $tocd         begsr
      *
     C                   move      *blank        dftonm
      *
     C                   select
     C                   when      dftocd = *blank
     C                   seton                                        49
     C                   if        error < maxmsg
     C                   add       1             error
     C                   eval      mgid(e) = 'HS09001'
     C                   endif
     C                   other
      *
     C     dftocd        chain     hsp109                             92
     C                   if        *in92 = *on                                  If bad office
     C                   seton                                        79
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS01196'     mgid(e)
     C                   clear                   mgw
     C                   movea     dftocd        mgw(1)
     C                   movea     mgw           mgdt(e)
     C                   endif
     C                   else
      *
      * Tran Office must be Active
      *
     C                   move      totonm        dftonm
      *
     C                   if        toaist <> 'A'
     C                   seton                                        49
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS01203'     mgid(e)
     C                   clear                   mgw
     C                   movea     dftocd        mgw(1)
     C                   movea     mgw           mgdt(e)
     C                   endif
     C                   endif
      *
     C                   endif                                                  If bad office
     C                   endsl
      *
     C                   endsr
      /EJECT
      *----------------------------------------------------------------
      * VALIDATE PRODUCTION TYPE
      *----------------------------------------------------------------
      *
     C     $PTCD         BEGSR
      *
     C                   MOVE      *BLANK        DFPTDS
     C     DFPTCD        IFEQ      *BLANK                                       IF NO TYPE
     C                   SETON                                        33
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS09001'     MGID(E)
     C                   ENDIF
     C                   ELSE
      *
     C     DFPTCD        CHAIN     HSP010                             90
     C     *IN90         IFEQ      *OFF                                         IF TYPE OK
     C                   MOVEL     PTPTDS        DFPTDS
     C                   ELSE
     C                   SETON                                        33
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS00180'     MGID(E)
     C                   CLEAR                   MGW
     C                   MOVEA     DFPTCD        MGW(1)
     C                   MOVEA     MGW           MGDT(E)
     C                   ENDIF
     C                   ENDIF                                                  IF TYPE OK
     C                   ENDIF                                                  IF NO TYPE
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * VALIDATE PRODUCTION PHASE
      *----------------------------------------------------------------
      *
     C     $PPCD         BEGSR
      *
     C                   MOVE      *BLANK        DFPPDS
     C     DFPPCD        IFEQ      *BLANK                                       IF NO PHASE
     C                   SETON                                        34
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS09001'     MGID(E)
     C                   ENDIF
     C                   ELSE
      *
     C     DFPPCD        CHAIN     HSP011                             90
     C     *IN90         IFEQ      *OFF                                         IF PHASE OK
     C                   MOVEL     PPPPDS        DFPPDS
     C                   ELSE
     C                   SETON                                        34
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS00170'     MGID(E)
     C                   CLEAR                   MGW
     C                   MOVEA     DFPPCD        MGW(1)
     C                   MOVEA     MGW           MGDT(E)
     C                   ENDIF
     C                   ENDIF                                                  IF PHASE OK
     C                   ENDIF                                                  IF NO PHASE
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * VALIDATE JDE FIELDS
      *----------------------------------------------------------------
      *
     C     $CJD          BEGSR
      *
      *
      * EDIT G/L COST CENTER AGAINST JDE COST CENTER MASTER
      *
     C                   MOVE      *BLANK        DFCCDS
     C     DFCJD         IFEQ      *BLANK
     C                   ELSE
     C                   MOVEL     DFCJD         INCJD
     C                   EXSR      $JDEC
      *
      * IF THERE IS AN ERROR WITH THE COST CENTER, SETON
      * INDICATOR; ELSE MOVE DESCRIPTION TO DISPLAY FIELD.
      *
     C     JDEFL         IFEQ      YES
     C                   SETON                                        44
     C                   ELSE
     C                   MOVEL     OUCCDS        DFCCDS
     C                   ENDIF
     C                   ENDIF
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * VALIDATE COMPANY RELATIONSHIP
      *----------------------------------------------------------------
      *
     C     $CRCD         BEGSR
      *
     C                   MOVE      *BLANK        DFCRDS
     C     DFCRCD        IFEQ      *BLANK                                       IF NO RELAT
     C                   SETON                                        35
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS09001'     MGID(E)
     C                   ENDIF
     C                   ELSE
      *
     C     DFCRCD        CHAIN     HSP012                             90
     C     *IN90         IFEQ      *OFF                                         IF RELAT OK
     C                   MOVEL     CRCRDS        DFCRDS
     C                   ELSE
     C                   SETON                                        35
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS00175'     MGID(E)
     C                   CLEAR                   MGW
     C                   MOVEA     DFCRCD        MGW(1)
     C                   MOVEA     MGW           MGDT(E)
     C                   ENDIF                                                  IF RELAT OK
     C                   ENDIF                                                  IF NO RELAT
     C                   ENDIF
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * VALIDATE CELL
      *----------------------------------------------------------------
      *
     C     $CELL         BEGSR
      *
     C     DFCELL        IFEQ      0                                            IF NO CELL
     C                   SETON                                        37
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS09001'     MGID(E)
     C                   ENDIF
     C                   ELSE
     C     DFCELL        IFLT      0                                            IF NEG CELL
     C                   SETON                                        37
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS09000'     MGID(E)
     C                   ENDIF
     C                   ENDIF                                                  IF NEG CELL
     C                   ENDIF                                                  IF NO CELL
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * VALIDATE POD
      *----------------------------------------------------------------
      *
      * Validations:
      *   1) required when phase is BGF
      *        cannot be negative
      *   2) must be zero when phase is not BGF
      *
      *
     C     $pod          begsr
      *
     C                   select
     C                   when      dfppcd = 'BGF  '
      *
     c                   select
     C                   when      dfpod = 0
     C                   seton                                        38
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS00182'     mgid(e)
     C                   endif
      *
     C                   when      dfpod < 0
     C                   seton                                        38
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS09000'     mgid(e)
     C                   endif
     c                   endsl
      *
     C                   when      (dfppcd = 'NUR  ' or dfppcd = 'GF   ')
     C                             and dfpod <> 0
     C                   seton                                        38
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS00183'     mgid(e)
     C                   endif
     C                   endsl
      *
     C                   endsr
      /EJECT
      *----------------------------------------------------------------
      * VALIDATE CONSOLIDATED FARM SITE CODE
      *----------------------------------------------------------------
      *
      * Validations:
      *   1) required
      *   2) must be a valid farm site
      *
      *
     C     $cnfscd       begsr
      *
     C                   move      *blank        dfcnfsnm
      *
     C                   select
     C                   when      dfcnfscd = 0
     C                   seton                                        36
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS09001'     mgid(e)
     C                   endif
      *
     C                   when      dfcnfscd < 0
     C                   seton                                        36
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS09000'     mgid(e)
     C                   endif
     C                   other
      *
     C     dfcnfscd      chain(n)  hsp018                             92
     C                   if        *in92 = *off                                 If hit
     C                   move      fsfsnm        dfcnfsnm
     C                   else
      *
     C                   if        mode = create and dfcnfscd = dffscd          If create
     C                   else
     C                   seton                                        36
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS00131'     mgid(e)
     C                   clear                   mgw
     C                   move      dfcnfscd      XXALPHFSCD
     C                   movea     XXALPHFSCD    mgw(1)
     C                   movea     mgw           mgdt(e)
     C                   endif
     C                   endif                                                  If create
      *
     C                   endif                                                  If hit
      *
     C                   endsl
      *
     C                   endsr
      /EJECT
      *----------------------------------------------------------------
      * VALIDATE GROUP TYPE
      *----------------------------------------------------------------
      *
     C     $GTCD         BEGSR
      *
     C                   MOVE      *BLANK        DFGTDS
     C     DFGTCD        IFEQ      *BLANK                                       IF NO TYPE
     C                   SETON                                        47
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS09001'     MGID(E)
     C                   ENDIF
     C                   ELSE
      *
     C     DFGTCD        CHAIN     HSP025                             90
     C     *IN90         IFEQ      *OFF                                         IF RELAT OK
     C                   MOVEL     GTGTDS        DFGTDS
      *
     C                   if        dfppcd = 'BGF  ' and dfgtcd = 'W'
     C                   seton                                        47
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS01169'     mgid(e)
     C                   clear                   mgw
     C                   movea     dfgtcd        mgw(1)
     C                   movea     mgw           mgdt(e)
     C                   endif
     C                   endif
      *
     C                   ELSE
     C                   SETON                                        47
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS00137'     MGID(E)
     C                   CLEAR                   MGW
     C                   MOVEA     DFGTCD        MGW(1)
     C                   MOVEA     MGW           MGDT(E)
     C                   ENDIF                                                  IF RELAT OK
     C                   ENDIF                                                  IF NO RELAT
     C                   ENDIF
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * VALIDATE TYPE OF FARM
      *----------------------------------------------------------------
      *
      * Validations:
      *   1) required
      *   2) must be a valid type
      *   3) cannot be wean-to-finish if user has set up default phase as BGF
      *
      *
     C     $tfcd         begsr
      *
     C                   move      *blank        dftfds
      *
     C                   select
     C                   when      dftfcd = *blank
     C                   seton                                        30
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS09001'     mgid(e)
     C                   endif
     C                   other
      *
     C     dftfcd        chain     hsp101                             92
     C                   if        *in92 = *on                                  If no hit
     C                   seton                                        30
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS01167'     mgid(e)
     C                   clear                   mgw
     C                   movea     dftfcd        mgw(1)
     C                   movea     mgw           mgdt(e)
     C                   endif
     C                   else
      *
     C                   move      tftfds        dftfds
      *
     C                   if        dftfcd = 'WTF  ' and dfppcd = 'BGF  '
     C                   seton                                        30
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS01168'     mgid(e)
     C                   clear                   mgw
     C                   movea     dftfcd        mgw(1)
     C                   movea     mgw           mgdt(e)
     C                   endif
     C                   endif
      *
     C                   endif                                                  If no hit
      *
     C                   endsl
      *
     C                   endsr
      /EJECT
      *----------------------------------------------------------------
      * VALIDATE FEED MILL BUSINESS OFFICE
      *----------------------------------------------------------------
      *
      * EDIT FEED MILL BUSINESS OFFICE
      *   FEED MILL IS REQUIRED AND DIVISION MUST EQ 'FEED '
      *
     C     $FMBO         BEGSR
      *
     C                   MOVE      *BLANK        DFFMDS
     C     DFFMBO        IFEQ      *BLANK                                       IF NO MILL
     C                   SETON                                        40
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS09001'     MGID(E)
     C                   ENDIF
     C                   ELSE
      *
     C     DFFMBO        CHAIN     HSP009                             90
     C     *IN90         IFEQ      *OFF
     C                   MOVEL     BOBODS        DFFMDS
     C                   ENDIF
     C     *IN90         IFEQ      *ON                                          IF BAD MILL
     C     BODVCD        ORNE      'FEED '
     C                   SETON                                        40
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS00113'     MGID(E)
     C                   CLEAR                   MGW
     C                   MOVEA     DFFMBO        MGW(1)
     C                   MOVEA     MGW           MGDT(E)
     C                   ENDIF
     C                   ENDIF                                                  IF BAD MILL
     C                   ENDIF                                                  IF NO MILL
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * VALIDATE DELIVERY ZONE
      *----------------------------------------------------------------
      *
      * EDIT FEED MILL DELIVERY ZONE  - REQUIRED
      *
     C     $DZCD         BEGSR
      *
     C                   MOVE      *BLANK        DFDZDS
     C     DFDZCD        IFEQ      *BLANK                                       IF NO ZONE
     C                   SETON                                        42
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS09001'     MGID(E)
     C                   ENDIF
     C                   ELSE
      *
     C     KL013         CHAIN     HSP013                             90
     C     *IN90         IFEQ      *OFF                                         IF BAD ZONE
     C                   MOVEL     DZDZDS        DFDZDS
     C                   ELSE
     C                   SETON                                        42
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS00191'     MGID(E)
     C                   CLEAR                   MGW
     C                   MOVEA     DFDZCD        MGW(1)
     C                   MOVEA     DFFMBO        MGW(6)
     C                   MOVEA     MGW           MGDT(E)
     C                   ENDIF
     C                   ENDIF                                                  IF BAD ZONE
     C                   ENDIF                                                  IF NO ZONE
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * VALIDATE FARM SITE JDE ADDRESS CODE
      *----------------------------------------------------------------
      *
     C     $FAJD         BEGSR
      *
      * EDIT FARM SITE JDE NUMBER AGAINST JDE ADDRESS MASTER
      * CALL THE GENERIC JDE VALIDATION PROGRAM WITH A SEARCH
      * TYPE OF 'F' FOR 'FACILITY'.
      *
     C                   MOVE      *BLANK        DFFJNM
     C     DFFAJD        IFNE      0                                            IF FARM JDE
     C                   Z-ADD     DFFAJD        INAJD
     C                   MOVEL(P)  'F'           INAT1
     C                   EXSR      $JDEV
      *
     C     JDEFL         IFEQ      YES
     C                   SETON                                        45
     C                   ELSE
     C                   MOVEL     OUALPH        DFFJNM
     C                   ENDIF
     C                   ENDIF                                                  IF FARM JDE
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * VALIDATE PRODUCER JDE ADDRESS CODE
      *----------------------------------------------------------------
      *
     C     $PAJD         BEGSR
      *
      * EDIT PRODUCER NO. AGAINST JDE ADDRESS MASTER
      * WITH SEARCH TYPE 'P' FOR PRODUCER
      *
     C                   MOVE      *BLANK        DFPJNM
     C     DFPAJD        IFNE      0
     C                   Z-ADD     DFPAJD        INAJD
     C                   MOVEL(P)  'P'           INAT1
     C                   EXSR      $JDEV
      *
     C     JDEFL         IFEQ      YES
     C                   SETON                                        46
     C                   ELSE
     C                   MOVEL     OUALPH        DFPJNM
     C                   ENDIF
     C                   ENDIF
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $JDEV - SUBROUTINE TO CALL THE EXTERNAL PROGRAM TO VALIDATE JDE
      *         ADDRESS NUMBERS.
      *----------------------------------------------------------------
      *
     C     $JDEV         BEGSR
      *
     C                   MOVEL     NO            JDEFL                          JDE ERRORS
      *
     C                   CALL      'HPJDEV'
     C                   PARM                    MGI                            MSG ID'S
     C                   PARM                    MGD                            MSG DATA FLD
     C                   PARM                    ERROR                          # ERR MSGS
     C                   PARM                    INAJD                          JDE ADDR #
     C                   PARM                    INAT1                          JDE ADDR TYP
     C                   PARM                    JDEFL                          ERROR FLAG
     C                   PARM      *BLANK        OUALPH                         JDE NAME
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $JDEC - SUBROUTINE TO CALL THE EXTERNAL PROGRAM TO VALIDATE JDE
      *         COST CENTERS.
      *----------------------------------------------------------------
      *
     C     $JDEC         BEGSR
      *
     C                   MOVE      *BLANK        OUCCDS
     C                   MOVEL     NO            JDEFL
      *
     C                   CALL      'HPJDEC'
     C                   PARM                    MGI                            MSG ID'S
     C                   PARM                    MGD                            MSG DATA FLD
     C                   PARM                    ERROR                          # ERR MSGS
     C                   PARM                    INCJD                          COST CENTER
     C                   PARM                    BOCOCD                         COMPANY
     C                   PARM                    OUCCDS                         C.C. DESCR
     C                   PARM                    JDEFL                          ERROR FLAG
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * Validate county
      *----------------------------------------------------------------
      *
      * Validations:
      *  1) not required
      *
      *
     C     $cnty         begsr
      *
     C                   select
     C                   when      dfcnty = *blank
     C                   seton                                        48
     C                   if        error < maxmsg
     C                   add       1             error
     C                   eval      mgid(e) = 'HS09001'
     C                   endif
     C                   other
      *
      * Trim leading blanks
      *
     C                   eval      dfcnty = %triml(dfcnty)
     C                   endsl
      *
     C                   endsr
      /EJECT
      *----------------------------------------------------------------
      * Validate state code
      *----------------------------------------------------------------
      *
      * Validate description
      *  1) required
      *  2) must be valid in J.D. Edwards
      *
      *
     C     $sacd         begsr
      *
     C                   eval      dfsanm = *blank
      *
     C                   select
     C                   when      dfsacd = *blank
     C                   seton                                        39
     C                   if        error < maxmsg
     C                   add       1             error
     C                   eval      mgid(e) = 'HS09001'
     C                   endif
     C                   other
      *
      * Trim leading blanks and call the J.D. Edwards validation program
      *
     C                   eval      dfsacd = %triml(dfsacd)
      *
     C                   call      'STATERET'
     C                   parm      *blank        xxretrn
     C                   parm      dfsacd        xxsacd
     C     dfsanm        parm      *blank        xxsanm
      *
     C                   if        xxretrn <> *blank
     C                   seton                                        39
     C                   if        error < maxmsg
     C                   add       1             error
     C                   eval      mgid(e) = 'HS09038'
     C                   clear                   mgw
     C                   movea     dfsacd        mgw(1)
     C                   movea     mgw           mgdt(e)
     C                   endif
     C                   endif
      *
     C                   endsl
      *
     C                   endsr
      /EJECT
      *----------------------------------------------------------------
      * $sanm - Retrieve state name from J.D. Edwards
      *----------------------------------------------------------------
      *
     C     $sanm         begsr
      *
     C                   call      'STATERET'
     C                   parm      *blank        xxretrn
     C                   parm      dfsacd        xxsacd
     C     dfsanm        parm      *blank        xxsanm
      *
     C                   if        xxretrn <> *blank
     C                   eval      dfsanm = 'UNKNOWN'
     C                   endif
      *
     C                   endsr
      /EJECT
      *----------------------------------------------------------------
      * $UPDAT - SUBROUTINE TO WRITE/UPDATE/DELETE DATABASE FILE
      *----------------------------------------------------------------
      *
     C     $UPDAT        BEGSR
      *
      * IF USER ACTION IS REVISING OR DELETING, DETERMINE THAT THE
      * RECORD HE IS ACCESSING STILL EXISTS PRIOR TO ATTEMPTING TO
      * UPDATE.  LOCK THE RECORD FOR UPDATE.
      *
     C     ERROR         IFEQ      0                                            IF OK
     C     ACTION        IFEQ      DELETE                                       IF D/R
     C     ACTION        OREQ      REVISE
     C     DFFSCD        CHAIN     HSP018                             93
     C     *IN93         IFEQ      *ON                                          IF NOT FOUND
     C                   SETON                                        62
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS00132'     MGID(E)
     C                   CLEAR                   MGW
     C                   MOVE      DFFSCD        XXALPHFSCD
     C                   MOVEA     XXALPHFSCD    MGW(1)
     C                   MOVEA     MGW           MGDT(E)
     C                   ENDIF
     C                   ENDIF                                                  IF NOT FOUND
     C                   ENDIF                                                  IF D/R
     C                   ENDIF                                                  IF OK
      *
      * PERFORM DELETION INTEGRITY CHECKS IF THE USER IS
      * DELETING AND THE FARM SITE RECORD STILL EXISTS.
      *
     C     ACTION        IFEQ      DELETE                                       IF DELETE
     C     *IN93         ANDEQ     *OFF
     C                   EXSR      $DLTCK
     C                   ENDIF                                                  IF DELETE
      *
      *  IF THERE ARE NO ERRORS,
      *  DELETE, REVISE OR CREATE THE RECORD
      *
     C     ERROR         IFEQ      0                                            IF NO ERR
      *
     C     ACTION        IFEQ      DELETE                                       IF DELETE
     C                   DELETE    FSREC
      *
      * P408 - Delete Farm Site Additional Info (HSP0181) Record.
P408 C     DFFSCD        CHAIN     HSP0181
  |  C                   If        %Found
  |  C                   Delete    FIREC
P408 C                   EndIf
      *
     C                   ELSE
      *
      *  MOVE DSPF FIELDS TO DATABASE FILE FIELDS
      *
     C                   MOVEL     SVFSBO        FSFSBO
     C                   Z-ADD     DFFSCD        FSFSCD
     C                   MOVEL     DFTOCD        FSTOCD
     C                   MOVEL     DFPTCD        FSPTCD
     C                   MOVEL     DFPPCD        FSPPCD
     C                   Z-ADD     DFPAJD        FSPAJD
     C                   Z-ADD     DFFAJD        FSFAJD
     C                   MOVEL     DFCRCD        FSCRCD
     C                   Z-ADD     DFCELL        FSCELL
     C                   Z-ADD     DFPOD         FSPOD
     C                   Z-ADD     DFCNFSCD      FSCNFSCD
     C                   MOVEL     DFGTCD        FSGTCD
     C                   MOVEL     DFCJD         FSCJD
     C                   MOVEL     DFFMBO        FSFMBO
     C                   MOVEL     DFDZCD        FSDZCD
     C                   MOVEL     DFMTEC        FSMTECHREF
     C                   MOVEL     DFFSNM        FSFSNM
     C                   MOVEL     DFSACD        FSSACD
     C                   MOVEL     DFCNTY        FSCNTY
     C                   MOVEL     DFTFCD        FSTFCD
      *
     C     ACTION        IFEQ      CREATE                                       IF CREATE
     C                   MOVEL     SVRGCD        FSRGCD
     C                   MOVEL     SVDVCD        FSDVCD
     C                   MOVEL     SVCOCD        FSCOCD
      *
      * Hardcode managers when creating new farms to be 'unassigned'.
      *
     C                   MOVE      '999  '       FSCMGCD
     C                   MOVE      '999  '       FSPMGCD
     C                   MOVE      '999  '       FSMMGCD
      *
      * Set status to active
      *
     C                   MOVE      'A'           FSAIST
      *
     C                   WRITE     FSREC
      *
      * P408 - Write Farm Site Additional Info (HSP0181) Record.
P408 C     DFFSCD        CHAIN     HSP0181
  |  C                   If        Not %Found
  |  C                   Z-Add     DFFSCD        FIFSCD
  |  C                   MOVEL     DFPRM         FIPRCD
  |  C                   WRITE     FIREC
P408 C                   EndIf
      *
     C                   ELSE
     C                   UPDATE    FSREC
      *
      * P408 - Update Farm Site Additional Info (HSP0181) Record.
P408 C     DFFSCD        CHAIN     HSP0181
  |  C                   If        %Found
  |  C                   MOVEL     DFPRM         FIPRCD
  |  C                   UPDATE    FIREC
P408 C                   EndIf
      *
     C                   if        version = 'PROD' or version = '    '
     C                   call      'HP159'
     C                   parm                    fsfscd
     C                   parm                    fsfsnm
     C                   endif
      *
     C                   ENDIF                                                  IF CREATE
     C                   ENDIF                                                  IF DELETE
     C                   ENDIF                                                  IF NO ERR
      *
      *  IF THERE ARE ANY ERRORS, REDISPLAY THE SCREEN; OTHERWISE, EXIT
      *  THE PROGRAM AUTOMATICALLY. (THE USER DOES NOT HAVE TO TAKE
      *  ANY FUNCTION KEY TO EXIT)
      *
     C     ERROR         IFGT      0
     C                   MOVEL     SCRN1         ROUTNE
     C                   ELSE
     C                   MOVEL     EXIT          ROUTNE
     C                   ENDIF
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * $DLTCK - DELETION INTEGRITY CHECKS                               *
      *---------------------------------------------------------------
      *
     C     $DLTCK        BEGSR
      *
      * CANNOT DELETE FARM SITE IF HOG GROUPS EXIST.
      *
     C     DFFSCD        CHAIN     HSL034A                            94
     C     *IN94         IFEQ      *OFF                                         IF GROUPS
     C                   SETON                                        62
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS00134'     MGID(E)
     C                   CLEAR                   MGW
     C                   MOVE      DFFSCD        XXALPHFSCD
     C                   MOVEA     XXALPHFSCD    MGW(1)
     C                   MOVEA     MGW           MGDT(E)
     C                   ENDIF
     C                   ENDIF                                                  IF GROUPS
      *
      * CANNOT DELETE FARM SITE IF IT IS IN FARM COST FILE
      *
     C     DFFSCD        CHAIN     HSP028                             94
     C     *IN94         IFEQ      *OFF                                         IF FARM COST
     C                   SETON                                        62
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS00507'     MGID(E)
     C                   CLEAR                   MGW
     C                   MOVE      DFFSCD        XXALPHFSCD
     C                   MOVEA     XXALPHFSCD    MGW(1)
     C                   MOVEA     MGW           MGDT(E)
     C                   ENDIF
     C                   ENDIF                                                  IF FARM COST
      *
      * CANNOT DELETE FARM SITE IF EXPENSE TICKET EXISTS
      *
     C     DFFSCD        CHAIN     HSL048F                            94
     C     *IN94         IFEQ      *OFF                                         IF EXPENSES
     C                   SETON                                        62
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS00511'     MGID(E)
     C                   CLEAR                   MGW
     C                   MOVE      DFFSCD        XXALPHFSCD
     C                   MOVEA     XXALPHFSCD    MGW(1)
     C                   MOVEA     MGW           MGDT(E)
     C                   ENDIF
     C                   ENDIF                                                  IF EXPENSES
      *
      * CANNOT DELETE FARM SITE IF FEED TICKET DETAIL EXISTS
      *
     C     DFFSCD        CHAIN     HSL038N                            94
     C     *IN94         IFEQ      *OFF                                         IF FEED DTL
     C                   SETON                                        62
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS00509'     MGID(E)
     C                   CLEAR                   MGW
     C                   MOVE      DFFSCD        XXALPHFSCD
     C                   MOVEA     XXALPHFSCD    MGW(1)
     C                   MOVEA     MGW           MGDT(E)
     C                   ENDIF
     C                   ENDIF                                                  IF FEED DTL
      *
      * CANNOT DELETE FARM SITE IF KILLED/DEAD HOGS EXIST
      *
     C     DFFSCD        CHAIN     HSL068C                            94
     C     *IN94         IFEQ      *OFF                                         IF DEAD HOGS
     C                   SETON                                        62
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS00686'     MGID(E)
     C                   CLEAR                   MGW
     C                   MOVE      DFFSCD        XXALPHFSCD
     C                   MOVEA     XXALPHFSCD    MGW(1)
     C                   MOVEA     MGW           MGDT(E)
     C                   ENDIF
     C                   ENDIF                                                  IF DEAD HOGS
      *
      * CANNOT DELETE FARM SITE IF INVENTORY ADJUSTMENTS EXIST
      *
     C     DFFSCD        CHAIN     HSL069D                            94
     C     *IN94         IFEQ      *OFF                                         IF ADJUSTS
     C                   SETON                                        62
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS00687'     MGID(E)
     C                   CLEAR                   MGW
     C                   MOVE      DFFSCD        XXALPHFSCD
     C                   MOVEA     XXALPHFSCD    MGW(1)
     C                   MOVEA     MGW           MGDT(E)
     C                   ENDIF
     C                   ENDIF                                                  IF ADJUSTS
      *
      * CANNOT DELETE FARM SITE IF SALES MOVEMENTS EXIST
      *
     C     DFFSCD        CHAIN     HSL084C                            94
     C     *IN94         IFEQ      *OFF                                         IF SALES
     C                   SETON                                        62
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS00688'     MGID(E)
     C                   CLEAR                   MGW
     C                   MOVE      DFFSCD        XXALPHFSCD
     C                   MOVEA     XXALPHFSCD    MGW(1)
     C                   MOVEA     MGW           MGDT(E)
     C                   ENDIF
     C                   ENDIF                                                  IF SALES
      *
      * CANNOT DELETE FARM SITE IF IT HAS BEEN USED AS AN
      * ORIGIN OR DESTINATION FARM SITE ON A TRANSFER MOVEMENT.
      * (NOTE: ONLY ISSUE ONE MESSAGE IF THE FARM HAS BEEN USED
      * AS BOTH AN ORIGIN AND A DESTINATION FARM SITE)
      *
     C     DFFSCD        CHAIN     HSL074C                            94
     C     *IN94         IFEQ      *OFF                                         IF ORIGIN
     C                   SETON                                        62
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS00689'     MGID(E)
     C                   CLEAR                   MGW
     C                   MOVE      DFFSCD        XXALPHFSCD
     C                   MOVEA     XXALPHFSCD    MGW(1)
     C                   MOVEA     MGW           MGDT(E)
     C                   ENDIF
     C                   ELSE
      *
      * CANNOT DELETE FARM SITE IF IT HAS BEEN USED AS A
      * DESTINATION FARM SITE ON A TRANSFER MOVEMENT
      *
     C     DFFSCD        CHAIN     HSL074D                            94
     C     *IN94         IFEQ      *OFF                                         IF DEST
     C                   SETON                                        62
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS00689'     MGID(E)
     C                   CLEAR                   MGW
     C                   MOVE      DFFSCD        XXALPHFSCD
     C                   MOVEA     XXALPHFSCD    MGW(1)
     C                   MOVEA     MGW           MGDT(E)
     C                   ENDIF
     C                   ENDIF                                                  IF DEST
     C                   ENDIF                                                  IF ORIGIN
      *
      * CANNOT DELETE FARM SITE IF INSTALLED EQUIPMENT EXISTS
      *
     C     DFFSCD        CHAIN     HSP023                             96
     C     *IN96         IFEQ      *OFF                                         IF EQUIP
     C                   SETON                                        62
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS00178'     MGID(E)
     C                   CLEAR                   MGW
     C                   MOVE      DFFSCD        XXALPHFSCD
     C                   MOVEA     XXALPHFSCD    MGW(1)
     C                   MOVEA     MGW           MGDT(E)
     C                   ENDIF
     C                   ENDIF                                                  IF EQUIP
      *
      * CANNOT DELETE FARM SITE IF ROOMS EXIST
      *
     C     DFFSCD        CHAIN     HSP020                             95
     C     *IN95         IFEQ      *OFF                                         IF ROOMS
     C                   SETON                                        62
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS00177'     MGID(E)
     C                   CLEAR                   MGW
     C                   MOVE      DFFSCD        XXALPHFSCD
     C                   MOVEA     XXALPHFSCD    MGW(1)
     C                   MOVEA     MGW           MGDT(E)
     C                   ENDIF
     C                   ENDIF                                                  IF ROOMS
      *
      * CANNOT DELETE FARM SITE IF BUILDINGS EXIST
      *
     C     DFFSCD        CHAIN     HSP019                             94
     C     *IN94         IFEQ      *OFF                                         IF BLDGS
     C                   SETON                                        62
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS00176'     MGID(E)
     C                   CLEAR                   MGW
     C                   MOVE      DFFSCD        XXALPHFSCD
     C                   MOVEA     XXALPHFSCD    MGW(1)
     C                   MOVEA     MGW           MGDT(E)
     C                   ENDIF
     C                   ENDIF                                                  IF BLDS
      *
      *
      * CANNOT DELETE FARM SITE IF BINS EXIST
      *
     C     DFFSCD        CHAIN     HSP036                             94
     C     *IN94         IFEQ      *OFF                                         IF BIN
     C                   SETON                                        62
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS00512'     MGID(E)
     C                   CLEAR                   MGW
     C                   MOVE      DFFSCD        XXALPHFSCD
     C                   MOVEA     XXALPHFSCD    MGW(1)
     C                   MOVEA     MGW           MGDT(E)
     C                   ENDIF
     C                   ENDIF                                                  IF BIN
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * F4 PROMPT ON SCREEN 1                                    *
      *---------------------------------------------------------------
      *
     C     $F4S1         BEGSR
      *
     C                   SELECT
      *
      * Transportation office
      *
     C                   when      fld = 'DFTOCD'
     C                   call      'HP5026'
     C                   parm      'A'           xxaist
     C                   parm      *blank        xxtocd
     C                   parm      *blank        xxtonm
     C                   seton                                        79
     C                   if        xxtocd <> *blank
     C                   movel     xxtocd        dftocd
     C                   movel     xxtonm        dftonm
     C                   endif
      * IF PRODUCTION TYPE
      *
     C     FLD           WHENEQ    'DFPTCD'
     C                   CALL      'HP504'
     C                   PARM      *BLANK        XXPTCD
     C                   PARM      *BLANK        XXPTDS
     C                   SETON                                        53
     C     XXPTCD        IFNE      *BLANK                                       IF SELECTED
     C                   MOVEL     XXPTCD        DFPTCD
     C                   MOVEL     XXPTDS        DFPTDS
     C                   ENDIF                                                  IF SELECTED
      *
      * IF PRODUCTION PHASE
      *
     C     FLD           WHENEQ    'DFPPCD'
     C     *IN63         ANDEQ     *OFF
     C                   CALL      'HP507'
     C                   PARM      *BLANK        XXPPCD
     C                   PARM      *BLANK        XXPPDS
     C                   SETON                                        54
     C     XXPPCD        IFNE      *BLANK                                       IF SELECTED
     C                   MOVEL     XXPPCD        DFPPCD
     C                   MOVEL     XXPPDS        DFPPDS
     C                   ENDIF                                                  IF SELECTED
      *
      *
      * TYPE OF FARM
      *
     C     FLD           WHENEQ    'DFTFCD'
     C                   CALL      'HP5018'
     C                   PARM      *BLANK        XXTFCD
     C                   PARM      *BLANK        XXTFDS
     C                   SETON                                        70
     C     XXTFCD        IFNE      *BLANK
     C                   MOVEL     XXTFCD        DFTFCD
     C                   MOVEL     XXTFDS        DFTFDS
     C                   ENDIF                                                  IF SELECTED
      *
      * IF PRODUCER ADDRESS NO. FROM JDE
      *
     C     FLD           WHENEQ    'DFPAJD'
     C                   MOVEL(P)  'P'           INAT1
      *
     C                   CALL      'HP513'
     C                   PARM                    BOCOCD                         COMPANY
     C                   PARM                    INAT1                          SEARCH TYPE
     C                   PARM      *ZERO         OUAJD                          JDE NUMBER
     C                   PARM      *BLANK        OUALPH                         JDE NAME
      *
     C                   SETON                                        51
     C     OUAJD         IFNE      0                                            IF SELECTED
     C                   Z-ADD     OUAJD         DFPAJD
     C                   MOVEL     OUALPH        DFPJNM
     C                   ENDIF                                                  IF SELECTED
      *
      * IF G/L COST CENTER
      *
     C     FLD           WHENEQ    'DFCJD'
     C                   CALL      'HP514'
     C                   PARM                    BOCOCD
     C                   PARM      *BLANK        OUCJD
     C                   PARM      *BLANK        OUCCDS
     C                   SETON                                        52
     C     OUCJD         IFNE      *BLANK                                       IF SELECTED
     C                   MOVEL     OUCJD         DFCJD
     C                   MOVEL     OUCCDS        DFCCDS
     C                   ENDIF                                                  IF SELECTED
      *
      * IF COMPANY RELATIONSHIP
      *
     C     FLD           WHENEQ    'DFCRCD'
     C                   CALL      'HP506'
     C                   PARM      *BLANK        XXCRCD
     C                   PARM      *BLANK        XXCRDS
     C                   SETON                                        55
     C     XXCRCD        IFNE      *BLANK                                       IF SELECTED
     C                   MOVEL     XXCRCD        DFCRCD
     C                   MOVEL     XXCRDS        DFCRDS
     C                   ENDIF                                                  IF SELECTED
      *
      * GROUP TYPE
     C                   when      FLD = 'DFGTCD'
     C                   CALL      'HP531'
     C                   PARM      *blank        XXGTCD
     C                   PARM      *blank        XXGTDS
     C                   seton                                        57
     C                   if        XXGTCD <> *blank                             IF SELECTED
     C                   move      XXGTCD        DFGTCD
     C                   move      XXGTDS        DFGTDS
     C                   endif                                                  IF SELECTED
      *
      * Consolidated Farm Site
      *
     C     FLD           WHENEQ    'DFCNFSCD'
     C                   CALL      'HP505'
     C                   PARM      SVFSBO        XXFSBO
     C                   PARM      0             XXFSCD
     C                   PARM      *blank        XXFSNM
     C                   PARM      DFPPCD        XXPPCD
     C                   PARM      DFPTCD        XXPTCD
     C                   SETON                                        58
     C     XXFSCD        IFNE      0                                            IF SELECTED
     C                   Z-ADD     XXFSCD        DFCNFSCD
     C                   MOVE      XXFSNM        DFCNFSNM
     C                   ENDIF                                                  IF SELECTED
      *
      *
      * IF FEED MILL B.O.
      *
     C     FLD           WHENEQ    'DFFMBO'
     C                   CALL      'HP503'
     C                   PARM      *BLANK        XXFMBO
     C                   PARM      *BLANK        XXFMDS
     C                   PARM                    WKDVCD
     C                   SETON                                        58
     C     XXFMBO        IFNE      *BLANK                                       IF SELECTED
     C                   MOVEL     XXFMBO        DFFMBO
     C                   MOVEL     XXFMDS        DFFMDS
     C                   ENDIF                                                  IF SELECTED
      *
      * IF FEED MILL DELIVERY ZONE
      *
     C     FLD           WHENEQ    'DFDZCD'
     C                   CALL      'HP511'
     C                   PARM      *BLANK        XXDZCD
     C                   PARM      *BLANK        XXDZDS
     C                   PARM                    DFFMBO
     C                   SETON                                        59
     C     XXDZCD        IFNE      *BLANK                                       IF SELECTED
     C                   MOVEL     XXDZCD        DFDZCD
     C                   MOVEL     XXDZDS        DFDZDS
     C                   ENDIF                                                  IF SELECTED
      *
      * IF FARM SITE, RETRIEVE J.D. EDWARDS 'FACILITY' NUMBER
      *
     C     FLD           WHENEQ    'DFFAJD'
     C     *IN63         ANDEQ     *OFF
     C                   MOVEL(P)  'F'           INAT1
      *
     C                   CALL      'HP513'
     C                   PARM                    BOCOCD                         COMPANY
     C                   PARM                    INAT1                          SEARCH TYPE
     C                   PARM      *ZERO         OUAJD                          JDE NUMBER
     C                   PARM      *BLANK        OUALPH                         JDE NAME
      *
     C                   SETON                                        50
     C     OUAJD         IFNE      0                                            IF SELECTED
     C                   Z-ADD     OUAJD         DFFAJD
     C                   MOVEL     OUALPH        DFFJNM
     C                   ENDIF                                                  IF SELECTED
     C                   OTHER
      *
      * F4 NOT VALID FOR THIS FIELD SO ISSUE MESSAGE
      *
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR                                        013
     C                   MOVEL     'HS09011'     MGID(E)
     C                   ENDIF
      *
     C                   ENDSL
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * $WRMSG - SUBROUTINE TO WRITE ERROR MESSAGES
      *---------------------------------------------------------------
      *
     C     $WRMSG        BEGSR
      *
     C                   CALL      'UT80045J'                           43
     C                   PARM                    MGID
     C                   PARM                    MGDT
     C                   PARM                    ERROR
     C                   PARM      SDPGM         PGM
     C                   PARM      MSGFIL        MSGFL
      *
      * IF CALL TO UT80045J FAILED, A MESSAGE HARDCODED IN THE DSPF
      * COMMAND KEY RECORD WILL BE SET ON. SO, REDISPLAY SCREEN TO USER
      *
     C     *IN43         IFEQ      *ON
     C                   MOVEL     SET1          ROUTNE
     C                   ENDIF
      *
     C                   ENDSR
      *
      *----------------------------------------------------------------
      * $CLMSG - SUBROUTINE TO CLEAR MESSAGES
      *----------------------------------------------------------------
      *
     C     $CLMSG        BEGSR
      *
     C                   CALL      'UT80045J'                           43
     C                   PARM      *BLANK        MGI
     C                   PARM      *BLANK        MGD
     C                   PARM      *ZERO         ERROR
     C                   PARM      SDPGM         PGM
     C                   PARM      MSGFIL        MSGFL
      *
      * IF CALL TO UT80045J FAILED, A MESSAGE HARD CODED IN THE DSPF
      * COMMAND KEY RECORD WILL BE SET ON. SO, REDISPLAY SCREEN TO USER
      *
     C     *IN43         IFEQ      *ON
     C                   MOVEL     SET1          ROUTNE
     C                   ENDIF
      *
     C                   ENDSR
      *
      *----------------------------------------------------------------
      * $EXIT - SUBROUTINE TO SET ON LR AND END JOB
      *----------------------------------------------------------------
      *
     C     $EXIT         BEGSR
     C                   SETON                                        LR
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * *INZSR - INITIALIZATION SUBROUTINE
      *---------------------------------------------------------------
      *
     C     *INZSR        BEGSR
      *
      * KEY LISTS
      *
      * HSP013  FEED MILL DELIVERY ZONE
      *
     C     KL013         KLIST
     C                   KFLD                    DFFMBO
     C                   KFLD                    DFDZCD
      *
      * PARM LISTS
      *     MODE IS THE MODE COMING IN FROM THE WORK WITH.
      *     THE USER CAN COME IN IN REVISE MODE AND TAKE F11=DELETE
      *     WHICH WILL CHANGE THE PROCESSING TO THE 'DELETE' ACTION.
      *     DFFSCD IS THE FARM SITE PASSED IN FROM THE WORK WITH
      *
     C     *ENTRY        PLIST
     C                   PARM                    MODE
     C                   PARM                    SVFSBO
     C                   PARM                    DFFSCD
     C                   PARM                    VERSION           4
     C                   PARM                    RTNCD
      *
      * Both 'accounting' and 'production' have access to this function.
      * The input-capable fields are extremely limited on the 'production' version.
      *
     C                   select
     C                   when      version = 'ACCT'
     C                   eval      dfversion = 'Accounting Version'
      *
     C                   when      version = 'PROD'
     C                   eval      dfversion = 'Production Version'
      *
     C                   when      version = '    '
     C                   endsl
      *
     C                   MOVEL     MODE          DFMODE
      *
     C     *LIKE         DEFINE    BORGCD        SVRGCD
     C     *LIKE         DEFINE    BODVCD        SVDVCD
     C     *LIKE         DEFINE    BOCOCD        SVCOCD
     C     *LIKE         DEFINE    FSFSBO        SVFSBO
      *
      * PARMS FOR F4=PROMPT CALLS AND MESSAGES
      *
     C     *LIKE         DEFINE    totocd        xxtocd
     C     *LIKE         DEFINE    totonm        xxtonm
     C     *LIKE         DEFINE    dftfcd        xxtfcd
     C     *LIKE         DEFINE    dftfds        xxtfds
     C     *LIKE         DEFINE    fsfsbo        xxfsbo
     C     *LIKE         DEFINE    dffsnm        xxfsnm
     C     *LIKE         DEFINE    dfsacd        xxsacd
     C     *LIKE         DEFINE    dfsanm        xxsanm
     C                   move      *blank        xxaist            1
     C                   move      *blank        xxretrn           7
     C                   MOVE      *BLANK        XXALPHFSCD        5
     C     *LIKE         DEFINE    DFFSCD        xxfscd
     C     *LIKE         DEFINE    PTPTCD        XXPTCD
     C     *LIKE         DEFINE    PTPTDS        XXPTDS
     C     *LIKE         DEFINE    PPPPCD        XXPPCD
     C     *LIKE         DEFINE    PPPPDS        XXPPDS
     C     *LIKE         DEFINE    CRCRCD        XXCRCD
     C     *LIKE         DEFINE    CRCRDS        XXCRDS
     C     *LIKE         DEFINE    BOBOBO        XXFMBO
     C     *LIKE         DEFINE    BOBODS        XXFMDS
     C     *LIKE         DEFINE    DZDZCD        XXDZCD
     C     *LIKE         DEFINE    DZDZDS        XXDZDS
     C     *LIKE         DEFINE    GTGTCD        XXGTCD
     C     *LIKE         DEFINE    GTGTDS        XXGTDS
      *
      * GLOBAL DEFINES
      *
     C                   MOVE      *BLANK        PGM              10            USED IN MSG
     C                   MOVE      *BLANK        MSGFL            10            USED IN MSG
     C                   MOVE      *BLANK        ROUTNE            6
     C     *LIKE         DEFINE    DFMODE        ACTION
     C     *LIKE         DEFINE    DFMODE        MODE
     C                   MOVE      MODE          ACTION
     C     *LIKE         DEFINE    FSDVCD        WKDVCD
     C                   MOVEL     'FEED '       WKDVCD
     C                   MOVE      *BLANK        JDEFL             1            JDE ERR FLAG
     C                   MOVE      *BLANK        INAT1             3            JDE ADDR TYP
     C                   MOVE      *BLANK        OUALPH           40            JDE NAME
     C     *LIKE         DEFINE    FSFAJD        INAJD                          JDE ADDR IN
     C     *LIKE         DEFINE    DFCCDS        OUCCDS                         COST CNTR DESC
     C     *LIKE         DEFINE    FSFAJD        OUAJD                          JDR ADDR OUT
     C     *LIKE         DEFINE    FSCJD         OUCJD                          COST CENTER SC
     C     *LIKE         DEFINE    FSCJD         INCJD                          COST CENTER SC
      *
      * INITIALIZE RETURN CODE
      *
     C                   MOVEL     '00'          RTNCD             2            RETURN CODE
      *
      * RETRIEVE BUSINESS OFFICE FIELDS
      *
     C     SVFSBO        CHAIN     HSP009                             90
     C     *IN90         IFEQ      *OFF
     C                   MOVEL     BORGCD        SVRGCD
     C                   MOVEL     BODVCD        SVDVCD
     C                   MOVEL     BOCOCD        SVCOCD
     C                   ENDIF
      *
      * THE FOLLOWING STANDARD CODE MUST BE INCLUDED TO MAKE THE
      * STANDARD ERROR MESSAGE HANDLING PROGRAM FUNCTION PROPERLY.
      * THIS CODE SETS AND CLEARS THE PROGRAM MESSAGE QUEUE.
      *
     C                   Z-ADD     20            MAXMSG            2 0
     C                   MOVEL     '*'           MSGPGM                         SET QUEUE
     C                   EXSR      $CLMSG                                       CLEAR MSG
      *
     C                   ENDSR
      /EJECT
