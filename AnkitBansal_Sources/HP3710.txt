      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Hog Production System
      * PROGRAM:     HP3710
      * TITLE:       Create Inactive Annual Budgets with Listing
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     02/23/04
      *
      *            This function creates Annual Farm Budgets and prints a listing.
      *            The budgets are created with a status of Inactive.
      *
      *            The function is submitted from HP4710-Specify Options for Creating
      *            Annual Budgets.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 10/13/04  LeAnne Fedor
      *           Recompile only. Cost Per Unit Amount was changed from
      *           9,2 to 9,4.
      *
      * 10/03/05  LeAnne Fedor
      *           Removed all/any logic that limited processing to Non-Zero
      *           Budgeted Quantities. The users now want to include items
      *           with budgeted quantities of zero in all processing.
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fhsp018    if   e           k disk
      *    Farm sites
      *
      *
     Fhsl185b   if   e           k disk
      *    Budget template header
      *
      *
     Fhsp186    if   e           k disk
      *    Budget template detail
      *
      *
      *
     Fhsp187    if   e           k disk
      *    Budget template/farm links
      *
      *
     Fhsl188g   if   e           k disk    rename(jhrec:jhrecg)
      *    Farm budget header
      *
      *
     Fhsl188i   if   e           k disk    rename(jhrec:jhreci)
      *    Farm budget header
      *
      *
     Fhsp188    o    e           k disk
      *    Farm budget header
      *
      *
     Fhsp189    o    e           k disk
      *    Farm budget detail
      *
      *
     Fqprint    o    f   90        printer oflind(*inof)
      *
      /EJECT
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      *
     D crtmsg          c                   const('The following Inactive Annual-
     D                                      budgets were created:')
      *
     D hadmsg          c                   const('The following farms already h-
     D                                     ad Inactive Annual budgets:')
      *
     D baddat          c                   const('There were date conflicts wit-
     D                                     h Active Budgets for farms:')
      *
      *
      ****************************************************************
      * ARRAYS AND TABLES
      ****************************************************************
      *
     D ifscd           s              5  0 dim(900)
     D ifbsn           s              7  0 dim(900)
      *
     D afscd           s              5  0 dim(900)
     D afbsn           s              7  0 dim(900)
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Standard fields
      *
     D dash            s             90    inz(*all'-')
     d rtime           s              6  0
      *
      *
      * Control fields
      *
     D procfl          s              1    inz('Y')
     D first           s              1    inz('Y')
      *
      *
      * Print fields
      *
     D l1fscd          s                   like(jhfscd)
     D l1fbsn          s                   like(jhfbsn)
     D l1msg           s             56
      *
      *
      * Array indexes
      *
     D x               s              3  0
     D y               s              3  0
      *
     D w               s              3  0
     D z               s              3  0
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *---------------------------------------------------------------
      * Local data area.
      *---------------------------------------------------------------
      *
     Dlda             uds                  dtaara(*lda)
     D  ldfmdy                 1      6  0
     D  ldtmdy                 7     12  0
     D  ldfcymd               13     20  0
     D  ldtcymd               21     28  0
      *
      *---------------------------------------------------------------------------------
      * Definition for external data area 'DAFBSN' for assigning the next budget number
      *---------------------------------------------------------------------------------
      *
     Dnextfbsn         s              7  0 dtaara(dafbsn)
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      *******************************************************************************
      * MAINLINE
      *******************************************************************************
      *
      * Prepare to read all Annual Budget Templates in the Budget Template header
      * file.
      *
     C     'A'           setll     hsl185b
      *
      * If the Annual Template is Active, continue.
      *
     C                   dou       *in90 = *on                                  Main do
     C     'A'           reade     hsl185b                                90
     C                   if        *in90 = *off and thaist = 'A'                If not EOF
     C                   move      yes           procfl
      *
      * Print the Headings
      *
     C                   exsr      $h1hdr
      *
      * See if any Farms are linked to the Template. If not, print message.
      *
     C     thbtcd        chain     hsp187                             92
     C                   if        *in92 = *on
     C                   move      no            procfl
     C                   except    nolink
     C                   endif
      *
      * See if any Items exist for the Template. If not, print message.
      *
     C     thbtcd        chain     hsp186                             92
     C                   if        *in92 = *on
     C                   move      no            procfl
     C                   except    noitem
     C                   endif
      *
      * So, if OK so far, process each Farm that is linked to this
      * Template.
      *
     C                   if        procfl = yes
     C                   exsr      $links
     C                   endif
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Main do
      *
      *-------------------
      * EOF processing
      *-------------------
      *
     C                   seton                                        lr
      /eject
      *-------------------------------------------------------------------------------------
      * Read/process each Farm that is linked to this Template
      *-------------------------------------------------------------------------------------
      *
     C     $links        begsr
      *
     C     thbtcd        setll     hsp187
      *
     C                   dou       *in91 = *on                                  Do farms
     C     thbtcd        reade     hsp187                                 91
     C                   if        *in91 = *off                                 If not EOF
     C                   move      yes           procfl
      *
      * If an Inactive budget already exists for this farm/template,
      *     write the farm and budget number to arrays for later printing
      *
     C     key01         chain     hsl188g                            92
     C                   if        *in92 = *off                                 If has 'I'
     C                   move      no            procfl
     c                   exsr      $iarr
     C                   endif                                                  If has 'I'
      *
      * Check that the dates do not conflict with an existing Active
      * Budget--if there is one.
      *
     C     key01         chain     hsl188i                            92
     C                   if        *in92 = *off                                 If conflict
      *
     C                   if        (ldfcymd >= jhbfdt and ldfcymd <= jhbtdt) or
     C                             (ldtcymd >= jhbfdt and ldtcymd <= jhbtdt)
     C                   move      no            procfl
     c                   exsr      $aarr
     C                   endif
      *
     C                   endif                                                  If conflict
      *
      * There are no problems. So, go make a budget.
      *
     C                   if        procfl = yes
     C                   exsr      $items
     C                   endif
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Do farms
      *
      * Now, if you had any farms that already had budgets, print those.
      *
     C                   if        x > 0
     C                   exsr      $prtarray1
     C                   endif
      *
     C                   if        w > 0
     C                   exsr      $prtarray2
     C                   endif
      *
     C                   exsr      $clear
      *
     C                   endsr
      /eject
      *-------------------------------------------------------------------------------------
      * Read all Budget Items for the Template
      *-------------------------------------------------------------------------------------
      *
     C     $items        begsr
      *
     C                   move      yes           first
     C     thbtcd        setll     hsp186
      *
     C                   dou       *in93 = *on                                  Do items
     C     thbtcd        reade     hsp186                                 93
     C                   if        *in93 = *off                                 If not EOF
      *
      * Write a Farm Budget Header record and print the first time thru.
      *
     C                   if        first = yes
     C                   exsr      $wrt188
     C                   z-add     tlfscd        l1fscd
     C                   z-add     jhfbsn        l1fbsn
     C                   eval      l1msg = crtmsg
     C                   exsr      $farm
     C                   move      no            first
     C                   endif
      *
      * Always write a Farm Budget Detail record
      *
     C                   z-add     jhfbsn        jdfbsn
     C                   z-add     jhfscd        jdfscd
     C                   move      jhbtcd        jdbtcd
     C                   move      tdbgit        jdbgit
     C                   z-add     tdbtqt        jdbgqt
     C                   z-add     0             jdcpuam
     C                   z-add     0             jdvol
     C                   z-add     0             jdfbam
      *
     C                   write     jdrec
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Do farms
      *
     C                   endsr
      /eject
      *-------------------------------------------------------------------------------------
      * Build an array of Farm/budgets that already have Inactive Budgets
      *-------------------------------------------------------------------------------------
      *
     C     $iarr         begsr
      *
     C                   z-add     1             x
     C     jhfscd        lookup    ifscd(x)                               92
     C                   if        *in92 = *off                                 If not there
     C                   add       1             y
     C                   z-add     jhfscd        ifscd(y)
     C                   z-add     jhfbsn        ifbsn(y)
     C                   endif                                                  If not there
      *
     C                   endsr
      *
      *-------------------------------------------------------------------------------------
      * Build an array of Farm/budgets where the dates conflict with Active Budgets
      *-------------------------------------------------------------------------------------
      *
     C     $aarr         begsr
      *
     C                   z-add     1             w
     C     jhfscd        lookup    afscd(w)                               92
     C                   if        *in92 = *off                                 If not there
     C                   add       1             z
     C                   z-add     jhfscd        afscd(z)
     C                   z-add     jhfbsn        afbsn(z)
     C                   endif                                                  If not there
      *
     C                   endsr
      /eject
      *-------------------------------------------------------------------------------------
      * Write a record to the Farm Budget Header file
      *-------------------------------------------------------------------------------------
      *
     C     $wrt188       begsr
      *
      * Get the next system-assigned 'farm budget number' from the Data Area
      *
     C     *lock         in        nextfbsn
     C                   add       1             nextfbsn
     C                   eval      jhfbsn = nextfbsn
     C                   out       nextfbsn
      *
      * Populate the remaining fields and write the record
      *
     C                   z-add     tlfscd        jhfscd
     C                   move      thbtcd        jhbtcd
     C                   z-add     thbtwk        jhbtwk
     C                   z-add     ldfcymd       jhbfdt
     C                   z-add     ldtcymd       jhbtdt
     C                   move      'I'           jhfbscd
      *
     C                   write     jhrec
      *
     C                   endsr
      /eject
      *-------------------------------------------------------------------------------------
      * Retrieve Farm info and print a line for the farm
      *-------------------------------------------------------------------------------------
      *
     C     $farm         begsr
      *
     C     l1fscd        chain     hsp018                             92
     C                   if        *in92 = *off                                 If farm hit
      *
     C                   if        *inof = *on
     C                   exsr      $h1hdr
     C                   endif
     C                   except    l1dtl
     C                   setoff                                       87
      *
     C                   endif                                                  If farm hit
      *
     C                   endsr
      /eject
      *-------------------------------------------------------------------------------------
      * Print headings
      *-------------------------------------------------------------------------------------
      *
     C     $h1hdr        begsr
      *
     C                   seton                                        87
     C                   except    h1hdr
      *
     C                   endsr
      /eject
      *-------------------------------------------------------------------------------------
      * Print array of Farms that already had Inactive Budgets
      *-------------------------------------------------------------------------------------
      *
     C     $prtarray1    begsr
      *
     C                   seton                                        87
     C                   eval      l1msg = hadmsg
      *
     C                   do        y             x
     C                   z-add     ifscd(x)      l1fscd
     C                   z-add     ifbsn(x)      l1fbsn
     C                   exsr      $farm
     C                   enddo
      *
     C                   endsr
      *
      *-------------------------------------------------------------------------------------
      * Print array of Farms that already had Date Conflicts with Active Budgets
      *-------------------------------------------------------------------------------------
      *
     C     $prtarray2    begsr
      *
     C                   seton                                        87
     C                   eval      l1msg = baddat
      *
     C                   do        z             x
     C                   z-add     afscd(x)      l1fscd
     C                   z-add     afbsn(x)      l1fbsn
     C                   exsr      $farm
     C                   enddo
      *
     C                   endsr
      /eject
      *-------------------------------------------------------------------------------------
      * Clear arrays and initialize indexes
      *-------------------------------------------------------------------------------------
      *
     C     $clear        begsr
      *
     C                   z-add     0             ifscd
     C                   z-add     0             ifbsn
     C                   z-add     0             x
     C                   z-add     0             y
      *
     C                   z-add     0             afscd
     C                   z-add     0             afbsn
     C                   z-add     0             w
     C                   z-add     0             z
      *
     C                   endsr
      /eject
      *-------------------------------------------------------------------------------------
      * Initialization subroutine
      *-------------------------------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Key lists
      *
     C     key01         klist
     C                   kfld                    tlfscd
     C                   kfld                    tlbtcd
      *
      * Set up heading info
      *
     C                   time                    rtime
      *
     C                   endsr
      /EJECT
      *-------------------------------------------------------------
      * Report heading lines
      *-------------------------------------------------------------
      *
     Oqprint    e            h1hdr          1 01
     O                       sdpgm               10
     O                                           55 'HOG PRODUCTION SYSTEM'
     O                                           80 'DATE'
     O                       udate         y     90
      *
     O          e            h1hdr          1
     O                       sdusr               10
     O                                           53 'LISTING FROM ANNUAL BUDGET'
     O                                           62 ' CREATION'
     O                                           80 'TIME'
     O                       rtime               90 '  :  :  '
      *
     O          e            h1hdr          1
     O                                           80 'PAGE'
     O                       page          z     90
      *
      *
     O          e            h1hdr          1
     O                                           13 'From date ..:'
     O                       ldfmdy              23 '  /  /  '
      *
      *
     O          e            h1hdr          2
     O                                           13 'To date ....:'
     O                       ldtmdy              23 '  /  /  '
      *
      *-------------------------------------------------------------
      * Column headings
      *-------------------------------------------------------------
      *
     O          e            h1hdr       0  1
     O                                            7 'Budget'
     O                                           15 'Farm'
     O                                           68 'Type of'
     O                                           88 '-- Production --'
      *
      *
     O          e            h1hdr       0  1
     O                                            7 'Number'
     O                                           15 'Site'
     O                                           23 'Name'
     O                                           52 'Cell'
     O                                           66 'Farm'
     O                                           78 'Phase'
     O                                           87 'Type'
      *
     O          e            h1hdr       0  2
     O                       dash                90
      *
      *
     O          e            h1hdr       0  1
     O                                            9 'Template:'
     O                       thbtcd              17
     O                       thbtds              44
      *
      *-------------------------------------------------------------
      * Detail line
      *-------------------------------------------------------------
      *
     O          e    87      l1dtl       1  2
     O                       l1msg               56
      *
     O          e            l1dtl          1
     O                       l1fbsn        z      7
     O                       l1fscd        m     16
     O                       fsfsnm              44
     O                       fscell        m     52
     O                       fstfcd              67
     O                       fsppcd              78
     O                       fsptcd              88
      *
      *-------------------------------------------------------------
      * No Linked Farms message line
      *-------------------------------------------------------------
      *
     O          e            nolink      1  1
     O                                           22 'No Farms are linked to'
     O                                           37 ' this template.'
      *
      *-------------------------------------------------------------
      * There are no items message line
      *-------------------------------------------------------------
      *
     O          e            noitem      1  1
     O                                           23 'No Items are set up for'
     O                                           38 ' this template.'
      *
