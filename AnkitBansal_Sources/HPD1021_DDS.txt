     A*%%TS  SD  20040712  171028  ISLRAMS     REL-V5R2M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION
     A*  DISPLAY FILE:  HPD1021
     A*  TITLE:         REVISE BGF WEEKLY WEAN PROJECTIONS
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  DATE:          07/01/04
     A*
     A*  INDICATOR 60 IS ON WHEN IN REVISE MODE
     A*  INDICATOR 61 IS ON WHEN IN CREATE MODE
     A*  INDICATOR 62 IS ON WHEN IN DELETE MODE
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CF03(03 'EXIT')
     A N90N62                               CF05(05 'REFRESH')
     A N90                                  CF09(09 'ACCEPT')
     A N90 60                               CF11(11 'DELETE')
     A*
     A*-----------------------------------------------------------------
     A* RECORD FORMAT ONE
     A*-----------------------------------------------------------------
     A          R HP1021R1
     A*%%TS  SD  20040712  171028  ISLRAMS     REL-V5R2M0  5722-WDS
     A N90                                  CF04(04 'PROMPT')
     A                                      SETOF(31)
     A                                      SETOF(32)
     A                                      SETOF(33)
     A                                      SETOF(34)
     A                                      SETOF(35)
     A                                      SETOF(36)
     A                                      SETOF(37)
     A                                      SETOF(38)
     A                                      SETOF(39)
     A                                      SETOF(51)
     A                                      SETOF(70)
     A                                      SETOF(71)
     A                                      SETOF(72)
     A                                      SETOF(73)
     A                                      SETOF(74)
     A                                      SETOF(75)
     A                                      RTNCSRLOC(&RCD &FLD)
     A                                      OVERLAY
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 29'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A            D1MODE         6A  O  2  2DSPATR(HI)
     A                                  2 22'REVISE BGF WEEKLY WEAN PROJECTIONS'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  4  2'Farm site ..'
     A  61                              4 15'?'
     A            D1FSCD    R        B  4 17REFFLD(HGFSCD)
     A                                      EDTCDE(M)
     A  31
     AO 51                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A N31 61                               DSPATR(HI)
     A  61                                  DSPATR(UL)
     A  60
     AO 62                                  DSPATR(PR)
     A            D1FSNM    R        O  4 25REFFLD(FSFSNM)
     A                                  5 17'Report week/year..'
     A                                      DSPATR(HI)
     A            D1RPCDWK  R        B  5 36REFFLD(HSRREF/@@RPCDWK *LIBL/HSPREF)
     A                                      EDTCDE(M)
     A  33                                  DSPATR(PC)
     A  33                                  DSPATR(RI)
     A N33 61                               DSPATR(HI)
     A  61                                  DSPATR(UL)
     A  60
     AO 62                                  DSPATR(PR)
     A  60                                  COLOR(PNK)
     A            D1RPCDYR  R        B  5 40REFFLD(HSRREF/@@RPCDYR *LIBL/HSPREF)
     A                                      EDTCDE(M)
     A  32                                  DSPATR(PC)
     A  32                                  DSPATR(RI)
     A N32 61                               DSPATR(HI)
     A  61                                  DSPATR(UL)
     A  60
     AO 62                                  DSPATR(PR)
     A  60                                  COLOR(PNK)
     A                                  5 52'Week-ending date:'
     A            D1RPWEMDY R        O  5 71REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A                                      COLOR(PNK)
     A                                  6  2'1 Week Out Wean Projections Week:'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A            D1PWCDWK1 R        O  6 36REFFLD(HSRREF/@@PWCDWK *LIBL/HSPREF)
     A                                      EDTCDE(Z)
     A                                      COLOR(PNK)
     A            D1PWCDYR1 R        O  6 40REFFLD(HSRREF/@@PWCDYR *LIBL/HSPREF)
     A                                      EDTCDE(Z)
     A                                      COLOR(PNK)
     A                                  6 52'Week-ending date:'
     A            D1PWWEMDY1R        O  6 71REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A                                      COLOR(PNK)
     A                                  7 44'Sunday'
     A            D1SUPWHD1 R        B  7 52REFFLD(HSRREF/@@SUPWHD *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A  34                                  DSPATR(PC)
     A  34                                  DSPATR(RI)
     A N62N34                               DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(PR)
     A                                  8 44'Monday'
     A            D1MOPWHD1 R        B  8 52REFFLD(HSRREF/@@MOPWHD *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A  35                                  DSPATR(PC)
     A  35                                  DSPATR(RI)
     A N62N35                               DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(PR)
     A                                  9 43'Tuesday'
     A            D1TUPWHD1 R        B  9 52REFFLD(HSRREF/@@TUPWHD *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A  36                                  DSPATR(PC)
     A  36                                  DSPATR(RI)
     A N62N36                               DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(PR)
     A                                 10 41'Wednesday'
     A            D1WEPWHD1 R        B 10 52REFFLD(HSRREF/@@WEPWHD *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A  37                                  DSPATR(PC)
     A  37                                  DSPATR(RI)
     A N62N37                               DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(PR)
     A                                 11 42'Thursday'
     A            D1THPWHD1 R        B 11 52REFFLD(HSRREF/@@THPWHD *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A  38                                  DSPATR(PC)
     A  38                                  DSPATR(RI)
     A N62N38                               DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(PR)
     A                                 12 44'Friday'
     A            D1FRPWHD1 R        B 12 52REFFLD(HSRREF/@@FRPWHD *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A  39                                  DSPATR(PC)
     A  39                                  DSPATR(RI)
     A N62N39                               DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(PR)
     A                                 13 42'Saturday'
     A            D1SAPWHD1 R        B 13 52REFFLD(HSRREF/@@SAPWHD *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A  70                                  DSPATR(PC)
     A  70                                  DSPATR(RI)
     A N62N70                               DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(PR)
     A                                 14 18'1 week out total piglets weaned:'
     A            D1TOPWHD1 R        O 14 52REFFLD(HSRREF/@@TOPWHD *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A                                 15 25'1 week out weaned sows ..'
     A            D1PWSOHD1 R        B 15 52REFFLD(HSRREF/@@PWSOHD *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A  71                                  DSPATR(PC)
     A  71                                  DSPATR(RI)
     A N62N71                               DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(PR)
     A                                 16 25'1 week out sow culls ....'
     A            D1SOCUHD1 R        B 16 52REFFLD(HSRREF/@@PWSOHD *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A  74                                  DSPATR(PC)
     A  74                                  DSPATR(RI)
     A N62N74                               DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(PR)
     A                                 17 25'1 week out boar culls ...'
     A            D1BOCUHD1 R        B 17 52REFFLD(HSRREF/@@PWSOHD *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A  75                                  DSPATR(PC)
     A  75                                  DSPATR(RI)
     A N62N75                               DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(PR)
     A                                 19  2'2 Week Out Wean Projections Week:'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A            D1PWCDWK2 R        O 19 36REFFLD(HSRREF/@@PWCDWK *LIBL/HSPREF)
     A                                      EDTCDE(Z)
     A                                      COLOR(PNK)
     A            D1PWCDYR2 R        O 19 40REFFLD(HSRREF/@@PWCDYR *LIBL/HSPREF)
     A                                      EDTCDE(Z)
     A                                      COLOR(PNK)
     A                                 19 52'Week-ending date:'
     A            D1PWWEMDY2R        O 19 71REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A                                      COLOR(PNK)
     A                                 20 16'2 week out total piglets weaned ..'
     A            D1TOPWHD2 R        B 20 52REFFLD(HSRREF/@@TOPWHD *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A  72                                  DSPATR(PC)
     A  72                                  DSPATR(RI)
     A N62N72                               DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(PR)
     A                                 21 25'2 week out weaned sows ..'
     A            D1PWSOHD2 R        B 21 52REFFLD(HSRREF/@@PWSOHD *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A  73                                  DSPATR(PC)
     A  73                                  DSPATR(RI)
     A N62N73                               DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(PR)
     A*
     A*-----------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*-----------------------------------------------------------------
     A          R HP1021K1
     A*%%TS  SD  20040706  073333  ISLRAMS     REL-V5R2M0  5722-WDS
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A                                 23 11'F5=Refresh'
     A N62N90                          23 23'F9=Accept'
     A N90 60                          23 34'F11=Delete'
      *
      *------------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *-----------------------------------------------------------------
     A          R HP1021ES                  SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
      *------------------------------------------------------------------
      * ERROR MESSAGE SUBFILE CONTROL FORMAT
      *-----------------------------------------------------------------
     A          R HP1021EC                  SFLCTL(HP1021ES)
      *
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
