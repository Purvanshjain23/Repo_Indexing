     h option(*SRCSTMT:*NODEBUGIO)
      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Triumph Foods
      * PROGRAM:     TF677
      * TITLE:       Yields: Primal Weekly Yield Report (Detail)
      * PROGRAMMER:  LeAnne Ramsey
      * CREATED:     12/15/08
      *
      * FUNCTION:  Batch program to print the report at the Item level.
      *            We exclude all BP Items except Picnic Cushions and Kill Offal.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 01/29/09  LeAnne Ramsey
      *           Recompile only. Fields were added to Weekly Yield file.
      *
      * 02/02/09  LeAnne Ramsey
      *           Exclude all BP Items except Picnic Cushions.
      *
      * 02/20/09  LeAnne Ramsey
      *           We have removed from the Weekly Yield file the values calculated
      *           using Killed/Cut data. So, we will now calc these values in this
      *           print program.
      *
      * 04/01/09  LeAnne Ramsey
      *           Marty H. and Brad Hamilton wanted to add 4 columns (the SBF/TF Per Head
      *           columns) for the 740-Kill Offal data to the ByProduct Reports. We did not
      *           have room. So, Damon Ginther approved moving Kill Offal to the PRIMAL reports.
      *
      * 04/06/09  LeAnne Ramsey
      *           We will now move all the 750-Render BPs to the PRIMAL reports.
      *
      * 12/10/09  LeAnne Ramsey
      *           Removed file OMBYREL1-Item Default; it was not being used.
      *
      * 11/09/17  Danny Nguyen    - R12011-Kill Cut Data for STF
      *           File TFP050 was changed. Recompile only. No logic change.
      *
      * 02/08/22  Danny Nguyen   - DO2484 - WI479 STF Variance Reporting
      *           Recompile only due to TFP076 DBF change.
      *           No changes to report.
      *
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fome8rel0  if   e           k disk
      *    OM Product type
      *
      *
     Fomfarel1  if   e           k disk
      *    OM Product class
      *
      *
     Fpmaprel1  if   e           k disk
      *    TF Meat cost group
      *
      *
     Ftfp050    if   e           k disk
      *    Kill/Cut data
      *
      *
     Ftfl076a   if   e           k disk
      *  Weekly yields  (records selected with open query)
      *
      *
     Fprint198  o    f  198        printer oflind(*inof)
      *
      /EJECT
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Standard fields
      *
     D dash            s            198    inz(*all'-')
     d rtime           s              6  0
      *
      *
      * Control fields
      *
     D first           s              1    inz('Y')
     D newisty         s              1    inz('Y')
     d svtfclcd        s                   like(ywtfclcd)
     d svisgrcd        s                   like(ywisgrcd)
     d svisclcd        s                   like(ywisclcd)
     d svsumlev        s                   like(ywsumlev)
     d cnt             s              7  0
     d cntbp           s              7  0
     d cntsumbp        s              7  0
     d cntprimal       s              7  0
      *
      *
      * Workfields
      *
     D wkdesc          s             47
     D wkalph          s              3
     D wktfcgcd        s                   like(ywtfcgcd)
     D wkcoownfl       s                   like(ywcoownfl)
      *
      *
      * Parms
      *
     D xxorigin        s              1
     D xxwedt          s              8  0
     D xxistycd        s                   like(ywistycd)
     D xxisgrcd        s                   like(ywisgrcd)
     D xxtfclcd        s                   like(ywtfclcd)
     D xxtfcgcd        s                   like(ywtfcgcd)
     D xxspulb         s                   like(ywspulb)
     D xxspuslb        s                   like(ywspuslb)
     D xxtpulb         s                   like(ywtpulb)
     D xxtpuslb        s                   like(ywtpuslb)
     D xxspc           s              7  4
     D xxskllb         s              7  4
     D xxsctlb         s              7  4
     D xxtpc           s              7  4
     D xxtkllb         s              7  4
     D xxtctlb         s              7  4
      *
      *
      * Print fields
      *
     d h1demand        s             35
     d h2desc          s             34
     d h3desc          s             66
      *
     d t1desc          s             74
      *
      * Array index
      *
     d x               s              1  0
      *
      ****************************************************************
      * ARRAYS AND TABLES
      ****************************************************************
      *
     D arspulb         s             11  2 dim(4)
     D arspuslb        s             11  2 dim(4)
     D arspc           s              7  4 dim(4)
     D arskllb         s              7  4 dim(4)
     D arsctlb         s              7  4 dim(4)
      *
     D artpulb         s             11  2 dim(4)
     D artpuslb        s             11  2 dim(4)
     D artpc           s              7  4 dim(4)
     D artkllb         s              7  4 dim(4)
     D artctlb         s              7  4 dim(4)
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *---------------------------------------------------------------
      * Local data area.
      *---------------------------------------------------------------
      *
     Dlda             uds                  dtaara(*lda)
     D  ldpfcd                 1      1
     D  ldyr                   2      5  0
     D  ldwk                   6      7  0
      *
     D  ldwedt                29     36  0
     D  ldwemdy               44     49  0
      *
     D  ldtfcgcd             139    140
     D  ldtfcgds             141    155
      *
     D  ldtfclcd             156    158
     D  ldtfclds             159    188
      *
     D  ldistycd             189    191  0
     D  ldistyds             192    221
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ***************************************************************************************
      * MAINLINE
      ***************************************************************************************
      *
      * Retrieve Kill/Cut Data
      * Print headings
      *
     C     ldwedt        chain     tfp050
     C                   exsr      $h1hdr
      *
      * The breaks were so difficult that we will drive this by reading the
      * master file of Item Structure Types.
      *
     C     *loval        setll     ome8rel0
      *
     C                   dou       *in90 = *on                                  Main do
     C                   read      ome8rel0                               90
     C                   if        *in90 = *off and                             If not eof
     C                             (ldistycd = 0 or ldistycd = e8rgnb)
      *
     C                   z-add     0             cnt
     C                   z-add     0             cntbp
     C                   z-add     0             cntsumbp
     C                   z-add     0             cntprimal
      *
     C                   z-add     1             x
     C                   exsr      $clear
      *
     C                   move      yes           newisty
      *
     C                   move      e8rgnb        wkalph
     C                   eval      h2desc = %trim(wkalph) + '-'
     C                                      + %trim(e8dctx)
      *
      * Print CVs (Not CoOwned)
      *
     C                   eval      wktfcgcd = 'CV'
     C                   move      no            wkcoownfl
     C                   exsr      $rtv076
      *
      * Print NVs (Not CoOwned)
      *
     C                   eval      wktfcgcd = 'NV'
     C                   move      no            wkcoownfl
     C                   exsr      $rtv076
      *
      * Print VAs (not CoOwned)
     C                   eval      wktfcgcd = 'VA'
     C                   move      no            wkcoownfl
     C                   exsr      $rtv076
      *
      * All CoOwned Except BP/OT
      *
     C                   eval      wktfcgcd = *blank
     C                   move      yes           wkcoownfl
     C                   exsr      $rtv076co
      *
      * Print "Primal" subtotal
      * Note: We are only printing a Primal Subtotal on Picnics
      *
     C                   evalr     t1desc = 'Primal Total:'
     C                   z-add     2             x
     C                   if        cntprimal > 1 and e8rgnb = 600
     C                   exsr      $t2tot
     C                   else
     C                   exsr      $clear
     C                   endif
      *
      * Clear the array entries that will hold the ByProduct Summary Level totals
      *
     C                   z-add     1             x
     C                   exsr      $clear
      *
      * Print BPs (not coowned)
      * Note: The only BPs we are processing are Picnic Cushions and Kill Offal
      *
     C                   eval      wktfcgcd = 'BP'
     C                   move      no            wkcoownfl
     C                   exsr      $rtv076
      *
      * Print BPs (coowned)
      * Note: The only BPs we are processing are Picnic Cushions and Kill Offal
      *
     C                   eval      wktfcgcd = 'BP'
     C                   move      yes           wkcoownfl
     C                   exsr      $rtv076
      *
      * Print "BP" subtotal
      * Note: The only BPs we are processing are Picnic Cushions, Kill Offal, Render
      *
     C                   evalr     t1desc = 'ByProduct Total:'
     C                   z-add     2             x
     C                   if        cntbp > 1 and
     C                             (e8rgnb=600 or e8rgnb=740 or e8rgnb=750)
     C                   exsr      $t2tot
     C                   else
     C                   exsr      $clear
     C                   endif
      *
      * Print Item Structure Type total
      *
     C                   if        cnt > 1
     C                   exsr      $t3tot
     C                   else
      * Primal/ByProduct
     C                   z-add     2             x
     C                   exsr      $clear
      * Item Struct Type
     C                   z-add     3             x
     C                   exsr      $clear
     C                   endif
      *
      * Print OTs (both CoOwned and Not CoOwned)
      *
     C                   eval      wktfcgcd = 'OT'
     C                   move      *blank        wkcoownfl
     C                   exsr      $rtv076
      *
     C                   endif                                                  If not eof
     C                   enddo                                                  Main do
      *
      * EOF processing
     C                   if        first = yes
     C                   except    nodata
     C                   else
     C                   exsr      $t4tot
     C                   endif
      *
     C                   seton                                        lr
      /EJECT
      *---------------------------------------------------------------
      * Retrieve Weekly Yield Records
      *---------------------------------------------------------------
      *
     C     $rtv076       begsr
      *
     C                   move      *blank        svtfclcd
     C                   z-add     0             svisclcd
     C                   z-add     0             svisgrcd
      *
      * Retrieve all Weekly Yield records for this:
      *  1) Item Structure Type
      *  2) TF Class Group
      *  Include the following (even though they are ByProducts, they are printed on the
      *  PRIMAL reports:
      *    1) Picnic Cushions
      *    2) Kill Offal
      *    3) Render
      *
     C     key01         setll     tfl076a
      *
     C                   dou       *in91 = *on                                  Do loop
     C     key01         reade     tfl076a                                91
     C                   if        *in91 = *off and                             If not EOF
     C                             (wkcoownfl = *blank or wkcoownfl = ywcoownfl)
     C                             and (ywtfcgcd <> 'BP' or
     C                                 (ywistycd = 600 and ywisgrcd = 630 and
     C                                  ywtfcgcd = 'BP' and ywtfclcd = 'BP5') or
     C                                 (ywistycd = 740 and ywtfclcd = 'OFF' and
     C                                  ywtfcgcd = 'BP') or
     C                                 (ywistycd = 750 and ywtfcgcd = 'BP'))
      * Break logic
     C                   select
     C                   when      newisty = yes
     C                   exsr      $h2hdr
     C                   move      no            newisty
     C                   move      ywtfclcd      svtfclcd
     C                   z-add     ywisclcd      svisclcd
     C                   z-add     ywisgrcd      svisgrcd
     C                   seton                                        87
      *
     C                   when      ywtfclcd <> svtfclcd or
     C                             ywisclcd <> svisclcd or
     C                             ywisgrcd <> svisgrcd
     C                   move      ywtfclcd      svtfclcd
     C                   z-add     ywisclcd      svisclcd
     C                   z-add     ywisgrcd      svisgrcd
     C                   seton                                        87
     C                   endsl
      *
      * Print detail line
     C                   exsr      $l1dtl
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Do loop
      *
     C                   endsr
      /EJECT
      *----------------------------------------------------------------------------------------
      * Retrieve Weekly Yield Records (CoOwned Only--Exclude OT and BP)
      *----------------------------------------------------------------------------------------
      *
     C     $rtv076co     begsr
      *
     C                   move      *blank        svtfclcd
     C                   z-add     0             svisclcd
     C                   z-add     0             svisgrcd
      *
     C     e8rgnb        setll     tfl076a
      *
     C                   dou       *in91 = *on                                  Do CoOwned
     C     e8rgnb        reade     tfl076a                                91
     C                   if        *in91 = *off and                             If not EOF
     C                             ywtfcgcd <> 'BP' and
     C                             ywtfcgcd <> 'OT' and
     C                             ywcoownfl = yes
      *
     C                   select
     C                   when      newisty = yes
     C                   exsr      $h2hdr
     C                   move      no            newisty
     C                   move      ywtfclcd      svtfclcd
     C                   z-add     ywisclcd      svisclcd
     C                   z-add     ywisgrcd      svisgrcd
     C                   seton                                        87
      *
     C                   when      ywtfclcd <> svtfclcd or
     C                             ywisclcd <> svisclcd or
     C                             ywisgrcd <> svisgrcd
     C                   move      ywtfclcd      svtfclcd
     C                   z-add     ywisclcd      svisclcd
     C                   z-add     ywisgrcd      svisgrcd
     C                   seton                                        87
     C                   endsl
      *
     C                   exsr      $l1dtl
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Do CoOwned
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Print detail line
      *---------------------------------------------------------------
      *
     C     $l1dtl        begsr
      *
      * Do you need to print a BP Summary Level?
      *
     C                   if        ywtfcgcd = 'BP' and ywsumlev <> svsumlev     If byprod break
      *
     C                   if        cntsumbp > 1                                 If need total
     C                   eval      wkdesc = %subst(h3desc:19:47)
     C                   evalr     t1desc = 'Total for ' +
     C                                      %trim(wkdesc)
     C                   z-add     1             x
     C                   exsr      $t1tot
     C                   else
     C                   exsr      $clear
     C                   endif                                                  If need total
      *
     C                   exsr      $h3desc
     C                   exsr      $h3hdr
     C                   move      ywsumlev      svsumlev
     C                   z-add     0             cntsumbp
     C                   endif                                                  If byprod break
      *
      * Accumulate for totals
      *
     C                   if        wktfcgcd <> 'OT'
     C                   add       ywspulb       arspulb
     C                   add       ywspuslb      arspuslb
     C                   add       ywtpulb       artpulb
     C                   add       ywtpuslb      artpuslb
     C                   endif
      *
      * Call program to return calc'd values
      *
     C                   call      'TF814'
     C                   parm      ywwedt        xxwedt
     C                   parm      ywistycd      xxistycd
     C                   parm      ywisgrcd      xxisgrcd
     C                   parm      ywtfclcd      xxtfclcd
     C                   parm      ywtfcgcd      xxtfcgcd
     C                   parm      ywspulb       xxspulb
     C                   parm      ywspuslb      xxspuslb
     C                   parm      ywtpulb       xxtpulb
     C                   parm      ywtpuslb      xxtpuslb
     C                   parm      0             xxspc
     C                   parm      0             xxsctlb
     C                   parm      0             xxskllb
     C                   parm      0             xxtpc
     C                   parm      0             xxtctlb
     C                   parm      0             xxtkllb
      *
      * Accumulate into arrays for totals
      *
     C                   add       xxspc         arspc
     C                   add       xxsctlb       arsctlb
     C                   add       xxskllb       arskllb
      *
     C                   add       xxtpc         artpc
     C                   add       xxtctlb       artctlb
     C                   add       xxtkllb       artkllb
      *
      * Have you hit overflow?
      *
     C                   if        *inof = *on
     C                   exsr      $h1hdr
     C                   exsr      $h2hdr
     C                   if        *in86 = *on
     C                   exsr      $h3hdr
     C                   endif
     C                   endif
      *
      * Print detail line
     C                   except    l1dtl
     C                   setoff                                       8687
     C                   move      no            first
      *
      *
      * To control printing of subtotals, count:
      *
     C                   select
     C                   when      ywtfcgcd = 'OT'
      *
     C                   when      ywtfcgcd = 'BP'
     C                   add       1             cnt
     C                   add       1             cntbp
     C                   add       1             cntsumbp
     C                   other
      *
     C                   add       1             cnt
     C                   add       1             cntprimal
     C                   endsl
      *
     C                   endsr
      /EJECT
      *------------------------------------------------------------------------------------------
      * Concatenate together a description for the ByProduct Summary Level
      *------------------------------------------------------------------------------------------
      *
     C     $h3desc       begsr
      *
      * The 'descriptions' on the report aren't really making much sense. So, we keep
      * trying to come up with some combination of fields that are "descriptive".
      *
      * Currently, the "description" will be one of these:
      *  1) Summary Level
      *  2) Meat Cost Group Description + Summary Level
      *  3) Item Structure Class Description + Summary Level
      *
      * Default to Summary Level
      *
     C                   eval      h3desc = 'BP Summary Level: ' +
     C                                      %trim(ywsumlev)
      *
      * Then, override to the Meat Cost Group Code Description/Summary Level
      *
     C     ywcgcd        chain     pmaprel1                           92
     C                   if        *in92 = *off                                 If hit
      *
     C                   eval      h3desc = 'BP Summary Level: ' +
     C                                      %trim(aprnna) + ' ' +
     C                                      %trim(ywsumlev)
      *
      * Then, override again to the Item Structure Class Description/Summary Level
      * if the Meat Cost Group Code Summary Level is 'CL'
      *
     C                   if        apeetx = 'CL'                                If cl
     C     ywisclcd      chain     omfarel1                           92
     C                   if        *in92 = *off
      *
     C                   eval      h3desc = 'BP Summary Level: ' +
     C                                      %trim(fadetx) + ' ' +
     C                                      %trim(ywsumlev)
     C                   endif
     C                   endif                                                  If cl
     C                   endif                                                  If hit
      *
      *
     C                   endsr
      /EJECT
      *------------------------------------------------------------------------------------------
      * Print total line for ByProduct Summary Level
      *------------------------------------------------------------------------------------------
      *
     C     $t1tot        begsr
      *
     C                   if        *inof = *on
     C                   exsr      $h1hdr
     C                   endif
     C                   except    t1tot
      *
      * Clear/initialize
     C                   exsr      $clear
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Print total line  (Primal/ByProduct Total)
      *---------------------------------------------------------------
      * This subroutine is ONLY executed for Item Structure Types of:
      *     600-Picnics, 740-Kill, 750-Render
      * For Picnics, we print both Primal and ByProduct Subtotals.
      * For Kill and Render, we do not print a Primal Subtotal....just a ByProduct Subtotal.
      *
     C     $t2tot        begsr
      *
     C                   if        *inof = *on
     C                   exsr      $h1hdr
     C                   endif
     C                   except    t1tot
      *
      * Clear/initialize
     C                   exsr      $clear
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Print total line for Item Structure Type
      *---------------------------------------------------------------
      *
     C     $t3tot        begsr
      *
     C                   z-add     3             x
     C                   seton                                        88
      *
      * For the ByProducts, we will not print an underscore; set the indicator
      * to control this.
     C                   if        e8rgnb=600 or e8rgnb=740 or e8rgnb=750
     C                   seton                                        89
     C                   endif
      *
     C                   if        cnt > 1
     C                   evalr     t1desc = 'Total for ' + %trim(h2desc) + ':'
      *
     C                   if        *inof = *on
     C                   exsr      $h1hdr
     C                   endif
     C                   except    t1tot
     C                   endif
      *
     C                   setoff                                       8889
      * Clear/initialize
     C                   exsr      $clear
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Print Report Total
      *---------------------------------------------------------------
      *
     C     $t4tot        begsr
      *
     C                   seton                                        8889
     C                   z-add     4             x
      *
     C                   evalr     t1desc = 'Report Total:'
      *
     C                   if        *inof = *on
     C                   exsr      $h1hdr
     C                   else
     C                   except    blank
     C                   endif
      *
     C                   except    t1tot
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Clear arrays
      *---------------------------------------------------------------
      *
     C     $clear        begsr
      *
     C                   z-add     0             arspuslb (x)
     C                   z-add     0             arspulb  (x)
     C                   z-add     0             arspc    (x)
     C                   z-add     0             arsctlb  (x)
     C                   z-add     0             arskllb  (x)
      *
     C                   z-add     0             artpuslb (x)
     C                   z-add     0             artpulb  (x)
     C                   z-add     0             artpc    (x)
     C                   z-add     0             artctlb  (x)
     C                   z-add     0             artkllb  (x)
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Print report heading
      *---------------------------------------------------------------
      *
     C     $h1hdr        begsr
      *
     C                   except    h1hdr
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Item Structure Header
      *---------------------------------------------------------------
      *
     C     $h2hdr        begsr
      *
     C                   if        *inof = *on
     C                   exsr      $h1hdr
     C                   endif
      *
     C                   except    h2hdr
     C                   seton                                        87
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * BP Summary Level Header
      *---------------------------------------------------------------
      *
     C     $h3hdr        begsr
      *
     C                   if        *inof = *on
     C                   exsr      $h1hdr
     C                   exsr      $h2hdr
     C                   endif
      *
     C                   except    h3hdr
     C                   seton                                        8687
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Parm lists
      *
     C     *entry        plist
     C                   parm                    xxorigin
      *
      * Key Lists
      *
     C     key01         klist
     C                   kfld                    e8rgnb
     C                   kfld                    wktfcgcd
      *
      * Retrieve time for report heading.
      *
     C                   time                    rtime
      *
      * This report is generated:
      *  1) From Inventory Stock Closing
      *  2) As a Preliminary/Final from Margin Adjustment Close
      *  3) On-demand
      *
     C                   select
     C                   when      xxorigin = 'N'
     C                   eval      %subst(h1demand:3:28) =
     C                             'From Inventory Stock Closing'
      *
     C                   when      ldpfcd = 'D'
     C                   eval      %subst(h1demand:13:9) = 'On-Demand'
      *
     C                   when      ldpfcd = 'F'
     C                   eval      %subst(h1demand:4:29) =
     C                             'Final Margin Adjustment Close'
      *
     C                   when      ldpfcd = 'P'
     C                   eval      %subst(h1demand:1:35) =
     C                             'Preliminary Margin Adjustment Close'
     C                   endsl
      *
      *
      * Set up Report Headings
      *
     C                   if        ldistycd = 0
     C                   seton                                        95
     C                   endif
      *
     C                   if        ldtfcgcd = *blank
     C                   seton                                        94
     C                   endif
      *
     C                   if        ldtfclcd = *blank
     C                   seton                                        93
     C                   endif
      *
     C                   endsr
      /EJECT
      *-------------------------------------------------------------------------------------
      * Report heading lines
      *-------------------------------------------------------------------------------------
      *
     Oprint198  e            h1hdr          1 01
     O                       sdpgm               10
     O                                          101 'PRIMAL WEEKLY YIELD REPORT'
     O                                          108 '-DETAIL'
     O                                          188 'DATE'
     O                       udate         y    198
      *
     O          e            h1hdr          1
     O                       sdusr               10
     O                       h1demand           111
     O                                          188 'TIME'
     O                       rtime              198 '  :  :  '
      *
     O          e            h1hdr          1
     O                                          188 'PAGE'
     O                       page          z    198
      *
     O          e            h1hdr          1
     O                                           17 'Week-ending date:'
     O                       ldwemdy             26 '  /  /  '
      *
     O                                           60 'TF Class group code:'
     O               94                          65 'All'
     O              n94      ldtfcgcd            64
     O              n94      ldtfcgds            82
      *
     O                                          130 'Head'
     O                                          147 'Hot Carcass'
     O                                          159 'Head'
     O                                          176 'Cold Carcass'
      *
     O          e            h1hdr          1
     O                                           17 'Year/Week:'
     O                       ldyr          m     23
     O                       ldwk          m     27
      *
     O                                           60 'TF Classification code:'
     O               93                          65 'All'
     O              n93      ldtfclcd            65
     O              n93      ldtfclds            97
      *
      *
     O                                          130 'Killed'
     O                                          147 'Pounds'
     O                                          159 'Cut'
     O                                          176 'Pounds'
      *
      *
     O          e            h1hdr          1
     O                                           60 'Item structure type code:'
     O               95                          65 'All'
     O              n95      ldistycd            65
     O              n95      ldistyds            97
      *
     O                                          130 '--------'
     O                                          147 '--------------'
     O                                          159 '--------'
     O                                          176 '--------------'
      *
     O          e            h1hdr          1
     O                                          120 'Seaboard:'
     O                       kcsklhd       k    131
     O                       kcshclb       k    148
     O                       kcscthd       k    160
     O                       kcscclb       k    177
      *
     O          e            h1hdr          2
     O                                          120 'Triumph:'
     O                       kctklhd       k    131
     O                       kcthclb       k    148
     O                       kctcthd       k    160
     O                       kctcclb       k    177
      *
      *-------------------------------------------------------------------------------------
      * Column headings
      *-------------------------------------------------------------------------------------
      *
     O          e            h1hdr       1  1
     O                                            9 'TF '
     O                                           29 'Co-'
      *
     O                                           89 'SBF'
     O                                          105 'SBF'
     O                                          115 'SBF'
     O                                          125 'SBF Prod'
     O                                          135 'SBF Prod'
      *
     O                                          151 'TF'
     O                                          167 'TF'
     O                                          177 'TF'
     O                                          187 'TF Prod'
     O                                          197 'TF Prod'
      *
     O          e            h1hdr       0  1
     O                                            2 'IS'
     O                                            9 'Cls'
     O                                           13 'TF'
     O                                           18 'IS'
     O                                           23 'IS'
     O                                           33 'Itm'
     O                                           29 'Own'
     O                                           42 'Product'
      *
     O                                           89 'Produced'
     O                                          105 'Produced'
     O                                          115 'Yield'
     O                                          125 'Lbs Per'
     O                                          135 'Lbs Per'
      *
     O                                          151 'Produced'
     O                                          167 'Produced'
     O                                          177 'Yield'
     O                                          187 'Lbs Per'
     O                                          197 'Lbs Per'
      *
     O          e            h1hdr       0  1
     O                                            4 'Type'
     O                                            9 'Grp'
     O                                           14 'Cls'
     O                                           19 'Grp'
     O                                           24 'Cls'
     O                                           29 'Flg'
     O                                           33 'Typ'
     O                                           41 'Code'
     O                                           55 'Description'
      *
     O                                           89 'Start Pounds'
     O                                          105 'Pounds'
     O                                          115 'Percent'
     O                                          125 'Hd Cut'
     O                                          135 'Hd Kill'
      *
     O                                          151 'Start Pounds'
     O                                          167 'Pounds'
     O                                          177 'Percent'
     O                                          187 'Hd Cut'
     O                                          197 'Hd Kill'
      *
     O          e            h1hdr          0
     O                       dash               198
      *
      *-------------------------------------------------------------------------------------
      * Heading line for Item Structure type
      *-------------------------------------------------------------------------------------
      *
     O          e            h2hdr       2  0
     O                       h2desc              34
      *
     O          e            h2hdr       0  0
     O                       h2desc              34
      *
      *
      *-------------------------------------------------------------------------------------
      * BP Summary Level Heading
      *-------------------------------------------------------------------------------------
      *
     O          e            h3hdr       2  0
     O                       h3desc              71
      *
     O          e            h3hdr       0  0
     O                       h3desc              71
      *
      *-------------------------------------------------------------------------------------
      * Detail line
      *-------------------------------------------------------------------------------------
      *
     O          e    87n86   l1dtl       1
      *
     O          e            l1dtl       1
     O               87      ywtfcgcd             9
     O               87      ywtfclcd            14
     O               87      ywisgrcd      m     20
     O               87      ywisclcd      m     25
     O                       ywcoownfl           28
     O                       ywitycd             34
     O                       ywprcd        z     41
     O                       ywstar              43
     O                       ywprds              74
      *
     O                       ywspuslb      k     90
     O                       ywspulb       k    106
     O                       xxspc         kb   116
     O                       xxsctlb       kb   126
     O                       xxskllb       kb   136
      *
     O                       ywtpuslb      k    152
     O                       ywtpulb       k    168
     O                       xxtpc         kb   178
     O                       xxtctlb       kb   188
     O                       xxtkllb       kb   198
      *
      *-------------------------------------------------------------------------------------
      * Total Line for all Levels
      *-------------------------------------------------------------------------------------
      *
     O          e    89      t1tot          1
      *
     O          e   n89      t1tot       1  1
     O                                           89 '--------------'
     O                                          105 '--------------'
     O                                          115 '--------'
     O                                          125 '--------'
     O                                          136 '--------'
      *
     O                                          151 '--------------'
     O                                          167 '--------------'
     O                                          177 '--------'
     O                                          187 '--------'
     O                                          197 '--------'
      *
     O          e    88      t1tot       0  0
     O                       t1desc              74
     O                       arspuslb (x)  k     90
     O                       arspulb  (x)  k    106
     O                       arspc    (x)  k    116
     O                       arsctlb  (x)  k    126
     O                       arskllb  (x)  k    136
      *
     O                       artpuslb (x)  k    152
     O                       artpulb  (x)  k    168
     O                       artpc    (x)  k    178
     O                       artctlb  (x)  k    188
     O                       artkllb  (x)  k    198
      *
     O          e            t1tot          1
     O                       t1desc              74
     O                       arspuslb (x)  k     90
     O                       arspulb  (x)  k    106
     O                       arspc    (x)  kb   116
     O                       arsctlb  (x)  kb   126
     O                       arskllb  (x)  kb   136
      *
     O                       artpuslb (x)  k    152
     O                       artpulb  (x)  k    168
     O                       artpc    (x)  kb   178
     O                       artctlb  (x)  kb   188
     O                       artkllb  (x)  kb   198
      *
      *-------------------------------------------------------------
      * Blank
      *-------------------------------------------------------------
      *
     O          e            blank       1
      *
      *-------------------------------------------------------------
      * No data message line
      *-------------------------------------------------------------
      *
     O          e            nodata      1
     O                                           19 'No data meets your'
     O                                           39 'selection criteria.'
