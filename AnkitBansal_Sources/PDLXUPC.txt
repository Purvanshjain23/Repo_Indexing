/********************************************************************/
/*                                                                  */
/*  SYNOPSIS -  JOB CONTROL PROMPT FOR PRINTER OVERRIDE INFORMATION */
/*              GENERIC PROMPTING FUNCTION. CAUSE PRINTER OVERRIDE  */
/*              PROMPTING AND PASSES COMPANY, LOAD,SCHED SHIP DATE, */
/*              DEPARTURE DATE,TIME, LOAD GRADE                     */
/*                                                                  */
/*  SYSTEM    - ORDER MANAGEMENT SYSTEM                             */
/*  PROGRAM   - PRINT REPORTS                                       */
/*  DATE      - 03/27/98                                            */
/*                                                                  */
/*      PARAMETERS:                                                 */
/*                                                                  */
/*     O    &RETURN     - RETURN CODE                               */
/*     I    &APPLIC     - APPLICATION CODE                          */
/*     I    &PGM        - CL PROGRAM TO SUBMIT                      */
/*     I    &DESC       - PRINT PROGRAM DESCRIPTION (FOR PROMPT     */
/*                         SCREEN)                                  */
/*     B    &OUTQ       -  OUTPUT QUEUE                             */
/*                         IF THIS IS BLANK AT START, PROMPT        */
/*                         PGM IS CALLED; OTHERWISE, THE PRINT CL   */
/*                         IS SUBMITTED WITHOUT PROMPTING           */
/*     B    &HOLD       -  HOLD STATUS                              */
/*     B    &SAVE       -  SAVE STATUS                              */
/*     B    &NQSTAT     -  INDICATOR WHETHER MUST RUN IN NIGHT Q    */
/*     B    &NIGHTQ     -  NIGHT JOB QUEUE                          */
/*     B    &COMPNYN    - COMPANY NUMBER (PASSED)                   */
/*     B    &LOADIDN    - LOAD ID        (PASSED)                   */
/*     B    &SHIPDTN    - DATE 1 PASS    (PASSED)                   */
/*     B    &DPRTDTN    - DEPART DATE    (PASSED)                   */
/*     B    &DPRTTMN    - DEPART TIME    (PASSED)                   */
/*     B    &LOADG      - LOAD GRADE     (PASSED)                   */
/*     B    &COPYN      - NO. OF COPIES  (PASSED)                   */
/*                                                                  */
/********************************************************************/
/* RMC 04/26/07 DONT PASS LOAD GRADE PARM, OR USE AS THE KEY IN THE PRTF */
/********************************************************************/

 PDLXUPC:    PGM        PARM(&RETURN &APPLIC &PGM &DESC &OUTQ &HOLD +
                          &SAVE &NQSTAT &NIGHTQ &COMPNYN &SHIPDTN +
                          &COPYN &LOADIDN &DPRTDTN &DPRTTMN)
/* PARMS                                                                      */
        DCL    &RETURN     *CHAR   (7)           /* Return Code               */
        DCL    &APPLIC     *CHAR   (06)          /* Application Code          */
        DCL    &PGM        *CHAR   (10)          /* CL to Submit              */
        DCL    &DESC       *CHAR   (25)          /* PRT Pgm Description       */
        DCL    &OUTQ       *CHAR   (10)          /* Output Queue              */
        DCL    &HOLD       *CHAR   (4)           /* Hold Status               */
        DCL    &SAVE       *CHAR   (4)           /* Save Status               */
        DCL    &NQSTAT     *CHAR   (01)          /* If MUST run in Night Q    */
        DCL    &NIGHTQ     *CHAR   (10)          /* Night Job Queue           */
        DCL    &DESC       *CHAR   (25)          /* PRT Pgm Description       */
        DCL    &LOADG      *CHAR   (1)           /* LOAD GRADE     (Passed)   */
             DCL        VAR(&PAPER) TYPE(*CHAR) LEN(1) +
                          VALUE('L') /* LEGAL, +
                          LETTER             */
        DCL    &COPYN      *DEC                  /* No. of Copies             */
        DCL    &COMPNYN    *DEC                  /* Company Number (Passed)   */
        DCL    &SHIPDTN    *DEC                  /* SHIP DATE      (Passed)   */
        DCL    &LOADIDN    *DEC                  /* LOAD ID      (Pas  sed)   */
        DCL    &DPRTDTN    *DEC                  /* DEPART DATE    (Passed)   */
        DCL    &DPRTTMN    *DEC                  /* DEPART TIME    (Passed)   */

/* INTERNAL FIELDS                                                            */
        DCL    &COPY       *DEC    (2 0)         /* Number of copies          */

/* ERROR MESSAGE FIELDS                                                       */
        DCL    &MSG        *CHAR   (512)         /* message                   */
        DCL    &MSGID      *CHAR   (7)           /* message id                */
        DCL    &MSGL       *DEC    (5 0)

/********************************************************************/
/*   INITIALIZE VARIABLES                                           */
/********************************************************************/

             CHGVAR     VAR(&RETURN) VALUE('       ')
             CHGVAR     VAR(&COPY) VALUE(&COPYN)
/********************************************************************/
/*   MONITOR FOR ALL MESSAGES AT PROGRAM LEVEL                      */
/********************************************************************/

             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(MSGTRAP))

/********************************************************************/
/*   CALL PROMPT PROGRAM                                            */
/********************************************************************/

             CALL       PGM(PDV8PVR) PARM(&RETURN &DESC &OUTQ &HOLD +
                          &SAVE &COPY &NQSTAT &NIGHTQ &APPLIC &PAPER)
             CHGVAR     VAR(&COPYN) VALUE(&COPY)
             IF         COND(&RETURN *NE ' ') THEN(GOTO +
                          CMDLBL(USERQUIT))

/********************************************************************/
/*   SUBMIT PRINT CL PROGRAM                                        */
/********************************************************************/

 SUBMIT:     SBMJOB     CMD(CALL PGM(&PGM) PARM(&COMPNYN &LOADIDN +
                          &SHIPDTN &DPRTDTN &DPRTTMN  &OUTQ +
                          &HOLD &SAVE &COPYN)) JOB(LOADSCHED) +
                          JOBQ(&NIGHTQ)

             GOTO       CMDLBL(END)

/*-------------------------USER QUIT--------------------------------*/
 USERQUIT:   CHGVAR     VAR(&RETURN) VALUE('Y2U0005')
             GOTO       CMDLBL(END)

/*----------------------RECEIVE MESSAGES----------------------------*/

 MSGTRAP:    RCVMSG     MSGTYPE(*EXCP) MSGDTA(&MSG) MSGDTALEN(&MSGL) +
                          MSGID(&MSGID)
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(END))

             SNDPGMMSG  MSGID(&MSGID) MSGF(QCPFMSG) MSGDTA(%SST(&MSG +
                          1 &MSGL)) MSGTYPE(*ESCAPE)
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(END))

 END:        ENDPGM

