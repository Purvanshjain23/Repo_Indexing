     A*%%TS  SD  20010904  165432  ISLRAMS     REL-V4R4M0  5769-PW1
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD151
     A*  TITLE:         MAINTAIN BUILDINGS
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  CREATED:       03/18/94
     A*
     A*  INDICATOR 60 IS ON WHEN IN REVISE MODE
     A*  INDICATOR 61 IS ON WHEN IN CREATE MODE
     A*  INDICATOR 62 IS ON WHEN USER HAS DELETED OR CREATED A RECORD
     A*               IN THE SUBFILE
     A*  INDICATOR 90 IS ON WHEN THE FARM SITE THE USER HAS SELECTED
     A*               FROM THE WORK WITH HAS BEEN DELETED BY ANOTHER
     A*               USER
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A N90                                  CF05(05 'REFRESH')
     A  60N90                               CF06(06 'CREATE')
     A                                      CA12(12 'PREVIOUS')
     A N82N90                               ROLLUP(25 'PAGE FORWARD')
     A*
     A*--------------------------------------------------------------*
     A* SUBFILE FORMAT ONE
     A*--------------------------------------------------------------*
     A          R HP151S1                   SFL
     A*%%TS  SD  20010904  165432  ISLRAMS     REL-V4R4M0  5769-PW1
     A                                      SETOF(30)
     A                                      SETOF(31)
     A                                      SETOF(32)
     A                                      SETOF(62)
     A  85                                  SFLNXTCHG
     A            HDRRN1         4Y 0H
     A            DFOPT          2A  B  9  2CHGINPDFT(UL)
     A  61
     AO 62
     AO 90                                  DSPATR(PR)
     A  61
     AO 90                                  DSPATR(ND)
     A  30                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A N30N61N90                            DSPATR(HI)
     A  60N30N90                            DSPATR(UL)
     A            DFBLCD    R        B  9  8REFFLD(FBBLCD)
     A  60
     AO 62
     AO 90                                  DSPATR(PR)
     A  31                                  DSPATR(RI)
     A  31                                  DSPATR(PC)
     A N31N60                               DSPATR(HI)
     A  61N31N90                            DSPATR(UL)
     A            DFBLDS    R        B  9 16REFFLD(FBBLDS)
     A  62
     AO 90                                  DSPATR(PR)
     A  32                                  DSPATR(RI)
     A  32                                  DSPATR(PC)
     A N32                                  DSPATR(HI)
     A N32N90                               DSPATR(UL)
     A            DFACT          8A  O  9 40
     A*
     A*--------------------------------------------------------------*
     A* SUBFILE CONTROL FORMAT ONE
     A*--------------------------------------------------------------*
     A          R HP151C1                   SFLCTL(HP151S1)
     A*%%TS  SD  20010904  165432  ISLRAMS     REL-V4R4M0  5769-PW1
     A                                      SFLSIZ(0100)
     A                                      SFLPAG(0013)
     A                                      OVERLAY
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 25'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A            DFMODE    R        O  2  2REFFLD(@@MODE)
     A                                      DSPATR(HI)
     A                                  2 26'MAINTAIN BUILDINGS'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  4  2'Farm site ..:'
     A            C1FSCD    R        O  4 17REFFLD(FSFSCD)
     A                                      EDTCDE(M)
     A            C1FSNM    R        O  4 24REFFLD(FSFSNM)
     A N61                              6  2'4=Delete'
     A N61                              6 12'7=Maintain Rooms'
     A N61                              8  2'Opt'
     A                                      DSPATR(HI)
     A                                  8  8'Bldg'
     A                                      DSPATR(HI)
     A                                  8 16'Building Description'
     A                                      DSPATR(HI)
     A*
     A*--------------------------------------------------------------*
     A* COMMAND KEY RECORD 1
     A*--------------------------------------------------------------*
     A          R HP151K1
     A*%%TS  SD  19940321  100022  ISLRAMS     REL-V2R2M0  5738-PW1
     A                                      SETOF(60)
     A                                      SETOF(61)
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A                                      DSPATR(HI)
     A                                 23  2'F3=Exit'
     A N90                             23 11'F5=Refresh'
     A  60N90                          23 23'F6=Create'
     A                                 23 34'F12=Previous'
     A*
     A*--------------------------------------------------------------*
     A* ERROR MESSAGE SUBFILE FORMAT
     A*--------------------------------------------------------------*
     A          R HP151ES                   SFL SFLMSGRCD(24)
     A*
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
     A*                                                                *********
     A*--------------------------------------------------------------*
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*--------------------------------------------------------------*
     A          R HP151EC                   SFLCTL(HP151ES)
     A*
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
