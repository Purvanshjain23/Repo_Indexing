     A*%%TS  SD  19940629  155449  ISLRAMS     REL-V2R2M0  5738-PW1
     A*****************************************************************
     A*
     A*  SYSTEM:        HOG PRODUCTION
     A*  DISPLAY FILE:  HPD563 - SELECT SEQUENCE OF FARM COST LISTING
     A*  RPG PROGRAM:   HP563  - SELECT SEQUENCE OF FARM COST LISTING
     A*  CREATED BY:    LEANNE FEDOR
     A*  DATE:          06/29/94
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA12(12 'PREVIOUS')
     A N82                                  ROLLUP(25 'PAGE FORWARD')
     A*---------------------------------------------------------------
     A* REFERENCE WINDOW RECORD FORMAT 1
     A*---------------------------------------------------------------
     A          R HP563R1
     A                                      TEXT('Reference window')
     A                                      WINDOW(*DFT 8 37)
     A                                      WDWBORDER((*DSPATR RI) (*CHAR '    -
     A                                          '))
     A*---------------------------------------------------------------
     A* RECORD FORMAT TWO
     A*---------------------------------------------------------------
     A          R HP563R2
     A                                      TEXT('Written in RPG pgm to prevent-
     A                                       screen from clearing')
     A                                      CLRL(*NO)
     A                                      OVERLAY
     A*---------------------------------------------------------------
     A* RECORD FORMAT THREE
     A* THIS FORMAT IS NOT WRITTEN IN THE RPG PGM.  IT PREVENTS THE
     A* CALLING PROGRAM SCREEN FROM BEING CLEARED.
     A*---------------------------------------------------------------
     A          R HP563R3
     A                                      TEXT('Dummy format not written in R-
     A                                      PG pgm.')
     A                                      ASSUME
     A                                 11  1' '
     A*---------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP563S1                   SFL
     A*%%TS  SD  19940408  150401  ISMPALM     REL-V2R2M0  5738-PW1
     A                                      TEXT('subfile record format one')
     A            HDRRN1         4S 0H
     A            DFSQ      R        O  4  1REFFLD(@@FLAG)
     A            DFSQDS    R        O  4  5REFFLD(@@DS30)
     A
     A*---------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP563C1                   SFLCTL(HP563S1)
     A                                      SFLSIZ(0005)
     A                                      SFLPAG(0002)
     A                                      WINDOW(HP563R1)
     A                                      TEXT('control format one')
     A                                      OVERLAY
     A                                      SFLCSRRRN(&HDRRN)
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND(*MORE)
     A                                      RMVWDW
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            HDRRN          5S 0H
     A                                  1  1'SELECT SEQUENCE OF FARM COST LISTI-
     A                                      NG'
     A                                      DSPATR(HI)
     A                                  2  1'Position Cursor, Press Enter'
     A
     A*--------------------------------------------------------------
     A* COMMAND LINE
     A*--------------------------------------------------------------
     A          R HP563K1
     A                                      WINDOW(HP563R1)
     A                                  7  1'F12=Previous'
