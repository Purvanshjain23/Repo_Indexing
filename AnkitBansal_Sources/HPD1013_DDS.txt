     A*%%TS  SD  20030226  162329  ISLRAMS     REL-V5R1M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD1013
     A*  TITLE:         REVISE LOAD CREW
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  CREATED:       12/23/02
     A*
     A*
     A*  INDICATOR 60 IS ON WHEN IN REVISE MODE
     A*  INDICATOR 61 IS ON WHEN IN CREATE MODE
     A*  INDICATOR 62 IS ON WHEN IN DELETE MODE OR WHEN THE RECORD
     A*               HAS BEEN DELETED OUT FROM UNDER THE USER.
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A N62                                  CF04(04 'PROMPT')
     A  60N62                               CF05(05 'REFRESH')
     A  60N62                               CF11(11 'DELETE')
     A                                      CA12(12 'PREVIOUS')
     A*
     A*---------------------------------------------------------------
     A* RECORD FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP1013R1
     A*%%TS  SD  20030226  162329  ISLRAMS     REL-V5R1M0  5722-WDS
     A                                      SETOF(34)
     A                                      SETOF(35)
     A                                      SETOF(36)
     A                                      SETOF(37)
     A                                      SETOF(38)
     A                                      SETOF(56)
     A                                      SETOF(57)
     A                                      RTNCSRLOC(&RCD &FLD)
     A                                      OVERLAY
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 27'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USR)
     A                                      DSPATR(HI)
     A            D1MODE    R        O  2  2REFFLD(@@MODE)
     A                                      DSPATR(HI)
     A                                  2 29'REVISE LOAD CREW'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@JOB)
     A                                      DSPATR(HI)
     A                                  4  2'Load crew code ..............'
     A            D1LCCD    R        B  4 34REFFLD(@@LCCD)
     A N60N62N35                            DSPATR(HI)
     A  35                                  DSPATR(PC)
     A  35                                  DSPATR(RI)
     A  60
     AO 62                                  DSPATR(PR)
     A N60N62                               DSPATR(UL)
     A                                  5  2'Load crew description .......'
     A            D1LCDS    R        B  5 34REFFLD(HSRREF/@@LCDS *LIBL/HSPREF)
     A                                      CHECK(LC)
     A  34                                  DSPATR(RI)
     A  34                                  DSPATR(PC)
     A  62                                  DSPATR(PR)
     A N62                                  DSPATR(UL)
     A N62N34                               DSPATR(HI)
     A                                  6  2'Active/inactive status ......'
     A N62                              6 32'?'
     A            D1AIST    R        B  6 34REFFLD(HSRREF/@@AIST *LIBL/HSPREF)
     A  36                                  DSPATR(RI)
     A  36
     AO 56                                  DSPATR(PC)
     A  62                                  DSPATR(PR)
     A N62                                  DSPATR(UL)
     A N62N36                               DSPATR(HI)
     A                                  7  2'Transportation office code ..'
     A N62                              7 32'?'
     A            D1TOCD    R        B  7 34REFFLD(HSRREF/@@TOCD *LIBL/HSPREF)
     A  37                                  DSPATR(RI)
     A  37
     AO 57                                  DSPATR(PC)
     A  62                                  DSPATR(PR)
     A N62                                  DSPATR(UL)
     A N62N37                               DSPATR(HI)
     A            D1TONM    R        O  7 42REFFLD(HSRREF/@@TONM *LIBL/HSPREF)
     A                                  8  2'Default shift ...............'
     A            D1SHIFT   R        B  8 34REFFLD(HSRREF/@@SHIFT *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A  38                                  DSPATR(RI)
     A  38                                  DSPATR(PC)
     A  62                                  DSPATR(PR)
     A N62                                  DSPATR(UL)
     A N62N38                               DSPATR(HI)
     A*
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R HP1013K1
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A    N62                          23 11'F4=Prompt'
     A N62N61                          23 23'F5=Refresh'
     A N62N61                          23 35'F11=Delete'
     A                                 23 45'F12=Previous'
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *---------------------------------------------------------------
     A          R HP1013ES                  SFL SFLMSGRCD(24)
      *
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE CONTROL FORMAT
      *---------------------------------------------------------------
     A          R HP1013EC                  SFLCTL(HP1013ES)
      *
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
