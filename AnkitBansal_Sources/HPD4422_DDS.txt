     A*%%TS  SD  20120119  164715  ISLRAMS     REL-V6R1M0  5761-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION
     A*  DISPLAY FILE:  HPD4422
     A*  TITLE:         SPECIFY OPTIONS FOR BGF WEEKLY WEAN PROJECTIONS
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  DATE:          07/07/04
     A*
     A*  NOTE:  THIS IS A POP-UP WINDOW. THE WINDOW DEFINITION IS IN
     A*         RECORD FORMAT HP4422EC
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CF03(03 'EXIT')
     A                                      CF04(04 'PROMPT')
     A                                      CF08(08 'SUBMIT')
     A*
     A*----------------------------------------------------------------
     A* RECORD FORMAT ONE
     A*----------------------------------------------------------------
     A          R HP4422R1
     A*%%TS  SD  20120119  164715  ISLRAMS     REL-V6R1M0  5761-WDS
     A                                      WINDOW(HP4422EC)
     A                                      TEXT('INPUT SCREEN')
     A                                      SETOF(30)
     A                                      SETOF(31)
     A                                      SETOF(32)
     A                                      SETOF(33)
     A                                      SETOF(34)
     A                                      SETOF(35)
     A                                      SETOF(36)
     A                                      SETOF(40)
     A                                      SETOF(51)
     A                                      OVERLAY
     A                                      RTNCSRLOC(&RCD &FLD)
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  1REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 14'SPECIFY OPTIONS FOR BGF WEEKLY WEA-
     A                                      N PROJECTIONS LISTING'
     A                                      DSPATR(HI)
     A                                  3 27'Year'
     A                                  3 35'Week'
     A                                  4  1'From report year/week .'
     A            D1FCDYR   R        B  4 27REFFLD(@@RPCDYR)
     A                                      EDTCDE(M)
     A  32                                  DSPATR(PC)
     A  32                                  DSPATR(RI)
     A N32                                  DSPATR(HI)
     A            D1FCDWK   R        B  4 36REFFLD(@@RPCDWK)
     A                                      EDTCDE(M)
     A  33                                  DSPATR(PC)
     A  33                                  DSPATR(RI)
     A N33                                  DSPATR(HI)
     A                                  5  1'To report year/week ...'
     A            D1TCDYR   R        B  5 27REFFLD(@@RPCDYR)
     A                                      EDTCDE(M)
     A  35                                  DSPATR(PC)
     A  35                                  DSPATR(RI)
     A N35                                  DSPATR(HI)
     A            D1TCDWK   R        B  5 36REFFLD(@@RPCDWK)
     A                                      EDTCDE(M)
     A  36                                  DSPATR(PC)
     A  36                                  DSPATR(RI)
     A N36                                  DSPATR(HI)
     A                                  7  1'Report/Download .......'
     A                                  7 25'?'
     A N99        D1RPFL    R        B  7 27REFFLD(HSRREF/@@FLAG *LIBL/HSPREF)
     A N30                                  DSPATR(HI)
     A  30                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A                                  7 35'(R=Report, D=Download)'
     A                                  8  7'Email address ...'
     A            D1EMAIL   R        B  8 26REFFLD(HSRREF/@@EMAIL *LIBL/HSPREF)
     A                                      CHGINPDFT(UL LC FE)
     A N40                                  DSPATR(HI)
     A  40                                  DSPATR(PC)
     A  40                                  DSPATR(RI)
     A                                 10  1'Cell ..................'
     A            D1CELL    R        B 10 27REFFLD(HSRREF/@@CELL *LIBL/HSPREF)
     A                                      EDTCDE(M)
     A  34                                  DSPATR(PC)
     A  34                                  DSPATR(RI)
     A N34                                  DSPATR(HI)
     A                                 10 35'(Blank for all)'
     A                                 11  1'Farm site .............'
     A                                 11 25'?'
     A            D1FSCD    R        B 11 27REFFLD(HSRREF/@@FSCD *LIBL/HSPREF)
     A                                      EDTCDE(M)
     A  31
     AO 51                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A N31                                  DSPATR(HI)
     A                                 11 35'(Blank for all)'
     A*
     A*----------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*----------------------------------------------------------------
     A          R HP4422K1
     A*%%TS  SD  20120119  161842  ISLRAMS     REL-V6R1M0  5761-WDS
     A                                      WINDOW(HP4422EC)
     A                                      TEXT('COMMAND KEYS')
     A                                      OVERLAY
     A  43                             12  2'Message program failed. Call IS.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 13  1'F3=Exit'
     A                                 13 10'F4=Prompt'
     A                                 13 21'F8=Submit'
      *
      *----------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *----------------------------------------------------------------
     A          R HP4422ES
     A                                      TEXT('Error message subfile')
     A                                      SFL SFLMSGRCD(14)
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
     A*
     A*----------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT WITH WINDOW DEFINITION
     A*----------------------------------------------------------------
     A          R HP4422EC                  SFLCTL(HP4422ES)
     A*%%TS  SD  20000628  103649  ISLRAMS     REL-V4R2M0  5769-PW1
     A                                      TEXT('Error message subfile')
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A                                      WINDOW(*DFT 15 75)
     A                                      WDWBORDER((*DSPATR RI) (*CHAR '    -
     A                                          '))
     A            MSGPGM                    SFLPGMQ(10)
     A*
     A*----------------------------------------------------------------
     A* ASSUME FORMAT - REQUIRED FOR WINDOWS
     A*----------------------------------------------------------------
     A*
     A          R ASSUME
     A*%%TS  SD  20000627  095625  ISLRAMS     REL-V4R2M0  5769-PW1
     A                                      TEXT('ASSUME FORMAT FOR WINDOWS')
     A                                      ASSUME
     A                                      OVERLAY
     A                                 24 79' '
