     A*%%TS  SD  20040225  103144  ISLRAMS     REL-V5R2M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD1713
     A*  TITLE:         CHANGE FARM BUDGET DATES
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  CREATED:       02/25/04
     A*
     A*
     A*  INDICATOR 62 IS ON WHEN IN DELETE MODE OR WHEN THE RECORD
     A*               HAS BEEN DELETED OUT FROM UNDER THE USER.
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A N62                                  CF05(05 'REFRESH')
     A*
     A*---------------------------------------------------------------
     A* RECORD FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP1713R1
     A*%%TS  SD  20040225  102304  ISLRAMS     REL-V5R2M0  5722-WDS
     A                                      SETOF(33)
     A                                      SETOF(34)
     A                                      OVERLAY
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 27'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USR)
     A                                      DSPATR(HI)
     A                                  2 26'CHANGE FARM BUDGET DATES'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@JOB)
     A                                      DSPATR(HI)
     A*
     A                                  4  2'Farm site .........:'
     A            D1FSCD    R        O  4 24REFFLD(HSRREF/@@FSCD *LIBL/HSPREF)
     A                                      EDTCDE(M)
     A            D1FSNM    R        O  4 31REFFLD(HSRREF/FSFSNM *LIBL/HSPREF)
     A                                  5  2'Budget template ...:'
     A            D1BTCD    R        O  5 23REFFLD(HSRREF/@@BTCD *LIBL/HSPREF)
     A            D1BTDS    R        O  5 31REFFLD(HSRREF/@@BTDS *LIBL/HSPREF)
     A                                  6  2'Budget availability:'
     A            D1BACD    R        O  6 23REFFLD(HSRREF/@@BACD *LIBL/HSPREF)
     A            D1BADS    R        O  6 31REFFLD(HSRREF/@@BADS *LIBL/HSPREF)
     A                                  7  2'Creation schedule .:'
     A            D1CSCD    R        O  7 23REFFLD(HSRREF/@@CSCD *LIBL/HSPREF)
     A            D1CSDS    R        O  7 31REFFLD(HSRREF/@@CSDS *LIBL/HSPREF)
     A                                  8  2'Weeks .............:'
     A            D1BTWK    R        O  8 23REFFLD(HSRREF/@@BTWK *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A                                 10  2'Budget number:'
     A            D1FBSN    R        O 10 18REFFLD(HSRREF/@@FBSN *LIBL/HSPREF)
     A                                      EDTCDE(Z)
     A                                 11  2'Budget status:'
     A            D1FBSDS   R        O 11 18REFFLD(HSRREF/@@FBSDS *LIBL/HSPREF)
     A                                 13  2'From date ....'
     A            D1FMDY    R        B 13 18REFFLD(HSRREF/@@DT06 *LIBL/HSPREF)
     A                                      EDTWRD('  /  /  ')
     A  33                                  DSPATR(RI)
     A  33                                  DSPATR(PC)
     A N33N62                               DSPATR(HI)
     A  62                                  DSPATR(PR)
     A N62                                  DSPATR(UL)
     A                                 14  2'To date ......'
     A            D1TMDY    R        B 14 18REFFLD(HSRREF/@@DT06 *LIBL/HSPREF)
     A                                      EDTWRD('  /  /  ')
     A  34                                  DSPATR(RI)
     A  34                                  DSPATR(PC)
     A N34N62                               DSPATR(HI)
     A  62                                  DSPATR(PR)
     A N62                                  DSPATR(UL)
     A*
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R HP1713K1
     A*%%TS  SD  20040225  103144  ISLRAMS     REL-V5R2M0  5722-WDS
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A N62                             23 11'F5=Refresh'
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *---------------------------------------------------------------
     A          R HP1713ES                  SFL SFLMSGRCD(24)
      *
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE CONTROL FORMAT
      *---------------------------------------------------------------
     A          R HP1713EC                  SFLCTL(HP1713ES)
      *
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
