      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Hog Production System
      * PROGRAM:     HP6702
      * TITLE:       Template/Farm Budget Variance Listing
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     02/09/04
      *
      *            This listing is submitted from HP4700-Work With Budget Templates.
      *
      *            The listing prints in 3 sections:
      *               1) quantity variances on matched items
      *               2) template items omitted from farm budgets
      *               3) farm budget items not on the template
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 10/13/04  LeAnne Fedor
      *           Recompile only. Cost Per Unit Amount was changed from
      *           9,2 to 9,4.
      *
      * 10/03/05  LeAnne Fedor
      *           Removed all/any logic that limited processing to Non-Zero
      *           Budgeted Quantities.
      *
      * 10/16/13  LeAnne Ramsey (E2831)
      *           Recompile only. Added field 'MTech Reference'.
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fhsp018    if   e           k disk
      *    Farm site
      *
      *
     Fhsl181c   if   e           k disk
      *  Items (Logical selects only active items where Budget Flag is YES)
      *
      *
     Fhsp185    if   e           k disk
      *    Budget template header
      *
      *
     Fhsp186    if   e           k disk
      *    Budget template detail
      *
      *
     Fhsl186b   if   e           k disk    rename(tdrec:tdrecb)
      *    Budget template detail
      *
      *
     Fhsl188a   if   e           k disk
      *    Farm budget header
      *
      *
     Fhsp189    if   e           k disk
      *    Farm budget detail
      *
      *
     Fhsj189c   if   e           k disk    rename(jdrec:jdrecc)
      *    Farm budget detail + farm budget header
      *
      *
     Fqprint    o    f  165        printer oflind(*inof)
      *
      /EJECT
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Standard fields
      *
     D prtfl           s              1    inz('N')
     D dash            s            165    inz(*all'-')
     d rtime           s              6  0
      *
      *
      * Print fields
      *
     D l2fsnm          s                   like(fsfsnm)
     D l2desc          s                   like(imitds)
      *
     D l3fsnm          s                   like(fsfsnm)
     D l3desc          s                   like(imitds)
      *
     D l4fsnm          s                   like(fsfsnm)
     D l4desc          s                   like(imitds)
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *---------------------------------------------------------------
      * Local data area.
      *---------------------------------------------------------------
      *
     Dlda             uds                  dtaara(*lda)
     D  ldbtcd                 2      7
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * MAINLINE
      ****************************************************************
      *
      * Retrieve the Budget Template Description and print headings.
      *
     C     ldbtcd        chain     hsp185                             92
     C                   if        *in92 = *off                                 If header hit
     C                   except    h1hdr
     C                   endif                                                  If header hit
      *
     C                   except    h2hdr
      *
      * Retrieve/print all records where the Template Quantity does not
      * match the Farm Budget quantity.
      *
     C                   exsr      $quantity
      *
      * Retrieve/print all Items that are on a Template but not on the Farm
      * Budget.
     C                   exsr      $template
      *
      *
      * Retrieve/print all Items that are on Farm Budget but not on the Template.
      *
     C                   exsr      $farms
      *
      *--------------------
      * EOF processing
      *--------------------
      *
     C                   seton                                        lr
      /eject
      *------------------------------------------------------------------------------------
      * Retrieve/print all Template items where the Farm Budget quantity is different
      *------------------------------------------------------------------------------------
      *
     C     $quantity     begsr
      *
     C                   move      no            prtfl
      *
      * Read all Budget Template Detail records for this Template. Process
      * all records--even those with zero quantity.
      *
     C     ldbtcd        setll     hsp186
     C                   dou       *in91 = *on                                  Do items
     C     ldbtcd        reade     hsp186                                 91
     C                   if        *in91 = *off                                 If not eof
      *
      * Populate 'description' for the Budget Item
      *
     C     tdbgit        chain     hsl181c                            92
     C                   select
     C                   when      *in92 = *on
     C                   eval      l2desc = 'Unknown'
      *
     C                   when      imitcd = imbgit
     C                   eval      l2desc = imitds
     C                   other
      *
     C                   eval      l2desc = imbgit
     C                   endsl
      *
      * Read all Inactive/Active Farm Budgets for this Budgeted Item
      *
     C     key01         setll     hsj189c
     C                   dou       *in93 = *on                                  Do farm items
     C     key01         reade     hsj189c                                93
     C                   if        *in93 = *off and                             If mismatch
     C                             tdbtqt <> jdbgqt
      *
      * Retrieve Farm Site Name
      *
     C     jhfscd        chain     hsp018                             92
     C                   if        *in92 = *off
     C                   eval      l2fsnm = fsfsnm
     C                   else
     C                   eval      l2fsnm = 'Unknown'
     C                   endif
      * Print
     C                   if        *inof = *on
     C                   except    h1hdr
     C                   except    h2hdr
     C                   endif
     C                   except    l2dtl
     C                   move      yes           prtfl
      *
     C                   endif                                                  If mismatch
     C                   enddo                                                  Do farm items
      *
     C                   endif                                                  If not eof
     C                   enddo                                                  Do items
      *
     C                   if        prtfl = no
     C                   except    noquantity
     C                   endif
      *
     C                   endsr
      /eject
      *------------------------------------------------------------------------------------
      * Retrieve/print all Items on a Template that are not on the Farm Budget
      *------------------------------------------------------------------------------------
      *
     C     $template     begsr
      *
     C                   except    h3hdr
     C                   move      no            prtfl
      *
      * Read all Budget Template Detail records for this Template. Process
      * all records--even zero quantity records.
      *
     C     ldbtcd        setll     hsp186
     C                   dou       *in91 = *on                                  Do items
     C     ldbtcd        reade     hsp186                                 91
     C                   if        *in91 = *off                                 If more
      *
      *
      * Populate 'description' for the Budget Item
      *
     C     tdbgit        chain     hsl181c                            92
     C                   select
     C                   when      *in92 = *on
     C                   eval      l3desc = 'Unknown'
      *
     C                   when      imitcd = imbgit
     C                   eval      l3desc = imitds
     C                   other
      *
     C                   eval      l3desc = imbgit
     C                   endsl
      *
      *  Read all Farm Budget Header Records for this template that
      *  are not Closed.
      *
     C     ldbtcd        setll     hsl188a
     C                   dou       *in93 = *on                                  Do farms
     C     ldbtcd        reade     hsl188a                                93
     C                   if        *in93 = *off                                 If not eof
      *
      * For each Farm, see if it has this Template item.
      *
     C     key02         chain     hsp189                             92
     C                   if        *in92 = *on                                  If missing
      *
      * Retrieve Farm Site name
      *
     C     jhfscd        chain     hsp018                             92
     C                   if        *in92 = *off
     C                   eval      l3fsnm = fsfsnm
     C                   else
     C                   eval      l3fsnm = 'Unknown'
     C                   endif
      *
     C                   if        *inof = *on
     C                   except    h1hdr
     C                   except    h3hdr
     C                   endif
     C                   except    l3dtl
     C                   move      yes           prtfl
     C                   endif                                                  If missing
      *
     C                   endif                                                  If not eof
     C                   enddo                                                  Do farms
      *
     C                   endif                                                  If more
     C                   enddo                                                  Do items
      *
     C                   if        prtfl = no
     C                   except    notemplate
     C                   endif
      *
     C                   endsr
      /eject
      *--------------------------------------------------------------------------------------
      * Find/print all items on Farm Budgets that are not on the Template
      *--------------------------------------------------------------------------------------
      *
     C     $farms        begsr
      *
     C                   except    h4hdr
     C                   move      no            prtfl
      *
      * Read all Inactive/Active Farm Budgets for this Template
      *
     C     ldbtcd        setll     hsj189c
     C                   dou       *in93 = *on                                  Do farm items
     C     ldbtcd        reade     hsj189c                                93
     C                   if        *in93 = *off and jdbgqt <> 0                 If not eof
      *
      * See if this Item is in the Template Detail file
      *
     C     key03         chain     hsl186b                            92
     C                   if        *in92 = *on                                  If not on temp
      *
      *
      * Populate 'description' for the Budget Item
      *
     C     tdbgit        chain     hsl181c                            92
     C                   select
     C                   when      *in92 = *on
     C                   eval      l4desc = 'Unknown'
      *
     C                   when      imitcd = imbgit
     C                   eval      l4desc = imitds
     C                   other
      *
     C                   eval      l4desc = imbgit
     C                   endsl
      *
      * Retrieve Farm Site name
      *
     C     jhfscd        chain     hsp018                             92
     C                   if        *in92 = *off
     C                   eval      l4fsnm = fsfsnm
     C                   else
     C                   eval      l4fsnm = 'Unknown'
     C                   endif
      *
     C                   if        *inof = *on
     C                   except    h1hdr
     C                   except    h4hdr
     C                   endif
     C                   except    l4dtl
     C                   move      yes           prtfl
      *
     C                   endif                                                  If not on temp
     C                   endif                                                  If not eof
     C                   enddo                                                  Do farm items
      *
     C                   if        prtfl = no
     C                   except    nofarms
     C                   endif
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Key lists
      *
     C     key01         klist
     C                   kfld                    ldbtcd
     C                   kfld                    tdbgit
      *
     C     key02         klist
     C                   kfld                    jhfbsn
     C                   kfld                    tdbgit
      *
     C     key03         klist
     C                   kfld                    jdbgit
     C                   kfld                    jdbtcd
      *
      * Set up a line of underscores and retrieve time for report heading.
      *
     C                   time                    rtime
      *
     C                   endsr
      /EJECT
      *-------------------------------------------------------------
      * Report heading lines
      *-------------------------------------------------------------
      *
     Oqprint    e            h1hdr          1 01
     O                       sdpgm               10
     O                                           85 'HOG PRODUCTION SYSTEM'
     O                                          155 'DATE'
     O                       udate         y    165
      *
     O          e            h1hdr          1
     O                       sdusr               10
     O                                           78 'TEMPLATE/FARM BUDGET '
     O                                           94 'VARIANCE LISTING'
     O                                          155 'TIME'
     O                       rtime              165 '  :  :  '
      *
     O          e            h1hdr          2
     O                                          155 'PAGE'
     O                       page          z    165
      *
      *
     O          e            h1hdr          2
     O                                           16 'Budget template:'
     O                       ldbtcd              24
     O                       thbtds              51
      *
      *
     O          e            h1hdr          1
     O                                           53 'Template'
     O                                           73 'Farm Budget'
     O                                           84 'Farm'
      *
     O          e            h1hdr          1
     O                                           23 'Budgeted Item'
     O                                           53 'Quantity'
     O                                           73 'Quantity'
     O                                           84 'Site'
     O                                           90 'Name'
     O                                          136 'Budget Item Description'
      *
     O          e            h1hdr          0
     O                       dash               165
      *
      *-------------------------------------------------------------
      * Headings for Mismatched Quantity Section
      *-------------------------------------------------------------
      *
     O          e            h2hdr       2  2
     O                                           22 'Quantity Variances on '
     O                                           37 'Matching Items:'
      *
      *-------------------------------------------------------------
      * Headings for Template Items Omitted from Farm Budgets
      *-------------------------------------------------------------
      *
     O          e            h3hdr       2  2
     O                                           22 'Template Items Omitted'
     O                                           41 ' from Farm Budgets:'
      *
      *
      *-------------------------------------------------------------
      * Headings for Farm Budget Items not Template
      *-------------------------------------------------------------
      *
     O          e            h4hdr       2  2
     O                                           22 'Farm Budget Items not '
     O                                           38 'on the Template:'
      *
      *-------------------------------------------------------------
      * Detail line for quantity variance section
      *-------------------------------------------------------------
      *
     O          e            l2dtl          1
     O                       tdbgit              35
     O                       tdbtqt        k     54
     O                       jdbgqt        k     74
     O                       jdfscd        z     84
     O                       l2fsnm             111
     O                       l2desc             163
      *
      *-------------------------------------------------------------
      * Detail line for Template Items Missing from Farm Budgets
      *-------------------------------------------------------------
      *
     O          e            l3dtl          1
     O                       tdbgit              35
     O                       tdbtqt        k     54
     O                       jhfscd        z     84
     O                       l3fsnm             111
     O                       l3desc             163
      *
      *-------------------------------------------------------------
      * Detail line for Farm Budget Items not on the Tempalte
      *-------------------------------------------------------------
      *
     O          e            l4dtl          1
     O                       jdbgit              35
     O                       jdbgqt        k     74
     O                       jhfscd        z     84
     O                       l4fsnm             111
     O                       l4desc             163
      *
      *-------------------------------------------------------------
      * No quantity variance line
      *-------------------------------------------------------------
      *
     O          e            noquantity  1  1
     O                                           31 'There are no quantity'
     O                                           42 ' variances.'
      *-------------------------------------------------------------
      * No template items are missing from any active budgets
      *-------------------------------------------------------------
      *
     O          e            notemplate  1  1
     O                                           33 'No item on the template'
     O                                           55 ' has been omitted from'
     O                                           72 ' any Farm Budget.'
      *----------------------------------------------------------------------------
      * No Farm Budgets have any item that is not also on the Template
      *----------------------------------------------------------------------------
      *
     O          e            nofarms     1  1
     O                                           33 'There are no items on a'
     O                                           55 'ny Farm Budgets that a'
     O                                           78 're not on the Template.'
