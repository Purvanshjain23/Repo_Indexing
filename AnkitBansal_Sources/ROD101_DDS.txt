     A*%%TS  SD  20140120  102459  ISLRAMS     REL-V6R1M0  5761-WDS
     A*****************************************************************
     A*
     A*  ENVIRONMENT:   PORK DIVISION
     A*  SYSTEM:        RESOURCE OPTIMIZATION
     A*  DISPLAY FILE:  ROD101
     A*  TITLE:         REVISE RESOURCE OPTIMIZATION HEADER
     A*  PROGRAMMER:    LEANNE RAMSEY
     A*  DATE:          01/16/07
     A*
     A*  INDICATOR 60 IS ON WHEN IN REVISE MODE
     A*  INDICATOR 61 IS ON WHEN IN CREATE MODE
     A*  INDICATOR 62 IS ON WHEN IN DELETE MODE
     A*  INDICATOR 63 IS ON WHEN STATUS IS PRELIMINARY OR FINAL
     A*  INDICATOR 64 IS ON WHEN THE PROGRAM IS POPULATING WEEK-ENDING DATE
     A*  INDICATOR 65 FORCES THE CURSOR PAST WEEK-ENDING DATE TO THE FIRST ENTRY
     A*               FIELD.
     A*****************************************************************
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/ROPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A  60N62                               CF05(05 'REFRESH')
     A  60
     AO 61                                  CF09(09 'ACCEPT')
     A  60N62                               CF11(11 'DELETE')
     A*--------------------------------------------------------------
     A* RECORD FORMAT ONE
     A*--------------------------------------------------------------
     A          R RO101R1
     A*%%TS  SD  20140120  102459  ISLRAMS     REL-V6R1M0  5761-WDS
     A                                      SETOF(26)
     A                                      SETOF(27)
     A                                      SETOF(28)
     A                                      SETOF(29)
     A                                      SETOF(30)
     A                                      SETOF(31)
     A                                      SETOF(32)
     A                                      SETOF(33)
     A                                      SETOF(34)
     A                                      SETOF(35)
     A                                      SETOF(36)
     A                                      SETOF(37)
     A                                      SETOF(38)
     A                                      SETOF(39)
     A                                      SETOF(40)
     A                                      SETOF(41)
     A                                      SETOF(42)
     A                                      SETOF(44)
     A                                      SETOF(45)
     A                                      SETOF(46)
     A                                      SETOF(47)
     A                                      SETOF(48)
     A                                      SETOF(49)
     A                                      OVERLAY
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 25'RESOURCE OPTIMIZATION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USR)
     A                                      DSPATR(HI)
     A            D1MODE    R        O  2  2REFFLD(@@MODE)
     A                                      DSPATR(HI)
     A                                  2 21'REVISE RESOURCE OPTIMIZATION HEADE-
     A                                      R'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USR)
     A                                      DSPATR(HI)
     A                                  4  2'Company number ....:'
     A            D1CONO    R        O  4 23REFFLD(@@CONO)
     A                                      EDTCDE(M)
     A            D1CONM    R        O  4 28REFFLD(@ABREAI/ABADTX *LIBL/CAABREP)
     A                                  4 60'Status:'
     A            D1RCSTCD  R        O  4 68REFFLD(@@RCSTCD)
     A                                  5  2'Week-ending date ..:'
     A            D1WEMDY   R        B  5 23REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  ')
     A  61N30N64                            DSPATR(HI)
     A  30                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A N64                                  DSPATR(PR)
     A  61N64                               DSPATR(UL)
     A  63                              7  2'BI credit value ...............:'
     A  63        D1BICRVL  R        O  7 35REFFLD(OHREC/OHBICRVL *LIBL/ROP100)
     A                                      EDTCDE(K)
     A  63                              7 48'Sirloin Component Allotment %'
     A                                      DSPATR(UL)
     A                                      COLOR(BLU)
     A                                  8  2'Smoker yield % from BI picnic ..'
     A            D1SMOKYPC R        B  8 35REFFLD(OHREC/OHALTRPC *LIBL/ROP100)
     A                                      EDTCDE(K)
     A  34
     AO 65                                  DSPATR(PC)
     A  34                                  DSPATR(RI)
     A N62N34                               DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(PR)
     A  63                              8 48'Bone-in ......:'
     A  63        D1BICAPC  R        O  8 65REFFLD(OHREC/OHBICAPC *LIBL/ROP100)
     A                                      EDTCDE(K)
     A                                  9  2'Sirloin trim 100% BI loin ......'
     A            D1BITRPC  R        B  9 35REFFLD(OHREC/OHBITRPC *LIBL/ROP100)
     A                                      EDTCDE(K)
     A  31                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A N62N31                               DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(PR)
     A*
     A  63                              9 48'Boneless .....:'
     A  63        D1BLCAPC  R        O  9 65REFFLD(OHREC/OHBLCAPC *LIBL/ROP100)
     A                                      EDTCDE(K)
     A*
     A  63                             10  2'Sirloin trim allocation % .....:'
     A  63        D1ALTRPC  R        O 10 35REFFLD(OHREC/OHALTRPC *LIBL/ROP100)
     A                                      EDTCDE(K)
     A                                 10 48'Sirloin ByProduct Yield %    '
     A                                      DSPATR(UL)
     A                                      COLOR(BLU)
     A                                 11 48'Bone-in .......'
     A            D1BIBYPC  R        B 11 65REFFLD(OHREC/OHBIBYPC *LIBL/ROP100)
     A                                      EDTCDE(K)
     A  32                                  DSPATR(PC)
     A  32                                  DSPATR(RI)
     A N62N32                               DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(PR)
     A                                 12  2'Cap&Blade Allocation %       '
     A                                      DSPATR(UL)
     A                                      COLOR(BLU)
     A                                 12 48'Boneless ......'
     A            D1BLBYPC  R        B 12 65REFFLD(OHREC/OHBLBYPC *LIBL/ROP100)
     A                                      EDTCDE(K)
     A  33                                  DSPATR(PC)
     A  33                                  DSPATR(RI)
     A N62N33                               DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(PR)
     A                                 13  2'Regular trim ..'
     A            D1CBRTPC  R        B 13 19REFFLD(OHREC/OHCBRTPC *LIBL/ROP100)
     A                                      EDTCDE(K)
     A  35                                  DSPATR(PC)
     A  35                                  DSPATR(RI)
     A N62N35                               DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(PR)
     A*
     A                                 14  2'Special trim ..'
     A            D1CBSTPC  R        B 14 19REFFLD(OHREC/OHCBSTPC *LIBL/ROP100)
     A                                      EDTCDE(K)
     A  36                                  DSPATR(PC)
     A  36                                  DSPATR(RI)
     A N62N36                               DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(PR)
     A                                 14 43'Skirt Meat Allocation   SBF   TF  -
     A                                        '
     A                                      DSPATR(UL)
     A                                      COLOR(BLU)
     A                                 15 49'Small commodity'
     A            D1SSCFL   R        B 15 68REFFLD(ROREF/@@SSCFL *LIBL/ROPREF)
     A  41                                  DSPATR(PC)
     A  41                                  DSPATR(RI)
     A N62N41                               DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(PR)
     A            D1TSCFL   R        B 15 73REFFLD(ROREF/@@TSCFL *LIBL/ROPREF)
     A  45                                  DSPATR(PC)
     A  45                                  DSPATR(RI)
     A N62N45                               DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(PR)
     A                                 16  2'Cap&Blade Yield %                 -
     A                                              '
     A                                      DSPATR(UL)
     A                                      COLOR(BLU)
     A                                 16 48'Medium commodity'
     A            D1SMCFL   R        B 16 68REFFLD(ROREF/@@SMCFL *LIBL/ROPREF)
     A  42                                  DSPATR(PC)
     A  42                                  DSPATR(RI)
     A N62N42                               DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(PR)
     A            D1TMCFL   R        B 16 73REFFLD(ROREF/@@TMCFL *LIBL/ROPREF)
     A  48                                  DSPATR(PC)
     A  48                                  DSPATR(RI)
     A N62N48                               DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(PR)
     A                                 17  7'Items:'
     A                                 17 14'See Listed Items'
     A                                      DSPATR(UL)
     A                                      COLOR(BLU)
     A  46                                  DSPATR(RI)
     A                                 17 33'83356/89998'
     A                                      DSPATR(UL)
     A                                      COLOR(BLU)
     A  47                                  DSPATR(RI)
     A                                 17 47'Converted Brisket'
     A            D1SCBFL   R        B 17 68REFFLD(ROREF/@@SCBFL *LIBL/ROPREF)
     A  44                                  DSPATR(PC)
     A  44                                  DSPATR(RI)
     A N62N44                               DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(PR)
     A            D1TCBFL   R        B 17 73REFFLD(ROREF/@@TCBFL *LIBL/ROPREF)
     A  49                                  DSPATR(PC)
     A  49                                  DSPATR(RI)
     A N62N49                               DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(PR)
     A                                 18  5'Cap Meat'
     A            D1CBCM1PC R        B 18 17REFFLD(OHREC/OHCBRTPC *LIBL/ROP100)
     A                                      EDTCDE(K)
     A  37                                  DSPATR(PC)
     A  37                                  DSPATR(RI)
     A N62N37                               DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(PR)
     A            D1CBCM2PC R        B 18 34REFFLD(OHREC/OHCBRTPC *LIBL/ROP100)
     A                                      EDTCDE(K)
     A  26                                  DSPATR(PC)
     A  26                                  DSPATR(RI)
     A N62N26                               DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(PR)
     A                                 19  3'Blade Meat'
     A            D1CBBM1PC R        B 19 17REFFLD(OHREC/OHCBRTPC *LIBL/ROP100)
     A                                      EDTCDE(K)
     A  38                                  DSPATR(PC)
     A  38                                  DSPATR(RI)
     A N62N38                               DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(PR)
     A            D1CBBM2PC R        B 19 34REFFLD(OHREC/OHCBRTPC *LIBL/ROP100)
     A                                      EDTCDE(K)
     A  27                                  DSPATR(PC)
     A  27                                  DSPATR(RI)
     A N62N27                               DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(PR)
     A                                 19 47'Cap&Blade Yield % Listed Items:'
     A                                      COLOR(BLU)
     A                                      DSPATR(UL)
     A                                 20  5'Reg Trim'
     A            D1CBRT1PC R        B 20 17REFFLD(OHREC/OHCBRTPC *LIBL/ROP100)
     A                                      EDTCDE(K)
     A  39                                  DSPATR(PC)
     A  39                                  DSPATR(RI)
     A N62N39                               DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(PR)
     A            D1CBRT2PC R        B 20 34REFFLD(OHREC/OHCBRTPC *LIBL/ROP100)
     A                                      EDTCDE(K)
     A  28                                  DSPATR(PC)
     A  28                                  DSPATR(RI)
     A N62N28                               DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(PR)
     A                                 20 47'951 953 955 83354 83357 89990'
     A                                 21  5'Cut Loss'
     A            D1CBCL1PC R        B 21 17REFFLD(OHREC/OHCBRTPC *LIBL/ROP100)
     A                                      EDTCDE(K)
     A  40                                  DSPATR(PC)
     A  40                                  DSPATR(RI)
     A N62N40                               DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(PR)
     A            D1CBCL2PC R        B 21 34REFFLD(OHREC/OHCBRTPC *LIBL/ROP100)
     A                                      EDTCDE(K)
     A  29                                  DSPATR(PC)
     A  29                                  DSPATR(RI)
     A N62N29                               DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(PR)
     A                                 21 47'89995 89996 89997 89999'
     A*
     A*--------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*--------------------------------------------------------------
     A          R RO101K1
     A*%%TS  SD  20100301  090440  ISLRAMS     REL-V5R4M0  5722-WDS
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A N62N61                          23 11'F5=Refresh'
     A N62                             23 23'F9=Accept'
     A N62N61                          23 34'F11=Delete'
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *---------------------------------------------------------------
     A          R RO101ES                   SFL SFLMSGRCD(24)
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE CONTROL FORMAT
      *---------------------------------------------------------------
     A          R RO101EC                   SFLCTL(RO101ES)
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
