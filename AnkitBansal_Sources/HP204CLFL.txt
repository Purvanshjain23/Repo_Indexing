/******************************************************************* */
/*                                                                   */
/*  PROGRAM NUMBER:  HP204CLFD                                       */
/*  PROGRAM NAME:    BUILD JDE UPLOAD FILE: HPS FEED POUNDS          */
/*  PROGRAMMER:      LEANNE FEDOR                                    */
/*  CREATE DATE:     11/07/01                                        */
/*                                                                   */
/*  FUNCTION:        THIS CL IS SUBMITTED FROM THE DRIVER CL         */
/*                   HP404CL.                                        */
/*                   OPTIONS ARE PASSED THROUGH THE LDA.             */
/*                   AN OPNQRY STATEMENT IS USED TO SELECT           */
/*                   RECORDS.                                        */
/*                                                                   */
/*                                                                   */
/*******************************************************************/
/* MODIFICATIONS:                                                  */
/* 10/17/18  Danny Nguyen  (S13682)                                */
/*           Retrieve Company Code from LDA. Added OPNQRYF logic   */
/*           for HSL018I to select by Company. Increase  &QDATA    */
/*           from 13 to 40.                                        */
/*                                                                 */
/*******************************************************************/

             PGM

             DCL        VAR(&QDATA) TYPE(*CHAR) LEN(40)
             DCL        VAR(&QDATAT) TYPE(*CHAR) LEN(65)

             DCL        VAR(&LDBPDT) TYPE(*DEC) LEN(8)
             DCL        VAR(&LDEPDT) TYPE(*DEC) LEN(8)
             DCL        VAR(&LDCOCD) TYPE(*CHAR) LEN(3)

             CHGVAR     VAR(&LDBPDT) VALUE(%SST(*LDA 7 8))
             CHGVAR     VAR(&LDEPDT) VALUE(%SST(*LDA 15 8))
             CHGVAR     VAR(&LDCOCD) VALUE(%SST(*LDA 25 3))


/*********************************************************************/
/*  OPEN QUERY FOR FARMS                                             */
/*********************************************************************/

/* ALWAYS SELECT ONLY FARMS THAT HAVE A COST CENTER VALUE         */

             CHGVAR     VAR(%SST(&QDATA 1 13)) VALUE('FSCJD *NE " "')

/*  S13682-ALWAYS SELECT BY COMPANY CODE.                        */

             CHGVAR     VAR(%SST(&QDATA 15 17)) VALUE('*AND FSCOCD +
                          *EQ "')
             CHGVAR     VAR(%SST(&QDATA 32 3)) VALUE(&LDCOCD)
             CHGVAR     VAR(%SST(&QDATA 35 1)) VALUE('"')

/* OVERRIDE FARM FILE                                            */

             OVRDBF     FILE(HSL018I) SHARE(*YES)
             OPNQRYF    FILE((HSL018I)) QRYSLT(&QDATA) KEYFLD(*FILE) +
                          SEQONLY(*YES)


/*********************************************************************/
/*  OPEN QUERY FOR FEED DETAIL RECORDS                               */
/*********************************************************************/

/*  ALWAYS SELECT ALL FEED TICKET DETAIL RECORDS FOR THE DATE RANGE OF   */
/*  THE CURRENTLY OPEN PERIOD                                            */

             CHGVAR     VAR(%SST(&QDATAT 1 10)) VALUE('FDTKDT *GE')
             CHGVAR     VAR(%SST(&QDATAT 12 8)) VALUE(&LDBPDT)
             CHGVAR     VAR(%SST(&QDATAT 21 15)) VALUE('*AND FDTKDT +
                          *LE')
             CHGVAR     VAR(%SST(&QDATAT 37 8)) VALUE(&LDEPDT)

/*  ALWAYS OMIT 'ADJUSTMENT' TICKETS                                     */

             CHGVAR     VAR(%SST(&QDATAT 46 19)) VALUE('*AND FDTRCD +
                          *NE "A"')

/* OVERRIDE FEED DETAIL FILE                                     */

             OVRDBF     FILE(HSL038B) SHARE(*YES)
             OPNQRYF    FILE((HSL038B)) QRYSLT(&QDATAT) +
                          KEYFLD(*FILE) SEQONLY(*YES)


/*********************************************************************/
/*  CALL PROGRAM TO BUILD UPLOAD FILE OF FEED POUNDS                 */
/*********************************************************************/

             CALL       PGM(HP204FL)

/*******************************************************************/
/*  DELETE FILE OVERRIDES                                          */
/*******************************************************************/

             CLOF       OPNID(HSL018I)
             DLTOVR     FILE(HSL018I)

             CLOF       OPNID(HSL038B)
             DLTOVR     FILE(HSL038B)

             ENDPGM

