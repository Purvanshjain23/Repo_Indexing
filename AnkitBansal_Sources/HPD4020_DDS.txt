     A*%%TS  SD  20040830  085335  ISLEANNER   REL-V5R2M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD4020
     A*  TITLE:         WORK WITH BGF DAILY PRODUCTION
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  DATE:          12/13/01
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A N99                                  CF04(04 'PROMPT')
     A N99                                  CF05(05 'REFRESH')
     A N99                                  CF06(06 'CREATE')
     A N99                                  CF07(07 'LISTING')
     A N99                                  CF17(17 'PRINT OPTIONS')
     A N99                                  CF20(20 'WRKSPLF')
     A*
     A*
     A*---------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP4020S1                  SFL
     A*%%TS  SD  20011220  153523  ISLRAMS     REL-V4R4M0  5769-PW1
     A  85                                  SFLNXTCHG
     A            HDRRN1         4Y 0H
     A            HDDPDT    R        H      REFFLD(BDDPDT)
     A            D1OPT          2A  B 12  2CHGINPDFT(UL)
     A  30                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A N30                                  DSPATR(HI)
     A  99                                  DSPATR(PR)
     A            D1CDYR    R        O 12  7REFFLD(HSRREF/@@CDYR *LIBL/HSPREF)
     A                                      EDTCDE(M)
     A            D1CDWK    R        O 12 14REFFLD(HSRREF/@@CDWK *LIBL/HSPREF)
     A                                      EDTCDE(M)
     A            D1DPMDY   R        O 12 20REFFLD(HSRREF/@@DT06 *LIBL/HSPREF)
     A                                      EDTWRD('  /  /  ')
     A            D1FSCD    R        O 12 31REFFLD(FSFSCD)
     A                                      EDTCDE(M)
     A            D1WEMDY   R        O 12 39REFFLD(HSRREF/@@DT06 *LIBL/HSPREF)
     A                                      EDTWRD('  /  /  ')
     A            D1CELL    R        O 12 50REFFLD(FSREC/FSCELL *LIBL/HSP018)
     A                                      EDTCDE(M)
     A            D1FSNM    R        O 12 55REFFLD(FSFSNM)
     A*
     A*---------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP4020C1                  SFLCTL(HP4020S1)
     A*%%TS  SD  20040830  085335  ISLEANNER   REL-V5R2M0  5722-WDS
     A                                      SFLSIZ(0050)
     A                                      SFLPAG(0009)
     A N82N99                               ROLLUP(25 'PAGE FORWARD')
     A                                      SETOF(31)
     A                                      SETOF(51)
     A                                      SETOF(52)
     A                                      RTNCSRLOC(&RCD &FLD)
     A                                      OVERLAY
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 26'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  2 21'WORK WITH BGF DAILY PRODUCTION'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  4  2'Business office ..'
     A N99                              4 21'?'
     A            C1FSBO    R        B  4 23REFFLD(FSFSBO)
     A  31
     AO 51                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A N31N99                               DSPATR(HI)
     A  99                                  DSPATR(PR)
     A            C1BODS    R        O  4 30REFFLD(BOBODS)
     A N99                              6  2'2=Revise'
     A N99                              6 12'4=Delete'
     A                                  6 22'6=Print'
     A                                  7 33'?'
     A                                      DSPATR(HI)
     A                                  8 21'Daily'
     A                                      DSPATR(HI)
     A                                  8 41'Week'
     A                                      DSPATR(HI)
     A                                  9  7'-Calendar-'
     A                                      DSPATR(HI)
     A                                  9 19'Production'
     A                                      DSPATR(HI)
     A                                  9 32'Farm'
     A                                      DSPATR(HI)
     A                                  9 40'Ending'
     A                                      DSPATR(HI)
     A                                 10  2'Opt'
     A                                      DSPATR(HI)
     A                                 10  7'Year'
     A                                      DSPATR(HI)
     A                                 10 13'Week'
     A                                      DSPATR(HI)
     A                                 10 22'Date'
     A                                      DSPATR(HI)
     A                                 10 32'Site'
     A                                      DSPATR(HI)
     A                                 10 41'Date'
     A                                      DSPATR(HI)
     A                                 10 49'Cell'
     A                                      DSPATR(HI)
     A                                 10 55'Farm Description'
     A                                      DSPATR(HI)
     A N99        C1CDYR    R        B 11  7REFFLD(HSRREF/@@CDYR *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A                                      EDTCDE(M)
     A N31N51                               DSPATR(PC)
     A N99        C1CDWK    R        B 11 14REFFLD(HSRREF/@@CDWK *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A                                      EDTCDE(M)
     A            C1DPMDY   R        B 11 20REFFLD(HSRREF/@@DT06 *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A                                      EDTWRD('  /  /  ')
     A            C1FSCD    R        B 11 31REFFLD(FSFSCD)
     A                                      EDTCDE(M)
     A                                      DSPATR(HI)
     A  52                                  DSPATR(PC)
     A N99        C1WEMDY   R        B 11 39REFFLD(@@DT06)
     A                                      DSPATR(HI)
     A                                      EDTWRD('  /  /  -')
     A N99        C1CELL    R        B 11 50REFFLD(FSCELL)
     A                                      EDTCDE(M)
     A                                      DSPATR(HI)
     A*
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R HP4020K1
     A*%%TS  SD  20020110  153220  ISLRAMS     REL-V4R4M0  5769-PW1
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 22  2'F3=Exit'
     A N99                             22 11'F4=Prompt'
     A N99                             22 22'F5=Refresh'
     A N99                             22 34'F6=Create'
     A N99                             22 45'F7=Listing'
     A N99                             22 57'F17=Print Options'
     A N99                             23 57'F20=Spool Files'
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *---------------------------------------------------------------
     A          R HP4020ES                  SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE CONTROL FORMAT
      *---------------------------------------------------------------
     A          R HP4020EC                  SFLCTL(HP4020ES)
      *
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
