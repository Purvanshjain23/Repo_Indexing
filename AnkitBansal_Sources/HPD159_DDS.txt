     A*%%TS  SD  20030109  151218  ISLRAMS     REL-V5R1M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION
     A*  DISPLAY FILE:  HPD159
     A*  TITLE:         SET UP CAPACITY AND REVISE MANAGERS FOR A FARM
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  DATE:          02/22/02
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A                                      CF04(04 'PROMPT')
     A                                      CF05(05 'REFRESH')
     A*
     A*--------------------------------------------------------------*
     A* RECORD FORMAT ONE
     A*--------------------------------------------------------------*
     A          R HP159R1
     A*%%TS  SD  20030109  151218  ISLRAMS     REL-V5R1M0  5722-WDS
     A                                      SETOF(31)
     A                                      SETOF(32)
     A                                      SETOF(33)
     A                                      SETOF(35)
     A                                      SETOF(37)
     A                                      SETOF(38)
     A                                      SETOF(39)
     A                                      SETOF(46)
     A                                      SETOF(47)
     A                                      SETOF(48)
     A                                      SETOF(49)
     A                                      SETOF(51)
     A                                      SETOF(52)
     A                                      SETOF(53)
     A                                      SETOF(59)
     A                                      OVERLAY
     A                                      RTNCSRLOC(&RCD &FLD)
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 29'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A            D1MODE         6A  O  2  2DSPATR(HI)
     A                                  2 16'SET UP CAPACITY AND REVISE MANAGER-
     A                                      S FOR A FARM'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  4  2'Farm site:'
     A            D1FSCD    R        O  4 14REFFLD(HSRREF/@@FSCD *LIBL/HSPREF)
     A                                      EDTCDE(Z)
     A            D1FSNM    R        O  4 21REFFLD(HSRREF/FSFSNM *LIBL/HSPREF)
     A                                  5  2'Total square feet of all active ro-
     A                                      oms on the farm:'
     A            D1FSSQFT  R        O  5 54REFFLD(HSRREF/@@FSSQFT *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A                                  7  2'Active/inactive status ...........-
     A                                      .'
     A                                  7 38'?'
     A            D1AIST    R        B  7 40REFFLD(HSRREF/@@AIST *LIBL/HSPREF)
     A N39                                  DSPATR(HI)
     A  39
     AO 59                                  DSPATR(PC)
     A  39                                  DSPATR(RI)
     A                                  9  2'Target square feet per pig .......-
     A                                      .'
     A            D1HDSQFT  R        B  9 40REFFLD(HSRREF/@@HDSQFT *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A N35                                  DSPATR(HI)
     A  35                                  DSPATR(PC)
     A  35                                  DSPATR(RI)
     A                                      DSPATR(UL)
     A                                 10  2'Cell manager .....................-
     A                                      .'
     A                                 10 38'?'
     A            D1CMGCD   R        B 10 40REFFLD(HSRREF/@@MGCD *LIBL/HSPREF)
     A N31                                  DSPATR(HI)
     A  51
     AO 31                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A                                      DSPATR(UL)
     A            D1CMGNM   R        O 10 48REFFLD(HSRREF/MGMGNM *LIBL/HSPREF)
     A                                 11  2'Production manager ...............-
     A                                      .'
     A                                 11 38'?'
     A            D1PMGCD   R        B 11 40REFFLD(HSRREF/@@MGCD *LIBL/HSPREF)
     A N32                                  DSPATR(HI)
     A  52
     AO 32                                  DSPATR(PC)
     A  32                                  DSPATR(RI)
     A                                      DSPATR(UL)
     A            D1PMGNM   R        O 11 48REFFLD(HSRREF/MGMGNM *LIBL/HSPREF)
     A                                 12  2'Multisite manager ................-
     A                                      .'
     A                                 12 38'?'
     A            D1MMGCD   R        B 12 40REFFLD(HSRREF/@@MGCD *LIBL/HSPREF)
     A N33                                  DSPATR(HI)
     A  33
     AO 53                                  DSPATR(PC)
     A  33                                  DSPATR(RI)
     A                                      DSPATR(UL)
     A            D1MMGNM   R        O 12 48REFFLD(HSRREF/MGMGNM *LIBL/HSPREF)
     A                                 14  2'Minutes to plant .................-
     A                                      .'
     A            D1PLMN    R        B 14 40REFFLD(HSRREF/@@PLMN *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A N46                                  DSPATR(HI)
     A  46                                  DSPATR(PC)
     A  46                                  DSPATR(RI)
     A                                      DSPATR(UL)
     A                                 15  2'Miles to plant ...................-
     A                                      .'
     A            D1PLMI    R        B 15 40REFFLD(HSRREF/@@PLMI *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A N47                                  DSPATR(HI)
     A  47                                  DSPATR(PC)
     A  47                                  DSPATR(RI)
     A                                      DSPATR(UL)
     A                                 16  2'Minutes to transportation office .-
     A                                      .'
     A            D1TOMN    R        B 16 40REFFLD(HSRREF/@@TOMN *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A N48                                  DSPATR(HI)
     A  48                                  DSPATR(PC)
     A  48                                  DSPATR(RI)
     A                                      DSPATR(UL)
     A                                 17  2'Miles to transportation office ...-
     A                                      .'
     A            D1TOMI    R        B 17 40REFFLD(HSRREF/@@TOMI *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A N49                                  DSPATR(HI)
     A  49                                  DSPATR(PC)
     A  49                                  DSPATR(RI)
     A                                      DSPATR(UL)
     A                                 19  2'E-mail address ......'
     A            D1EMAIL   R        B 19 24REFFLD(HSRREF/@@EMAIL *LIBL/HSPREF)
     A                                      CHGINPDFT(UL LC FE)
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 20  2'FAX:'
     A                                 20  7'Area code ......'
     A            D1FXACNO  R        B 20 24REFFLD(HSRREF/@@FXACNO *LIBL/HSPREF)
     A                                      EDTCDE(M)
     A N37                                  DSPATR(HI)
     A  37                                  DSPATR(PC)
     A  37                                  DSPATR(RI)
     A                                      DSPATR(UL)
     A                                 21  7'Phone number ...'
     A            D1FXPHNO  R        B 21 24REFFLD(HSRREF/@@FXPHNO *LIBL/HSPREF)
     A                                      EDTWRD('   -    -')
     A N38                                  DSPATR(HI)
     A  38                                  DSPATR(PC)
     A  38                                  DSPATR(RI)
     A                                      DSPATR(UL)
     A*--------------------------------------------------------------*
     A* COMMAND KEY RECORD 1
     A*--------------------------------------------------------------*
     A          R HP159K1
     A*%%TS  SD  20010709  133004  ISLRAMS     REL-V4R4M0  5769-PW1
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A                                 23 11'F4=Prompt'
     A                                 23 22'F5=Refresh'
      *
      *--------------------------------------------------------------*
      * ERROR MESSAGE SUBFILE FORMAT
      *--------------------------------------------------------------*
     A          R HP159ES                   SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
      *--------------------------------------------------------------*
      * ERROR MESSAGE SUBFILE CONTROL FORMAT
      *--------------------------------------------------------------*
     A          R HP159EC                   SFLCTL(HP159ES)
      *
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
