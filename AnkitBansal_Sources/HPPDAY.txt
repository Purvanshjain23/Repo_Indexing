      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      HOG PRODUCTION
      * PROGRAM:     HPPDAY
      * TITLE:       GENERIC PIG DAYS CALCULATION
      * PROGRAMMER:  DEBBIE HINES
      * CREATED:     5/16/94
      *
      * NOTE:        THIS PROGRAM DOES NOT SET ON LR--IT USES RETURN.
      *              SO, IF YOUR PROGRAM CALLS THIS PROGRAM OR A
      *              PROGRAM THAT CALLS THIS PROGRAM, THEN YOU MUST
      *              CALL THE CLP TO RECLAIM RESOURCES AS YOU EXIT THE
      *              CALLING PROGRAM.
      *
      * FUNCTION: GENERIC PROGRAM TO BE CALLED FROM MULTIPLE PROGRAMS
      *           TO CALCULATE THE PIG DAYS IN A DATE RANGE.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      * 99/99/99  NAME
      *
      * 09/19/94  MARY PALMER - ADDED ADDITIONAL DAY OF INVENTORY HEAD
      *                         COUNT FOR LAST DAY. ADDED TRANSFER W/IN
      *                         PHASE & NEXT PHASE PARMS TO HPMOVE.
      *
      * 02/07/95  LEANNE FEDOR
      *           REMOVED 'SETON LR' IN AN ATTEMPT TO SPEED PROCESSING.
      *           'RETURN' WILL BE USED INSTEAD.
      *
      * 04/18/95  LEANNE FEDOR
      *           RECOMPILE ONLY.  FARM SITE CHANGED TO NUMERIC FIELD.
      *
      * 07/05/95  LEANNE FEDOR
      *           REWRITTEN FOR MOVEMENT REWRITE.
      *
      * 08/07/96  LEANNE FEDOR
      *           INCREASED THE SIZE OF THE 'POUNDS' PARMS FOR HPMOVE
      *           FROM 5,0 TO 9,2.
      *
      * 03/18/97  LEANNE FEDOR
      *           RECOMPILE ONLY. UPDATE TIME STAMP ADDED TO MOVEMENT
      *           EVENT FILE.
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     FHSL058D   IF   E           K DISK
      *    MOVEMENT EVENT            (KEY: MEHGSN MEMEDT)
      *
      *
      ****************************************************************
      * INPUT SPECS
      ****************************************************************
      *
     D                 DS
     D  DUMMYD                 1      1
      *  THE PRECEDING DUMMY DATA STRUCTURE IS ONLY TO KEEP THE I-SPEC
      *  INFO FROM RUNNING OVER INTO THE 'CALC SPECS' ON THE COMPILE
      *  LISTING.  IT SERVES NO OTHER PURPOSE AND CAN BE DELETED
      *  AT ANY TIME!!!
     C/EJECT
      ****************************************************************
      * MAIN LINE
      ****************************************************************
      *
      * SINCE THIS PROGRAM USES 'RETURN' INSTEAD OF SETTING ON LR,
      * YOU MUST FORCE THE PROGRAM TO GET THE INCOMING PARMS AND CLEAR
      * THE OUTGOING PARM FIELDS EACH TIME THIS PROGRAM IS CALLED.
      *
     C                   EXSR      $CLEAR
      *
      *---------------------------------------------------------------
      * PROCESS ALL MOVEMENTS FOR A HOG GROUP WITHIN THE DATE RANGE.
      *---------------------------------------------------------------
      *
     C     K1L058        SETLL     HSL058D
     C     *IN92         DOUEQ     *ON                                          MAIN DO
     C     XXHGSN        READE     HSL058D                                92
      *
      * AT END OF FILE, IF THE USER'S "TO DATE" IS AFTER THE
      * EVENT DATE OF THE LAST MOVEMENT PROCESSED, THEN
      * RESET THE 'TO DATE' AND PERFORM MOVEMENT PROCESSING.
      *
     C     *IN92         IFEQ      *ON                                          IF EOF
     C     XXTYMD        IFGT      WKFYMD
     C                   Z-ADD     XXTYMD        PTO8
     C                   EXSR      $PROC
     C                   ENDIF
     C                   ELSE
      *
      * IT IS NOT EOF, SO PROCESS THE MOVEMENT EVENT IF IT IS
      * STILL IN THE DATE RANGE SELECTED AND RESET THE NEXT
      * 'FROM' DATE
      *
     C     MEMEDT        IFLE      XXTYMD                                       IF IN RANGE
     C                   Z-ADD     MEMEDT        PTO8
     C                   EXSR      $PROC
     C                   Z-ADD     MEMEDT        WKFYMD
     C                   ELSE
      *
      * YOU ARE OUTSIDE THE DATE RANGE, SO PERFORM PROCESSING
      * FOR THE PRIOR MOVEMENT AND FORCE AN EXIT FROM THE LOOP.
      *
     C                   Z-ADD     XXTYMD        PTO8
     C                   EXSR      $PROC
     C                   SETON                                        92
     C                   ENDIF                                                  IF IN RANGE
     C                   ENDIF                                                  IF EOF
     C                   ENDDO                                                  MAIN DO
      *
      *---------------------------------------------------------------
      * EOF PROCESSING
      *---------------------------------------------------------------
      *
      * ADD GROUP INVENTORY HEAD TO PIG DAYS COUNT FOR LAST DAY
      *
     C                   ADD       GPIHD         TPDAY
      *
      * MOVE THE ACCUMULATED PIG DAYS FOR THE GROUP INTO THE
      * OUTPUT PARM TO BE RETURNED TO THE CALLING PROGRAM
      *
     C                   Z-ADD     TPDAY         XXPDAY
      *
      * RETURN TO CALLER
      *
     C                   RETURN
      /EJECT
      *----------------------------------------------------------------
      * $PROC  - PROCESS THE MOVEMENT
      *----------------------------------------------------------------
      *
      * THIS SUBROUTINE:
      *  1) CALCULATES THE HOG INVENTORY STARTING AT THE 'FROM' DATE
      *  2) GETS THE NUMBER OF DAYS BETWEEN DATES
      *  3) CALCULATES NUMBER OF PIG DAYS
      *
     C     $PROC         BEGSR
      *
     C                   Z-ADD     XXHGSN        ZZHGSN
     C                   Z-ADD     WKFYMD        XXCYMD
     C                   Z-ADD     0             GPIHD
      *
     C                   CALL      'HPMOVE'
     C                   PARM                    ZZHGSN                         GROUP
     C                   PARM                    XXCYMD                         CUT-OFF DATE
      * OUTPUT PARMS
     C                   PARM                    GPIHD                          GROUP INV
     C                   PARM                    PINHD                          PIG IN
     C                   PARM                    TINHD                          TRSF IN
     C                   PARM                    TISPHD                         SAME PHASE
     C                   PARM                    TIDPHD                         DIFF PHASE
     C                   PARM                    POUHD                          PIGS OUT
     C                   PARM                    TOUHD                          TRSF OUT
     C                   PARM                    TOSPHD                         SAME PHASE
     C                   PARM                    TODPHD                         DIFF PHASE
     C                   PARM                    RINHD                          REJECT IN
     C                   PARM                    ROUHD                          REJECT OUT
     C                   PARM                    QINHD                          QUALITY IN
     C                   PARM                    QOUHD                          QUALITY OUT
     C                   PARM                    MORHD                          MORTALITY
     C                   PARM                    INAHD                          INV ADJ
      * POUNDS
     C                   PARM                    PINLB                          PIG IN
     C                   PARM                    TINLB                          TRSF IN
     C                   PARM                    TISPLB                         SAME PHASE
     C                   PARM                    TIDPLB                         DIFF PHASE
     C                   PARM                    POULB                          PIG OUT
     C                   PARM                    TOULB                          TRSF OUT
     C                   PARM                    TOSPLB                         SAME PHASE
     C                   PARM                    TODPLB                         DIFF PHASE
     C                   PARM                    RINLB                          REJECT IN
     C                   PARM                    ROULB                          REJECT OUT
     C                   PARM                    QINLB                          QUALITY IN
     C                   PARM                    QOULB                          QUALITY OUT
     C                   PARM                    MORLB                          MORTALITY
      *
      *
      *
      * IF THERE ARE ANY PIGS IN INVENTORY
      *   1) FIND THE NUMBER OF DAYS IN THE RANGE
      *   2) CALCULATE PIG DAYS FOR THE RANGE
      *   3) ACCUMULATE PIG DAYS FOR THE GROUP
      *
     C     GPIHD         IFNE      0                                            IF PIGGIES
     C                   Z-ADD     WKFYMD        PFRM8
     C                   MOVEL     'CYMD'        PFRFMT
     C                   MOVEL     'CYMD'        PTOFMT
     C                   MOVE      'W'           PCODE
      *
     C                   CALL      'UT80060R'
     C                   PARM                    PFRM8             8 0
     C                   PARM                    PTO8              8 0
     C                   PARM                    PFRFMT            4
     C                   PARM                    PTOFMT            4
     C                   PARM                    PDAYS             4 0
     C                   PARM                    PCODE             1
     C                   PARM                    PRTRN             1
      *
      * IF NO ERROR CODE IS RETURNED FROM DATE UTILITY PROGRAM,
      * CALCULATE THE NUMBER OF PIG DAYS FOR THIS MOVEMENT
      * AND ACCUMULATE THE TOTAL PIG DAYS FOR THIS GROUP
      *
     C     PRTRN         IFEQ      *BLANK
     C     PDAYS         MULT      GPIHD         WKPDAY
     C                   ADD       WKPDAY        TPDAY
     C                   ENDIF
     C                   ENDIF                                                  IF PIGGIES
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * $CLEAR - CLEAR FIELD, ETC.
      *---------------------------------------------------------------
      *
     C     $CLEAR        BEGSR
      *
     C     *ENTRY        PLIST
     C                   PARM                    XXHGSN
     C     WKFYMD        PARM                    XXFYMD
     C                   PARM                    XXTYMD
     C                   PARM                    XXPDAY
      *
     C                   Z-ADD     0             XXPDAY
     C                   Z-ADD     0             TPDAY
      *
     C                   MOVE      *OFF          *IN
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * *INZSR - INITIALIZATION SUBROUTINE
      *---------------------------------------------------------------
      *
     C     *INZSR        BEGSR
      *
      * KEY FOR MOVEMENT POINTER
      *
     C     K1L058        KLIST
     C                   KFLD                    XXHGSN
     C                   KFLD                    XXFYMD
      *
      * DEFINE INPUT PARAMETERS FOR THE PIG DAYS CALCULATIONS PGM
      *
     C     *LIKE         DEFINE    MEHGSN        XXHGSN
     C     *LIKE         DEFINE    MEMEDT        XXFYMD
     C     *LIKE         DEFINE    MEMEDT        XXTYMD
      *
      * DEFINE OUTPUT PARAMETERS FOR THE PIG DAYS CALCULATIONS PGM
      *
     C                   Z-ADD     0             XXPDAY           11 0
     C                   Z-ADD     0             TPDAY            11 0
      *
      * DEFINE WORKFIELDS
      *
     C     *LIKE         DEFINE    XXFYMD        WKFYMD
     C                   Z-ADD     0             WKPDAY           11 0
      *
      * DEFINE PARAMETERS FOR THE MOVEMENT PROGRAM
      *
     C     *LIKE         DEFINE    MEHGSN        ZZHGSN
     C     *LIKE         DEFINE    MEMEDT        XXCYMD
      * HEAD
     C                   Z-ADD     0             @@NU50            5 0
     C     *LIKE         DEFINE    @@NU50        GPIHD
     C     *LIKE         DEFINE    @@NU50        PINHD
     C     *LIKE         DEFINE    @@NU50        TINHD
     C     *LIKE         DEFINE    @@NU50        TISPHD
     C     *LIKE         DEFINE    @@NU50        TIDPHD
     C     *LIKE         DEFINE    @@NU50        POUHD
     C     *LIKE         DEFINE    @@NU50        TOUHD
     C     *LIKE         DEFINE    @@NU50        TOSPHD
     C     *LIKE         DEFINE    @@NU50        TODPHD
     C     *LIKE         DEFINE    @@NU50        RINHD
     C     *LIKE         DEFINE    @@NU50        ROUHD
     C     *LIKE         DEFINE    @@NU50        QINHD
     C     *LIKE         DEFINE    @@NU50        QOUHD
     C     *LIKE         DEFINE    @@NU50        MORHD
     C     *LIKE         DEFINE    @@NU50        INAHD
      * POUNDS
     C                   Z-ADD     0             @@NU92            9 2
     C     *LIKE         DEFINE    @@NU92        PINLB
     C     *LIKE         DEFINE    @@NU92        TINLB
     C     *LIKE         DEFINE    @@NU92        TISPLB
     C     *LIKE         DEFINE    @@NU92        TIDPLB
     C     *LIKE         DEFINE    @@NU92        POULB
     C     *LIKE         DEFINE    @@NU92        TOULB
     C     *LIKE         DEFINE    @@NU92        TOSPLB
     C     *LIKE         DEFINE    @@NU92        TODPLB
     C     *LIKE         DEFINE    @@NU92        RINLB
     C     *LIKE         DEFINE    @@NU92        ROULB
     C     *LIKE         DEFINE    @@NU92        QINLB
     C     *LIKE         DEFINE    @@NU92        QOULB
     C     *LIKE         DEFINE    @@NU92        MORLB
      *
     C                   ENDSR
      /EJECT
