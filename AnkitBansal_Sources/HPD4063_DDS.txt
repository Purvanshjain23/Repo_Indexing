     A*%%TS  SD  20090709  162453  ISLRAMS     REL-V5R4M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD4063
     A*  TITLE:         UT: MOVE EDITED TRANSFERS INTO HPS
     A*  PROGRAMMER:    LEANNE RAMSEY
     A*  DATE:          07/09/09
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A                                      CF08(08 'SUBMIT')
     A                                      CF20(20 'WRKSPLF')
     A*
     A*----------------------------------------------------------------
     A* RECORD FORMAT ONE
     A*----------------------------------------------------------------
     A          R HP4063R1
     A*%%TS  SD  20090709  162453  ISLRAMS     REL-V5R4M0  5722-WDS
     A                                      OVERLAY
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 27'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  2 23'MOVE EDITED TRANSFERS INTO HPS'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  5  2'This is the final step in the inte-
     A                                      rface of Transfer Data into HPS.'
     A                                  6  2'This function actually moves/creat-
     A                                      es/puts the Transfer Data into the'
     A                                  7  2'HPS Production files.'
     A                                  9  2'At the end of this function, the E-
     A                                      diting File will be cleared so that'
     A                                 10  2'it is ready for the next upload.'
     A                                 10 35'Any'
     A                                 10 39'subsequent'
     A                                 10 50'changes'
     A                                 10 58'to'
     A                                 10 61'the'
     A                                 10 65'Transfers'
     A                                 11  2'must be made through the normal HP-
     A                                      S functions.'
     A                                 14  2'Business Office.....'
     A                                      COLOR(BLU)
     A            SDBOBO         5A    14 23
     A                                      DSPATR(HI)
     A            SDBODS        30A    14 30
     A                                      COLOR(BLU)
     A                                 17  2'Take F8-Submit to submit the final-
     A                                       move of the data into HPS.'
     A                                      DSPATR(HI)
     A*--------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*--------------------------------------------------------------
     A          R HP4063K1
     A*%%TS  SD  20041011  083140  ISLRAMS     REL-V5R2M0  5722-WDS
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A                                 23 11'F8=Submit'
     A                                 23 22'F20=Spool Files'
      *
      *--------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *--------------------------------------------------------------
     A          R HP4063ES                  SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
     A*
     A*----------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*----------------------------------------------------------------
     A*
     A          R HP4063EC                  SFLCTL(HP4063ES)
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
