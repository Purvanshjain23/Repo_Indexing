      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      HOG PRODUCTION
      * PROGRAM:     HP3718
      * TITLE:       Populate Missing Farm Budget Numbers
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     03/22/04
      *
      * FUNCTION:  This program reads the records selected by the open query in HP3718CL
      *            and attempts to populate the Farm Budget on Request Detail records.
      *            If the Item is not on any active Farm Budget, the logic will set the
      *            Farm Budget to 9999999.
      *
      *            This function was needed to populate the records that MP2 creates in
      *            the HPS file. ALL the records that originate in MP2 will have missing
      *            Farm Budget Numbers.
      *
      *            This function should be run nightly from the AS400 Job Scheduler--after
      *            John L. has run his function. Or, John L. should submit it from his
      *            function as a last step.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 10/13/04  LeAnne Fedor
      *           Recompile only. Cost Per Unit Amount was changed from
      *           9,2 to 9,4.
      /EJECT
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
     Fhsj189j   if   e           k disk
      *    Farm budget detail + farm budget header
      *
      *
     Fhsp192    if   e           k disk
      *    Request header
      *
      *
     Fhsj193i   if   e           k disk
      *    Request detail + Item master (Records selected by open query)
      *
      *
     Fhsl193d   uf   e           k disk    rename(qdrec:qdrecd) prefix(p1)
      *    Request detail
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Workfields
      *
     D wkfbsn          s                   like(qdfbsn)
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * Mainline
      ****************************************************************
      *
      * The open query has selected records from the join logical where:
      *  1) farm budget number is zero
      *  2) requested quantity is zero (ie: the record did not originate in HPS)
      *  3) used quantity is not zero  (ie: MP2 has touched the record)
      *  4) line status is not delete or error
      *  5) the item has a budget flag of yes
      *
     C                   dou       *in90 = *on                                  Main do
     C                   read      hsj193i                                90
     C                   if        *in90 = *off                                 If not EOF
      *
      * Set a default value for Farm Budget
      *
     C                   z-add     9999999       wkfbsn
      *
      * Retrieve the Farm Site of this request from the Request Header.
      * (You need it for keying purposes.)
      *
     C     qdrqsn        chain     hsp192                             92
     C                   if        *in92 = *off                                 If header hit
      *
      * Retrieve any active budget for this farm/budget item to get a
      * Farm Budget Number.
      *
     C     key01         chain     hsj189j                            92
     C                   if        *in92 = *off                                 If active
     C                   z-add     jhfbsn        wkfbsn
     C                   endif                                                  If active
      *
     C                   endif                                                  If header hit
      *
      * Now, retrieve/update the request detail record with this Farm
      * Budget Number.
      *
     C     key02         chain     hsl193d                            92
     C                   if        *in92 = *off                                 If found
     C                   z-add     wkfbsn        p1qdfbsn
     C                   update    qdrecd
     C                   endif                                                  If found
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Main do
      *
     C                   seton                                        lr
      /EJECT
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Key lists
      *
     C     key01         klist
     C                   kfld                    qhfscd
     C                   kfld                    imbgit
      *
     C     key02         klist
     C                   kfld                    qdrqsn
     C                   kfld                    qdlnno
      *
     C                   endsr
      /EJECT
