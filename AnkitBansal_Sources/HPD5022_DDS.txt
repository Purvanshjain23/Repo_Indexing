     A*%%TS  SD  20021114  102056  ISLRAMS     REL-V5R1M0  5722-WDS
     A*****************************************************************
     A*
     A*  SYSTEM:        HOG PRODUCTION
     A*  DISPLAY FILE:  HPD5022- SELECT TATTOO/KILL DATE
     A*  CREATED BY:    LEANNE FEDOR
     A*  DATE:          11/07/02
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA12(12 'PREVIOUS')
     A N82                                  ROLLUP(25 'PAGE FORWARD')
     A*---------------------------------------------------------------
     A* REFERENCE WINDOW RECORD FORMAT 1
     A*---------------------------------------------------------------
     A          R HP5022R1
     A*%%TS  SD  19950619  151645  ISLRAMS     REL-V3R0M5  5763-PW1
     A                                      TEXT('Reference window')
     A                                      WINDOW(*DFT 17 74)
     A                                      WDWBORDER((*DSPATR RI) (*CHAR '    -
     A                                          '))
     A*---------------------------------------------------------------
     A* RECORD FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP5022R2
     A                                      TEXT('Written in RPG pgm to prevent-
     A                                       screen from clearing')
     A                                      CLRL(*NO)
     A                                      OVERLAY
     A*---------------------------------------------------------------
     A* RECORD FORMAT TWO
     A*---------------------------------------------------------------
     A* THIS FORMAT IS NOT WRITTEN IN THE RPG PGM.  IT PREVENTS THE
     A* CALLING_PROGRAM_SCREEN FROM BEING CLEARED.
     A*---------------------------------------------------------------
     A          R HP5022R3
     A                                      TEXT('Dummy format not written in R-
     A                                      PG pgm.')
     A                                      ASSUME
     A                                 11  1' '
     A*----------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*----------------------------------------------------------------
     A          R HP5022S1                  SFL
     A*%%TS  SD  20021107  172538  ISLRAMS     REL-V5R1M0  5722-WDS
     A                                      TEXT('subfile record format one')
     A            HDRRN1         4S 0H
     A            DFKMDY    R        O  7  1REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  ')
     A            DFTANO    R        O  7 12REFFLD(HSRREF/CDTANO *LIBL/HSPREF)
     A                                      EDTCDE(M)
     A            DFMVSN    R        O  7 22REFFLD(CDMVSN)
     A                                      EDTCDE(M)
     A            DFCVNO    R        O  7 33REFFLD(CDCVNO)
     A                                      EDTCDE(M)
     A            DFCNAM    R        O  7 44REFFLD(@@DS30)
     A*--------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*--------------------------------------------------------------
     A          R HP5022C1                  SFLCTL(HP5022S1)
     A*%%TS  SD  20021114  102056  ISLRAMS     REL-V5R1M0  5722-WDS
     A                                      SFLSIZ(0050)
     A                                      SFLPAG(0008)
     A                                      WINDOW(HP5022R1)
     A                                      TEXT('control format one')
     A                                      OVERLAY
     A                                      SFLCSRRRN(&HDRRN)
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND(*MORE)
     A                                      RMVWDW
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            HDRRN          5S 0H
     A            SDPGM     R        O  1  1REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 23'SELECT TATTOO/KILL DATE'
     A                                      DSPATR(HI)
     A                                  2  1'Position Cursor, Press Enter'
     A                                  4  3'Kill'
     A                                      DSPATR(HI)
     A                                  4 22'Sales'
     A                                      DSPATR(HI)
     A                                  4 33'Customer'
     A                                      DSPATR(HI)
     A                                  5  3'Date'
     A                                      DSPATR(HI)
     A                                  5 11'Tattoo'
     A                                      DSPATR(HI)
     A                                  5 22'Movement'
     A                                      DSPATR(HI)
     A                                  5 33'Number'
     A                                      DSPATR(HI)
     A                                  5 44'Customer Name'
     A                                      DSPATR(HI)
     A            C1KMDY    R        B  6  1REFFLD(@@DT06)
     A                                      DSPATR(HI)
     A                                      EDTWRD('  /  /  -')
     A            C1TANO    R        B  6 12REFFLD(HSRREF/CDTANO *LIBL/HSPREF)
     A                                      EDTCDE(M)
     A                                      DSPATR(HI)
     A            C1CVNO    R        B  6 33REFFLD(SHCVNO)
     A                                      DSPATR(HI)
     A                                      EDTCDE(M)
     A*----------------------------------------------------------------
     A          R HP5022K1
     A*----------------------------------------------------------------
     A                                      OVERLAY
     A                                      WINDOW(HP5022R1)
     A                                 16  1'F12=Previous'
