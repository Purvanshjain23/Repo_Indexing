      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      HOG PRODUCTION
      * PROGRAM:     HP621
      * TITLE:       LISTING OF PREMIUM/DISCOUNT CODES
      * PROGRAMMER:  ROSE CENTONZE
      * CREATED:     5/03/94
      *
      * FUNCTION: BATCH PROGRAM TO LIST THE RECORDS IN THE
      *           PREMIUM/DISCOUNT CODE FILE
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      * 06/18/97  LEANNE FEDOR
      *           ADDED USER ID TO HEADINGS.
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     FHSP045    IF   E           K DISK
      *    PREMIUM/DISCOUNT CODE MASTER FILE  (KEY: PDPDCD)
      *
     FHSP006    IF   E           K DISK
      *    REPORT CODE MASTER (KEY: RPRPCD)
      *
     FQPRINT    O    F  132        PRINTER OFLIND(*INOF)
      *
      /EJECT
      ****************************************************************
      * INPUT SPECS
      ****************************************************************
      *
      *---------------------------------------------------------------
      * STANDARD PROGRAM STATUS DATA STRUCTURE
      *---------------------------------------------------------------
      *    EXTERNALLY DEFINED AS UTPGFR (RECORD FORMAT: PGMDSR)
     D PGMDS         ESDS                  EXTNAME(UTPGFR)
      *
      *---------------------------------------------------------------
      *  NAMED CONSTANTS
      *---------------------------------------------------------------
      *
     D YES             C                   CONST('Y')
     D NO              C                   CONST('N')
      *
      /EJECT
      ****************************************************************
      * MAIN LINE
      ****************************************************************
      *
      * PREPARE TO READ THE DEDUCTION/BONUS MASTER FILE AND
      * PRINT HEADINGS FOR FIRST TIME THROUGH
      *
     C     *LOVAL        SETLL     HSP045
     C                   EXCEPT    HDRPT
      *
      * READ AND PRINT ALL RECORDS IN THE MASTER.
      *
     C     *IN92         DOUEQ     *ON                                          MAIN DO
     C                   READ      HSP045                                 92
      *
     C     *IN92         IFEQ      *OFF                                         IF NOT EOF
      *
     C     *INOF         IFEQ      *ON
     C                   EXCEPT    HDRPT
     C                   ENDIF
      *
      * CHAIN TO REPORT CODE MASTER FILE FOR LONG DESCRIPTION
      *
     C     PDRPCD        CHAIN     HSP006                             95
      *
      * PRINT LINE
      *
     C                   EXCEPT    DTTYPE
     C                   MOVEL     YES           PRTFLG
      *
     C                   ENDIF                                                   IF NOT EOF
      *
      *  IF THE MASTER FILE WAS EMPTY AND NO RECORDS WERE PRINTED,
      *  PRINT THE STANDARD 'NO DATA...' ERROR MESSAGE
      *
     C     PRTFLG        IFEQ      NO
     C                   EXCEPT    NODATA
     C                   ENDIF
      *
     C                   ENDDO                                                  MAIN DO
      *
     C                   SETON                                        LR
      /EJECT
      *---------------------------------------------------------------
      * *INZSR - INITIALIZATION SUBROUTINE
      *---------------------------------------------------------------
      *
     C     *INZSR        BEGSR
      *
      * SET PRINT FLAG TO DETERMINE IF ANY RECORDS WERE PRINTED
      *
     C                   MOVEL     NO            PRTFLG            1
      *
     C                   MOVEL     *ALL'-'       DASH             80
      *
      * RETRIEVE TIME FOR REPORT HEADING
      *
     C                   TIME                    RTIME             6 0
      *
     C                   ENDSR
      *
      /EJECT
      *-------------------------------------------------------------
      * REPORT LAYOUT
      *-------------------------------------------------------------
      *
      *-------------------------------------------------------------
      * REPORT HEADING LINES
      *-------------------------------------------------------------
      *
     OQPRINT    E            HDRPT          1 01
     O                       SDPGM               10
     O                                           48 'HOG PRODUCTION SYSTEM'
     O                                           66 'DATE'
     O                       UDATE         Y     76
      *
     O          E            HDRPT          1
     O                       SDUSR               10
     O                                           41 ' LISTING OF PREMIUM/'
     O                                           55 'DISCOUNT CODES'
     O                                           66 'TIME'
     O                       RTIME               76 '  :  :  '
      *
     O          E            HDRPT          2
     O                                           66 'PAGE'
     O                       PAGE          Z     71
      *
     O          E            HDRPT          1
     O                                           22 '--PREMIUM/DISCOUNT---'
     O                                           44 'PREM/DISC'
     O                                           66 '------REPORT------'
      *
     O          E            HDRPT          1
     O                                            5 'TYPE'
     O                                           22 'DESCRIPTION'
     O                                           43 'FLAG'
     O                                           66 'CODE     LONG NAME'
      *
     O          E            HDRPT          1
     O                       DASH                80
      *
      *-------------------------------------------------------------
      *  DETAIL LINE
      *-------------------------------------------------------------
      *
     O          E            DTTYPE      1
     O                       PDPDCD               6
     O                       PDPDDS              31
     O                       PDPDFL              41
     O                       PDRPCD              53
     O              N95      RPRLDS              77
     O               95                          64 'UNKNOWN'
      *
      *-------------------------------------------------------------
      * NO DATA MESSAGE LINE
      *-------------------------------------------------------------
      *
     O          E            NODATA      1
     O                                           19 'NO DATA MEETS YOUR'
     O                                           38 'SELECTION CRITERIA'
