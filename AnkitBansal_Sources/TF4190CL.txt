/***************************************************************** */
/*                                                                 */
/*  SYSTEM:          TRIUMPH FOODS                                 */
/*  PROGRAM NUMBER:  TF4190CL                                      */
/*  PROGRAM NAME:    SPECIFY PRODUCT EXCEPTION LISTING OPTIONS     */
/*  PROGRAMMER:      LEANNE RAMSEY                                 */
/*  CREATE DATE:     09/12/06                                      */
/*                                                                 */
/*******************************************************************/
/*                                                                 */
/* 04/16/07  LeAnne Ramsey                                         */
/*           We switched the following flag fields:                */
/*             1) Exclude from Mix Flag    is now Mix Flag         */
/*             2) Exclude from Volume Flag is now Volume Flag      */
/*           Before this change Y=Yes meant EXCLUDE the product    */
/*           After this change  Y=Yes means INCLUDE the product    */
/*                                                                 */
/* 06/12/08  LeAnne Ramsey                                         */
/*           The unique key to TFP019 has changed. So, I switched  */
/*           from the PF to a LF that has the appropriate keys.    */
/*           And, added a lot more selectors.                      */
/*                                                                 */
/* 07/14/08  LeAnne Ramsey                                         */
/*           We are now generating this automatically from the     */
/*           Margin Adjustment Close as well as on-demand. So,     */
/*           the LDA positions had to be changed so that they      */
/*           were not in conflict with the positions used for the  */
/*           Margin Adjustment Close.                              */
/*                                                                 */
/* 11/21/08  LeAnne Ramsey                                         */
/*           As part of synchronizing the LDAs between the         */
/*           TFS Margin Adjustment Close and the Meat Costing,     */
/*           the LDA positions were changed.                       */
/*                                                                 */
/* 02/02/09  LeAnne Ramsey                                         */
/*           Added "program" as a new report selection.            */
/*                                                                     */
/* 05/07/09  LEANNE RAMSEY                                             */
/*           CHANGED PRINT LOGIC TO MATCH MEAT COSTING.                */
/*******************************************************************/

             PGM

             DCL        VAR(&QDATA) TYPE(*CHAR) LEN(230)

             DCL        VAR(&OUTQ) TYPE(*CHAR) LEN(10)
             DCL        VAR(&HOLD) TYPE(*CHAR) LEN(4)
             DCL        VAR(&SAVE) TYPE(*CHAR) LEN(4)
             DCL        VAR(&COPY) TYPE(*DEC)  LEN(1)

             DCL        VAR(&LDFWEDT) TYPE(*DEC) LEN(8)
             DCL        VAR(&LDTWEDT) TYPE(*DEC) LEN(8)

             DCL        VAR(&LDPRCD) TYPE(*DEC) LEN(7)
             DCL        VAR(&LDPGM)   TYPE(*CHAR) LEN(10)
             DCL        VAR(&LDMIXFL) TYPE(*CHAR) LEN(1)
             DCL        VAR(&LDVOLFL) TYPE(*CHAR) LEN(1)
             DCL        VAR(&LDSYSFL) TYPE(*CHAR) LEN(1)

             DCL        VAR(&LDCGCD)    TYPE(*CHAR) LEN(4)
             DCL        VAR(&LDTFCGCD)  TYPE(*CHAR) LEN(2)
             DCL        VAR(&LDTFCLCD)  TYPE(*CHAR) LEN(3)

             CHGVAR     VAR(&LDFWEDT) VALUE(%SST(*LDA 85 8))
             CHGVAR     VAR(&LDTWEDT) VALUE(%SST(*LDA 99 8))

             CHGVAR     VAR(&LDMIXFL) VALUE(%SST(*LDA 136 1))
             CHGVAR     VAR(&LDVOLFL) VALUE(%SST(*LDA 137 1))
             CHGVAR     VAR(&LDSYSFL) VALUE(%SST(*LDA 138 1))

             CHGVAR     VAR(&LDTFCGCD) VALUE(%SST(*LDA 139 2))
             CHGVAR     VAR(&LDTFCLCD) VALUE(%SST(*LDA 156 3))
             CHGVAR     VAR(&LDPRCD)   VALUE(%SST(*LDA 222 7))
             CHGVAR     VAR(&LDCGCD)   VALUE(%SST(*LDA 295 4))
             CHGVAR     VAR(&LDPGM)    VALUE(%SST(*LDA 386 10))

/*----------------------------------------------------------------*/
/* SET PARAMETERS FROM LDA FOR LISTINGS                           */
/*----------------------------------------------------------------*/

 OUTQ:       CHGVAR     VAR(&OUTQ) VALUE(%SST(*LDA 401 10))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(*JOB))

 HOLD:       CHGVAR     VAR(&HOLD) VALUE(%SST(*LDA 411 4))
             IF         COND(&HOLD *NE '*YES' *AND &HOLD *NE '*NO ') +
                          THEN(CHGVAR VAR(&HOLD) VALUE(*NO))

 COPY:       CHGVAR     VAR(&COPY) VALUE(%SST(*LDA 419 1))
             IF         COND(&COPY = 0) THEN(CHGVAR VAR(&COPY) +
                          VALUE(1))

 SAVE:       CHGVAR     VAR(&SAVE) VALUE(%SST(*LDA 415 4))
             IF         COND(&SAVE *NE '*YES' *AND &SAVE *NE '*NO ') +
                          THEN(CHGVAR VAR(&SAVE) VALUE(*NO))

/*---------------------------------------------------------------------*/
/* EXTRACT THE DATA FROM THE PRODUCT EXCEPTIONS FILE                   */
/*---------------------------------------------------------------------*/

             CHGVAR     VAR(&QDATA) VALUE(' ')

/* FROM DATE                                                            */
             CHGVAR     VAR(%SST(&QDATA 1 10)) VALUE('PEWEDT *GE')
             CHGVAR     VAR(%SST(&QDATA 12 8)) VALUE(&LDFWEDT)

/* TO DATE                                                              */
             CHGVAR     VAR(%SST(&QDATA 21 15)) VALUE('*AND PEWEDT *LE')
             CHGVAR     VAR(%SST(&QDATA 37 8)) VALUE(&LDTWEDT)

/* IF PRODUCT CODE                                                      */

             IF         COND(&LDPRCD *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 46 15)) VALUE('*AND PEPRCD *EQ')
             CHGVAR     VAR(%SST(&QDATA 62 7)) VALUE(&LDPRCD)
             ENDDO

/* IF MIX FLAG                                                          */

             IF         COND(&LDMIXFL *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 70 18)) VALUE('*AND +
                          PEMIXFL *EQ "')
             CHGVAR     VAR(%SST(&QDATA 88 1)) VALUE(&LDMIXFL)
             CHGVAR     VAR(%SST(&QDATA 89 1)) VALUE('"')
             ENDDO

/* VOLUME FLAG                                                         */

             IF         COND(&LDVOLFL *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 92 18)) VALUE('*AND +
                          PEVOLFL *EQ "')
             CHGVAR     VAR(%SST(&QDATA 110 1)) VALUE(&LDVOLFL)
             CHGVAR     VAR(%SST(&QDATA 111 1)) VALUE('"')
             ENDDO

/* SYSTEM-GENERATED FLAG                                               */

             IF         COND(&LDSYSFL *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 113 18)) VALUE('*AND +
                          PESYSFL *EQ "')
             CHGVAR     VAR(%SST(&QDATA 131 1)) VALUE(&LDSYSFL)
             CHGVAR     VAR(%SST(&QDATA 132 1)) VALUE('"')
             ENDDO

/* MEAT COST GROUP                                                     */

             IF         COND(&LDCGCD *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 134 17)) VALUE('*AND +
                          PECGCD *EQ "')
             CHGVAR     VAR(%SST(&QDATA 151 4)) VALUE(&LDCGCD)
             CHGVAR     VAR(%SST(&QDATA 155 1)) VALUE('"')
             ENDDO

/* TF CLASS GROUP                                                      */

             IF         COND(&LDTFCGCD *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 157 19)) VALUE('*AND +
                          PETFCGCD *EQ "')
             CHGVAR     VAR(%SST(&QDATA 176 2)) VALUE(&LDTFCGCD)
             CHGVAR     VAR(%SST(&QDATA 178 1)) VALUE('"')
             ENDDO

/* TF CLASSIFICATION                                                   */

             IF         COND(&LDTFCLCD *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 180 19)) VALUE('*AND +
                          PETFCLCD *EQ "')
             CHGVAR     VAR(%SST(&QDATA 199 3)) VALUE(&LDTFCLCD)
             CHGVAR     VAR(%SST(&QDATA 202 1)) VALUE('"')
             ENDDO

/* PROGRAM                                                              */

             IF         COND(&LDPGM *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 204 16)) VALUE('*AND PEPGM +
                          *EQ "')
             CHGVAR     VAR(%SST(&QDATA 220 10)) VALUE(&LDPGM)
             CHGVAR     VAR(%SST(&QDATA 230 1)) VALUE('"')
             ENDDO

/* OVERRIDE PRINT FILE                                                */

             OVRPRTF    FILE(PRINT198) OUTQ(&OUTQ) COPIES(&COPY) +
                          HOLD(&HOLD) SAVE(&SAVE)

/* OVERRIDE PRODUCT EXCEPTION FILE                                      */

             OVRDBF     FILE(TFL019C) SHARE(*YES)
             OPNQRYF    FILE((TFL019C)) OPTION(*INP) QRYSLT(&QDATA) +
                          KEYFLD(*FILE) SEQONLY(*YES)
             CALL       PGM(TF619)
             CLOF       OPNID(TFL019C)
             DLTOVR     FILE(TFL019C)

/*---------------------------------------------------------------------*/
/* END OF PROCESSING                                                   */
/*---------------------------------------------------------------------*/

             DLTOVR     FILE(*ALL)

             ENDPGM

