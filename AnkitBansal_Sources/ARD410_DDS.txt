     A*%%TS  SD  19971028  161318  ISLRAMS     REL-V3R6M0  5716-PW1
     A*****************************************************************
     A*  SYSTEM:        ACCOUNT RECEIVABLE SYSTEM
     A*  DISPLAY FILE:  ARD410
     A*  TITLE:         FOREIGN CORPORATE TAX SAVINGS
     A*                 SPECIFY YEAR FOR ANALYSIS
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  DATE:          10/23/97
     A*
     A*****************************************************************
     A* MODIFICATIONS:
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      HELP
     A                                      HLPDOC('AR410' 'AR410' 'COR.IS/DOCA-
     A                                      RSR')
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A                                      CF04(04 'PROMPT')
     A  44                                  CF08(08 'SUBMIT')
     A                                      CF20(20 'WRKSPLF')
     A*
     A*---------------------------------------------------------------
     A* RECORD FORMAT ONE
     A*---------------------------------------------------------------
     A          R AR410R1
     A*%%TS  SD  19971028  155437  ISLRAMS     REL-V3R6M0  5716-PW1
     A                                      SETOF(31)
     A                                      SETOF(32)
     A                                      SETOF(37)
     A                                      SETOF(39)
     A                                      SETOF(40)
     A                                      SETOF(57)
     A                                      SETOF(59)
     A                                      OVERLAY
     A                                      RTNCSRLOC(&RCD &FLD)
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 27'ACCOUNTS RECEIVABLE SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  2  8'SPECIFY TAX SAVINGS ANALYSIS FOR F-
     A                                      OREIGN SALES CORPORATIONS'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  4  2'Year ......................'
     A            DFCCYY    R     Y  B  4 32REFFLD(@@NU40)
     A                                      EDTCDE(Z)
     A N31                                  DSPATR(HI)
     A  31                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A                                  6  2'Report or files (R or F) ..'
     A            DFRFFL         1   B  6 32
     A N32                                  DSPATR(HI)
     A  32                                  DSPATR(PC)
     A  32                                  DSPATR(RI)
     A                                  8  2'Number of copies ..........'
     A            DFCOPY    R        B  8 32REFFLD(FNCOPY)
     A                                      RANGE(0 9)
     A                                      EDTCDE(Z)
     A N40                                  DSPATR(HI)
     A  40                                  DSPATR(RI)
     A  40                                  DSPATR(PC)
     A                                  9  2'Hold printed output .......'
     A                                  9 30'?'
     A            DFHOLD    R        B  9 32REFFLD(FNHOLD)
     A N39                                  DSPATR(HI)
     A  39                                  DSPATR(RI)
     A  39
     AO 59                                  DSPATR(PC)
     A                                 10  2'Send output to queue ......'
     A            DFOUTQ    R        B 10 32REFFLD(OQOUTQ)
     A                                      DSPATR(HI)
     A                                 11  2'Run in night jobq .........'
     A                                 11 30'?'
     A            DFJOBQ    R        B 11 32REFFLD(@@FLAG)
     A N37                                  DSPATR(HI)
     A  37                                  DSPATR(RI)
     A  37
     AO 57                                  DSPATR(PC)
     A*
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R AR410K1
     A*%%TS  SD  19971028  161318  ISLRAMS     REL-V3R6M0  5716-PW1
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A                                 23 11'F4=Prompt'
     A  44                             23 22'F8=Submit'
     A                                 23 33'F20=Spool Files'
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *---------------------------------------------------------------
     A          R AR410ES                   SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE CONTROL FORMAT
      *---------------------------------------------------------------
     A          R AR410EC                   SFLCTL(AR410ES)
      *
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
