     Z* CRTBNDRPG
     Z* DFTACTGRP(*NO) BNDDIR(YBNDDIR) DBGVIEW(*SOURCE)
     Z* CVTOPT(*DATETIME) ACTGRP(*CALLER) OPTIMIZE(*BASIC)
      *****************  RPG PROGRAM HEADING  ************************
      * SYSTEM:      Hog Production / JD Edwards
      * PROGRAM:     HPJDE204
      * TITLE:       Upload HPS Values to JDE
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     10/03/01
      *
      *  FUNCTION:   This program uploads period data from HPS to JDE.
      *              This function must be run prior to the period close in HPS.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 02/05/13  LeAnne Ramsey (E2405)
      *           MTech wants to be able to upload all of the data to JDE
      *           in a single submission. So, in preparation for that, I
      *           am changing this program to use LF HSL204B...which is
      *           keyed to accomodate the all-at-once scenario.
      *
      * 02/27/16  Barb Gutierrez  (E005290)
      *           Removed hard coding of company 350 because of Iowa Farms that
      *           is company 340 and in subsequent companies we may add.
      *           Company number is now in HSP204.
      * 03/02/16  Barb Gutierrez  (E005290)
      *           Added population of service tax date.
      * 08/17/20  Rose Centonze (P16169)
      *           Update glcff1 with "I" so it wont sync to E1
      /EJECT
      ****************************************************************
      * File specifications
      ****************************************************************
      *
     Fhsl204b   if   e           k disk
      *    HPS to JDE upload
      *
      *
     Ff0011     uf a e           k disk
      *   JDE Batch control record
      *
      *
     Ff0901     if   e             disk
      *   JDE Account master
      *
      *
     Ff0911     uf a e             disk
      *   JDE Account ledger
      *
      /EJECT
      ****************************************************************
      * Definition specifications
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Named Constants
      *---------------------------------------------------------------
      *
     D yes             c                   'Y'
     D no              c                   'N'
     D userid          c                   'HPSUPL    '
      *
      *
      *---------------------------------------------------------------------
      * Standalone fields
      *---------------------------------------------------------------------
      *
      * Control fields
      *
     d first           s              1    inz('Y')
      *
      *
      * Workfields
      *
     d wkco            s              5
     d wkbatcnt        s              5  0
     d wkdoccnt        s              5  0
     d wkaid           s              8
     d wkani           s             29
     d wkbatval        s             15  0
      *
     d wktime          s             12  0
     d wkdate          s              6  0
      *
      *
      *
      * Parm fields
      *
      * For call to JDE date routine
      *
     d xxsidat         s              6
     d xxedat          s              8
     d xxffmt          s              7
     d xxtfmt          s              7
     d xxsep           s              7
     d xxertst         s              1
     d xxctry          s              2
      *
      * For call to JDE account routine
      *
     d xxbatch         s                   like(glicu)
     d xxsy            s              4
     d xxidx           s              2
     d xxsym           s              1
     d xxomod          s              1
     d xximod          s              1
     d xxerrm          s              4
      *
     d xxani           s                   like(glani)
     d xxicu           s                   like(glicu)
     d xxdoc           s                   like(gldoc)
      *
      ****************************************************************
      * Data structures
      ****************************************************************
      *
      *
     d i0901ds         ds           500
     d xxco                                like(gmco)
     d xxaid                               like(gmaid)
     d xxmcu                               like(gmmcu)
     d xxobj                               like(gmobj)
     d xxsub                               like(gmsub)
     d xxans                               like(gmans)
     d xxdl01                              like(gmdl01)
     d xxlda                               like(gmlda)
     d xxbpc                               like(gmbpc)
     d xxpec                               like(gmpec)
     d xxbill                              like(gmbill)
     d xxcrcd                              like(gmcrcd)
     d xxum                                like(gmum)
     d xxr001                              like(gmr001)
     d xxr002                              like(gmr002)
     d xxr003                              like(gmr003)
     d xxr004                              like(gmr004)
     d xxr005                              like(gmr005)
     d xxr006                              like(gmr006)
     d xxr007                              like(gmr007)
     d xxr008                              like(gmr008)
     d xxr009                              like(gmr009)
     d xxr010                              like(gmr010)
     d xxr011                              like(gmr011)
     d xxr012                              like(gmr012)
     d xxr013                              like(gmr013)
     d xxr014                              like(gmr014)
     d xxr015                              like(gmr015)
     d xxr016                              like(gmr016)
     d xxr017                              like(gmr017)
     d xxr018                              like(gmr018)
     d xxr019                              like(gmr019)
     d xxr020                              like(gmr020)
     d xxr021                              like(gmr021)
     d xxr022                              like(gmr022)
     d xxr023                              like(gmr023)
     d xxobja                              like(gmobja)
     d xxsuba                              like(gmsuba)
     d xxwcmp                              like(gmwcmp)
     d xxcct                               like(gmcct)
     d xxerc                               like(gmerc)
     d xxhtc                               like(gmhtc)
     d xxqlda                              like(gmqlda)
     d xxccc                               like(gmccc)
     d xxfmod                              like(gmfmod)
     d xxuser                              like(gmuser)
     d xxpid                               like(gmpid)
     d xxjobn                              like(gmjobn)
     d xxupmj                              like(gmupmj)
     d xxupmt                              like(gmupmt)
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *---------------------------------------------------------------
      * Break CCYY into CC and YY fields
      *---------------------------------------------------------------
      *
     D wkyear          ds
     D   wkccyy                       4  0
     D   wkcc                         2  0 overlay(wkccyy:1)
     D   wkyy                         2  0 overlay(wkccyy:3)
      *
      *----------------------------------------------------------------
      * LDA - local data area
      *----------------------------------------------------------------
      *
     Dlda             uds                  dtaara(*lda)
     D  ldacyr                 1      4  0                                      Year - ccyy
     D  ldacpe                 5      6  0                                      Period number
     D  ldbpdt                 7     14  0                                      Begin period date
     D  ldepdt                15     22  0                                      End period date
     D  ldhucd                23     23
      *
     D  ldhuds               100    129
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      *----------------------------------------------------------------
      * MAINLINE
      *---------------------------------------------------------------
      *
      * Read each record in the HPS upload file. The 'offsetting' records have
      * already been written in this upload file. So, just write ledger records
      * for each upload record.
      *
     C     *loval        setll     hsl204b
      *
     C                   dou       *in90 = *on                                  Main do
     C                   read      hsl204b                                90
     C                   if        *in90 = *off                                 If not EOF
      *
      * Control break logic
      *
     C                   select
     C                   when      first = yes
     C                   move      no            first
     C                   exsr      $docnbr
     C                   exsr      $batnbr
     C                   other
      *
     C                   if        hjnewbat = yes                               If new batch
     C                   exsr      $batwrt
     C                   exsr      $batnbr
     C                   z-add     0             wkbatcnt
     C                   z-add     0             wkbatval
     C                   endif                                                  If new batch
      *
     C                   if        hjnewdoc = yes                               If new doc
     C                   exsr      $docnbr
     C                   z-add     0             wkdoccnt
     C                   endif                                                  If new doc
      *
     C                   endsl
      *
      *
      * Detail processing: Write each record to the JDE ledger file.
      *
     C                   exsr      $detail
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Main do
      *
      *-----------------------------
      * EOF processing
      *-----------------------------
      *
      * Write out last batch record
      *
     C                   if        wkbatcnt <> 0
     C                   exsr      $batwrt
     C                   endif
      *
     C                   seton                                        lr
      /eject
      *---------------------------------------------------------------
      * Retrieve a document number from JDE
      *---------------------------------------------------------------
      *
     C     $docnbr       begsr
      *
     C                   call      'X0010 '
     C                   parm      '09  '        xxsy
     C                   parm      '02'          xxidx
     C     gldoc         parm                    xxdoc
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Retrieve a batch number from JDE
      *---------------------------------------------------------------
      *
     C     $batnbr       begsr
      *
     C                   call      'X0010 '
     C                   parm      '00  '        xxsy
     C                   parm      '01'          xxidx
     C     glicu         parm                    xxbatch
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Detail logic for each HPS upload record
      *---------------------------------------------------------------
      *
     C     $detail       begsr
      *
      * Move fields from HPS upload file to JDE Account ledger record
      *
     C                   move      hjexa         glexa
     C                   move      hjdesc        glexr
     C                   move      hjmcu         glmcu
     C                   move      hjobj         globj
     C                   move      hjre          glre
     C                   move      *zeros        wkco
     C                   move      hjcocd        wkco
     C                   move      wkco          glco
     C                   move      wkco          glkco
      *
     C                   select
     C                   when      hjtype = 'AU'
     C                   z-add     hjjdeval      glu
     C                   z-add     0             glaa
     C                   when      hjtype = 'AA'
     C                   z-add     hjjdeval      glaa
     C                   z-add     0             glu
     C                   endsl
      *
      * Retrieve JDE account ID
      *
     C                   call      'X0901'
     C                   parm                    xxsym
     C                   parm      '1'           xxomod
     C                   parm      '9'           xximod
     C     wkaid         parm      *blank        xxani
     C                   parm      hjmcu         xxmcu
     C                   parm      hjobj         xxobj
     C                   parm      hjsub         xxsub
     C                   parm      *blank        xxerrm
     c                   parm                    i0901ds
      *
     C                   if        xxerrm = *blank
     C                   move      xxsub         glsub
     C                   move      wkaid         glaid
     C                   else
     C                   move      *blank        glsub
     C                   move      *blank        glaid
     C                   endif
      *
      * Retrieve JDE unstructured account number
      *
     C                   call      'X0901'
     C                   parm                    xxsym
     C                   parm      '2'           xxomod
     C                   parm      '1'           xximod
     C     wkani         parm      wkaid         xxani
     C                   parm                    xxmcu
     C                   parm                    xxobj
     C                   parm                    xxsub
     C                   parm      *blank        xxerrm
     c                   parm                    i0901ds
      *
     C                   if        xxerrm = *blank
     C                   move      wkani         glani
     C                   else
     C                   move      *blank        glani
     C                   endif
      *
      * Increment the counters of a) the batch and b) the document group
      *
     C                   add       1             wkbatcnt
     C                   add       1             wkdoccnt
     C                   z-add     wkdoccnt      gljeln
      *
      * Accumulate the value for the batch header 'amount entered' field.
      *
     C                   add       hjbatval      wkbatval
      *
     C                   write     i0911
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Write a batch record
      *---------------------------------------------------------------
      *
     C     $batwrt       begsr
      *
      * Write a batch header record to batch control file
      *
     C     key01         chain     f0011                              92
     C                   if        *in92 = *on                                  If no hit
     C                   z-add     wkbatcnt      icndo
     C                   z-add     wkbatcnt      icdocn
     C                   z-add     0             icaicu
     C                   z-add     wkbatval      icame
      *
     C                   z-add     glicu         icicu
     C                   move      *blank        icist
     C                   movel     userid        icuser
     C                   move      glicut        icicut
     C                   move      gldicj        icdicj
     C                   move      'A'           iciapp
     C                   move      'Y'           icbal
     C                   move      ' '           icbalj
     C                   move      'N'           icpob
     C                   move      'Y'           iciboi
      *
     C                   write     i0011
     C                   endif                                                  If no hit
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Get dates into JDE format
      *---------------------------------------------------------------
      *
     C     $date         begsr
      *
     C                   call      'X0028'
     C                   parm                    xxsidat
     C                   parm      *blank        xxedat
     C                   parm                    xxffmt
     C                   parm      '*JUL   '     xxtfmt
     C                   parm      '*NONE'       xxsep
     C                   parm      *blank        xxertst
     C                   parm      *blank        xxctry
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Key lists
      *
     C     key01         klist
     C                   kfld                    glicut
     C                   kfld                    xxbatch
      *
      * Set up defaults for records in F0911-Account Ledger File
      *
      * Get system date into the format required by JDE by calling JDE's routine.
      *
     C                   time                    wktime
     C                   move      wktime        wkdate
     C                   move      wkdate        xxsidat
     C                   move      '*SYSVAL'     xxffmt
      *
     C                   exsr      $date
     C                   move      xxsidat       glupmj
     C                   move      xxsidat       gldsyj
     C                   move      xxsidat       gldicj
      *
      *
      * Get period end date in JDE format by calling JDE date routine.
      *
     C                   move      ldepdt        xxsidat
     C                   move      '*YMD   '     xxffmt
     C                   exsr      $date
     C                   move      xxsidat       gldgj
     C                   move      xxsidat       gldsvj
      *
      *
      * Set up some defaults
      *
     C**                 move      *zero         wkco
     C**                 move      '350'         wkco
     C**                 move      wkco          glco
     C**                 move      wkco          glkco
      *
     C                   movel     sdjob         gljobn
     C                   time                    glticu
     C                   time                    glupmt
      *
     C                   z-add     ldacpe        glpn
      *
     C                   z-add     ldacyr        wkccyy
     C                   z-add     wkcc          glctry
     C                   z-add     wkyy          glfy
      *
      *
     C                   move      userid        gluser
     C                   move      sdusr         gltorg
      *
     C                   move      '2'           glam
     C                   move      'AA'          gllt
     C                   move      '00000'       glpkco
     C                   move      '00000'       glokco
     C                   move      '000'         glpsfx
     C                   move      'JE'          gldct
     C                   move      'G '          glicut
     C                   move      'R'           glrcnd
     C                   move      'I'           glcff1                         P16169
      *
      *
     C                   endsr
      /eject
