     A*%%TS  SD  20070919  145620  ISLRAMS     REL-V5R4M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION
     A*  DISPLAY FILE:  HPD4025
     A*  TITLE:         SPECIFY NUR/FIN TARGET LISTING OPTIONS
     A*  PROGRAMMER:    SHARON ZURN
     A*  DATE:          09/20/00
     A*
     A*  NOTE:  THIS IS A POP-UP WINDOW. THE WINDOW DEFINITION IS IN
     A*         RECORD FORMAT HP4025EC
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CF03(03 'EXIT')
     A                                      CF08(08 'SUBMIT')
     A                                      CF17(17 'PRINT OPTIONS')
     A                                      CF20(20 'SPOOL FILES')
     A*
     A*----------------------------------------------------------------
     A* RECORD FORMAT ONE
     A*----------------------------------------------------------------
     A          R HP4025R1
     A*%%TS  SD  20070919  145620  ISLRAMS     REL-V5R4M0  5722-WDS
     A                                      WINDOW(HP4025EC)
     A                                      TEXT('INPUT SCREEN')
     A                                      SETOF(31)
     A                                      SETOF(32)
     A                                      SETOF(33)
     A                                      OVERLAY
     A            SDPGM     R        O  1  1REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 14'SPECIFY NUR/FIN TARGET LISTING OPT-
     A                                      IONS'
     A                                      DSPATR(HI)
     A                                  3  1'Origin BGF cell ..'
     A            D1OBCELL  R        B  3 22REFFLD(HSRREF/@@OBCELL *LIBL/HSPREF)
     A                                      EDTCDE(M)
     A  32                                  DSPATR(PC)
     A  32                                  DSPATR(RI)
     A N32                                  DSPATR(HI)
     A                                  3 27'(Blank for all)'
     A                                  4  1'Origin BGF pod ...'
     A            D1OBPOD   R        B  4 21REFFLD(HSRREF/@@OBPOD *LIBL/HSPREF)
     A                                      EDTCDE(M)
     A  33                                  DSPATR(PC)
     A  33                                  DSPATR(RI)
     A N33                                  DSPATR(HI)
     A                                  4 27'(Blank for all)'
     A                                  5  1'Year .............'
     A            D1CDYR    R        B  5 20REFFLD(HSRREF/@@CDYR *LIBL/HSPREF)
     A                                      EDTCDE(M)
     A  31                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A N31                                  DSPATR(HI)
     A                                  5 27'(Blank for all)'
     A*
     A*----------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*----------------------------------------------------------------
     A          R HP4025K1
     A*%%TS  SD  20070919  145620  ISLRAMS     REL-V5R4M0  5722-WDS
     A                                      WINDOW(HP4025EC)
     A                                      TEXT('COMMAND KEYS')
     A                                      OVERLAY
     A  43                              7  2'Message program failed. Call IS.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                  8  1'F3=Exit'
     A                                  8 10'F8=Submit'
     A                                  8 21'F17=Print Options'
     A                                  8 40'F20=Spool Files'
      *
      *----------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *----------------------------------------------------------------
     A          R HP4025ES
     A                                      TEXT('Error message subfile')
     A                                      SFL SFLMSGRCD(09)
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
     A*
     A*----------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT WITH WINDOW DEFINITION
     A*----------------------------------------------------------------
     A          R HP4025EC                  SFLCTL(HP4025ES)
     A                                      TEXT('Error message subfile')
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A                                      WINDOW(*DFT 10 60)
     A                                      WDWBORDER((*DSPATR RI) (*CHAR '    -
     A                                          '))
     A            MSGPGM                    SFLPGMQ(10)
     A*
     A*----------------------------------------------------------------
     A* ASSUME FORMAT - REQUIRED FOR WINDOWS
     A*----------------------------------------------------------------
     A*
     A          R ASSUME
     A*%%TS  SD  20000921  090016  ISLRAMS     REL-V4R4M0  5769-PW1
     A                                      TEXT('ASSUME FORMAT FOR WINDOWS')
     A                                      ASSUME
     A                                      OVERLAY
     A                                 24 79' '
