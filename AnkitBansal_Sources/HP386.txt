      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      HOG PRODUCTION
      * PROGRAM:     HP386
      * TITLE:       HOG GROUP REVIEW: BUILD FEED TICKET WORKFILE
      * PROGRAMMER:  LEANNE FEDOR
      * CREATED:     09/30/97
      *
      *  FUNCTION:   THIS BATCH PROGRAM IS CALLED FROM HP440CL AND
      *              HP441CL.  IT BUILDS A FEED TICKET WORKFILE
      *              USED IN THE HOG GROUP REVIEW REPORT.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 03/09/98  LeAnne Fedor
      *           Recompile only. Feed pounds and feed amount fields
      *           were increased in size per Nathan Malone.
      *
      * 01/30/01  LeAnne Fedor
      *           Recompile only.  Production type was removed from feed files.
      *
      * 12/09/02  LeAnne Fedor
      *           Removed 'production phase' from the Feed Ticket Detail file.
      *           So, changed logic in this program to retrieve 'production phase' from
      *           the Hog Group file.
      *
      * 12/30/05  LeAnne Fedor
      *           Added Hog Group Code to workfile.
      *
      * 01/04/06  LeAnne Fedor
      *           Added more Hog Group fields to workfile in preparation for Feed Ticket
      *           download function.
      *
      * 02/22/06  LeAnne Fedor
      *           The users wanted dates in MMDDCCYY format. So, added more
      *           fields to the file.
      *
      * 02/28/06  LeAnne Fedor
      *           Removed the dates added on 2/22/06. We will now accomplish this
      *           in the Sequel view.
      *
      * 04/23/09  LeAnne Ramsey
      *           Recompile only. Costed Time/User added to Feed Ticket Header.
      *
      * 05/12/09  LeAnne Ramsey
      *           Recompile only. PickUp Ticket Type added to Feed Ticket Header.
      *
      * 07/06/09  LeAnne Ramsey
      *           Recompile only. Added new field 'Continuous Flow Flag' to Hog Group file.
      *
      * 09/21/09  LeAnne Ramsey
      *           Recompile only. Added new field 'Upload Date' to Feed Ticket Header.
      *
      * 03/26/20  Danny Nguyen - P405 - Farm Number Increase
      *           Recompile only. Increased Bin Code (@@BNCD) in HSPREF file from 5A to 6A.
      *
      * 05/10/22 Eric L SDN736 Recompiled ticket nbr increase (TKNO & RTNO 7.0 TO 9.0)
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     FHSL034D   IF   E           K DISK
      *  HOG GROUP
      *
      *
     FHSP037    IF   E           K DISK
      *    FEED TICKET HEADER
      *
      *
     FHSL038E   IF   E           K DISK
      *    FEED TICKET DETAIL
      *
      *
     FHSP386    O    E           K DISK
      *    WORKFILE FOR FEED TICKET
      *
      /EJECT
      ****************************************************************
      * Definition Specifications
      ****************************************************************
      *
      *---------------------------------------------------------------
      * STANDARD PROGRAM STATUS DATA STRUCTURE
      *---------------------------------------------------------------
      *    EXTERNALLY DEFINED AS UTPGFR (RECORD FORMAT: PGMDSR)
     D PGMDS         ESDS                  EXTNAME(UTPGFR)
      *
      *---------------------------------------------------------------
      *  NAMED CONSTANTS
      *---------------------------------------------------------------
      *
     D YES             C                   CONST('Y')
     D NO              C                   CONST('N')
      *
      *---------------------------------------------------------------
      * LDA - LOCAL DATA AREA
      *---------------------------------------------------------------
      *
     D                UDS
     D  LDFSBO                 1      5
     D  LDHGSN                 6     12  0
     D  LDCOST                20     20
     D  LDBODS                21     50
     D  LDRPFL                51     51
      *
      *---------------------------------------------------------------
      *  DATE FORMATS
      *---------------------------------------------------------------
      *
     D wkisodate       s               d   datfmt(*iso)
      *
      *
      * DATE IN MMDDYY FORMAT
      *
     D                 DS
     D  WKMDY                  1      6  0
     D  WKMM                   1      2  0
     D  WKDD                   3      4  0
     D  WKYY                   5      6  0
      *
      * DATE IN CCYYMMDD FORMAT
      *
     D                 DS
     D  WKCYMD                 1      8  0
     D  WKCEN                  1      2  0
     D  WKYER                  3      4  0
     D  WKMON                  5      6  0
     D  WKDAY                  7      8  0
      *
      * DATE FOR CALL TO SYNON SYSTEM IN FORMAT CYYMMDD
      *
     D                 DS
     D  WKSYMD                 1      7  0
     D  WKSCEN                 1      1  0
     D  WKSYER                 2      3  0
     D  WKSMON                 4      5  0
     D  WKSDAY                 6      7  0
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * MAIN LINE
      ****************************************************************
      *
      * PROCESS EACH FEED TICKET DETAIL RECORD FOR THE HOG
      * GROUP SPECIFIED IN THE LOCAL DATA AREA.
      *
     C     LDHGSN        SETLL     HSL038E                                90
      *
     C     *IN90         DOUEQ     *ON                                          MAIN DO
     C     LDHGSN        READE     HSL038E                                90
     C     *IN90         IFEQ      *OFF                                         IF NOT EOF
      *
      * MOVE ALL EXISTING VALUES FROM THE FEED TICKET
      * DETAIL FILE TO THE WORKFILE.
      *
     C                   MOVEL     FDRGCD        WTRGCD
     C                   MOVEL     FDFMBO        WTFMBO
     C                   Z-ADD     FDFSCD        WTFSCD
      *
     C                   Z-ADD     FDTKNO        WTTKNO
     C                   MOVEL     FDTRCD        WTTRCD
     C                   Z-ADD     FDLNNO        WTLNNO
     C                   MOVEL     FDALFL        WTALFL
     C                   Z-ADD     FDALDT        WTALDT
      *
     C                   Z-ADD     FDRTNO        WTRTNO
     C                   Z-ADD     FDRTDT        WTRTDT
      *
     C                   MOVEL     FDBLCD        WTBLCD
     C                   MOVEL     FDRMCD        WTRMCD
     C                   MOVEL     FDBNCD        WTBNCD
     C                   Z-ADD     FDHGSN        WTHGSN
      *
     C                   MOVEL     FDRNCD        WTRNCD
     C                   MOVEL     FDADCD        WTADCD
     C                   MOVEL     FDFTCD        WTFTCD
     C                   Z-ADD     FDFDLB        WTFDLB
      *
     C                   Z-ADD     FDINAM        WTINAM
     C                   Z-ADD     FDRCDT        WTRCDT
      *
     C                   Z-ADD     FDMFAM        WTMFAM
     C                   Z-ADD     FDMCDT        WTMCDT
      *
     C                   Z-ADD     FDDLAM        WTDLAM
     C                   Z-ADD     FDMDAM        WTMDAM
      *
      * Ticket Date in 2 formats: CCYYMMDD, MMDDYY
      *
     C                   z-add     fdtkdt        wttkdt
     C                   if        fdtkdt <> 0
     C     *iso          test(d)                 fdtkdt                 92
     C                   if        *in92 = *off                                 If OK date
     C                   move      fdtkdt        wkisodate
     C     *mdy          move      wkisodate     wtmdy
     C                   endif                                                  If OK date
     C                   endif
      *
      *
      * SUM INGREDIENT, MANUFACTURING AND MEDICATION DOLLARS
      * FOR THE FEED AMOUNT.
      *
     C     FDINAM        ADD       FDMFAM        WTFDAM
     C     WTFDAM        ADD       FDMDAM        WTFDAM
      *
      *
      * CALCULATE FEED COST PER TON
      *
     C     FDFDLB        DIV       2000          WKF152
     C     WKF152        IFNE      0
     C     WTFDAM        DIV(H)    WKF152        WTTNRT
     C                   ENDIF
      *
      *
      * Retrieve Hog Group data
      *
     C     WTHGSN        CHAIN     HSL034D                            92
     C     *IN92         IFEQ      *OFF
     C                   MOVEL     HGHGCD        WTHGCD
     C                   MOVEL     HGPPCD        WTPPCD
     C                   MOVEL     HGPTCD        WTPTCD
     C                   MOVEL     HGGSCD        WTGSCD
     C                   MOVEL     HGGTCD        WTGTCD
     C                   MOVEL     HGSXCD        WTSXCD
      *
      * Created Date in 2 formats: CCYYMMDD, MMDDYY
      *
     C                   Z-ADD     HGCRDT        WTCRDT
     C                   if        hgcrdt <> 0
     C     *iso          test(d)                 hgcrdt                 92
     C                   if        *in92 = *off                                 If OK date
     C                   move      hgcrdt        wkisodate
     C     *mdy          move      wkisodate     wtcrdtmdy
     C                   endif                                                  If OK date
     C                   endif
      *
      * Opened Date in 2 formats: CCYYMMDD, MMDDYY
      *
     C                   Z-ADD     HGOPDT        WTOPDT
     C                   if        hgopdt <> 0
     C     *iso          test(d)                 hgopdt                 92
     C                   if        *in92 = *off                                 If OK date
     C                   move      hgopdt        wkisodate
     C     *mdy          move      wkisodate     wtopdtmdy
     C                   endif                                                  If OK date
     C                   endif
      *
      * Disposed Date in 2 formats: CCYYMMDD, MMDDYY
      *
     C                   Z-ADD     HGDSDT        WTDSDT
     C                   if        hgdsdt <> 0
     C     *iso          test(d)                 hgdsdt                 92
     C                   if        *in92 = *off                                 If OK date
     C                   move      hgdsdt        wkisodate
     C     *mdy          move      wkisodate     wtdsdtmdy
     C                   endif                                                  If OK date
     C                   endif
      *
     C                   Z-ADD     HGADOI        WTADOI
     C                   Z-ADD     HGADOO        WTADOO
     C                   ENDIF
      *
      *
      * RETRIEVE FIELDS FROM FEED TICKET HEADER FILE
      *
     C     KEY01         CHAIN     HSP037                             92
     C     *IN92         IFEQ      *OFF                                         IF FOUND
     C                   MOVEL     FHPRFL        WTPRFL
     C                   MOVEL     FHCSFL        WTCSFL
     C                   MOVEL     FHTSCD        WTTSCD
     C                   ENDIF                                                  IF FOUND
      *
      *
      * RETRIEVE ACCOUNTING YEAR/PERIOD ASSOCIATED WITH
      * FEED TICKET DATE FROM CALENDAR FILE IN OMS SYSTEM.
      *
     C                   EXSR      $ACCT
      *
     C                   WRITE     WTREC
     C                   CLEAR                   WTREC
      *
     C                   ENDIF                                                  IF NOT EOF
     C                   ENDDO                                                  MAIN DO
      *
     C                   SETON                                        LR
      /EJECT
      *---------------------------------------------------------------
      * $TOMDY - FLIP DATE FROM YYMMDD TO MMDDYY
      *---------------------------------------------------------------
      *
     C     $TOMDY        BEGSR
      *
     C                   Z-ADD     WKMON         WKMM
     C                   Z-ADD     WKDAY         WKDD
     C                   Z-ADD     WKYER         WKYY
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * $ACCT - RETRIEVE ACCOUNTING YEAR/PERIOD
      *---------------------------------------------------------------
      *
     C     $ACCT         BEGSR
      *
      * RETRIEVE ACCOUNTING YEAR/PERIOD ASSOCIATED WITH
      * FEED TICKET DATE FROM CALENDAR FILE IN OMS SYSTEM.
      * PRIOR TO CALLING THE PROGRAM TO MAKE THIS RETRIEVAL,
      * CONVERT THE 'TICKET DATE' FROM THE HPS FORMAT OF
      * CCYYMMDD TO THE OMS SYSTEM FORMAT OF CYYMMDD.
      *
      * THE OMS SYNON SYSTEM CARRIES THE DATE AS A 7,0 NUMERIC
      * FIELD WITH THE FIRST DIGIT BEING 0=20TH CENTURY OR
      * 1=21ST CENTURY.
      * EXAMPLE: 0951225 FOR CHRISTMAS IN 1995 AND
      *          1951225 FOR CHRISTMAS IN 2095
      *
     C                   Z-ADD     FDTKDT        WKCYMD
     C     WKCEN         IFEQ      19
     C                   Z-ADD     0             WKSCEN
     C                   ELSE
     C                   Z-ADD     1             WKSCEN
     C                   ENDIF
      *
     C                   Z-ADD     WKYER         WKSYER
     C                   Z-ADD     WKMON         WKSMON
     C                   Z-ADD     WKDAY         WKSDAY
      *
      * CALL THE OMS PROGRAM
      *
     C                   CALL      'PDT3XFR'
     C                   PARM      *BLANK        XXRTN
     C                   PARM      350           XXCOCD
     C                   PARM      WKSYMD        XXDATE
     C                   PARM      0             WTACYR
     C                   PARM      0             WTACPR
     C                   PARM      0             XXWEEK
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * *INZSR - INITIALIZATION SUBROUTINE
      *---------------------------------------------------------------
      *
     C     *INZSR        BEGSR
      *
      * KEY FOR FEED TICKET DETAIL
      *
     C     KEY01         KLIST
     C                   KFLD                    FDFMBO
     C                   KFLD                    FDTKNO
     C                   KFLD                    FDTRCD
      *
      * DEFINE PARMS
      *
     C                   Z-ADD     0             XXCOCD            3 0
     C                   Z-ADD     0             XXDATE            7 0
     C                   Z-ADD     0             XXWEEK            3 0
     C                   MOVE      *BLANK        XXRTN             7
      *
      * DEFINE WORKFIELDS
      *
     C                   Z-ADD     0             WKF152           15 2
      *
     C                   ENDSR
      /EJECT
