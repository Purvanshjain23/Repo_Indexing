     A*%%TS  SD  20250701  012639  ISSGOYA     REL-V7R5M0  5770-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD1026
     A*  TITLE:         REVISE COMPANY
     A*  PROGRAMMER:    SHEFALI GOYAL
     A*  CREATED:       06/18/25
     A*
     A*
     A*  INDICATOR 60 IS ON WHEN IN REVISE MODE
     A*  INDICATOR 61 IS ON WHEN IN CREATE MODE
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A  60                                  CF05(05 'REFRESH')
     A                                      CA12(12 'PREVIOUS')
     A*
     A*---------------------------------------------------------------
     A* RECORD FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP1026R1
     A*%%TS  SD  20250701  012639  ISSGOYA     REL-V7R5M0  5770-WDS
     A                                      SETOF(34)
     A                                      SETOF(35)
     A                                      RTNCSRLOC(&RCD &FLD)
     A                                      OVERLAY
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 27'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USR)
     A                                      DSPATR(HI)
     A            D1MODE    R        O  2  2REFFLD(@@MODE)
     A                                      DSPATR(HI)
     A                                  2 30'REVISE COMPANY'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@JOB)
     A                                      DSPATR(HI)
     A                                  6  7'Company Code.......'
     A                                  8  7'Company Name.......'
     A            D1CONM    R        B  8 28REFFLD(COREC/COCONM *LIBL/HSP062)
     A N34                                  DSPATR(HI)
     A  34                                  DSPATR(RI)
     A  34                                  DSPATR(PC)
     A                                      CHECK(LC)
     A            D1COCD    R        B  6 28REFFLD(COREC/COCOCD *LIBL/HSP062)
     A N60N35                               DSPATR(HI)
     A  35                                  DSPATR(RI)
     A N60                                  DSPATR(UL)
     A  35                                  DSPATR(PC)
     A  60                                  DSPATR(PR)
     A*
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R HP1026K1
     A*%%TS  SD  20250701  012639  ISSGOYA     REL-V7R5M0  5770-WDS
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  4'F3=Exit   F5=Refresh'
     A*
     A*---------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE FORMAT
     A*---------------------------------------------------------------
     A*
     A          R HP1026ES                  SFL
     A*%%TS  SD  20250626  074131  ISSGOYA     REL-V7R5M0  5770-WDS
     A                                      SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ(10)
     A*
     A*---------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*---------------------------------------------------------------
     A*
     A          R HP1026EC                  SFLCTL(HP1026ES)
     A*%%TS  SD  20250626  074131  ISSGOYA     REL-V7R5M0  5770-WDS
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ(10)
