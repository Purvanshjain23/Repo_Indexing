      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      HOG PRODUCTION
      * PROGRAM:     HP663
      * TITLE:       FEED TICKET EXCEPTION REPORT
      * PROGRAMMER:  MARY PALMER
      * CREATED:     4/26/94
      *
      *  FUNCTION:   THIS PROGRAM PRINTS A SINGLE LINE REPORT
      *              OF FEED TICKET HEADER RECORDS FOR A SELECTED
      *              BUSINESS OFFICE AND CUT-OFF DATE.
      *
      *              THIS REPORT RUNS WHEN THE USER HAS SELECTED
      *              ANY OF THE FOLLOWING OPTIONS FROM HP463.
      *                      NOT COSTED
      *                      NOT POSTED TO PERIOD END
      *                      NOT POSTED TO GENERAL LEDGER
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 01/30/01  LeAnne Fedor
      *           Production type was removed from the feed ticket files.
      *           So, removed all production type logic from program.
      *
      * 07/16/02  LeAnne Fedor
      *           Removed 'Production Phase' from the listing since the field has
      *           been removed from the Feed Ticket Header file.
      *
      * 04/23/09  LeAnne Ramsey
      *           Recompile only. Costed Time/User added to Feed Ticket Header.
      *
      * 05/12/09  LeAnne Ramsey
      *           Recompile only. PickUp Ticket Type added to Feed Ticket Header.
      *
      * 09/21/09  LeAnne Ramsey
      *           Recompile only. Added new field 'Upload Date' to Feed Ticket Header.
      *
      * 05/10/22 Eric L SDN736 Recompiled ticket nbr increase (TKNO & RTNO 7.0 TO 9.0)
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     FHSP037    IF   E           K DISK
      *    FEED TICKET HEADER      (KEY: FHFMBO FHTKNO FHTRCD)
      *
     FQPRINT    O    F  132        PRINTER OFLIND(*INOF)
      /EJECT
      ****************************************************************
      * INPUT SPECS
      ****************************************************************
      *
      *---------------------------------------------------------------
      * STANDARD PROGRAM STATUS DATA STRUCTURE
      *---------------------------------------------------------------
      *    EXTERNALLY DEFINED AS UTPGFR (RECORD FORMAT: PGMDSR)
     D PGMDS         ESDS                  EXTNAME(UTPGFR)
      *
      *---------------------------------------------------------------
      *  NAMED CONSTANTS
      *---------------------------------------------------------------
      *
     D YES             C                   CONST('Y')
     D NO              C                   CONST('N')
      *
      *---------------------------------------------------------------
      *  DATE FORMATS
      *---------------------------------------------------------------
      *
      * DATE IN MMDDYY FORMAT
      *
     D                 DS
     D  WKMDY                  1      6  0
     D  WKMM                   1      2  0
     D  WKDD                   3      4  0
     D  WKYY                   5      6  0
      *
      * DATE IN CCYYMMDD FORMAT
      *
     D                 DS
     D  WKCYMD                 1      8  0
     D  WKCEN                  1      2  0
     D  WKYER                  3      4  0
     D  WKMON                  5      6  0
     D  WKDAY                  7      8  0
      *
      *---------------------------------------------------------------
      * LDA - REPORT SELECTIONS
      *---------------------------------------------------------------
      *
     D                UDS
     D  LDFMBO                 1      5
     D  LDCYMD                 6     13  0
     D  LDROCD                14     14
     D  LDRODS                15     44
     D  LDCMDY                45     50  0
     D  LDBODS                51     80
      /EJECT
      ****************************************************************
      * MAIN LINE
      ****************************************************************
      *
      * READ AND PRINT FEED TICKETS SELECTED BY THE OPEN QUERY
      *
     C     *IN92         DOUEQ     *ON                                          MAIN DO
     C                   READ      HSP037                                 92
     C     *IN92         IFEQ      *OFF                                         IF NOT EOF
      *
      * FLIP REFERENCE TICKET DATE TO MDY
      *
     C                   Z-ADD     FHRTDT        WKCYMD
     C                   EXSR      $TOMDY
     C                   Z-ADD     WKMDY         DTRMDY
      *
      * FLIP TICKET DATE TO MDY
      *
     C                   Z-ADD     FHTKDT        WKCYMD
     C                   EXSR      $TOMDY
     C                   Z-ADD     WKMDY         DTTMDY
      *
     C     *INOF         IFEQ      *ON
     C                   EXCEPT    HRHDR
     C                   ENDIF
      *
      * PRINT DETAIL LINE
     C                   EXCEPT    DTLINE
     C                   MOVEL     YES           PRTFLG
     C                   ENDIF                                                  IF NOT EOF
     C                   ENDDO                                                  MAIN DO
      *
      *---------------------------------------------------------------
      * EOF PROCESSING
      *---------------------------------------------------------------
      *  IF THE NO HEADER RECORDS MATCHED THE SELECTION CRITERIA, THEN
      *  PRINT THE STANDARD 'NO DATA...' ERROR MESSAGE
      *
     C     PRTFLG        IFEQ      NO
     C                   EXCEPT    NODATA
     C                   ENDIF
      *
     C                   SETON                                        LR
      /EJECT
      *---------------------------------------------------------------
      * $TOMDY:  FLIP DATE FROM YYMMDD TO MMDDYY
      *---------------------------------------------------------------
      *
     C     $TOMDY        BEGSR
      *
     C                   Z-ADD     WKMON         WKMM
     C                   Z-ADD     WKDAY         WKDD
     C                   Z-ADD     WKYER         WKYY
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * *INZSR - INITIALIZATION SUBROUTINE
      *---------------------------------------------------------------
      *
     C     *INZSR        BEGSR
      *
      * DEFINE PRINT FIELDS FOR DETAIL LINE
      *
     C                   Z-ADD     0             DTRMDY            6 0
     C                   Z-ADD     0             DTTMDY            6 0
      *
      * SET PRINT FLAG TO DETERMINE IF ANY RECORDS WERE PRINTED
      * SET UP HEADING UNDERLINE FIELD FOR PRINT
      * RETRIEVE TIME FOR REPORT HEADING
      *
     C                   MOVEL     NO            PRTFLG            1
     C                   MOVEL     *ALL'-'       DASH             80
     C                   TIME                    RTIME             6 0
      *
      * PRINT HEADINGS FOR FIRST TIME THROUGH
      *
     C                   EXCEPT    HRHDR
      *
     C                   ENDSR
      /EJECT
      *-------------------------------------------------------------
      * REPORT HEADING LINES
      *-------------------------------------------------------------
      *
     OQPRINT    E            HRHDR          1 01
     O                       SDPGM               10
     O                                           48 'HOG PRODUCTION SYSTEM'
     O                                           70 'DATE'
     O                       UDATE         Y     80
      *
     O          E            HRHDR          1
     O                       SDUSR               10
     O                                           47 'FEED TICKET EXCEPTION RE'
     O                                           51 'PORT'
     O                                           70 'TIME'
     O                       RTIME               80 '  :  :  '
      *
     O          E            HRHDR          1
     O                                           70 'PAGE'
     O                       PAGE          Z     80
      *
     O          E            HRHDR          1
     O                                           18 'BUSINESS OFFICE ..'
     O                       LDFMBO              25
     O                       LDBODS              60
      *
     O          E            HRHDR          1
     O                                           18 'CUT-OFF DATE .....'
     O                       LDCMDY        Y     27
      *
     O          E            HRHDR          2
     O                                           18 'REPORT OPTION ....'
     O                       LDROCD              21
     O                       LDRODS              60
     O*
     O          E            HRHDR          1
     O                                           19 '---- REFERENCE ----'
     O                                           28 'TICKET'
     O                                           34 'TRAN'
     O                                           43 'TICKET'
     O                                           50 'PROD'
     O                                           56 'COST'
     O                                           60 'TK'
     O                                           66 'FARM'
      *
     O          E            HRHDR          1
     O                                            8 'TICKET #'
     O                                           17 'DATE'
     O                                           28 'NUMBER'
     O                                           34 'CODE'
     O                                           42 'DATE'
     O                                           49 'Y/N'
     O                                           55 'Y/N'
     O                                           60 'ST'
     O                                           66 'SITE'
      *
     O          E            HRHDR          2
     O                       DASH                80
      *
      *-------------------------------------------------------------
      * FEED TICKET DETAIL LINE
      *-------------------------------------------------------------
      *
     O          E            DTLINE      1
     O                       FHRTNO        Z      9
     O                       DTRMDY        Y     19
     O                       FHTKNO        M     30
     O                       FHTRCD              33
     O                       DTTMDY        Y     44
     O                       FHPRFL              48
     O                       FHCSFL              55
     O                       FHTSCD              60
     O                       FHFSCD        M     67
      *
      *-------------------------------------------------------------
      * NO DATA MESSAGE LINE
      *-------------------------------------------------------------
      *
     O          E            NODATA      1
     O                                           19 'NO DATA MEETS YOUR'
     O                                           38 'SELECTION CRITERIA'
