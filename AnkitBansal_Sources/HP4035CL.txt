/***************************************************************** */
/*                                                                 */
/*  PROGRAM NUMBER:  HP4035CL                                      */
/*  PROGRAM NAME:    HOG DEFECT DOWNLOAD                           */
/*  PROGRAMMER:      LEANNE RAMSEY                                 */
/*  CREATE DATE:     12/22/08                                      */
/*                                                                 */
/*  FUNCTION:        THIS CL IS SUBMITTED WITH QCMDEXC FROM        */
/*                   HP4035-SPECIFY OPTIONS FOR HOG DEFECT DOWNLOAD*/
/*                                                                 */
/*                   OPTIONS ARE PASSED THROUGH THE LDA.           */
/*                                                                 */
/*******************************************************************/

             PGM

             DCL        VAR(&QDATA)  TYPE(*CHAR) LEN(100)

             DCL        VAR(&LDFKLDT) TYPE(*DEC)  LEN(7)
             DCL        VAR(&LDTKLDT) TYPE(*DEC)  LEN(7)
             DCL        VAR(&LDFSCD)  TYPE(*DEC)  LEN(5)
             DCL        VAR(&LDCELL)  TYPE(*DEC)  LEN(2)
             DCL        VAR(&LDTFCD)  TYPE(*CHAR) LEN(5)
             DCL        VAR(&LDEMAIL) TYPE(*CHAR) LEN(50)
             DCL        VAR(&FLAG)    TYPE(*CHAR) LEN(1)

             CHGVAR     VAR(&LDFKLDT) VALUE(%SST(*LDA 1 7))
             CHGVAR     VAR(&LDTKLDT) VALUE(%SST(*LDA 8 7))
             CHGVAR     VAR(&LDFSCD)  VALUE(%SST(*LDA 15 5))
             CHGVAR     VAR(&LDCELL)  VALUE(%SST(*LDA 20 2))
             CHGVAR     VAR(&LDTFCD)  VALUE(%SST(*LDA 22 5))
             CHGVAR     VAR(&LDEMAIL) VALUE(%SST(*LDA 300 50))

/*-----------------------------------------------------------------*/
/* CREATE THE WORKFILE                                             */
/*-----------------------------------------------------------------*/

             CRTDUPOBJ  OBJ(HSP3035) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)

/*-------------------------------------------------------------------*/
/* BUILD/RUN QUERY OVER HPS FILE: FARM SITE                          */
/*-------------------------------------------------------------------*/

/* INTIALIZE QUERY                                                   */

             CHGVAR     VAR(%SST(&QDATA 1 12)) VALUE('FSFSCD *NE 0')

/* FARM                                                                   */
             IF         COND(&LDFSCD *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 14 15)) VALUE('*AND FSFSCD +
                          *EQ')
             CHGVAR     VAR(%SST(&QDATA 30 5)) VALUE(&LDFSCD)
             ENDDO

/* CELL                                                                   */
             IF         COND(&LDCELL *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 36 15)) VALUE('*AND FSCELL +
                          *EQ')
             CHGVAR     VAR(%SST(&QDATA 52 2)) VALUE(&LDCELL)
             ENDDO

/* TYPE OF FARM                                                           */
             IF         COND(&LDTFCD *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 55 17)) VALUE('*AND FSTFCD +
                          *EQ "')
             CHGVAR     VAR(%SST(&QDATA 72 5)) VALUE(&LDTFCD)
             CHGVAR     VAR(%SST(&QDATA 77 1)) VALUE('"')
             ENDDO

             OVRDBF     FILE(HSP018) SHARE(*YES)
             OPNQRYF    FILE((HSP018)) FORMAT(*FILE) +
                          QRYSLT(&QDATA) KEYFLD(*FILE) SEQONLY(*YES)

/*-------------------------------------------------------------------*/
/* BUILD/RUN QUERY OVER HPE FILE: TATTOO HEADER                      */
/*-------------------------------------------------------------------*/

/*  ALWAYS SELECT COMPANY 360 RECORDS IN THE SELECTED DATE RANGE     */

             CHGVAR     VAR(&QDATA) VALUE(' ')
             CHGVAR     VAR(%SST(&QDATA 1 10)) VALUE('A1B0DT *GE')
             CHGVAR     VAR(%SST(&QDATA 12 7)) VALUE(&LDFKLDT)
             CHGVAR     VAR(%SST(&QDATA 20 15)) VALUE('*AND A1B0DT +
                          *LE')
             CHGVAR     VAR(%SST(&QDATA 36 7)) VALUE(&LDTKLDT)

             CHGVAR     VAR(%SST(&QDATA 44 19)) VALUE('*AND A1HMNB +
                          *EQ 360')

             OVRDBF     FILE(PKA1CPK1) SHARE(*YES)
             OPNQRYF    FILE((PKA1CPK1)) FORMAT(*FILE) QRYSLT(&QDATA) +
                          KEYFLD(*FILE) SEQONLY(*YES)

/*-----------------------------------------------------------------*/
/* BUILD THE WORKFILE                                              */
/*-----------------------------------------------------------------*/

             CALL       PGM(HP3035) PARM(&FLAG)

             CLOF       OPNID(HSP018)
             DLTOVR     FILE(HSP018)

             CLOF       OPNID(PKA1CPK1)
             DLTOVR     FILE(PKA1CPK1)

/*-------------------------------------------------------------------*/
/* DOWNLOAD                                                          */
/*-------------------------------------------------------------------*/

             IF         COND(&FLAG = 'Z')     THEN(DO)
             ESNDMAIL   RECIPIENT(&LDEMAIL) SUBJECT('Hog Defect +
                          Download') MSG('Your selections on +
                          HP4035-Specify Options for Hog Defect +
                          Download did not extract any data for the +
                          spreadsheet download.  Please change your +
                          selections and submit the Download again.')
             MONMSG     MSGID(CPF0000)
             ENDDO

             IF         COND(&FLAG = 'T')     THEN(DO)
             ESNDMAIL   RECIPIENT(&LDEMAIL) SUBJECT('Hog Defect +
                          Download') MSG('Your selections on +
                          HP4035-Specify Options for Hog Defect +
                          Download extracted over 66,000 +
                          records--which is too many records for +
                          the spreadsheet to handle. Change your +
                          selections to extract fewer records and +
                          submit the Download again.')
             MONMSG     MSGID(CPF0000)
             ENDDO

             IF         COND(&FLAG = ' ')     THEN(DO)
             EXECUTE    SQL('SELECT * FROM HSP3035 ORDER BY W1KLDT, +
                          W1FSCD, W1DFCD') PCFMT(*XLS) +
                          REPLACE(*YES) RECIPIENT(&LDEMAIL) +
                          EMLMSG('HP4035 Hog Defect Download') +
                          TEXT('HPS Hog Defect Download')
             MONMSG     MSGID(CPF0000)
             ENDDO

             ENDPGM

