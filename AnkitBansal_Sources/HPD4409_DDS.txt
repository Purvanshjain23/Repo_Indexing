     A*%%TS  SD  20050131  113359  ISLRAMS     REL-V5R2M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION
     A*  DISPLAY FILE:  HPD4409
     A*  TITLE:         SPECIFY OPTIONS FOR SIRE LINE LISTING
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  DATE:          01/31/05
     A*
     A*  NOTE:  THIS IS A POP-UP WINDOW. THE WINDOW DEFINITION IS IN
     A*         RECORD FORMAT HP4409EC
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CF03(03 'EXIT')
     A                                      CF04(04 'PROMPT')
     A                                      CF08(08 'SUBMIT')
     A*
     A*----------------------------------------------------------------
     A* RECORD FORMAT ONE
     A*----------------------------------------------------------------
     A          R HP4409R1
     A*%%TS  SD  20050131  113359  ISLRAMS     REL-V5R2M0  5722-WDS
     A                                      WINDOW(HP4409EC)
     A                                      TEXT('INPUT SCREEN')
     A                                      SETOF(31)
     A                                      SETOF(32)
     A                                      SETOF(33)
     A                                      SETOF(34)
     A                                      SETOF(35)
     A                                      SETOF(51)
     A                                      SETOF(52)
     A                                      SETOF(53)
     A                                      SETOF(54)
     A                                      SETOF(55)
     A                                      OVERLAY
     A                                      RTNCSRLOC(&RCD &FLD)
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  1REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 19'SPECIFY OPTIONS FOR SIRE LINE LIST-
     A                                      ING'
     A                                      DSPATR(HI)
     A                                  3  1'Sort sequence ......'
     A                                  3 22'?'
     A            D1LISTCD       1A  B  3 24
     A  31
     AO 51                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A N31                                  DSPATR(HI)
     A            D1LISTDS      35A  O  3 29
     A                                  5  1'Sire line code .....'
     A                                  5 22'?'
     A            D1SICD    R        B  5 24REFFLD(HSRREF/@@SICD *LIBL/HSPREF)
     A  32
     AO 52                                  DSPATR(PC)
     A  32                                  DSPATR(RI)
     A N32                                  DSPATR(HI)
     A                                  5 41'(Blank for All)'
     A                                  6  1'Sire line company ..'
     A                                  6 22'?'
     A            D1SICOMP  R        B  6 24REFFLD(HSRREF/@@SICOMP *LIBL/HSPREF)
     A  33
     AO 53                                  DSPATR(PC)
     A  33                                  DSPATR(RI)
     A N33                                  DSPATR(HI)
     A                                  6 41'(Blank for All)'
     A                                  7  1'Prime line .........'
     A                                  7 22'?'
     A            D1PRIME   R        B  7 24REFFLD(HSRREF/@@PRIME *LIBL/HSPREF)
     A                                      EDTCDE(M)
     A  34
     AO 54                                  DSPATR(PC)
     A  34                                  DSPATR(RI)
     A N34                                  DSPATR(HI)
     A                                  7 41'(Blank for All)'
     A                                  9  1'Active/inactive status .......'
     A                                  9 32'?'
     A            D1AIST    R        B  9 34REFFLD(HSRREF/@@AIST *LIBL/HSPREF)
     A  35
     AO 55                                  DSPATR(PC)
     A  35                                  DSPATR(RI)
     A N35                                  DSPATR(HI)
     A                                  9 41'(Blank for All)'
     A                                 11  1'Cognos report grouping .......'
     A            D1GG0     R        B 11 34REFFLD(SIREC/SIGG0 *LIBL/HSP098)
     A                                 11 41'(Blank for All)'
     A                                 12  1'Alternate report grouping 1 ..'
     A            D1GG1     R        B 12 34REFFLD(SIREC/SIGG1 *LIBL/HSP098)
     A                                 12 41'(Blank for All)'
     A                                 13  1'Alternate report grouping 2 ..'
     A            D1GG2     R        B 13 34REFFLD(SIREC/SIGG2 *LIBL/HSP098)
     A                                 13 41'(Blank for All)'
     A*
     A*----------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*----------------------------------------------------------------
     A          R HP4409K1
     A*%%TS  SD  20040503  104056  ISLRAMS     REL-V5R2M0  5722-WDS
     A                                      WINDOW(HP4409EC)
     A                                      TEXT('COMMAND KEYS')
     A                                      OVERLAY
     A  43                             15  2'Message program failed. Call IS.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 16  1'F3=Exit'
     A                                 16 10'F4=Prompt'
     A                                 16 21'F8=Submit'
      *
      *----------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *----------------------------------------------------------------
     A          R HP4409ES
     A                                      TEXT('Error message subfile')
     A                                      SFL SFLMSGRCD(17)
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
     A*
     A*----------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT WITH WINDOW DEFINITION
     A*----------------------------------------------------------------
     A          R HP4409EC                  SFLCTL(HP4409ES)
     A*%%TS  SD  20000628  103649  ISLRAMS     REL-V4R2M0  5769-PW1
     A                                      TEXT('Error message subfile')
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A                                      WINDOW(*DFT 18 70)
     A                                      WDWBORDER((*DSPATR RI) (*CHAR '    -
     A                                          '))
     A            MSGPGM                    SFLPGMQ(10)
     A*
     A*----------------------------------------------------------------
     A* ASSUME FORMAT - REQUIRED FOR WINDOWS
     A*----------------------------------------------------------------
     A*
     A          R ASSUME
     A*%%TS  SD  20000627  095625  ISLRAMS     REL-V4R2M0  5769-PW1
     A                                      TEXT('ASSUME FORMAT FOR WINDOWS')
     A                                      ASSUME
     A                                      OVERLAY
     A                                 24 79' '
