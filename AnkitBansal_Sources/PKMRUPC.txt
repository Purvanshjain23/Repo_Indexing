/***************************************************************** */
/*                                                                 */
/*  PROGRAM NUMBER:  PKHWUPR                                       */
/*  PROGRAM NAME:    PRINT CARCASS DETAIL                          */
/*  PROGRAMMER:      MARK ARENSBERG                                */
/*  CREATE DATE:     02/24/97                                      */
/*                                                                 */
/*  FUNCTION:        THIS CL IS CALLED BY THE PAYMENT POST PROGRAM */
/*                   (PKTFXFR) AND THE PRINT CARC DTL & SUM CLP    */
/*                   (PKXCUPR).  THE PURPOSE OF THE CL IS TO       */
/*                   CHANGE THE OVERRIDE ON THE PRINT FILE FOR     */
/*                   FAXING OR FOR REPORTING.                      */
/*                   USER OPTIONS ARE PASSED THROUGH THE PARMS.    */
/*                                                                 */
/*******************************************************************/
/*  Modification History                                           */
/*  SLM     03/21/97 DO NOT ALLOW PROGRAMERS TO FAST FAX ON        */
/*          EITHER LITTBITE OR BIGBYTE                             */
/*                                                                 */
/*******************************************************************/
             PGM        PARM(&RTN &XTATSQ &ANAL &SYSVAL +
                             &PDEV1 &HOLD &SAVE &XCOPIES +
                             &XPRDVN# &XPRDLCVN# &FXPROD &FXPRDLC +
                             &IGNFAX)
/* INPUT PARMS                                             */
             DCL        VAR(&RTN) TYPE(*CHAR) LEN(7)
             DCL        VAR(&PDEV1) TYPE(*CHAR) LEN(10)
             DCL        VAR(&HOLD) TYPE(*CHAR) LEN(4)
             DCL        VAR(&SAVE) TYPE(*CHAR) LEN(4)
             DCL        VAR(&XTATSQ) TYPE(*CHAR) LEN(15)
             DCL        VAR(&XPRDVN#) TYPE(*CHAR) LEN(15)
             DCL        VAR(&XPRDLCVN#) TYPE(*CHAR) LEN(15)
             DCL        VAR(&XCOPIES) TYPE(*CHAR) LEN(15)
             DCL        VAR(&ANAL) TYPE(*CHAR) LEN(1)
             DCL        VAR(&SYSVAL) TYPE(*CHAR) LEN(33)
             DCL        VAR(&LPP) TYPE(*CHAR) LEN(40)
             DCL        VAR(&FXPROD) TYPE(*CHAR) LEN(1)
             DCL        VAR(&FXPRDLC) TYPE(*CHAR) LEN(1)
             DCL        VAR(&IGNFAX) TYPE(*CHAR) LEN(1)

/* NUMERIC VARIABLES                                             */
             DCL        VAR(&COPIES) TYPE(*DEC) LEN(2 0)
             DCL        VAR(&TATSQ) TYPE(*DEC) LEN(5 0)
             DCL        VAR(&PRDVN#) TYPE(*DEC) LEN(8 0)
             DCL        VAR(&PRDLCVN#) TYPE(*DEC) LEN(8 0)
             DCL        VAR(&GRPAUT) TYPE(*CHAR) LEN(10)

/* SBDPRTOVR VARIABLE                                          */
             DCL        VAR(&CMD) TYPE(*CHAR) LEN(512)

/* CHANGE ALPHA PARMS TO NUMERIC VARIABLES                       */
             CHGVAR     VAR(&COPIES) VALUE(&XCOPIES)
             CHGVAR     VAR(&TATSQ) VALUE(&XTATSQ)
             CHGVAR     VAR(&PRDVN#) VALUE(&XPRDVN#)
             CHGVAR     VAR(&PRDLCVN#) VALUE(&XPRDLCVN#)
             CHGVAR     VAR(&LPP) VALUE(&SYSVAL)


             RTVUSRPRF  GRPAUT(&GRPAUT)

/* WHEN TESTING ON LITTLBITE OR BIGBYTE PROGRAMERS WILL     */
/* NOT USE THE FASTFAX ROUTINE                              */

             IF         COND(&GRPAUT *EQ 'QGPMR') THEN(DO)
             CHGVAR     VAR(&FXPROD) VALUE('N')
             CHGVAR     VAR(&FXPRDLC) VALUE('N')
             ENDDO

/* IF THE REPORT IS GOING TO BE FAXED THEN SETUP THE        */
/* OVERRIDE TO THE FAST FAX QUEUE ELSE OVERRIDE FOR PRINTING  */
             IF         COND((&FXPROD *EQ 'Y' *OR &FXPRDLC *EQ 'Y') +
                          *AND (&IGNFAX *EQ 'N')) THEN(DO)

             OVRPRTF    FILE(PKTCPFR$) OUTQ(FASTFAX/FFXOTQIAP)

             ENDDO

             ELSE DO
             SBDPRTOVR  CMDVAR(&CMD) FILE(PKTCPFR$) OUTQ(&PDEV1) +
                          COPIES(&COPIES) HOLD(&HOLD) SAVE(&SAVE) +
                          LETTER(*YES)
             CALL       PGM(QCMDEXC) PARM(&CMD 512)


             ENDDO

/* PRINT CARCASS SUMMARY                                       */

             CALL       PGM(PKTCPFR) PARM(&RTN &TATSQ &ANAL &LPP)


/*   SEND FAX IF PRODUCER OR PRODUCER LOCATION IS SETUP TO         */
             IF         COND((&FXPROD *EQ 'Y' *OR &FXPRDLC *EQ 'Y') +
                          *AND (&IGNFAX *EQ 'N')) THEN(DO)

             CALL PGM(PKHVXFR) PARM(&RTN &PRDVN# &PRDLCVN# &FXPROD +
                                    &FXPRDLC)

             ENDDO

             DLTOVR *ALL

             ENDPGM
