      *****************  RPG PROGRAM HEADING  ************************
     h option(*SRCSTMT:*NODEBUGIO)
      ****************************************************************
      *
      * SYSTEM:      Triumph Foods
      * PROGRAM:     TF231 - Margin: Volume Summary Extension
      * PROGRAMMER:  LeAnne Ramsey
      * CREATED:     03/19/07
      *
      * Function:    When the Margin Adjustment Close function is run, this program is called
      *              to:
      *                 1) Summarize the Margin Adjustment Detail data for NVA data only
      *
      *              Only records with a TF Classifcation Code = 'NVA' are processed.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 04/16/07  LeAnne Ramsey
      *           We renamed the following flag field:
      *             1) Exclude from Volume Flag is now Volume Flag
      *           Before this change Y=Yes meant EXCLUDE the product from processing
      *           After this change  Y=Yes means INCLUDE the product in processing
      *
      * 05/29/07  LeAnne Ramsey
      *           Changed program documentation.
      *
      * 06/20/07  LeAnne Ramsey
      *           Recompile only. New field "Item Type" (will hold FG or WP) was added
      *           to the Margin Adjustment file.
      *
      * 09/05/12  LeAnne Ramsey (E2243)
      *           Recompile only.
      *           Per Damon G. we must now write more/special records to TFP014 for the
      *           Items that receive lbs/dollars from Skirt Meat Items!! So, I had to add
      *           a new field to TFP014:
      *                         ADSMSFL-Skirt Meat Split Flag
      *           I also added ADSMSFL to the key fields on TFL014C.
      *
      * 02/08/22  Danny Nguyen   - DO2484 - WI479 STF Variance Reporting
      *           Recompile only due to TFP014 DBF change. Added the following fields:
      *             ADXPULB  - STF PRODUCED LBS
      *             ADXPUSLB - STF PRODUCED START WEIGHT
      *             ADXYPC   - STF STD YIELD %
      *             ADXPMPPC - STF PUMP %
      *             ADXSLLB  - STF SOLD LBS
      *             ADXSLSLB - STF SOLD START WEIGHT
      *          ‚The 6 new STF fields will NOT be added to TFP318 or TFP331 files at this time.
      *
      * 10/10/22  R Centonze   W105621 - Increase price fields to handle > 999.99
      *         COPY @@SPU1MG TO  @@SPU1MG10 SIZE 10.6  -> FIELD TFP014.ADSPU1MG
      *         COPY @@TPU1MG TO  @@TPU1MG10 SIZE 10.6  -> FIELD TFP014.ADTPU1MG
      *         COPY @@SSL1MG TO  @@SSL1MG10 SIZE 10.6  -> FIELD TFP014.ADSSL1MG
      *         COPY @@SSL2MG TO  @@SSL2MG10 SIZE 10.6  -> FIELD TFP014.ADSSL2MG
      *         COPY @@TSL1MG TO  @@TSL1MG10 SIZE 10.6  -> FIELD TFP014.ADTSL1MG
      *         COPY @@TSL2MG TO  @@TSL2MG10 SIZE 10.6  -> FIELD TFP014.ADTSL2MG
      *          ‚Not changing tfp318/tfp331 at this time!
      * 05/07/24  Santosh Patil P310149 - Field length is increased
      *           in TFP014. Function is recompiled only.
     ‚* 06/03/24  Jagdish Mistry (JM-P310149),Service request number-3014062
     ‚*           Requirement 1-Exclude items volume flag = N for Triumph
     ‚*                         from volume calculations.
     ‚*           Requirement 2-Add work in process items to match
     ‚*                          volume adjustment detail & summary report.
     ‚*                          Consider NVZ in Header PRODUCED Pounds of
     ‚*                          NVA Products.
      *
      /eject
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Ftfl014c   if   e           k disk
      *  Product margin adjustment transaction detail  (open query selects records)
      *
      *
     Ftfp318    uf   e           k disk
      *  Workfile: Volume summary
      *
      *
     Ftfp331    uf a e           k disk
      *  Workfile: Volume summary extension
     ‚* Project-JM-P310149-START
     FTFL331A   Uf   e           k disk    Rename(ESREC:R331A)
     F                                     Prefix(A1:2)
     FPPAOREL1  IF   E           K Disk
     ‚* Project-JM-P310149-END
      *
      *
      /eject
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
     ‚* Project-JM-P310149-START
     D wkwkpe          s                   like(eswkpe)
     ‚* Project-JM-P310149-END
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
     ‚* Project-JM-P310149-START
     D Triumph         c                   'T'
     ‚* Project-JM-P310149-END
      *
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Control fields
      *
     D first           s              1    inz('Y')
      *
      * Work fields
      *
     D wkpc            s                   like(esspuypc)
     D wkrate          s              9  6
      *
     D wkvolfl         s                   like(advolfl)  inz('N')
     D wkrmfl          s                   like(adrmfl)   inz('N')
     D wktfclcd        s                   like(adtfclcd) inz('NVA')
     ‚* Project-JM-P310149-START
     ‚* When NVZ is found variable will be used to update NVA as NVA + NVZ
     D wktfclcdNVA     s                   like(adtfclcd) inz('NVA')
     ‚* Project-JM-P310149-END
      *
      *
      * Parm fields
      *
     D xxwkpe          s              1
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *---------------------------------------------------------------
      *  Local data area
      *---------------------------------------------------------------
      *
     d lda            uds                  dtaara(*lda)
     D  ldyr                   2      5  0
     D  ldwk                   6      7  0
      *
     D  ldwbdt                 8     15  0
     D  ldwedt                29     36  0
      *
     D  ldpe                  51     52  0
     D  ldpbdt                53     60  0
     D  ldpedt                67     74  0
     ‚* Project-JM-P310149-START
     D  ldwpfl               134    134
     ‚* Project-JM-P310149-END
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * Mainline
      ****************************************************************
      *
      * Process each Volume Summary record in the Volume Summary workfile.
      * For each record (ie: Item Structure Type Code)
      *  1) retrieve/summarize NVA records from the Margin Adjustment Detail file
      *  2) perform calcs for the NVA records
      *  3) write an NVA record to the Volume Summary Extension file.
      *  4) calc a Margin Per Pound value for the Volume Summary record that
      *     subtracts the NVA margin per pound from the CV margin per pound
      *
     C     *loval        setll     tfp318
      *
     C                   dou       *in90 = *on                                  Main do
     C                   read      tfp318                                 90
     C                   if        *in90 = *off                                 If not EOF
      *
      * Clear Volume Summary Extension record.
      *
     C                   clear                   esrec
      *
      * Summarize NVA data and write Volume Summary Extension record
      *
     C                   exsr      $summarize
      *
      * Update the Volume Summary record
      *
     C                   exsr      $upd318
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Main do

     ‚* Project-JM-P310149-START
     ‚* NVA & NVZ are handled on Item Structure Type code level
     C                   Eval      wktfclcd = 'NVZ'
     C     *Loval        SetLL     TFP318
     C                   DoU       *In90 = *On                                  Main do
     C                   Read      TFP318                                 90
     C                   If        *In90 = *Off                                 If not EOF
     C                   Clear                   esrec
     C                   ExSr      $Summarize
     C                   EndIf                                                  If not EOF
     C                   EndDo                                                  Main do
     ‚* Project-JM-P310149-END
      *
     C                   seton                                        lr
      /eject
      *---------------------------------------------------------------------------------------
      * Summarize the NVA Detail records into a Volume Summary Extension record
      *---------------------------------------------------------------------------------------
      *
     C     $summarize    begsr
      *
      * Read each record for this Item Structure Type Code in the Margin Detail file.
      * An open query over the file excludes/includes:
      *     1) all records with TF Class Group Code = OT or BP are excluded
      *     2) only records for the week/period being processed are included
      *     3) only records for Finished Goods items are included
      *
      * By hardcoding our key field values, our logic will only summarize
      * records for this Item Structure Type code where:
      *       1) Converted raw material is NO.
      *       2) Volume Flag is NO.
      *       3) TF Classification Code is NVA.
      *
      * And, program logic limits processing to records where either SBF or TF
      *      have some produced pounds.
      *
     C                   move      yes           first
     C     key01         setll     tfl014c
      *
     C                   dou       *in91 = *on                                  Do NVAs
     C     key01         reade     tfl014c                                91
     C                   if        *in91 = *off and                             If no EOF
     C                             (adspulb <> 0 or adtpulb <> 0)
     ‚* Project-JM-P310149-START
     ‚* Exclude vol flag = 'N' items from calculations for Triumph
     C     Key04         Chain     PPAOREL1
     C                   If        %Found(PPAOREL1) And
     C                             AOSXSX = No And AOD3TX = Triumph
     C                   Iter
     C                   EndIf
     ‚* Project-JM-P310149-END
      *
     C                   if        first = yes
     C                   move      no            first
     C                   endif
      * Accumulate
     C                   exsr      $accum
     C                   endif                                                  If not EOF
     C                   enddo                                                  Do NVAs
      *
      *
      * If you had summarized data,
      * Write a Volume Summary Extension record (for the NVA values).
      *
     C                   if        first = no
     C                   exsr      $wrt331
     C                   endif
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Accumulate NVA values from the Margin Adjustment Detail file
      *---------------------------------------------------------------
      *
     C     $accum        begsr
      *
      * Produced pounds
     C                   add       adspulb       esspulb
     C                   add       adtpulb       estpulb
      *
      * Produced value amount
      *
     C                   add       adspuam       esspuam
     C                   add       adtpuam       estpuam
      *
      * Produced Start Weight
      *
     C                   add       adspuslb      esspuslb
     C                   add       adtpuslb      estpuslb
      *
      * Meat cost amounts
     C                   add       adspumam      esspumam
     C                   add       adtpumam      estpumam
      *
      * Labor cost amounts
      *
     C                   add       adspulam      esspulam
     C                   add       adtpulam      estpulam
      *
      * Packaging cost amounts
      *
     C                   add       adspukam      esspukam
     C                   add       adtpukam      estpukam
      *
      * Ingredient cost amounts
      *
     C                   add       adspuiam      esspuiam
     C                   add       adtpuiam      estpuiam
      *
      * Other cost amounts
      *
     C                   add       adspuoam      esspuoam
     C                   add       adtpuoam      estpuoam
      *
      * Product cost amounts
      *
     C                   add       adspupam      esspupam
     C                   add       adtpupam      estpupam
      *
      * Margin amounts
     C                   add       adspumgam     esspumgam
     C                   add       adtpumgam     estpumgam
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------------------------------
      * Write an NVA record to the Volume Summary Extension file for this Item Structure Type
      *---------------------------------------------------------------------------------------
      *
     C     $wrt331       begsr
      *
     C                   move      wktfclcd      estfclcd
     C                   z-add     vsistycd      esistycd
      *
     C                   z-add     ldyr          esyr
      *
     C                   select
      * Weekly
     C                   when      xxwkpe = 'W'
     C                   z-add     ldwk          eswkpe
     C                   z-add     ldwbdt        esbdt
     C                   z-add     ldwedt        esedt
      * Period
     C                   when      xxwkpe = 'P'
     C                   z-add     ldpe          eswkpe
     C                   z-add     ldpbdt        esbdt
     C                   z-add     ldpedt        esedt
     C                   endsl
      *
      * When Produced Pounds are zero, override Produced Start Weight values to zero.
      *
     C                   if        esspulb = 0
     C                   z-add     0             esspuslb
     C                   endif
      *
     C                   if        estpulb = 0
     C                   z-add     0             estpuslb
     C                   endif
      *
      * Aggregate fields
      *
     C     esspulb       add       estpulb       esapulb
     C     esspuslb      add       estpuslb      esapuslb
     C     esspumgam     add       estpumgam     esapumgam
      *
      *
      * Yield Percents:
      *       (produced pounds / produced start weight) * 100
      *
     C                   if        (esspuslb + estpuslb) <> 0
     C                   eval(h)   wkpc = ((esspulb + estpulb) /
     C                                        (esspuslb + estpuslb)) * 100
     C                   if        wkpc <= 999
     C                   z-add     wkpc          esapuypc
     C                   endif
     C                   endif
      *
     C                   if        esspuslb <> 0
     C                   eval(h)   wkpc = (esspulb / esspuslb) * 100
     C                   if        wkpc <= 999
     C                   z-add     wkpc          esspuypc
     C                   endif
     C                   endif
      *
     C                   if        estpuslb <> 0
     C                   eval(h)   wkpc = (estpulb / estpuslb) * 100
     C                   if        wkpc <= 999
     C                   z-add     wkpc          estpuypc
     C                   endif
     C                   endif
      *
      *
      * Make "Per Pound" calculations based on Produced Pounds.
      *
      *       Aggregate:
     C                   if        (esspulb + estpulb) <> 0
      *
     C                   eval(h)   esapumco = (esspumam  + estpumam) /           Meat
     C                                        (esspulb + estpulb)
      *
     C                   eval(h)   esapulco = (esspulam  + estpulam) /           Labor
     C                                        (esspulb + estpulb)
      *
     C                   eval(h)   esapukco = (esspukam  + estpukam) /           Packaging
     C                                        (esspulb + estpulb)
      *
     C                   eval(h)   esapuico = (esspuiam  + estpuiam) /           Ingredient
     C                                        (esspulb + estpulb)
      *
     C                   eval(h)   esapuoco = (esspuoam  + estpuoam) /           Other
     C                                        (esspulb + estpulb)
      *
     C                   eval(h)   esapupco = esapumco + esapulco +              Product
     C                                        esapukco + esapuico +
     C                                        esapuoco
      *
     C                   eval(h)   esapupr  = (esspuam + estpuam) /              Price
     C                                        (esspulb + estpulb)
      *
     C                   eval(h)   esapu1mg = esapupr - esapupco
     C                   endif
      *
      *       Seaboard:
     C                   if        esspulb <> 0
     C                   eval(h)   esspumco = esspumam / esspulb                Meat
     C                   eval(h)   esspulco = esspulam / esspulb                Labor
     C                   eval(h)   esspukco = esspukam / esspulb                Packaging
     C                   eval(h)   esspuico = esspuiam / esspulb                Ingredient
     C                   eval(h)   esspuoco = esspuoam / esspulb                Other
      *
     C                   eval      esspupco = esspumco + esspulco +             Product
     C                                        esspukco + esspuico +
     C                                        esspuoco
      *
     C                   eval(h)   esspupr  = esspuam / esspulb                 Price
     C                   eval(h)   esspu1mg = esspupr - esspupco                Margin
     C                   endif
      *       Triumph:
     C                   if        estpulb <> 0
     C                   eval(h)   estpumco = estpumam / estpulb                Meat
     C                   eval(h)   estpulco = estpulam / estpulb                Labor
     C                   eval(h)   estpukco = estpukam / estpulb                Packaging
     C                   eval(h)   estpuico = estpuiam / estpulb                Ingredient
     C                   eval(h)   estpuoco = estpuoam / estpulb                Other
      *
     C                   eval      estpupco = estpumco + estpulco +             Product
     C                                        estpukco + estpuico +
     C                                        estpuoco
      *
     C                   eval(h)   estpupr  = estpuam / estpulb                 Price
     C                   eval(h)   estpu1mg = estpupr - estpupco                Margin
     C                   endif
      *
      * Produced margin per produced start weight:
      *    Produced margin amount / produced start weight
      *
     C                   if        esspuslb <> 0
     C     esspumgam     div       esspuslb      esspu2mg
     C                   endif
      *
     C                   if        estpuslb <> 0
     C     estpumgam     div       estpuslb      estpu2mg
     C                   endif
      *
     C                   if        esapuslb <> 0
     C     esapumgam     div       esapuslb      esapu2mg
     C                   endif
      *
      * Aggregate produced margin per produced start weight:
      *    Aggregate produced margin amount / Aggregate produced start weight
      *
     C                   if        esapuslb <> 0
     C                   eval(h)   esapu2mg = esapumgam / esapuslb
     C                   endif
     ‚* Project-JM-P310149-START
     ‚* Execute Produced margin per pound calculations i.e. NVA + NVZ,Update NVA
     ‚* Update NVA will be used in volume summary report for print
     C                   If        wktfclcd = 'NVZ'
     C                   ExSr      $NVZmargin
     C                   EndIf
     ‚* Project-JM-P310149-END
      *
      * (Note: Don't clear the record after the write--you will need 1 of the values
      *  later.)
     C                   write     esrec
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Update the Volume Summary record
      *---------------------------------------------------------------
      *
     C     $upd318       begsr
      *
      * Calc: Aggregate Produced Margin per Pound = CV margin less NVA margin
      * Using:
      *    For CV:  AGG PROD MRG PER PROD STR (from Volume Summary)
      *    For NVA: AGG PROD MRG PER PROD STR (from Volume Summary Extension)
      *
     C     vsapu2mg      sub       esapu2mg      vsapu3mg
      *
      *
      * TF Volume Adjustment Payment  (Seaboard pays Triumph this amount):
      *      TF Produced Total Start Weight *
      *         (TF Volume adjustment percentage excess / 100) *
      *              Aggregate produced margin per pound
      *
     C                   eval      wkrate = vstevolpc / 100
     C                   eval(h)   vstvolam = vstputslb * wkrate * vsapu3mg
      *
      *
      * SBF Volume Adjustment Payment (Triumph pays Seaboard):
      *      SBF Produced Total Start Weight *
      *         (SBF Volume adjustment percentage excess / 100) *
      *              Aggregate produced margin per pound
      *
     C                   eval      wkrate = vssevolpc / 100
     C                   eval(h)   vssvolam = vssputslb * wkrate * vsapu3mg
      *
     C                   update    vsrec
      *
     C                   endsr
      /eject
     ‚*---------------------------------------------------------------
     ‚* NVZ margin per pound calculations
     ‚*---------------------------------------------------------------
     C     $NVZmargin    begsr
     ‚* Project-JM-P310149-START
     ‚*
     C     Key02         Chain     TFL331A
     C                   If        %Found(TFL331A)
     ‚*
     ‚* Add weights(Produced LBS) to match header
     C                   Eval      A1SPULB   += ESSPULB
     C                   Eval      A1TPULB   += ESTPULB
     C                   Eval      A1APULB   += ESAPULB
     ‚*
     ‚* Produced start weight
     ‚* To be used for margin per pound calculation NVA = NVA + NVZ
     C                   Eval      A1SPUSLB  += esspuslb
     C                   Eval      A1TPUSLB  += estpuslb
     C                   Eval      A1APUSLB  += esapuslb
     ‚*
     ‚* Copied from $wrt331
     ‚* Produced margin per produced start weight:
     ‚*    Produced margin amount / produced start weight
     C                   if        A1spuslb <> 0
     C     A1spumgam     div       A1spuslb      A1spu2mg
     C                   endif
     ‚*
     C                   if        A1tpuslb <> 0
     C     A1tpumgam     div       A1tpuslb      A1tpu2mg
     C                   endif
     ‚*
     C                   if        A1apuslb <> 0
     C                   eval(h)   A1apu2mg = A1apumgam / A1apuslb
     C                   endif
     ‚*
     C                   Update    R331A
     ‚*
     ‚* Copied from $UPD318
     ‚* Calc: Aggregate Produced Margin per Pound = CV margin less NVA margin
     ‚* Using:
     ‚*    For CV:  AGG PROD MRG PER PROD STR (from Volume Summary)
     ‚*    For NVA: AGG PROD MRG PER PROD STR (from Volume Summary Extension)
     ‚*
     C     vsapu2mg      sub       A1apu2mg      vsapu3mg
     ‚*
     ‚* TF Volume Adjustment Payment  (Seaboard pays Triumph this amount):
     ‚*      TF Produced Total Start Weight *
     ‚*         (TF Volume adjustment percentage excess / 100) *
     ‚*              Aggregate produced margin per pound
     C                   eval      wkrate = vstevolpc / 100
     C                   eval(h)   vstvolam = vstputslb * wkrate * vsapu3mg
     ‚*
     ‚* SBF Volume Adjustment Payment (Triumph pays Seaboard):
     ‚*      SBF Produced Total Start Weight *
     ‚*         (SBF Volume adjustment percentage excess / 100) *
     ‚*              Aggregate produced margin per pound
     C                   eval      wkrate = vssevolpc / 100
     C                   eval(h)   vssvolam = vssputslb * wkrate * vsapu3mg
      *
     C                   update    vsrec
     ‚*
     C                   EndIf
     C                   EndSr
     ‚* Project-JM-P310149-START
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Parm list
      *
     C     *entry        plist
     C                   parm                    xxwkpe
      *
      * Key lists
      *
     C     key01         klist
     C                   kfld                    vsistycd
     C                   kfld                    wkvolfl
     C                   kfld                    wkrmfl
     C                   kfld                    wktfclcd
     ‚* Project-JM-P310149-START
     C     key02         klist
     C                   kfld                    vsistycd
     C                   kfld                    wktfclcdNVA
     C                   kfld                    ldyr
     C                   kfld                    wkwkpe
     ‚*
     C     Key04         KList
     C                   KFld                    ADTFCLCD                       TF CLASSIFICATION
     C                   KFld                    ADISTYCD                       ITEM STRUC TYPE CODE
     C                   KFld                    ADISGRCD                       ITEM STRUC GROUP COD
     C                   KFld                    ADISCLCD                       ITEM STRUC CLASS COD
     ‚*
     ‚* Identify if report is generated for "weekly" data or "period" data.
     C                   Select
     C                   When      ldwpfl = 'W'
     C                   z-add     ldwk          wkwkpe
     C                   When      ldwpfl = 'P'
     C                   z-add     ldpe          wkwkpe
     C                   EndSl
     ‚* Project-JM-P310149-END
      *
     C                   endsr
      /eject
