/********************************************************************/
/*                                                                  */
/*  SYNOPSIS -  PRINT PROGRAM WITH COMPANY, WAREHOUSE,              */
/*              FROM DATE, TO DATE, ITEM STR CLASS AND ITEM TYPE    */
/*              OPEN QUERY FILES OVER IC ITEM BALANCE               */
/*                                                                  */
/*  COMPANY   - SEABOARD CORP                                       */
/*  SYSTEM    - INVENTORY CONTROL                                   */
/*  DATE      - 07/15/96                                            */
/*                                                                  */
/*      PARAMETERS RECEIVED:                                        */
/*                                                                  */
/*          &PGM        - PROGRAM NAME                              */
/*          &COMPL      - COMPANY NUMBER                            */
/*          &FDATEL     - FROM DATE                                 */
/*          &TDATEL     - TO DATE                                   */
/*          &WHSE       - WAREHOUSE CODE                            */
/*          &ITMCLS     - ITEM STR CLASS                            */
/*          &ITMTYP     - ITEM TYPE                                 */
/*          &PRTZR      - PRINT NON ZERO QTYS                       */
/*          &RPTTYP     - REPORT TYPE                               */
/*          &OUTQ       - OUTPUT QUEUE                             */
/*          &HOLD       - HOLD                                     */
/*          &SAVE       - SAVE                                     */
/*          &COPIESL    - NUMBER OF COPIES                          */
/*          &NIGHTQ     - NIGHT QUEUE                               */
/********************************************************************/

 PDM2CLP:    PGM        PARM(&PGM &COMPL &FDATEL &TDATEL &WHSE +
                         &ITMCLSL &ITMTYP &PRTZR &RPTTYP &OUTQ &HOLD +
                         &SAVE &COPIESL &NIGHTQ)

             DCL        &COPIESL    *DEC
             DCL        &COPIES     *DEC     LEN(3 0)
             DCL        &COMPL      *DEC         /*  CO. #       */
             DCL        &COMP       *DEC     LEN(3 0)
             DCL        &FDATEL     *DEC        /*  FROM DATE    */
             DCL        &FDATE      *DEC     LEN(7 0)
             DCL        &TDATEL     *DEC        /*  TO DATE      */
             DCL        &TDATE      *DEC     LEN(7 0)
             DCL        &ITMCLSL    *DEC         /*  ITEM CLASS   */
             DCL        &ITMCLS     *DEC     LEN(3 0)
             DCL        &ITMTYP     *CHAR    3
             DCL        &PRTZR      *CHAR    1
             DCL        &RPTTYP     *CHAR    1
             DCL        &OUTQ       *CHAR    10  /* OUTPUT QUEUE */
             DCL        &NIGHTQ     *CHAR   10  /*  NIGHT JOB QUEUE*/
             DCL        &HOLD       *CHAR   4   /* HOLD        */
             DCL        &SAVE       *CHAR   4   /* SAVE        */
             DCL        &PGM        *CHAR    10  /*  PGM NAME    */
             DCL        &COMPA      *CHAR    3  /* CO. # FOR MSG */
             DCL        &ITMCLSA    *CHAR    3  /* ITEM CLS FOR QRY */
             DCL        &WHSE       *CHAR    3  /* WAREHOUSE     */
             DCL        &PGMO       *CHAR    10 /* PRINTER NAME  */
             DCL        &RETURN     *CHAR    7
             DCL        &COMMAND    *CHAR    512
             DCL        &SELECT     *CHAR    1
             DCL        &A          *CHAR    25 /* QRY SELECT    */
             DCL        &B          *CHAR    36 /* QRY SELECT    */
             DCL        &C          *CHAR    37 /* QRY SELECT    */
             DCL        &D          *CHAR   100 /* QRY SELECT    */

/********************************************************************/
/* APPEND $ TO THE PROGRAM NAME                                     */
/********************************************************************/
             IF         COND(&RPTTYP = 'C')  THEN(DO)
             CHGVAR     VAR(&PGM) VALUE('PDSGPFR')
             ENDDO

             CHGVAR     VAR(&PGMO) VALUE(&PGM *TCAT '$')
             CHGVAR     VAR(&SELECT) VALUE('N')
             CHGVAR     VAR(&COMP) VALUE(&COMPL) /* Change to length +
                          for print program */
             CHGVAR     VAR(&COMPA) VALUE(&COMP) /* Change FOR  +
                          MESSAGES for print program */
             CHGVAR     VAR(&COPIES) VALUE(&COPIESL) /* Change to length +
                          for print program */
             CHGVAR     VAR(&FDATE) VALUE(&FDATEL) /* Change to +
                          length for print program */
             CHGVAR     VAR(&TDATE) VALUE(&TDATEL) /* Change to +
                          length for print program */
             CHGVAR     VAR(&ITMCLS) VALUE(&ITMCLSL) /* Change to +
                          length for print program */
             CHGVAR     VAR(&ITMCLSA) VALUE(&ITMCLS)  /*  FOR QRY +
                          SELECTION  */

/********************************************************************/
/* Override printer file with user specified values                 */
/********************************************************************/

             SBDPRTOVR  CMDVAR(&COMMAND) FILE(&PGMO) OUTQ(&OUTQ) +
                          COPIES(&COPIES) HOLD(&HOLD) SAVE(&SAVE)

             CALL       PGM(QCMDEXC) PARM(&COMMAND 512)

/*********************************************************************/
/*   CREATE OPEN QUERY FILE TO BE USED TO DISPLAY DETAILS            */
/*********************************************************************/

             IF         COND(&COMPA *NE '000') THEN(DO)
             CHGVAR     VAR(&A) VALUE('B0AIC3 *EQ ' *CAT &COMPA)
             CHGVAR     VAR(&SELECT) VALUE('Y')
             ENDDO

             IF         COND(&ITMCLSA *NE '000') THEN(DO)
             IF         COND(&SELECT *EQ 'Y') THEN(DO)
             CHGVAR     VAR(&B) VALUE('*AND B0RINB *EQ ' *CAT &ITMCLSA)
             ENDDO
             ELSE DO
             CHGVAR      VAR(&B) VALUE('B0RINB *EQ ' *CAT &ITMCLSA)
             CHGVAR      VAR(&SELECT) VALUE('Y')
             ENDDO
             ENDDO

             IF         COND(&WHSE *NE '   ') THEN(DO)
             IF         COND(&SELECT *EQ 'Y') THEN(DO)
             CHGVAR     VAR(&C) VALUE('*AND B0AJCD *EQ "' *CAT &WHSE +
                        *CAT '"')
             ENDDO
             ELSE DO
             CHGVAR     VAR(&C) VALUE('B0AJCD *EQ "' *CAT &WHSE +
                        *CAT '"')
             CHGVAR     VAR(&SELECT) VALUE('Y')
             ENDDO
             ENDDO

             CHGVAR     VAR(&D) VALUE(&A *BCAT &B *BCAT &C)

/********************  PRINT REPORT  **************************/

/*   OPEN QUERY FOR DETAIL PRINT.    */

             IF         COND(&SELECT *EQ 'N') THEN(DO)
             CHGVAR     VAR(&D) VALUE('*ALL')
             ENDDO

/*   PRINT INVENTORY STOCK CLOSING BY WAREHOUSE   */
             IF         COND(&RPTTYP = 'W')  THEN(DO)
             OVRDBF     FILE(CAB0RELP) SHARE(*YES)
             OPNQRYF    FILE((CAB0RELP)) QRYSLT(&D) KEYFLD(*FILE) +
                           SEQONLY(*YES)
             ENDDO

/*   PRINT INVENTORY STOCK CLOSING BY STRUCTURE CLASS   */
             IF         COND(&RPTTYP = 'C')  THEN(DO)
             OVRDBF     FILE(CAB0REL2) SHARE(*YES)
             OPNQRYF    FILE((CAB0REL2)) QRYSLT(&D) KEYFLD(*FILE) +
                           SEQONLY(*YES)
             ENDDO

             CALL       PGM(&PGM) PARM(&RETURN &COMP &WHSE +
                          &ITMCLS &ITMTYP &PRTZR &RPTTYP &FDATE &TDATE)

             IF         COND(&RPTTYP = 'W')  THEN(DO)
             CLOF       OPNID(CAB0RELP) /* Close query file */
             DLTOVR     FILE(CAB0RELP) /* Delete override */
             ENDDO

             IF         COND(&RPTTYP = 'C')  THEN(DO)
             CLOF       OPNID(CAB0REL2) /* Close query file */
             DLTOVR     FILE(CAB0REL2) /* Delete override */
             ENDDO
