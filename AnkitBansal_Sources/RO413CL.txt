/***************************************************************** */
/*                                                                 */
/*  SYSTEM:          RESOURCE OPTIMIZATION SYSTEM                  */
/*  PROGRAM NUMBER:  RO413CL                                       */
/*  PROGRAM NAME:    REPORTS                                       */
/*  PROGRAMMER:      LEANNE RAMSEY                                 */
/*  CREATE DATE:     01/22/07                                      */
/*                                                                 */
/*******************************************************************/
/* MODIFICATIONS                                                   */
/*                                                                 */
/* 01/03/08  LEANNE RAMSEY                                         */
/*           ADDED NEW 'HAM GROUP YIELD BREAKDOWN' REPORT.         */
/*                                                                 */
/* 11/21/08  LeAnne Ramsey                                         */
/*           As part of synchronizing the LDAs between the         */
/*           TFS Margin Adjustment Close and the Meat Costing,     */
/*           changed the LDA positions.                            */
/*******************************************************************/

             PGM

             DCL        VAR(&QDATA) TYPE(*CHAR) LEN(115)

             DCL        VAR(&HOLD) TYPE(*CHAR) LEN(4)
             DCL        VAR(&SAVE) TYPE(*CHAR) LEN(4)
             DCL        VAR(&COPY) TYPE(*DEC)  LEN(1)
             DCL        VAR(&OUTQ) TYPE(*CHAR) LEN(10)

             DCL        VAR(&LDRPFL)   TYPE(*CHAR) LEN(1)
             DCL        VAR(&LDCONO)   TYPE(*DEC)  LEN(3)
             DCL        VAR(&LDWEDT)   TYPE(*DEC)  LEN(8)
             DCL        VAR(&LDPGCD)   TYPE(*CHAR) LEN(6)
             DCL        VAR(&LDPLCD)   TYPE(*CHAR) LEN(6)
             DCL        VAR(&LDITCD)   TYPE(*DEC)  LEN(7)

             CHGVAR     VAR(&LDWEDT) VALUE(%SST(*LDA 29 8))
             CHGVAR     VAR(&LDCONO) VALUE(%SST(*LDA 82 3))
             CHGVAR     VAR(&LDRPFL) VALUE(%SST(*LDA 113 1))
             CHGVAR     VAR(&LDITCD) VALUE(%SST(*LDA 222 7))
             CHGVAR     VAR(&LDPGCD) VALUE(%SST(*LDA 259 6))
             CHGVAR     VAR(&LDPLCD) VALUE(%SST(*LDA 299 6))

/*----------------------------------------------------------------*/
/* SET PRINT PARAMETERS FROM LDA                                  */
/*----------------------------------------------------------------*/

 OUTQ:       CHGVAR     VAR(&OUTQ) VALUE(%SST(*LDA 401 10))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(*JOB))

 HOLD:       CHGVAR     VAR(&HOLD) VALUE(%SST(*LDA 411 4))
             IF         COND(&HOLD *NE '*YES' *AND &HOLD *NE '*NO ') +
                          THEN(CHGVAR VAR(&HOLD) VALUE(*NO))

 SAVE:       CHGVAR     VAR(&SAVE) VALUE(%SST(*LDA 415 4))
             IF         COND(&SAVE *NE '*YES' *AND &SAVE *NE '*NO ') +
                          THEN(CHGVAR VAR(&SAVE) VALUE(*NO))

 COPY:       CHGVAR     VAR(&COPY) VALUE(%SST(*LDA 419 1))
             IF         COND(&COPY = 0) THEN(CHGVAR VAR(&COPY) +
                          VALUE(1))

/*---------------------------------------------------------------------*/
/* Extract Item Meat Cost records                                      */
/*---------------------------------------------------------------------*/

             CHGVAR     VAR(%SST(&QDATA 1 10)) VALUE('IMWEDT *EQ')
             CHGVAR     VAR(%SST(&QDATA 12 8)) VALUE(&LDWEDT)

             CHGVAR     VAR(%SST(&QDATA 21 15)) VALUE('*AND IMCONO *EQ')
             CHGVAR     VAR(%SST(&QDATA 37 3)) VALUE(&LDCONO)

/* Primal group                                                        */
             IF         COND(&LDPGCD *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 41 17)) VALUE('*AND IMPGCD *EQ "')
             CHGVAR     VAR(%SST(&QDATA 58 6)) VALUE(&LDPGCD)
             CHGVAR     VAR(%SST(&QDATA 64 1)) VALUE('"')
             ENDDO

/* Production line                                                     */
             IF         COND(&LDPLCD *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 66 17)) VALUE('*AND IMPLCD *EQ "')
             CHGVAR     VAR(%SST(&QDATA 83 6)) VALUE(&LDPLCD)
             CHGVAR     VAR(%SST(&QDATA 89 1)) VALUE('"')
             ENDDO
/* Item                                                                */
             IF         COND(&LDITCD *NE 0)   THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 91 15)) VALUE('*AND IMITCD *EQ')
             CHGVAR     VAR(%SST(&QDATA 107 7)) VALUE(&LDITCD)
             ENDDO

             OVRDBF     FILE(ROL103B) SHARE(*YES)
             OPNQRYF    FILE((ROL103B)) OPTION(*INP) QRYSLT(&QDATA) +
                          KEYFLD(*FILE) SEQONLY(*YES)

/* PRINT REPORT                                                        */

             OVRPRTF    FILE(PRINT198) OUTQ(&OUTQ) COPIES(&COPY) +
                          HOLD(&HOLD) SAVE(&SAVE)

             IF         COND(&LDRPFL *EQ 'M') THEN(DO)
             CALL       PGM(RO613) /* Meat Cost Report */
             ENDDO

             IF         COND(&LDRPFL *EQ 'H') THEN(DO)
             CALL       PGM(RO614) /* Ham Group Yield Breakdown */
             ENDDO

             CLOF       OPNID(ROL103B)
             DLTOVR     FILE(*ALL)

             ENDPGM

