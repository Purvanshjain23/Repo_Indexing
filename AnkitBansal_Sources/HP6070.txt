      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Hog Production
      * PROGRAM:     HP6070
      * TITLE:       Print Sales Movement Bill of Lading
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     10/12/04
      *
      *  FUNCTION:   This program prints a BOL for a sales movement.
      *              This is a rewrite that uses an external print file with an overlay.
      *              The users no longer want to use preprinted forms. So, we have gone to
      *              a custom print file using an overlay.
      *
      *              Once everybody is using the new form, the original print program
      *              (HP670) will be replaced everywhere with this new print program.
      *
      * ***ˆNOTE: (P408)
      * ***ˆANY CHANGES HERE OR TO HP60701 SHOULD CONSIDER KEEPING BOTH PROGRAMS
      * ***ˆIN SYNC. THIS VERSION WILL PRINT THE BOL ONLY.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 07/06/09  LeAnne Ramsey
      *           Recompile only. Added new field 'Continuous Flow Flag' to Hog Group file.
      *
      * 03/10/11  LeAnne Ramsey  (C1421)
      *           Program bug. Field WKBLCD was not being cleared before being populated.
      *
      * 02/14/12  LeAnne Ramsey  (C1933)
      *           Program bug. The Group/Bld/Room workfields were not being populated
      *           prior to printing the L2DTL line in the Facility Receipt Section.
      *
      * 10/15/13  LeAnne Ramsey (E2831)
      *           Recompile only. Added field 'MTech Reference'.
      *
      * 10/04/17  Brad Baden    (E010812)
      *           Print Company Name on the BOL.
      *
      * 11/09/17  Brad Baden    (E010812)
      *           Print Company PO# on the BOL.
      * 12/16/17  Brad Baden    (E010812)
      *           Recompile for new field in file HSL0841A
      *
      * 07/09/20  Susan Blakey & Danny Nguyen  (P408)
      *           Added Premise ID & Animal Movement Number to BOL.
      *           If Sales Type='MRKTS' then print the Premise ID.
      *           If Sales Type='MRKTS' & Co=340 then print the Animal Movement
      *           Number verbiage.
      *
      * 11/03/20  ISE           (H16853)
      *           Recompile only. Increased the Buy Order length from 5 to 7.
      *
      * 01/22/21  ISE             TSN593
      *           Replaced *loval with *start
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fhsp016    if   e           k disk
      *    Managers
      *
      *
     Fhsp018    if   e           k disk
      *    Farm site
      *
      *
P408 Fhsp0181   if   e           k disk
P408  *    Farm Site Additional Information
      *
      *
     Fhsp046    if   e           k disk
      *    Trucker
      *
      *
     Fhsp066    if   e           k disk
      *    Sales type
      *
      *
     Fhsl034d   if   e           k disk
      *    Hog group
      *
      *
     Fhsp084    uf   e           k disk
      * Sales movement header
      * (Records selected/keyed in open query)
      *
      *
     Fhsl0841a  if   e           k disk
      * Sales movement header extension
      *
      *
     Fhsp085    if   e           k disk
      * Sales movement detail
      *
      *
     Fcaabrel1  if   e           k disk
      * Company Name and Address
      *
      *
     Fqprint    o    f  198        printer oflind(*inof)
      *
      /EJECT
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Control fields
      *
     D procfl          s              1
      *
      *
      * Workfields
      *
     D nbrchar         s              1  0
     D start           s              1  0
     D wkalphptct      s              3
      *
     D wkarea          s              3
     D wk3alph         s              3
     D wk4alph         s              4
     D wk7alph         s              7
     D wkphone         s             14
      *
     D wkschd          s                   like(sgschd)
     D wkscmdy         s              6  0
      *
     D wkfscd          s                   like(fsfscd)
     D wkfsnm          s                   like(fsfsnm)
     D wkhgcd          s                   like(hghgcd)
     D wkblcd          s                   like(hgblcd)
     D wkrmcd          s                   like(hgrmcd)
      *
      *
      * Workfields for date manipulation
      *
     D wkisodate       s               D   datfmt(*iso)
      *
      *
      * Print fields
      *
     D h1desc          s             21
     D h1reprint       s             13
      *
     D h1cvno          s                   like(shcvno)
     D h1cnam          s             15
      *
     D h1ticd          s                   like(shticd)
     D h1tinm          s             17
      *
     D h1cocda         s                   like(fscocd)
     D h1cocdn         s                   like(abaic3)
     D h1conm          s                   like(abadtx)
     D h1cupono        s              8  0
      *
     D h1pmgcd         s                   like(fspmgcd)
     D h1pmgnm         s                   like(mgmgnm)
     D h1phone         s             14
      *
     D h1mmgcd         s                   like(fsmmgcd)
     D h1mmgnm         s                   like(mgmgnm)
      *
      *
      * Parm fields
      *
     D oualph          s             40
     D ouajd           s                   like(shcvno)
     D inat1           s              3
     D inajd           s                   like(shcvno)
     D jdefl           s              1
     D incocd          s              3
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *
      * For error message handling, a packed index (3) is required.
      * For program readability, define a corresponding error count
      * field called 'error'
      *
     D                 ds                  inz
     D  error                         2p 0
     D   e                            2p 0 overlay(error)
      *---------------------------------------------------------------
      *  Standard message data structures
      *---------------------------------------------------------------
      *
      * The following 3 data structures are used to speed message
      * handling since it is faster to clear data structures than
      * arrays.  Each is associated with a standard message array.
      *
     D mgi             ds                  inz
     D  mgid                          7    dim(20)
      *
     D mgd             ds                  inz
     D  mgdt                         50    dim(20)
      *
     D mgwk            ds                  inz
     D  mgw                           1    dim(50)
      *
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *---------------------------------------------------------------
      *  LDA
      *---------------------------------------------------------------
      *
     Dlda             uds                  dtaara(*lda)
     D  ldtocd                 1      5
     D  ldcell                32     33  0
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * Mainline
      ****************************************************************
      *
      * Read all sales movements selected by the open query.
      *
593  C*    *loval        setll     hsp084
593  C     *start        setll     hsp084
      *
     C                   dou       *in90 = *on                                  Main do
     C                   read      hsp084                                 90
     C                   if        *in90 = *off                                 If not eof
     C                   move      yes           procfl
      *
      * Retrieve Farm Cell and Transporation Office to compare with
      * user's selections.
      *
     C                   if        ldcell <> 0 or ldtocd <> *blank
     C     shfscd        chain     hsp018                             92
     C                   if        *in92 = *on or
     C                             (ldcell <> 0 and ldcell <> fscell) or
     C                             (ldtocd <> *blank and fstocd <> ldtocd)
     C                   move      no            procfl
     C                   endif
     C                   endif
      *
      *
     C                   if        procfl = yes                                 If process
      *
      * Clear workfields
     C                   exsr      $clear
      *
      * Set up/print data from Movement Header
      *
     C                   exsr      $header
      *
      * Read/print all movement detail records.
      *
     C                   exsr      $detail
      *
     C                   add       1             shptct
     C                   update    shrec
      *
      * P408 - Now, print Premise Id & Animal Movement Number above the Facility
P408  *        Receipt & the Shipping Farm Receipt BUT only if this is a 'MRKTS'
  |   *        Sales Type code.
P408 C   93              exsr      $habvrec
      *
      * Now, print two more sections:
      *    Receiving facility receipt
      *    Shipping farm receipt
      *
     C                   exsr      $receiving
     C                   exsr      $shipping
      *
     C                   endif                                                  If process
     C                   endif                                                  if not eof
     C                   enddo                                                  Main do
      *
     C                   seton                                        lr
      /EJECT
      *---------------------------------------------------------------
      * Clear workfields
      *---------------------------------------------------------------
      *
     C     $clear        begsr
      *
     C                   z-add     0             wkscmdy
     C                   z-add     0             wkfscd
     C                   move      *blank        wkfsnm
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Retrieve/print info for Movement Header record
      *---------------------------------------------------------------
      *
     C     $header       begsr
      *
      * Flip Scheduled Ship Date to MMDDYY format
      *
     C     *iso          test(d)                 shscdt                 92
     C                   if        *in92 = *off                                 If OK date
     C                   move      shscdt        wkisodate
     C     *mdy          move      wkisodate     wkscmdy
     C                   endif                                                  If OK date
      *
      * P408 - Set off 93 for Sale Type Code = 'MRKTS'.
P408 C                   eval      *in93 = *off
      *
      * Set up Sales Type description
      *
     C     shstcd        chain     hsp066                             92
     C                   if        *in92 = *off
     C                   eval      h1desc = ststds
      * P408 - Set on 93 for Sale Type Code = 'MRKTS'.
P408 C     shstcd        ifeq      'MRKTS'
  |  C                   eval      *in93 = *on
P408 C                   endif
     C                   else
     C                   move      *blank        h1desc
     C                   endif
      *
      *
      * Is this a reprint of this BOL?
      *
     C                   if        shptct = 0
     C                   move      *blank        h1reprint
     C                   else
     C                   eval      wkalphptct = %triml(%editc(shptct:'Z'))
     C                   eval      h1reprint = 'RE-PRINT #' + %trim(wkalphptct)
     C                   endif
      *
      * Retrieve info for:
      *  1) farm site
      *  2) customer
      *  3) trucker
     C                   exsr      $fscd
     C                   exsr      $cvno
     C                   exsr      $ticd
      *
      * Print first heading section
      *
     C                   except    h1hdr
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Retrieve Farm Site info
      *---------------------------------------------------------------
      *
     C     $fscd         begsr
      *
     C                   z-add     shfscd        wkfscd
     C     shfscd        chain     hsp018                             92
     C                   if        *in92 = *off                                 If farm
     C                   movel     fsfsnm        wkfsnm
     C                   move      fsmmgcd       h1mmgcd
     C                   move      fspmgcd       h1pmgcd
     C                   move      fscocd        h1cocda
      *
      * Company Name
      *
     C                   monitor                                                monitor
     C                   eval      h1cocdn = %dec(h1cocda:3:0)
     C                   on-error  105
     C                   eval      h1cocdn = 0
     C                   endmon                                                 end monitor

     C                   if        h1cocdn <> *zero                             If company
     C     h1cocdn       chain     caabrel1                           92
     C                   if        %found                                       If %found
     C                   eval      h1conm = abadtx
     C                   else
     C                   eval      h1conm = *blanks
     C                   endif                                                  If %found
     C                   endif                                                  If company
      *
      * Customer PO number
      *
     C     shmvsn        chain     hsl0841a                           92
     C                   if        *in92 = *off
     C                   eval      h1cupono = sxcupono
     C                   endif
      *
      * Multi-manager
      *
     C                   if        fsmmgcd <> *blank                            If multi mgr
     C     fsmmgcd       chain     hsp016                             92
     C                   if        *in92 = *off
     C                   movel     mgmgnm        h1mmgnm
     C                   endif
     C                   endif                                                  If multi mgr
      *
      * Production manager
      *
     C                   if        fspmgcd <> *blank                            If prod mgr
     C     fspmgcd       chain     hsp016                             92
     C                   if        *in92 = *off                                 If valid
     C                   movel     mgmgnm        h1pmgnm
      *
     C                   if        mgtlacno <> 0 or mgtlphno <> 0               If got phone
     C                   exsr      $phone
     C                   eval      h1phone = wkphone
     C                   seton                                        97
     C                   else
     C                   setoff                                       97
     C                   endif                                                  If got phone
      *
     C                   endif                                                  If valid
     C                   endif                                                  If prod mgr
      *
     C                   endif                                                  If farm
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Format telephone number
      *---------------------------------------------------------------
      *
     C     $phone        begsr
      *
     C                   move      *blank        wkarea
     C                   move      *blank        wk3alph
     C                   move      *blank        wk4alph
     C                   move      *blank        wk7alph
     C                   move      *blank        wkphone
      *
     C                   if        mgtlacno <> 0
     C                   move      mgtlacno      wkarea
     C                   endif
      *
     C                   if        mgtlphno <> 0
     C                   movel     mgtlphno      wk7alph
     C                   eval      wk3alph = %subst(wk7alph:1:3)
     C                   eval      wk4alph = %subst(wk7alph:4:4)
     C                   endif
      *
     C                   eval      wkphone = '(' + wkarea + ') ' +
     C                                          wk3alph + '-' + wk4alph
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Retrieve Customer info
      *---------------------------------------------------------------
      *
     C     $cvno         begsr
      *
     C                   z-add     shcvno        h1cvno
      *
      * Retrieve Customer Name
      *
     C                   z-add     shcvno        inajd
     C                   move      no            jdefl
     C                   eval      inat1 = 'C'
      *
     C                   call      'HPJDEV'
     C                   parm                    mgi                            MSG ID'S
     C                   parm                    mgd                            MSG DATA FLD
     C                   parm                    error                          # ERR MSGS
     C                   parm                    inajd                          JDE addR #
     C                   parm                    inat1                          JDE addR TYP
     C                   parm                    jdefl                          error FLAG
     C                   parm      *blank        oualph                         JDE NAME
      *
     C                   if        jdefl = no
     C                   eval      h1cnam = oualph
     C                   else
     C                   eval      h1cnam = 'Unknown'
     C                   endif
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Retrieve Trucker info
      *---------------------------------------------------------------
      *
     C     $ticd         begsr
      *
     C                   move      shticd        h1ticd
      *
     C     shticd        chain     hsp046                             92
     C                   if        *in92 = *off
     C                   eval      h1tinm = titinm
     C                   else
     C                   eval      h1tinm = 'Unknown'
     C                   endif
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Retrieve/print Detail Lines for the BOL
      *---------------------------------------------------------------
      *
     C     $detail       begsr
      *
      * Print blank lines to position for the first line of the detail data.
      *
     C******             do        9
     C                   do        6
     C                   except    blank
     C                   enddo
      *
      * Process all movement detail records for this movement.
      *
     C     shmvsn        setll     hsp085
      *
     C                   dou       *in91 = *on                                  Do detail
     C     shmvsn        reade     hsp085                                 91
     C                   if        *in91 = *off                                 If not EOF
      *
     C                   z-add     sgschd        wkschd
      *
      * Retrieve group info
      *
     C                   exsr      $hgsn
      * Print
     C                   except    l1dtl
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Do detail
      *
     C                   endsr
      /EJECT
P408  *---------------------------------------------------------------
  |   * P408 - SR $habvrec to Perform Print of the Premise ID & Animal Movement
  |   *        Number above the Facility Receipt.
  |   *        *IN93 = *On then Sales Type = 'MRKTS'.
  |   *        *IN94 = *On then Print Premise ID.
  |   *        *IN94 = *Off then Print 'underline' for Premise ID since Premise ID is blank.
  |   *        *IN95 = *On then Print Animal Movement #.
  |   *---------------------------------------------------------------
  |   *
  |  C     $habvrec      begsr
  |  C                   eval      *in94 = *off
  |  C                   eval      *in95 = *off
  |   *
  |  C     shfscd        chain     hsp0181
  |  C                   if        %Found
  |   * Print Premise ID.
  |  C                   if        FIPRCD <> *blank
  |  C                   eval      *in94 = *on                                  Prt Premise ID
  |  C                   else
  |  C                   eval      *in94 = *off                                 Prt Underline PremID
  |  C                   endif
  |   * Print Animal Movement Number if this is Seaboard Foods of Iowa
  |   * Farm Co = 340.
  |  C     FSCOCD        ifeq      '340'                                        sbd of iowa
  |  C                   eval      *in95 = *on                                  Prt Animal Movement
  |  C                   endif
  |  C                   endif
  |   *
  |  C                   except    h2idl
  |   *
P408 C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Retrieve/print the Receiving Facility Receipt
      *---------------------------------------------------------------
      *
     C     $receiving    begsr
      *
      * Print the section heading lines
      *
     C                   except    h2hdr
      *
      * Print blank lines to position for the first detail line.
      *
     C                   do        3
     C                   except    blank
     C                   enddo
      *
      * Process movement detail lines
      *
     C     shmvsn        setll     hsp085
      *
     C                   dou       *in91 = *on                                  Do receiving
     C     shmvsn        reade     hsp085                                 91
     C                   if        *in91 = *off                                 If not EOF
     C                   z-add     sgschd        wkschd
      *
      * Retrieve group info
      *
     C                   exsr      $hgsn
      * Print
     C                   except    l2dtl
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Do receiving
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Retrieve/print the Shipping Farm Receipt
      *---------------------------------------------------------------
      *
     C     $shipping     begsr
      *
      * Print the section heading lines
      *
     C                   except    h3hdr
      *
      * Print blank lines to position for the first line of the detail data.
      *
     C****               do        4
     C                   do        3
     C                   except    blank
     C                   enddo
      *
      * Process movement detail lines
      *
     C     shmvsn        setll     hsp085
      *
     C                   dou       *in91 = *on                                  Do shipping
     C     shmvsn        reade     hsp085                                 91
     C                   if        *in91 = *off                                 If not EOF
      *
     C                   z-add     sgschd        wkschd
      *
      * Retrieve group detail and print
      *
     C                   exsr      $hgsn
     C                   except    l3dtl
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Do shipping
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Retrieve group info
      *---------------------------------------------------------------
      *
     C     $hgsn         begsr
      *
     C                   move      *blank        wkhgcd
     C                   move      *blank        wkblcd
     C                   move      *blank        wkrmcd
      *
     C     sghgsn        chain     hsl034d                            92
     C                   if        *in92 = *on                                  If hit
     C                   else
      *
     C                   movel     hghgcd        wkhgcd
     C                   eval      wkrmcd = %triml(hgrmcd)
      *
     C     ' '           checkr    hgblcd        nbrchar
     C                   eval      start = 5 - nbrchar + 1
     C                   eval      %subst(wkblcd:start) =
     C                             %subst(hgblcd:1:nbrchar)
     C                   endif                                                  If hit
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Initialization
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
     C                   endsr
      /EJECT
      *-------------------------------------------------------------
      * Output specs
      *-------------------------------------------------------------
      *
     Oqprint    e            h1hdr          1  4
     O                       shmvsn        z     32
     O                       h1conm              72
     O                       shmvsn        z     91
      *
     O          e            h1hdr             6
     O                       h1desc              30
     O                                           65 'Cust PO:'
     O                       h1cupono            74
     O                       h1reprint           93
      *
     O          e            h1hdr             8
     O                       wkscmdy       y     36
      *
     O          e            h1hdr            12
     O                       h1cvno        zb    30
     O                       h1cnam         b    47
      *
     O          e            h1hdr            19
     O                       h1ticd         b    27
     O                       h1tinm         b    47
      *
     O          e            h1hdr            28
     O                       h1mmgcd        b    28
     O                       h1mmgnm        b    53
      *
     O          e            h1hdr            30
     O                       h1pmgcd        b    28
     O                       h1pmgnm        b    53
      *
     O          e    97      h1hdr            31
     O                       h1phone        b    41
      *
     O          e            h1hdr            37
     O                       wkfscd        z     20
     O                       wkfsnm              47
      *
      *
      *---------------------------------------------------------------
      * Detail line for printing group data
      *---------------------------------------------------------------
      *
     O          e            l1dtl          2
     O                       wkhgcd              12
     O                       wkblcd              18
     o                                           19 '/'
     O                       wkrmcd              24
     O                       wkschd        k     30
      *
      *
      *---------------------------------------------------------------
      * P408 - Print Premise ID & Animal Movement Number.
      *---------------------------------------------------------------
P408 O          e            h2idl            53
  |  O                                           13 'Premise ID'
  |  O               94      FIPRCD              21
  |  O              N94                          24 '__________'
  |   *
  |  O               95                          47 'Animal Movement Number'
P408 O               95                          58 '__________'
      *
      *
      *---------------------------------------------------------------
      * Receiving Facility Receipt Section--Headings
      *---------------------------------------------------------------
      *
     O          e            h2hdr            57
     O                       shmvsn        z     30
     O*****                  wkscmdy       y     81
     O                       wkscmdy       y     85
      *
     O          e            h2hdr            59
     O                       wkfscd        z     15
     O                       wkfsnm              42
      *
      *---------------------------------------------------------------
      * Receiving Facility Receipt Section--Detail
      *---------------------------------------------------------------
      *
     O          e            l2dtl          2
     O                       wkhgcd              12
     O                       wkblcd              18
     o                                           19 '/'
     O                       wkrmcd              24
      *
      *
      *---------------------------------------------------------------
      * Shipping Farm Receipt Section--Headings
      *---------------------------------------------------------------
      *
     O          e            h3hdr            73
     O                       shmvsn        z     30
     O****                   wkscmdy       y     81
     O                       wkscmdy       y     85
      *
     O          e            h3hdr            75
     O                       wkfscd        z     15
     O                       wkfsnm              42
      *
      *---------------------------------------------------------------
      * Shipping Farm Receipt Section--Detail
      *---------------------------------------------------------------
      *
     O          e            l3dtl          2
     O                       wkhgcd              12
     O                       wkblcd              18
     O                                           19 '/'
     O                       wkrmcd              24
     O                       wkschd        k     30
      *
      *---------------------------------------------------------------
      * Print a blank line
      *---------------------------------------------------------------
      *
     O          e            blank          1
      /EJECT
