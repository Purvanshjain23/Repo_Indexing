/*****************************************************************   */
/*  PROGRAM NUMBER --> HP226CL                                       */
/*  PROGRAM NAME ----> DATAMART FIN: EOP PROCESSING                  */
/*  PROGRAMMER ------> LEANNE FEDOR                                  */
/*  DATE ------------> 08/26/03                                      */
/*                                                                   */
/*  PROGRAM PURPOSE -> BUILD THE COST DETAIL FILES FOR COGNOS. THESE */
/*                     FILES WILL ONLY INCLUDE OPEN GROUPS FOR       */
/*                     NUR/FIN ONLY.  THE FILES BUILT ARE HPPF035    */
/*                     AND HPPF121.                                  */
/*********************************************************************/
/* MODIFICATIONS                                                     */
/* DATE      PROGRAMMER                                              */
/* 09/25/03  BARB GUTIERREZ                                          */
/*           PASS IN PARM OF PERIOD END DATE FROM DATA AREA DAINF    */
/*           RETRIEVED IN PROGRAM BIBLDCD$.                          */
/* 05/12/06  BARB GUTIERREZ                                          */
/*           PASS IN COMPANY NUMBER AND ADD TO OPEN QUERY. E5752     */
/*******************************************************************/

             PGM        PARM(&DAEPDT &COCD)

             DCL        VAR(&QDATA)  TYPE(*CHAR) LEN(111)
             DCL        VAR(&LDEPDT) TYPE(*DEC) LEN(8)
             DCL        VAR(&DAEPDT) TYPE(*CHAR) LEN(8)
             DCL        VAR(&COCD) TYPE(*CHAR) LEN(3)        /* E5752 */

             CHGVAR     VAR(&LDEPDT) VALUE(&DAEPDT)

/*-------------------------------------------------------------------*/
/*  SETUP OPNQRY STATEMENT                                           */
/*-------------------------------------------------------------------*/
/*  THE OPEN QUERY WILL:                                             */
/*         1) ONLY PROCESS COMPANY BEING PASSED IN   (E5752)         */
/*         2) OMIT ALL "BGF" PRODUCTION PHASE GROUPS                 */
/*         3) OMIT ALL HOG GROUPS WITH A STATUS OF VOIDED            */
/*         4) SELECT ONLY GROUPS THAT:                               */
/*             A) HAVE AN CLOSED DATE GREATER THAN THE EOP DATE OR   */
/*             B) HAVE A CLOSED DATE OF ZERO (IE: ARE STILL OPEN)    */

             CHGVAR     VAR(&QDATA) VALUE(' ')
             CHGVAR     VAR(%SST(&QDATA 1 10)) VALUE('HGCOCD +
                          *EQ')
             CHGVAR     VAR(%SST(&QDATA 12 3)) VALUE(&COCD)

             CHGVAR     VAR(%SST(&QDATA 16 23)) VALUE('*AND HGPPCD +
                          *NE "BGF  "')

             CHGVAR     VAR(%SST(&QDATA 40 20)) VALUE('*AND HGGSCD +
                          *NE "VD"')

             CHGVAR     VAR(%SST(&QDATA 61 16)) VALUE('*AND (HGCLDT +
                          *GT')
             CHGVAR     VAR(%SST(&QDATA 78 8)) VALUE(&LDEPDT)
             CHGVAR     VAR(%SST(&QDATA 87 14)) VALUE('*OR HGCLDT +
                          *EQ')
             CHGVAR     VAR(%SST(&QDATA 102 8)) VALUE(00000000)
             CHGVAR     VAR(%SST(&QDATA 110 1)) VALUE(')')


             OVRDBF     FILE(HSP034) SHARE(*YES)
             OPNQRYF    FILE((HSP034)) QRYSLT(&QDATA) KEYFLD(*FILE) +
                          SEQONLY(*YES)

             CALL       PGM(HP226) PARM(&COCD)    /* E5752 */

             CLOF       OPNID(HSP034)
             DLTOVR     FILE(HSP034)

             ENDPGM

