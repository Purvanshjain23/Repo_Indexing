     A*%%TS  SD  20100604  152536  ISLRAMS     REL-V6R1M0  5761-WDS
     A*****************************************************************
     A*  SYSTEM:        TRIUMPH FOODS
     A*  DISPLAY FILE:  TFD4450
     A*  TITLE:         SPECIFY PRODUCT SPLIT LISTING OPTIONS
     A*  PROGRAMMER:    LEANNE RAMSEY
     A*  DATE:          10/10/06
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/TFPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CF03(03 'EXIT')
     A                                      CF04(04 'PROMPT')
     A                                      CF08(08 'SUBMIT')
     A                                      CF17(17 'PRINT OPTIONS')
     A                                      CF20(20 'SPOOL FILES')
     A*
     A*----------------------------------------------------------------
     A* RECORD FORMAT ONE
     A*----------------------------------------------------------------
     A          R TF4450R1
     A*%%TS  SD  20100604  152536  ISLRAMS     REL-V6R1M0  5761-WDS
     A                                      WINDOW(TF4450EC)
     A                                      TEXT('INPUT SCREEN')
     A                                      SETOF(31)
     A                                      SETOF(32)
     A                                      SETOF(33)
     A                                      SETOF(34)
     A                                      SETOF(35)
     A                                      SETOF(36)
     A                                      SETOF(51)
     A                                      SETOF(53)
     A                                      SETOF(54)
     A                                      SETOF(55)
     A                                      SETOF(56)
     A                                      OVERLAY
     A                                      RTNCSRLOC(&RCD &FLD)
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  1REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 17'SPECIFY PRODUCT SPLIT LISTING OPTI-
     A                                      ONS'
     A                                      DSPATR(HI)
     A                                  4  1'Cut-off effective date ....'
     A            D1CMDY    R        B  4 31REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A  32                                  DSPATR(PC)
     A  32                                  DSPATR(RI)
     A N32                                  DSPATR(HI)
     A                                  4 43'(Blank for all)'
     A                                  5  1'Product code ..............'
     A                                  5 29'?'
     A            D1PRCD    R        B  5 31REFFLD(TFREF/@@PRCD *LIBL/TFPREF)
     A                                      EDTCDE(M)
     A  31
     AO 51                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A N31                                  DSPATR(HI)
     A                                  5 43'(Blank for all)'
     A                                  7  1'Item structure type code ..'
     A                                  7 29'?'
     A            D1ISTYCD  R        B  7 31REFFLD(TFREF/@@ISTYCD *LIBL/TFPREF)
     A                                      EDTCDE(K)
     A  33
     AO 53                                  DSPATR(PC)
     A  33                                  DSPATR(RI)
     A N33                                  DSPATR(HI)
     A                                  7 43'(Blank for all)'
     A                                  8  1'TF class group code .......'
     A                                  8 29'?'
     A            D1TFCGCD  R        B  8 31REFFLD(TFREF/@@TFCGCD *LIBL/TFPREF)
     A  34
     AO 54                                  DSPATR(PC)
     A  34                                  DSPATR(RI)
     A N34                                  DSPATR(HI)
     A                                  8 43'(Blank for all)'
     A                                  9  1'TF classification code ....'
     A                                  9 29'?'
     A            D1TFCLCD  R        B  9 31REFFLD(TFREF/@@TFCLCD *LIBL/TFPREF)
     A  35
     AO 55                                  DSPATR(PC)
     A  35                                  DSPATR(RI)
     A N35                                  DSPATR(HI)
     A                                  9 43'(Blank for all)'
     A                                 10  1'Active/inactive status ....'
     A                                 10 29'?'
     A            D1AIST    R        B 10 31REFFLD(TFREF/@@AIST *LIBL/TFPREF)
     A  36
     AO 56                                  DSPATR(PC)
     A  36                                  DSPATR(RI)
     A N36                                  DSPATR(HI)
     A                                 10 43'(Blank for all)'
     A*
     A*----------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*----------------------------------------------------------------
     A          R TF4450K1
     A*%%TS  SD  20060911  154226  ISLRAMS     REL-V5R3M0  5722-WDS
     A                                      WINDOW(TF4450EC)
     A                                      TEXT('COMMAND KEYS')
     A                                      OVERLAY
     A  43                             12  2'Message program failed. Call IS.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 13  1'F3=Exit'
     A                                 13  9'F4=Prompt'
     A                                 13 19'F8=Submit'
     A                                 13 30'F17=Print Options'
     A                                 13 48'F20=Spool Files'
      *
      *----------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *----------------------------------------------------------------
     A          R TF4450ES
     A                                      TEXT('Error message subfile')
     A                                      SFL SFLMSGRCD(13)
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
     A*
     A*----------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT WITH WINDOW DEFINITION
     A*----------------------------------------------------------------
     A          R TF4450EC                  SFLCTL(TF4450ES)
     A*%%TS  SD  20000628  103649  ISLRAMS     REL-V4R2M0  5769-PW1
     A                                      TEXT('Error message subfile')
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A                                      WINDOW(*DFT 14 65)
     A                                      WDWBORDER((*DSPATR RI) (*CHAR '    -
     A                                          '))
     A            MSGPGM                    SFLPGMQ(10)
     A*
     A*----------------------------------------------------------------
     A* ASSUME FORMAT - REQUIRED FOR WINDOWS
     A*----------------------------------------------------------------
     A*
     A          R ASSUME
     A*%%TS  SD  20000627  095625  ISLRAMS     REL-V4R2M0  5769-PW1
     A                                      TEXT('ASSUME FORMAT FOR WINDOWS')
     A                                      ASSUME
     A                                      OVERLAY
     A                                 24 79' '
