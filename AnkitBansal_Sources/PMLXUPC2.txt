/***************************************************************** */
/*                                                                 */
/*  PROGRAM NUMBER:  PMLXUPC2                                      */
/*  PROGRAM NAME  :  PRE-PURGE LARGE FILES PRIOR TO ADDING THEM    */
/*                   TO THE JOB SCHEDULER PURGE JOB                */
/*                                                                 */
/*  PROGRAMMER:      LARA BUSER                                    */
/*  CREATE DATE:     03/11/2009                                    */
/*                                                                 */
/*  CALLED FROM PMLXUPC, EXECUTES CPYF THEN CALLS PML2UPC          */
/*  THIS PROGRAM MANUALLY PURGES THE PASSED IN FILE BASED ON       */
/*       PARAMETERS STORED IN PMFECPP-WF PREPURGE CONTROL.         */
/*       THE SAVE WILL BE ACCOMPLISHED BY EXECUTING A CPYF COMMAND */
/*       TO THE PURGE LIBRARY AND THE PURGE WILL BE ACCOMPLISHED   */
/*       BY CALLING PML2UPC2 WHICH EXECUTES A SEQUEL DELETE COMMAND*/
/*                                                                 */
/*******************************************************************/
 PMLXUPC2:   PGM        PARM(&RTNCODE &PURGEDATE &PCGCODE &PCGLIB +
                          &FILENAME &PCFIELD &DATEFMT &TOLIB)

             DCL        VAR(&RTNCODE) TYPE(*CHAR) LEN(7)
             DCL        VAR(&PURGEDATE) TYPE(*DEC) LEN(7 0)
             DCL        VAR(&PCGCODE) TYPE(*CHAR) LEN(3)
             DCL        VAR(&PCGLIB) TYPE(*CHAR) LEN(10) /* FROM LIB */
             DCL        VAR(&PCAPPLIC) TYPE(*CHAR) LEN(6)
             DCL        VAR(&FILENAME) TYPE(*CHAR) LEN(10)
             DCL        VAR(&PCFLGL) TYPE(*CHAR) LEN(10)
             DCL        VAR(&PCFTAPE) TYPE(*CHAR) LEN(10)
             DCL        VAR(&PCFIELD) TYPE(*CHAR) LEN(6)
             DCL        VAR(&DATEFMT) TYPE(*CHAR) LEN(10)
             DCL        VAR(&TOLIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&TOMBR) TYPE(*CHAR) LEN(10)
             DCL        VAR(&PREFIX) TYPE(*CHAR) LEN(4)
             DCL        VAR(&CYMDDATE) TYPE(*CHAR) LEN(7)
             DCL        VAR(&DATE) TYPE(*CHAR) LEN(6)
             DCL        VAR(&PRGDATE) TYPE(*CHAR) LEN(7)
             DCL        VAR(&PURGEDATEA) TYPE(*CHAR)  LEN(8)

             RTVJOBA    CYMDDATE(&CYMDDATE)
             CHGVAR     VAR(&DATE) VALUE(%SST(&CYMDDATE 2 6))
             CHGVAR     VAR(&PREFIX) VALUE('Z' *TCAT &PCGCODE)
             CHGVAR     VAR(&TOMBR) VALUE(&PREFIX *TCAT &DATE)
             CHGVAR     VAR(&PCFTAPE) VALUE(&FILENAME *TCAT 'PRG')
             CHGVAR     VAR(&PRGDATE) VALUE(&PURGEDATE)
             CHGVAR     VAR(&PURGEDATEA) VALUE(&PRGDATE)
             IF         COND(&DATEFMT *NE '      ') THEN(DO)
                CVTDAT     DATE(&PRGDATE) TOVAR(&PURGEDATEA) +
                            FROMFMT(*CYMD) TOFMT(&DATEFMT) TOSEP(*NONE)
                MONMSG     MSGID(CPF0000) EXEC(DO)
                CHGVAR     VAR(&RTNCODE) VALUE('CVTDATERR')
                SNDPGRMSG  TOPGR(PRKPURGE) MSG('Convert Date for '   +
                           *CAT &FILENAME *CAT ' in library ' *CAT +
                            &PCGLIB *CAT ' ended in error. Purge for +
                            this file was halted.') CONFMSG(*NO)
                CHGJOB     LOG(4 00 *SECLVL)
                GOTO       CMDLBL(END)
                ENDDO      /* END MONMSG */
             ENDDO      /* END IF FMT = BLANKS */
/********************************************************************/
/* PURGE FILES                                                      */
/********************************************************************/
 CPYF:       CPYF       FROMFILE(&PCGLIB/&FILENAME) +
                          TOFILE(&TOLIB/&PCFTAPE) FROMMBR(*FIRST) +
                          TOMBR(&TOMBR) MBROPT(*ADD) CRTFILE(*YES) +
                          INCREL((*IF &PCFIELD *LE &PURGEDATEA))

             MONMSG     MSGID(CPF3213 CPF2888) EXEC(DO)
                CHGPF      FILE(&TOLIB/&PCFTAPE) MAXMBRS(*NOMAX) +
                             SIZE(*NOMAX)
                GOTO       CMDLBL(CPYF)
                ENDDO      /* END MONMSG */
             MONMSG     MSGID(CPF0000) EXEC(DO)
               CHGVAR     VAR(&RTNCODE) VALUE('CPYERR ')
               SNDPGRMSG  TOPGR(PRKPURGE) MSG('Copy file for ' *CAT +
                            &FILENAME *CAT ' in library ' *CAT +
                            &PCGLIB *CAT ' ended in error. Purge for +
                            this file was halted.') CONFMSG(*NO)
               CHGJOB     LOG(4 00 *SECLVL)
               GOTO       CMDLBL(END)
               ENDDO      /* END MONMSG */

 /**GE:      CALL       PGM(PML2UPC) PARM(&RTNCODE &PURGEDATE +
                          &PCGLIB &FILENAME &PCFIELD &DATEFMT)*****/
 PURGE:      CALL       PGM(PML2UPC) PARM(&RTNCODE &PURGEDATE +
                          &PCGCODE &PCGLIB &PCAPPLIC &FILENAME +
                          &PCFLGL &PCFTAPE &PCFIELD &DATEFMT)


             IF         COND(&RTNCODE *NE '       ') THEN(DO)
               SNDPGRMSG  TOPGR(PRKPURGE) MSG('SQL Delete for ' *CAT +
                            &FILENAME *CAT ' in library ' *CAT +
                            &PCGLIB *CAT ' ended in error. Delete +
                            for this file was halted.') CONFMSG(*NO)
               CHGJOB     LOG(4 00 *SECLVL)
               ENDDO      /* END IF RTNCODE IS NOT BLANK */

/********************************************************************/
 END:        ENDPGM
/********************************************************************/
