/***************************************************************** */
/*                                                                 */
/*  SYSTEM:          TRIUMPH FOODS                                 */
/*  PROGRAM NUMBER:  TF406CL                                       */
/*  PROGRAM NAME:    CASH DISTRIBUTION BALANCE REPORT              */
/*  PROGRAMMER:      LEANNE FEDOR                                  */
/*  CREATE DATE:     10/05/05                                      */
/*                                                                 */
/*                                                                 */
/*******************************************************************/

             PGM

             DCL        VAR(&QDATA) TYPE(*CHAR) LEN(110)

             DCL        VAR(&OUTQ) TYPE(*CHAR) LEN(10)
             DCL        VAR(&HOLD) TYPE(*CHAR) LEN(4)
             DCL        VAR(&SAVE) TYPE(*CHAR) LEN(4)
             DCL        VAR(&COPY) TYPE(*DEC)  LEN(1)

             DCL        VAR(&LDFWEDT)   TYPE(*DEC) LEN(8)
             DCL        VAR(&LDTDT)     TYPE(*DEC) LEN(8)
             DCL        VAR(&LDIECD)    TYPE(*CHAR) LEN(1)
             DCL        VAR(&LDRTCD)    TYPE(*CHAR) LEN(1)

             CHGVAR     VAR(&LDFWEDT)   VALUE(%SST(*LDA 21 8))
             CHGVAR     VAR(&LDTDT)     VALUE(%SST(*LDA 80 8))
             CHGVAR     VAR(&LDIECD)    VALUE(%SST(*LDA 29 1))
             CHGVAR     VAR(&LDRTCD)    VALUE(%SST(*LDA 56 1))

/*---------------------------------------------------------------------*/
/* SET UP QUERY FOR CASH DISTRIBUTION BALANCE RECORDS                  */
/*---------------------------------------------------------------------*/

             CHGVAR     VAR(&QDATA) VALUE(' ')

/* START WEEK-ENDING DATE                                                  */

             CHGVAR     VAR(%SST(&QDATA 1 10)) VALUE('CBWEDT *GE')
             CHGVAR     VAR(%SST(&QDATA 12 8)) VALUE(&LDFWEDT)

             CHGVAR     VAR(%SST(&QDATA 21 15)) VALUE('*AND CBWEDT *LE')
             CHGVAR     VAR(%SST(&QDATA 37 8)) VALUE(&LDTDT)

/* ONLY INCLUDE RECORDS BUILT IN A "FINAL" RUN OF THE WEEKLY REVENUE      */
/* CLOSE. ALICE DOES NOT WANT ANY "PRELIMINARY" DATA ON THIS REPORT.      */

             CHGVAR     VAR(%SST(&QDATA 46 19)) VALUE('*AND CBPFCD +
                          *EQ "F"')

/* IF ONLY THE 'EXEMPT' RECORDS                                          */

             IF         COND(&LDIECD *EQ 'E') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 66 21)) VALUE('*AND CBRCEXFL +
                          *EQ "Y"')
             ENDDO

/* IF ONLY THE 'NON-EXEMPT' RECORDS                                      */

             IF         COND(&LDIECD *EQ 'N') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 88 21)) VALUE('*AND CBRCEXFL +
                          *EQ "N"')
             ENDDO

/*---------------------------------------------------------------------*/
/* OVERRIDE FILE KEYS AND EXTRACT CASH DISTRIBUTION BALANCE DATA       */
/*---------------------------------------------------------------------*/

             OVRDBF     FILE(TFL011D) SHARE(*YES)
             OPNQRYF    FILE((TFL011D)) QRYSLT(&QDATA) KEYFLD(*FILE) +
                          SEQONLY(*YES)


/*---------------------------------------------------------------------*/
/* SET UP QUERY FOR CASH DISTRIBUTION DETAIL RECORDS                   */
/*---------------------------------------------------------------------*/

             CHGVAR     VAR(&QDATA) VALUE(' ')

/* WEEK-ENDING DATE                                                       */

             CHGVAR     VAR(%SST(&QDATA 1 9)) VALUE('CDDDT *LE')
             CHGVAR     VAR(%SST(&QDATA 11 8)) VALUE(&LDTDT)

/* IF ONLY THE 'EXEMPT' RECORDS                                          */

             IF         COND(&LDIECD *EQ 'E') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 20 21)) VALUE('*AND CDRCEXFL +
                          *EQ "Y"')
             ENDDO

/* IF ONLY THE 'NON-EXEMPT' RECORDS                                      */

             IF         COND(&LDIECD *EQ 'N') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 42 21)) VALUE('*AND CDRCEXFL +
                          *EQ "N"')
             ENDDO

/*---------------------------------------------------------------------*/
/* OVERRIDE FILE KEYS AND EXTRACT CASH DISTRIBUTION DETAIL DATA        */
/*---------------------------------------------------------------------*/

             OVRDBF     FILE(TFL013B) SHARE(*YES)
             OPNQRYF    FILE((TFL013B)) QRYSLT(&QDATA) KEYFLD(*FILE) +
                          SEQONLY(*YES)

/*----------------------------------------------------------------*/
/* SET PARAMETERS FROM LDA FOR LISTINGS                           */
/*----------------------------------------------------------------*/

 OUTQ:       CHGVAR     VAR(&OUTQ) VALUE(%SST(*LDA 401 10))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(*JOB))

 HOLD:       CHGVAR     VAR(&HOLD) VALUE(%SST(*LDA 411 4))
             IF         COND(&HOLD *NE '*YES' *AND &HOLD *NE '*NO ') +
                          THEN(CHGVAR VAR(&HOLD) VALUE(*NO))

 SAVE:       CHGVAR     VAR(&SAVE) VALUE(%SST(*LDA 415 4))
             IF         COND(&SAVE *NE '*YES' *AND &SAVE *NE '*NO ') +
                          THEN(CHGVAR VAR(&SAVE) VALUE(*NO))

 COPY:       CHGVAR     VAR(&COPY) VALUE(%SST(*LDA 419 1))
             IF         COND(&COPY = 0) THEN(CHGVAR VAR(&COPY) +
                          VALUE(1))

/*---------------------------------------------------------------------*/
/* CALL THE APPROPRIATE PRINT PROGRAM                                  */
/*---------------------------------------------------------------------*/

             OVRPRTF    FILE(PRINT198) OUTQ(&OUTQ) COPIES(&COPY) +
                          HOLD(&HOLD) SAVE(&SAVE)

/* SALES EXPENSE                                                       */

             IF         COND(&LDRTCD *EQ 'S') THEN(DO)
             CALL       PGM(TF606)
             ENDDO

/* CASH DISTRIBUTION                                                   */

             IF         COND(&LDRTCD *EQ 'C') THEN(DO)
             CALL       PGM(TF607)
             ENDDO


             CLOF       OPNID(TFL011D)
             CLOF       OPNID(TFL013B)
             DLTOVR     FILE(*ALL)

             ENDPGM

