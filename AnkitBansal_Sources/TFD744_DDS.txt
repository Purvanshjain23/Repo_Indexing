     A*%%TS  SD  20080910  111511  ISLRAMS     REL-V5R4M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        TRIUMPH FOODS
     A*  DISPLAY FILE:  TFD744
     A*  TITLE:         VIEW UPLOADED STANDARD COSTS
     A*  PROGRAMMER:    LEANNE RAMSEY
     A*  DATE:          10/09/07
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/TFPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A                                      CF05(05 'REFRESH')
     A                                      CF07(07 'LISTING')
     A                                      CF13(13 'FOLD/DROP')
     A                                      CF17(17 'PRINT OPTIONS')
     A                                      CF20(20 'WRKSPLF')
     A*
     A*-------------------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*-------------------------------------------------------------------------
     A          R TF744S1                   SFL
     A*%%TS  SD  20071009  151904  ISLRAMS     REL-V5R4M0  5722-WDS
     A  85                                  SFLNXTCHG
     A            HDRRN1    R        H      REFFLD(@@RRN)
     A            D1MDY          6Y 0O  7  2EDTWRD('  /  /  ')
     A            D1ITEM    R        O  7 12REFFLD(U1REC/U1ITEM *LIBL/TFP244)
     A                                      EDTCDE(M)
     A            D1COST    R        O  7 20REFFLD(U1REC/U1COST *LIBL/TFP244)
     A                                      EDTCDE(K)
     A            D1OTHER   R        O  7 42REFFLD(U1REC/U1OTHER *LIBL/TFP244)
     A                                      EDTCDE(K)
     A                                  8 39'Desc:'
     A                                      DSPATR(HI)
     A            D1ITDS        30A  O  8 45COLOR(BLU)
     A*
     A*-------------------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*-------------------------------------------------------------------------
     A          R TF744C1                   SFLCTL(TF744S1)
     A*%%TS  SD  20080910  111511  ISLRAMS     REL-V5R4M0  5722-WDS
     A                                      SFLSIZ(0050)
     A                                      SFLPAG(0007)
     A N82                                  ROLLUP(25 'PAGE FORWARD')
     A                                      OVERLAY
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A  78                                  SFLFOLD(CF13)
     A  79                                  SFLDROP(CF13)
     A                                      SFLMODE(&FOLD)
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            FOLD           1A  H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 24'VIEW UPLOADED STANDARD COSTS'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USR)
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@JOB)
     A                                      DSPATR(HI)
     A                                  4 13'Item'
     A                                      DSPATR(HI)
     A                                  4 20'Std Meat/Pkg/Inc Lbr'
     A                                      DSPATR(HI)
     A                                  4 42'Yield % or'
     A                                      DSPATR(HI)
     A                                  5  4'Date'
     A                                      DSPATR(HI)
     A                                  5 13'Code'
     A                                      DSPATR(HI)
     A                                  5 20'Ingredient Cost'
     A                                      DSPATR(HI)
     A                                  5 42'Total Labor Cost'
     A                                      DSPATR(HI)
     A            C1ITCD    R        B  6 12REFFLD(POAXCPDWL/F2 *LIBL/POAXCPDWL)
     A                                      DSPATR(HI)
     A                                      EDTCDE(M)
     A*
     A*-------------------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*-------------------------------------------------------------------------
     A          R TF744K1
     A*%%TS  SD  20071009  152736  ISLRAMS     REL-V5R4M0  5722-WDS
     A  43                             21  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 22  2'F3=Exit'
     A                                 22 11'F5=Refresh'
     A                                 22 23'F7=Listing'
     A                                 22 35'F13=Fold/Unfold'
     A                                 22 52'F17=Print Options'
     A                                 23 52'F20=Spool Files'
      *
     A*-------------------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
     A*-------------------------------------------------------------------------
     A          R TF744ES                   SFL SFLMSGRCD(24)
      *
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
     A*-------------------------------------------------------------------------
      * ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*-------------------------------------------------------------------------
     A          R TF744EC                   SFLCTL(TF744ES)
      *
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
