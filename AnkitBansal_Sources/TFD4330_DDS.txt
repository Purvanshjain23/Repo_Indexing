     A*%%TS  SD  20050315  132952  ISLRAMS     REL-V5R2M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        TRIUMPH FOODS
     A*  DISPLAY FILE:  TFD4330
     A*  TITLE:         SPECIFY INVOICE LISTING OPTIONS
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  DATE:          03/15/05
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/TFPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CF03(03 'EXIT')
     A                                      CF04(04 'PROMPT')
     A                                      CF08(08 'SUBMIT')
     A*
     A*----------------------------------------------------------------
     A* RECORD FORMAT ONE
     A*----------------------------------------------------------------
     A          R TF4330R1
     A*%%TS  SD  20050315  132952  ISLRAMS     REL-V5R2M0  5722-WDS
     A                                      WINDOW(TF4330EC)
     A                                      TEXT('INPUT SCREEN')
     A                                      SETOF(30)
     A                                      SETOF(32)
     A                                      SETOF(33)
     A                                      SETOF(34)
     A                                      SETOF(35)
     A                                      SETOF(37)
     A                                      SETOF(38)
     A                                      SETOF(39)
     A                                      SETOF(50)
     A                                      SETOF(54)
     A                                      SETOF(55)
     A                                      SETOF(57)
     A                                      OVERLAY
     A                                      RTNCSRLOC(&RCD &FLD)
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  1REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 14'SPECIFY INVOICE LISTING OPTIONS'
     A                                      DSPATR(HI)
     A                                  3  1'Include posted invoices ..'
     A                                  3 28'?'
     A            D1YNFL    R        B  3 30REFFLD(@@FLAG)
     A  30
     AO 50                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A N30                                  DSPATR(HI)
     A                                  4  1'From invoice date ........'
     A            D1FMDY    R        B  4 30REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A  32                                  DSPATR(PC)
     A  32                                  DSPATR(RI)
     A N32                                  DSPATR(HI)
     A                                  4 43'(Blank for all)'
     A                                  5  1'To invoice date ..........'
     A            D1TMDY    R        B  5 30REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A  33                                  DSPATR(PC)
     A  33                                  DSPATR(RI)
     A N33                                  DSPATR(HI)
     A                                  5 43'(Blank for all)'
     A                                  6  1'Invoice status code ......'
     A                                  6 28'?'
     A            D1INSTCD  R        B  6 30REFFLD(@@STCD)
     A  35
     AO 55                                  DSPATR(PC)
     A  35                                  DSPATR(RI)
     A N35                                  DSPATR(HI)
     A                                  6 43'(Blank for all)'
     A                                  7  1'Invoice frequency code ...'
     A                                  7 28'?'
     A            D1CFQCD   R        B  7 30REFFLD(TFREF/@@CFQCD *LIBL/TFPREF)
     A  37
     AO 57                                  DSPATR(PC)
     A  37                                  DSPATR(RI)
     A N37                                  DSPATR(HI)
     A                                  7 43'(Blank for all)'
     A                                  8  1'Due date .................'
     A            D1DMDY    R        B  8 30REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A  38                                  DSPATR(PC)
     A  38                                  DSPATR(RI)
     A N38                                  DSPATR(HI)
     A                                  8 43'(Blank for all)'
     A                                  9  1'Posted date ..............'
     A            D1PMDY    R        B  9 30REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A  39                                  DSPATR(PC)
     A  39                                  DSPATR(RI)
     A N39                                  DSPATR(HI)
     A                                  9 43'(Blank for all)'
     A                                 10  1'Invoice number ...........'
     A                                 10 28'?'
     A            D1INSN    R        B 10 30REFFLD(@@INSN)
     A                                      EDTCDE(M)
     A  34
     AO 54                                  DSPATR(PC)
     A  34                                  DSPATR(RI)
     A N34                                  DSPATR(HI)
     A                                 10 43'(Blank for all)'
     A*
     A*----------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*----------------------------------------------------------------
     A          R TF4330K1
     A*%%TS  SD  20050315  132952  ISLRAMS     REL-V5R2M0  5722-WDS
     A                                      WINDOW(TF4330EC)
     A                                      TEXT('COMMAND KEYS')
     A                                      OVERLAY
     A  43                             11  1'Message program failed. Call IS.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 12  1'F3=Exit'
     A                                 12 10'F4=Prompt'
     A                                 12 21'F8=Submit'
      *
      *----------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *----------------------------------------------------------------
     A          R TF4330ES
     A                                      TEXT('Error message subfile')
     A                                      SFL SFLMSGRCD(13)
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
     A*
     A*----------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT WITH WINDOW DEFINITION
     A*----------------------------------------------------------------
     A          R TF4330EC                  SFLCTL(TF4330ES)
     A*%%TS  SD  20000628  103649  ISLRAMS     REL-V4R2M0  5769-PW1
     A                                      TEXT('Error message subfile')
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A                                      WINDOW(*DFT 14 76)
     A                                      WDWBORDER((*DSPATR RI) (*CHAR '    -
     A                                          '))
     A            MSGPGM                    SFLPGMQ(10)
     A*
     A*----------------------------------------------------------------
     A* ASSUME FORMAT - REQUIRED FOR WINDOWS
     A*----------------------------------------------------------------
     A*
     A          R ASSUME
     A*%%TS  SD  20000627  095625  ISLRAMS     REL-V4R2M0  5769-PW1
     A                                      TEXT('ASSUME FORMAT FOR WINDOWS')
     A                                      ASSUME
     A                                      OVERLAY
     A                                 24 79' '
