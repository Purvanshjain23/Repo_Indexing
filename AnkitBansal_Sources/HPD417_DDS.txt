     A*%%TS  SD  20011227  102238  ISLRAMS     REL-V4R4M0  5769-PW1
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION
     A*  DISPLAY FILE:  HPD417
     A*  TITLE:         WORK WITH RATIONS
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  CREATED:       12/02/94
     A*
     A*****************************************************************
     A* MODIFICATIONS:
     A* 06/18/97  LEANNE FEDOR
     A*           ADDED A FUNCTION KEY FOR PRINT OPTIONS.
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A N99                                  CF04(04 'PROMPT')
     A N99                                  CF05(05 'REFRESH')
     A N99                                  CF06(06 'CREATE')
     A N99                                  CF07(07 'LISTING')
     A N99                                  CF17(17 'PRINT OPTIONS')
     A N99                                  CF20(20 'WRKSPLF')
     A*
     A*---------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP417S1                   SFL
     A*%%TS  SD  20011227  102238  ISLRAMS     REL-V4R4M0  5769-PW1
     A  85                                  SFLNXTCHG
     A            HDRRN1         4Y 0H
     A            DFOPT          2A  B  9  2CHGINPDFT(UL)
     A  30                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A N30                                  DSPATR(HI)
     A  99                                  DSPATR(PR)
     A            DFRNCD    R        O  9  8REFFLD(RNRNCD)
     A            DFRNDS    R        O  9 17REFFLD(RNRNDS)
     A            DFPPCD    R        O  9 41REFFLD(RNPPCD)
     A            DFRTCD    R        O  9 52REFFLD(RNRTCD)
     A            DFRNST    R        O  9 63REFFLD(RNRNST)
     A            DFMAXDAY  R        O  9 72REFFLD(HSRREF/@@MAXDAY *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A*
     A*---------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP417C1                   SFLCTL(HP417S1)
     A*%%TS  SD  20011227  101248  ISLRAMS     REL-V4R4M0  5769-PW1
     A                                      SFLSIZ(0050)
     A                                      SFLPAG(0012)
     A N82N99                               ROLLUP(25 'PAGE FORWARD')
     A                                      SETOF(51)
     A                                      SETOF(52)
     A                                      SETOF(53)
     A                                      RTNCSRLOC(&RCD &FLD)
     A                                      OVERLAY
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            RCD           10A  H
     A            FLD           10A  H
     A                                  1  2'HP417'
     A                                      DSPATR(HI)
     A                                  1 28'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  2 30'WORK WITH RATIONS'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  4  2'2=Revise'
     A                                  4 12'4=Delete'
     A                                  4 22'5=View Ration Additives'
     A                                  4 47'7=Maintain Ration Additives'
     A                                  5 43'?'
     A                                      DSPATR(HI)
     A                                  5 53'?'
     A                                      DSPATR(HI)
     A                                  5 63'?'
     A                                      DSPATR(HI)
     A                                  6 38'Production'
     A                                      DSPATR(HI)
     A                                  6 51'Ration'
     A                                      DSPATR(HI)
     A                                  6 70'Maximum'
     A                                      DSPATR(HI)
     A                                  7  2'Opt'
     A                                      DSPATR(HI)
     A                                  7  8'Ration'
     A                                      DSPATR(HI)
     A                                  7 17'Description'
     A                                      DSPATR(HI)
     A                                  7 41'Phase'
     A                                      DSPATR(HI)
     A                                  7 52'Type'
     A                                      DSPATR(HI)
     A                                  7 61'Status'
     A                                      DSPATR(HI)
     A                                  7 69'Age(Days)'
     A                                      DSPATR(HI)
     A            C1RNCD    R        B  8  8REFFLD(RNRNCD)
     A                                      DSPATR(HI)
     A            C1PPCD    R        B  8 41REFFLD(RNPPCD)
     A                                      DSPATR(HI)
     A  51                                  DSPATR(PC)
     A  99                                  DSPATR(PR)
     A            C1RTCD    R        B  8 52REFFLD(RNRTCD)
     A                                      DSPATR(HI)
     A  52                                  DSPATR(PC)
     A  99                                  DSPATR(PR)
     A            C1RNST    R        B  8 63REFFLD(RNRNST)
     A                                      DSPATR(HI)
     A  53                                  DSPATR(PC)
     A  99                                  DSPATR(PR)
     A            C1MAXDAY  R        B  8 72REFFLD(HSRREF/@@MAXDAY *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A*
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R HP417K1
     A*%%TS  SD  19941202  102319  ISLRAMS     REL-V3R0M5  5763-PW1
     A  43                             21  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 22  2'F3=Exit'
     A N99                             22 11'F4=Prompt'
     A N99                             22 22'F5=Refresh'
     A N99                             22 34'F6=Create'
     A N99                             22 45'F7=Listing'
     A N99                             22 57'F17=Print Options'
     A N99                             23 57'F20=Spool Files'
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *---------------------------------------------------------------
     A          R HP417ES                   SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE CONTROL FORMAT
      *---------------------------------------------------------------
     A          R HP417EC                   SFLCTL(HP417ES)
      *
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
