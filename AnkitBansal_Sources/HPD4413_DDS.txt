     A*%%TS  SD  20000628  103649  ISLRAMS     REL-V4R2M0  5769-PW1
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION
     A*  DISPLAY FILE:  HPD4413
     A*  TITLE:         SPECIFY SOW FARM SIRE LIHES LISTING OPTIONS
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  DATE:          06/28/00
     A*
     A*  NOTE:  THIS IS A POP-UP WINDOW. THE WINDOW DEFINITION IS IN
     A*         RECORD FORMAT HP4413EC
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CF03(03 'EXIT')
     A                                      CF04(04 'PROMPT')
     A                                      CF08(08 'SUBMIT')
     A*
     A*----------------------------------------------------------------
     A* RECORD FORMAT ONE
     A*----------------------------------------------------------------
     A          R HP4413R1
     A*%%TS  SD  20000628  103649  ISLRAMS     REL-V4R2M0  5769-PW1
     A                                      WINDOW(HP4413EC)
     A                                      TEXT('INPUT SCREEN')
     A                                      SETOF(30)
     A                                      SETOF(31)
     A                                      SETOF(50)
     A                                      SETOF(51)
     A                                      OVERLAY
     A                                      RTNCSRLOC(&RCD &FLD)
     A            RCD           10A  H
     A            FLD           10A  H
     A                                  1  8'SPECIFY SOW FARM SIRE LINE LISTING-
     A                                       OPTIONS'
     A                                      DSPATR(HI)
     A                                  3  1'Show current entries only ..'
     A                                  3 30'?'
     A            D1YNFL    R        B  3 33REFFLD(HSRREF/@@FLAG *LIBL/HSPREF)
     A  30
     AO 50                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A N30                                  DSPATR(HI)
     A                                  4  1'Farm site ..................'
     A                                  4 30'?'
     A            D1FSCD    R        B  4 33REFFLD(HSRREF/@@FSCD *LIBL/HSPREF)
     A                                      EDTCDE(M)
     A  31
     AO 51                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A N31                                  DSPATR(HI)
     A                                  4 43'(Blank for all)'
     A*
     A*----------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*----------------------------------------------------------------
     A          R HP4413K1
     A*%%TS  SD  20000605  133618  ISLRAMS     REL-V4R2M0  5769-PW1
     A                                      WINDOW(HP4413EC)
     A                                      TEXT('COMMAND KEYS')
     A                                      OVERLAY
     A  43                              7  2'Message program failed. Call IS.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                  8  2'F3=Exit'
     A                                  8 11'F4=Prompt'
     A                                  8 22'F8=Submit'
      *
      *----------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *----------------------------------------------------------------
     A          R HP4413ES
     A                                      TEXT('Error message subfile')
     A                                      SFL SFLMSGRCD(09)
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
     A*
     A*----------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT WITH WINDOW DEFINITION
     A*----------------------------------------------------------------
     A          R HP4413EC                  SFLCTL(HP4413ES)
     A*%%TS  SD  20000628  103649  ISLRAMS     REL-V4R2M0  5769-PW1
     A                                      TEXT('Error message subfile')
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A                                      WINDOW(*DFT 10 60)
     A                                      WDWBORDER((*DSPATR RI) (*CHAR '    -
     A                                          '))
     A            MSGPGM                    SFLPGMQ(10)
     A*
     A*----------------------------------------------------------------
     A* ASSUME FORMAT - REQUIRED FOR WINDOWS
     A*----------------------------------------------------------------
     A*
     A          R ASSUME
     A*%%TS  SD  20000627  095625  ISLRAMS     REL-V4R2M0  5769-PW1
     A                                      TEXT('ASSUME FORMAT FOR WINDOWS')
     A                                      ASSUME
     A                                      OVERLAY
     A                                 24 79' '
