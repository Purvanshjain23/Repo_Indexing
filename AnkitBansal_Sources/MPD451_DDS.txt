     A*%%TS  SD  20140312  133942  ISLRAMS     REL-V6R1M0  5761-WDS
     A*****************************************************************
     A*  ENVIRONMENT:   PORK DIVISION
     A*  SYSTEM:        AS/400
     A*  DISPLAY FILE:  MPD451
     A*  TITLE:         SPECIFY LIST OF OVERRIDDEN ITEMS
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  CREATED:       04/30/02
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A                                      CF04(04 'PROMPT')
     A                                      CF08(08 'SUBMIT')
     A                                      CF17(17 'PRINT OPTIONS')
     A                                      CF20(20 'WRKSPLF')
     A*
     A*---------------------------------------------------------------
     A* RECORD FORMAT ONE
     A*---------------------------------------------------------------
     A          R MP451R1
     A*%%TS  SD  20140312  133942  ISLRAMS     REL-V6R1M0  5761-WDS
     A                                      SETOF(31)
     A                                      SETOF(32)
     A                                      SETOF(33)
     A                                      SETOF(35)
     A                                      SETOF(36)
     A                                      SETOF(50)
     A                                      OVERLAY
     A                                      RTNCSRLOC(&RCD &FLD)
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM         10A  O  1  2DSPATR(HI)
     A                                  1 26'MP2 INTERFACE TO JDE'
     A                                      DSPATR(HI)
     A            SDUSR         10A  O  1 70DSPATR(HI)
     A                                  2 16'SPECIFY LIST OF OVERRIDDEN ITEMS O-
     A                                      N INVOICES'
     A                                      DSPATR(HI)
     A            SDJOB         10A  O  2 70DSPATR(HI)
     A                                  3 15'(Only Includes Invoices Already Po-
     A                                      sted to JDE)'
     A                                  5  2'ENTER:'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  6  7'Batch number .......'
     A            D1BATNO   R        B  6 28REFFLD(MPREC/@@BATNO PRKPLIB/MP2REF)
     A                                      EDTCDE(M)
     A N32                                  DSPATR(HI)
     A  32                                  DSPATR(RI)
     A  32                                  DSPATR(PC)
     A                                  8 11'-- OR --'
     A                                      DSPATR(HI)
     A                                 10  7'Accounting company .'
     A                                 10 28'?'
     A            D1ACONO   R        B 10 30REFFLD(PHREC/PHACONO *LIBL/MPP102)
     A N33                                  DSPATR(HI)
     A  33
     AO 50                                  DSPATR(PC)
     A  33                                  DSPATR(RI)
     A                                      EDTCDE(K)
     A            D1ACONM       30A  O 10 39
     A                                 11  7'From batch date ....'
     A            D1FMDY         6Y 0B 11 30EDTWRD('  /  /  -')
     A N35                                  DSPATR(HI)
     A  35                                  DSPATR(PC)
     A  35                                  DSPATR(RI)
     A                                 12  7'To batch date ......'
     A            D1TMDY         6Y 0B 12 30EDTWRD('  /  /  -')
     A N36                                  DSPATR(HI)
     A  36                                  DSPATR(PC)
     A  36                                  DSPATR(RI)
     A                                 13  7'JDE vendor number ..'
     A            D1JDVNNO  R        B 13 30REFFLD(PHREC/PHJDVNNO *LIBL/MPP102)
     A N31                                  DSPATR(HI)
     A  31                                  DSPATR(RI)
     A  31                                  DSPATR(PC)
     A                                 13 41'(Blank for All)'
     A            D1JDVNNM  R        O 14 30REFFLD(PHREC/PHJDVNNM *LIBL/MPP102)
     A*
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R MP451K1
     A*%%TS  SD  20140312  133942  ISLRAMS     REL-V6R1M0  5761-WDS
     A  43                             21  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A                                 23 11'F4=Prompt'
     A                                 23 22'F8=Submit'
     A                                 23 33'F17=Print Options'
     A                                 23 52'F20=Spool Files'
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *---------------------------------------------------------------
     A          R MP451ES                   SFL SFLMSGRCD(24)
      *
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
     A*---------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*---------------------------------------------------------------
     A*
     A          R MP451EC                   SFLCTL(MP451ES)
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
