     A*%%TS  SD  20030120  103042  ISLRAMS     REL-V5R1M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION
     A*  DISPLAY FILE:  HPD165
     A*  TITLE:         CREATE MULTIPLE SCHEDULED SALES MOVEMENTS
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  DATE:          11/24/99
     A*
     A*****************************************************************
     A* INDICATORS
     A* 90  PROTECT ALL FIELDS. USER NEEDS TO SEE THE SYSTEM ASSIGNED
     A*     MOVEMENT NUMBER FOR EACH DETAIL LINE. SO, AFTER THEY ACCEPT
     A*     WE MUST REDISPLAY THE SCREEN WITH ALL FIELDS PROTECTED.
     A* 98  MAKE FIELDS NON-DISPLAY
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A N90                                  CF04(04 'PROMPT')
     A                                      CF05(05 'REFRESH')
     A N90                                  CF09(09 'ACCEPT')
     A                                      CA12(12 'PREVIOUS')
     A                                      CF13(13 'FOLD/DROP')
     A*---------------------------------------------------------------
     A* SUBFILE FORMAT 1
     A*---------------------------------------------------------------
     A*
     A          R HP165S1                   SFL
     A*%%TS  SD  20020717  140318  ISLRAMS     REL-V5R1M0  5722-WDS
     A  85                                  SFLNXTCHG
     A            HDRRN1         4Y 0H
     A            HDHGSN    R        H      REFFLD(HGHGSN)
     A            HDFSBO    R        H      REFFLD(HGFSBO)
     A            HDSCDT    R        H      REFFLD(@@DT08)
     A            HDSCKLDT  R        H      REFFLD(@@DT08)
     A            DFFSCD    R        B 10  2REFFLD(@@FSCD)
     A  31
     AO 51                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A  98                                  DSPATR(ND)
     A N31N90                               DSPATR(HI)
     A N31N90                               DSPATR(UL)
     A            DFBLCD    R        B 10  9REFFLD(@@BLCD)
     A  35
     AO 55                                  DSPATR(PC)
     A  35                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A  98                                  DSPATR(ND)
     A N35N90                               DSPATR(HI)
     A N35N90                               DSPATR(UL)
     A            DFRMCD    R        B 10 15REFFLD(@@RMCD)
     A  40
     AO 50                                  DSPATR(PC)
     A  40                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A  98                                  DSPATR(ND)
     A N40N90                               DSPATR(HI)
     A N40N90                               DSPATR(UL)
     A            DFHGCD    R        B 10 21REFFLD(HGHGCD)
     A  34
     AO 54                                  DSPATR(PC)
     A  34                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A  98                                  DSPATR(ND)
     A N34N90                               DSPATR(HI)
     A N34N90                               DSPATR(UL)
     A            DFSCHD    R        B 10 29REFFLD(MDSCHD)
     A                                      EDTCDE(K)
     A  38                                  DSPATR(PC)
     A  38                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A  98                                  DSPATR(ND)
     A N38N90                               DSPATR(HI)
     A N38N90                               DSPATR(UL)
     A            DFGSCD    R        O 10 40REFFLD(HGGSCD)
     A            DFMDYSCDT R        B 10 46REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  ')
     A  37                                  DSPATR(PC)
     A  37                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A  98                                  DSPATR(ND)
     A N37N90                               DSPATR(HI)
     A N37N90                               DSPATR(UL)
     A            DFMDYKLDT R        B 10 56REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  ')
     A  30                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A  98                                  DSPATR(ND)
     A N30N90                               DSPATR(HI)
     A N30N90                               DSPATR(UL)
     A            DFLTCD    R        B 10 67REFFLD(HSRREF/@@LTCD *LIBL/HSPREF)
     A  48
     AO 58                                  DSPATR(PC)
     A  48                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A  98                                  DSPATR(ND)
     A N48N90                               DSPATR(HI)
     A N48N90                               DSPATR(UL)
     A            DFMVSN    R        O 10 72REFFLD(@@MVSN)
     A                                      EDTCDE(Z)
     A            DFFSNM    R        O 11  9REFFLD(FSFSNM)
     A                                 11 45'Production Type/Phase:'
     A            DFPTCD    R        O 11 68REFFLD(HSRREF/@@PTCD *LIBL/HSPREF)
     A            DFPPCD    R        O 11 74REFFLD(HSRREF/@@PPCD *LIBL/HSPREF)
     A*
     A*----------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT  1
     A*----------------------------------------------------------------
     A*
     A          R HP165C1                   SFLCTL(HP165S1)
     A*%%TS  SD  20030120  103042  ISLRAMS     REL-V5R1M0  5722-WDS
     A                                      SFLSIZ(0100)
     A                                      SFLPAG(0006)
     A                                      ROLLUP(25 'PAGE FORWARD')
     A                                      SFLCSRRRN(&HDRR1)
     A                                      OVERLAY
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A  88                                  SFLFOLD(CF13)
     A  89                                  SFLDROP(CF13)
     A                                      SFLMODE(&FOLD)
     A                                      SETOF(30)
     A                                      SETOF(31)
     A                                      SETOF(32)
     A                                      SETOF(33)
     A                                      SETOF(34)
     A                                      SETOF(35)
     A                                      SETOF(36)
     A                                      SETOF(37)
     A                                      SETOF(38)
     A                                      SETOF(39)
     A                                      SETOF(40)
     A                                      SETOF(41)
     A                                      SETOF(47)
     A                                      SETOF(50)
     A                                      SETOF(51)
     A                                      SETOF(52)
     A                                      SETOF(53)
     A                                      SETOF(54)
     A                                      SETOF(55)
     A                                      SETOF(56)
     A                                      SETOF(57)
     A                                      RTNCSRLOC(&RCD &FLD)
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            HDRR1          5S 0H
     A            FOLD           1A  H
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A            C1MODE    R        O  1 14REFFLD(@@MODE)
     A                                      DSPATR(HI)
     A                                  1 31'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A            C1VERSION     18A  O  2  2DSPATR(HI)
     A                                  2 26'CREATE SCHEDULED SALES MOVEMENTS'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  4  2'Sale type .............'
     A N90                              4 26'?'
     A            C1STCD    R        B  4 28REFFLD(@@STCD)
     A  36
     AO 56                                  DSPATR(PC)
     A  36                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A N36                                  DSPATR(HI)
     A N36                                  DSPATR(UL)
     A                                  4 43'Load type ........'
     A N90                              4 62'?'
     A            C1LTCD    R        B  4 64REFFLD(HSRREF/@@LTCD *LIBL/HSPREF)
     A  47
     AO 57                                  DSPATR(PC)
     A  47                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A N47                                  DSPATR(HI)
     A N47                                  DSPATR(UL)
     A                                  5  2'Scheduled ship date ...'
     A            C1MDYSCDT R     S  B  5 28REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A  39                                  DSPATR(PC)
     A  39                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A N39                                  DSPATR(HI)
     A N39                                  DSPATR(UL)
     A                                  5 43'Trucker ..........'
     A N90                              5 62'?'
     A            C1TICD    R        B  5 64REFFLD(@@TICD)
     A  33
     AO 53                                  DSPATR(PC)
     A  33                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A N33                                  DSPATR(HI)
     A N33                                  DSPATR(UL)
     A                                  6  2'Scheduled kill date ...'
     A            C1MDYKLDT R     S  B  6 28REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A  41                                  DSPATR(PC)
     A  41                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A N41                                  DSPATR(HI)
     A N41                                  DSPATR(UL)
     A                                  6 43'Customer number ..'
     A N90                              6 62'?'
     A            C1CVNO    R        B  6 64REFFLD(SHREC/SHCVNO *LIBL/HSP084)
     A  32
     AO 52                                  DSPATR(PC)
     A  32                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A N32                                  DSPATR(HI)
     A N32                                  DSPATR(UL)
     A            C1CNAM    R        O  7 49REFFLD(@@DS30)
     A N90                              8  4'?'
     A N90                              8 11'?'
     A N90                              8 16'?'
     A N90                              8 23'?'
     A                                  8 30'Sched'
     A                                  8 38'Group'
     A                                  8 48'Sched'
     A                                  8 58'Sched'
     A                                  8 66'Load'
     A                                  8 72'Movement'
     A                                  9  2'Farm'
     A                                  9  9'Bldg'
     A                                  9 15'Room'
     A                                  9 21'Group'
     A                                  9 31'Head'
     A                                  9 38'Status'
     A                                  9 47'Ship Dt'
     A                                  9 57'Kill Dt'
     A                                  9 66'Type'
     A                                  9 73'Number'
     A*----------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*----------------------------------------------------------------
     A          R HP165K1
     A*%%TS  SD  19991111  095829  ISLRAMS     REL-V4R2M0  5769-PW1
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A N90                             23 11'F4=Prompt'
     A                                 23 22'F5=Refresh'
     A N90                             23 34'F9=Accept'
     A                                 23 45'F12=Previous'
     A                                 23 59'F13=Fold/Unfold'
      *----------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *----------------------------------------------------------------
     A          R HP165ES                   SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
     A*----------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*----------------------------------------------------------------
     A*
     A          R HP165EC                   SFLCTL(HP165ES)
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
