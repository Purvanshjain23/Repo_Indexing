      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Hog Production System
      * PROGRAM:     HP6021
      * TITLE:       Listing of a Single BGF Weekly Wean Projection
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     07/07/04
      *
      * Function:    This batch listing prints a single BGF Weekly Wean Projection
      *              record. It is submitted when the user takes Option 6=Print from
      *              the W/W BGF Weekly Wean Projections function.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fhsl086a   if   e           k disk
      *    BGF weekly wean projections
      *
      *
     Fqprint    o    f   80        printer oflind(*inof)
      *
      /EJECT
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Print fields
      *
     D rtime           s              6  0
      *
      *
     D l1rpcdyr        s                   like(pwrpcdyr)
     D l1rpcdwk        s                   like(pwrpcdwk)
     D l1rpwemdy       s              6  0
      *
     D l1pwcdyr1       s                   like(pwpwcdyr)
     D l1pwcdwk1       s                   like(pwpwcdwk)
     D l1pwwemdy1      s              6  0
      *
     D l1pwcdyr2       s                   like(pwpwcdyr)
     D l1pwcdwk2       s                   like(pwpwcdwk)
     D l1pwwemdy2      s              6  0
      *
     D l1supwhd        s                   like(pwsupwhd)
     D l1mopwhd        s                   like(pwsupwhd)
     D l1tupwhd        s                   like(pwsupwhd)
     D l1wepwhd        s                   like(pwsupwhd)
     D l1thpwhd        s                   like(pwsupwhd)
     D l1frpwhd        s                   like(pwsupwhd)
     D l1sapwhd        s                   like(pwsupwhd)
     D l1topwhd1       s                   like(pwsupwhd)
     D l1pwsohd1       s                   like(pwpwsohd)
     D l1socuhd1       s                   like(pwsocuhd)
     D l1bocuhd1       s                   like(pwbocuhd)
      *
     D l1topwhd2       s                   like(pwtopwhd)
     D l1pwsohd2       s                   like(pwpwsohd)
      *
      *
      * Work fields for date manipulation
      *
     D wkisodate       s               D   datfmt(*iso)
      *
      *
      * Control values
      *
     D x               s              1  0
     d prtfl           s              1    inz('N')
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *---------------------------------------------------------------
      * Standard printer file data structure
      *---------------------------------------------------------------
      *    externally defined as UTPPFR (record format:FDBCKP)
     D prtds         e ds                  extname(utppfr)
      *
      *
      *---------------------------------------------------------------
      * Local data area.
      *---------------------------------------------------------------
      *
     Dlda             uds                  dtaara(*lda)
     D  ldlist                 1      1
     D  ldfscd                 2      6  0
     D  ldfsnm                 7     31
     D  ldrpwedt              32     39  0
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * MAINLINE
      ****************************************************************
      *
      *
     C     key01         setll     hsl086a
      *
     C                   do        2             x
     C     key01         reade     hsl086a                                92
     C                   if        *in92 = *off
     C                   move      yes           prtfl
     C                   exsr      $pop
     C                   endif
     C                   enddo
      *
      * Print headings
     C                   except    h1hdr
      *
      * Print detail or No Data message
      *
     C                   select
     C                   when      prtfl = yes
     C                   except    l1dtl
     C                   other
     C                   except    nodata
     C                   endsl
      *
     C                   seton                                        lr
      /eject
      *---------------------------------------------------------------
      * Populate the print fields
      *---------------------------------------------------------------
      *
     C     $pop          begsr
      *
      * RECORD 1--1 Week Out
      *
     C                   select
     C                   when      x = 1
      * Report dates
     C                   z-add     pwrpcdwk      l1rpcdwk
     C                   z-add     pwrpcdyr      l1rpcdyr
      *
     C     *iso          test(d)                 pwrpwedt               92
     C                   if        *in92 = *off                                 If OK date
     C                   move      pwrpwedt      wkisodate
     C     *mdy          move      wkisodate     l1rpwemdy
     C                   endif                                                  If OK date
      *
      * Week 1 dates
     C                   z-add     pwpwcdwk      l1pwcdwk1
     C                   z-add     pwpwcdyr      l1pwcdyr1
      *
     C     *iso          test(d)                 pwpwwedt               92
     C                   if        *in92 = *off                                 If OK date
     C                   move      pwpwwedt      wkisodate
     C     *mdy          move      wkisodate     l1pwwemdy1
     C                   endif                                                  If OK date
      *
      * Sunday thru Saturday stuff
      *
     C                   z-add     pwsupwhd      l1supwhd
     C                   z-add     pwmopwhd      l1mopwhd
     C                   z-add     pwtupwhd      l1tupwhd
     C                   z-add     pwwepwhd      l1wepwhd
     C                   z-add     pwthpwhd      l1thpwhd
     C                   z-add     pwfrpwhd      l1frpwhd
     C                   z-add     pwsapwhd      l1sapwhd
     C                   z-add     pwtopwhd      l1topwhd1
     C                   z-add     pwpwsohd      l1pwsohd1
     C                   z-add     pwsocuhd      l1socuhd1
     C                   z-add     pwbocuhd      l1bocuhd1
      *
      * RECORD 2--2 Weeks Out
      *
     C                   when      x = 2
     C                   z-add     pwpwcdwk      l1pwcdwk2
     C                   z-add     pwpwcdyr      l1pwcdyr2
      *
     C     *iso          test(d)                 pwpwwedt               92
     C                   if        *in92 = *off                                 If OK date
     C                   move      pwpwwedt      wkisodate
     C     *mdy          move      wkisodate     l1pwwemdy2
     C                   endif                                                  If OK date
      *
     C                   z-add     pwtopwhd      l1topwhd2
     C                   z-add     pwpwsohd      l1pwsohd2
     C                   endsl
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Key lists
      *
     C     key01         klist
     C                   kfld                    ldfscd
     C                   kfld                    ldrpwedt
      *
      *
      * Retrieve time for report heading.
      *
     C                   time                    rtime
      *
     C                   endsr
      /eject
      *-------------------------------------------------------------
      * Report Headings
      *-------------------------------------------------------------
      *
     Oqprint    e            h1hdr          1 01
     O                       sdpgm               10
     O                                           50 'HOG PRODUCTION SYSTEM'
     O                                           70 'DATE'
     O                       udate         y     80
      *
     O          e            h1hdr          1
     O                       sdusr               10
     O                                           45 'LISTING OF BGF WEEKLY WEAN'
     O                                           56 ' PROJECTION'
     O                                           70 'TIME'
     O                       rtime               80 '  :  :  '
      *
     O          e            h1hdr          2
     O                                           70 'PAGE'
     O                       page          z     80
      *
     O          e            h1hdr          2
     O                                           10 'Farm site:'
     O                       ldfscd        z     16
     O                       ldfsnm              43
      *
      *-------------------------------------------------------------
      * Detail line
      *-------------------------------------------------------------
      *
      *
     O          e            l1dtl       1  1
     O                                           33 'Report calendar week/year:'
     O                       l1rpcdwk      z     37
     O                       l1rpcdyr      z     43
      *
     O                                           70 'Week-ending date:'
     O                       l1rpwemdy           80 '  /  /  '
      *
      * Projection Week 1
      *
     O          e            l1dtl       3  1
     O                                           16 '1 Week Out Wean '
     O                                           33 'Projections Week:'
     O                       l1pwcdwk1     z     37
     O                       l1pwcdyr1     z     43
      *
     O                                           70 'Week-ending date:'
     O                       l1pwwemdy1          80 '  /  /  '
      *
      *
     O          e            l1dtl       1
     O                                           42 'Sunday'
     O                       l1supwhd      k     51
      *
      *
     O          e            l1dtl       1
     O                                           42 'Monday'
     O                       l1mopwhd      k     51
      *
      *
     O          e            l1dtl       1
     O                                           42 'Tuesday'
     O                       l1tupwhd      k     51
      *
      *
     O          e            l1dtl       1
     O                                           42 'Wednesday'
     O                       l1wepwhd      k     51
      *
      *
     O          e            l1dtl       1
     O                                           42 'Thursday'
     O                       l1thpwhd      k     51
      *
      *
     O          e            l1dtl       1
     O                                           42 'Friday'
     O                       l1frpwhd      k     51
      *
      *
     O          e            l1dtl       1
     O                                           42 'Saturday'
     O                       l1sapwhd      k     51
      *
      *
     O          e            l1dtl       1
     O                                           51 '-------'
      *
      *
     O          e            l1dtl       1  1
     O                                           35 '1 week out total piglets '
     O                                           42 'weaned:'
     O                       l1topwhd1     k     51
      *
      *
     O          e            l1dtl       1
     O                                           42 '1 week out weaned sows:'
     O                       l1pwsohd1     k     51
      *
      *
     O          e            l1dtl       1
     O                                           42 '1 week out sow culls:'
     O                       l1socuhd1     k     51
      *
      *
     O          e            l1dtl       1
     O                                           42 '1 week out boar culls:'
     O                       l1bocuhd1     k     51
      *
      * Projection Week 2
      *
     O          e            l1dtl       4  1
     O                                           17 '2 Weeks Out Wean '
     O                                           34 'Projections Week:'
     O                       l1pwcdwk2     z     37
     O                       l1pwcdyr2     z     43
      *
     O                                           70 'Week-ending date:'
     O                       l1pwwemdy2          80 '  /  /  '
      *
      *
     O          e            l1dtl       1
     O                                           36 '2 weeks out total piglets '
     O                                           43 'weaned:'
     O                       l1topwhd2     k     51
      *
      *
     O          e            l1dtl       1
     O                                           43 '2 weeks out weaned sows:'
     O                       l1pwsohd2     k     51
      *
      *
      *-------------------------------------------------------------
      * NO DATA MESSAGE LINE
      *-------------------------------------------------------------
      *
     O          e            nodata      2
     O                                           19 'No data meets your'
     O                                           39 'selection criteria.'
      /eject
