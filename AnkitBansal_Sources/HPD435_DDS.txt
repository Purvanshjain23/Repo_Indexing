     A*%%TS  SD  20080917  163355  ISLRAMS     REL-V5R4M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD435
     A*  TITLE:         WORK WITH CHECKS
     A*  PROGRAMMER:    ROSE CENTONZE
     A*  DATE:          04/29/94
     A*
     A*****************************************************************
     A* RMC 2/22/2016 E5190  ADD BUSINESS OFFICE RESTRICTOR
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CF03(03 'EXIT')
     A N99                                  CF04(04 'PROMPT')
     A N99                                  CF05(05 'REFRESH')
     A N99                                  CF06(06 'CREATE')
     A N99                                  CF17(17 'PRINT OPTIONS')
     A N99                                  CF20(20 'WRKSPLF')
     A*
     A*
     A*---------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP435S1                   SFL
     A*%%TS  SD  19960308  102705  ISLRAMS     REL-V3R1M0  5763-PW1
     A  85                                  SFLNXTCHG
     A            HDRRN1         4Y 0H
     A            HDCHDT    R        H      REFFLD(CHCHDT)
     A            DFOPT          2A  B 11  2CHGINPDFT(UL)
     A  30                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A N30                                  DSPATR(HI)
     A  99                                  DSPATR(PR)
     A            DFCMDY    R        O 11  6REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A            DFCVNO    R        O 11 17REFFLD(CHCVNO)
     A                                      EDTCDE(Z)
     A            DFCNAM        12A  O 11 27
     A            DFCHNO    R        O 11 41REFFLD(CHCHNO)
     A                                      EDTCDE(Z)
     A            DFSTCD    R        O 11 50REFFLD(CHSTCD)
     A            DFTPCD    R        O 11 58REFFLD(CHTPCD)
     A            DFCHAM    R        O 11 62REFFLD(CHCHAM)
     A                                      EDTCDE(K)
     A            DFDSCD    R        O 11 78REFFLD(CHDSCD)
     A*
     A*---------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP435C1                   SFLCTL(HP435S1)
     A*%%TS  SD  20080917  163355  ISLRAMS     REL-V5R4M0  5722-WDS
     A                                      SFLSIZ(0050)
     A                                      SFLPAG(0011)
     A N82N99                               ROLLUP(25 'PAGE FORWARD')
     A                                      SETOF(30)
     A                                      SETOF(31)
     A                                      SETOF(51)
     A                                      SETOF(52)
     A                                      SETOF(53)
     A                                      SETOF(54)
     A                                      RTNCSRLOC(&RCD &FLD)
     A                                      OVERLAY
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 26'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  2 28'WORK WITH CHECKS'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  4  2'Business office .. ?'
     A            C1FSBO    R        B  4 23REFFLD(HGFSBO)
     A  31
     AO 51                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A N31                                  DSPATR(HI)
     A  99                                  DSPATR(PR)
     A            C1BODS    R        O  4 30REFFLD(BOBODS)
     A                                  6  2'2=Revise'
     A                                  6 12'5=View'
     A                                  6 20'6=Print Check Register'
     A                                  6 44'7=Resynch Movements'
     A                                  7 20'?'
     A                                      DSPATR(HI)
     A                                  7 52'?'
     A                                      DSPATR(HI)
     A                                  7 59'?'
     A                                      DSPATR(HI)
     A                                  7 77'?'
     A                                      DSPATR(HI)
     A                                  8  8'Check'
     A                                      DSPATR(HI)
     A                                  8 17'Customer'
     A                                      DSPATR(HI)
     A                                  8 27'Customer'
     A                                      DSPATR(HI)
     A                                  8 43'Check'
     A                                      DSPATR(HI)
     A                                  8 50'Sale'
     A                                      DSPATR(HI)
     A                                  8 56'Payment'
     A                                      DSPATR(HI)
     A                                  8 68'Check'
     A                                      DSPATR(HI)
     A                                  8 76'Data'
     A                                      DSPATR(HI)
     A                                  9  2'Opt'
     A                                      DSPATR(HI)
     A                                  9  9'Date'
     A                                      DSPATR(HI)
     A                                  9 19'Number'
     A                                      DSPATR(HI)
     A                                  9 27'Name'
     A                                      DSPATR(HI)
     A                                  9 42'Number'
     A                                      DSPATR(HI)
     A                                  9 50'Type'
     A                                      DSPATR(HI)
     A                                  9 57'Type'
     A                                      DSPATR(HI)
     A                                  9 67'Amount'
     A                                      DSPATR(HI)
     A                                  9 75'Source'
     A                                      DSPATR(HI)
     A            C1CMDY    R        B 10  6REFFLD(@@DT06)
     A                                      DSPATR(HI)
     A                                      EDTWRD('  /  /  -')
     A            C1CVNO    R        B 10 17REFFLD(CHCVNO)
     A                                      DSPATR(HI)
     A                                      EDTCDE(Z)
     A  51                                  DSPATR(PC)
     A            C1CHNO    R        B 10 41REFFLD(CHCHNO)
     A                                      DSPATR(HI)
     A                                      EDTCDE(Z)
     A            C1STCD    R        B 10 50REFFLD(CHSTCD)
     A                                      DSPATR(HI)
     A  52                                  DSPATR(PC)
     A            C1TPCD    R        B 10 58REFFLD(CHTPCD)
     A                                      DSPATR(HI)
     A  54                                  DSPATR(PC)
     A            C1DSCD    R        B 10 78REFFLD(CHDSCD)
     A                                      DSPATR(HI)
     A  53                                  DSPATR(PC)
     A*
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R HP435K1
     A*%%TS  SD  20040602  143240  ISLRAMS     REL-V5R2M0  5722-WDS
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A                                 23 11'F4=Prompt'
     A                                 23 22'F5=Refresh'
     A                                 23 34'F6=Create'
     A N99                             23 45'F17=Print Options'
     A                                 23 64'F20=Spool Files'
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *---------------------------------------------------------------
     A          R HP435ES                   SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
     A*
     A*---------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*----------------------------------------------------------------
     A*
     A          R HP435EC                   SFLCTL(HP435ES)
     A*%%TS  SD  19940429  155529  ISRCENT     REL-V2R2M0  5738-PW1
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
