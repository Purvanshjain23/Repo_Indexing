      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Hog Production
      * PROGRAM:     TF312 - Workfile: Upload Claim Adjustment to JDE
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     03/07/06
      *
      * FUNCTION:    Build a workfile of Claims data that will be uploaded to JDE.
      *
      *              This program runs during a "Final" of the Weekly Revenue Close.
      *
      *
      **********************************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 04/25/06  LeAnne Fedor
      *           The users wanted the signs "reversed".
      *
      * 08/29/06  LeAnne Ramsey
      *           Recompile only. New fields were added to TFP022-Claim Adjustments.
      *
      * 06/06/07  LeAnne Ramsey
      *           Added parms to the call to TF813:
      *               Co-owned Flag
      *               Producing Company
      *
      * 09/11/08  LeAnne Ramsey
      *           Added parm to the call to TF813:
      *               Export/Domestic Flag
      *
      * 01/05/11  LeAnne Ramsey (E1245)
      *           Recompile only. One new field was added to TFP022-Claim Adjustments:
      *              1) customer type
      *
      * 11/28/17  Danny Nguyen  (R12011A-Weekly Product Revenue)
      *           Added 2 STF fields to TFP022 file:
      *             CLXRPPC  - STF RESPONSIBILITY %
      *             CLXNCLAM - STF NET CLAIM AMT
      *           Recompile only, no logic change.
      *           STF Responsibility % will always be 100 % so no changes required.
      *
      * W105621 RMC 10/10/2022 INCREASE FIELD SIZES TO HANDLE PRICES > 999.99
      *         CHANGE @@ANPRPR TO SIZE 10.6
      *           CHANGES CLANPRPR IN TFP022
      *         CHANGE @@NCLPR  TO SIZE 10.6
      *           CHANGES CLNCLPR IN TFP022
      *
      /eject
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fpdjirel3  if   e           k disk
      *    Item category
      *
      *
     Ftfl022c   if   e           k disk
      *    Claim adjustments
      *
      *
     Ftfp312    uf a e           k disk
      *    Workfile: Claim Adjustment Upload to JDE
      *
      /eject
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      *
      ****************************************************************
      * ARRAYS AND TABLES
      ****************************************************************
      *
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Work fields
      *
     d wksbatnbr       s                   like(w1batnbr)
     d wksdocnbr       s                   like(w1docnbr)
      *
     d wktbatnbr       s                   like(w1batnbr)
     d wktdocnbr       s                   like(w1docnbr)
      *
      *
      *
      * Parm fields
      *
     D xxprcd          s                   like(clprcd)
     D xxistycd        s              3p 0
     D xxisgrcd        s              3p 0
     D xxisclcd        s              3p 0
     D xxtfclcd        s              3
     D xxtfcgcd        s              2
     D xxglsub         s              3
     D xxitycd         s              3
     D xxedfl          s              1
     D xxcoownfl       s              1
     D xxprdcmp        s              3
      *
     d xxbatnbr        s                   like(w1batnbr)
     d xxdocnbr        s                   like(w1docnbr)
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /eject
      ****************************************************************************************
      * Mainline
      ****************************************************************************************
      *
      * Read all Claim Adjustment records selected by the open query.
      * Process each record where:
      *  1) the claim is NOT 100% to the Claim Company
      *
     C     *loval        setll     tfl022c
      *
     C                   dou       *in90 = *on                                  Main do
     C                   read      tfl022c                                90
     C                   if        *in90 = *off and                             If not EOF
     C                             ((clcono = 360 and clsrppc <> 100) or
     C                             (clcono = 960 and cltrppc <> 100))
      *
      * Write a workfile record for the "claim" company.
      *
     C                   exsr      $claim
      *
      * Write a workfile record for the "other" (not-the-claim) company.
      *
     C                   exsr      $other
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Main do
      *---------------
      * EOF processing
      *---------------
      *
     C                   seton                                        lr
      /eject
      *----------------------------------------------------------------
      * Write the workfile record for the "CLAIM" company
      *----------------------------------------------------------------
      *
     C     $claim        begsr
      *
      * Set the "Claim Company/Other Company" flag and populate the Company field.
      *
     C                   move      'C'           w1flag
     C                   z-add     clcono        w1cono
      *
      * Set the Post Amount equal to the "Net Claim Amount' of the "other" company.
      *
     C                   select
     C                   when      clcono = 360
     C                   z-add     cltnclam      w1am
      *
     C                   when      clcono = 960
     C                   z-add     clsnclam      w1am
     C                   other
      *
     C                   z-add     0             w1am
     C                   endsl
      *
      *
      * Write the workfile record
      *
     C                   exsr      $wrt312
      *
     C                   endsr
      /eject
      *----------------------------------------------------------------
      * Write the workfile record for the "OTHER" company
      *----------------------------------------------------------------
      *
     C     $other        begsr
      *
      * Set the "Claim Company/Other Company" flag
      *
     C                   move      'O'           w1flag
      *
      * Populate the "company" field with the "other/not-the-claim" company.
      *
     C                   select
     C                   when      clcono = 360
     C                   z-add     960           w1cono
      *
     C                   when      clcono = 960
     C                   z-add     360           w1cono
     C                   other
      *
     C                   z-add     0             w1cono
     C                   endsl
      *
      * We will use the Post Amount calculated for the "Claim" company;
      * BUT, we will 'flip' the sign.
      *
     C                   mult      -1            w1am
      *
      * Write a workfile record.
      *
     C                   exsr      $wrt312
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Write a workfile record
      *---------------------------------------------------------------
      *
     C     $wrt312       begsr
      *
     C                   z-add     clprcd        w1prcd
      *
     C                   z-add     clagclam      w1agclam
     C                   z-add     clsnclam      w1snclam
     C                   z-add     cltnclam      w1tnclam
      *
     C                   z-add     clclno        w1clno
     C                   z-add     clcllnno      w1cllnno
     C                   z-add     clmmno        w1mmno
     C                   move      clmmty        w1mmty
      *
      * Get Company Number into a 5-alpha field (required for JDE files)
      *
     C                   move      *zero         w1co5a
     C                   move      w1cono        w1co5a
      *
      * Populate the Debit/Credit Amount fields which are used in a later
      * program to print the JDE backup report.
      *
     C                   z-add     0             w1dbam
     C                   z-add     0             w1cram
      *
     C                   select
     C                   when      w1am > 0
     C                   z-add     w1am          w1dbam
      *
     C                   when      w1am < 0
     C                   z-add     w1am          w1cram
     C                   endsl
      *
      * Set appropriate Batch and Document numbers
      *
     C                   select
     C                   when      w1cono = 360
     C                   z-add     wksbatnbr     w1batnbr
     C                   z-add     wksdocnbr     w1docnbr
      *
     C                   when      w1cono = 960
     C                   z-add     wktbatnbr     w1batnbr
     C                   z-add     wktdocnbr     w1docnbr
     C                   other
      *
     C                   z-add     0             w1batnbr
     C                   z-add     0             w1docnbr
     C                   endsl
      *
      * Retrieve the GL Sub Code for this product.
      *
     C                   call      'TF813'
     C                   parm      w1prcd        xxprcd
     C                   parm      0             xxistycd
     C                   parm      0             xxisgrcd
     C                   parm      0             xxisclcd
     C                   parm      *blank        xxtfclcd
     C                   parm      *blank        xxtfcgcd
     C     w1glsub       parm      *blank        xxglsub
     C                   parm      *blank        xxitycd
     C                   parm                    xxcoownfl
     C                   parm                    xxprdcmp
     C                   parm      *blank        xxedfl
      *
      * Retrieve a GL Sub description
      *
     C     w1glsub       chain     pdjirel3                           92
     C                   if        *in92 = *off
     C                   eval      w1desc = jiauna
     C                   else
     C                   eval      w1desc = 'Unknown'
     C                   endif
      *
     C                   write     w1rec
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Retrieve/save a batch Number for:
      *   1) Seaboard
      *   2) Triumph
     C                   call      'TFJDE800'
     C     wksbatnbr     parm      0             xxbatnbr
      *
     C                   call      'TFJDE800'
     C     wktbatnbr     parm      0             xxbatnbr
      *
      *
      * Retrieve/save a Document Number for:
      *   1) Seaboard
      *   2) Triumph
     C                   call      'TFJDE801'
     C     wksdocnbr     parm      0             xxdocnbr
      *
     C                   call      'TFJDE801'
     C     wktdocnbr     parm      0             xxdocnbr
      *
     C                   endsr
      *
