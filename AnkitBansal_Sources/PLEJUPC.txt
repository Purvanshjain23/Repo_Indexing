/********************************************************************/
/*                                                                  */
/*  SYNOPSIS - PRINT PROGRAM WITH SELECTIONS FOR DEFERRAL PAYMENT.  */
/*             COMPANY, KILL DATES, SOURCE CODE, PRODUCER,          */
/*             DED/ADD CODE, DETAIL OR SUMMARY,                     */
/*                                                                  */
/*  SYSTEM    - HPE SYSTEM                                          */
/*  PROGRAM   - PRINT DEFERRAL PAYMENT REPORT                       */
/*  DATE      - 03/25/2004                                          */
/*                                                                  */
/*      PARAMETERS RECEIVED:                                        */
/*                                                                  */
/*          &COMPNYN    - COMPANY NUMBER     - REQUIRED             */
/*          &FRMDATN    - FROM KILL DATE     - REQUIRED             */
/*          &TODATN     - TO   KILL DATE     - REQUIRED             */
/*                                                                  */
/*                        ALL REQUIRED FIELDS WILL BE SELECTED      */
/*                        IN THE QUERY, THE REMAINDING FIELDS       */
/*                        WILL BE SELECTED IN THE REPORT PGM        */
/*                                                                  */
/*          &SOURCE     - SOURCE CODE                               */
/*          &PRODUCER   - PRODUCER CODE                             */
/*          &DADDCDE    - DED/ADD CODE                              */
/*          &DTLSUM     - DETAIL OR SUMMARY                         */
/*                                                                  */
/*          &PRTD       - PRINT DEVICE                              */
/*          &HOLD       - HOLD PRINT                                */
/*          &SAVE       - SAVE PRINT                                */
/*          &XCOPIES    - NUMBER OF COPIES                          */
/*          &OUTQ       - OUTPUT QUEUE                              */
/*          &OMS        - OMS NAME                                  */
/*                                                                  */
/********************************************************************/
/*  SLM 3/25/2004  CREATION OF CL                                   */
/********************************************************************/
/* Modifications:                                                   */
/*                                                                  */
/*   Date     Pgmr     Description                                  */
/* -------- ---------- -------------------------------------------- */
/* 11/05/20    ISE     RECOMPILED DUE TO CHANGE IN BUY ORDER NUMBER */
/*********************************************************************/

             PGM        PARM(&COMPNYN &FRMDATN &SOURCE &PRODUCER +
                          &TODATN &DADDCDE &DTLSUM &PDEV &HOLD +
                          &SAVE &XCOPIES &OMS)

        DCL    &COMPNYN    *DEC   (3)   /* COMPANY NUMBER           */
        DCL    &FRMDATN    *DEC (7 0)   /* FROM KILL DATE           */
        DCL    &TODATN     *DEC (7 0)   /* THRU KILL DATE           */

/* FUNCTION SPECIFIC PARMS                                          */
/*          &SOURCE     - SOURCE CODE                               */
/*          &PRODUCER   - PRODUCER CODE                             */
/*          &DADDCDE    - DED/ADD CODE                              */
/*          &DTLSUM     - DETAIL OR SUMMARY                         */

        DCL    &SOURCE     *CHAR   (3)  /* SOURCE                   */
        DCL    &PRODUCER   *CHAR   (6)  /* PRODUCER                 */
        DCL    &DADDCDE    *CHAR   (5)  /* DED OR ADD CODE          */
        DCL    &DTLSUM     *CHAR   (1)  /* DETAL OR SUMMARY         */


/* SYSTEM NAME PARM                                                 */
        DCL    &OMS        *CHAR  (40)

/* PRINT OPTION PARMS                                               */
        DCL    &PDEV       *CHAR   (10) /* PRINT DEVICE             */
        DCL    &HOLD       *CHAR    (4) /* Hold Status              */
        DCL    &SAVE       *CHAR    (4) /* Save Status              */
        DCL    &XCOPIES    *CHAR   (15) /* No. of COPIES            */

        DCL    &COMMAND    *CHAR  (512)

/* INTERNAL FIELDS                                                            */
        DCL    &COPIES     *DEC    (2 0)         /* Number of copies          */
        DCL    &COMPANY    *DEC    (3 0)         /* Company Number            */
        DCL    &FRMDAT     *DEC    (7 0)         /* FROM KILL DATE            */
        DCL    &TODAT      *DEC    (7 0)         /* THRU KILL DATE            */

/* OPENQRYF FIELDS                                                            */
        DCL    &COMPA      *CHAR    3            /* ACK COMPLETE              */
        DCL    &FRMDATA    *CHAR    7            /* FROM KILL DATE (Passed)   */
        DCL    &TODATA     *CHAR    7            /* THRU KILL DATE (Passed)   */
        DCL     &A        *CHAR   25              /* QRY COMPANY    */
        DCL     &B        *CHAR   25              /* QRY FROM DATE  */
        DCL     &C        *CHAR   25              /* QRY TO DATE    */
        DCL     &G        *CHAR  145              /* QRY ALL        */

/********************************************************************/
/* CHANGE VARIABLES FOR USE IN PROGRAM                              */
/********************************************************************/

             CHGVAR     VAR(&COMPANY) VALUE(&COMPNYN)
             CHGVAR     VAR(&COMPA) VALUE(&COMPANY)
             CHGVAR     VAR(&FRMDAT) VALUE(&FRMDATN)
             CHGVAR     VAR(&FRMDATA) VALUE(&FRMDAT)
             CHGVAR     VAR(&TODAT) VALUE(&TODATN)
             CHGVAR     VAR(&TODATA) VALUE(&TODAT)
             CHGVAR     VAR(&COPIES) VALUE(&XCOPIES)

             CHGVAR     VAR(&A) VALUE('A1HMNB *EQ ' *CAT +
                         &COMPA)

             CHGVAR     VAR(&B) VALUE('*AND A1B0DT *GE ' *CAT +
                         &FRMDATA)

             CHGVAR     VAR(&C) VALUE('*AND A1B0DT *LE ' *CAT +
                         &TODATA)

             CHGVAR     VAR(&G) VALUE(&A *BCAT &B *BCAT &C)


/*********************************************************************/
/*   CREATE OPEN QUERY FILE TO BE USED FOR DEFERRAL REPORT           */
/*********************************************************************/
/*  RUN QUERY TO READ TATTOO HEADER BY COMPANY/DATE RANAGE  */

             OVRDBF     FILE(PKA1CPME) SHARE(*YES)
             OPNQRYF    FILE(PKA1CPME) +
                        QRYSLT(&G) +
                        KEYFLD(*FILE)

/********************************************************************/
/* OVERRIDE PRINTER FILE WITH USER SPECIFIED SELECTION VALUES       */
/********************************************************************/

             SBDPRTOVR  CMDVAR(&COMMAND) FILE(PLEKPFR$) OUTQ(&PDEV) +
                          COPIES(&COPIES) HOLD(&HOLD) SAVE(&SAVE)
             CALL       PGM(QCMDEXC) PARM(&COMMAND 512)


/*********************************************************************/
/*   CALL PRINT PROGRAM -
/*********************************************************************/

             CALL       PGM(PLEKPFR) PARM(' ' &COMPNYN &FRMDATN +
                          &SOURCE &PRODUCER &TODATN &DADDCDE +
                          &DTLSUM &OMS)



 END:        ENDPGM
