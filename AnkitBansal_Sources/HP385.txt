      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      HOG PRODUCTION
      * PROGRAM:     HP385
      * TITLE:       BUILD WORKFILE FOR DAILY DEAD, DOAS AND REJECT
      *              REPORT
      * PROGRAMMER:  LEANNE FEDOR
      * CREATED:     08/07/95
      *
      *  FUNCTION:   THIS BATCH PROGRAM IS CALLED FROM HP455CL.
      *              IT BUILDS WORKFILE HSP385 THAT IS USED IN THE
      *              REPORT PROGRAM HP685.
      *
      *              THE USER'S SUBMISSION CRITERIA ARE PASSED IN THE
      *              LDA FROM HP455.  AN OPEN QUERIES ARE USED IN THE
      *              CL TO MAKE AN INITIAL SELECTION OF DATA.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 02/06/96  LEANNE FEDOR
      *           RECOMPILE ONLY. NEW FIELD 'BOL PRINTED COUNT' ADDED
      *           TO SALES MOVEMENT HEADER FILE.
      *
      * 02/13/96  LEANNE FEDOR
      *           RECOMPILE ONLY. NEW FIELD 'BOL PRINTED COUNT' ADDED
      *           TO TRANSFER MOVEMENT HEADER FILE.
      *
      * 02/16/98  LeAnne Fedor
      *           Recompile only. Added the following fields to the
      *           database for sales movement detail and check detail:
      *                     condemned head and pounds
      *                     yard dead head and pounds
      *
      * 01/11/99  LeAnne Fedor
      *           Recompile only. New field 'scheduled kill date' added
      *           to sales movement header file.
      *
      * 10/17/00  LeAnne Fedor
      *           Recompile only. New field 'load type' added to the sales
      *           movement head file.
      *
      * 09/06/01  LeAnne Fedor
      *           'Group type' was added as a report selection. So, we added
      *           logic to process only the selected group type.
      *
      * 12/02/02  LeAnne Fedor
      *           Production Type and Production Phase were removed from the Killed/
      *           Dead file. So, we changed the logic here to retrieve these values
      *           from the Hog Group master file.
      *
      * 12/18/02  LeAnne Fedor
      *           Production Phase/Type were removed from Sales Movement Header
      *           file. So, we changed the logic here to retrieve these values
      *           from the Hog Group master file.
      *
      * 07/06/09  LeAnne Ramsey
      *           Recompile only. Added new field 'Continuous Flow Flag' to Hog Group file.
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     FHSL034D   IF   E           K DISK
      *    HOG GROUP
      *
      *
     FHSP041    IF   E           K DISK
      *    DEATH/DISEASE REASONS
      *
      *
     FHSP068    IF   E           K DISK
      *    KILLED/DEAD HOGS       (records selected in OPNQRY)
      *
      *
     FHSJ074B   IF   E           K DISK    RENAME(MDREC:MDRECB)
      *  SALE MOVEMENT HEADER + DETAIL + REJECTED HEAD WITH REASONS (records selected in OPNQRY)
      *
      *
     FHSP074    IF   E           K DISK
      *  TRANSFER MOVEMENT HEADER   (records selected in OPNQRY)
      *
      *
     FHSP075    IF   E           K DISK
      *  TRANSFER MOVEMENT DETAIL   (records selected in OPNQRY)
      *
      *
     FHSP084    IF   E           K DISK
      *  SALES MOVEMENT HEADER      (records selected in OPNQRY)
      *
      *
     FHSP085    IF   E           K DISK
      *  SALES MOVEMENT DETAIL      (records selected in OPNQRY)
      *
      *
     FHSP385    UF A E           K DISK
      *    WORKFILE
      *
      /EJECT
      ****************************************************************
      * Definition Specifications
      ****************************************************************
      *
      *---------------------------------------------------------------
      *  NAMED CONSTANTS
      *---------------------------------------------------------------
      *
     D DOAS            C                   CONST('DEAD-ON-ARRIVAL,-
     D                                      SALES')
     D DOAT            C                   CONST('DEAD-ON-ARRIVAL,-
     D                                      TRANSFERS')
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      *---------------------------------------------------------------
      * STANDALONE FIELDS
      *---------------------------------------------------------------
      *
      * Control fields
      *
     D procfl          s              1    inz('Y')
      *
      *
      * Workfields
      *
     D wkhgsn          s                   like(hghgsn)
      *
      *----------------------------------------------------------------
      * Local data area
      *----------------------------------------------------------------
      *
     D                UDS
     D  LDPPCD                22     26
     D  LDGTCD                47     47
     D  LDPTCD                48     52
      *
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * MAIN LINE
      ****************************************************************
      *
      * DATA FOR THE WORKFILE IS RETRIEVED IN 4 STEPS.
      *     STEP 1: KILLED/DEAD HOGS
      *     STEP 2: REJECTED HOGS FROM TRANSFERS
      *     STEP 3: DOA HOGS FROM TRANSFERS
      *     STEP 4: DOA HOGS FROM SALES
      *
      *----------------------------------------------------------------
      * STEP 1: DAILY KILLED/DEAD HOGS
      *----------------------------------------------------------------
      *
      * READ EACH KILLED/DEAD HOG RECORD SELECTED IN THE OPEN QUERY.
      * IF THE USER HAS LIMITED THE REPORT TO A SPECIFIC 'GROUP TYPE', CHECK
      * THE GROUP'S GROUP TYPE. FOR EACH RECORD THAT YOU PROCESS,
      * EITHER UPDATE A WORKFILE RECORD OR WRITE A NEW WORKFILE RECORD.
      *
     C     *LOVAL        SETLL     HSP068
      *
     C     *IN90         DOUEQ     *ON                                          DO DEAD
     C                   READ      HSP068                                 90
     C     *IN90         IFEQ      *OFF                                         IF NOT EOF
      *
      * Retrieve 'group' info and compare to user's selections
      *
     C                   z-add     kdhgsn        wkhgsn
     C                   exsr      $hgcd
      *
      * Process record
     C                   if        procfl = yes                                 If process
     C     K1P385        CHAIN     HSP385                             92
     C     *IN92         IFEQ      *OFF                                         IF FOUND
     C                   ADD       KDKDHD        WFKDHD
     C                   ADD       KDKDLB        WFKDLB
     C                   UPDATE    WFREC
     C                   ELSE
     C                   MOVEL     HGPTCD        WFPTCD
     C                   MOVEL     HGPPCD        WFPPCD
     C                   MOVEL     KDDRCD        WFDRCD
     C                   Z-ADD     KDKDHD        WFKDHD
     C                   Z-ADD     KDKDLB        WFKDLB
     C                   Z-ADD     0             WFRJHD
     C                   EXSR      $DRDS
     C                   WRITE     WFREC
     C                   ENDIF                                                  IF FOUND
     C                   endif                                                  If process
      *
     C                   ENDIF                                                  IF NOT EOF
     C                   ENDDO                                                  DO DEAD
      *
      /EJECT
      *----------------------------------------------------------------
      * STEP 2: REJECTED HOGS FROM TRANSFERS
      *----------------------------------------------------------------
      *
      * READ EACH TRANSFER RECORD WITH REJECTED HEAD SELECTED BY THE
      * OPEN QUERY.
      * IF THE USER HAS LIMITED THE REPORT TO A SPECIFIC 'GROUP TYPE', CHECK
      * THE GROUP'S GROUP TYPE. FOR EACH RECORD THAT YOU PROCESS,
      * EITHER UPDATE A WORKFILE RECORD OR WRITE A NEW WORKFILE RECORD.
      *
     C     *LOVAL        SETLL     HSJ074B
      *
     C     *IN90         DOUEQ     *ON                                          DO REJECTS
     C                   READ      HSJ074B                                90
     C     *IN90         IFEQ      *OFF                                         IF NOT EOF
     C                   move      yes           procfl
      *
      * Check 'group type' if the user has limited the report to a single type.
      *
     C                   if        ldgtcd <> *blank                             If 1 type
     C                   z-add     mdorsn        wkhgsn
     C                   exsr      $gtcd
     C                   endif                                                  If 1 type
      *
     C                   if        procfl = yes                                 If process
      *
      * WRITE A RECORD SHOWING, FOR THE ORIGIN SIDE OF THE
      * TRANSFER, THE HEAD ULTIMATELY REJECTED AT THE
      * DESTINATION GROUP.
      *
     C     K2P385        CHAIN     HSP385                             92
     C     *IN92         IFEQ      *OFF                                         IF FOUND
     C                   ADD       RJRJHD        WFRJHD
     C                   UPDATE    WFREC
     C                   ELSE
     C                   MOVEL     MHORPT        WFPTCD
     C                   MOVEL     MHORPP        WFPPCD
     C                   MOVEL     RJDRCD        WFDRCD
     C                   Z-ADD     RJRJHD        WFRJHD
     C                   Z-ADD     0             WFKDHD
     C                   Z-ADD     0             WFKDLB
     C                   EXSR      $DRDS
     C                   WRITE     WFREC
     C                   ENDIF                                                  IF FOUND
     C                   endif                                                  If process
      *
     C                   ENDIF                                                  IF NOT EOF
     C                   ENDDO                                                  DO REJECTS
      /EJECT
      *----------------------------------------------------------------
      * STEP 3: DOA HOGS FROM TRANSFERS
      *----------------------------------------------------------------
      *
      * READ ALL TRANSFER MOVEMENTS SELECTED BY THE OPEN QUERY. FOR
      * EACH MOVEMENT, RETRIEVE/PROCESS ALL ASSOCIATED DETAIL RECORDS.
      *
      * AN OPEN QUERY OVER THE DETAIL RECORDS WILL HAVE LIMITED THE
      * DATA TO THE USER-SELECTED HOG GROUP. BUT, IF THE USER LIMITED THE
      * REPORT TO A SPECIFIC 'GROUP TYPE', COMPARE THE GROUP'S GROUP TYPE
      * TO THE USER'S SELECTED TYPE BEFORE PROCESSING THE DETAIL RECORD.
      *
     C     *LOVAL        SETLL     HSP074
      *
     C     *IN90         DOUEQ     *ON                                          DO TRAN DOA
     C                   READ      HSP074                                 90
     C     *IN90         IFEQ      *OFF                                         IF TRAN DOA
      *
     C     MHMVSN        SETLL     HSP075
     C     *IN91         DOUEQ     *ON                                          DO TRAN DTL
     C     MHMVSN        READE     HSP075                                 91
     C     *IN91         IFEQ      *OFF                                         IF TRAN DTL
     C                   move      yes           procfl
      *
      *
      * Check 'group type' if the user has limited the report to a single type.
      *
     C                   if        ldgtcd <> *blank                             If 1 type
     C                   z-add     mdorsn        wkhgsn
     C                   exsr      $gtcd
     C                   endif                                                  If 1 type
      *
     C                   if        procfl = yes                                 If process
      *
      * PROCESS THE ORIGIN OR SHIPPED OUT SIDE OF THE TRANSFER.
      * HARD-CODE THE DEATH/DISEASE REASON CODE TO INDICATE THAT
      * THESE DOA HOGS ARE FROM TRANSFERS.  THUS, THIS ORIGIN
      * SITE HAD HOGS ARRIVING DEAD AT THE DESTINATION.
      *
     C                   MOVEL     'DOA-T'       WFDRCD
     C     K3P385        CHAIN     HSP385                             92
     C     *IN92         IFEQ      *OFF                                         IF FOUND
     C                   ADD       MDDOHD        WFKDHD
     C                   ADD       MDDOLB        WFKDLB
     C                   UPDATE    WFREC
     C                   ELSE
     C                   MOVEL     'DOA-T'       WFDRCD
     C                   MOVEL     MHORPT        WFPTCD
     C                   MOVEL     MHORPP        WFPPCD
     C                   Z-ADD     MDDOHD        WFKDHD
     C                   Z-ADD     MDDOLB        WFKDLB
     C                   Z-ADD     0             WFRJHD
     C                   EXSR      $DRDS
     C                   WRITE     WFREC
     C                   ENDIF                                                  IF FOUND
     C                   endif                                                  If process
      *
     C                   ENDIF                                                  IF TRAN DTL
     C                   ENDDO                                                  DO TRAN DTL
      *
     C                   ENDIF                                                  IF TRAN DOA
     C                   ENDDO                                                  DO TRAN DOA
      /EJECT
      *----------------------------------------------------------------
      * STEP 4: DOA HOGS FROM SALES
      *----------------------------------------------------------------
      *
      * READ ALL SALES MOVEMENTS SELECTED BY THE OPEN QUERY. FOR
      * EACH MOVEMENT, RETRIEVE/PROCESS ALL ASSOCIATED DETAIL RECORDS.
      *
      * AN OPEN QUERY OVER THE DETAIL RECORDS WILL HAVE LIMITED THE
      * DATA TO THE USER-SELECTED HOG GROUP. BUT, IF THE USER LIMITED THE
      * REPORT TO A SPECIFIC 'GROUP TYPE', COMPARE THE GROUP'S GROUP TYPE
      * TO THE USER'S SELECTED TYPE BEFORE PROCESSING THE DETAIL RECORD.
      *
     C     *LOVAL        SETLL     HSP084
      *
     C     *IN90         DOUEQ     *ON                                          DO SALE DOA
     C                   READ      HSP084                                 90
     C     *IN90         IFEQ      *OFF                                         IF SALE HDR
      *
      * PROCESS THE ORIGIN OR SHIPPED OUT SIDE OF THE SALE.
      * HARD-CODE THE DEATH/DISEASE REASON CODE TO INDICATE THAT
      * THESE DOA HOGS ARE FROM SALES.  THUS, THIS ORIGIN
      * SITE HAD HOGS ARRIVING DEAD AT THE CUSTOMER SITE.
      *
     C     SHMVSN        SETLL     HSP085
     C     *IN91         DOUEQ     *ON                                          DO SALE DTL
     C     SHMVSN        READE     HSP085                                 91
     C     *IN91         IFEQ      *OFF                                         IF SALE DTL
      *
      * Retrieve group info and compare to user selections.
      *
     C                   z-add     sghgsn        wkhgsn
     C                   exsr      $hgcd
      *
      * Process record
     C                   if        procfl = yes                                 If process
     C                   MOVEL     'DOA-S'       WFDRCD
      *
     C     KEY04         CHAIN     HSP385                             92
     C     *IN92         IFEQ      *OFF                                         IF FOUND
     C                   ADD       SGDOHD        WFKDHD
     C                   ADD       SGDOLB        WFKDLB
     C                   UPDATE    WFREC
     C                   ELSE
     C                   MOVEL     'DOA-S'       WFDRCD
     C                   MOVEL     HGPTCD        WFPTCD
     C                   MOVEL     HGPPCD        WFPPCD
     C                   Z-ADD     SGDOHD        WFKDHD
     C                   Z-ADD     SGDOLB        WFKDLB
     C                   Z-ADD     0             WFRJHD
     C                   EXSR      $DRDS
     C                   WRITE     WFREC
     C                   ENDIF                                                  IF FOUND
     C                   endif                                                  If process
      *
     C                   ENDIF                                                  IF SALE DTL
     C                   ENDDO                                                  DO SALE DTL
      *
     C                   ENDIF                                                  IF SALE HDR
     C                   ENDDO                                                  DO SALE DOA
      *
     C                   SETON                                        LR
      /EJECT
      *-------------------------------------------------------------------
      * Retrieve a group's group type and compare to the user's selection
      *-------------------------------------------------------------------
      *
     C     $gtcd         begsr
      *
     C     wkhgsn        chain     hsl034d                            92
     C                   if        *in92 = *on or hggtcd <> ldgtcd
     C                   move      no            procfl
     C                   endif
      *
     C                   endsr
      /EJECT
      *----------------------------------------------------------------
      * $DRDS - RETRIEVE THE DEATH REASON CODE DESCRIPTION
      *----------------------------------------------------------------
      *
     C     $DRDS         BEGSR
      *
     C                   SELECT
     C     WFDRCD        WHENEQ    'DOA-S'
     C                   MOVEL(P)  DOAS          WFDRDS
      *
     C     WFDRCD        WHENEQ    'DOA-T'
     C                   MOVEL(P)  DOAT          WFDRDS
     C                   OTHER
      *
     C     WFDRCD        CHAIN     HSP041                             92
     C     *IN92         IFEQ      *OFF
     C                   MOVEL(P)  DRDRDS        WFDRDS
     C                   ELSE
     C                   MOVEL(P)  'UNKNOWN'     WFDRDS
     C                   ENDIF
     C                   ENDSL
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * Retrieve group info and compare to user selections
      *---------------------------------------------------------------
      *
     C     $hgcd         begsr
      *
     C     wkhgsn        chain     hsl034d                            92
     C                   select
     C                   when      *in92 = *on
     C                   move      no            procfl
      *
     C                   when      (ldgtcd <> *blank and hggtcd <> ldgtcd) or
     C                             (ldptcd <> *blank and hgptcd <> ldptcd) or
     C                             (ldppcd <> *blank and hgppcd <> ldppcd)
     C                   move      no            procfl
     C                   other
      *
     C                   move      yes           procfl
     C                   endsl
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * *INZSR - INITIALIZATION SUBROUTINE
      *---------------------------------------------------------------
      *
     C     *INZSR        BEGSR
      *
      * KEY FOR WORKFILE USING KILLED/DEAD HOG DATA
      *
     C     K1P385        KLIST
     C                   KFLD                    HGPTCD
     C                   KFLD                    HGPPCD
     C                   KFLD                    KDDRCD
      *
      * KEY FOR WORKFILE FOR TRANSFER REJECTS
      *
     C     K2P385        KLIST
     C                   KFLD                    MHORPT
     C                   KFLD                    MHORPP
     C                   KFLD                    RJDRCD
      *
      * KEY FOR WORKFILE FOR DOA'S FROM TRANSFERS
      *
     C     K3P385        KLIST
     C                   KFLD                    MHORPT
     C                   KFLD                    MHORPP
     C                   KFLD                    WFDRCD
      *
      * KEY FOR WORKFILE FOR DOA'S FROM SALES
      *
     C     KEY04         KLIST
     C                   KFLD                    HGPTCD
     C                   KFLD                    HGPPCD
     C                   KFLD                    WFDRCD
      *
     C                   ENDSR
      /EJECT
