     A*%%TS  SD  20040413  105908  ISLRAMS     REL-V5R2M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD7709
     A*  TITLE:         VIEW ALL ITEMS FOR A BUDGET ITEM
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  DATE:          04/13/04
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A                                      CF04(04 'PROMPT')
     A                                      CF05(05 'REFRESH')
     A*
     A*
     A*---------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP7709S1                  SFL
     A*%%TS  SD  20040413  105908  ISLRAMS     REL-V5R2M0  5722-WDS
     A  85                                  SFLNXTCHG
     A            HDRRN1         4Y 0H
     A            D1ITCD    R        O  9  2REFFLD(HSRREF/@@ITCD *LIBL/HSPREF)
     A            D1ITDS    R        O  9 28REFFLD(@@ITDS)
     A            D1AIST    R        O  9 79REFFLD(HSRREF/@@AIST *LIBL/HSPREF)
     A*
     A*
     A*---------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP7709C1                  SFLCTL(HP7709S1)
     A*%%TS  SD  20040413  105908  ISLRAMS     REL-V5R2M0  5722-WDS
     A                                      SFLSIZ(0050)
     A                                      SFLPAG(0013)
     A N82                                  ROLLUP(25 'PAGE FORWARD')
     A                                      SETOF(53)
     A                                      RTNCSRLOC(&RCD &FLD)
     A                                      OVERLAY
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 26'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  2 20'VIEW ALL ITEMS FOR A BUDGET ITEM'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  4  2'Budget item:'
     A                                      DSPATR(HI)
     A            C1BGIT    R        O  4 16REFFLD(HSRREF/@@BGIT *LIBL/HSPREF)
     A                                  5  2'Description:'
     A                                      DSPATR(HI)
     A            C1BGITDS  R        O  5 16REFFLD(HSRREF/@@ITDS *LIBL/HSPREF)
     A                                  6 79'?'
     A                                      DSPATR(HI)
     A                                  7  2'Item Number'
     A                                      DSPATR(HI)
     A                                  7 28'Description'
     A                                      DSPATR(HI)
     A                                  7 78'A/I'
     A                                      DSPATR(HI)
     A            C1ITCD    R        B  8  2REFFLD(HSRREF/@@ITCD *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A            C1ITDS    R        B  8 28REFFLD(HSRREF/@@ITDS *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A            C1AIST    R        B  8 79REFFLD(HSRREF/@@AIST *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A  53                                  DSPATR(PC)
     A*
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R HP7709K1
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A                                 23 11'F4=Prompt'
     A                                 23 22'F5=Refresh'
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *---------------------------------------------------------------
     A          R HP7709ES                  SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE CONTROL FORMAT
      *---------------------------------------------------------------
     A          R HP7709EC                  SFLCTL(HP7709ES)
      *
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
