/********************************************************************/
/*                                                                  */
/*  SYNOPSIS - PRINT PROGRAM WITH SELECTIONS FOR EXPORT LOADS RPT.  */
/*             COMPANY, DATES, LOAD, CARRIER                        */
/*                                                                  */
/*  SYSTEM    - ORDER MANAGEMENT SYSTEM                             */
/*  PROGRAM   - PRINT EXPORT LOADS REPORT                           */
/*  DATE      - 12/16/96                                            */
/*                                                                  */
/*  PARAMETERS RECEIVED:                                            */
/*  &COMPNYN    - COMPANY NUMBER - BLANK FOR ALL                    */
/*  &FRMDATN    - FROM SHIP DATE                                    */
/*  &THRDATN    - THROUGH SHIP DATE                                 */
/*  &LOADN      - LOAD NUMBER                                       */
/*  &CARRIER    - CARRIER CODE                                      */
/*  &OUTQ       - OUTPUT QUEUE                                      */
/*  &HOLD       - HOLD PRINT                                        */
/*  &SAVE       - SAVE PRINT                                        */
/*  &COPYN      - NUMBER OF COPIES                                  */
/*  &NIGHTQ     - NIGHT OUTPUT QUEUE                                */
/*  &CUST       - SHIP TO CUST   (PASSED)                           */
/*  &COLLECT    - INCLUDE COLLECT TRUCKS Y OR N                     */
/*  &TRANSTYP1  - CUSTOMER TYPE CODE (2 CHAR)                       */
/*  &WHSETYPE   - WAREHOUSE TYPE (FR OR FZ)                         */
/*  &TYPECUST   - TYPE OF CUSTOMER (SHIP TO OR BILL TO)             */
/*  &LC         - SELECT L/C ENTERED OR NOT ENTERED OR ALL          */
/*                                                                  */
/*  NOTE:   &SUBJECT CAN ONLY BE 65 CHARACTERS MAX.                 */
/********************************************************************/
/*  RMC 1/02/2001  ADD SALESPERSON PARM, BUT ITS NOT USED IN PRTF   */
/*  RMC 6/27/2001  ADDED REASON PARM, BUT ITS NOT USED IN PRTF      */
/*  RMC 5/14/2004  REVERSED COMPANY/LOAD PARM ORDER,CO IS OPTIONAL  */
/*  SLM 1/06/2005  PASS IN THE SELECTION OF THE ORDER COMPANY       */
/*                 ORDER CO WILL BE SELECTED INSIDE THE REPORT      */
/*  LJB 12/07/2005 REMOVED &SALESP, &LOADT, &RPTSEL, &REASON,       */
/*                 &UOM WHICH WERE NOT BEING USED ANYWHERE.         */
/* 01/30/2008 PKD E00025 ADDED WAREHOUSE & COUNTRY, PASS TO REPORT  */
/* 08/22/2008 LJB E00210 ADD COLLECT Y/N TO SCREEN AND PASS         */
/* 11/14/2008 LJB E00269 ADD EMAIL ADDRESS                          */
/* 11/17/2008 LJB E00281 ADD MULTIPLE COUNTRY CODES                 */
/* 03/01/2011 RMC C01397 PASS 2 25 CHAR PARMS FOR EMAIL INSTEAD     */
/*                OF ONE 50                                         */
/* 04/29/2011 LJB P000737 ADD 5 MORE COUNTRY CODES AND ADD A TOTAL  */
/*                OF 10 CARRIER CODES. ADD REPORT SELECTION.        */
/*                ADD LOAD HAUL TYPE. ADD 5 SHIPPING LINES.         */
/*                PASS TO THE EXPORT LD RPT                         */
/* 12/29/2011 LJB E001788 ADD BOOKING NUMBER                        */
/* 02/12/2012 LJB E001704 ADD BROKER CODE FOR SUMIE'S REPORTS       */
/* 03/09/2012 LJB E001994 ADD SELECT EXPORT = 'Y' TO QRYSLT         */
/* 03/13/2012 LJB E001992 ADD NEW BOOKING REPORT AS &SELECT = "B"   */
/*                AND OPTION TO DOWNLOAD TO EXCEL.                  */
/*                (BOOKING REPORT ONLY, MULTIPLE OPTIONS)           */
/* 01/11/2013 LJB E002395 ADD TYPE CODE TO SELECT EXPORT SEABOARD   */
/*                BOOKINGS IF NEEDED. ADD TO EACH OF THE THREE      */
/*                REPORTS AND TO THE SEQUEL VIEW                    */
/* 05/08/2013 LJB E002587 ADD WAREHOUSE TYPE AND TYPE OF CUSTOMER   */
/* 07/24/2013 LJB E002587 REMOVED PARM LIST-NOT NEEDED SINCE THIS   */
/*                IS CALLED FROM ANOTHER CLP.                       */
/* 11/22/2013 LJB E002768 ADD SELECT IF LETTER OF CREDIT IS ENTERED,*/
/*                NOT ENTERED OR ALL                                */
/* 05/22/2015 LJB E004105 ADD 7 ADDITIONAL WAREHOUSE CODES          */
/* 07/18/2019 DN  R15161 - EXPORT LOAD AS400 UPDATES.               */
/*                CHANGED &BOOKING PARM FROM 12A TO 15A.            */
/* 01/17/2020 DN  S16050 - PASSED IN 9TH WAREHOUSE CODE AS INPUT    */
/*                PARM & PASS IT TO REPORT PROGRAMS.                */
/*                ADDED REPORT OPTION 'BOOKING RPT 4/EXPORT-LESS'   */
/*                (L) TO PRINT PUPCPFR REPORT AND/OR EXECUTE SEQUEL */
/*                PNGYCPPV4 & SEND AS XLS OR PDF.                   */
/* PIO 06/12/2023 REPLACE OPNQRYF WITH OPNSQLF FOR OMFJCPP FILE     */
/*                ADDED NEEDED OVRDBF AROUND THE COMMAND            */
/*                CHANGE *AND TO AND, *EQ TO =, *GE TO >=, *LE TO <=*/
/*                *GT TO > IN &E                                    */
/********************************************************************/

 PDUJPCLP:   PGM        PARM(&OUTQ &HOLD &SAVE &NIGHTQ &COMPNYN +
                          &FRMDATN &THRDATN &LOADN &COPYN +
                          &TRANSTYP1 &WHSETYPE &LDHAUL &TYPECUST +
                          &CUST &COLLECT &SELECT &LC &CARR1 &CARR2 +
                          &CARR3 &CARR4 &CARR5 &CARR6 &CARR7 &CARR8 +
                          &CARR9 &CARR10 &SHIP1 &SHIP2 &SHIP3 +
                          &SHIP4 &SHIP5 &CNTRY1 &CNTRY2 &CNTRY3 +
                          &CNTRY4 &CNTRY5 &CNTRY6 &CNTRY7 &CNTRY8 +
                          &CNTRY9 &CNTRY10 &WHS &WHS1 &WHS2 &WHS3 +
                          &WHS4 &WHS5 &WHS6 &WHS7 &WHS8 &EMAILYN +
                          &EMAIL1 &EMAIL2 &BOOKING &BROKER)

/* PARMS                                                         */
        DCL    &COMPNYN    *DEC        /* LOAD COMPANY   (PASSED)*/
        DCL    &FRMDATN    *DEC        /* FROM SHIP DATE (PASSED)*/
        DCL    &THRDATN    *DEC        /* THRU SHIP DATE (PASSED)*/
        DCL    &LOADN      *DEC        /* LOAD NUMBER    (PASSED)*/
        DCL    &CARR1      *CHAR   (3) /* CARRIER CODE         */
        DCL    &CARR2      *CHAR   (3) /* CARRIER CODE         */
        DCL    &CARR3      *CHAR   (3) /* CARRIER CODE         */
        DCL    &CARR4      *CHAR   (3) /* CARRIER CODE         */
        DCL    &CARR5      *CHAR   (3) /* CARRIER CODE         */
        DCL    &CARR6      *CHAR   (3) /* CARRIER CODE         */
        DCL    &CARR7      *CHAR   (3) /* CARRIER CODE         */
        DCL    &CARR8      *CHAR   (3) /* CARRIER CODE         */
        DCL    &CARR9      *CHAR   (3) /* CARRIER CODE         */
        DCL    &CARR10     *CHAR   (3) /* CARRIER CODE         */
        DCL    &SHIP1      *CHAR   (3) /* CARRIER CODE         */
        DCL    &SHIP2      *CHAR   (3) /* CARRIER CODE         */
        DCL    &SHIP3      *CHAR   (3) /* CARRIER CODE         */
        DCL    &SHIP4      *CHAR   (3) /* CARRIER CODE         */
        DCL    &SHIP5      *CHAR   (3) /* CARRIER CODE         */
        DCL    &CNTRY1     *CHAR   (6) /* COUNTRY              */
        DCL    &CNTRY2     *CHAR   (6) /* COUNTRY              */
        DCL    &CNTRY3     *CHAR   (6) /* COUNTRY              */
        DCL    &CNTRY4     *CHAR   (6) /* COUNTRY              */
        DCL    &CNTRY5     *CHAR   (6) /* COUNTRY              */
        DCL    &CNTRY6     *CHAR   (6) /* COUNTRY              */
        DCL    &CNTRY7     *CHAR   (6) /* COUNTRY              */
        DCL    &CNTRY8     *CHAR   (6) /* COUNTRY              */
        DCL    &CNTRY9     *CHAR   (6) /* COUNTRY              */
        DCL    &CNTRY10    *CHAR   (6) /* COUNTRY              */
        DCL    &WHS        *CHAR   (3) /* WAREHOUSE            */
        DCL    &WHS1       *CHAR   (3) /* WAREHOUSE            */
        DCL    &WHS2       *CHAR   (3) /* WAREHOUSE            */
        DCL    &WHS3       *CHAR   (3) /* WAREHOUSE            */
        DCL    &WHS4       *CHAR   (3) /* WAREHOUSE            */
        DCL    &WHS5       *CHAR   (3) /* WAREHOUSE            */
        DCL    &WHS6       *CHAR   (3) /* WAREHOUSE            */
        DCL    &WHS7       *CHAR   (3) /* WAREHOUSE            */
        DCL    &WHS8       *CHAR   (3) /* WAREHOUSE            */
        DCL    &LDHAUL     *CHAR   (3) /* LOAD HAUL TYPE       */
        DCL    &TRANSTYP1  *CHAR   (2) /* CUSTOMER TYPE CODE   */
        DCL    &WHSETYPE   *CHAR   (2) /* WAREHOUSE TYPE       */
        DCL    &TYPECODE   *CHAR   (2) /* SQL VIEW TYPE CODE   */
        DCL    &TYPECUST   *CHAR   (1)
        DCL    &EMAIL1     *CHAR   (25)
        DCL    &EMAIL2     *CHAR   (25)
        DCL    &EMAIL      *CHAR   (50)
        DCL    &EMAILYN    *CHAR   (1)
        DCL    &BROKER     *CHAR   (6)   /* BROKER CODE        */
        DCL    &OUTQ       *CHAR   (10)  /* OUTPUT QUEUE       */
        DCL    &HOLD       *CHAR   (4)   /* HOLD STATUS        */
        DCL    &SAVE       *CHAR   (4)   /* SAVE STATUS        */
        DCL    &COPYN      *DEC          /* NO. OF COPIES      */
        DCL    &NIGHTQ     *CHAR   (10)  /* NIGHT JOB QUEUE    */
        DCL    &CUST       *DEC          /* SHIP TO CUSTOMER   */
        DCL    &RETURN     *CHAR   (7)   /* RETURN CODE        */
        DCL    &COLLECT    *CHAR   (1)   /* COLLECT Y/N        */
        DCL    &SELECT     *CHAR   (1)   /* REPORT SELECT TYPE */
        DCL    &LC         *CHAR   (1)   /* SELECT L/C         */
        DCL    &COUNT      *DEC    (1 0) /* COLLECT Y/N        */
        DCL    &PRTF       *CHAR   (10)  /* PRINT FILE NAME    */
        DCL    &SUBJECT    *CHAR   (60)  /* EMAIL SUBJECT      */
        DCL    &BOOKING    *CHAR   (15)  /* BOOKING NUMBER     */

/* INTERNAL FIELDS                                               */
        DCL    &COMMAND    *CHAR   (512)
        DCL    &COPY       *DEC    (2 0) /* NUMBER OF COPIES   */
        DCL    &COMPANY    *DEC    (3 0) /* COMPANY NUMBER     */
        DCL    &FRMDAT     *DEC    (7 0) /* FROM SHIP DATE     */
        DCL    &FDMMDDYY   *CHAR   (8)   /* FROM SHIP DATE ALPHA */
        DCL    &THRDAT     *DEC    (7 0) /* THRU SHIP DATE     */
        DCL    &TDMMDDYY   *CHAR   (8)   /* THRU SHIP DATE ALPHA */
        DCL    &LOAD       *DEC    (7 0) /* LOAD NUMBER        */
        DCL    &TYPECDE    *CHAR   (4)   /* TYPE CODE FOR SQL  */
        DCL    &EMLMSG     *CHAR   (120) /* EMAIL MESSAGE      */

/* OPENQRYF FIELDS                                                    */
        DCL    &COMPA      *CHAR    3    /* ACK COMPLETE       */
        DCL    &FRMDATA    *CHAR    7    /* FROM SHIP DATE (PASSED)  */
        DCL    &THRDATA    *CHAR    7    /* THRU SHIP DATE (PASSED)  */
        DCL     &A        *CHAR   20     /* QRY FROM DATE      */
        DCL     &B        *CHAR   25     /* QRY TO DATE        */
        DCL     &C        *CHAR   25     /* QRY COMPANY        */
        DCL     &D        *CHAR   25     /* QRY EXPORT=Y       */
        DCL     &E        *CHAR  100     /* QRY ALL            */

    /* S16050 - COMMENTED OUT OTHERWISE ALL ERRORS WOULD BE IGNORED. */
    /*       MONMSG     MSGID(CPF0000)                              */

/********************************************************************/
/* CHANGE VARIABLES FOR USE IN PROGRAM                              */
/********************************************************************/
             CHGVAR     VAR(&COMPANY) VALUE(&COMPNYN)
             CHGVAR     VAR(&COMPA) VALUE(&COMPANY)
             CHGVAR     VAR(&FRMDAT) VALUE(&FRMDATN)
             CHGVAR     VAR(&FRMDATA) VALUE(&FRMDAT)
             CHGVAR     VAR(&THRDAT) VALUE(&THRDATN)
             CHGVAR     VAR(&THRDATA) VALUE(&THRDAT)
             CHGVAR     VAR(&LOAD) VALUE(&LOADN)
             CHGVAR     VAR(&COPY) VALUE(&COPYN)
             CHGVAR     VAR(&EMAIL) VALUE(&EMAIL1 *CAT &EMAIL2)
             CHGVAR     VAR(&COUNT) VALUE(0)

/*********************************************************************/
/* CREATE OPEN QUERY FILE TO BE USED FOR REPORTS                     */
/* SELECT ON DATE RANGE AND/OR COMPANY NUMBER                        */
/*********************************************************************/
/* SELECT SCHEDULED SHIP DATE                                        */
  /*PIO      CHGVAR     VAR(&A) VALUE('FJAODT *GE ' *CAT + +
                         &FRMDATA)                        */
  /*PIO*/    CHGVAR     VAR(&A) VALUE('FJAODT  >= ' *CAT +
                         &FRMDATA)
  /*PIO      CHGVAR     VAR(&B) VALUE('*AND FJAODT *LE ' *CAT + +
                         &THRDATA)                             */
  /*PIO*/    CHGVAR     VAR(&B) VALUE(' AND FJAODT <=  ' *CAT +
                         &THRDATA)
/* SELECT COMPANY NUMBER AND SELECT EXPORTS ONLY                     */
             IF         (&COMPA *NE '000') THEN(DO)
  /*PIO      CHGVAR     VAR(&C) VALUE('*AND FJAIC3 *EQ ' *CAT +  +
                         &COMPA)                                */
  /*PIO*/    CHGVAR     VAR(&C) VALUE(' AND FJAIC3  =  ' *CAT +
                         &COMPA)
             ENDDO

  /*PIO      CHGVAR     VAR(&D) VALUE('*AND FJU2ST *EQ ' *BCAT '"Y"') */
  /*PIO*/    CHGVAR     VAR(&D) VALUE(' AND FJU2ST  =  ' *BCAT '"Y"')
/* BUILD QUERY SELECT                                                */
             CHGVAR     VAR(&E) VALUE(&A *BCAT &B *BCAT &C *BCAT &D)

/*  RUN QUERY TO READ LOAD HEADER BY DATE RANGE, EXPORTS ONLY       */
   /*PIO     OVRDBF     FILE(OMFJCPL4) SHARE(*YES)   +
             OPNQRYF    FILE(OMFJCPL4) +             +
                        QRYSLT(&E) +                 +
                        KEYFLD(*FILE)               */

  /*PIO*/    OVRDBF     FILE(OMFJCPP) TOFILE(OMFJCPP) LVLCHK(*NO)
             OPNSQLF    SQL('Select * from OMFJCPP Where  ' *BCAT &E +
                          *BCAT ' ORDER BY FJAODT ASC, FJBZNA ASC') +
                          OPNID(OMFJCPL4) OPTION(*ALL) SEQONLY(*NO) +
                          RCDFMT(@FJCPL9) ALWCPY(*IFRQD) /*PIO*/
  /*PIO*/    OVRDBF     FILE(OMFJCPL4) TOFILE(OMFJCPP) LVLCHK(*NO) +
                           OVRSCOPE(*JOB) SHARE(*YES) OPNSCOPE(*JOB)


/********************************************************************/
/* OVERRIDE PRINTER FILE WITH USER SPECIFIED SELECTION VALUES       */
/* CALL ORIGINAL EXPORT LOAD REPORT                                  */
/*      ADD 7 MORE WAREHOUSE CODES    E004105                        */
/********************************************************************/
 ORIG:       IF         COND(&SELECT *EQ 'E') THEN(DO)
             CHGVAR     VAR(&PRTF) VALUE(PDUIPFR$)
             SBDPRTOVR  CMDVAR(&COMMAND) FILE(&PRTF) OUTQ(&OUTQ) +
                          COPIES(&COPY) HOLD(&HOLD) SAVE(&SAVE)
             CALL       PGM(QCMDEXC) PARM(&COMMAND 512)

    /* 01/17/20 DN S16050-ADDED &WHS8 AS PARM. */
             CALL       PGM(PDUIPFR) PARM(&RETURN &LOAD &COMPANY +
                          &FRMDAT &BOOKING &THRDAT &CUST &BROKER +
                          &CNTRY1 &CNTRY2 &CNTRY3 &CNTRY4 &CNTRY5 +
                          &CNTRY6 &CNTRY7 &CNTRY8 &CNTRY9 &CNTRY10 +
                          &TRANSTYP1 &WHSETYPE &LDHAUL &TYPECUST +
                          &COLLECT &LC &CARR1 &CARR2 &CARR3 &CARR4 +
                          &CARR5 &CARR6 &CARR7 &CARR8 &CARR9 +
                          &CARR10 &SHIP1 &SHIP2 &SHIP3 &SHIP4 +
                          &SHIP5 &WHS &WHS1 &WHS2 &WHS3 &WHS4 &WHS5 +
                          &WHS6 &WHS7 &WHS8)

/* EMAIL THE REPORT IF REQUESTED                                     */
             IF         COND((&EMAILYN *EQ 'Y') *OR (&EMAILYN *EQ +
                          'E')) THEN(DO)
             IF         COND(&SELECT *EQ 'E') THEN(CHGVAR +
                          VAR(&SUBJECT) VALUE('Export Load Report'))
 RETRYE:     ESNDMAIL   RECIPIENT(&EMAIL) SUBJECT(&SUBJECT) +
                         MSGTYPE(*DFT) ATTLIST((&PRTF *DFT *N &PRTF))

/* IF ERROR, RETRY TWICE THEN SKIP OUT (PREVENT LOOPING FOREVER)     */
             MONMSG     MSGID(EML0023) EXEC(DO)
                CHGVAR  VAR(&COUNT) VALUE(&COUNT + 1)
                IF      COND(&COUNT *GT 2) THEN(GOTO CMDLBL(CLOSEF))
                GOTO    CMDLBL(RETRYE)
             ENDDO      /* END MONMSG  */
             ENDDO      /* END EMAILYN */

             ENDDO      /* END SELECT *EQ 'E' */

/*********************************************************************/
/* OVERRIDE PRINTER FILE WITH USER SPECIFIED SELECTION VALUES       */
/* CALL EXPORT LOAD REPORT WITH AMOUNT AND WEIGHTS                   */
/*      ADD 7 MORE WAREHOUSE CODES    E004105                        */
/*********************************************************************/
 WEIGHTS:    IF         COND(&SELECT *EQ 'W') THEN(DO)
             CHGVAR     VAR(&PRTF) VALUE(PNW5PFR$)
             SBDPRTOVR  CMDVAR(&COMMAND) FILE(&PRTF) OUTQ(&OUTQ) +
                          COPIES(&COPY) HOLD(&HOLD) SAVE(&SAVE)
             CALL       PGM(QCMDEXC) PARM(&COMMAND 512)

    /* 01/17/20 DN S16050-ADDED &WHS8 AS PARM. */
             CALL       PGM(PNW5PFR) PARM(&RETURN &LOAD &COMPANY +
                          &FRMDAT &BOOKING &THRDAT &CUST &BROKER +
                          &CNTRY1 &CNTRY2 &CNTRY3 &CNTRY4 &CNTRY5 +
                          &CNTRY6 &CNTRY7 &CNTRY8 &CNTRY9 &CNTRY10 +
                          &TRANSTYP1 &WHSETYPE &LDHAUL &TYPECUST +
                          &COLLECT &LC &CARR1 &CARR2 &CARR3 &CARR4 +
                          &CARR5 &CARR6 &CARR7 &CARR8 &CARR9 +
                          &CARR10 &SHIP1 &SHIP2 &SHIP3 &SHIP4 +
                          &SHIP5 &WHS &WHS1 &WHS2 &WHS3 &WHS4 &WHS5 +
                          &WHS6 &WHS7 &WHS8)

/* EMAIL THE REPORT IF REQUESTED                                     */
             IF         COND((&EMAILYN *EQ 'Y') *OR (&EMAILYN *EQ +
                          'E')) THEN(DO)
             IF         COND(&SELECT *EQ 'W') THEN(CHGVAR +
                          VAR(&SUBJECT) VALUE('Export Load Report +
                          with Amounts/Weights'))
 RETRYW:     ESNDMAIL   RECIPIENT(&EMAIL) SUBJECT(&SUBJECT) +
                         MSGTYPE(*DFT) ATTLIST((&PRTF *DFT *N &PRTF))

/* IF ERROR, RETRY TWICE THEN SKIP OUT (PREVENT LOOPING FOREVER)     */
             MONMSG     MSGID(EML0023) EXEC(DO)
                CHGVAR  VAR(&COUNT) VALUE(&COUNT + 1)
                IF      COND(&COUNT *GT 2) THEN(GOTO CMDLBL(CLOSEF))
                GOTO    CMDLBL(RETRYW)
             ENDDO      /* END MONMSG  */
             ENDDO      /* END EMAILYN */

             ENDDO      /* END SELECT *EQ 'W'  */

/*********************************************************************/
/* OVERRIDE PRINTER FILE WITH USER SPECIFIED SELECTION VALUES        */
/* CALL BOOKING REPORT FOR EXPORT LOADS                              */
/* CREATE PNGYCPP IN QTEMP. IF IT ALREADY EXISTS, CLEAR THE FILE     */
/* EXECUTE SEQUEL VIEW TO CREATE A SPREADSHEET OF THE RESULTS OF     */
/* THE REPORT THEN EMAIL TO SELECTED RECIPIENT.                      */
/*      ADD 7 MORE WAREHOUSE CODES    E004105                        */
/*********************************************************************/
/* S16050 - ADDED SELECT REPORT OPTION 'L' - BOOKING RPT 4/EXPORT-LESS */
 BOOKING:    IF         COND((&SELECT *EQ 'B') *OR (&SELECT *EQ 'C') +
                          *OR (&SELECT *EQ 'L')) THEN(DO)

/***    GOTO BLDWRKFIL            ****/

/* S16050 - ADDED SELECT REPORT OPTION 'L'.                */
             IF         COND(&SELECT *NE 'L') THEN(DO)     /* S16050 */
BLDWRKFIL:  CHGVAR     VAR(&PRTF) VALUE(PBCSPFR$)
             ENDDO                                         /* S16050 */
             ELSE       CMD(DO)                            /* S16050 */
             CHGVAR     VAR(&PRTF) VALUE(PUPCPFR$)         /* REPORT = 'L' */
             ENDDO                                         /* S16050 */

             SBDPRTOVR  CMDVAR(&COMMAND) FILE(&PRTF) OUTQ(&OUTQ) +
                          COPIES(&COPY) HOLD(&HOLD) SAVE(&SAVE)
             CALL       PGM(QCMDEXC) PARM(&COMMAND 512)

             CRTDUPOBJ  OBJ(PNGYCPP) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)
             MONMSG     MSGID(CPD2104 CPF2130) EXEC(CLRPFM +
                          FILE(PNGYCPP))
             CHGPF      FILE(QTEMP/PNGYCPP) LVLCHK(*NO)
             OVRDBF     FILE(PNGYCPP) TOFILE(QTEMP/PNGYCPP)
             CRTDUPOBJ  OBJ(PNGYCPL0) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)
             MONMSG     MSGID(CPD2104 CPF2130)
             CHGLF      FILE(QTEMP/PNGYCPL0) LVLCHK(*NO)
             OVRDBF     FILE(PNGYCPL0) TOFILE(QTEMP/PNGYCPL0)
             CRTDUPOBJ  OBJ(PNGYCPL1) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)
             MONMSG     MSGID(CPD2104 CPF2130)
             CHGLF      FILE(QTEMP/PNGYCPL1) LVLCHK(*NO)
             OVRDBF     FILE(PNGYCPL1) TOFILE(QTEMP/PNGYCPL1)
             CRTDUPOBJ  OBJ(PNGYCPL2) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)
             MONMSG     MSGID(CPD2104 CPF2130)
             CHGLF      FILE(QTEMP/PNGYCPL2) LVLCHK(*NO)
             OVRDBF     FILE(PNGYCPL2) TOFILE(QTEMP/PNGYCPL2)
             CRTDUPOBJ  OBJ(PNGYCPL3) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)
             MONMSG     MSGID(CPD2104 CPF2130)
             CHGLF      FILE(QTEMP/PNGYCPL3) LVLCHK(*NO)
             OVRDBF     FILE(PNGYCPL3) TOFILE(QTEMP/PNGYCPL3)

/* S16050 - ADDED SELECT REPORT OPTION 'L'.                */
             IF         COND(&SELECT *NE 'L') THEN(DO)     /* S16050 */
    /* 01/17/20 DN S16050-ADDED &WHS8 AS PARM. */
             CALL       PGM(PBCSPFR) PARM(&RETURN &LOAD &COMPANY +
                          &FRMDAT &BOOKING &THRDAT &CUST &CNTRY1 +
                          &CNTRY2 &CNTRY3 &CNTRY4 &CNTRY5 &CNTRY6 +
                          &CNTRY7 &CNTRY8 &CNTRY9 &CNTRY10 &BROKER +
                          &TRANSTYP1 &WHSETYPE &LDHAUL &TYPECUST +
                          &COLLECT &LC &CARR1 &CARR2 &CARR3 &CARR4 +
                          &CARR5 &CARR6 &CARR7 &CARR8 &CARR9 +
                          &CARR10 &SHIP1 &SHIP2 &SHIP3 &SHIP4 +
                          &SHIP5 &WHS &WHS1 &WHS2 &WHS3 &WHS4 &WHS5 +
                          &WHS6 &WHS7 &WHS8)
             ENDDO                                         /* S16050 */
             ELSE       CMD(DO)                            /* S16050 */
/* S16050 - PUPCPFR IS A CLONED OF PBCSPFR REPORT BUT WITH 'LESS' INFO. */
             CALL       PGM(PUPCPFR) PARM(&RETURN &LOAD &COMPANY +
                          &FRMDAT &BOOKING &THRDAT &CUST &CNTRY1 +
                          &CNTRY2 &CNTRY3 &CNTRY4 &CNTRY5 &CNTRY6 +
                          &CNTRY7 &CNTRY8 &CNTRY9 &CNTRY10 &BROKER +
                          &TRANSTYP1 &WHSETYPE &LDHAUL &TYPECUST +
                          &COLLECT &LC &CARR1 &CARR2 &CARR3 &CARR4 +
                          &CARR5 &CARR6 &CARR7 &CARR8 &CARR9 +
                          &CARR10 &SHIP1 &SHIP2 &SHIP3 &SHIP4 +
                          &SHIP5 &WHS &WHS1 &WHS2 &WHS3 &WHS4 &WHS5 +
                          &WHS6 &WHS7 &WHS8)               /* S16050 */
             ENDDO                                         /* S16050 */

/***ˆFOR TESTING                                +
             CPYF       FROMFILE(QTEMP/PNGYCPP) +
                          TOFILE(OMSDEVGEN/PNGYCPPSV) +
                          MBROPT(*REPLACE) CRTFILE(*YES)  */

/* S16050 - REPORT OPTION='L' BOOKING REPORT FOR EXPORT-LESS INFO */
             IF         COND((&SELECT *EQ 'L') *AND (&EMAILYN *NE ' +
                          ')) THEN(DO)

             CHGVAR     VAR(&TYPECODE) VALUE(&TRANSTYP1)

             IF         COND(&TRANSTYP1 *EQ ' ') THEN(DO)
             CHGVAR     VAR(&TYPECDE) VALUE('*ALL')
             ENDDO
             IF         COND(&TRANSTYP1 *NE ' ') THEN(DO)
             CHGVAR     VAR(&TYPECDE) VALUE('"' *CAT &TRANSTYP1 *CAT +
                          '"')
             ENDDO

    /* Convert CYMD to MM/DD/YY Date Format */
             CALL       PGM(PUJLXFR) PARM('' &FRMDAT &FDMMDDYY)  /*From Date*/
             CALL       PGM(PUJLXFR) PARM('' &THRDAT &TDMMDDYY)  /*Thru Date*/

             CHGVAR     VAR(&EMLMSG) VALUE('Booking Report for +
                          Export Loads - Less Info; From Date' +
                          *BCAT &FDMMDDYY *BCAT '- ' *BCAT &TDMMDDYY)

             IF         COND(&TRANSTYP1 *NE '  ') THEN(DO)
             CHGVAR     VAR(&EMLMSG) VALUE(&EMLMSG *BCAT '; For Type +
                          Code=' *CAT &TRANSTYP1)
             ENDDO

    /* Send as XLS via Email */
             IF         COND(&EMAILYN *EQ 'S') THEN(DO)

 EXCEL4:     EXECUTE    VIEW(PNGYCPPV4) MBROPT(*REPLACE) PCFMT(*XLS) +
                          RECIPIENT(&EMAIL) EMLMSG(&EMLMSG) +
                          SETVAR((&TYPECODE &TYPECDE))

             ENDDO      /* &EMAILYN = 'S' */

    /* Send as PDF via Email */
             IF         COND((&EMAILYN *EQ 'Y') *OR (&EMAILYN *EQ +
                          'E')) THEN(DO)

             CHGVAR     VAR(&SUBJECT) VALUE('Booking Report for +
                          Export Loads - Less Info')

 RETRYL:     ESNDMAIL   RECIPIENT(&EMAIL) SUBJECT(&SUBJECT) +
                          MSG(&EMLMSG) MSGTYPE(*DFT) ATTLIST((&PRTF +
                          *DFT *N &PRTF))

/* IF ERROR, RETRY TWICE THEN SKIP OUT (PREVENT LOOPING FOREVER)     */
             MONMSG     MSGID(EML0023) EXEC(DO)
                CHGVAR  VAR(&COUNT) VALUE(&COUNT + 1)
                IF      COND(&COUNT *GT 2) THEN(GOTO CMDLBL(CLOSEF))
                GOTO    CMDLBL(RETRYL)
             ENDDO      /* MONMSG       */

             ENDDO      /* &EMAILYN = Y */

             GOTO       CMDLBL(CLOSEF)

             ENDDO      /* &SELECT = 'L' */


/* DOWNLOAD THE BOOKING REPORT TO EXCEL IF REQUESTED-ALL TYPE CODES  */
             IF         COND(&EMAILYN *EQ 'S') THEN(DO)

             IF         COND(&TRANSTYP1 *EQ '  ') THEN(DO)
             CHGVAR     VAR(&SUBJECT) VALUE('Download Booking Report +
                          for Export Loads')
 EXCEL:      EXECUTE    VIEW(PNGYCPPV1) MBROPT(*REPLACE) PCFMT(*XLS) +
                          RECIPIENT(&EMAIL) EMLMSG('Booking Report +
                          for Export Loads for Date Range' *BCAT +
                          &FRMDATA *BCAT 'through ' *BCAT &THRDATA)
             ENDDO      /* END TRANSTYP1 *EQ *BLANKS */

/* DOWNLOAD THE BOOKING REPORT TO EXCEL IF REQUESTED                 */
/* SELECTED TYPE CODES (I.E. ES FOR EXPORT SEABOARD BOOKING)         */
             IF         COND(&TRANSTYP1 *NE '  ') THEN(DO)
             CHGVAR     VAR(&TYPECODE) VALUE(&TRANSTYP1)

/* BOOKING REPORT FOR EXPORTS                                        */
             IF         COND(&SELECT *EQ 'B') THEN(DO)
             CHGVAR     VAR(&SUBJECT) VALUE('Download Booking Report +
                          for Export Loads for Type Code = ' *CAT +
                          &TRANSTYP1)

 EXCEL1:     EXECUTE    VIEW(PNGYCPPV2) MBROPT(*REPLACE) PCFMT(*XLS) +
                          RECIPIENT(&EMAIL) EMLMSG('Booking Report +
                          for Export Loads for Date Range' *BCAT +
                          &FRMDATA *BCAT 'through ' *BCAT &THRDATA) +
                          SETVAR((&TYPECODE &TYPECODE))
             ENDDO      /* END  SUBJECT = B          */

/*-------------------------------------------------------------------*/
/* BOOKING REPORT FOR EXPORT COORDINATOR             E002984         */
             IF         COND(&SELECT *EQ 'C') THEN(DO)
             CHGVAR     VAR(&SUBJECT) VALUE('Download Booking Report +
                          for Export Coordinator for Type Code = ' +
                          *CAT &TRANSTYP1)

 EXCEL2:     EXECUTE    VIEW(PNGYCPPV3) MBROPT(*REPLACE) PCFMT(*XLS) +
                          RECIPIENT(&EMAIL) EMLMSG('Export +
                          Coordinator Booking Report for Date +
                          Range' *BCAT &FRMDATA *BCAT 'through ' +
                          *BCAT &THRDATA) SETVAR((&TYPECODE &TYPECODE))
             ENDDO      /* END  SUBJECT = C          */
             ENDDO      /* END TRANSTYP1 *NE *BLANKS */

             ENDDO      /* END EMAILYN *EQ 'S' */
             ENDDO      /* END SELECT 'B' BOOKING REPORT */

/*********************************************************************/
 CLOSEF:     CLOF       OPNID(OMFJCPL4)
    /*PIO    DLTOVR     FILE(OMFJCPL4)  */
             DLTOVR     FILE(OMFJCPL4) LVL(*JOB) /*PIO*/

/* S16050 - ADDED SELECT REPORT OPTION 'C' & 'L'.                */
             IF         COND((&SELECT *EQ 'B') *OR (&SELECT *EQ 'C') +
                          *OR (&SELECT *EQ 'L')) THEN(DO)
             DLTF       FILE(QTEMP/PNGYCPL0)
             DLTF       FILE(QTEMP/PNGYCPL1)
             DLTF       FILE(QTEMP/PNGYCPL2)
             DLTF       FILE(QTEMP/PNGYCPL3)               /* S16050 */
             DLTF       FILE(QTEMP/PNGYCPP)
             ENDDO                 /* END CLOSE IF SELECT='B' ***** */

 END:        ENDPGM
