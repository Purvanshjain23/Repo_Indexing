     A*%%TS  SD  20071227  102125  ISLRAMS     REL-V5R4M0  5722-WDS
     A*****************************************************************
     A*
     A*  ENVIRONMENT:   PORK DIVISION
     A*  SYSTEM:        RESOURCE OPTIMIZATION
     A*  DISPLAY FILE:  ROD711 - VIEW HAM GROUP YIELD BREAKDOWN
     A*  CREATED BY:    LEANNE RAMSEY
     A*  DATE:          11/09/07
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/ROPREF)
     A                                      PRINT
     A                                      CF03(03 'EXIT')
     A N82                                  ROLLUP(25 'PAGE FORWARD')
     A*---------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*---------------------------------------------------------------
     A          R RO711S1                   SFL
     A*%%TS  SD  20071227  102125  ISLRAMS     REL-V5R4M0  5722-WDS
     A            HDRRN1    R        H      REFFLD(@@RRN)
     A            D1CIITCD  R        O  9  2REFFLD(@@CIITCD)
     A                                      EDTCDE(Z)
     A            D1CICGCD  R        O  9 14REFFLD(@@CICGCD)
     A            D1CISYPC  R        O  9 22REFFLD(ROREF/@@SYPC *LIBL/ROPREF)
     A                                      EDTCDE(K)
     A*
     A*---------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*---------------------------------------------------------------
     A          R RO711C1                   SFLCTL(RO711S1)
     A*%%TS  SD  20071227  102125  ISLRAMS     REL-V5R4M0  5722-WDS
     A                                      SFLSIZ(0100)
     A                                      SFLPAG(0008)
     A                                      WINDOW(RO711EC)
     A                                      OVERLAY
     A                                      SFLCSRRRN(&HDRRN)
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A            HDPAG1    R        H      REFFLD(@@PAG)
     A                                      SFLRCDNBR
     A            HDRRN     R        H      REFFLD(@@RRN)
     A            SDPGM     R        O  1  1REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  2  1'VIEW HAM GROUP YIELD BREAKDOWN'
     A                                      DSPATR(HI)
     A                                  4  1'Item code:'
     A            C1ITCD    R        O  4 12REFFLD(ROREF/@@ITCD *LIBL/ROPREF)
     A                                      EDTCDE(Z)
     A                                      DSPATR(HI)
     A            C1SYPC    R        O  4 22REFFLD(ROREF/@@SYPC *LIBL/ROPREF)
     A                                      EDTCDE(K)
     A  87                              4 32'%'
     A                                  6  1'Component'
     A                                      DSPATR(HI)
     A                                  6 14'Meat'
     A                                      DSPATR(HI)
     A                                  7  4'Item'
     A                                      DSPATR(HI)
     A                                  7 14'Cost'
     A                                      DSPATR(HI)
     A                                  7 25'Sales'
     A                                      DSPATR(HI)
     A                                  8  4'Code'
     A                                      DSPATR(HI)
     A                                  8 14'Group'
     A                                      DSPATR(HI)
     A                                  8 23'Yield %'
     A                                      DSPATR(HI)
     A*---------------------------------------------------------------
     A* COMMAND LINE LINE
     A*---------------------------------------------------------------
     A          R RO711K1
     A                                      WINDOW(RO711EC)
     A                                      OVERLAY
     A  43                             17  1'Message program failed. Call IS.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 18  1'F3=Exit'
      *
     A*-------------------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
     A*-------------------------------------------------------------------------
     A          R RO711ES                   SFL SFLMSGRCD(19)
      *
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
     A*
     A*-------------------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*-------------------------------------------------------------------------
     A*
     A          R RO711EC                   SFLCTL(RO711ES)
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A                                      WINDOW(*DFT 20 32)
     A                                      WDWBORDER((*DSPATR RI) (*CHAR '    -
     A                                          '))
     A            MSGPGM                    SFLPGMQ(10)
     A*
     A*---------------------------------------------------------------
     A* ASSUME FORMAT -- REQUIRED FOR WINDOWS
     A*---------------------------------------------------------------
     A          R ASSUME
     A                                      ASSUME
     A                                      OVERLAY
     A                                 24 79' '
