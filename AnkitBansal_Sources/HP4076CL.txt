/***************************************************************** */
/*                                                                 */
/*  PROGRAM NUMBER:  HP4076CL                                      */
/*  PROGRAM NAME:    FARMS/BINS LISTING                            */
/*  PROGRAMMER:      LEANNE FEDOR                                  */
/*  CREATE DATE:     12/06/00                                      */
/*                                                                 */
/*  FUNCTION:        THIS CL IS SUBMITTED WITH QCMDEXC FROM        */
/*                   HP4076-SPECIFY OPTIONS FOR FARMS/BINS LISTING */
/*                                                                 */
/*                   OPTIONS ARE PASSED THROUGH THE LDA.           */
/*                                                                 */
/*******************************************************************/
/*  MODIFIED:                                                      */
/*******************************************************************/

/*******************************************************************/

             PGM

/* DEFINE THE VARIABLE REQUIRED IN THE COMMAND TO OVERRIDE THE     */
/* PRINT FILE TO A LASER PRINTER                                   */

             DCL        VAR(&CMD) TYPE(*CHAR) LEN(512)
             DCL        VAR(&QDATA) TYPE(*CHAR) LEN(145)

             DCL        VAR(&LDFSBO) TYPE(*CHAR) LEN(5)
             DCL        VAR(&LDPTCD) TYPE(*CHAR) LEN(5)
             DCL        VAR(&LDPPCD) TYPE(*CHAR) LEN(5)
             DCL        VAR(&LDCELL) TYPE(*DEC) LEN(2)
             DCL        VAR(&LDFSCD) TYPE(*DEC) LEN(5)
             DCL        VAR(&LDRMST) TYPE(*CHAR) LEN(1)
             DCL        VAR(&LDFSST) TYPE(*CHAR) LEN(1)

             DCL        VAR(&HOLD) TYPE(*CHAR) LEN(10)
             DCL        VAR(&COPYS) TYPE(*DEC) LEN(2)
             DCL        VAR(&OUTQ) TYPE(*CHAR) LEN(10)

             CHGVAR     VAR(&LDFSBO) VALUE(%SST(*LDA 1 5))
             CHGVAR     VAR(&LDPTCD) VALUE(%SST(*LDA 6 5))
             CHGVAR     VAR(&LDPPCD) VALUE(%SST(*LDA 11 5))
             CHGVAR     VAR(&LDCELL) VALUE(%SST(*LDA 16 2))
             CHGVAR     VAR(&LDFSCD) VALUE(%SST(*LDA 18 5))
             CHGVAR     VAR(&LDRMST) VALUE(%SST(*LDA 119 1))
             CHGVAR     VAR(&LDFSST) VALUE(%SST(*LDA 140 1))



/*-------------------------------------------------------------------*/
/*  SETUP QUERY OVER THE FARM SITE MASTER                            */
/*-------------------------------------------------------------------*/


/*  SET A DEFAULT STARTING POINT                                     */

             CHGVAR     VAR(%SST(&QDATA 1 12)) VALUE('FSFSCD *NE 0')


/* BUSINESS OFFICE                                                     */

             IF         COND(&LDFSBO *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 14 17)) VALUE('*AND FSFSBO *EQ "')
             CHGVAR     VAR(%SST(&QDATA 31 5)) VALUE(&LDFSBO)
             CHGVAR     VAR(%SST(&QDATA 36 1)) VALUE('"')
             ENDDO

/* PRODUCTION PHASE                                                      */

             IF         COND(&LDPPCD *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 38 17)) VALUE('*AND FSPPCD +
                          *EQ "')
             CHGVAR     VAR(%SST(&QDATA 55 5)) VALUE(&LDPPCD)
             CHGVAR     VAR(%SST(&QDATA 60 1)) VALUE('"')
             ENDDO

/* PRODUCTION TYPE                                         */

             IF         COND(&LDPTCD *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA  62 17)) VALUE('*AND FSPTCD +
                          *EQ "')
             CHGVAR     VAR(%SST(&QDATA 79 5)) VALUE(&LDPTCD)
             CHGVAR     VAR(%SST(&QDATA 84 1)) VALUE('"')
             ENDDO

/* CELL                                                       */
             IF         COND(&LDCELL *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 86 16)) VALUE('*AND FSCELL +
                           *EQ ')
             CHGVAR     VAR(%SST(&QDATA 102 2)) VALUE(&LDCELL)
             ENDDO


/* FARM SITE                                                  */

             IF         COND(&LDFSCD *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 105 16)) VALUE('*AND FSFSCD +
                           *EQ ')
             CHGVAR     VAR(%SST(&QDATA 121 5)) VALUE(&LDFSCD)
             ENDDO

/* FARM STATUS                                                */

             IF         COND(&LDFSST *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 127 17)) VALUE('*AND FSAIST +
                          *EQ "')
             CHGVAR     VAR(%SST(&QDATA 144 1)) VALUE(&LDFSST)
             CHGVAR     VAR(%SST(&QDATA 145 1)) VALUE('"')
             ENDDO

             OVRDBF     FILE(HSL018J) SHARE(*YES)
             OPNQRYF    FILE((HSL018J)) QRYSLT(&QDATA) KEYFLD(*FILE) +
                          SEQONLY(*YES)



/*-------------------------------------------------------------------*/
/*  SETUP QUERY OVER THE ROOM FILE                                   */
/*-------------------------------------------------------------------*/


/*  SET A DEFAULT STARTING POINT                                     */

             CHGVAR     VAR(&QDATA) VALUE(' ')
             CHGVAR     VAR(%SST(&QDATA 1 12)) VALUE('BRFSCD *NE 0')


/* ROOM STATUS                                                         */

             IF         COND(&LDRMST *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 14 17)) VALUE('*AND BRRMST *EQ "')
             CHGVAR     VAR(%SST(&QDATA 31 5)) VALUE(&LDRMST)
             CHGVAR     VAR(%SST(&QDATA 36 1)) VALUE('"')
             ENDDO

             OVRDBF     FILE(HSP020) SHARE(*YES)
             OPNQRYF    FILE((HSP020)) QRYSLT(&QDATA) KEYFLD(*FILE) +
                          SEQONLY(*YES)


/*-------------------------------------------------------------------*/
/* SET UP PRINT OVERRIDES                                            */
/*-------------------------------------------------------------------*/

 HOLD:       IF         COND(%SST(*LDA 411 1) = 'Y') THEN(CHGVAR +
                          VAR(&HOLD) VALUE(*YES))
             ELSE       CMD(CHGVAR VAR(&HOLD) VALUE(*NO))

 COPYS:      CHGVAR     VAR(&COPYS) VALUE(%SST(*LDA 412 1))
             IF         COND(&COPYS = 0) THEN(CHGVAR VAR(&COPYS) +
                          VALUE(1))

OUTQ:        CHGVAR     VAR(&OUTQ) VALUE(%SST(*LDA 413 10))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(%SST(*LDA 401 10)))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(*JOB))

/*------------------------------------------------------------------*/
/* OVERRIDE PRINT FILE                                              */
/*------------------------------------------------------------------*/

/* USE THE CUSTOMIZED SEABOARD COMMAND TO OVERRIDE THE PRINT FILE.  */
/* THIS COMMAND ALLOWS THE REPORT TO BE GENERATED ON A STANDARD LINE*/
/* PRINTER OR A LASER PRINTER. TO USE THIS OVERRIDE YOU MUST DECLARE*/
/* A COMMAND VARIABLE AND FOLLOW THE OVERRIDE WITH A CALL TO        */
/* QCMDEXC.                                                         */

/* THIS REPORT IS STANDARD PRINT AT 10 CPI:  SO, ON A LASER PRINTER */
/* IT CAN PRINT 'PORTRAIT' ON LETTER PAPER                          */

             SBDPRTOVR  CMDVAR(&CMD) FILE(QPRINT) OUTQ(&OUTQ) +
                          WIDTH(94) LANDSCAPE(*PRTF) DRAWER(*PRTF) +
                          COPIES(&COPYS) HOLD(&HOLD)

             CALL       PGM(QCMDEXC) PARM(&CMD 512)


/*-------------------------------------------------------------------*/
/* PRINT THE LISTING                                                 */
/*-------------------------------------------------------------------*/

             CALL       PGM(HP6076)

             CLOF       OPNID(HSL018J)
             CLOF       OPNID(HSP020)
             DLTOVR     FILE(*ALL)

             ENDPGM

