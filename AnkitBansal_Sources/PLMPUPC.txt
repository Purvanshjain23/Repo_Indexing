/***************************************************************** */
/*                                                                 */
/*  PROGRAM NUMBER:  PLMPUPC                                       */
/*  PROGRAM NAME:    START WORK WITH DELIVERY SECURITY             */
/*  PROGRAMMER:      PURVA DROGE                                   */
/*  CREATE DATE:     08/05/08                                      */
/*                                                                 */
/*  FUNCTION:        THIS CL STARTS TO PROGRAM FOR ENTERING        */
/*                   THE DATE AND TIME OF WHEN THE TRUCKS ARE      */
/*                   ENTERING THE FACILITY FOR DELIVERY OF HOGS.   */
/*                                                                 */
/*******************************************************************/
/*  Modification History                                           */
/*                                                                 */
/*                                                                 */
/*******************************************************************/
             PGM        PARM(&CO)

/*-----------------------------------------------------------------*/
/*                      PARAMETER                                  */
/*-----------------------------------------------------------------*/
/* Print Options                                                   */

/* Function Specific                                               */
             DCL        VAR(&CO) TYPE(*DEC) LEN(3)
             DCL        VAR(&VALCDE) TYPE(*CHAR) LEN(10) +
                          VALUE('HPESECGTE')
             DCL        VAR(&VALUE) TYPE(*CHAR) LEN(40)
             DCL        VAR(&DEVICE) TYPE(*CHAR) LEN(10)
             DCL        VAR(&JOB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&USER) TYPE(*CHAR) LEN(10)
             DCL        VAR(&NBR) TYPE(*CHAR) LEN(6)
             DCL        VAR(&DTAARA) TYPE(*CHAR) LEN(26)
             DCL        VAR(&MSGDATA) TYPE(*CHAR) LEN(512)
             DCL        VAR(&MSGID) TYPE(*CHAR) LEN(7)

/*-----------------------------------------------------------------*/
/*                    PROGRAM VARIABLES                            */
/*-----------------------------------------------------------------*/

/*-----------------------------------------------------------------*/
/*                     INITIALIZATION                              */
/*-----------------------------------------------------------------*/

             CHGJOB     LOG(4 00 *SECLVL) INQMSGRPY(*DFT)

/* TRY TO ALLOCATE THE DATAAREA EXCLUSIVELY.  IF IS ALREADY        */
/* ALLOCATED WITH *SHRRD TO ANOTHER JOB, THEN GO TO THE ERROR      */
/* PROCESSING BECAUSE THERE IS ALREADY ANOTHER JOB RUNNING.        */

             ALCOBJ     OBJ((HPESECGATE *DTAARA *EXCL))
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(ALCOBJ))

/* ALLOCATE THE DATAAREA TO THIS JOB SO ANOTHER JOB CANNOT GET     */
/* EXCLUSIVE LOCK ON THE DATAAREA                                  */

             ALCOBJ     OBJ((HPESECGATE *DTAARA *SHRRD))

/* RELEASE EXCLUSIVE LOCK ON THE DATAAREA.  IF THIS IS THE ONLY    */
/* JOB, THEN THE LOCK NEEDS TO *SHRRD.                             */
             DLCOBJ     OBJ((HPESECGATE *DTAARA *EXCL))

/*-----------------------------------------------------------------*/
/*                           MAIN                                  */
/*-----------------------------------------------------------------*/
             CALL       PGM(PDGKXFR) PARM(' ' &CO &VALCDE &VALUE)

             CHGVAR     VAR(&DEVICE) VALUE(&VALUE)

             OVRDSPF    FILE(PLMMDFR#) TOFILE(PLMMDFR#) DEV(&DEVICE)
             OVRDSPF    FILE(PLNNE1R#) TOFILE(PLNNE1R#) DEV(&DEVICE)
             OVRDSPF    FILE(PLNOE1R#) TOFILE(PLNOE1R#) DEV(&DEVICE)
             OVRDSPF    FILE(PLNRDFR#) TOFILE(PLNRDFR#) DEV(&DEVICE)

             RTVJOBA    JOB(&JOB) USER(&USER) NBR(&NBR)
             CHGVAR     VAR(%SST(&DTAARA  1 10)) VALUE(&JOB)
             CHGVAR     VAR(%SST(&DTAARA 11 10)) VALUE(&USER)
             CHGVAR     VAR(%SST(&DTAARA 21  6)) VALUE(&NBR)
             CHGDTAARA  DTAARA(HPESECGATE) VALUE(&DTAARA)

LOOP:
             CALL       PGM(PLMMDFR) PARM(' ' 'D')
             MONMSG     MSGID(CPF4128) EXEC(GOTO CMDLBL(ALCOBJ))

             GOTO       CMDLBL(LOOP)

/*-----------------------------------------------------------------*/
/*                     ERROR MESSAGES                              */
/*-----------------------------------------------------------------*/

 ALCOBJ:
             /* RECEIVE THE MESSAGE THAT INDICATES WHO SUBMITTED */
             /* THIS JOB AND SEND THEM A MESSAGE.                */
             RCVMSG     PGMQ(*EXT) RMV(*NO) MSGDTA(&MSGDATA) +
                          MSGID(&MSGID)
             WRKOBJLCK  OBJ(PLMMDFR#) OBJTYPE(*FILE) OUTPUT(*PRINT)
             IF         COND(&MSGID *EQ 'CPF4128') THEN(DO)
             CHGVAR     VAR(&USER) VALUE(%SST(&MSGDATA 37 10))
             SNDUSRMSG  MSG('Security Gate Job is already running.  +
                          Please take the option to End Security +
                          Gate Device from the Menu.  After the Job +
                          has ended, take the option to Start +
                          Security Gate Device.') TOUSR(&USER)
             GOTO       CMDLBL(ENDPGM)
             ENDDO
             GOTO       CMDLBL(ALCOBJ)

 ERROR2:

/*-----------------------------------------------------------------*/
/*                      PROGRAM END                                */
/*-----------------------------------------------------------------*/
 ENDPGM:
             ENDPGM
