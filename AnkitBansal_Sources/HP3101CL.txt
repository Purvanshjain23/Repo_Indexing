/***************************************************************** */
/*                                                                 */
/*  PROGRAM NUMBER:  HP3101CL                                      */
/*  PROGRAM NAME:    Validate Producer and Producer Location       */
/*                   values                                        */
/*                                                                 */
/*  PROGRAMMER:      Brad Baden                                    */
/*  CREATE DATE:     12/29/2017                                    */
/*                                                                 */
/*  FUNCTION:        This CL adds and removes a library from the   */
/*                   current library list based on the Action      */
/*                   parameter value.  It returns a Remove flag    */
/*                   when adding.  If the library already is in    */
/*                   the list when trying to add the libray, the   */
/*                   the flag is set to 'N'.  If the library is    */
/*                   added, the Remove flag is set to yes, so      */
/*                   when the action is 'R', the library is not    */
/*                   removed.                                      */
/*                                                                 */
/*=================================================================*/
/*  Modification History                                           */
/*  xx/xx/xx xxx Exxxxx   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx   */
/*                        xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx   */
/*                                                                 */
/*******************************************************************/

             PGM        PARM(&LIBRARY &ACTION   &RMVFLAG)

             DCL        VAR(&LIBRARY)  TYPE(*CHAR) LEN(10)
             DCL        VAR(&ACTION) TYPE(*CHAR) LEN(1)
             DCL        VAR(&RMVFLAG) TYPE(*CHAR) LEN(1)

             /* Only process if Library is not blank */
             IF         COND(&LIBRARY *NE '          ') THEN(DO)

             SELECT
             /* Adding a Library */
             WHEN       COND(&ACTION *EQ 'A') THEN(DO)

             /* Default Remove flag to 'Y' */
             CHGVAR     VAR(&RMVFLAG) VALUE('Y')

             ADDLIBLE   LIB(&LIBRARY) POSITION(*AFTER QTEMP)
             MONMSG     MSGID(CPF2103) EXEC(CHGVAR VAR(&RMVFLAG) +
                          VALUE('N'))
             ENDDO

             /* Removing a Library */
             WHEN       COND(&ACTION *EQ 'R') THEN(DO)

             IF         COND(&RMVFLAG *EQ 'Y') THEN(DO)
             RMVLIBLE   LIB(&LIBRARY)
             MONMSG     MSGID(CPF2104)
             CHGVAR     VAR(&RMVFLAG) VALUE(' ')
             ENDDO
             ENDDO

             ENDSELECT

             ENDDO

             ENDPGM
