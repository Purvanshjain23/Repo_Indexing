     A*%%TS  SD  20020827  080922  ISLRAMS     REL-V5R1M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD486
     A*  TITLE:         WORK WITH POSTED TRANFERS
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  DATE:          08/27/02
     A*
     A*
     A*****************************************************************
     A* RMC 2/22/2016 E5190  ADD BUSINESS OFFICE RESTRICTOR
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A                                      CF04(04 'PROMPT')
     A                                      CF05(05 'REFRESH')
     A                                      CF20(20 'WRKSPLF')
     A*
     A*----------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*----------------------------------------------------------------
     A          R HP486S1                   SFL
     A*%%TS  SD  20020827  080922  ISLRAMS     REL-V5R1M0  5722-WDS
     A  85                                  SFLNXTCHG
     A            HDRRN1         4Y 0H
     A            DFOPT          2A  B 11  2CHGINPDFT(UL)
     A  30                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A N30                                  DSPATR(HI)
     A            DFMVSN    R        O 11  6REFFLD(MHMVSN)
     A                                      EDTCDE(4)
     A            DFMSCD    R        O 11 15REFFLD(MHMSCD)
     A            DFSMDY    R     Y  O 11 18REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  ')
     A            DFSCHD    R     Y  O 11 27REFFLD(SGSCHD)
     A                                      EDTCDE(K)
     A            DFORHG    R        O 11 35REFFLD(HGHGCD)
     A            DFORFS    R        O 11 43REFFLD(FSFSCD)
     A                                      EDTCDE(Z)
     A            DFORBL    R        O 11 49REFFLD(HSRREF/HGBLCD *LIBL/HSPREF)
     A            DFORPP    R        O 11 55REFFLD(HGPPCD)
     A            DFDNHG    R        O 11 62REFFLD(HGHGCD)
     A            DFDNFS    R        O 11 70REFFLD(HGFSCD)
     A                                      EDTCDE(Z)
     A            DFDNBL    R        O 11 76REFFLD(HSRREF/HGRMCD *LIBL/HSPREF)
     A*
     A*----------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*----------------------------------------------------------------
     A          R HP486C1                   SFLCTL(HP486S1)
     A*%%TS  SD  20020827  080922  ISLRAMS     REL-V5R1M0  5722-WDS
     A                                      SFLSIZ(0050)
     A                                      SFLPAG(0011)
     A N82                                  ROLLUP(25 'PAGE FORWARD')
     A                                      SETOF(52)
     A                                      SETOF(31)
     A                                      SETOF(53)
     A                                      SETOF(54)
     A                                      SETOF(55)
     A                                      SETOF(56)
     A                                      RTNCSRLOC(&RCD &FLD)
     A                                      OVERLAY
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 26'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  2 24'WORK WITH POSTED TRANSFERS'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  4  2'Business office .. ?'
     A            C1FSBO    R        B  4 23REFFLD(HGFSBO)
     A  31
     AO 51                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A N31                                  DSPATR(HI)
     A  99                                  DSPATR(PR)
     A            C1BODS    R        O  4 30REFFLD(BOBODS)
     A                                  6  2'2=Revise'
     A                                  7 37'?'
     A                                      DSPATR(HI)
     A                                  7 45'?'
     A                                      DSPATR(HI)
     A                                  7 57'?'
     A                                      DSPATR(HI)
     A                                  7 64'?'
     A                                      DSPATR(HI)
     A                                  7 72'?'
     A                                      DSPATR(HI)
     A                                  8 20'Ship'
     A                                      DSPATR(HI)
     A                                  8 28'Sched'
     A                                      DSPATR(HI)
     A                                  8 35'-------- ORIGIN ---------'
     A                                      DSPATR(HI)
     A                                  8 62'--- DESTINATION ---'
     A                                      DSPATR(HI)
     A                                  9  2'Opt'
     A                                      DSPATR(HI)
     A                                  9  6'Movemnt'
     A                                      DSPATR(HI)
     A                                  9 15'St'
     A                                      DSPATR(HI)
     A                                  9 20'Date'
     A                                      DSPATR(HI)
     A                                  9 29'Head'
     A                                      DSPATR(HI)
     A                                  9 35'Group'
     A                                      DSPATR(HI)
     A                                  9 44'Farm'
     A                                      DSPATR(HI)
     A                                  9 49'Bldg'
     A                                      DSPATR(HI)
     A                                  9 55'Phase'
     A                                      DSPATR(HI)
     A                                  9 62'Group'
     A                                      DSPATR(HI)
     A                                  9 71'Farm'
     A                                      DSPATR(HI)
     A                                  9 76'Bldg'
     A                                      DSPATR(HI)
     A            C1MVSN    R        B 10  6REFFLD(MHMVSN)
     A                                      EDTCDE(4)
     A                                      DSPATR(HI)
     A N37N57                               DSPATR(PC)
     A            C1SMDY    R     Y  B 10 18REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  ')
     A                                      DSPATR(HI)
     A            C1ORHG    R        B 10 35REFFLD(HGHGCD)
     A                                      DSPATR(HI)
     A  52                                  DSPATR(PC)
     A            C1ORFS    R        B 10 43REFFLD(HGFSCD)
     A                                      EDTCDE(Z)
     A                                      DSPATR(HI)
     A  53                                  DSPATR(PC)
     A            C1ORBL    R        B 10 49REFFLD(HSRREF/HGBLCD *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A            C1ORPP    R        B 10 55REFFLD(HGPPCD)
     A                                      DSPATR(HI)
     A  54                                  DSPATR(PC)
     A            C1DNHG    R        B 10 62REFFLD(HGHGCD)
     A                                      DSPATR(HI)
     A  55                                  DSPATR(PC)
     A            C1DNFS    R        B 10 70REFFLD(HGFSCD)
     A                                      EDTCDE(Z)
     A                                      DSPATR(HI)
     A  56                                  DSPATR(PC)
     A            C1DNBL    R        B 10 76REFFLD(HSRREF/HGRMCD *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A*
     A*----------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*----------------------------------------------------------------
     A          R HP486K1
     A*%%TS  SD  19960213  095527  ISLRAMS     REL-V3R1M0  5763-PW1
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A                                 23 11'F4=Prompt'
     A                                 23 22'F5=Refresh'
     A                                 23 34'F20=Spool Files'
      *----------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *----------------------------------------------------------------
     A          R HP486ES                   SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
      *----------------------------------------------------------------
      * ERROR MESSAGE SUBFILE CONTROL FORMAT
      *----------------------------------------------------------------
     A          R HP486EC                   SFLCTL(HP486ES)
      *
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
