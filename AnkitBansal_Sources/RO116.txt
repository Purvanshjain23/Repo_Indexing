      *****************  RPG PROGRAM HEADING  ************************
     h option(*SRCSTMT:*NODEBUGIO)
      ****************************************************************
      *
      * ENVIRONMENT: Pork Division
      * SYSTEM:      Resource Optimization
      * PROGRAM:     RO116
      * TITLE:       Revise Meat Cost Override
      * PROGRAMMER:  LeAnne Ramsey
      * CREATED:     07/31/08
      *
      * FUNCTION: Uses an edit record format to add/change/delete records.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 02/17/10  LeAnne Ramsey
      *           Recompile only. Two new fields were added to the Resource Optimization Header:
      *                Cap&Blade Allocation % for Special Trim
      *                Cap&Blade Allocation % for Regular Trim
      *
      * 03/08/12  LeAnne Ramsey (E1984)
      *           Recompile only.
      *           Added 6 new Skirt Meat Allocation Flag fields to ROP100.
      /eject
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Frod116    cf   e             workstn infds(iofeed)
      *
      *
     Fombyrel1  if   e           k disk
      *    Item default
      *
      *
     Frol100c   if   e           k disk
      *   Resource optimization header
      *
      *
     Frol100d   if   e           k disk    rename(ohrec:ohrecd)
      *   Resource optimization header (LF selects F=Final records)
      *
      *
     Frop016    uf a e           k disk
      *   Meat cost overrides
      *
      *
     Frol016c   if   e           k disk    rename(morec:morecc)
      *   Meat cost overrides
      *
      /eject
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D create          c                   'CREATE'
     D revise          c                   'REVISE'
     D delete          c                   'DELETE'
     D yes             c                   'Y'
     D no              c                   'N'
     D set1            c                   'SET1  '
     D edit1           c                   'EDIT1 '
     D scrn1           c                   'SCRN1 '
     D exit            c                   'EXIT  '
     D update          c                   'UPDATE'
      *
      *
      ****************************************************************
      * ARRAYS AND TABLES
      ****************************************************************
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Standard fields
      *
     D mode            s              6
     D pgm             s             10
     D msgfl           s             10
     D routne          s              6
     D rtncd           s              2
     D action          s                   like(mode)
     D maxmsg          s              2  0  inz(20)
      *
      *
      * Control fields
      *
     D svwedt          s                   like(moefdt)
     D svmcdt          s                   like(moefdt)
     D activefl        s              1
     D disable11       s              1    inz('N')
      *
      *
      *
      * Workfields for date manipulation
      *
     D wkisodate       s               D   datfmt(*iso)
     D wkdt            s              8  0
      *
      *
      * Parms
      *
     D xxwedt          s                   like(moefdt)
     D xxsatdt         s              8  0
     D xxdt            s              8  0
     D xxyr            s              4  0
     D xxpe            s              4  0
     D xxwk            s              2  0
      *
      *
     D xxitcd          s                   like(d1itcd)
     D xxovitcd        s                   like(d1ovitcd)
     D xxitds          s                   like(d1itds)
     D xxalphitcd      s              7
      *
     D xxaist          s                   like(d1aist)
     D xxaids          s              8
      *
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * standard error message handling data structures
      *---------------------------------------------------------------
      *
      * For error message handling, a packed index (3) is required.
      * For program readability, define a corresponding error count
      * field called 'error'
      *
     D                 ds                  inz
     D  error                         2p 0
     D   e                            2p 0 overlay(error)
      *
      * This data structure supplies the name of the message file to
      * the message handling CL program.  The field name MSGFIL must be
      * constant. The value in quotes is the name of the specific
      * message file containing the user defined messages.
      *
     D                 ds                  inz
     D  msgfil                       10    inz('ROMSGF    ')
      *
      *---------------------------------------------------------------
      *  Standard message data structures
      *---------------------------------------------------------------
      *
      * The following 3 data structures are used to speed message
      * handling since it is faster to clear data structures than
      * arrays.  Each is associated with a standard message array.
      *
     D mgi             ds                  inz
     D  mgid                          7    dim(20)
      *
     D mgd             ds                  inz
     D  mgdt                         50    dim(20)
      *
     D mgwk            ds                  inz
     D  mgw                           1    dim(50)
      *
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *---------------------------------------------------------------
      * Standard workstation information data structure
      *---------------------------------------------------------------
      *    externally defined as UTWSFR (record format: UTIDFRR)
     D iofeed        e ds                  extname(utwsfr)
      *
      *
      *---------------------------------------------------------------
      * Standard database file information data structure
      *---------------------------------------------------------------
      *    externally defined as UTDBGR (record format: FDBCKD)
     D dbfeed        e ds                  extname(utdbfr)
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /eject
      ****************************************************************
      * MAINLINE
      ****************************************************************
      *
     C                   eval      routne = set1
      *
     C     *inlr         doweq     *off                                         Main do loop
     C     routne        caseq     set1          $set1
     C     routne        caseq     scrn1         $scrn1
     C     routne        caseq     edit1         $edit1
     C     routne        caseq     update        $upd1
     C     routne        caseq     exit          $exit
     C                   endcs
     C                   enddo                                                  Main do loop
      /eject
      *----------------------------------------------------------------
      * Set environment for screen 1
      *----------------------------------------------------------------
      *
     C     $set1         begsr
      *
      * If incoming mode is REVISE or DELETE, then retrieve the database
      * record.  Do not lock the record.  Typically, in DELETE mode, these
      * values will not be displayed. However, if there is an error on the
      * delete action, the panel will be displayed with messages.
      *
     C                   select
     C                   when      mode = create                                WH create
     C                   z-add     0             d1itcd
     C                   move      *blank        d1itds
     C                   z-add     0             d1ovitcd
     C                   move      *blank        d1ovitds
     C                   move      *blank        d1aist
      *
     C                   when      mode = revise or mode = delete               WH revise/delete
     C     key01         chain(n)  rop016                             92
     C                   if        *in92 = *on                                  If no hit
     C                   seton                                        62
     C                   if        error < maxmsg
     C                   add       1             error
     C                   eval      mgid(e) = 'RO09008'
     C                   endif
     C                   else
      *
     C                   z-add     moitcd        d1itcd
     C                   z-add     moovitcd      d1ovitcd
     C                   move      moaist        d1aist
      *
      * Flip Effective Date to MMDDYY format
      *
     C     *iso          test(d)                 moefdt                 92
     C                   if        *in92 = *off                                 If OK date
     C                   move      moefdt        wkisodate
     C     *mdy          move      wkisodate     d1efmdy
     C                   z-add     d1efmdy       d1wemdy
     C                   else
     C                   z-add     0             d1efmdy
     C                   z-add     0             d1wemdy
     C                   endif                                                  If OK date
      *
      * Retrieve Descriptions
      *
     C                   exsr      $desc
     C                   endif                                                  If no hit
     C                   endsl
      *
      * If the user is deleting and there were no errors when retrieving the
      * record, go on to the update routine without displaying the screen.
      * Otherwise, display the screen.
      *
     C                   if        error = 0 and action = delete                If no error
     C                   eval      routne = update
     C                   else
     C                   eval      routne = scrn1
     C                   endif                                                  If no error
      *
     C                   endsr
      /eject
      *----------------------------------------------------------------
      * Perform operator I/O for screen 1
      *----------------------------------------------------------------
      *
     C     $scrn1        begsr
      *
      * Write the error messages from the error arrays to the
      * error message subfile
      *
     C                   exsr      $wrmsg
      *
      * Set indicators to control input/output fields and available
      * function keys.
     C                   exsr      $ind
      *
      *
      * Write screen 1 to CRT
      *
     C                   write     ro116k1
     C                   write     ro116ec
     C                   exfmt     ro116r1
      *
      * Clear messages
     C                   exsr      $clmsg
      *
      *
      * Reset action to be equal to the incoming mode.  This is required
      * in case the user took F11-Delete and got an error on the
      * deletion and now needs to be able to change the record and take
      * an action other than delete
      *
     C                   eval      action = mode
      *
      * Get user's input and set routine
      *
     C                   select
     C                   when      *in03 = *on                                  F3-exit
     C                   eval      rtncd = '03'
     C                   eval      routne = exit
      *
     C                   when      *in04 = *on                                  F4=prompt
     C                   exsr      $f4s1
      *
     C                   when      *in05 = *on                                  F5-refresh
     C                   eval      routne = set1
      *
     C                   when      *in11 = *on or mode = delete                 F11-delete
     C                   eval      action = delete
     C                   eval      routne = update
     C                   other
      *
      * User has pressed enter
      *
     C                   eval      routne = edit1
     C                   endsl
      *
     C                   endsr
      /eject
      *----------------------------------------------------------------
      * Set indicators to control/protect screen fields
      *----------------------------------------------------------------
      *
     C     $ind          begsr
      *
     C                   select
     C                   when      mode = revise
     C                   seton                                        60
     C                   if        disable11 = yes
     C                   seton                                        63
     C                   endif
      *
     C                   when      mode = create
     C                   seton                                        61
      *
     C                   when      mode = delete
     C                   seton                                        62
     c                   endsl
      *
     C                   endsr
      /eject
      *----------------------------------------------------------------
      * Edit input fields for screen 1
      *----------------------------------------------------------------
      *
     C     $edit1        begsr
      *
      * *IN62 will be on if the record the user is trying to access has
      * been deleted by another user.  So, only perform the edits for the
      * fields on the screen if the indicator is off
      *
     C                   if        *in62 = *off                                 If here
      *
      * In Create Mode, validate:
      *   1) Effective date
      *   2) Item code
      *   3) Override item
      *
     C                   if        mode = create
     C                   exsr      $efmdy
     C                   exsr      $itcd
     C                   exsr      $ovitcd
     C                   endif
      *
      * Always validate:
      *   1) Active/inactive status
      *
     C                   exsr      $aist
     C                   endif                                                  If here
      *
      * If there are no errors, attempt the write/update; otherwise,
      * redisplay the screen.  If a record has been deleted out from
      * under the user (*IN62 is ON), then the error message will be
      * issued in the update subroutine.  So, always perform this code.
      *
     C                   if        error = 0
     C                   exsr      $upd1
     C                   else
     C                   eval      routne = scrn1
     C                   endif
      *
     C                   endsr
      /eject
      *------------------------------------------------------------------------------------
      * Validate Effective Date (in CREATE mode only)
      *------------------------------------------------------------------------------------
      *
      * Validations:
      *  1) required
      *  2) must be a valid date
      *  3) must be on/after the last Finalized Meat Cost week.
      *
     C     $efmdy        begsr
      *
     C                   z-add     0             d1wemdy
     C                   z-add     0             svwedt
      *
     C                   select
     C                   when      d1efmdy = 0
     C                   seton                                        30
     C                   if        error < maxmsg
     C                   add       1             error
     C                   eval      mgid(e) = 'RO09001'
     C                   endif
      *
     C                   when      d1efmdy < 0
     C                   seton                                        30
     C                   if        error < maxmsg
     C                   add       1             error
     C                   eval      mgid(e) = 'RO09000'
     C                   endif
     C                   other
      *
     C     *mdy          test(d)                 d1efmdy                92
     C                   if        *in92 = *on                                  If bad date
     C                   seton                                        30
     C                   if        error < maxmsg
     C                   add       1             error
     C                   eval      mgid(e) = 'RO09004'
     C                   endif
     C                   else
      *
      * Flip the date from MMDDYY to CCYYMMDD and continue editing.
      *
     C     *mdy          move      d1efmdy       wkisodate
     C                   move      wkisodate     wkdt
      *
      * Retrieve Saturday week-ending date associated with the date
      * entered.
     C                   call      'TF811'
     C                   parm      wkdt          xxdt
     C                   parm      0             xxsatdt
     C                   parm      0             xxyr
     C                   parm      0             xxpe
     C                   parm      0             xxwk
      *
      * Get returned 'week-ending date' into MMDDYY format.
      *
     C     *iso          test(d)                 xxsatdt                92
     C                   if        *in92 = *on                                  If bad Saturday
     C                   seton                                        30
     C                   if        error < maxmsg
     C                   add       1             error
     C                   eval      mgid(e) = 'RO09229'
     C                   endif
     C                   else
     C                   z-add     xxsatdt       svwedt
     C                   move      xxsatdt       wkisodate
     C     *mdy          move      wkisodate     d1wemdy
     C                   endif                                                  If bad Saturday
     C                   endif                                                  If bad date
      *
      * If date is OK so far,
      *   the date must be after the most recent Finalized Meat Cost week
      *
     C                   if        *in30 = *off and                             If ok so far
     C                             svwedt < svmcdt
     C                   seton                                        30
     C                   if        error < maxmsg
     C                   add       1             error
     C                   eval      mgid(e) = 'RO00139'
     C                   endif
     C                   endif                                                  If ok so far
      *
     C                   endsl
      *
     C                   endsr
      /eject
      *------------------------------------------------------------------------------------
      * Validate Item Code      (in CREATE mode only)
      *------------------------------------------------------------------------------------
      *
      * Validations:
      *  1) required
      *  2) must exist in the Item Default file
      *  3) must be an FG or WP Item
      *  4) the item/week-ending date cannot already exist in the Meat Cost Override file
      *
     C     $itcd         begsr
      *
     C                   move      *blank        d1itds
      *
     C                   select
     C                   when      d1itcd = 0
     C                   seton                                        31
     C                   if        error < maxmsg
     C                   add       1             error
     C                   eval      mgid(e) = 'RO09001'
     C                   endif
      *
     C                   when      d1itcd < 0
     C                   seton                                        31
     C                   if        error < maxmsg
     C                   add       1             error
     C                   eval      mgid(e) = 'RO09000'
     C                   endif
     C                   other
      *
      * Item must exist in the Item Default file
      *
     C     d1itcd        chain     ombyrel1                           92
     C                   if        *in92 = *on                                  If invalid
     C                   seton                                        31
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'RO00107'     mgid(e)
     C                   clear                   mgw
     C                   move      d1itcd        xxalphitcd
     C                   movea     xxalphitcd    mgw(1)
     C                   movea     mgw           mgdt(e)
     C                   endif
     C                   else
      *
     C                   eval      d1itds  = byavna
      *
      * Must be an FG or WP Item
      *
     C                   if        byn0cd = 'FG ' or byn0cd = 'WP '
     C                   else
     C                   seton                                        31
     C                   if        error < maxmsg
     C                   add       1             error
     C                   eval      mgid(e) = 'RO00141'
     C                   endif
     C                   endif
      *
      * The Item/Week-ending Date cannot already exist in the Meat Cost Override
      * file.
     C                   if        *in30 = *off                                 If date OK
     C     key01         chain(n)  rop016                             92
     C                   if        *in92 = *off
     C                   seton                                        31
     C                   if        error < maxmsg
     C                   add       1             error
     C                   eval      mgid(e) = 'RO00136'
     C                   endif
     C                   endif
     C                   endif                                                  If date OK
      *
     C                   endif                                                  If invalid
     C                   endsl
      *
     C                   endsr
      /eject
      *------------------------------------------------------------------------------------
      * Validate Override Item Code   (in CREATE mode only)
      *------------------------------------------------------------------------------------
      *
      * Validations:
      *  1) required
      *  2) must exist in the Item Default file
      *  3) Override Item cannot be the same as Item
      *  4) must be an FG or WP Item
      *  5) You can only have 1 record for each combination of Item/Override Item
      *
     C     $ovitcd       begsr
      *
     C                   move      *blank        d1ovitds
      *
     C                   select
     C                   when      d1ovitcd = 0
     C                   seton                                        32
     C                   if        error < maxmsg
     C                   add       1             error
     C                   eval      mgid(e) = 'RO09001'
     C                   endif
      *
     C                   when      d1ovitcd < 0
     C                   seton                                        32
     C                   if        error < maxmsg
     C                   add       1             error
     C                   eval      mgid(e) = 'RO09000'
     C                   endif
     C                   other
      *
      * Item must exist in the Item Default file
      *
     C     d1ovitcd      chain     ombyrel1                           92
     C                   if        *in92 = *on                                  If invalid
     C                   seton                                        32
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'RO00107'     mgid(e)
     C                   clear                   mgw
     C                   move      d1ovitcd      xxalphitcd
     C                   movea     xxalphitcd    mgw(1)
     C                   movea     mgw           mgdt(e)
     C                   endif
     C                   else
      *
     C                   eval      d1ovitds  = byavna
      *
      * Must be an FG or WP Item
      *
     C                   if        byn0cd = 'FG ' or byn0cd = 'WP '
     C                   else
     C                   seton                                        32
     C                   if        error < maxmsg
     C                   add       1             error
     C                   eval      mgid(e) = 'RO00141'
     C                   endif
     C                   endif
      *
      * The Override Item cannot be the same as the Item
      *
     C                   if        *in31 = *off and d1itcd = d1ovitcd           If same
     C                   seton                                        32
     C                   if        error < maxmsg
     C                   add       1             error
     C                   eval      mgid(e) = 'RO00137'
     C                   endif
     C                   endif                                                  If same
     C                   endif                                                  If invalid
      *
      * If you are in Create mode, see if you already have this Item/Override
      * Item combination.
     C                   if        mode = create and                            If dup
     C                             *in31 = *off and *in32 = *off
     C     key02         chain     rol016c                            92
     C                   if        *in92 = *off                                 If hit
     C                   seton                                        32
     C                   if        error < maxmsg
     C                   add       1             error
     C                   eval      mgid(e) = 'RO00140'
     C                   endif
     C                   endif                                                  If hit
     C                   endif                                                  If dup
      *
     C                   endsl
      *
     C                   endsr
      /eject
      *------------------------------------------------------------------------------------
      * Validate Active/Inactive Status
      *------------------------------------------------------------------------------------
      *
     C     $aist         begsr
      *
     C                   select
     C                   when      d1aist <> 'A' and d1aist <> 'I'
     C                   seton                                        36
     C                   if        error < maxmsg
     C                   add       1             error
     C                   eval      mgid(e) = 'RO09105'
     C                   endif
      *
      * There can only be 1 "active" record for a Item. (Only make this
      * check when you have a valid date.)
      *
     C                   when      d1aist = 'A' and *in30 = *off
      *
     C     d1itcd        setll     rop016
     C                   move      no            activefl
      *
     C                   dou       *in91 = *on or activefl = yes                Do loop
     C     d1itcd        reade(n)  rop016                                 91
     C                   if        *in91 = *off and moefdt <> svwedt            If not EOF
     C                             and moaist = 'A'
     C                   move      yes           activefl
     C                   endif                                                  If not EOF
     C                   enddo                                                  Do loop
      *
     C                   if        activefl = yes                               If toomany
     C                   seton                                        36
     C                   if        error < maxmsg
     C                   add       1             error
     C                   eval      mgid(e) = 'RO00138'
     C                   endif
     C                   endif                                                  If toomany
      *
     C                   endsl
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * F4 prompt on screen 1
      *---------------------------------------------------------------
      *
     C     $f4s1         begsr
      *
      * Item code
     C                   select
     C                   when      fld = 'D1ITCD' and *in61 = *on
     C                   seton                                        51
     C                   call      'RO504'
     C                   parm      0             xxitcd
     C                   parm      *blank        xxitds
      *
     C                   if        xxitcd <> 0
     C                   z-add     xxitcd        d1itcd
     C                   eval      d1itds = xxitds
     C                   endif
      *
      * Override item code
     C                   when      fld = 'D1OVITCD' and *in61 = *on
     C                   seton                                        52
     C                   call      'RO504'
     C                   parm      0             xxitcd
     C                   parm      *blank        xxitds
      *
     C                   if        xxitcd <> 0
     C                   z-add     xxitcd        d1ovitcd
     C                   eval      d1ovitds = xxitds
     C                   endif
      *
      * Active/inactive status
      *
     C                   when      fld = 'D1AIST'
     C                   seton                                        56
     C                   call      'RO512'
     C                   parm      *blank        xxaist
     C                   parm      *blank        xxaids
      *
     C                   if        xxaist <> *blank
     C                   move      xxaist        d1aist
     C                   endif
      *
      * F4 not valid for this field...issue message
      *
     C                   other
     C                   if        error < maxmsg
     C                   add       1             error                                        013
     C                   movel     'RO09011'     mgid(e)
     C                   endif
     C                   endsl
      *
     C                   endsr
      /eject
      *----------------------------------------------------------------
      * Write/update/delete record in database file
      *----------------------------------------------------------------
      *
     C     $upd1         begsr
      *
      * If deleting, perform deletion integrity checks.
      * Note: We are controlling deletes with *in63.
      *
     C                   if        action = delete
     C                   endif
      *
      * If user action is revising or deleting, determine that the
      * record he is accessing still exists prior to attempting to
      * update.  Lock the record for update.
      *
     C                   if        error = 0 and                                If OK
     C                             (action = delete or action = revise)
     C     key01         chain     rop016                             92
     C                   if        *in92 = *on                                  If not found
     C                   seton                                        62
     C                   if        error < maxmsg
     C                   add       1             error
     C                   eval      mgid(e) = 'RO09008'
     C                   endif
     C                   endif                                                  If not found
     C                   endif                                                  If OK
      *
      *
      * If there are no errors, delete, revise or create the record.
      *
     C                   if        error = 0                                    If no error
      *
     c                   select
     C                   when      action = delete
     C                   delete    morec
     C                   other
      *
      * Move DSPF fields to database file fields
      *
     C                   z-add     svwedt        moefdt
     C                   z-add     d1itcd        moitcd
     C                   z-add     d1ovitcd      moovitcd
     C                   move      d1aist        moaist
      *
     C                   if        action = create
     C                   write     morec
     C                   else
     C                   update    morec
     C                   endif                                                  If create
     C                   endsl
     C                   endif                                                  If no error
      *
      * Always redisplay when:
      *     1) there are errors
      *     2) you are creating records
      *
     C                   select
     C                   when      error > 0
     C                   movel     scrn1         routne
      *
     C                   when      mode = create
     C                   add       1             error
     C                   eval      mgid(e) = 'RO09005'
     C                   eval      routne = set1
     C                   movel     set1          routne
     C                   other
      *
     C                   eval      rtncd = '00'
     C                   movel     exit          routne
     C                   endsl
      *
     C                   endsr
      /eject
      *-------------------------------------------------------------------------------------
      * Write error messages
      *-------------------------------------------------------------------------------------
      *
     C     $wrmsg        begsr
      *
     C                   call      'UT80045J'                           43
     C                   parm                    mgid
     C                   parm                    mgdt
     C                   parm                    error
     C                   parm      sdpgm         pgm
     C                   parm      msgfil        msgfl
      *
      * If call to UT80045J failed, a message hardcoded in the DSPF
      * command line format will be set on. So, redisplay screen.
      *
     C                   if        *in43 = *on
     C                   eval      routne = set1
     C                   endif
      *
     C                   endsr
      *
      *----------------------------------------------------------------
      * Clear messages
      *----------------------------------------------------------------
      *
     C     $clmsg        begsr
      *
     C                   call      'UT80045J'                           43
     C                   parm      *blank        mgi
     C                   parm      *blank        mgd
     C                   parm      *zero         error
     C                   parm      sdpgm         pgm
     C                   parm      msgfil        msgfl
      *
      * If call to UT80045J failed, a message hardcoded in the DSPF
      * command line format will be set on; so, redisplay screen.
      *
     C                   if        *in43 = *on
     C                   eval      routne = set1
     C                   endif
      *
     C                   endsr
      /eject
      *----------------------------------------------------------------
      * Set on last record indicator and end job
      *----------------------------------------------------------------
      *
     C     $exit         begsr
     C                   seton                                        lr
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Retrieve descriptions
      *---------------------------------------------------------------
      *
     C     $desc         begsr
      *
      * Item
      *
     C     d1itcd        chain     ombyrel1                           92
     C                   if        *in92 = *off
     C                   eval      d1itds  = byavna
     C                   else
     C                   eval      d1itds = 'Unknown'
     C                   endif
      * Override Item
      *
     C     d1ovitcd      chain     ombyrel1                           92
     C                   if        *in92 = *off
     C                   eval      d1ovitds  = byavna
     C                   else
     C                   eval      d1ovitds = 'Unknown'
     C                   endif
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Parm Lists
      *
     C     *entry        plist
     C     d1mode        parm                    mode
     C     svwedt        parm                    xxwedt
     C     d1itcd        parm                    xxitcd
     C     d1ovitcd      parm                    xxovitcd
     C                   parm                    rtncd
      *
     C                   movel     mode          action
      *
      * Key lists
      *
     C     key01         klist
     C                   kfld                    d1itcd
     C                   kfld                    svwedt
      *
     C     key02         klist
     C                   kfld                    d1itcd
     C                   kfld                    d1ovitcd
      *
      *
      * If in Create mode,
      *   1) Default Status to Active
      *   2) Default into Effective Date, the WeekEnding Date that is 1 week after
      *      the most recent finalized Meat Cost record.
      *
     C                   select
     C                   when      mode = create
     C                   move      'A'           d1aist
      *
     C     99999999      setgt     rol100d
     C                   readp     rol100d                                92
     C                   if        *in92 = *off                                 If hit
      *
     C     *iso          test(d)                 ohwedt                 92
     C                   if        *in92 = *off                                 If OK date
     C                   move      ohwedt        wkisodate
     C                   adddur    7:*days       wkisodate
     C     *iso          move      wkisodate     svmcdt
     C     *mdy          move      wkisodate     d1efmdy
     C                   else
     C                   z-add     0             d1efmdy
     C                   endif                                                  If OK date
     C                   endif                                                  If hit
     C                   other
      *
      * We will disable the F11-Delete key when:
      *  1) The incoming WeekEnding Date is for a Meat Cost Week that is already
      *     finalized.
      *
     C     xxwedt        chain     rol100c                            92
     C                   if        *in92 = *off and                             If final
     C                             ohrcstcd = 'F'
     C                   move      yes           disable11
     C                   endif                                                  If final
     C                   endsl
      *
      *
      * The following standard code must be included to make the
      * standard error message handling program function properly.
      * This code sets and clears the program message queue.
      *
     C                   movel     '*'           msgpgm
     C                   exsr      $clmsg
      *
     C                   endsr
      /eject
