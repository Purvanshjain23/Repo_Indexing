/***************************************************************** */
/*                                                                 */
/*  SYSTEM:          HOG PRODUCTION SYSTEM                         */
/*  PROGRAM NUMBER:  HP267CL                                       */
/*  PROGRAM NAME:    SAFEDATA: UPLOAD BGF WEEKLY PRODUCTION        */
/*  PROGRAMMER:      LEANNE FEDOR                                  */
/*  CREATE DATE:     01/17/02                                      */
/*                                                                 */
/*  FUNCTION:        THIS CL IS CALLED FROM AN FTP CL WHICH IS     */
/*                   SUBMITTED FROM THE JOB SCHEDULER.             */
/*                                                                 */
/*  THE FUNCTION:                                                  */
/*  1) WRITES THE RECEIVING FILE TO AN EDITING FILE                */
/*  2) PRINTS AN EDIT LISTING OVER THE EDITING FILE                */
/*     AND WRITES RECORDS WITHOUT ERRORS TO HPS DATABASE           */
/*                                                                 */
/*******************************************************************/
/* MODIFIED: BARB GUTIERREZ  10/23/02                              */
/*           TOOK REPORT OFF HOLD SO IT WILL AUTOMATICALLY PRINT   */
/*           PER ANNE MARIE DUSHAY.                                */
/*                                                                 */
/* 02/22/11  LeAnne Ramsey  (E1370)                                */
/*           Sami Wilson wants HP6050-Listing of Farms with Daily  */
/*           Entries but No Weekly to print at the end of this     */
/*           upload. So, I added logic to HP268 to populate Year/WK*/
/*           in the LDA. Then, I added an OPNQRY and a call to     */
/*           HP6050.                                               */
/*                                                                 */
/* 03/30/16  Barb Gutierrez (E5495)                                */
/*           Changed the printer from GUYPRT06 to GUYPRT03 per     */
/*           users request.                                        */
/*                                                                 */
/*  1/24/2020 JBB WI314 - Live Reporting IBMi                      */
/*                Add logic to override printer file.  If user     */
/*                is a programmer, hold report and do not put      */
/*                report on the Guymon printer.  Also, retrieve    */
/*                system date and CHGJOB with system date.         */
/*                                                                 */
/*******************************************************************/

             PGM

             DCL        VAR(&QDATA) TYPE(*CHAR) LEN(35)
             DCL        VAR(&HOLD)  TYPE(*CHAR) LEN(10)
             DCL        VAR(&COPYS) TYPE(*DEC) LEN(2)
             DCL        VAR(&LDCDYR)  TYPE(*DEC) LEN(4)
             DCL        VAR(&LDCDWK)  TYPE(*DEC) LEN(2)
             DCL        VAR(&GRPPRFL)  TYPE(*CHAR) LEN(10)
             DCL        VAR(&SYSDATE)  TYPE(*CHAR) LEN(6)

/*********************************************************************/
/*  Retrieve the user's Group Profile.  Set printer overrides based  */
/*  on the group profile.                                            */
/*********************************************************************/

             RTVUSRPRF  GRPPRF(&GRPPRFL)

/*-------------------------------------------------------------------*/
/*  WRITE THE BGF WEEKLY PRODUCTION RECEIVING TO THE EDITING FILE    */
/*-------------------------------------------------------------------*/

             CALL       PGM(HP267)

/*------------------------------------------------------------------*/
/*  SET UP PRINT FILE OVERRIDES AND OVERRIDE FILE                   */
/*------------------------------------------------------------------*/

             SELECT
             WHEN       COND(&GRPPRFL = 'SBDPGMR ') THEN(DO)
             CHGVAR     VAR(&HOLD) VALUE(*YES)
             CHGVAR     VAR(&COPYS) VALUE(1)
             OVRPRTF    FILE(QPRINT) COPIES(&COPYS) HOLD(&HOLD) +
                          SHARE(*YES)
             ENDDO

             OTHERWISE  CMD(DO)

 HOLD:       CHGVAR     VAR(&HOLD) VALUE(*NO)
 COPYS:      CHGVAR     VAR(&COPYS) VALUE(2)

 OVRPRTF:    OVRPRTF    FILE(QPRINT) OUTQ(GUYPRT03) COPIES(&COPYS) +
                          HOLD(&HOLD) SHARE(*YES)
             ENDDO

             ENDSELECT

/*-------------------------------------------------------------------*/
/*  RETRIEVE QSYS DATE                                               */
/*  This was done because the date the "listener" program started is */
/*  the date that was being used as the Create Date on the HSP082    */
/*  file.  Using the CHGJOB command changes the job date to the      */
/*  system date.                                                     */
/*-------------------------------------------------------------------*/
             RTVSYSVAL  SYSVAL(QDATE) RTNVAR(&SYSDATE)
             CHGJOB     DATE(&SYSDATE)

/*-------------------------------------------------------------------*/
/*  CALL PROGRAM TO EDIT/WRITE TO DATABASE                           */
/*-------------------------------------------------------------------*/

             CALL       PGM(HP268)

/*-------------------------------------------------------------------*/
/*  CALL PROGRAM TO PRINT HP6050                                     */
/*-------------------------------------------------------------------*/
/*  Always select records for the specified Calendar Year/Week   */
/*  from the HPS Daily Production file. (We populated these      */
/*  LDA positions in HP268...just so we would have them here.)   */

             CHGVAR     VAR(&LDCDYR)    VALUE(%SST(*LDA 1 4))
             CHGVAR     VAR(&LDCDWK)    VALUE(%SST(*LDA 5 2))

             CHGVAR     VAR(&QDATA)  VALUE(*BLANK)
             CHGVAR     VAR(%SST(&QDATA 1 10)) VALUE('BDCDYR *EQ')
             CHGVAR     VAR(%SST(&QDATA 12 4)) VALUE(&LDCDYR)

             CHGVAR     VAR(%SST(&QDATA 17 15)) VALUE('*AND BDCDWK +
                          *EQ')
             CHGVAR     VAR(%SST(&QDATA 33 2)) VALUE(&LDCDWK)

             OVRDBF     FILE(HSL082A) SHARE(*YES)
             OPNQRYF    FILE((HSL082A)) QRYSLT(&QDATA) KEYFLD(*FILE) +
                          SEQONLY(*YES)

             CALL       PGM(HP6050)

             CLOF       OPNID(HSL082A)
             DLTOVR     FILE(*ALL)

/*-------------------------------------------------------------------*/
/*  REORG THE EDITING FILE                                           */
/*-------------------------------------------------------------------*/
             RGZPFM     *LIBL/HSP268
             MONMSG     MSGID(CPF0000)

             ENDPGM
