/********************************************************************/
/*                                                                  */
/*  PNJCUPC   - PRT JCO PLT INVC-LOAD  CL                           */
/*  SYNOPSIS  - SUBMITTED FROM PNJDUPC, PASSES JOB CONTROL          */
/*              INFORMATION, PRINTS, ALSO EMAILS IF REQUESTED,      */
/*              ALL ORDERS FOR A SINGLE LOAD ID ON ONE              */
/*              GATE PRICED INVOICE                                 */
/*                                                                  */
/*  SYSTEM    - ORDER MANAGEMENT SYSTEM                             */
/*  DATE      - 05/22/2009                                          */
/*            - LARA BUSER                                          */
/*                                                                  */
/*  PARAMETERS:                                                     */
/*   &RETURN     - RETURN CODE                                      */
/*   &APPLIC     - APPLICATION                                      */
/*   &COMPNYN    - COMPANY NUMBER 15.5                              */
/*   &ORDERN     - ORDER NUMBER   15.5                              */
/*   &DESC       - PRINT PROGRAM DESCRIPTION (FOR PROMPT            */
/*                  SCREEN)                                         */
/*   &OUTQ       - OUTPUT QUEUE                                     */
/*   &HOLD       - HOLD STATUS                                      */
/*   &SAVE       - SAVE STATUS                                      */
/*   &COPYN      - NO. OF COPIES  15.5                              */
/*   &NQSTAT     - INDICATOR WHETHER MUST RUN IN NIGHT Q            */
/*   &NIGHTQ     - NIGHT JOB QUEUE                                  */
/*   &EMAILS     - EMAIL SELECTION                                  */
/*   &EMAILADDR  - EMAIL ADDRESS                                    */
/*                                                                  */
/********************************************************************/
/* MODIFICATIONS:                                                   */
/*------------------------------------------------------------------*/
/* 11/09/20 S16998 DN  - Use Customer PO# as filename when sending/ */
/*                       attaching PDF attachment via ESEND.        */
/*                       Change PMYLUPC program also.               */
/* 01/04/21 S16998 DN  - Concatenate 'INV' after PO# filename.      */
/********************************************************************/
 PNJCUPC:    PGM        PARM(&RETURN &APPLIC &DESC &OUTQ &HOLD &SAVE +
                          &NQSTAT &NIGHTQ &COMPNYN &COPYN &LOADID +
                          &EMAILS &EMAILADDR)

/* PARMS                                                             */
             DCL        VAR(&RETURN) TYPE(*CHAR) LEN(7)
             DCL        VAR(&APPLIC) TYPE(*CHAR) LEN(6)
             DCL        VAR(&COMPNYN) TYPE(*DEC)
             DCL        VAR(&DESC) TYPE(*CHAR) LEN(25)
             DCL        VAR(&OUTQ) TYPE(*CHAR) LEN(10)
             DCL        VAR(&HOLD) TYPE(*CHAR) LEN(4)
             DCL        VAR(&SAVE) TYPE(*CHAR) LEN(4)
             DCL        VAR(&NQSTAT) TYPE(*CHAR) LEN(01)
             DCL        VAR(&NIGHTQ) TYPE(*CHAR) LEN(10)
             DCL        VAR(&COPYN) TYPE(*DEC)
             DCL        VAR(&LOADID) TYPE(*DEC)
             DCL        VAR(&EMAILS) TYPE(*CHAR) LEN(1)
             DCL        VAR(&EMAILADDR) TYPE(*CHAR) LEN(50)
             DCL        VAR(&COMP) TYPE(*DEC) LEN(3 0)
             DCL        VAR(&COMPA) TYPE(*CHAR) LEN(3)
             DCL        VAR(&COPY) TYPE(*DEC) LEN(2 0)
             DCL        VAR(&LOAD) TYPE(*DEC) LEN(7 0)
             DCL        VAR(&LOADA) TYPE(*CHAR) LEN(7)

             DCL        VAR(&DATA) TYPE(*CHAR) LEN(10)
             DCL        VAR(&DATAH) TYPE(*CHAR) LEN(10)
             DCL        VAR(&TEST) TYPE(*CHAR) LEN(6)
             DCL        VAR(&CUSTPO#) TYPE(*CHAR) LEN(15)
             DCL        VAR(&POFILENM) TYPE(*CHAR) LEN(24)

/*----------------------------------------------------------------- */
/* PARMS FOR THE RETRIEVE OVERLAY PARAMETERS PROGRAM                */
/*----------------------------------------------------------------- */
             DCL        VAR(&TYPE) TYPE(*CHAR) LEN(2)
             DCL        VAR(&DEPT) TYPE(*CHAR) LEN(6)
             DCL        VAR(&OVERLAY) TYPE(*CHAR) LEN(8)
             DCL        VAR(&CDDPGM) TYPE(*CHAR) LEN(10)
             DCL        VAR(&CDDFILE) TYPE(*CHAR) LEN(10)
             DCL        VAR(&ADDLOBJ) TYPE(*CHAR) LEN(10)
             DCL        VAR(&SENDPHONE) TYPE(*CHAR) LEN(10)
             DCL        VAR(&SENDFAX) TYPE(*CHAR) LEN(10)
             DCL        VAR(&OVOUTQ) TYPE(*CHAR) LEN(10)
             DCL        VAR(&OVHOLD) TYPE(*CHAR) LEN(4)
             DCL        VAR(&OVSAVE) TYPE(*CHAR) LEN(4)
             DCL        VAR(&DRAWER) TYPE(*DEC) LEN(3 0)
             DCL        VAR(&PRTDEVTYPE) TYPE(*CHAR) LEN(10)
             DCL        VAR(&LENGTH) TYPE(*DEC) LEN(3 0)
             DCL        VAR(&WIDTH) TYPE(*DEC) LEN(3 0)
             DCL        VAR(&CPI) TYPE(*DEC) LEN(3 1)
             DCL        VAR(&LPI) TYPE(*DEC) LEN(2 0)
             DCL        VAR(&OVERFLOW) TYPE(*DEC) LEN(3 0)
             DCL        VAR(&ROTATE) TYPE(*CHAR) LEN(5)
             DCL        VAR(&COMMENT1) TYPE(*CHAR) LEN(50)
             DCL        VAR(&COMMENT2) TYPE(*CHAR) LEN(50)
             DCL        VAR(&COMMENT3) TYPE(*CHAR) LEN(50)
             DCL        VAR(&COMMENT4) TYPE(*CHAR) LEN(50)
             DCL        VAR(&COMMENT5) TYPE(*CHAR) LEN(50)
             DCL        VAR(&RECSTS) TYPE(*CHAR) LEN(1)

/********************************************************************/
/* CHANGE VARIABLES FOR USE IN PROGRAM                              */
/*------------------------------------------------------------------*/
             CHGVAR     VAR(&COMP) VALUE(&COMPNYN)
             CHGVAR     VAR(&COMPA) VALUE(&COMP)
             CHGVAR     VAR(&LOAD) VALUE(&LOADID)
             CHGVAR     VAR(&LOADA) VALUE(&LOAD)

/*------------------------------------------------------------------*/
             CHGVAR     VAR(&COPY) VALUE(&COPYN)
             CHGVAR     VAR(&TYPE) VALUE('IN') /* INVOICE */
             IF         COND(&EMAILS *EQ 'E') THEN(DO)
             CHGVAR     VAR(&SAVE) VALUE('*YES')
             CHGVAR     VAR(&HOLD) VALUE('*YES')
             CHGVAR     VAR(&DEPT) VALUE('JCOEML') /* JCO EMAIL   */
             ENDDO
             IF         COND(&EMAILS *EQ 'P') THEN(DO)
             CHGVAR     VAR(&DEPT) VALUE('JCOINV') /* JCO INVOICE */
             ENDDO

/*------------------------------------------------------------------*/
/* CHECK IF USING PROD OR TEST DATA LIBRARY.                        */
/*------------------------------------------------------------------*/
             CALL       PGM(CAQXXFR) PARM(' ' &DATA &DATAH)
             IF         COND(&DATA *NE 'PRKFLIB') THEN(DO)
             CHGVAR     VAR(&TEST) VALUE('- TEST')
             ENDDO

/********************************************************************/
/* GET THE PRINTER OVERRIDES FROM CO DOCUMENTS DEFAULT FILE         */
/*----------------------------------------------------------------- */
             CALL       PGM(PMG9XFR) PARM(' ' &COMP &TYPE &DEPT +
                          &OVERLAY &CDDPGM &CDDFILE &ADDLOBJ +
                          &SENDPHONE &SENDFAX &OVOUTQ &OVHOLD +
                          &OVSAVE &DRAWER &PRTDEVTYPE &LENGTH +
                          &WIDTH &LPI &CPI &OVERFLOW &ROTATE +
                          &COMMENT1 &COMMENT2 &COMMENT3 &COMMENT4 +
                          &COMMENT5 &RECSTS)

             IF         COND(&OVERLAY *EQ '     ') THEN(CHGVAR +
                          VAR(&OVERLAY) VALUE('*NONE'))
             IF         COND(&CDDFILE *NE '       ') THEN(OVRPRTF +
                          FILE(&CDDFILE) DEVTYPE(&PRTDEVTYPE) +
                          PAGESIZE(&LENGTH &WIDTH) LPI(&LPI) +
                          CPI(&CPI) OVRFLW(&OVERFLOW) +
                          DRAWER(&DRAWER) PAGRTT(&ROTATE) +
                          FRONTOVL(&OVERLAY) OUTQ(&OVOUTQ) +
                          HOLD(&OVHOLD) SAVE(&OVSAVE))

/********************************************************************/
/* CALL THE PRINT PLANT STYLE INVOICE PROGRAM                       */
/********************************************************************/
             IF         COND(&EMAILS *EQ 'P') THEN(DO)
             IF         COND(&CDDFILE *EQ '       ') THEN(OVRPRTF +
                          FILE(PNI9PFR$) OUTQ(&OUTQ) COPIES(&COPY) +
                          HOLD(&HOLD) SAVE(&SAVE))
             CALL       PGM(PNI9PFR) PARM(&RETURN &LOAD)
             ENDDO

/********************************************************************/
/* EMAIL TO RECIPIENT, IF REQUESTED                                 */
/********************************************************************/
             IF         COND(&EMAILS *EQ 'E') THEN(DO)
             IF         COND(&CDDFILE *EQ '       ') THEN(OVRPRTF +
                          FILE(PNJAPFR$) OUTQ(&OUTQ) COPIES(&COPY) +
                          HOLD(&HOLD) SAVE(&SAVE))

             CALL       PGM(PNJAPFR) PARM(&RETURN &LOAD)

    /* S16998 - Get/Use Customer PO# as Filename PDF Attachment +
                and add 'INV' after PO#.                             */
             CALL       PGM(PUR6XFR) PARM('' &LOADA &CUSTPO#)
             CHGVAR     VAR(&POFILENM) VALUE(&CUSTPO# *BCAT 'INV' +
                          *TCAT '.PDF')

         /*  ESEND/ESNDMAIL RECIPIENT(&EMAILADDR) SUBJECT('Load ID' +
                          *BCAT &LOADA *BCAT 'for Company' *BCAT +
                          &COMPA) MSG('Load ID' *BCAT &LOADA *BCAT +
                          'for  Company' *BCAT &COMPA) ATTLIST((* +
                          *PDF *N PNJAPFR$))                         */
             ESEND/ESNDMAIL RECIPIENT(&EMAILADDR) SUBJECT('Load ID' +
                          *BCAT &LOADA *BCAT 'for Company' *BCAT +
                          &COMPA) MSG('Load ID' *BCAT &LOADA *BCAT +
                          'for Company' *BCAT &COMPA *BCAT &TEST) +
                          ATTLIST((&POFILENM *PDF *N PNJAPFR$))
             ENDDO

 END:        ENDPGM
