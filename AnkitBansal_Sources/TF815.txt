      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Triumph Foods
      * PROGRAM:     TF815
      * TITLE:       Retrieve Year/Period/Week for a Date
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     09/29/05
      *
      * FUNCTION:  All retrievals are made with:
      *               1) the incoming date and
      *               2) a hardcoded company of 350
      *
      *            The values retrieved depend on the incoming flag value:
      *            A=Accounting or P=Production.
      *
      *            For P=Production: This program retrieves the HPS year/period/week.
      *
      *            For A=Accounting: This program retrieves the Accounting year/period.
      *                              Week is set to zero.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 11/30/06  LeAnne Ramsey
      *           Changed to use the Datamart Calendar file.
      *
      * 12/06/06  LeAnne Ramsey
      *           We changed the file name/text on the Calendar file.
      *
      * 09/10/07  LeAnne Ramsey
      *           Recompile only. We added "sales date" fields to the Calendar file.
      *
      * 01/21/08  LeAnne Ramsey
      *           Changed logic to retrieve either P=Production or A=Accounting values.
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fcalcald   if   e           k disk
      *  Calendar file
      *
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      ****************************************************************
      * ARRAYS AND TABLES
      ****************************************************************
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Work fields
      *
     D wkcocd          s              3  0 inz(350)
      *
      *
      * Parms
      *
     D xxdt            s              8  0
     D xxapfl          s              1
     D xxyr            s              4  0
     D xxpe            s              2  0
     D xxwk            s              2  0
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * Mainline
      ****************************************************************
      *
      * Retrieve the year/period/week for the company/date.
      *
     C     key01         chain     calcald                            92
     C                   if        *in92 = *off                                 If hit
      *
     C                   select
      *       Accounting
     C                   when      xxapfl = 'A'
     C                   z-add     caacyr        xxyr
     C                   z-add     caaper        xxpe
     C                   z-add     0             xxwk
      *       Production
     C                   when      xxapfl = 'P'
     C                   z-add     cahpsyr       xxyr
     C                   z-add     cahpspe       xxpe
     C                   z-add     cahpswk       xxwk
     C                   endsl
      *
     C                   endif                                                  If hit
      *
     C                   seton                                        lr
      /EJECT
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Parm lists
      *
     C     *entry        plist
     C                   parm                    xxdt
     C                   parm                    xxapfl
     C                   parm                    xxyr
     C                   parm                    xxpe
     C                   parm                    xxwk
      *
      *
     C     key01         klist
     C                   kfld                    wkcocd
     C                   kfld                    xxdt
      *
     C                   endsr
      /EJECT
