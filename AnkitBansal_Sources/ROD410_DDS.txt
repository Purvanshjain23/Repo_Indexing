     A*%%TS  SD  20070118  150124  ISLRAMS     REL-V5R3M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        MEAT COST
     A*  DISPLAY FILE:  ROD410
     A*  TITLE:         SPECIFY WEEKLY MEAT COST CALCULATIONS
     A*  PROGRAMMER:    LEANNE RAMSEY
     A*  CREATED:       12/06/06
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/ROPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A                                      CF04(04 'PROMPT')
     A                                      CF08(08 'SUBMIT')
     A                                      CF17(17 'PRINT OPTIONS')
     A                                      CF20(20 'WRKSPLF')
     A*
     A*---------------------------------------------------------------
     A* RECORD FORMAT ONE
     A*---------------------------------------------------------------
     A          R RO410R1
     A*%%TS  SD  20070118  150124  ISLRAMS     REL-V5R3M0  5722-WDS
     A                                      SETOF(31)
     A                                      SETOF(32)
     A                                      SETOF(51)
     A                                      SETOF(52)
     A                                      OVERLAY
     A                                      RTNCSRLOC(&RCD &FLD)
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 24'RESOURCE OPTIMIZATION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USR)
     A                                      DSPATR(HI)
     A                                  2 20'SPECIFY WEEKLY MEAT COST CALCULATI-
     A                                      ON'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@JOB)
     A                                      DSPATR(HI)
     A                                  4  2'Company ........:'
     A            D1CONO    R        O  4 20REFFLD(ROREF/@@CONO *LIBL/ROPREF)
     A                                  4 33'Year'
     A                                      DSPATR(UL)
     A                                      COLOR(BLU)
     A                                  4 39'Week'
     A                                      DSPATR(UL)
     A                                      COLOR(BLU)
     A                                  4 45'Period'
     A                                      DSPATR(UL)
     A                                      COLOR(BLU)
     A                                  5  2'Week-ending date:'
     A            D1WEMDY   R        O  5 20REFFLD(TFREF/@@DT06 *LIBL/TFPREF)
     A                                      EDTWRD('  /  /  ')
     A            D1YR      R        O  5 33REFFLD(TFREF/@@YR *LIBL/TFPREF)
     A                                      EDTCDE(Z)
     A            D1WK      R        O  5 40REFFLD(TFREF/@@WK *LIBL/TFPREF)
     A                                      EDTCDE(Z)
     A            D1PE      R        O  5 47REFFLD(ROREF/@@PE *LIBL/ROPREF)
     A                                      EDTCDE(Z)
     A                                  8  2'Preliminary/final .......'
     A                                  8 28'?'
     A            D1PFCD    R        B  8 30REFFLD(ROREF/@@PFCD *LIBL/ROPREF)
     A  32
     AO 52                                  DSPATR(PC)
     A  32                                  DSPATR(RI)
     A N32                                  DSPATR(HI)
     A            D1PFDS    R        O  8 33REFFLD(@@PFDS)
     A                                  9  2'Include frozen product ..'
     A                                  9 28'?'
     A            D1FZFL    R        B  9 30REFFLD(ROREF/@@FLAG *LIBL/ROPREF)
     A  31
     AO 51                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A N31                                  DSPATR(HI)
     A*
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R RO410K1
     A*%%TS  SD  20060928  082122  ISLRAMS     REL-V5R3M0  5722-WDS
     A  43                             21  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A                                 23 11'F4=Prompt'
     A                                 23 22'F8=Submit'
     A                                 23 33'F17=Print Options'
     A                                 23 52'F20=Spool Files'
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *---------------------------------------------------------------
     A          R RO410ES                   SFL SFLMSGRCD(24)
      *
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
     A*---------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*---------------------------------------------------------------
     A*
     A          R RO410EC                   SFLCTL(RO410ES)
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
