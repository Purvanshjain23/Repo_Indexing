     h option(*SRCSTMT:*NODEBUGIO)
      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Triumph Foods
      * PROGRAM:     TF680
      * TITLE:       Yields: ByProduct Weekly Yield Report (Detail)
      * PROGRAMMER:  LeAnne Ramsey
      * CREATED:     02/09/09
      *
      * FUNCTION:  Batch program to print the report at the Item level.
      *            This report is only BP Items.
      *            Note: The Picnic Cushions and Kill Offal, even though they are BPs, are
      *                  on the "Primal" Report.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 03/31/09  LeAnne Ramsey
      *           Marty H. and Brad Hamilton wanted to add 4 columns (the SBF/TF Per Head
      *           columns for the 740-Kill Offal lines. We do NOT have room. So, per
      *           Damon Ginther, we will try moving 740-Kill to the "primal" reports.
      *
      * 04/06/09  LeAnne Ramsey
      *           We will now move all the 750-Render BPs to the PRIMAL reports.
      *
      * 02/08/22  Danny Nguyen   - DO2484 - WI479 STF Variance Reporting
      *           Recompile only due to TFP076 DBF change.
      *           No changes to report.
      *
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fome8rel0  if   e           k disk
      *    OM Product type
      *
      *
     Fomfarel1  if   e           k disk
      *    OM Product class
      *
      *
     Fpmaprel1  if   e           k disk
      *    TF Meat cost group
      *
      *
     Ftfl076a   if   e           k disk
      *  Weekly yields  (records selected with open query)
      *
      *
     Fprint198  o    f  198        printer oflind(*inof)
      *
      /EJECT
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Standard fields
      *
     D dash            s            198    inz(*all'-')
     d rtime           s              6  0
      *
      *
      * Control fields
      *
     D first           s              1    inz('Y')
     D newisty         s              1    inz('Y')
     d svtfclcd        s                   like(ywtfclcd)
     d svisgrcd        s                   like(ywisgrcd)
     d svisclcd        s                   like(ywisclcd)
     d svsumlev        s                   like(ywsumlev)
     d cnt             s              7  0
     d cntsum          s              7  0
      *
      *
      * Workfields
      *
     D wkdesc          s             47
     d wkpc            s             15  6
     D wkalph          s              3
     D wktfcgcd        s                   like(ywtfcgcd) inz('BP')
      *
      *
      * Parms
      *
     D xxorigin        s              1
      *
      *
      * Print fields
      *
     d l1sdifflb       s                   like(ywspulb)
     d l1tdifflb       s                   like(ywtpulb)
      *
     d h1demand        s             35
     d h2desc          s             34
     d h3desc          s             66
      *
     d t1desc          s             74
     d t1sdiffpc       s                   like(ywcalcypc)
     d t1tdiffpc       s                   like(ywcalcypc)
      *
      *
      * Array index
      *
     d x               s              1  0
      *
      ****************************************************************
      * ARRAYS AND TABLES
      ****************************************************************
      *
     D arspulb         s             11  2 dim(3)
     D arsstdplb       s             11  2 dim(3)
     D arsdifflb       s             11  2 dim(3)
      *
     D artpulb         s             11  2 dim(3)
     D artstdplb       s             11  2 dim(3)
     D artdifflb       s             11  2 dim(3)
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *---------------------------------------------------------------
      * Local data area.
      *---------------------------------------------------------------
      *
     Dlda             uds                  dtaara(*lda)
     D  ldpfcd                 1      1
     D  ldyr                   2      5  0
     D  ldwk                   6      7  0
      *
     D  ldwedt                29     36  0
     D  ldwemdy               44     49  0
      *
     D  ldtfcgcd             139    140
     D  ldtfcgds             141    155
      *
     D  ldtfclcd             156    158
     D  ldtfclds             159    188
      *
     D  ldistycd             189    191  0
     D  ldistyds             192    221
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ***************************************************************************************
      * MAINLINE
      ***************************************************************************************
      *
      * Print headings
      *
     C                   exsr      $h1hdr
      *
      * We will drive this by reading the master file of Item Structure Types.
      *
     C     *loval        setll     ome8rel0
      *
     C                   dou       *in90 = *on                                  Main do
     C                   read      ome8rel0                               90
     C                   if        *in90 = *off and                             If not eof
     C                             (ldistycd = 0 or ldistycd = e8rgnb)
      *
     C                   z-add     0             cnt
     C                   z-add     0             cntsum
     C                   move      yes           newisty
      *
     C                   move      e8rgnb        wkalph
     C                   eval      h2desc = %trim(wkalph) + '-'
     C                                      + %trim(e8dctx)
      *
      * Print BPs
     C                   exsr      $rtv076
      *
      * Print Item Structure Type total
      *
     C                   exsr      $t2tot
      *
     C                   endif                                                  If not eof
     C                   enddo                                                  Main do
      *
      * EOF processing
     C                   if        first = yes
     C                   except    nodata
     C                   else
     C                   exsr      $t3tot
     C                   endif
      *
     C                   seton                                        lr
      /EJECT
      *---------------------------------------------------------------
      * Retrieve Weekly Yield Records
      *---------------------------------------------------------------
      *
     C     $rtv076       begsr
      *
     C                   move      *blank        svsumlev
     C                   move      *blank        svtfclcd
     C                   z-add     0             svisclcd
     C                   z-add     0             svisgrcd
      *
      * Retrieve all Weekly Yield records for this:
      *  1) Item Structure Type
      *  2) TF Class Group = 'BP'
      *  Exclude the following (even though they are ByProducts, they are printed on the
      *  PRIMAL reports:
      *    1) Picnic Cushions
      *    2) Kill Offal
      *
     C     key01         setll     tfl076a
      *
     C                   dou       *in91 = *on                                  Do loop
     C     key01         reade     tfl076a                                91
     C                   if        *in91 = *off                                 If not EOF
      *
      * Exclude Picnic Cushions, Kill Offal, Render
      *
     C                   if        (ywistycd = 600 and ywisgrcd = 630 and       If exclude
     C                             ywtfclcd = 'BP5') or
     C                             (ywistycd = 740 and ywtfclcd = 'OFF' and
     C                              ywtfcgcd = 'BP') or
     C                             (ywistycd = 750 and ywtfcgcd = 'BP')
     C                   else
      * Break logic
     C                   select
     C                   when      newisty = yes
     C                   exsr      $h2hdr
     C                   exsr      $h3hdr
     C                   move      no            newisty
     C                   move      ywsumlev      svsumlev
     C                   move      ywtfclcd      svtfclcd
     C                   z-add     ywisclcd      svisclcd
     C                   z-add     ywisgrcd      svisgrcd
     C                   seton                                        87
      *
     C                   when      ywsumlev <> svsumlev
     C                   exsr      $t1tot
     C                   exsr      $h3hdr
     C                   move      ywsumlev      svsumlev
     C                   move      ywtfclcd      svtfclcd
     C                   z-add     ywisclcd      svisclcd
     C                   z-add     ywisgrcd      svisgrcd
      *
     C                   when      ywtfclcd <> svtfclcd or
     C                             ywisclcd <> svisclcd or
     C                             ywisgrcd <> svisgrcd
     C                   move      ywtfclcd      svtfclcd
     C                   z-add     ywisclcd      svisclcd
     C                   z-add     ywisgrcd      svisgrcd
     C                   seton                                        87
     C                   endsl
      *
      * Detail processing
      *
      * Calc the Pounds Difference as: Produced less Standard Production
      *
     C     ywspulb       sub       ywsstdplb     l1sdifflb
     C     ywtpulb       sub       ywtstdplb     l1tdifflb
      *
      *
      * Accumulate
     C                   add       ywspulb       arspulb
     C                   add       ywsstdplb     arsstdplb
     C                   add       l1sdifflb     arsdifflb
      *
     C                   add       ywtpulb       artpulb
     C                   add       ywtstdplb     artstdplb
     C                   add       l1tdifflb     artdifflb
      *
      * Print detail line
     C                   exsr      $l1dtl
      *
     C                   endif                                                  If exclude
     C                   endif                                                  If not EOF
     C                   enddo                                                  Do loop
      *
      * Print out last Summary Level Total
      *
     C                   exsr      $t1tot
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Print detail line
      *---------------------------------------------------------------
      *
     C     $l1dtl        begsr
      *
      * Have you hit overflow?
      *
     C                   if        *inof = *on
     C                   exsr      $h1hdr
     C                   exsr      $h2hdr
     C                   if        *in86 = *on
     C                   exsr      $h3hdr
     C                   endif
     C                   endif
      *
     C                   except    l1dtl
     C                   setoff                                       8687
     C                   move      no            first
     C                   add       1             cnt
     C                   add       1             cntsum
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Print total line for BP Summary Level
      *---------------------------------------------------------------
      *
     C     $t1tot        begsr
      *
     C                   z-add     1             x
      *
     C                   if        cntsum > 1                                   If printing
     C                   seton                                        88
      *
     C                   eval      wkdesc = %subst(h3desc:19:47)
     C                   evalr     t1desc = 'Total for ' +
     C                                      %trim(wkdesc)
     C                   exsr      $calc
      *
     C                   if        *inof = *on
     C                   exsr      $h1hdr
     C                   exsr      $h2hdr
     C                   seton                                        89
     C                   endif
     C                   except    t1tot
      *
     C                   setoff                                       8889
     C                   endif                                                  If printing
      *
      * Clear/initialize
     C                   exsr      $clear
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Print total line for Item Structure Type
      *---------------------------------------------------------------
      *
     C     $t2tot        begsr
      *
     C                   z-add     2             x
      *
     C                   if        cnt > 1                                      If more than 1
     C                   seton                                        88
      *
     C                   evalr     t1desc = 'Total for ' + %trim(h2desc) + ':'
     C                   exsr      $calc
      *
     C                   if        *inof = *on
     C                   exsr      $h1hdr
     C                   endif
     C                   except    t1tot
      *
     C                   setoff                                       8889
     C                   endif                                                  If more than 1
      *
      * Clear/initialize
     C                   exsr      $clear
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Print Report Total
      *---------------------------------------------------------------
      *
     C     $t3tot        begsr
      *
     C                   seton                                        8889
     C                   z-add     3             x
      *
     C                   evalr     t1desc = 'Report Total:'
     C                   exsr      $calc
      *
     C                   if        *inof = *on
     C                   exsr      $h1hdr
     C                   else
     C                   except    blank
     C                   endif
      *
     C                   except    t1tot
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Calcs for Total Line
      *---------------------------------------------------------------
      *
     C     $calc         begsr
      *
      * Calc the Difference in Pounds as a Percent of Standard Production Pounds
      *
      *         Seaboard
     C                   if        arsstdplb(x) <> 0
     C                   eval      wkpc = (arsdifflb(x) / arsstdplb(x)) * 100
     C                   if        wkpc < 999
     C                   z-add(h)  wkpc          t1sdiffpc
     C                   endif
     C                   endif
      *
      *          Triumph
     C                   if        artstdplb(x) <> 0
     C                   eval      wkpc = (artdifflb(x) / artstdplb(x)) * 100
     C                   if        wkpc < 999
     C                   z-add(h)  wkpc          t1tdiffpc
     C                   endif
     C                   endif
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Clear arrays
      *---------------------------------------------------------------
      *
     C     $clear        begsr
      *
     C                   z-add     0             arsstdplb(x)
     C                   z-add     0             arspulb  (x)
     C                   z-add     0             arsdifflb(x)
      *
     C                   z-add     0             artstdplb(x)
     C                   z-add     0             artpulb  (x)
     C                   z-add     0             artdifflb(x)
      *
     C                   z-add     0             cntsum
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Print report heading
      *---------------------------------------------------------------
      *
     C     $h1hdr        begsr
      *
     C                   except    h1hdr
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Item Structure Header
      *---------------------------------------------------------------
      *
     C     $h2hdr        begsr
      *
     C                   if        *inof = *on
     C                   exsr      $h1hdr
     C                   endif
      *
     C                   except    h2hdr
     C                   seton                                        87
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * BP Summary Level Header
      *---------------------------------------------------------------
      *
     C     $h3hdr        begsr
      *
     C                   if        *inof = *on
     C                   exsr      $h1hdr
     C                   exsr      $h2hdr
     C                   endif
      *
     C                   exsr      $h3desc
      *
     C                   except    h3hdr
     C                   seton                                        8687
      *
     C                   endsr
      /EJECT
      *------------------------------------------------------------------------------------------
      * Concatenate together a description for the ByProduct Summary Level
      *------------------------------------------------------------------------------------------
      *
     C     $h3desc       begsr
      *
      * The 'descriptions' on the report aren't really making much sense. So, we keep
      * trying to come up with some combination of fields that are "descriptive".
      *
      * Currently, the "description" will be one of these:
      *  1) Summary Level
      *  2) Meat Cost Group Description + Summary Level
      *  3) Item Structure Class Description + Summary Level
      *
      * Default to Summary Level
      *
     C                   eval      h3desc = 'BP Summary Level: ' +
     C                                      %trim(ywsumlev)
      *
      * Then, override to the Meat Cost Group Code Description
      *
     C     ywcgcd        chain     pmaprel1                           92
     C                   if        *in92 = *off                                 If hit
      *
     C                   eval      h3desc = 'BP Summary Level: ' +
     C                                      %trim(aprnna) + ' ' +
     C                                      %trim(ywsumlev)
      *
      * Then, override again to the Item Structure Class Description when the
      * Meat Cost Group Summary Level is 'CL'
      *
     C                   if        apeetx = 'CL'                                If cl
     C     ywisclcd      chain     omfarel1                           92
     C                   if        *in92 = *off
      *
     C                   eval      h3desc = 'BP Summary Level: ' +
     C                                      %trim(fadetx) + ' ' +
     C                                      %trim(ywsumlev)
     C                   endif
     C                   endif                                                  If cl
     C                   endif                                                  If hit
      *
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Parm lists
      *
     C     *entry        plist
     C                   parm                    xxorigin
      *
      * Key Lists
      *
     C     key01         klist
     C                   kfld                    e8rgnb
     C                   kfld                    wktfcgcd
      *
      * Retrieve time for report heading.
      *
     C                   time                    rtime
      *
      * This report is generated:
      *  1) From Inventory Stock Closing
      *  2) As a Preliminary/Final from Margin Adjustment Close
      *  3) On-demand
      *
     C                   select
     C                   when      xxorigin = 'N'
     C                   eval      %subst(h1demand:3:28) =
     C                             'From Inventory Stock Closing'
      *
     C                   when      ldpfcd = 'D'
     C                   eval      %subst(h1demand:13:9) = 'On-Demand'
      *
     C                   when      ldpfcd = 'F'
     C                   eval      %subst(h1demand:4:29) =
     C                             'Final Margin Adjustment Close'
      *
     C                   when      ldpfcd = 'P'
     C                   eval      %subst(h1demand:1:35) =
     C                             'Preliminary Margin Adjustment Close'
     C                   endsl
      *
      *
      * Set up Report Headings
      *
     C                   if        ldistycd = 0
     C                   seton                                        95
     C                   endif
      *
     C                   if        ldtfcgcd = *blank
     C                   seton                                        94
     C                   endif
      *
     C                   if        ldtfclcd = *blank
     C                   seton                                        93
     C                   endif
      *
     C                   endsr
      /EJECT
      *-------------------------------------------------------------------------------------
      * Report heading lines
      *-------------------------------------------------------------------------------------
      *
     Oprint198  e            h1hdr          1 01
     O                       sdpgm               10
     O                                          102 'BYPRODUCT WEEKLY YIELD '
     O                                          115 'REPORT-DETAIL'
     O                                          188 'DATE'
     O                       udate         y    198
      *
     O          e            h1hdr          1
     O                       sdusr               10
     O                       h1demand           116
     O                                          188 'TIME'
     O                       rtime              198 '  :  :  '
      *
     O          e            h1hdr          1
     O                                          188 'PAGE'
     O                       page          z    198
      *
      *
     O          e            h1hdr          1
     O                                           17 'Week-ending date:'
     O                       ldwemdy             26 '  /  /  '
      *
      *
     O                                           60 'TF Class group code:'
     O               94                          65 'All'
     O              n94      ldtfcgcd            64
     O              n94      ldtfcgds            82
      *
     O          e            h1hdr          1
     O                                           17 'Year/Week:'
     O                       ldyr          m     23
     O                       ldwk          m     27
      *
     O                                           60 'TF Classification code:'
     O               93                          65 'All'
     O              n93      ldtfclcd            65
     O              n93      ldtfclds            97
      *
     O          e            h1hdr          2
     O                                           60 'Item structure type code:'
     O               95                          65 'All'
     O              n95      ldistycd            65
     O              n95      ldistyds            97
      *
      *-------------------------------------------------------------------------------------
      * Column headings
      *-------------------------------------------------------------------------------------
      *
     O          e            h1hdr       1  1
     O                                            9 'TF '
     O                                           29 'Co-'
      *
     O                                           89 'SBF Std'
     O                                          105 'SBF'
     O                                          121 'SBF Produced'
     O                                          132 'SBF Diff'
      *
     O                                          151 'TF Std'
     O                                          167 'TF'
     O                                          184 'TF Produced'
     O                                          195 'TF Diff'
      *
     O          e            h1hdr       0  1
     O                                            2 'IS'
     O                                            9 'Cls'
     O                                           13 'TF'
     O                                           18 'IS'
     O                                           23 'IS'
     O                                           33 'Itm'
     O                                           29 'Own'
     O                                           42 'Product'
      *
     O                                           89 'Production'
     O                                          105 'Produced'
     O                                          121 'Minus Std'
     O                                          132 'as % of'
      *
     O                                          151 'Production'
     O                                          167 'Produced'
     O                                          184 'Minus Std'
     O                                          195 'as % of'
      *
     O          e            h1hdr       0  1
     O                                            4 'Type'
     O                                            9 'Grp'
     O                                           14 'Cls'
     O                                           19 'Grp'
     O                                           24 'Cls'
     O                                           29 'Flg'
     O                                           33 'Typ'
     O                                           41 'Code'
     O                                           55 'Description'
      *
     O                                           89 'Pounds'
     O                                          105 'Pounds'
     O                                          121 'Prod Pounds'
     O                                          132 'Std Lbs'
      *
     O                                          151 'Pounds'
     O                                          167 'Pounds'
     O                                          184 'Prod Pounds'
     O                                          195 'Std Lbs'
      *
     O          e            h1hdr          0
     O                       dash               198
      *
      *-------------------------------------------------------------------------------------
      * Heading line for Item Structure type
      *-------------------------------------------------------------------------------------
      *
     O          e            h2hdr       2  0
     O                       h2desc              34
      *
     O          e            h2hdr       0  0
     O                       h2desc              34
      *
      *
      *-------------------------------------------------------------------------------------
      * BP Summary Level Heading
      *-------------------------------------------------------------------------------------
      *
     O          e            h3hdr       2  0
     O                       h3desc              70
      *
     O          e            h3hdr       0  0
     O                       h3desc              70
      *
      *-------------------------------------------------------------------------------------
      * Detail line
      *-------------------------------------------------------------------------------------
      *
     O          e    87n86   l1dtl       1
      *
     O          e            l1dtl       1
     O               87      ywtfcgcd             9
     O               87      ywtfclcd            14
     O               87      ywisgrcd      m     20
     O               87      ywisclcd      m     25
     O                       ywcoownfl           28
     O                       ywitycd             34
     O                       ywprcd        z     41
     O                       ywstar              43
     O                       ywprds              74
      *
     O                       ywsstdplb     k     90
     O                       ywspulb       k    106
     O                       l1sdifflb     kb   122
      *
     O                       ywtstdplb     k    152
     O                       ywtpulb       k    168
     O                       l1tdifflb     kb   185
      *
      *-------------------------------------------------------------------------------------
      * Total Line for all Levels
      *-------------------------------------------------------------------------------------
      *
     O          e    89      t1tot          1
      *
     O          e   n89      t1tot       1  1
     O                                           89 '--------------'
     O                                          105 '--------------'
     O                                          121 '--------------'
     O                                          132 '--------'
      *
     O                                          151 '--------------'
     O                                          167 '--------------'
     O                                          184 '--------------'
     O                                          195 '--------'
      *
     O          e    88      t1tot       0  0
     O                       t1desc              74
     O                       arsstdplb(x)  k     90
     O                       arspulb  (x)  k    106
     O                       arsdifflb(x)  k    122
     O                       t1sdiffpc     k    133
      *
     O                       artstdplb(x)  k    152
     O                       artpulb  (x)  k    168
     O                       artdifflb(x)  k    185
     O                       t1tdiffpc     k    196
      *
     O          e            t1tot          1
     O                       t1desc              74
     O                       arsstdplb(x)  k     90
     O                       arspulb  (x)  k    106
     O                       arsdifflb(x)  k    122
     O                       t1sdiffpc     kb   133
      *
     O                       artstdplb(x)  k    152
     O                       artpulb  (x)  k    168
     O                       artdifflb(x)  k    185
     O                       t1tdiffpc     kb   196
      *
      *-------------------------------------------------------------
      * Blank
      *-------------------------------------------------------------
      *
     O          e            blank       1
      *
      *-------------------------------------------------------------
      * No data message line
      *-------------------------------------------------------------
      *
     O          e            nodata      1
     O                                           19 'No data meets your'
     O                                           39 'selection criteria.'
