/***************************************************************** */
/*                                                                 */
/*  PROGRAM NUMBER:  PKT8UPC                                       */
/*  PROGRAM NAME:    CALCULATE PRODUCER HISTORY                    */
/*  PROGRAMMER:      PURVA DROGE                                   */
/*  CREATE DATE:     12/02/99                                      */
/*                                                                 */
/*  FUNCTION:        THIS CL IS SUBMITTED FROM THE SCHEDULER       */
/*                   ON MONDAY EVENING FOR CALCULATING THE         */
/*                   PRODUCERS HISTORY FOR THE PRIOR WEEK, PRIOR   */
/*                   TWO WEEKS, AND PRIOR FOUR WEEKS.              */
/*                                                                 */
/*******************************************************************/
/*  Modification History                                           */
/*                                                                 */
/*  E9815 JJH 05/30/17 - PASS IN COMPANY NUMBER, AND PASS COMPANY  */
/*                       NUMBER INTO PKTBXFR.                      */
/*  E9815 RMC 02/09/21 - CHECK FOR HOLIDAY ->SUBMIT JOB TO RUN ON NEXT DAY */
/*******************************************************************/
             PGM        PARM(&COMPANY &PDEV &HOLD &SAVE &XCOPIES)
/* PRINT OPTION PARMS                                             */
             DCL        VAR(&COMPANY) TYPE(*CHAR) LEN(3)
             DCL        VAR(&PDEV) TYPE(*CHAR) LEN(10)
             DCL        VAR(&HOLD) TYPE(*CHAR) LEN(4)
             DCL        VAR(&SAVE) TYPE(*CHAR) LEN(4)
             DCL        VAR(&XCOPIES) TYPE(*CHAR) LEN(15)

/* SYSTEM NAME PARM                                               */

/* NUMERIC VARIABLES                                             */
             DCL        VAR(&COPIES) TYPE(*DEC) LEN(2 0)
/* DATE VARIABLES                                                 */
             DCL        VAR(&COMPANN) TYPE(*DEC) LEN(3 0)
   DCL        VAR(&CURDAT)  TYPE(*CHAR) LEN(6)
   DCL        VAR(&CURDATA) TYPE(*CHAR) LEN(7)
   DCL        VAR(&CURYYA)  TYPE(*CHAR) LEN(2)
   DCL        VAR(&NEXTDAY) TYPE(*DEC ) LEN(7 0)
   DCL        VAR(&HOLIDAY)  TYPE(*CHAR) LEN(1)
   DCL        VAR(&CURDATE) TYPE(*DEC ) LEN(7 0)
   DCL        VAR(&NEXTDAYA) TYPE(*CHAR) LEN(7)
   DCL        VAR(&NEXTMDY ) TYPE(*CHAR) LEN(6)
   DCL        VAR(&JOB)  TYPE(*CHAR) LEN(10)

/* SBDPRTOVR VARIABLE                                          */
             DCL        VAR(&CMD) TYPE(*CHAR) LEN(512)

 /***********************************************************************/
 /* CHECK FOR A MONDAY HOLIDAY - IF Y THEN SUBMIT JOB TO RUN NEXT DAY  */
 /***********************************************************************/
             RTVSYSVAL  SYSVAL(QDATE) RTNVAR(&CURDAT)
             CVTDAT     DATE(&CURDAT) TOVAR(&CURDAT) FROMFMT(*MDY) +
                          TOFMT(*YMD) TOSEP(*NONE)
             CHGVAR     VAR(&CURYYA) VALUE(%SUBSTRING(&CURDAT 1 2))
             IF (&CURYYA *LT '40') +
                CHGVAR VAR(&CURDATA) VALUE('1' *CAT &CURDAT)
             ELSE +
                CHGVAR VAR(&CURDATA) VALUE('0' *CAT &CURDAT)

             CHGVAR     VAR(&COMPANN) VALUE(&COMPANY)
             CHGVAR     VAR(&CURDATE) VALUE(&CURDATA)

             CALL       PGM(PMWBXFR) PARM('' &COMPANN &CURDATE &HOLIDAY)

             IF COND(&HOLIDAY = 'Y') THEN(DO)
             CALL       PGM(PMCYXFR) PARM('' &COMPANN &CURDATE &NEXTDAY)
               CHGVAR &NEXTDAYA VALUE(&NEXTDAY)
             CVTDAT     DATE(&NEXTDAYA) TOVAR(&NEXTMDY) +
                          FROMFMT(*CYMD) TOFMT(*MDY) TOSEP(*NONE)
             RTVJOBA    JOB(&JOB)
             SBMJOB     CMD(CALL PGM(PKT8UPC) PARM(&COMPANY &PDEV +
                          &HOLD &SAVE &XCOPIES)) JOB(&JOB) +
                          SCDDATE(&NEXTMDY) SCDTIME(*CURRENT)
             GOTO       CMDLBL(END)
             ENDDO
 /***********************************************************************/

/* CHANGE ALPHA PARMS TO NUMERIC VARIABLES                       */
             CHGVAR     VAR(&COPIES) VALUE(&XCOPIES)

/* CREATE PRINTER FILE OVERRIDE CODE                            */
   /* DETAIL  */
/*           SBDPRTOVR  CMDVAR(&CMD) FILE(PKTDPFR$) OUTQ(QPRINT) +
                          COPIES(&COPIES) HOLD(*YES) SAVE(&SAVE) */
/*           CALL       PGM(QCMDEXC) PARM(&CMD 512) */

   /* SUMMARY */
             SBDPRTOVR  CMDVAR(&CMD) FILE(PKVEPFR$) OUTQ(&PDEV) +
                          COPIES(&COPIES) HOLD(&HOLD) SAVE(&SAVE)
             CALL       PGM(QCMDEXC) PARM(&CMD 512)

/* CALCULATE PRODUCER HISTORY                                  */

             CALL       PGM(PKTBXFR) PARM(' ' &COMPANY)

/* DELETE THE DETAIL REPORT                                    */
/*           DLTSPLF    FILE(PKTDPFR$) */

             DLTOVR *ALL
 END:        ENDPGM
