     A*%%TS  SD  20050518  090245  ISLRAMS     REL-V5R2M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        TRIUMPH FOODS
     A*  DISPLAY FILE:  TFD706
     A*  TITLE:         VIEW ASSIGNED DEPOSITS
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  DATE:          05/18/05
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/TFPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CF03(03 'EXIT')
     A                                      CF05(05 'REFRESH')
     A*
     A*
     A*---------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*---------------------------------------------------------------
     A          R TF706S1                   SFL
     A*%%TS  SD  20050517  135912  ISLRAMS     REL-V5R2M0  5722-WDS
     A  85                                  SFLNXTCHG
     A            HDRRN1         4Y 0H
     A            D1MDY     R        O 10  7REFFLD(TFREF/@@DT06 *LIBL/TFPREF)
     A                                      EDTWRD('  /  /  ')
     A            D1TM      R        O 10 19REFFLD(TFREF/@@TM04 *LIBL/TFPREF)
     A                                      EDTWRD('  :  ')
     A            D1BANK         6   O 10 28
     A            D1AM      R        O 10 36REFFLD(TFREF/@@CPAM *LIBL/TFPREF)
     A                                      EDTCDE(K)
     A*
     A*---------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*---------------------------------------------------------------
     A          R TF706C1                   SFLCTL(TF706S1)
     A*%%TS  SD  20050518  090245  ISLRAMS     REL-V5R2M0  5722-WDS
     A                                      SFLSIZ(0050)
     A                                      SFLPAG(0012)
     A N82                                  ROLLUP(25 'PAGE FORWARD')
     A                                      OVERLAY
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 18'VIEW ASSIGNED DEPOSITS FOR A CASH -
     A                                      HEADER'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USR)
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@JOB)
     A                                      DSPATR(HI)
     A                                  3  2'Batch number:'
     A                                      DSPATR(HI)
     A            C1BTSN    R        O  3 16REFFLD(TFREF/@@BTSN *LIBL/TFPREF)
     A                                      EDTCDE(Z)
     A                                  3 26'Daily date:'
     A                                      DSPATR(HI)
     A            C1DDTMDY  R        O  3 38REFFLD(TFREF/@@DT06 *LIBL/TFPREF)
     A                                      EDTWRD('  /  / 0')
     A                                  3 51'Deposit Amounts            '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  4 30'Status:'
     A                                      DSPATR(HI)
     A            C1STDS    R        O  4 38REFFLD(TFREF/@@STDS *LIBL/TFPREF)
     A                                  4 53'Prior day:'
     A            C1DPDCCAM R        O  4 64REFFLD(TFREF/@@DPDCCAM *LIBL/TFPREF)
     A                                      EDTCDE(K)
     A                                  5 51'Current day:'
     A            C1DCDCCAM R        O  5 64REFFLD(TFREF/@@DCDCCAM *LIBL/TFPREF)
     A                                      EDTCDE(K)
     A                                  7  7'Deposit'
     A                                      DSPATR(HI)
     A                                  7 18'Deposit'
     A                                      DSPATR(HI)
     A                                  7 28'Bank'
     A                                      DSPATR(HI)
     A                                  7 43'Deposit'
     A                                      DSPATR(HI)
     A                                  8  9'Date'
     A                                      DSPATR(HI)
     A                                  8 20'Time'
     A                                      DSPATR(HI)
     A                                  8 28'Code'
     A                                      DSPATR(HI)
     A                                  8 44'Amount'
     A                                      DSPATR(HI)
     A            C1MDY     R        B  9  7REFFLD(TFREF/@@DT06 *LIBL/TFPREF)
     A                                      EDTWRD('  /  / 0')
     A                                      DSPATR(HI)
     A*
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R TF706K1
     A*%%TS  SD  20050518  080117  ISLRAMS     REL-V5R2M0  5722-WDS
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A                                 23 11'F5=Refresh'
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *---------------------------------------------------------------
     A          R TF706ES                   SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE CONTROL FORMAT
      *---------------------------------------------------------------
     A          R TF706EC                   SFLCTL(TF706ES)
      *
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
