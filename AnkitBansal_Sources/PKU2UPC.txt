/********************************************************************/
/*                                                                  */
/*  SYNOPSIS - CL PROGRAM FOR PRINTING THE STANDARD DEVIATION       */
/*                                                                  */
/*  SYSTEM    - HOG PROCUREMENT AND EVALUATION                      */
/*  DATE      - 03/22/00                                            */
/*  DEVELOPER - PURVA DROGE                                         */
/*                                                                  */
/********************************************************************/
/* MODIFICATIONS:                                                   */
/* 04/12/00  PKD  1) ADDED OPTIONAL SELECTION FOR SOURCE TYPE,      */
/*                   PRODUCER CODE, LOCATION CODE.                  */
/*                2) ADDED OPTION TO EMAIL THE FILE                 */
/* 05/02/01  RMC   REMOVED &TODOC ON SQL STMT                     */
/********************************************************************/
             PGM        PARM(&CO &TYPE &SRC &PROD &LOC &MO &WK &YR +
                          &RTYP &DOC &EMAIL &PDEV &HOLD &SAVE +
                          &XCOPIES &OMS)
/* PARAMETERS */
             DCL        VAR(&CO) TYPE(*DEC) LEN(3 0)
             DCL        VAR(&TYPE) TYPE(*CHAR) LEN(1)
             DCL        VAR(&SRC) TYPE(*CHAR) LEN(1)
             DCL        VAR(&PROD) TYPE(*CHAR) LEN(6)
             DCL        VAR(&LOC) TYPE(*CHAR) LEN(6)
             DCL        VAR(&MO) TYPE(*DEC) LEN(2 0)
             DCL        VAR(&WK) TYPE(*DEC) LEN(2 0)
             DCL        VAR(&YR) TYPE(*DEC) LEN(4 0)
             DCL        VAR(&RTYP) TYPE(*CHAR) LEN(1)
             DCL        VAR(&DOC) TYPE(*CHAR) LEN(6)
             DCL        VAR(&EMAIL) TYPE(*CHAR) LEN(50)
             DCL        VAR(&PDEV) TYPE(*CHAR) LEN(10)
             DCL        VAR(&HOLD) TYPE(*CHAR) LEN(4)
             DCL        VAR(&SAVE) TYPE(*CHAR) LEN(4)
             DCL        VAR(&XCOPIES) TYPE(*CHAR) LEN(15)

/* WORK FIELDS */
             DCL        VAR(&COX) TYPE(*CHAR) LEN(3)
             DCL        VAR(&YRX) TYPE(*CHAR) LEN(4)
             DCL        VAR(&MOX) TYPE(*CHAR) LEN(2)
             DCL        VAR(&WKX) TYPE(*CHAR) LEN(2)
             DCL        VAR(&COPIES) TYPE(*DEC) LEN(2 0)

/* SEQUEL SELECT */
             DCL        VAR(&SEL) TYPE(*CHAR) LEN(500)
             DCL        VAR(&STYPE) TYPE(*CHAR) LEN(25)
             DCL        VAR(&SSRC) TYPE(*CHAR) LEN(50)
             DCL        VAR(&SPROD) TYPE(*CHAR) LEN(25)
             DCL        VAR(&SLOC) TYPE(*CHAR) LEN(50)
             DCL        VAR(&SYR) TYPE(*CHAR) LEN(50)
             DCL        VAR(&SMO) TYPE(*CHAR) LEN(50)
             DCL        VAR(&SWK) TYPE(*CHAR) LEN(50)

/* SYSTEM NAME PARM                                              */
             DCL        VAR(&OMS) TYPE(*CHAR) LEN(33)
             DCL        VAR(&LPP) TYPE(*CHAR) LEN(40)

/* SBDPRTOVR VARIABLE                                          */
             DCL        VAR(&CMD) TYPE(*CHAR) LEN(512)

/********************************************************************/
/* INITIALIZE                                                       */
/********************************************************************/
/* CONVERT DATES */
             CHGVAR     VAR(&COX) VALUE(&CO)
             CHGVAR     VAR(&YRX) VALUE(&YR)
             CHGVAR     VAR(&MOX) VALUE(&MO)
             CHGVAR     VAR(&WKX) VALUE(&WK)
             CHGVAR     VAR(&LPP) VALUE(&OMS)
             CHGVAR     VAR(&COPIES) VALUE(&XCOPIES)

/*******************************************************************/
/* PRINT REPORT                                                   */
/*******************************************************************/

/* GO TO EMAIL IF DOC NE BLANK */
             IF         COND(&DOC *NE '    ') THEN(GOTO CMDLBL(EMAIL))
             SBDPRTOVR  CMDVAR(&CMD) FILE(PKU1PFR$) OUTQ(&PDEV) +
                          COPIES(&COPIES) HOLD(&HOLD) SAVE(&SAVE)
             CALL       PGM(QCMDEXC) PARM(&CMD 512)

             CALL       PGM(PKU1PFR) PARM(' ' &CO &TYPE &SRC &PROD +
                          &LOC &MO &WK &YR &RTYP &LPP)

/*******************************************************************/
/* EMAIL FILE                                                     */
/*******************************************************************/

EMAIL:
/* EXIT IF &DOC = BLANK */
             IF         COND(&DOC *EQ '    ') THEN(GOTO CMDLBL(ENDPGM))

/* DEVIATION TYPE */
             IF         COND(&TYPE = 'M') THEN(CHGVAR VAR(&STYPE) +
                          VALUE('EYV3ST="M"'))
             ELSE       CMD(CHGVAR VAR(&STYPE) VALUE('EYV3ST="W"'))

/* SOURCE TYPE */
             IF         COND(&SRC *NE ' ') THEN(CHGVAR VAR(&SSRC) +
                          VALUE(' AND EYV1ST="' *CAT &SRC *CAT '"'))
             ELSE       CMD(CHGVAR VAR(&SSRC) VALUE(' '))

/* PRODUCER CODE */
             IF         COND(&PROD *NE ' ') THEN(CHGVAR VAR(&SPROD) +
                          VALUE(' AND EYH0CD="' *CAT &PROD *CAT '"'))
             ELSE       CMD(IF COND(&RTYP *EQ 'S') THEN(CHGVAR +
                          VAR(&SPROD) VALUE(' AND EYH0CD="*ALL"')))
             ELSE       CMD(CHGVAR VAR(&SPROD) VALUE(' '))

/* LOCATION CODE */
             IF         COND(&LOC *NE ' ') THEN(CHGVAR VAR(&SLOC) +
                          VALUE(' AND EYH1CD="' *CAT &LOC *CAT '"'))
             ELSE       CMD(IF COND(&RTYP *EQ 'S') THEN(CHGVAR +
                          VAR(&SLOC) VALUE(' AND EYH1CD="*ALL"')))
             ELSE       CMD(CHGVAR VAR(&SLOC) VALUE(' '))

/* YEAR */
             IF         COND(&YR *NE 0) THEN(CHGVAR VAR(&SYR) +
                          VALUE(' AND YEAR=' *CAT &YRX))
             ELSE       CMD(CHGVAR VAR(&SYR) VALUE(' '))

/* MONTH */
             IF         COND(&MO *NE 0) THEN(CHGVAR VAR(&SMO) +
                          VALUE(' AND EYAWA1=' *CAT &MOX))
             ELSE       CMD(CHGVAR VAR(&SMO) VALUE(' '))

/* WEEK */
             IF         COND(&WK *NE 0) THEN(CHGVAR VAR(&SWK) +
                          VALUE(' AND EYANA1=' *CAT &WKX))
             ELSE       CMD(CHGVAR VAR(&SWK) VALUE(' '))


/* SELECT */
             CHGVAR     VAR(&SEL) VALUE('SELECT EYALA1, EYV3ST, +
                          YEAR(CVTDATE(EYENDT,CYMD)) LEN(4,0) +
                          NAME(YEAR), EYAWA1, EYANA1, EYENDT, +
                          EYEODT, EYV1ST, EYH0CD, EYH1CD, EYAOA1, +
                          EYAPA1, EYAQA1, EYH5PC, EYARA1, EYAUA1, +
                          EYASA1, EYH6PC, EYATA1, EYH7PC, EYAVA1 +
                          FROM PKEYCPP WHERE ' *CAT &STYPE *TCAT +
                          &SSRC *TCAT &SPROD *TCAT &SLOC *TCAT &SYR +
                          *TCAT &SMO *TCAT &SWK *TCAT ' ORDER BY +
                          EYALA1, EYV3ST, EYENDT')
/* EXECUTE SQL STATEMENT */
             EXECUTE    SQL(&SEL) REPLACE(*YES) TEXT('STANDARD +
                          DEVIATION') RECIPIENT(&EMAIL) +
                          EMLMSG('STANDARD DEVIATION') +
                          OPTIMIZE(*FIRSTIO) ALWCPY(*IFRQD)

/********************************************************************/
/* END OF PROGRAM                                                   */
/********************************************************************/
 ENDPGM:
             DLTOVR *ALL
             ENDPGM
