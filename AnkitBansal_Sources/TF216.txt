      *****************  RPG PROGRAM HEADING  ************************
     h option(*SRCSTMT:*NODEBUGIO)
      *
      * SYSTEM:      Triumph Foods
      * PROGRAM:     TF216 - Margin: Extract Revenue Data for Margin Detail
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     12/22/04
      *
      * Function:    When the Margin Adjustment Close function is run, we call this program
      *              to Extract Weekly Revenue data and perform Margin calcs.
      *
      ***************************************************************************************
      * MODIFICATIONS:
      ***************************************************************************************
      * DATE      PROGRAMMER
      *
      * 03/13/06  LeAnne Fedor
      *           Recompile only. "Floor" produced pounds and amount were added
      *           to file TFP010.
      *
      * 09/22/06  LeAnne Ramsey
      *           The Margin Close is now a 'weekly' process instead of a 'period'
      *           process. Also, we added 3 new fields/logic to TFP014:
      *                   1) mix group
      *                   2) exclude from mix flag
      *                   3) exclude from volume flag
      *
      * 10/11/06  LeAnne Ramsey
      *           Added logic to populate 2 of the new fields in TFP014:
      *              ADPEFL-Product Exception Flag
      *              ADSTAR-Star/Asterisk
      *
      * 10/24/06  LeAnne Ramsey
      *           Tweaked Produced Start Weight logic; there was a problem only when
      *           you had both a Record Exempt and a Record Not Exempt in the TFP010
      *           file for a Product and the Not Exempt record was processed before
      *           the Exempt record.
      *
      *           Also, we now will allow a Yield Percent up to 999%; prior to this
      *           we were setting Yield Percent to zero if the calculated value was
      *           greater than 100% (to prevent abends caused by goofy data.)
      *
      * 10/31/06  LeAnne Ramsey
      *           The 'tweaking' of 10/24/06 on Produced Start Weight screwed up the
      *           Produced Pounds logic. So, this time we tweaked the Produced Pounds
      *
      * 11/09/06  LeAnne Ramsey
      *           We added "period" fields (year/period/beginning date/ending date)
      *           to the Margin file. Also, changed LDA positions.
      *
      * 02/15/07  LeAnne Ramsey
      *           Added "other cost" logic to populate or new "other cost" fields.
      *
      * 04/16/07  LeAnne Ramsey
      *           We switched the following flag fields:
      *             1) Exclude from Mix Flag    is now Mix Flag
      *             2) Exclude from Volume Flag is now Volume Flag
      *           Before this change Y=Yes meant EXCLUDE the product from processing
      *           After this change  Y=Yes means INCLUDE the product in processing
      *
      * 05/18/07  LeAnne Ramsey
      *           Recompile only. Fields were added to the Weekly Product Revenue
      *           Detail file:
      *              TF Percent Owned
      *              Co-owned Flag
      *              Producing Company
      *
      * 06/20/07  LeAnne Ramsey
      *           The users want the Mix Adjustment to include the Work in Process items
      *           that fed the CoOwned Items. We will extract the WP items from the
      *           Inventory files in a separate program. The Finished Goods items will
      *           continue to be extracted from the Weekly Revenue file via this program
      *           TF216. BUT, to avoid coding the 'calcs' in 2 programs, we will perform
      *           all the calcs over both FG and WP simulatneously from another program.
      *
      *           We have added the "co-own" fields to the Margin Detail file and populate
      *           them in this program.
      *
      * 08/15/07  LeAnne Ramsey
      *           We have now put the Work-in-Process items into TFP010. So, this program
      *           now extracts both FG and WP items. And, since it now handles both FG/WP,
      *           we put all the calcs for TFP014 back into this program.
      *           BUT, do not put WP items with a TF Class Group Code of 'BP' into the
      *           TFP014 file. The WP/BP items are handled in the separate ByProduct Mix
      *           function.
      *
      * 08/14/08  LeAnne Ramsey
      *           Changed calc of ADSTRPAM-SBF CoOwned Transfer Product Cost Amount to
      *           handle CoOwned when Triumph is the Producing Company.
      *
      * 09/11/08  LeAnne Ramsey
      *           Recompile only. Export/Domestic Flag was added to TFP010-
      *           Weekly Product Revenue Detail file.
      *
      * 09/05/12  LeAnne Ramsey (E2243)
      *           Per Damon G. we must now write more/special records to TFP014 for the
      *           Items that receive lbs/dollars from Skirt Meat Items!!
      *           Note: I also had to do special calcs for this in RO214-Calc Meat Cost-
      *           Component Items and TF617-Mix Adjustment Report.
      *
      * 09/13/12  LeAnne Ramsey (E2243)
      *           Remove some code that I had inserted for testing. No logic
      *           was changed.
      *
      * 11/14/17  Danny Nguyen  (R12011A-Weekly Product Revenue)
      *           Recompile only. Added 25 STF fields to TFP010 file.
      *
      * 03/02/22  Danny Nguyen  (DO2484 - WI479 STF Variance Reporting)
      *           DBFC on TFP014 file. Added the following fields:
      *             ADXPULB  - STF PRODUCED LBS
      *             ADXPUSLB - STF PRODUCED START WEIGHT
      *             ADXYPC   - STF STD YIELD %
      *             ADXPMPPC - STF PUMP %
      *             ADXSLLB  - STF SOLD LBS
      *             ADXSLSLB - STF SOLD START WEIGHT
      *           Added subroutine '$upd014' will update the 2 STF fields only. Does
      *           not impact Guymon & Triumph Foods data. Changes were to use STF
      *           Yield & Pump % to calculate STF Produced Start Weight. Added
      *           TFL014H logical file to update.
      *           Use STF Yield % to calculate STF Produced Pounds if exist.
      *           NOTE: STF does NOT have any Product Exceptions.
      *
      * 10/10/22  R Centonze   W105621 - Increase price fields to handle > 999.99
      *                        CHANGE @@ANPRPR TO SIZE 10.6  --> pranprpr tfp010
      *         COPY @@SPU1MG TO  @@SPU1MG10 SIZE 10.6  -> FIELD TFP014.ADSPU1MG
      *         COPY @@TPU1MG TO  @@TPU1MG10 SIZE 10.6  -> FIELD TFP014.ADTPU1MG
      *         COPY @@SSL1MG TO  @@SSL1MG10 SIZE 10.6  -> FIELD TFP014.ADSSL1MG
      *         COPY @@SSL2MG TO  @@SSL2MG10 SIZE 10.6  -> FIELD TFP014.ADSSL2MG
      *         COPY @@TSL1MG TO  @@TSL1MG10 SIZE 10.6  -> FIELD TFP014.ADTSL1MG
      *         COPY @@TSL2MG TO  @@TSL2MG10 SIZE 10.6  -> FIELD TFP014.ADTSL2MG
      *
      * 03/23/23  Danny Nguyen  (W121675 - STF Primal Yield Reporting)
      *           Corrected calc for STF Yield % for STF Produced Pounds; do the
      *           calc if only TF Class Group Code = 'BP' (ByProduct). Do not calc
      *           if Primals.
      * 05/07/24  Santosh Patil P310149 - Field length for below flds is increased
      *           in TFP014 & function is only recompiled.
      *           @@SSL1MG10 10.6 to 12.6 -> TFP014.ADSSL1MG
      *           @@SSLMGAM  11.2 to 13.2 -> TFP014.ADSSLMGAM
      *           @@SSL2MG10 10.6 to 12.6 -> TFP014.ADSSL2MG
      *
      /eject
      ***************************************************************************************
      * FILE SPECIFICATIONS
      ***************************************************************************************
      *
     Fpoaxcpl1  if   e           k disk
      *  Item standard cost
      *
      *
     Fppaorel1  if   e           k disk
      *  TF margin adjustment group detail
      *
      *
     Ftfl010f   if   e           k disk
      *  Weekly product revenue detail
      *
      *
     Ftfp014    o    e           k disk
      *  Product margin adjustment transaction detail
      *
      *
     Ftfl014a   uf   e           k disk    rename(adrec:adreca)
      *  Product margin adjustment transaction detail
      *
      *
2484 Ftfl014h   uf   e           k disk    rename(adrec:adrech) prefix(p1)
      *  Product margin adjustment transaction detail
      *
      *
     Ftfl019b   if   e           k disk
      *  Product exceptions
      *
      *
     ftfl019d   if   e           k disk    rename(perec:perecd)
      *  Product exceptions
      *
      *
     ftfl019e   if   e           k disk    rename(perec:perece) prefix(p6)
      *  Product exceptions
      *
      /eject
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Control fields
      *
     D first           s              1    inz('Y')
     D svprcd          s                   like(prprcd)
      *
      *
      * Work fields
      *
     D wkcono          s                   like(axaic3) inz(360)
     d wkpgm232        s             10    inz('RO232     ')
     D wkistycd        s                   like(peistycd) inz(720)
     D wkisgrcd        s                   like(peisgrcd) inz(712)
     D wkisclcd        s                   like(peisclcd) inz(718)
      *
      *
     D wkpc            s             15  4
2484 D wkxypc          s             15  4
     D wklb            s             11  2
      *
     D wksexpulb       s                   like(pesexpulb)
     D wksexpuam       s                   like(pesexpuam)
      *
     D wkspulb         s             11  2
     D wksexsllb       s                   like(pesexsllb)
      *
     D wksexnpram      s                   like(pesexnpram)
      *
     D wktexpulb       s                   like(petexpulb)
     D wktexpuam       s                   like(petexpuam)
      *
     D wktpulb         s             11  2
     D wktexsllb       s                   like(petexsllb)
     D wktexnpram      s                   like(petexnpram)
      *
2484 D wkxpulb         s             11  2
      *
     D wkprexfl        s                   like(prrcexfl) inz('N')
     D wkrate          s              9  6
2484 D wkxrate         s              9  6
     D wkpump          s              9  6
2484 D wkxpump         s              9  6
     D wktown          s              9  6
     D wksown          s              9  6
      *
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *---------------------------------------------------------------
      *  Local data area
      *---------------------------------------------------------------
      *
     d lda            uds                  dtaara(*lda)
     D  ldyr                   2      5  0
     D  ldwk                   6      7  0
      *
     D  ldwbdt                 8     15  0
     D  ldwedt                29     36  0
     D  ldwesyn               37     43  0
      *
     D  ldpe                  51     52  0
     D  ldpbdt                53     60  0
     D  ldpedt                67     74  0
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * Mainline
      ****************************************************************
      *
      * Extract Revenue Data writing Margin records
      *
     C                   exsr      $revenue
      *
      * Write Margin records for Skirt Meat Exceptions (added in Sept 2012)
      *
     C                   exsr      $skirt
      *
      * Read Margin records and make calculations
      *
     C                   exsr      $margin
      *
     C                   seton                                        lr
      /eject
      *---------------------------------------------------------------
      * Extract Revenue Data
      *---------------------------------------------------------------
      *
     C     $revenue      begsr
      *
      * Process all Weekly Product Revenue Detail records for the Week where:
      *   1) Product Exempt Flag is NO
      *      (Note: the logic uses a hard-coded key field value of NO)
      *   2) Exclude WP items with TF Class Group Code of BP
      *
      * NOTE: We don't expect more than 2 records for a Product in a Week:
      *   1) There can be a 'Record-exempt' record
      *   2) There can be a 'Record-not-exempt' record
      *
     C     key02         setll     tfl010f
      *
     C                   dou       *in91 = *on                                  Main do
     C     key02         reade     tfl010f                                91
     C                   if        *in91 = *off and                             If not EOF
     C                             (pritycd = 'FG ' or
     C                             (pritycd = 'WP ' and prtfcgcd <> 'BP'))
      *
      * Control break logic.
      *
     C                   select
     C                   when      first = yes
     C                   move      no            first
     C                   exsr      $newprod
      *
     C                   when      prprcd <> svprcd
     C                   exsr      $wrt014
2484 C                   exsr      $upd014
     C                   exsr      $newprod
     C                   endsl
      *
      * Perform "produced/production" logic for every record.
      * (Since we will never have more than 2 records for a Product/Week,
      * this "produced" logic will, at most, be performed 2 times for
      * each Product.)
      *
     C                   exsr      $produced
      *
      * Perform "SOLD" logic for each Finished Goods "NOT EXEMPT" record.
      * (Since we will never have more than 1 "not exempt" record for a
      * Product/Week, this "sold" logic will only be performed 1 time for
      * each Product that we process.)
      *
     C                   if        prrcexfl = no and
     C                             pritycd = 'FG '
     C                   exsr      $sold
     C                   endif
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Main do
      *
      * EOF processing
     C                   if        first = no
     C                   exsr      $wrt014
2484 C                   exsr      $upd014
     C                   endif
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Logic done 1 time for each Product
      *---------------------------------------------------------------
      *
     C     $newprod      begsr
      *
     C                   z-add     prprcd        svprcd
      *
     C                   move      pritycd       aditycd
     C                   z-add     pristycd      adistycd
     C                   z-add     prisgrcd      adisgrcd
     C                   z-add     prisclcd      adisclcd
     C                   move      prtfclcd      adtfclcd
     C                   move      prtfcgcd      adtfcgcd
     C                   move      prglsub       adglsub
     C                   z-add     prpumppc      adpumppc
2484 C                   z-add     prxpmppc      adxpmppc
      *
      * CoOwned Values
     C                   move      prcoownfl     adcoownfl
     C                   move      prprdcmp      adprdcmp
     C                   z-add     prtownpc      adtownpc
      *
      * Per Pound Costs
     C                   z-add     prmco         admco
     C                   z-add     prico         adico
     C                   z-add     proco         adoco
     C                   z-add     prkco         adkco
     C                   z-add     prlco         adlco
     C                   z-add     prpco         adpco
      *
      * Retrieve the following values from the TF Margin Adjustment Group Detail
      * file using a key of:
      *  Values:                           Key:
      *    Converted raw material flag         TF Classification Code
      *    Mix group                           Item Structure Type Code
      *    Mix Flag                            Item Structure Group Code
      *    Volume Flag                         Item Structure Class Code
      *
     C     key01         chain     ppaorel1                           92
     C                   if        *in92 = *off                                 If hit
     C                   move      aox5sx        adrmfl
     C                   move      aoi1t1        admixgrp
     C                   move      aoswsx        admixfl
     C                   move      aosxsx        advolfl
     C                   endif                                                  If hit
      *
      * Set the "product split" flag to NO. It will be populated in a
      * later program.
      *
     C                   move      no            adpsfl
      *
     C                   z-add     0             wkspulb
     C                   z-add     0             wksexpulb
     C                   z-add     0             wksexpuam
     C                   z-add     0             wksexsllb
     C                   z-add     0             wksexnpram
      *
     C                   z-add     0             wktpulb
     C                   z-add     0             wktexpulb
     C                   z-add     0             wktexpuam
     C                   z-add     0             wktexsllb
     C                   z-add     0             wktexnpram
     C                   move      no            adpefl
     C                   move      no            adsmsfl
      *
2484 C                   z-add     0             wkxpulb
      *
      * Retrieve records from the Product Exception file using a key of:
      *   Product
      *   Week-ending date
      *
      *
     C     key03         setll     tfl019b
     C                   dou       *in93 = *on                                  Do excepts
     C     key03         reade     tfl019b                                93
     C                   if        *in93 = *off                                 If excepts
      *
      * Damon G. had us add special Skirt Meat logic in September 2012. So,
      * if this Item has received any pounds/$$ from a Skirt Meat split,
      * do NOT use/include that Skirt Meat Exception....we will get it later.
      *
     C                   if        (prtfcgcd = 'NV' or prtfcgcd = 'CV') and     If skirt
     C                             pepgm = 'RO232     '
     C                   move      '*'           adstar
     C                   else
      *
      * Set the Product Exception Flag to Yes and plop a "star" into the field
      * that we will print on reports to indicate that this Product was "messed"
      * with.
     C                   move      yes           adpefl
     C                   move      '*'           adstar
      *
      * Overlay the flags retrieved from Synon files.
      *
     C                   move      pemixfl       admixfl
     C                   move      pevolfl       advolfl
      *
      * Accumulate (into 15,4 and 15,6 sized workfields):
      *    1) produced pounds
      *    2) produced value amount
      *
      *        Seaboard
     C                   add       pesexpulb     wksexpulb
     C                   add       pesexpuam     wksexpuam
      *        Triumph
     C                   add       petexpulb     wktexpulb
     C                   add       petexpuam     wktexpuam
      *
      * Put the "sold" "exception" fields into workfields to be
      * conditionally used later:
      *    1) sold pounds
      *    2) net product revenue amount
      *
     C                   add       pesexsllb     wksexsllb
     C                   add       pesexnpram    wksexnpram
      *
     C                   add       petexsllb     wktexsllb
     C                   add       petexnpram    wktexnpram
     C                   endif                                                  If skirt
     C                   endif                                                  If excepts
     C                   enddo                                                  Do excepts
      *
      *
      * Get the 15,4 and 15,6 "produced" accumulators into 11,2 fields
      *
     C                   z-add(h)  wksexpulb     wkspulb
     C                   add(h)    wksexpulb     adspulb
     C                   add(h)    wksexpuam     adspuam
      *
     C                   z-add(h)  wktexpulb     wktpulb
     C                   add(h)    wktexpulb     adtpulb
     C                   add(h)    wktexpuam     adtpuam
      *
      * Get Yield Percent into a "rate" field.
      *
     C                   eval      wkrate = prypc / 100
2484 C                   eval      wkxrate = prxypc / 100                       For STF
      *
      * Get Pump Percent into a "rate" field (and add 1 to it).
      *
     C                   eval      wkpump = (prpumppc / 100) + 1
2484 C                   eval      wkxpump = (prxpmppc / 100) + 1                For STF
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Produced/Production logic
      *---------------------------------------------------------------
      *
     C     $produced     begsr
      *
      * Production scheduled pounds
      *
     C                   add       prspslb       adspslb
     C                   add       prtpslb       adtpslb
      *
      * Produced pounds
     C                   add       prspulb       adspulb
     C                   add       prtpulb       adtpulb
2484 C                   add       prxpulb       adxpulb                        STF Produced Lbs
      *
      * W121675 - Only Calc STF Yield % if TF Class Group Code='BP' (ByProduct).
     C                   if        prtfcgcd = 'BP'
      *
2484  *   Calc STF Produced Pounds with STF Yield %
  |   *     (STF produced pounds / STF yield rate) * 100
  |  C                   if        wkxrate <> 0
  |  C                   add       prxpulb       wkxpulb
  |  C                   eval      adxpulb = (wkxpulb / wkxrate)
  |  C                   z-add     0             wkxpulb
2484 C                   endif
      *
     C                   endif                                                  prtfcgcd='BP'
      *
      * Produced start weights
      *     (produced pounds / yield rate) / pump rate
      *
     C                   if        wkrate <> 0 and wkpump <> 0                  If not zero
      *
     C                   add       prspulb       wkspulb
     C                   eval      adspuslb = adspuslb +
     C                                       ((wkspulb / wkrate) / wkpump)
     C                   z-add     0             wkspulb
      *
     C                   add       prtpulb       wktpulb
     C                   eval      adtpuslb = adtpuslb +
     C                                       ((wktpulb / wkrate) / wkpump)
     C                   z-add     0             wktpulb
     C                   endif                                                  If not zero
      *
2484  * STF Produced start weights
  |   *     (produced pounds / yield rate) / pump rate
  |   *
  |  C                   if        wkxrate <> 0 and wkxpump <> 0                If not zero
  |  C                   add       prxpulb       wkxpulb
  |  C                   eval      adxpuslb = adxpuslb +
  |  C                                       ((wkxpulb / wkxrate) / wkxpump)
  |  C                   z-add     0             wkxpulb
2484 C                   endif
      *
      * Produced value amount
      *
     C                   add       prspuam       adspuam
     C                   add       prtpuam       adtpuam
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * "Sold" logic for "Not Exempt" records only.
      *---------------------------------------------------------------
      *
     C     $sold         begsr
      *
      * Initialize Sold Pounds and Net Product Revenue Amount with the "Exception"
      * values retrieved in the "New Product" routine.
2484  ** DO2484 - STF does NOT have any Product Exceptions.
      *
     C                   z-add(h)  wksexsllb     adssllb
     C                   z-add(h)  wksexnpram    adsnpram
      *
     C                   z-add(h)  wktexsllb     adtsllb
     C                   z-add(h)  wktexnpram    adtnpram
      *
2484  * DO2484 - Even though STF does not have any Product Exceptions we still need to
  |   *          initialize STF Sold Lbs.
2484 C                   z-add     0             adxsllb
      * Sold pounds
     C                   add       prssllb       adssllb
     C                   add       prtsllb       adtsllb
2484 C                   add       prxsllb       adxsllb                        STF Sold Lbs
      *
      * Sold start weights:
      *     (sold pounds / yield rate) / pump rate
      *
     C                   if        wkrate <> 0 and wkpump <> 0
     C                   eval      adsslslb = adsslslb +
     C                                       ((adssllb / wkrate) / wkpump)
     C                   eval      adtslslb = adtslslb +
     C                                       ((adtsllb / wkrate) / wkpump)
     C                   endif
      *
2484  *      Seaboard Triumph Foods
  |  C                   if        wkxrate <> 0 and wkxpump <> 0
  |  C                   eval      adxslslb = adxslslb +
  |  C                                       ((adxsllb / wkxrate) / wkxpump)
2484 C                   endif
      *
      * Net product revenue amount
      *
     C                   add       prsnpram      adsnpram
     C                   add       prtnpram      adtnpram
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Write a Product Margin Adjustment Transaction Detail record
      *---------------------------------------------------------------
      *
     C     $wrt014       begsr
      *
     C                   z-add     ldyr          adyr
     C                   z-add     ldwk          adwk
     C                   z-add     ldwbdt        adwbdt
     C                   z-add     ldwedt        adwedt
      *
     C                   z-add     ldpe          adpe
     C                   z-add     ldpbdt        adpbdt
     C                   z-add     ldpedt        adpedt
      *
     C                   z-add     svprcd        adprcd
      *
      * Yield Percent will be based on:
      *  1) Sold Start Weight when it is not zero
      *  2) Produced Start Weight when Sold Start Weight is zero
      *
     C                   z-add     0             wkpc
      *
     C                   select
     C                   when      (adsslslb + adtslslb) <> 0
     C     adsslslb      add       adtslslb      wklb
     C                   eval      wkpc = ((adssllb + adtsllb) / wklb) * 100
     C                   other
      *
     C     adspuslb      add       adtpuslb      wklb
     C                   if        wklb <> 0
     C                   eval      wkpc = ((adspulb + adtpulb) / wklb) * 100
     C                   endif
     C                   endsl
      *
     C                   if        wkpc <= 999
     C                   z-add     wkpc          adypc
     C                   endif
      *
     C                   write     adrec
      *
2484  * DO2484 - Save following fields to update in '$upd014' for TFL014H.
  |  C                   eval      p1adxpulb = adxpulb                          STF Produced Lbs
  |  C                   eval      p1adxpuslb= adxpuslb                         STF Produced Str Wg
  |  C                   eval      p1adxsllb = adxsllb                          STF Sold Lbs
2484 C                   eval      p1adxslslb = adxslslb                        STF Sold Start Wg
      *
     C                   clear                   adrec
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
2484  * DO2484 - Update a Product Margin Adjustment Transaction Detail record
2484  *          for STF STD Yield Percent. Company STF processing only.
  |   *---------------------------------------------------------------
  |   *
  |  C     $upd014       begsr
  |   *
  |   * Yield Percent will be based on:
  |   *  1) Sold Start Weight when it is not zero
  |   *  2) Produced Start Weight when Sold Start Weight is zero
  |   *
  |  C     key05         chain     tfl014h
  |  C                   if        %found(tfl014h)
  |   *
  |  C                   z-add     0             wkxypc
  |   *
  |  C                   select
  |  C                   when      p1adxslslb <> 0
  |  C                   eval      wkxypc = (p1adxsllb / p1adxslslb) * 100
  |  C                   other
  |   *
  |  C                   if        p1adxpuslb <> 0
  |  C                   eval      wkxypc = (p1adxpulb / p1adxpuslb) * 100
  |  C                   endif
  |  C                   endsl
  |   *
  |  C                   if        wkxypc <= 999
  |  C                   z-add     wkxypc        p1adxypc
  |  C                   update    adrech
  |  C                   endif
  |   *
  |  C                   endif
  |   *
2484 C                   endsr
      /eject
      *----------------------------------------------------------------------------------------
      * Special processing for Items that received Skirt Meat pounds/dollars (E2243: Sept 2012)
      *----------------------------------------------------------------------------------------
      *
     C     $skirt        begsr
      *
      **‚DO2484 - NOTE: WKCONO variable is hardcoded to 360. It is used in key15 when
      **‚               reading TFP019 Exceptions file which STF does NOT have any.
      *
      * Key fields: hardcoded value 'RO232'
      *             WeekEnding date (LDWEDT)
      *
     C     key13         setll     tfl019d
     C                   dou       *in94 = *on                                  Do exceptions
     C     key13         reade     tfl019d                                94
     C                   if        *in94 = *off and petfcgcd <> 'BP'            If not EOF
      *
      * Retrieve the TFP014 record that you wrote in Step 1..this will
      * give you much of the data that you need for this new TFP014 rec.
      *
     C     key12         chain(n)  tfl014a                            92
     C                   if        *in92 = *off                                 If tfp014 hit
      *
     C                   move      yes           adpefl
     C                   move      yes           adsmsfl
     C                   move      '*'           adstar
     C                   z-add     0             adssllb
     C                   z-add     0             adtsllb
2484 C                   z-add     0             adxsllb                        STF Sold Lbs
     C                   z-add     0             adsslslb
     C                   z-add     0             adtslslb
2484 C                   z-add     0             adxslslb                       STF Sold Start Wg
     C                   z-add     0             adsnpram
     C                   z-add     0             adtnpram
      *
     C                   z-add(h)  pesexpulb     adspulb
     C                   z-add(h)  petexpulb     adtpulb
     C                   z-add(h)  pesexpuam     adspuam
     C                   z-add(h)  petexpuam     adtpuam
      *
      * Produced start weights
      *     (produced pounds / yield rate) / pump rate
      * Get Yield Percent and Pump Percent into "rate" fields before calc'ing.
      *
     C                   eval      wkrate = adypc / 100
     C                   eval      wkpump = (adpumppc / 100) + 1
     C                   if        wkrate <> 0 and wkpump <> 0                  If not zero
     C                   eval      adspuslb = ((adspulb / wkrate) / wkpump)
     C                   eval      adtpuslb = ((adtpulb / wkrate) / wkpump)
     C                   endif                                                  If not zero
      *
      * For the next part, you will need the Standard Costs for the
      * Skirt Meat Item that the pounds came FROM. But, we don't have the
      * 'split from' Item in the Exception record we are processing. SO, using
      * the Exception Number and the hardcoded Item Structure of 720/712/718,
      * retrieve the 'From' Exception record to get the Item. Then, get that
      * Skirt Meat Item's Standard Packaging and Labor Costs.
      *
     C     key14         chain     tfl019e                            92
     C                   if        *in92 = *off                                 If From item
     C     key15         chain     poaxcpl1                           92
     C                   if        *in92 = *off                                 If hit
     C                   eval(h)   adkco = (axedpr/100)
     C                   eval(h)   adlco = (axeepr/100)
     C                   eval(h)   adico = (axefpr/100)
     C                   eval(h)   adoco = (axegpr/100)
     C                   eval(h)   adpco = adlco + adico + adkco + admco + adoco
     C                   endif                                                  If hit
      *
     C                   write     adrec
      *
     C                   endif                                                  If From item
     C                   endif                                                  If tfp014 hit
     C                   endif                                                  If not EOF
     C                   enddo                                                  Do exceptions
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------------------------------
      * Process Margin records just extracted/written from Revenue file
      *---------------------------------------------------------------------------------------
      *
     C     $margin       begsr
      *
     C     key04         setll     tfl014a
      *
     C                   dou       *in91 = *on                                  Main do
     C     key04         reade     tfl014a                                91
     C                   if        *in91 = *off                                 If not EOF

     C                   exsr      $calcs
     C                   update    adreca

     C                   endif                                                  If not EOF
     C                   enddo                                                  Main do
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Perform Calculations
      *---------------------------------------------------------------
      *
     C     $calcs        begsr
      *
      **‚DO2484 - NOTE: We are NOT tracking any cost amounts or margins for Company STF.
      *
      * Meat Cost Amounts:
      *
      *          Sold: meat cost per pound * sold pounds
      *
     C                   eval(h)   adsslmam = admco * adssllb
     C                   eval(h)   adtslmam = admco * adtsllb
      *
      *          Produced: meat cost per pound * produced pounds
      *
     C                   eval(h)   adspumam = admco * adspulb
     C                   eval(h)   adtpumam = admco * adtpulb
      *
      * Labor Cost Amounts:
      *
      *          Sold: labor cost per pound * sold pounds
      *
     C                   eval(h)   adssllam = adlco * adssllb
     C                   eval(h)   adtsllam = adlco * adtsllb
      *
      *          Produced: labor cost per pound * produced pounds
      *
     C                   eval(h)   adspulam = adlco * adspulb
     C                   eval(h)   adtpulam = adlco * adtpulb
      *
      * Packaging Cost Amounts:
      *
      *          Sold: packaging cost per pound * sold pounds
      *
     C                   eval(h)   adsslkam = adkco * adssllb
     C                   eval(h)   adtslkam = adkco * adtsllb
      *
      *          Produced: packaging cost per pound * produced pounds
      *
     C                   eval(h)   adspukam = adkco * adspulb
     C                   eval(h)   adtpukam = adkco * adtpulb
      *
      * Ingredient Cost Amounts:
      *
      *           Sold: ingredient cost per pound * sold pounds
      *
     C                   eval(h)   adssliam = adico * adssllb
     C                   eval(h)   adtsliam = adico * adtsllb
      *
      *           Produced: ingredient cost per pound * produced pounds
      *
     C                   eval(h)   adspuiam = adico * adspulb
     C                   eval(h)   adtpuiam = adico * adtpulb
      *
      * Other Cost Amounts:
      *
      *           Sold: other cost per pound * sold pounds
      *
     C                   eval(h)   adssloam = adoco * adssllb
     C                   eval(h)   adtsloam = adoco * adtsllb
      *
      *           Produced: other cost per pound * produced pounds
      *
     C                   eval(h)   adspuoam = adoco * adspulb
     C                   eval(h)   adtpuoam = adoco * adtpulb
      *
      * Product Cost Amounts:
      *
      *           Sold: sum of individual cost amounts
      *
     C                   eval      adsslpam = adsslmam + adssllam + adsslkam +
     C                                                   adssliam + adssloam
      *
     C                   eval      adtslpam = adtslmam + adtsllam + adtslkam +
     C                                                   adtsliam + adtsloam
      *
      *           Produced: sum of individual cost amounts
      *
     C                   eval      adspupam = adspumam + adspulam + adspukam +
     C                                                   adspuiam + adspuoam
      *
     C                   eval      adtpupam = adtpumam + adtpulam + adtpukam +
     C                                                   adtpuiam + adtpuoam
      *
      * Sold margin per sold pound:
      *         (net product revenue amount / sold lbs) - product cost per lb
      *
     C                   if        adssllb <> 0
     C                   eval(h)   adssl1mg = (adsnpram / adssllb) - adpco
     C                   endif
      *
     C                   if        adtsllb <> 0
     C                   eval(h)   adtsl1mg = (adtnpram / adtsllb) - adpco
     C                   endif
      *
      * Sold margin amount:
      *           sold margin per sold pound * sold pounds
      *
     C                   eval(h)   adsslmgam = adssl1mg * adssllb
     C                   eval(h)   adtslmgam = adtsl1mg * adtsllb
      *
      *
      * Sold margin per sold start weight:
      *           sold margin amount / sold start weight
      *
     C                   if        adsslslb <> 0
     C                   eval(h)   adssl2mg = adsslmgam / adsslslb
     C                   endif
      *
     C                   if        adtslslb <> 0
     C                   eval(h)   adtsl2mg = adtslmgam / adtslslb
     C                   endif
      *
      *
      * Produced margin per produced pound:
      *         (produced value amount / produced lbs) - product cost per lb
      *
     C                   if        adspulb <> 0
     C                   eval(h)   adspu1mg = (adspuam / adspulb) - adpco
     C                   endif
      *
     C                   if        adtpulb <> 0
     C                   eval(h)   adtpu1mg = (adtpuam / adtpulb) - adpco
     C                   endif
      *
      * Produced margin amount:
      *           produced margin per produced pound * produced pounds
      *
     C                   eval(h)   adspumgam = adspu1mg * adspulb
     C                   eval(h)   adtpumgam = adtpu1mg * adtpulb
      *
      * Calculate SBF CoOwned Transfer Product Cost Amount
      *
     C                   if        adtownpc <> 0                                If CoOwn %
     C                   eval      wktown = adtownpc / 100
     C                   eval      wksown = 1 - wktown
      *
     C                   select
     C                   when      adprdcmp = 'SBF'
     C                   eval      adstrpam = adspuam * wktown
      *
     C                   when      adprdcmp = 'TF '
     C                   eval      adstrpam = (adtpuam * wksown) * -1
     C                   endsl
     C                   endif                                                  If CoOwn%
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Key lists
      *
     C     key01         klist
     C                   kfld                    adtfclcd
     C                   kfld                    adistycd
     C                   kfld                    adisgrcd
     C                   kfld                    adisclcd
      *
     C     key02         klist
     C                   kfld                    wkprexfl
     C                   kfld                    ldwedt
      *
     C     key03         klist
     C                   kfld                    ldwedt
     C                   kfld                    prprcd
      *
     C     key04         klist
     C                   kfld                    ldyr
     C                   kfld                    ldwk
      *
     C     key12         klist
     C                   kfld                    ldyr
     C                   kfld                    ldwk
     C                   kfld                    peprcd
      *
2484 C     key05         klist
  |  C                   kfld                    ldwedt
2484 C                   kfld                    svprcd
      *
     C     key13         klist
     C                   kfld                    wkpgm232
     C                   kfld                    ldwedt
      *
     C     key14         klist
     C                   kfld                    pepesn
     C                   kfld                    wkistycd
     C                   kfld                    wkisgrcd
     C                   kfld                    wkisclcd
      *
     C     key15         klist
     C                   kfld                    wkcono
     C                   kfld                    p6peprcd
     C                   kfld                    ldwesyn
      *
     C                   endsr
      /eject
