      *****************  RPG PROGRAM HEADING  ************************
     h option(*SRCSTMT:*NODEBUGIO)
      ****************************************************************
      *
      * SYSTEM:      Triumph Foods
      * PROGRAM:     TF654
      * TITLE:       Inventory: Sales Timing Allocation Detail Report
      * PROGRAMMER:  LeAnne Ramsey
      * CREATED:     11/15/10
      *
      * FUNCTION:  Batch program to print the detail records from the Sales Timing
      *            Allocation Detail file. The users can elect a Download or a Report.
      *            To facilitate this, we create/use a Workfile.
      *
      *            This 'report' is generated:
      *               1) On-demand
      *               2) From the Preliminary run of the Weekly Inventory Close
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 11/18/10  LeAnne Ramsey
      *           Tom Dye now wants the Report generated automatically
      *           during the Preliminary run of the Weekly Inventory Close.
      *           So, I will now print 'On-Demand' or 'Preliminary Inventory Close'
      *           in the heading.
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Ftfp354    if   e           k disk
      *    Workfile: Sales timing allocation detail
      *
      *
     Fprint198  o    f  198        printer oflind(*inof)
      *
      /EJECT
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Standard fields
      *
     d rtime           s              6  0
      *
      *
      * Workfields
      *
     D wkprcd          s                   like(w1prcd)
      *
      *
      * Print fields
      *
     D dash            s            198    inz(*all'-')
     d h1demand        s             27
      *
      ****************************************************************
      * ARRAYS AND TABLES
      ****************************************************************
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *---------------------------------------------------------------
      * Local data area.
      *---------------------------------------------------------------
      *
     Dlda             uds                  dtaara(*lda)
     D  ldpfcd                 1      1
     D  ldyr                   2      5  0
     D  ldwk                   6      7  0
      *
     D  ldwedt                29     36  0
     D  ldwemdy               44     49  0
      *
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ***************************************************************************************
      * Mainline
      ***************************************************************************************
      *
     C                   exsr      $h1hdr
      *
      * The workfile will hold records for the selected WeekEnding Date.
      * Note: Item is zero in our "total" record; we will retrieve/print
      *       it at EOF.
      *
     C     *loval        setll     tfp354
      *
     C                   dou       *in90 = *on                                  Main do
     C                   read      tfp354                                 90
     C                   if        *in90 = *off and w1prcd <> 0                 If not EOF
     C                   exsr      $l1dtl
     C                   endif                                                  If not EOF
     C                   enddo                                                  Main do
      *
      * EOF: Retrieve/print the Total record
      *
     C                   exsr      $total
      *
     C                   seton                                        lr
      /EJECT
      *---------------------------------------------------------------
      * Print detail line
      *---------------------------------------------------------------
      *
     C     $l1dtl        begsr
      *
      * Print headings if necessary
      *
     C                   if        *inof = *on
     C                   exsr      $h1hdr
     C                   endif
      *
     C                   except    l1dtl
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Retrieve the "total" record from the workfile and print
      *---------------------------------------------------------------
      *
     C     $total        begsr
      *
     C     key01         chain     tfp354                             92
     C                   if        *in92 = *off
     C                   exsr      $l1dtl
     C                   endif
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Print report headings
      *---------------------------------------------------------------
      *
     C     $h1hdr        begsr
      *
     C                   except    h1hdr
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Key lists
      *
     C     key01         klist
     C                   kfld                    ldwedt
     C                   kfld                    wkprcd
      *
      *
      * This report is generated on-demand and also from the Preliminary run
      * of the Inventory Close function.
      *
     C                   select
     C                   when      ldpfcd = 'D'
     C                   eval      %subst(h1demand:10:9) = 'On-Demand'
      *
     C                   when      ldpfcd = 'P'
     C                   eval      %subst(h1demand:1:27) =
     C                             'Preliminary Inventory Close'
     C                   endsl
      *
      *
      * Retrieve time for report heading.
      *
     C                   time                    rtime
      *
     C                   endsr
      /eject
      *-------------------------------------------------------------
      * Report lines
      *-------------------------------------------------------------
      *
     Oprint198  e            h1hdr          1 01
     O                       sdpgm               10
     O                                          103 'Sales Timing Allocation'
     O                                          117 ' Detail Report'
     O                                          188 'DATE'
     O                       udate         y    198
      *
      *
     O          e            h1hdr          1
     O                       sdusr               10
     O                       h1demand           113
     O                                          188 'TIME'
     O                       rtime              198 '  :  :  '
      *
     O          e            h1hdr          1
     O                                          188 'PAGE'
     O                       page          z    198
      *
      *
     O          e            h1hdr          1
     O                                           17 'Year/week:'
     O                       ldyr          z     23
     O                       ldwk          z     27
      *
     O          e            h1hdr          1
     O                                           17 'Week-ending date:'
     O                       ldwemdy             27 '  /  /  '
      *
     O          e            h1hdr          1
     O                                           17 'Exempt flag:'
     O                                           21 'No'
      *
     O          e            h1hdr          1
     O                                           17 'Item type:'
     O                                           21 'FG'
      *
     O          e            h1hdr          1
     O                                           17 'Inventory flag:'
     O                                           22 'Yes'
      *
     O          e            h1hdr          2
     O                                           17 'CoOwned flag:'
     O                                           21 'No'
      *
      *-------------------------------------------------------------
      * Column headings
      *-------------------------------------------------------------
      *
     O          e            h1hdr          1
     O                                          115 'Seaboard Amounts (+)'
     O                                          142 'Seaboard Amounts (-)'
     O                                          171 'Triumph Amounts (+)'
     O                                          198 'Triumph Amounts (-)'
      *
     O          e            h1hdr          1
     O                                          115 '--------------------------'
     O                                          142 '-------------------------'
      *
     O                                          171 '--------------------------'
     O                                          198 '-------------------------'
      *
     O          e            h1hdr          1
     O                                          115 'Ending Inventory Value'
     O                                          142 'Beginning Inventory Value'
      *
     O                                          171 'Ending Inventory Value'
     O                                          198 'Beginning Inventory Value'
     O          e            h1hdr          1
     O                                            7 'Item'
     O                                           64 '----- Inventory Change'
     O                                           86 ' Actual Amounts ------'
      *
     O                                          115 'Net Product Revenue'
     O                                          142 'Produced Value'
      *
     O                                          171 'Net Product Revenue'
     O                                          198 'Produced Value'
     O          e            h1hdr          1
     O                                            7 'Code'
     O                                           20 'Description'
     O                                           54 'Seaboard'
     O                                           70 'Triumph'
     O                                           85 'Aggregate'
     O                                          106 'Except Net Product'
     O                                          115 ' Revenue'
     O                                          142 'Except Produced Value'
      *
     O                                          163 'Except Net Product'
     O                                          171 ' Revenue'
     O                                          198 'Except Produced Value'
     O          e            h1hdr          1
     O                       dash               198
      *
      *-------------------------------------------------------------
      * Detail line
      *-------------------------------------------------------------
      *
     O          e            l1dtl          1
     O                       w1prcd        z      7
     O                       w1prds              39
     O                       w1sicactam    k     55
     O                       w1ticactam    k     71
     O                       w1aicactam    k     86
      *
     O                       w1seiam       k    109
     O                       w1sbiam       k    138
     O                       w1teiam       k    165
     O                       w1tbiam       k    193
      *
     O          e            l1dtl          1
     O                       w1snpram      k    109
     O                       w1spuam       k    138
     O                       w1tnpram      k    165
     O                       w1tpuam       k    193
      *
     O          e            l1dtl          2
     O                       w1sexnpram    k    113
     O                       w1sexpuam     k    142
     O                       w1texnpram    k    169
     O                       w1texpuam     k    197
      /eject
