      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Hog Production System
      * PROGRAM:     Workfile: Process Nursery-to-Grow Finish Transfers
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     11/20/01
      *
      * FUNCTION:    This function writes records to the Workfile: Grow Finish Groups--
      *              Nursery Sources. One record is written for each unique combination of:
      *                         grow finish group (destination)
      *                         nursery group (origin)
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 07/06/09  LeAnne Ramsey
      *           Recompile only. Added new field 'Continuous Flow Flag' to Hog Group file.
      *
      * 10/15/13  LeAnne Ramsey (E2831)
      *           Recompile only. Added field 'MTech Reference'.
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fhsl034d   if   e           k disk
      *    Hog group
      *
      *
     Fhsp018    if   e           k disk
      *    Farm site
      *
      *
     Fhsj075g   if   e           k disk
      *  Transfer detail + transfer header
      *  (Records selected in open query)
      *
      *
     Fhsp329    uf a e           k disk
      *  Workfile: Grow finish groups--nursery sources
      *
      /EJECT
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      ****************************************************************
      * ARRAYS AND TABLES
      ****************************************************************
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ***************************************************************************************
      * MAINLINE
      ***************************************************************************************
      *
      * Process Nursery-to-Grow Finish transfers that have quality or rejected head.
      * (Records selected with open query and file keyed by Received Date.)
      *
     C                   dou       *in90 = *on                                  Main do loop
     C                   read      hsj075g                                90
     C                   if        *in90 = *off and                             If not EOF
     C                             (mdqlhd <> 0 or mdrjhd <> 0)
      *
      *
      * Either update or write a record to the workfile for this combination of:
      *  destination grow finish group/origin nursery group
      *
     C     key01         chain     hsp329                             92
     C                   if        *in92 = *off
     C                   exsr      $upd329
     C                   else
     C                   exsr      $wrt329
     C                   endif
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Main do loop
      *
      *
      * EOF Processing
      *
     C                   seton                                        lr
      /eject
      *--------------------------------------------------------------
      * Update a record
      *--------------------------------------------------------------
      *
     C     $upd329       begsr
      *
     C                   add       mdqlhd        w5rchd
     C                   add       mdrjhd        w5rchd
     C                   update    w5rec
      *
     C                   endsr
      /eject
      *--------------------------------------------------------------
      * Write a record
      *--------------------------------------------------------------
      *
     C     $wrt329       begsr
      *
      * Retrieve destination hog group code
      *
     C     mddnsn        chain     hsl034d                            92
     C                   if        *in92 = *off
     C                   move      hghgcd        w5dnhgcd
     C                   move      hgppcd        w5dnppcd
     C                   else
     C                   move      *blank        w5dnhgcd
     C                   move      *blank        w5dnppcd
     C                   endif
      *
      * Retrieve destination farm site cell
      *
     C     mhdnfs        chain     hsp018                             92
     C                   if        *in92 = *off
     C                   z-add     fscell        w5dncell
     C                   else
     C                   z-add     0             w5dncell
     C                   endif
      *
      * Retrieve origin hog group code
      *
     C     mdorsn        chain     hsl034d                            92
     C                   if        *in92 = *off
     C                   move      hghgcd        w5orhgcd
     C                   move      hgppcd        w5orppcd
     C                   else
     C                   move      *blank        w5orhgcd
     C                   move      *blank        w5orppcd
     C                   endif
      *
      * Retrieve origin farm site cell
      *
     C     mhorfs        chain     hsp018                             92
     C                   if        *in92 = *off
     C                   z-add     fscell        w5orcell
     C                   else
     C                   z-add     0             w5orcell
     C                   endif
      *
     C                   z-add     mdorsn        w5orhgsn
     C                   z-add     mhorfs        w5orfscd
     C                   z-add     mhdnfs        w5dnfscd
     C                   z-add     mddnsn        w5dnhgsn
     C     mdrjhd        add       mdqlhd        w5rchd
      *
     C                   write     w5rec
      *
     C                   endsr
      /eject
      *----------------------------------------------------------------
      * Initialization subroutine
      *----------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
     C     key01         klist
     C                   kfld                    mddnsn
     C                   kfld                    mdorsn
      *
     C                   endsr
      /EJECT
