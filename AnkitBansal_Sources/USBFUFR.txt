    ***USE THIS SOURCE MEMBER TO CONVERT A JULIAN DATE TO A CALENDAR DATE
    ***    PARAMETERS ARE INPUT  - JULIAN DATE
    ***                   OUTPUT - CALENDAR DATE IN CYMD FORMAT
    ***                   OUTPUT - STATUS CODE INDICATING IF ERROR OCCURRED
    ***
    ***    THE CODE BEFORE THE SUBROUTINE WILL APPEAR EVERYTIME THE
    ***    FUNCTION IS USED
    ***
     C                     MOVE #ICQNB    UDDD    50
     C                     EXSR U@DT3
     C                     MOVE UCYMD     #OAADT           VALID DATE
     C                     MOVE UERR      #OECST           ERROR STATUS
     C           U@DT3     BEGSR
      *================================================================
      * CONVERT TO CALENDAR DATE
      *================================================================
     C                     MOVE 'Y'       UERR    1
     C                     Z-ADD0         UCYMD            DATE
     C           UDDD      IFGT 0
     C                     MOVE 'N'       UERR
     C*  CALCULATE THE YEARS
     C           UDDD      DIV  365.25    UCYY    30       # OF YEARS
     C                     MVR            U040   040       DAY OF YEAR
     C           UCYY      DIV  4         U020   020       # OF LEAPS
     C                     MVR            U010   010       0 = LEAP
     C*  ADD A DAY IF 1901 OR GREATER AND NOT LEAP
     C           UCYY      IFGT 000
     C           U010      ANDNE0
     C                     ADD  1         U040
     C                     END
     C*  ADD ANOTHER DAY IF JAN OR FEB AND 1904 OR GREATER AND LEAP
     C           U040      IFLT 60                         JAN FEB
     C           UCYY      IFGE 004                        GE 1904
     C           U010      ANDEQ0                          LEAP
     C                     ADD  1         U040
     C                     END
     C                     ELSE
     C*  ADD ANOTHER 2 DAYS IF NOT JAN OR FEB
     C                     ADD  2         U040
     C                     END
     C*  ADD 30 DAYS SO THAT MONTHS ARE 1 THRU 12 AND NOT 0 THRU 11
     C                     ADD  30        U040
     C           U040      DIV  30.57     UMM     20
     C*  REMAINDER IS DAY OF THE MONTH LESS 1. CONVERT 0 TO 30 1 TO 31
     C                     MVR            U040
     C                     ADD  1         U040
     C*  COMBINE THE YEAR, MONTH AND DAYS INTO UCYYMMDD
     C                     MOVELUMM       U040
     C                     MOVE U040      UCYMD
     C                     MOVELUCYY      UCYMD   70
     C                     END
      *
      *================================================================
     C                     ENDSR
