     A*%%TS  SD  20030623  155811  ISLRAMS     REL-V5R2M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD1046
     A*  TITLE:         REVISE EXPENSE SUBCLASS CODE
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  CREATED:       06/19/03
     A*
     A*
     A*  INDICATOR 60 IS ON WHEN IN REVISE MODE
     A*  INDICATOR 61 IS ON WHEN IN CREATE MODE
     A*  INDICATOR 62 IS ON WHEN IN DELETE MODE OR WHEN THE RECORD
     A*               HAS BEEN DELETED OUT FROM UNDER THE USER.
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A  60N62                               CF05(05 'REFRESH')
     A  60N62                               CF11(11 'DELETE')
     A*
     A*---------------------------------------------------------------
     A* RECORD FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP1046R1
     A*%%TS  SD  20030619  162725  ISLRAMS     REL-V5R2M0  5722-WDS
     A                                      SETOF(33)
     A                                      SETOF(34)
     A                                      SETOF(35)
     A                                      OVERLAY
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 27'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USR)
     A                                      DSPATR(HI)
     A            D1MODE    R        O  2  2REFFLD(@@MODE)
     A                                      DSPATR(HI)
     A                                  2 26'REVISE EXPENSE SUBCLASS'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@JOB)
     A                                      DSPATR(HI)
     A                                  4  2'Expense classification:'
     A            D1ECCD    R        O  4 27REFFLD(HSRREF/@@ECCD *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A            D1ECDS    R        O  4 34REFFLD(HSRREF/ECECDS *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A                                  6  2'Expense subclass code .........'
     A            D1ESCD    R        B  6 34REFFLD(@@ESCD)
     A N60N62N35                            DSPATR(HI)
     A  35                                  DSPATR(PC)
     A  35                                  DSPATR(RI)
     A  60
     AO 62                                  DSPATR(PR)
     A N60N62                               DSPATR(UL)
     A                                  7  2'Expense subclass description ..'
     A            D1ESDS    R        B  7 34REFFLD(HSRREF/ESESDS *LIBL/HSPREF)
     A                                      CHECK(LC)
     A  34                                  DSPATR(RI)
     A  34                                  DSPATR(PC)
     A  62                                  DSPATR(PR)
     A N62                                  DSPATR(UL)
     A N62N34                               DSPATR(HI)
     A                                  8  2'Expense subclass sequence .....'
     A            D1ESSEQ   R        B  8 34REFFLD(HSRREF/@@SEQ *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A  33                                  DSPATR(RI)
     A  33                                  DSPATR(PC)
     A  62                                  DSPATR(PR)
     A N62                                  DSPATR(UL)
     A N62N33                               DSPATR(HI)
     A*
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R HP1046K1
     A*%%TS  SD  20030623  155811  ISLRAMS     REL-V5R2M0  5722-WDS
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A N62N61                          23 11'F5=Refresh'
     A N62N61                          23 23'F11=Delete'
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *---------------------------------------------------------------
     A          R HP1046ES                  SFL SFLMSGRCD(24)
      *
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE CONTROL FORMAT
      *---------------------------------------------------------------
     A          R HP1046EC                  SFLCTL(HP1046ES)
      *
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
