      *****************  RPG PROGRAM HEADING  ************************
      * SYSTEM:      HOG PRODUCTION
      * PROGRAM:     HP682
      * TITLE:       CLOSED GROUP PERFORMANCE REPORT-NOT RANKED
      * PROGRAMMER:  SHARON ZURN
      * CREATED:     7/29/94
      *
      *  FUNCTION:   THIS PROGRAM PRINTS WHEN THE USER HAS SELECTED THE
      *              OPTION OF 'NO RANKING'. IT PRINTS THE DATA FROM
      *              THE WORKFILES THAT ARE CREATED IN HP388:
      *                   HSP387--ONE RECORD FOR EACH HOG GROUP
      *                   HSP388--ONE TOTAL RECORD FOR EACH FARM SITE
      *                   HSP389--ONE TOTAL RECORD FOR EACH PHASE
      *
      *              THE DATA IS SORTED/KEYED IN THE FOLLOWING SEQUENCE
      *              BY THE OPNQRY: PHASE, FARM, DISPOSED DATE, GROUP
      *
      *           THIS PROGRAM IS SUBMITTED BY HP482CL WHICH IS CALLED
      *           FROM HP482 THROUGH QCMDEXC.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 09/12/94  LEANNE FEDOR
      *           RECOMPILE ONLY. REMOVED NEXT MOVEMENT NUMBER FROM
      *           BUSINESS OFFICE FILE.
      *
      * 10/05/94  MARY PALMER
      *           REMOVED AVERAGE CARCASS POUNDS AND GROWER FEE COST.
      *           ADDED TRANSFERRED OUT HEAD, LEAN %, AND AVERAGE DAILY
      *           GAIN.  MOVED LEAN %, BACKFAT INCHES, & CARCASS YIELD
      *           TO RIGHT END OF REPORT AND PRINT FOR GROW/FINISH ONLY
      *           ADDED TRANSFER IN $ PER HEAD MOVED AND FEED COST PER
      *           HEAD MOVED FOR NURSERY GROUPS. "BGF" GROUPS WILL NOT
      *           BE INCLUDED IN THE REPORT. CHANGED TOTAL COST/LB GAIN
      *           TO PHASE COST/LB GAIN (DEDUCTED TRANSFER IN $).
      *           MODIFIED CALCULATIONS TO USE HEAD MOVED INSTEAD OF
      *           HEAD SOLD FOR FEED LBS/HEAD MOVED, TRANI$, FEED COST,
      *           OTHER COST, AND TOTAL COST.  ADDED TRANO$ TO OTHER
      *           COST WHICH WILL CORRECT MARGIN.
      *
      * 10/12/94  MARY PALMER
      *           REWRITTEN TO RENAME FIELDS AND CLEAR OUT ALL
      *           WORK FIELDS. ADD TEST FOR OVERFLOW BEFORE PRINTING
      *           DETAIL LINE.
      *
      * 10/12/94  MARY PALMER
      *           ALLOW NO VALUE IN BUSINESSS OFFICE RESTRICTOR FOR ALL
      *           PRINT BUSINESS OFFICE ON SAME LINE WITH FARM SITE
      *
      * 10/22/94  MARY PALMER
      *           CORRECTED TOTAL COST PER CWT SUB-TOTAL TO ACCUMULATE
      *           ALL TOTAL COSTS IN WEIGHTED AVERAGE.
      *
      * 10/24/94  MARY PALMER
      *           CORRECTED MARGIN PER CWT TO WEIGHT AVERAGE SUB-TOTAL
      *           ONLY WHEN GROUP HAS NET SALES.
      *
      * 10/25/94  MARY PALMER
      *           CHANGED AVG LBS SOLD TO AVERAGE POUNDS PER HEAD MOVED
      *
      * 11/04/94  MARY PALMER
      *           CHANGED CALCULATION OF COST OF TRANSFERS AND COST
      *           OF SALES TO BE BASED ON $/LIVE CWT INSTEAD OF $/HD.
      *           MODIFIED HEADING FOR NET SALES TO BE PER CWT SOLD.
      *           INCREASED SIZE OF FEED CONSUMED POUNDS ACCUMULATOR
      *           FOR A PRODUCTION PHASE.
      *
      * 11/14/94  MARY PALMER
      *           IF NO FEED POUNDS FOR A GROUP, DO NOT ACCUMULATE FOR
      *           SUB-TOTALS.
      *
      * 11/21/94  MARY PALMER
      *           REPLACE CLOSED DATE WITH DISPOSED DATE
      *
      * 01/03/95  LEANNE FEDOR
      *           RECOMPILE ONLY FOR FEED REWRITE.
      *
      * 01/09/95  LEANNE FEDOR
      *           INCREASED THE SIZE OF WORKFIELDS AND ACCUMULATORS.
      *
      * 01/13/95  LEANNE FEDOR
      *           RECOMPILE ONLY. SEX ADDED TO HOG GROUP FILE.
      *
      * 01/16/95  MARY PALMER
      *           ADD SEX CODE COLUMN, MAKE PHASE AND FEED COSTS PER
      *           POUND OF GAIN PRINT SIZE 3.2 AND ADJUST HEADINGS
      *
      * 01/23/95  LEANNE FEDOR
      *           MODIFIED TO PRINT USING THE WORKFILES CREATED IN
      *           RPG PROGRAM HSP388.
      *
      * 04/25/95  LEANNE FEDOR
      *           CHANGED FARM SITE CODE FROM ALPHA TO NUMERIC.
      *
      * 08/20/95  LEANNE FEDOR
      *           ADDED LOGIC FOR A NEW USER SELECITON ON 'CELL'.
      *
      * 09/12/95  LEANNE FEDOR
      *           ADDED NEW COLUMNS: CULLS SOLD %, MORTALITY HEAD
      *           REMOVED COLUMNS:   NET SALES, COST OF SALES, MARGIN$
      *           CHANGED COLUMN HEADING 'DEATH LOSS %' TO BE
      *           MORTALITY PERCENT.
      *
      * 11/01/95  LEANNE FEDOR
      *           CHANGED HEADING TO REFLECT THE NEW SELECTION CRITERIA
      *           OF 'GROUP TYPE'.
      *
      * 04/12/96  LEANNE FEDOR
      *           CHANGED HEADING TO REFLECT THE NEW SELECTION CRITERIA
      *           OF 'BREED STOCK'.
      *
      * 08/08/96  LEANNE FEDOR
      *           ADD 2 FIELDS: ADJUSTED FEED CONVERSION
      *                         AVERAGE CALORIES PER POUND OF FEED
      *           TO SCRUNCH THESE FIELDS INTO THIS REPORT, WE:
      *             1) REMOVED ALL COMMAS FROM PRINTED VALUES
      *                  ON THE DETAIL PRINT LINE
      *             2) REDUCED THE DECIMAL POSITIONS FROM 2 TO 1 ON:
      *                  AVERAGE POUNDS PLACED
      *                  AVERAGE POUNDS PER HEAD MOVED
      *                  FEED POUNDS PER HEAD MOVED
      *                  LEAN PERCENT
      *                  CARCASS YIELD
      *             3) CHANGED COLUMN HEADING 'SEX' TO 'SX'
      *
      * 08/14/96  LEANNE FEDOR
      *           CAL STOLLE REQUEST THE ADDITION OF ANOTHER FIELD
      *           (ADJUSTED AVERAGE DAILY GAIN) TO THE REPORT.
      *
      *           TO SCRUNCH THIS FIELD ONTO THIS REPORT, WE:
      *           REDUCED THE DECIMAL POSITIONS FROM 2 TO 1 ON:
      *                  CULL SOLD PERCENT
      *                  MORTALITY PERCENT
      *                  AVERAGE FEED COST PER TON
      *
      *           ALSO, TO SCRUNCH MORE, THE POSITION USED FOR
      *           PRINTING THE 'NEGATIVE' SIGN ON EDITED NUMERIC
      *           FIELDS IS SERVING, IN MANY INSTANCES, AS THE
      *           SINGLE SPACE SEPARATING COLUMNS. I ATTEMPTED TO
      *           DO THIS ONLY ON COLUMNS THAT I THOUGHT WOULD NEVER
      *           BE NEGATIVE!!!
      *
      * 08/14/96  LEANNE FEDOR
      *           WHILE REVIEWING CHANGES NEW FIELDS, FOUND A BUG
      *           ON TOTAL LINES. A TOTAL 'MARGIN PER CWT' WAS
      *           PRINTING WHEN ALL GROUP DETAIL LINES FOR THE
      *           FARM HAD 'N/A' FOR 'MARGIN PER CWT'. THE
      *           FIELD BEING CHECKED TO CONTROL PRINTING OF 'N/A'
      *           ON THE GROUP LINE WAS 'NET SALES PER CWT' WHILE
      *           THE FIELD ON THE TOTAL LINES WAS 'NET SALES AMOUNT'.
      *           I CHANGED THE LOGIC FOR THE TOTAL LINES TO CHECK
      *           THE VALUE IN 'NET SALES PER CWT'.
      *
      * 04/13/98  LeAnne Fedor
      *           Recompile only. New field 'state' added to farm site file.
      *
      * 08/17/00  LeAnne Fedor
      *           Recompile only. New field 'adjusted transferred in head' was
      *           added to workfiles. Also, the cull% and mortality% values in
      *           these workfiles are now calculated with the new 'adjusted
      *           transferred in head' instead of the original 'transferred in' head.
      *
      * 06/05/01  LeAnne Fedor
      *           Recompile only. New field 'multisite' added to Farm Site file.
      *
      * 06/26/01  LeAnne Fedor
      *           Three fields (manager/supervisor/multisite) renamed in Farm Site file.
      *           Manager and supervisor fields removed from Hog Group file.
      *
      * 09/04/01  LeAnne Fedor
      *           Recompile only. New 'square feet' fields added to Farm Site file.
      *
      * 10/16/13  LeAnne Ramsey (E2831)
      *           Recompile only. Added field 'MTech Reference'.
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     FHSP009    IF   E           K DISK
      *    BUSINESS OFFICE
      *
     FHSP010    IF   E           K DISK
      *    PRODUCTION TYPE
      *
     FHSP011    IF   E           K DISK
      *    PRODUCTION PHASE
      *
     FHSP018    IF   E           K DISK
      *    FARM SITE
      *
     FHSP025    IF   E           K DISK
      *    GROUP TYPE
      *
     FHSP031    IF   E           K DISK
      *    BREED STOCK
      *
     FHSP387    IF   E           K DISK
      *    WORKFILE FOR GROUPS
      *    (KEYED IN OPNQRY)
      *
     FHSP388    IF   E           K DISK
      *    WORKFILE OF FARM TOTALS
      *
     FHSP389    IF   E           K DISK
      *    WORKFILE OF PHASE TOTALS
      *
     FQPRINT    O    F  198        PRINTER OFLIND(*INOF)
      /EJECT
      ****************************************************************
      * INPUT SPECS
      ****************************************************************
      *
      *---------------------------------------------------------------
      * STANDARD PROGRAM STATUS DATA STRUCTURE
      *---------------------------------------------------------------
      *    EXTERNALLY DEFINED AS UTPGFR (RECORD FORMAT: PGMDSR)
     D PGMDS         ESDS                  EXTNAME(UTPGFR)
      *
      * DATE FORMAT FOR FLIPPING DATES
      *
     D                 DS
     D  WKCYMD                 1      8  0
     D  WKCC                   1      2  0
     D  WKYY                   3      4  0
     D  WKMMDD                 5      8  0
      *
      *---------------------------------------------------------------
      *  LDA -  WITH THE REQUESTED PRINT SELECTIONS
      *---------------------------------------------------------------
      *
     D                UDS
     D  LDFSBO                 1      5
     D  LDFROM                 6     13  0
     D  LDTO                  14     21  0
     D  LDPTCD                22     26
     D  LDPPCD                27     31
     D  LDFSCD                32     36  0
     D  LDOPTN                37     37
     D  LDCELL                50     51  0
     D  LDGTCD                52     52
     D  LDBSCD                53     57
      *
      *---------------------------------------------------------------
      *  NAMED CONSTANTS
      *---------------------------------------------------------------
      *
     D YES             C                   CONST('Y')
     D NO              C                   CONST('N')
     D ALL             C                   CONST('ALL')
     D NUR             C                   CONST('NUR  ')
      /EJECT
      ****************************************************************
      * MAIN LINE
      ****************************************************************
      *
      * READ EACH HOG GROUP RECORD SELECTED BY THE OPEN QUERY.
      *
     C     *IN99         DOUEQ     *ON                                          MAIN DO
     C                   READ      HSP387                                 99
     C     *IN99         IFEQ      *OFF                                         IF NOT EOF
      *
      * IF THIS IS THE FIRST TIME THROUGH,
      *     SAVE CONTROL FIELDS
      *     RETRIEVE PHASE AND FARM DESCRIPTIONS
      *     PRINT HEADINGS
      *
     C     FIRST         IFEQ      YES
     C                   MOVEL     NO            FIRST
     C                   MOVEL     WGPPCD        SVPPCD
     C                   Z-ADD     WGFSCD        SVFSCD
     C                   EXSR      $PPDS
     C                   EXSR      $FARM
     C                   EXSR      $OFLOW
     C                   ENDIF
      *
      * IF THERE IS A CHANGE IN THE PRODUCTION PHASE
      *    PRINT FARM SITE TOTALS
      *    PRINT PRODUCTION PHASE TOTALS
      *    RETRIEVE NEW FARM AND PHASE DESCRIPTIONS
      *    FORCE A BREAK TO A NEW PAGE AND PRINT HEADINGS
      *    SAVE CONTROL FIELDS
      *
     C     WGPPCD        IFNE      SVPPCD                                       IF PP BREAK
     C                   EXSR      $FSTOT
     C                   EXSR      $PPTOT
      *
     C                   EXSR      $FARM
     C                   EXSR      $PPDS
     C                   EXSR      $OFLOW
      *
     C                   MOVEL     WGPPCD        SVPPCD
     C                   Z-ADD     WGFSCD        SVFSCD
     C                   ENDIF                                                  IF PP BREAK
      *
      * IF THERE IS A CHANGE IN THE FARM SITE,
      *    PRINT FARM SITE TOTAL
      *    RETRIEVE AND PRINT NEW FARM SITE HEADING
      *
     C     WGFSCD        IFNE      SVFSCD                                       IF FS BREAK
     C                   EXSR      $FSTOT
     C                   Z-ADD     WGFSCD        SVFSCD
     C                   EXSR      $FARM
      *
     C     *INOF         IFEQ      *ON
     C                   EXSR      $OFLOW
     C                   ELSE
     C                   EXCEPT    HFHDR
     C                   ENDIF
     C                   ENDIF                                                  IF FS BREAK
      *
      * FLIP THE DISPOSED DATE TO MDY FROM CCYYMMDD
      *
     C                   Z-ADD     WGDSDT        WKCYMD
     C                   MOVEL     WKMMDD        DTMDY
     C                   MOVE      WKYY          DTMDY
      *
      *
      * MOVE THE FOLLOWING FIELDS WHICH HAVE 2 DECIMAL
      * POSITIONS TO PRINT FIELDS WITH A SINGLE DECIMAL
      * POSITION TO SAVE ROOM ON THIS CROWDED REPORT.
      *
     C                   Z-ADD     WGAPLB        DTAPLB
     C                   Z-ADD     WGAMLB        DTAMLB
     C                   Z-ADD     WGAFLB        DTAFLB
     C                   Z-ADD     WGLEAN        DTLEAN
     C                   Z-ADD     WGCRYD        DTCRYD
      *
     C                   Z-ADD     WGCUPR        DTCUPR
     C                   Z-ADD     WGDDPR        DTDDPR
     C                   Z-ADD     WGFC$T        DTFC$T
      *
      * PRINT 'N/A' INSTEAD OF AMOUNT WHEN THE NET SALES PER CWT
      * IS ZERO.
      *
     C     WGN$CW        IFEQ      0
     C                   SETON                                        85
     C                   ELSE
     C                   SETOFF                                       85
     C                   ENDIF
      *
     C     *INOF         IFEQ      *ON
     C                   EXSR      $OFLOW
     C                   ENDIF
      *
      * PRINT HOG GROUP LINE
     C                   EXCEPT    DTLINE
      *
     C                   ENDIF                                                  IF NOT EOF
     C                   ENDDO                                                  MAIN DO
      *
      *--------------------------------------------------------------
      * EOF PROCESSING
      *--------------------------------------------------------------
      *  IF NO DETAIL RECORDS MATCHED THE SELECTION CRITERIA, THEN
      *  PRINT THE STANDARD 'NO DATA...' ERROR MESSAGE
      *
     C     FIRST         IFEQ      YES
     C                   EXCEPT    HRHDR
     C                   EXCEPT    NODATA
     C                   ELSE
      * PRINT TOTALS
     C                   EXSR      $FSTOT
     C                   EXSR      $PPTOT
     C                   ENDIF
      *
     C                   SETON                                        LR
      /EJECT
      *****************************************************************
      * $FSTOT - CONTROL BREAK IN FARM SITE
      *****************************************************************
      *
     C     $FSTOT        BEGSR
      *
      * RETRIEVE FARM TOTALS FROM WORKFILE FOR THE PHASE
      *
     C     KEY01         CHAIN     HSP388                             91
     C     *IN91         IFEQ      *OFF                                          IF FOUND
      *
      * PRINT 'N/A' INSTEAD OF AMOUNT WHEN THE NET SALES PER CWT
      * IS ZERO.
      *
     C     WFN$CW        IFEQ      0
     C                   SETON                                        86
     C                   ELSE
     C                   SETOFF                                       86
     C                   ENDIF
      *
      *
      * MOVE THE FOLLOWING FIELDS WHICH HAVE 2 DECIMAL
      * POSITIONS TO PRINT FIELDS WITH A SINGLE DECIMAL
      * POSITION TO SAVE ROOM ON THIS CROWDED REPORT.
      *
     C                   Z-ADD     WFAPLB        TFAPLB
     C                   Z-ADD     WFAMLB        TFAMLB
     C                   Z-ADD     WFAFLB        TFAFLB
     C                   Z-ADD     WFLEAN        TFLEAN
     C                   Z-ADD     WFCRYD        TFCRYD
      *
     C                   Z-ADD     WFCUPR        TFCUPR
     C                   Z-ADD     WFDDPR        TFDDPR
     C                   Z-ADD     WFFC$T        TFFC$T
      *
      * PRINT FARM TOTAL LINE
      *
     C     *INOF         IFEQ      *ON
     C                   EXSR      $OFLOW
     C                   ENDIF
     C                   EXCEPT    TFTOT
     C                   ENDIF                                                    IF FOUND
      *
     C                   ENDSR
      /EJECT
      *****************************************************************
      * $PPTOT - CONTROL BREAK IN PRODUCTION PHASE
      *****************************************************************
      *
     C     $PPTOT        BEGSR
      *
      * RETRIEVE PRODUCTION PHASE TOTALS FROM WORKFILE
      *
     C     SVPPCD        CHAIN     HSP389                             91
     C     *IN91         IFEQ      *OFF                                          IF FOUND
      *
      * PRINT 'N/A' INSTEAD OF AMOUNT WHEN THE NET SALES
      * PER CWT ARE ZERO
      *
     C     WPN$CW        IFEQ      0
     C                   SETON                                        87
     C                   ELSE
     C                   SETOFF                                       87
     C                   ENDIF
      *
      * MOVE THE FOLLOWING FIELDS WHICH HAVE 2 DECIMAL
      * POSITIONS TO PRINT FIELDS WITH A SINGLE DECIMAL
      * POSITION TO SAVE ROOM ON THIS CROWDED REPORT.
      *
     C                   Z-ADD     WPAPLB        TPAPLB
     C                   Z-ADD     WPAMLB        TPAMLB
     C                   Z-ADD     WPAFLB        TPAFLB
     C                   Z-ADD     WPLEAN        TPLEAN
     C                   Z-ADD     WPCRYD        TPCRYD
      *
     C                   Z-ADD     WPCUPR        TPCUPR
     C                   Z-ADD     WPDDPR        TPDDPR
     C                   Z-ADD     WPFC$T        TPFC$T
      *
      *
      * PRINT PRODUCTION PHASE TOTAL LINE
      *
     C     *INOF         IFEQ      *ON
     C                   EXSR      $OFLOW
     C                   ENDIF
     C                   EXCEPT    TPTOT
     C                   ENDIF                                                    IF FOUND
      *
     C                   ENDSR
      /EJECT
      *****************************************************************
      * $OFLOW - OVERFLOW
      *****************************************************************
      *
     C     $OFLOW        BEGSR
      *
     C                   EXCEPT    HRHDR
     C                   EXCEPT    HPHDR
     C                   EXCEPT    HFHDR
      *
     C                   ENDSR
      /EJECT
      ****************************************************************
      * $DATE -  MANIPULATE DATES WITH DATE UTILITY
      ****************************************************************
      *
     C     $DATE         BEGSR
      *
     C                   CALL      'UT80060R'
     C                   PARM                    PFRM8             8 0
     C                   PARM                    PTO8              8 0
     C                   PARM                    PFRFMT            4
     C                   PARM                    PTOFMT            4
     C                   PARM                    PDAY              4 0
     C                   PARM                    PCODE             1
     C                   PARM                    PRTRN             1
      *
     C                   ENDSR
      /EJECT
      *****************************************************************
      * $PPDS - RETRIEVE PRODUCTION PHASE DESCRIPTION
      *****************************************************************
      *
     C     $PPDS         BEGSR
      *
      * SETON INDICATOR 88 TO CONTROL HEADINGS AND PRINTING
      *
     C     WGPPCD        IFEQ      NUR
     C                   SETON                                        88
     C                   ELSE
     C                   SETOFF                                       88
     C                   ENDIF
      *
     C                   MOVE      *BLANKS       HPPPDS
     C     WGPPCD        CHAIN     HSP011                             91
     C     *IN91         IFEQ      *OFF                                         IF FOUND
     C                   MOVEL(P)  PPPPDS        HPPPDS
     C                   ELSE
     C                   MOVEL(P)  'UNKNOWN'     HPPPDS
     C                   ENDIF                                                  IF FOUND
      *
     C                   ENDSR
      /EJECT
      ****************************************************************
      * $FARM - RETRIEVE FARM SITE NAME
      ****************************************************************
      *
     C     $FARM         BEGSR
      *
     C                   MOVE      *BLANK        HFFSDS
     C     WGFSCD        CHAIN     HSP018                             92
     C     *IN92         IFEQ      *ON
     C                   MOVEL     'UNKNOWN'     HFFSDS
     C                   ELSE
     C                   MOVEL     FSFSNM        HFFSDS
     C                   ENDIF
      *
      * IF ALL BUSINESS OFFICES WERE SELECTED, MOVE THE FARM'S
      * B/O TO THE PRINT FIELD IN THE FARM HEADING.
      *
     C     LDFSBO        IFEQ      *BLANK
     C                   MOVEL     WGFSBO        HFFSBO
     C                   ENDIF
      *
     C                   ENDSR
      /EJECT
      ****************************************************************
      * *INZSR - INITIALIZATION SUBROUTINE
      ****************************************************************
      *
     C     *INZSR        BEGSR
      *
      *
      * Key lists
      *
     C     key01         klist
     C                   kfld                    svppcd
     C                   kfld                    svfscd
      *
      *
      * DEFINE SAVE FIELDS
      *
     C     *LIKE         DEFINE    WGPPCD        SVPPCD
     C     *LIKE         DEFINE    WGFSCD        SVFSCD
      *
      * REPORT HEADING PRINT FIELDS
      *
     C     *LIKE         DEFINE    WGFSBO        HRFSBO
     C     *LIKE         DEFINE    BOBODS        HRBODS
     C     *LIKE         DEFINE    PPPPCD        HRPPCD
     C     *LIKE         DEFINE    PPPPDS        HRPPDS
     C     *LIKE         DEFINE    PTPTCD        HRPTCD
     C     *LIKE         DEFINE    PTPTDS        HRPTDS
     C     *LIKE         DEFINE    FSFSNM        HRFSDS
     C                   MOVE      *BLANK        HRGTCD            3
     C     *LIKE         DEFINE    GTGTDS        HRGTDS
     C     *LIKE         DEFINE    BSBSCD        HRBSCD
     C     *LIKE         DEFINE    BSBSDS        HRBSDS
     C                   Z-ADD     0             HRTMDY            6 0
     C                   Z-ADD     0             HRFMDY            6 0
      *
      * PRODUCTION PHASE HEADING PRINT FIELDS
      *
     C     *LIKE         DEFINE    PPPPDS        HPPPDS
      *
      * FARM SITE HEADING PRINT FIELDS
      *
     C     *LIKE         DEFINE    FSFSNM        HFFSDS
     C     *LIKE         DEFINE    BOBOBO        HFFSBO
      *
      * DETAIL LINE PRINT FIELDS
      *
     C                   Z-ADD     0             DTMDY             6 0
     C                   Z-ADD     0             DTAPLB            4 1
     C                   Z-ADD     0             DTAMLB            4 1
     C                   Z-ADD     0             DTAFLB            4 1
     C                   Z-ADD     0             DTLEAN            4 1
     C                   Z-ADD     0             DTCRYD            4 1
     C                   Z-ADD     0             DTCUPR            4 1
     C                   Z-ADD     0             DTDDPR            4 1
     C                   Z-ADD     0             DTFC$T            4 1
      *
      * FARM TOTAL LINE PRINT FIELDS.
      *
     C                   Z-ADD     0             TFAPLB            4 1
     C                   Z-ADD     0             TFAMLB            4 1
     C                   Z-ADD     0             TFAFLB            4 1
     C                   Z-ADD     0             TFLEAN            4 1
     C                   Z-ADD     0             TFCRYD            4 1
     C                   Z-ADD     0             TFCUPR            4 1
     C                   Z-ADD     0             TFDDPR            4 1
     C                   Z-ADD     0             TFFC$T            4 1
      *
      * PRODUCTION PHASE TOTAL LINE PRINT FIELDS
      *
     C                   Z-ADD     0             TPAPLB            4 1
     C                   Z-ADD     0             TPAMLB            4 1
     C                   Z-ADD     0             TPAFLB            4 1
     C                   Z-ADD     0             TPLEAN            4 1
     C                   Z-ADD     0             TPCRYD            4 1
     C                   Z-ADD     0             TPCUPR            4 1
     C                   Z-ADD     0             TPDDPR            4 1
     C                   Z-ADD     0             TPFC$T            4 1
      *
      * RETRIEVE TIME FOR REPORT HEADING
      * SET UP A FIRST TIME FLAG
      * SET UP A DASHED LINE FOR UNDERLINING THE HEADING
      *
     C                   TIME                    RTIME             6 0
     C                   MOVEL     YES           FIRST             1
     C                   MOVEL     *ALL'-'       DASH            198
      *
      * FORMAT 'FROM' DATE TO MMDDYY
      *
     C                   Z-ADD     LDFROM        PFRM8
     C                   Z-ADD     0             PTO8
     C                   MOVEL     'CYMD'        PFRFMT
     C                   MOVEL     'MDY '        PTOFMT
     C                   Z-ADD     0             PDAY
     C                   MOVE      *BLANK        PCODE
     C                   MOVE      *BLANK        PRTRN
     C                   EXSR      $DATE
     C                   Z-ADD     PTO8          HRFMDY
      *
      * FORMAT 'TO' DATE TO MMDDYY
      *
     C                   Z-ADD     LDTO          PFRM8
     C                   Z-ADD     0             PTO8
     C                   MOVEL     'CYMD'        PFRFMT
     C                   MOVEL     'MDY '        PTOFMT
     C                   Z-ADD     0             PDAY
     C                   MOVE      *BLANK        PCODE
     C                   MOVE      *BLANK        PRTRN
     C                   EXSR      $DATE
     C                   Z-ADD     PTO8          HRTMDY            6 0
      *
      * GET BUSINESS OFFICE DESCRIPTION
      *
     C     LDFSBO        IFNE      *BLANK                                       IF ENTERED
     C                   MOVEL     LDFSBO        HRFSBO
     C     LDFSBO        CHAIN     HSP009                             92
     C     *IN92         IFEQ      *OFF                                         IF FOUND
     C                   MOVEL     BOBODS        HRBODS
     C                   ENDIF                                                  IF FOUND
     C                   ELSE
     C                   MOVEL     ALL           HRFSBO
     C                   ENDIF                                                  IF ENTERED
      *
      * GET PRODUCTION TYPE DESCRIPTION
      *
     C     LDPTCD        IFNE      *BLANK                                       IF ENTERED
     C     LDPTCD        CHAIN     HSP010                             93
     C                   MOVEL     LDPTCD        HRPTCD
     C     *IN93         IFEQ      *OFF                                         IF FOUND
     C                   MOVEL     PTPTDS        HRPTDS
     C                   ELSE
     C                   MOVEL     'UNKNOWN'     HRPTDS
     C                   ENDIF                                                  IF FOUND
     C                   ELSE
     C                   MOVEL     ALL           HRPTCD
     C                   ENDIF                                                  IF ENTERED
      *
      * CELL
     C     LDCELL        IFEQ      0
     C                   SETON                                        81
     C                   ENDIF
      *
      *
      * RETRIEVE FARM SITE NAME
      *
     C     LDFSCD        IFEQ      0                                            IF ENTERED
     C                   SETON                                        89
     C                   ELSE
     C     LDFSCD        CHAIN     HSP018                             95
     C     *IN95         IFEQ      *OFF                                         IF FOUND
     C                   MOVEL     FSFSNM        HRFSDS
     C                   ELSE
     C                   MOVEL     'UNKNOWN'     HRFSDS
     C                   ENDIF                                                  IF FOUND
     C                   ENDIF                                                  IF ENTERED
      *
      * GET THE PRODUCTION PHASE DESCRIPTION
      *
     C     LDPPCD        IFNE      *BLANK                                       IF ENTERED
     C                   MOVEL     LDPPCD        HRPPCD
     C     LDPPCD        CHAIN     HSP011                             94
     C     *IN94         IFEQ      *OFF                                         IF FOUND
     C                   MOVEL     PPPPDS        HRPPDS
     C                   ELSE
     C                   MOVEL     'UNKNOWN'     HRPPDS
     C                   ENDIF                                                  IF FOUND
     C                   ELSE
     C                   MOVEL     ALL           HRPPCD
     C                   ENDIF                                                  IF ENTERED
      *
      *
      * GET GROUP TYPE DESCRIPTION
      *
     C     LDGTCD        IFNE      *BLANK                                       IF ENTERED
     C     LDGTCD        CHAIN     HSP025                             93
     C                   MOVEL(P)  LDGTCD        HRGTCD
     C     *IN93         IFEQ      *OFF                                         IF FOUND
     C                   MOVEL     GTGTDS        HRGTDS
     C                   ELSE
     C                   MOVEL     'UNKNOWN'     HRGTDS
     C                   ENDIF                                                  IF FOUND
     C                   ELSE
     C                   MOVEL(P)  ALL           HRGTCD
     C                   ENDIF                                                  IF ENTERED
      *
      * GET BREED STOCK DESCRIPTION
      *
     C     LDBSCD        IFNE      *BLANK                                       IF ENTERED
     C     LDBSCD        CHAIN     HSP031                             92
     C                   MOVEL(P)  LDBSCD        HRBSCD
     C     *IN92         IFEQ      *OFF                                         IF FOUND
     C                   MOVEL     BSBSDS        HRBSDS
     C                   ELSE
     C                   MOVEL     'UNKNOWN'     HRBSDS
     C                   ENDIF                                                  IF FOUND
     C                   ELSE
     C                   MOVEL(P)  ALL           HRBSCD
     C                   ENDIF                                                  IF ENTERED
      *
     C                   ENDSR
      /EJECT
      *-------------------------------------------------------------
      * REPORT HEADING LINES
      *-------------------------------------------------------------
      *
     OQPRINT    E            HRHDR          1 01
     O                       SDPGM               10
     O                                          110 'HOG PRODUCTION SYSTEM'
     O                                          189 'DATE'
     O                       UDATE         Y    198
      *
     O          E            HRHDR          1
     O                       SDUSR               10
     O                                          108 'CLOSED GROUP PERFORMANCE'
     O                                          115 'REPORT'
     O                                          189 'TIME'
     O                       RTIME              198 '  :  :  '
      *
     O          E            HRHDR          1
     O                                          189 'PAGE'
     O                       PAGE          Z    198
      *
     O          E            HRHDR          1
     O                                           16 'BUSINESS OFFICE '
     O                                           22 '.....'
     O                       HRFSBO              29
     O                       HRBODS              63
      *
     O          E            HRHDR          1
     O                                           13 'FROM CLOSED D'
     O                                           22 'ATE .....'
     O                       HRFMDY        Y     32
      *
     O          E            HRHDR          1
     O                                           13 'TO CLOSED DAT'
     O                                           22 'E .......'
     O                       HRTMDY        Y     32
      *
      *
     O          E            HRHDR          1
     O                                           22 'PRODUCTION TYPE ......'
     O                       HRPTCD              29
     O                       HRPTDS              43
      *
     O          E            HRHDR          1
     O                                           22 'PRODUCTION PHASE .....'
     O                       HRPPCD              29
     O                       HRPPDS              63
      *
     O          E            HRHDR          1
     O                                           22 'CELL .................'
     O              N81      LDCELL        K     27
     O               81                          27 'ALL'
      *
     O          E            HRHDR          1
     O                                           22 'FARM SITE ............'
     O              N89      LDFSCD        M     30
     O               89                          27 'ALL'
     O              N89      HRFSDS              58
      *
     O          E            HRHDR          1
     O                                           22 'GROUP TYPE ...........'
     O                       HRGTCD              27
     O                       HRGTDS              48
      *
     O          E            HRHDR          2
     O                                           22 'BREED STOCK ..........'
     O                       HRBSCD              29
     O                       HRBSDS              53
      *
     O          E            HRHDR          1
     O                                           20 'AVG'
     O                                           71 'AVG'
     O                                           84 'PER HD MOVE'
     O                                           96 'ADJ'
     O                                          106 'AVERAGE'
     O                                          112 'AVG'
     O                                          118 'ADJ'
      *
     O                                          130 '--COST/LB-'
     O                                          135 'AVG'
     O                                          157 '--PER CWT LIVE MOVED'
     O                                          164 'OUT --'
     O                                          172 'NET SLS'
     O                                          180 'MARGIN'
     O              N88                         192 'BACK'
      *
     O          E            HRHDR          1
     O                                            5 'GROUP'
     O                                           13 'DATE'
     O                                           22 'WK-DAY'
     O                                           30 'TRANI'
     O                                           36 'HEAD'
     O                                           42 'TRANO'
     O                                           49 'CULLS'
     O                                           54 'SOLD'
     O                                           65 'MORTALITY'
     O                                           71 'LBS'
     O                                           78 'AVG'
     O                                           84 'FEED'
     O                                           90 'FEED'
     O                                           97 'FEED'
     O                                          106 'CALORIE'
     O                                          112 'DAILY'
     O                                          118 'DAILY'
      *
     O                                          130 '--OF GAIN-'
     O                                          136 'FEED'
     O                                          143 'TRANI'
     O                                          150 'FEED'
     O                                          157 'OTHER'
     O                                          164 'TOTAL'
     O                                          172 'PER CWT'
     O                                          178 'PER'
     O              N88                         191 'FAT'
     O              N88                         197 'CARC'
      *
     O          E            HRHDR          1
     O                                            6 'NUMBER'
     O                                           15 'DISPOSED'
     O                                           22 'IN PHS'
     O                                           25 'SX'
     O                                           30 'HEAD'
     O                                           36 'SOLD'
     O                                           42 'HEAD'
     O                                           48 'HEAD'
     O                                           53 '%'
     O                                           60 'HEAD'
     O                                           65 '%'
     O                                           72 'PLACE'
     O                                           78 'LBS'
     O                                           84 'LBS'
     O                                           91 'CONVS'
     O                                           98 'CONVS'
     O                                          106 'LB FEED'
     O                                          112 'GAIN'
     O                                          118 'GAIN'
     O                                          125 'PHASE'
     O                                          130 'FEED'
     O                                          137 'CST/TN'
     O                                          141 '$'
     O                                          150 'COST'
     O                                          156 'COST'
     O                                          163 'COST'
     O                                          171 'SOLD'
     O                                          178 'CWT'
     O              N88                         186 'LEAN%'
     O              N88                         192 'INCH'
     O              N88                         198 'YIELD'
      *
     O          E            HRHDR          2
     O                       DASH               198
      *
      *-------------------------------------------------------------
      * PRODUCTION PHASE HEADING
      *-------------------------------------------------------------
      *
     O          E            HPHDR          1
     O                       HPPPDS              30
      *
      *-------------------------------------------------------------
      * FARM SITE HEADING
      *-------------------------------------------------------------
      *
     O          E            HFHDR       1  1
     O                       HFFSDS              31
     O                       HFFSBO              37
      *
      *-------------------------------------------------------------
      * DETAIL LINE
      *-------------------------------------------------------------
      *
     O          E            DTLINE         1
     O                       WGHGCD         B     7
     O                       DTMDY         YB    16
     O                       WGWK           B    19
     O                                           20 '-'
     O                       WGDAY          B    21
     O                       WGSXCD         B    24
     O                       WGTIHD        MB    31
     O                       WGSLHD        MB    37
     O                       WGTOHD        MB    43
     O                       WGCUHD        MB    49
     O                       DTCUPR        MB    55
      *
     O                       WGDDHD        MB    61
     O                       DTDDPR        MB    67
     O                       DTAPLB        MB    73
     O                       DTAMLB        MB    79
     O                       DTAFLB        MB    85
     O                       WGFDCV        MB    92
     O                       WGAFCV        MB    99
     O                       WGACAL        MB   106
     O                       WGAGLB        MB   113
     O                       WGJGLB        MB   119
     O                       WGP$GN        MB   126
     O                       WGF$GN        MB   131
     O                       DTFC$T        MB   137
     O                       WGTI$M        MB   144
     O                       WGFC$M        MB   151
     O                       WGOC$M        MB   158
     O                       WGTC$M        MB   165
     O              N85      WGN$CW        MB   172
     O               85                         171 'N/A'
     O              N85      WGM$CW        MB   180
     O               85                         179 'N/A'
     O              N88      DTLEAN        2B   185
     O              N88      WGBFIN        2B   192
     O              N88      DTCRYD        2B   198
      *
      *-------------------------------------------------------------
      * FARM SITE TOTAL LINE
      *-------------------------------------------------------------
      *
     O          E            TFTOT       1  1
     O                       SVFSCD        MB     6
     O                                            7 '*'
     O                       WFTIHD        KB    31
     O                       WFTOHD        KB    43
     O                       WFDDHD        KB    61
     O                       TFDDPR        KB    67
      *
     O                       TFAPLB        KB    73
     O                       TFAMLB        KB    79
     O                       TFAFLB        KB    85
     O                       WFFDCV        KB    92
     O                       WFAFCV        MB    99
     O                       WFACAL        MB   106
     O                       WFAGLB        KB   113
     O                       WFJGLB        MB   119
      *
     O                       WFP$GN        KB   126
     O                       WFF$GN        KB   131
     O                       TFFC$T        KB   137
     O                       WFTI$M        KB   144
     O                       WFFC$M        KB   151
     O                       WFOC$M        KB   158
     O                       WFTC$M        KB   165
     O              N86      WFN$CW        KB   172
     O               86                         171 'N/A'
     O              N86      WFM$CW        KB   180
     O               86                         179 'N/A'
     O              N88      TFLEAN        2B   185
     O              N88      TFCRYD        2B   198
      *
     O          E            TFTOT          1
     O                       WFSLHD        KB    37
     O                       WFCUHD        KB    49
     O                       TFCUPR        KB    55
     O              N88      WFBFIN        2B   192
      *
      *-------------------------------------------------------------
      * PRODUCTION PHASE TOTAL LINE
      *-------------------------------------------------------------
      *
     O          E            TPTOT       2  1
      *
     O                       SVPPCD         B     5
     O                                            8 '**'
     O                       WPTIHD        KB    31
     O                       WPTOHD        KB    43
     O                       WPDDHD        KB    61
     O                       TPDDPR        KB    67
     O                       TPAPLB        KB    73
     O                       TPAMLB        KB    79
     O                       TPAFLB        KB    85
     O                       WPFDCV        KB    92
     O                       WPAFCV        MB    99
     O                       WPACAL        MB   106
     O                       WPAGLB        KB   113
     O                       WPJGLB        MB   119
      *
     O                       WPP$GN        KB   126
     O                       WPF$GN        KB   131
     O                       TPFC$T        KB   137
     O                       WPTI$M        KB   144
     O                       WPFC$M        KB   151
     O                       WPOC$M        KB   158
     O                       WPTC$M        KB   165
     O              N87      WPN$CW        KB   172
     O               87                         171 'N/A'
     O              N87      WPM$CW        KB   180
     O               87                         179 'N/A'
     O              N88      TPLEAN        2B   185
     O              N88      TPCRYD        2B   198
      *
     O          E            TPTOT          1
     O                       WPSLHD        KB    37
     O                       WPCUHD        KB    49
     O                       TPCUPR        KB    55
     O              N88      WPBFIN        2B   192
      *
      *-------------------------------------------------------------
      * NO DATA MESSAGE LINE
      *-------------------------------------------------------------
     O          E            NODATA         1
     O                                           19 'NO DATA MEETS YOUR'
     O                                           38 'SELECTION CRITERIA'
