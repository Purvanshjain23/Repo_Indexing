     A*%%TS  SD  20040204  171243  ISLRAMS     REL-V5R2M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD1703
     A*  TITLE:         SELECT OPTIONS FOR DEFAULTING BUDGET ITEMS
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  CREATED:       02/04/04
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A*
     A*---------------------------------------------------------------
     A* RECORD FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP1703R1
     A*%%TS  SD  20040204  171243  ISLRAMS     REL-V5R2M0  5722-WDS
     A                                      SETOF(30)
     A                                      SETOF(31)
     A                                      OVERLAY
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 25'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USR)
     A                                      DSPATR(HI)
     A                                  2 16'SELECT OPTIONS FOR DEFAULTING BUD-
     A                                      GET ITEMS'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@JOB)
     A                                      DSPATR(HI)
     A                                  4  2'No Budget Items exist yet for this-
     A                                       Budget Template.'
     A                                  5  2'To help you get started, we will d-
     A                                      efault Items into the Template for -
     A                                      you.'
     A                                  6  2'But, you must tell us whether to d-
     A                                      efault all Items or just Items for'
     A                                  7  2'specific Item Types.'
     A                                  9  2'So, at this point, if you do not w-
     A                                      ish to continue take F3-Exit.'
     A                                      DSPATR(HI)
     A                                 11  2'If you wish to continue, select on-
     A                                      e of the default actions below and'
     A                                      DSPATR(HI)
     A                                 12  2'press ENTER.'
     A                                      DSPATR(HI)
     A                                 14  2'1=Select'
     A                                      DSPATR(HI)
     A            D1ALL     R        B 15 13REFFLD(HSRREF/@@FLAG *LIBL/HSPREF)
     A N30                                  DSPATR(HI)
     A  30                                  DSPATR(RI)
     A  30                                  DSPATR(PC)
     A                                 15 16'Default ALL Budget Items into this-
     A                                       Template.'
     A            D1SOME    R        B 17 13REFFLD(HSRREF/@@FLAG *LIBL/HSPREF)
     A N31                                  DSPATR(HI)
     A  31                                  DSPATR(RI)
     A  31                                  DSPATR(PC)
     A                                 17 16'Let me specify Item Types for the -
     A                                      default.'
     A*
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R HP1703K1
     A*%%TS  SD  20010220  091710  ISLRAMS     REL-V4R4M0  5769-PW1
     A  43                             21  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *---------------------------------------------------------------
     A          R HP1703ES                  SFL SFLMSGRCD(24)
      *
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
     A*---------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*---------------------------------------------------------------
     A*
     A          R HP1703EC                  SFLCTL(HP1703ES)
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
