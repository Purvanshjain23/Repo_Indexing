      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      HOG PRODUCTION
      * PROGRAM:     HP666 - EXPENSE TICKET REPORT
      * PROGRAMMER:  LEANNE FEDOR
      * CREATED:     06/01/94
      *
      * FUNCTION: THIS PROGRAM PRINTS EACH RECORD IN THE EXPENSE
      *           TICKET FILE THAT MEETS THE USER'S SELECTION
      *           CRITERIA.
      *
      *           THIS PROGRAM IS SUBMITTED BY HP466CL WHICH IS CALLED
      *           FROM HP466 THROUGH QCMDEXC.
      *
      *           REQUIRED SELECTIONS:
      *                 FROM BEGIN DATE
      *                 TO BEGIN DATE
      *
      *           OPTIONAL SELECTIONS:
      *                 REGION
      *                 BUSINESS OFFICE (ALLOCATED)
      *                 PRODUCTION PHASE
      *                 FARM SITE
      *                 HOG GROUP
      *                 EXPENSE CLASS
      *                 EXPENSE SUBCLASS
      *                 EXPENSE
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 09/12/94  LEANNE FEDOR
      *           RECOMPILE ONLY. REMOVED NEXT MOVEMENT NUMBER FROM
      *           BUSINESS OFFICE FILE.
      *
      * 09/12/94  LEANNE FEDOR
      *           RECOMPILE ONLY.
      *           REMOVED FOUR FIELDS FROM THE EXPENSE TICKET FILE:
      *                 PIG DAYS ALLOCATED OVER
      *                 ALLOCATED TIME
      *                 G/L POST TIME
      *                 EOP POST TIME
      *
      * 01/13/95  LEANNE FEDOR
      *           RECOMPILE ONLY. SEX ADDED TO HOG GROUP FILE.
      *
      * 01/19/95  LEANNE FEDOR
      *           RECOMPILE--FIELDS WERE ADDED TO THE EXPENSE
      *           TICKET FILE. AND, ADDED COST CENTER IN THE HEADING
      *           AS A SELECTION.
      *
      * 01/31/95  LEANNE FEDOR
      *           CHANGED TO USE PRINT NAMING STANDARDS.
      *
      * 03/13/95  LEANNE FEDOR
      *           RECOMPILE ONLY. ORIGIN BGF FARM SITE AND ORIGIN
      *           NURSERY FARM SITE ADDED TO HOG GROUP FILE.
      *
      * 04/25/95  LEANNE FEDOR
      *           CHANGED FARM SITE CODE FROM ALPHA TO NUMERIC.
      *
      * 11/01/95  LEANNE FEDOR
      *           RECOMPILE ONLY. NEW FIELD 'GROUP TYPE' ADDED TO
      *           HOG GROUP FILE.
      *
      * 06/03/97  LEANNE FEDOR
      *           RECOMPILE ONLY. NEW FIELDS 'PURGE FLAG' AND 'DATE OF
      *           FIRST PURGE' ADDED TO HOG GROUP FILE.
      *
      * 12/09/97  LEANNE FEDOR
      *           EXPENSE TICKET NUMBER FIELD RENAMED FROM 'XTNO' TO
      *           'XTSN'.
      *
      * 06/28/01  LeAnne Fedor
      *           Recompile only.
      *           Manager and supervisor fields removed from Hog Group file.
      *
      * 07/18/01  LeAnne Fedor
      *           Recompile only.
      *           Expense ticket amount expanded from 7,2 to 9,2.
      *
      * 12/11/02  LeAnne Fedor
      *           Replaced Expense Ticket file with a Workfile.
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     FHSP008    IF   E           K DISK
      *    REGION                  (KEY: RGRGCD)
      *
      *
     FHSP009    IF   E           K DISK
      *    BUSINESS OFFICE         (KEY: BOBOBO)
      *
      *
     FHSP010    IF   E           K DISK
      *    PRODUCTION TYPE         (KEY: PTPTCD)
      *
      *
     FHSP011    IF   E           K DISK
      *    PRODUCTION PHASE         (KEY: PPPPCD)
      *
      *
     FHSP363    IF   E           K DISK
      *  Workfile: Expense Ticket Report
      *
      *
     FQPRINT    O    F  132        PRINTER OFLIND(*INOF)
     F                                     INFDS(DS#PRT)
      *
      /EJECT
      ****************************************************************
      * ARRAYS FOR ACCUMULATING TOTALS FOR PRINTING
      ****************************************************************
      *
     D LBS             S             11  2 DIM(9)
     D AMT             S             11  2 DIM(9)
     D QTY             S              9  2 DIM(9)
     D HED             S              9  0 DIM(9)
      /EJECT
      ****************************************************************
      * INPUT SPECS
      ****************************************************************
      *
      *---------------------------------------------------------------
      * STANDARD PROGRAM STATUS DATA STRUCTURE
      *---------------------------------------------------------------
      *    EXTERNALLY DEFINED AS UTPGFR (RECORD FORMAT: PGMDSR)
     D PGMDS         ESDS                  EXTNAME(UTPGFR)
      *
      *---------------------------------------------------------------
      *  NAMED CONSTANTS
      *---------------------------------------------------------------
      *
     D YES             C                   CONST('Y')
     D NO              C                   CONST('N')
     D NOBO            C                   CONST('NO BUSINESS OFFICE')
     D NORG            C                   CONST('NO REGION')
      *
      *----------------------------------------------------------------
      *  DS#PRT: PRINTER CONTROL FIELDS...
      *----------------------------------------------------------------
     D DS#PRT          DS
     D  OVFLIN               188    189B 0
     D  CURLIN               367    368B 0
      *
      *----------------------------------------------------------------
      * LDA - REPORT SELECTIONS
      *----------------------------------------------------------------
      *
     D                UDS
     D  LDFROM                 1     08  0
     D  LDTO                   9     16  0
     D  LDRGCD                17     21
     D  LDALBO                22     26
     D  LDPPCD                27     31
     D  LDFSCD                37     41  0
     D  LDHGSN                42     48  0
     D  LDECCD                49     53
     D  LDESCD                54     58
     D  LDEXCD                59     63
     D  LDHGCD                64     70
     D  LDBODS                71    100
     D  LDPPDS               111    140
     D  LDFSNM               141    165
     D  LDECDS               166    185
     D  LDESDS               186    205
     D  LDEXDS               206    225
     D  LDRGDS               226    245
     D  LDFMDY               251    256  0
     D  LDTMDY               261    266  0
     D  LDCJD                271    282
     D  LDCOCD               283    285
      /EJECT
      ****************************************************************
      * MAIN LINE
      ****************************************************************
      *
      * READ AND PRINT ALL EXPENSE TICKET RECORDS
      *
     C     *IN90         DOUEQ     *ON                                          MAIN DO
     C                   READ      HSP363                                 90
     C     *IN90         IFEQ      *OFF                                         IF NOT EOF
      *
      * IF THIS IS THE FIRST TIME
      *      SAVE CONTROL BREAK FIELDS
      *      RETRIEVE DESCRIPTIONS FOR PRINTING
      *      PRINT REPORT HEADINGS
      *
     C     FIRST         IFEQ      YES                                          IF FIRST
     C                   MOVEL     WTRGCD        SVRGCD
     C                   MOVEL     WTALBO        SVALBO
     C                   MOVEL     WTPTCD        SVPTCD
     C                   MOVEL     WTPPCD        SVPPCD
     C                   Z-ADD     WTFSCD        SVFSCD
     C                   MOVEL     WTECCD        SVECCD
     C                   MOVEL     WTESCD        SVESCD
     C                   MOVEL     WTEXCD        SVEXCD
      *
     C                   EXSR      $RGDS
     C                   EXSR      $BODS
     C                   EXSR      $PTDS
     C                   EXSR      $PPDS
      *
     C                   EXCEPT    HRHDR
     C                   MOVEL     NO            FIRST
     C                   ENDIF                                                  IF FIRST
      *
      *----------------------------------------------------------------
      * PROCESS CONTROL BREAKS
      *----------------------------------------------------------------
      *
     C                   SELECT
      * IF NEW REGION
      *
     C     WTRGCD        WHENNE    SVRGCD
      *
     C                   ADD       8             CURLIN
     C     CURLIN        IFGT      OVFLIN
     C                   EXCEPT    HRHDR
     C                   ENDIF
      *
     C                   EXSR      $EXTOT
     C                   EXSR      $ESTOT
     C                   EXSR      $ECTOT
     C                   EXSR      $FSTOT
     C                   EXSR      $PPTOT
     C                   EXSR      $PTTOT
     C                   EXSR      $BOTOT
     C                   EXSR      $RGTOT
      *
     C                   EXCEPT    HRHDR
      *
      *----------------------------------------------------------------
      * IF NEW BUSINESS OFFICE (ALLOCATED B/O---NOT ORIGINATING B/O)
      *
     C     WTALBO        WHENNE    SVALBO
      *
     C                   ADD       7             CURLIN
     C     CURLIN        IFGT      OVFLIN
     C                   EXCEPT    HRHDR
     C                   ENDIF
      *
     C                   EXSR      $EXTOT
     C                   EXSR      $ESTOT
     C                   EXSR      $ECTOT
     C                   EXSR      $FSTOT
     C                   EXSR      $PPTOT
     C                   EXSR      $PTTOT
     C                   EXSR      $BOTOT
      *
     C                   EXCEPT    HRHDR
      *
      *----------------------------------------------------------------
      * IF NEW PRODUCTION TYPE
      *
     C     WTPTCD        WHENNE    SVPTCD
      *
     C                   ADD       6             CURLIN
     C     CURLIN        IFGT      OVFLIN
     C                   EXCEPT    HRHDR
     C                   ENDIF
      *
     C                   EXSR      $EXTOT
     C                   EXSR      $ESTOT
     C                   EXSR      $ECTOT
     C                   EXSR      $FSTOT
     C                   EXSR      $PPTOT
     C                   EXSR      $PTTOT
      *
     C                   EXCEPT    HRHDR
      *
      *----------------------------------------------------------------
      * IF NEW PRODUCTION PHASE
      *
     C     WTPPCD        WHENNE    SVPPCD
      *
     C                   ADD       5             CURLIN
     C     CURLIN        IFGT      OVFLIN
     C                   EXCEPT    HRHDR
     C                   ENDIF
      *
     C                   EXSR      $EXTOT
     C                   EXSR      $ESTOT
     C                   EXSR      $ECTOT
     C                   EXSR      $FSTOT
     C                   EXSR      $PPTOT
      *
     C                   EXCEPT    HRHDR
      *
      *----------------------------------------------------------------
      * IF NEW FARM SITE
      *
     C     WTFSCD        WHENNE    SVFSCD
      *
     C                   ADD       4             CURLIN
     C     CURLIN        IFGT      OVFLIN
     C                   EXCEPT    HRHDR
     C                   ENDIF
      *
     C                   EXSR      $EXTOT
     C                   EXSR      $ESTOT
     C                   EXSR      $ECTOT
     C                   EXSR      $FSTOT
      *
      *----------------------------------------------------------------
      * IF NEW EXPENSE CLASS
      *
     C     WTECCD        WHENNE    SVECCD
      *
     C                   ADD       3             CURLIN
     C     CURLIN        IFGT      OVFLIN
     C                   EXCEPT    HRHDR
     C                   ENDIF
      *
     C                   EXSR      $EXTOT
     C                   EXSR      $ESTOT
     C                   EXSR      $ECTOT
      *
      *----------------------------------------------------------------
      * IF NEW EXPENSE SUBCLASS
      *
     C     WTESCD        WHENNE    SVESCD
      *
     C                   ADD       2             CURLIN
     C     CURLIN        IFGT      OVFLIN
     C                   EXCEPT    HRHDR
     C                   ENDIF
      *
     C                   EXSR      $EXTOT
     C                   EXSR      $ESTOT
      *
      *----------------------------------------------------------------
      * IF NEW EXPENSE CODE
      *
     C     WTEXCD        WHENNE    SVEXCD
      *
     C                   ADD       2             CURLIN
     C     CURLIN        IFGT      OVFLIN
     C                   EXCEPT    HRHDR
     C                   ENDIF
      *
     C                   EXSR      $EXTOT
      *
     C                   ENDSL
      *
      *----------------------------------------------------------------
      * DETAIL PROCESSING LOGIC
      *----------------------------------------------------------------
      *
      * CONVERT TICKET BEGIN DATE TO MMDDYY
      *
     C                   Z-ADD     WTXBDT        PFRM8
     C                   EXSR      $DATE
     C                   Z-ADD     PTO8          DTBMDY
      *
      * ACCUMULATE TOTALS
      *
     C                   ADD       WTXTLB        LBS
     C                   ADD       WTXTAM        AMT
     C                   ADD       WTXTQT        QTY
     C                   ADD       WTXTHD        HED
      *
      * IF OVERFLOW, PRINT HEADINGS
      *
     C     *INOF         IFEQ      *ON
     C                   EXCEPT    HRHDR
     C                   ENDIF
      *
     C                   EXCEPT    DTLINE
     C                   ENDIF                                                  IF NOT EOF
     C                   ENDDO                                                  MAIN DO
      *
      /EJECT
      *----------------------------------------------------------------
      * EOF PROCESSING
      *----------------------------------------------------------------
      *
      *  IF NO DETAIL RECORDS MATCHED THE SELECTION CRITERIA, THEN
      *  PRINT THE STANDARD 'NO DATA...' ERROR MESSAGE
      *
     C     FIRST         IFEQ      YES
     C                   EXCEPT    HRHDR
     C                   EXCEPT    NODATA
     C                   ELSE
      *
      * PRINT TOTALS
      *
     C                   ADD       8             CURLIN
     C     CURLIN        IFGT      OVFLIN
     C                   EXCEPT    HRHDR
     C                   ENDIF
      *
     C                   EXCEPT    EXTOT
     C                   EXCEPT    ESTOT
     C                   EXCEPT    ECTOT
     C                   EXCEPT    TFTOT
     C                   EXCEPT    TPTOT
     C                   EXCEPT    TTTOT
     C                   EXCEPT    TBTOT
     C                   EXCEPT    RGTOT
     C                   EXCEPT    TRTOT
     C                   ENDIF
      *
     C                   SETON                                        LR
      /EJECT
      *****************************************************************
      * $RGDS - RETRIEVE REGION DESCRIPTION
      *****************************************************************
      *
     C     $RGDS         BEGSR
      *
     C     SVRGCD        IFEQ      *BLANK
     C                   MOVEL(P)  NORG          SVRGDS
     C                   ELSE
     C     SVRGCD        CHAIN     HSP008                             96
     C     *IN96         IFEQ      *OFF                                         IF FOUND
     C                   MOVEL(P)  RGRGDS        SVRGDS
     C                   ELSE
     C                   MOVEL(P)  'UNKNOWN'     SVRGDS
     C                   ENDIF                                                  IF FOUND
     C                   ENDIF
      *
     C                   ENDSR
      /EJECT
      *****************************************************************
      * $BODS - RETRIEVE THE BUSINESS OFFICE DESCRIPTION
      *****************************************************************
      *
     C     $BODS         BEGSR
      *
     C     SVALBO        IFEQ      *BLANK
     C                   MOVEL(P)  NOBO          SVBODS
     C                   ELSE
     C     SVALBO        CHAIN     HSP009                             91
     C     *IN91         IFEQ      *OFF                                         IF FOUND
     C                   MOVEL(P)  BOBODS        SVBODS
     C                   ELSE
     C                   MOVEL(P)  'UNKNOWN'     SVBODS
     C                   ENDIF                                                  IF FOUND
     C                   ENDIF
      *
     C                   ENDSR
      /EJECT
      *****************************************************************
      * $PTDS - RETRIEVE PRODUCTION TYPE DESCRIPTION
      *****************************************************************
      *
     C     $PTDS         BEGSR
      *
     C     SVPTCD        IFEQ      *BLANK
     C                   MOVEL(P)  'NO TYPE'     SVPTDS
     C                   ELSE
     C     SVPTCD        CHAIN     HSP010                             91
     C     *IN91         IFEQ      *OFF                                         IF FOUND
     C                   MOVEL(P)  PTPTDS        SVPTDS
     C                   ELSE
     C                   MOVEL(P)  'UNKNOWN'     SVPTDS
     C                   ENDIF                                                  IF FOUND
     C                   ENDIF
      *
     C                   ENDSR
      /EJECT
      *****************************************************************
      * $PPDS - RETRIEVE PRODUCTION PHASE DESCRIPTION
      *****************************************************************
      *
     C     $PPDS         BEGSR
      *
     C     SVPPCD        IFEQ      *BLANK
     C                   MOVEL(P)  'NO PHASE'    SVPPDS
     C                   ELSE
     C     SVPPCD        CHAIN     HSP011                             91
     C     *IN91         IFEQ      *OFF                                         IF FOUND
     C                   MOVEL(P)  PPPPDS        SVPPDS
     C                   ELSE
     C                   MOVEL(P)  'UNKNOWN'     SVPPDS
     C                   ENDIF                                                  IF FOUND
     C                   ENDIF
      *
     C                   ENDSR
      /EJECT
      *****************************************************************
      * $RGTOT - REGION TOTALS
      *****************************************************************
      *
     C     $RGTOT        BEGSR
      *
     C                   EXCEPT    RGTOT
      *
     C     WTRGCD        IFNE      SVRGCD
     C                   MOVEL     WTRGCD        SVRGCD
     C                   EXSR      $RGDS
     C                   ENDIF
      *
     C                   ENDSR
      *
      *****************************************************************
      * $BOTOT - BUSINESS OFFICE TOTALS
      *****************************************************************
      *
     C     $BOTOT        BEGSR
      *
     C                   EXCEPT    TBTOT
      *
     C     WTALBO        IFNE      SVALBO
     C                   MOVEL     WTALBO        SVALBO
     C                   EXSR      $BODS
     C                   ENDIF
      *
     C                   ENDSR
      /EJECT
      *****************************************************************
      * $PTTOT - PRODUCTION TYPE TOTALS
      *****************************************************************
      *
     C     $PTTOT        BEGSR
      *
     C                   EXCEPT    TTTOT
      *
     C     WTPTCD        IFNE      SVPTCD
     C                   MOVEL     WTPTCD        SVPTCD
     C                   EXSR      $PTDS
     C                   ENDIF
      *
     C                   ENDSR
      *
      *****************************************************************
      * $PPTOT - PRODUCTION PHASE TOTALS
      *****************************************************************
      *
     C     $PPTOT        BEGSR
      *
     C                   EXCEPT    TPTOT
      *
     C     WTPPCD        IFNE      SVPPCD
     C                   MOVEL     WTPPCD        SVPPCD
     C                   EXSR      $PPDS
     C                   ENDIF
      *
     C                   ENDSR
      *
      *****************************************************************
      *  $FSTOT - FARM SITE TOTALS
      *****************************************************************
      *
     C     $FSTOT        BEGSR
      *
     C                   EXCEPT    TFTOT
     C                   Z-ADD     WTFSCD        SVFSCD
      *
     C                   ENDSR
      /EJECT
      *****************************************************************
      * $EXTOT - EXPENSE CODE TOTALS
      *****************************************************************
      *
     C     $EXTOT        BEGSR
      *
     C                   EXCEPT    EXTOT
     C                   MOVEL     WTEXCD        SVEXCD
      *
     C                   ENDSR
      *
      *****************************************************************
      * $ESTOT - EXPENSE SUBCLASS TOTALS
      *****************************************************************
      *
     C     $ESTOT        BEGSR
      *
     C                   EXCEPT    ESTOT
     C                   MOVEL     WTESCD        SVESCD
      *
     C                   ENDSR
      *
      *****************************************************************
      * $ECTOT - EXPENSE CLASS TOTALS
      *****************************************************************
      *
     C     $ECTOT        BEGSR
      *
     C                   EXCEPT    ECTOT
     C                   MOVEL     WTECCD        SVECCD
      *
     C                   ENDSR
      /EJECT
      ****************************************************************
      * $DATE -  MANIPULATE DATES WITH DATE UTILITY
      ****************************************************************
      *
     C     $DATE         BEGSR
      *
     C                   Z-ADD     0             PTO8
     C                   MOVE      *BLANKS       PFRFMT
     C                   MOVEL     'YMD '        PFRFMT
     C                   MOVE      *BLANKS       PTOFMT
     C                   MOVEL     'MDY '        PTOFMT
      *
     C                   CALL      'UT80060R'
     C                   PARM                    PFRM8             8 0
     C                   PARM                    PTO8              8 0
     C                   PARM                    PFRFMT            4
     C                   PARM                    PTOFMT            4
     C                   PARM                    PDAYS             4 0
     C                   PARM                    PCODE             1
     C                   PARM                    PRTRN             1
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * *INZSR - INITIALIZATION SUBROUTINE
      *---------------------------------------------------------------
      *
     C     *INZSR        BEGSR
      *
      * DEFINE SAVE FIELDS FOR CONTROL BREAKS
      *
     C     *LIKE         DEFINE    WTRGCD        SVRGCD
     C     *LIKE         DEFINE    RGRGDS        SVRGDS
     C     *LIKE         DEFINE    WTALBO        SVALBO
     C     *LIKE         DEFINE    BOBODS        SVBODS
     C     *LIKE         DEFINE    WTPTCD        SVPTCD
     C     *LIKE         DEFINE    PTPTDS        SVPTDS
     C     *LIKE         DEFINE    WTPPCD        SVPPCD
     C     *LIKE         DEFINE    PPPPDS        SVPPDS
     C     *LIKE         DEFINE    WTFSCD        SVFSCD
     C     *LIKE         DEFINE    WTECCD        SVECCD
     C     *LIKE         DEFINE    WTESCD        SVESCD
     C     *LIKE         DEFINE    WTEXCD        SVEXCD
      *
      * DEFINE WORK FIELDS USED IN PRINTING DETAIL LINES
      *
     C                   Z-ADD     0             DTBMDY            6 0
      *
      * SET UP REPORT HEADING FIELDS FOR USER'S SELECTIONS
      *
     C     LDRGCD        IFEQ      *BLANK
     C                   MOVEL(P)  'ALL'         LDRGCD
     C                   ENDIF
      *
     C     LDALBO        IFEQ      *BLANK
     C                   MOVEL(P)  'ALL'         LDALBO
     C                   ENDIF
      *
     C     LDPPCD        IFEQ      *BLANK
     C                   MOVEL(P)  'ALL'         LDPPCD
     C                   ENDIF
      *
     C     LDFSCD        IFEQ      0
     C                   SETON                                        89
     C                   ENDIF
      *
     C     LDHGCD        IFEQ      *BLANK
     C                   MOVEL(P)  'ALL'         LDHGCD
     C                   ENDIF
      *
     C     LDECCD        IFEQ      *BLANK
     C                   MOVEL(P)  'ALL'         LDECCD
     C                   ENDIF
      *
     C     LDESCD        IFEQ      *BLANK
     C                   MOVEL(P)  'ALL'         LDESCD
     C                   ENDIF
      *
     C     LDEXCD        IFEQ      *BLANK
     C                   MOVEL(P)  'ALL'         LDEXCD
     C                   ENDIF
      *
     C     LDCJD         IFEQ      *BLANK
     C                   MOVEL(P)  'ALL'         LDCJD
     C                   ENDIF
      *
      * SET UP A FIRST TIME FLAG
      * SET UP A DASHED LINE FOR UNDERLINING THE HEADING
      * RETRIEVE TIME FOR REPORT HEADING
      *
     C                   MOVEL     YES           FIRST             1
     C                   MOVEL     *ALL'-'       DASH            132
     C                   TIME                    RTIME             6 0
      *
     C                   ENDSR
      /EJECT
      *-------------------------------------------------------------
      * REPORT HEADING LINES
      *-------------------------------------------------------------
      *
     OQPRINT    E            HRHDR          1 01
     O                       SDPGM               10
     O                                           78 'HOG PRODUCTION SYSTEM'
     O                                          122 'DATE'
     O                       UDATE         Y    132
      *
     O          E            HRHDR          1
     O                       SDUSR               10
     O                                           78 'EXPENSE TICKET REPORT'
     O                                          122 'TIME'
     O                       RTIME              132 '  :  :  '
      *
     O          E            HRHDR          1
     O                       SVBODS             110
     O                                          122 'PAGE'
     O                       PAGE          Z    132
      *
     O          E            HRHDR          1
     O                                           22 'FROM BEGIN DATE ......'
     O                       LDFMDY        Y     32
     O                       SVRGDS             100
      *
     O          E            HRHDR          1
     O                                           22 'TO BEGIN DATE ........'
     O                       LDTMDY        Y     32
     O                                           93 'REGION ...............'
     O                       LDRGCD             100
     O                       LDRGDS             123
      *
     O          E            HRHDR          1
     O                                           22 'BUSINESS OFFICE ......'
     O                       LDALBO              29
     O                       LDBODS              63
     O                                           93 'EXPENSE CLASS ........'
     O                       LDECCD             100
     O                       LDECDS             123
      *
     O          E            HRHDR          1
     O                                           22 'PRODUCTION PHASE .....'
     O                       LDPPCD              29
     O                       LDPPDS              63
     O                                           93 'EXPENSE SUBCLASS .....'
     O                       LDESCD             100
     O                       LDESDS             123
      *
     O          E            HRHDR          1
     O                                           22 'FARM SITE ............'
     O              N89      LDFSCD        M     30
     O               89                          27 'ALL'
     O              N89      LDFSNM              58
     O                                           93 'EXPENSE CODE .........'
     O                       LDEXCD             100
     O                       LDEXDS             123
     O*
     O          E            HRHDR          2
     O                                           22 'HOG GROUP ............'
     O                       LDHGCD              31
     O                                           93 'COST CENTER ..........'
     O                       LDCJD              107
      *
      *-------------------------------------------------------------
      * COLUMN HEADING LINES
      *-------------------------------------------------------------
      *
     O          E            HRHDR          1
     O                                            5 'FARM'
     O                                           14 'TICKET'
     O                                           23 'BEGIN'
     O                                           31 'ALLOC'
     O                                           40 'TICKET'
     O                                           46 'HOG'
     O                                           69 '----- EXPENSE -----'
      *
     O          E            HRHDR          1
     O                                            5 'SITE'
     O                                           14 'NUMBER'
     O                                           23 'DATE'
     O                                           32 'METHOD'
     O                                           40 'STATUS'
     O                                           47 'GROUP'
     O                                           55 'CLASS'
     O                                           64 'SUBCLASS'
     O                                           69 'CODE'
     O                                           85 'AMOUNT'
     O                                          100 'HEAD'
     O                                          117 'POUNDS'
     O                                          131 'QUANTITY'
      *
     O          E            HRHDR          1
     O                       DASH               132
      *
      *-------------------------------------------------------------
      * PRODUCTION TYPE/PHASE HEADING
      *-------------------------------------------------------------
      *
     O          E            HRHDR          1
     O                       SVPTDS              10
     O                                           12 '-'
     O                       SVPPDS              43
      *
      *-------------------------------------------------------------
      * EXPENSE TICKET DETAIL LINE
      *-------------------------------------------------------------
      *
     O          E            DTLINE      1
     O                       WTFSCD        M      6
     O                       WTXTSN        M     15
     O                       DTBMDY        Y     24
     O                       WTAMCD              31
     O                       WTXSCD              38
     O                       WTHGCD              48
     O                       WTECCD              55
     O                       WTESCD              62
     O                       WTEXCD              69
      *
     O                       WTXTAM        K     86
     O                       WTXTHD        K    100
     O                       WTXTLB        K    117
     O                       WTXTQT        K    132
      *
      *-------------------------------------------------------------
      * EXPENSE CODE SUBTOTAL LINE
      *-------------------------------------------------------------
      *
     O          E            EXTOT       2  1
     O                                           58 'EXPENSE CODE TOTAL'
     O                                           63 '*'
     O                       SVEXCD              69
     O                       AMT(1)        KB    86
     O                       HED(1)        KB   100
     O                       LBS(1)        KB   117
     O                       QTY(1)        KB   132
      *
      *-------------------------------------------------------------
      * EXPENSE SUBCLASS SUBTOTAL LINE
      *-------------------------------------------------------------
      *
     O          E            ESTOT          1
     O                                           58 'EXPENSE SUBCLASS TOTAL'
     O                                           63 '**'
     O                       SVESCD              69
     O                       AMT(2)        KB    86
     O                       HED(2)        KB   100
     O                       LBS(2)        KB   117
     O                       QTY(2)        KB   132
      *
      *
      *-------------------------------------------------------------
      * EXPENSE CLASS SUBTOTAL LINE
      *-------------------------------------------------------------
      *
     O          E            ECTOT          1
     O                                           58 'EXPENSE CLASS TOTAL'
     O                                           63 '***'
     O                       SVECCD              69
     O                       AMT(3)        KB    86
     O                       HED(3)        KB   100
     O                       LBS(3)        KB   117
     O                       QTY(3)        KB   132
      *
      *-------------------------------------------------------------
      * FARM SITE TOTAL LINE
      *-------------------------------------------------------------
      *
     O          E            TFTOT          1
     O                                           58 'FARM SITE TOTAL'
     O                                           63 '****'
     O                       SVFSCD        M     69
     O                       AMT(4)        KB    86
     O                       HED(4)        KB   100
     O                       LBS(4)        KB   117
     O                       QTY(4)        KB   132
      *
      *-------------------------------------------------------------
      * PRODUCTION PHASE TOTAL LINE
      *-------------------------------------------------------------
      *
     O          E            TPTOT       1  1
     O                                           63 'PRODUCTION PHASE TOTAL'
     O                       SVPPCD              69
     O                       AMT(5)        KB    86
     O                       HED(5)        KB   100
     O                       LBS(5)        KB   117
     O                       QTY(5)        KB   132
      *
      *-------------------------------------------------------------
      * PRODUCTION TYPE TOTAL LINE
      *-------------------------------------------------------------
      *
     O          E            TTTOT          1
     O                                           63 'PRODUCTION TYPE TOTAL'
     O                       SVPTCD              69
     O                       AMT(6)        KB    86
     O                       HED(6)        KB   100
     O                       LBS(6)        KB   117
     O                       QTY(6)        KB   132
      *
      *-------------------------------------------------------------
      * BUSINESS OFFICE TOTAL LINE
      *-------------------------------------------------------------
      *
     O          E            TBTOT          1
     O                                           63 'BUSINESS OFFICE TOTAL'
     O                       SVALBO              69
     O                       AMT(7)        KB    86
     O                       HED(7)        KB   100
     O                       LBS(7)        KB   117
     O                       QTY(7)        KB   132
      *
      *-------------------------------------------------------------
      * REGION TOTAL LINE
      *-------------------------------------------------------------
      *
     O          E            RGTOT          1
     O                                           63 'REGION TOTAL'
     O                       SVRGCD              69
     O                       AMT(8)        KB    86
     O                       HED(8)        KB   100
     O                       LBS(8)        KB   117
     O                       QTY(8)        KB   132
      *
      *-------------------------------------------------------------
      * REPORT TOTALS
      *-------------------------------------------------------------
      *
     O          E            TRTOT          1
     O                                           63 'REPORT TOTAL'
     O                       AMT(9)        KB    86
     O                       HED(9)        KB   100
     O                       LBS(9)        KB   117
     O                       QTY(9)        KB   132
      *-------------------------------------------------------------
      * NO DATA MESSAGE LINE
      *-------------------------------------------------------------
      *
     O          E            NODATA         1
     O                                           19 'NO DATA MEETS YOUR'
     O                                           38 'SELECTION CRITERIA'
