      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      HOG PRODUCTION
      * PROGRAM:     HP657 - DAILY DEATH REPORT BY DATE
      * PROGRAMMER:  LEANNE FEDOR
      * CREATED:     04/05/95
      *
      * FUNCTION:    PRINTS THE DEATHS FOR GROUPS SEQUENCED BY DATE.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 09/01/99  LEANNE FEDOR
      *           CHANGED HEADING LOGIC. BUSINESS OFFICE IS NOW AN
      *           OPTIONAL REPORT SELECTION.
      *
      * 11/01/95  LEANNE FEDOR
      *           RECOMPILE ONLY. NEW FIELD 'GROUP TYPE' ADDED TO
      *           HOG GROUP FILE.
      *
      * 11/03/99  LeAnne Fedor
      *           Added building/room to report.
      *
      * 06/28/01  LeAnne Fedor
      *           Recompile only.
      *           Manager and supervisor fields removed from Hog Group file.
      *
      * 09/06/01  LeAnne Fedor
      *           'Group type' added as a new report option. Also, new 'square feet'
      *           fields added to Farm Site file requiring a recompile.
      *
      * 12/02/02  LeAnne Fedor
      *           'Production Phase' and 'Production Type' were removed from the Killed/Dead
      *           file. So, logic was changed here to use the values from the Hog Group Master.
      *
      * 07/06/09  LeAnne Ramsey
      *           Recompile only. Added new field 'Continuous Flow Flag' to Hog Group file.
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     FHSP010    IF   E           K DISK
      *    PRODUCTION TYPE
      *
      *
     FHSP011    IF   E           K DISK
      *    PRODUCTION PHASE
      *
      *
     FHSP041    IF   E           K DISK
      *    DEATH/DISEASE REASONS
      *
      *
     FHSJ068A   IF   E           K DISK
      *    DEAD HOGS + HOG GROUP  (records selected/keyed in OPNQRY)|
      *
      *
     FQPRINT    O    F  132        PRINTER OFLIND(*INOF)
      *
      /EJECT
      ****************************************************************
      * TABLE AND ARRAY SPECIFICATIONS
      ****************************************************************
      *
      *  PRINT ARRAYS FOR TOTALS
      *
     D LBS             S             11  0 DIM(6)
     D HD              S             11  0 DIM(6)
      /EJECT
      ****************************************************************
      * INPUT SPECS
      ****************************************************************
      *
      *---------------------------------------------------------------
      * STANDARD PROGRAM STATUS DATA STRUCTURE
      *---------------------------------------------------------------
      *    EXTERNALLY DEFINED AS UTPGFR (RECORD FORMAT: PGMDSR)
     D PGMDS         ESDS                  EXTNAME(UTPGFR)
      *
      *---------------------------------------------------------------
      *  DATE FORMATS
      *---------------------------------------------------------------
      *
      * DATE IN MMDDYY FORMAT
      *
     D                 DS
     D  WKMDY                  1      6  0
     D  WKMM                   1      2  0
     D  WKDD                   3      4  0
     D  WKYY                   5      6  0
      *
      * DATE IN CCYYMMDD FORMAT
      *
     D                 DS
     D  WKCYMD                 1      8  0
     D  WKCEN                  1      2  0
     D  WKYER                  3      4  0
     D  WKMON                  5      6  0
     D  WKDAY                  7      8  0
      *
      *---------------------------------------------------------------
      *  NAMED CONSTANTS
      *---------------------------------------------------------------
      *
     D YES             C                   CONST('Y')
     D NO              C                   CONST('N')
     D UNKNWN          C                   CONST('UNKNOWN')
     D ALL             C                   CONST('ALL')
      *
      *----------------------------------------------------------------
      * LDA - REPORT SELECTIONS
      *----------------------------------------------------------------
      *
     D                UDS
     D  LDFSBO                 1      5
     D  LDFYMD                 6     13  0
     D  LDTYMD                14     21  0
     D  LDPPCD                22     26
     D  LDFSCD                27     31  0
     D  ldgtcd                47     47
     D  LDPTCD                48     52
     D  LDHGSN                53     59  0
     D  LDFMDY                60     65  0
     D  LDTMDY                66     71  0
      *
     D  LDBODS                72    101
     D  LDPTDS               102    111
     D  LDPPDS               112    141
     D  LDFSNM               142    166
     D  LDHGCD               167    173
     D  LDRPFL               172    172
     D  ldgtds               182    196
      *
     D  LDOUTU               401    410
     D  LDHOLD               411    411
     D  LDCOPY               412    412
     D  LDOUTQ               413    422
      /EJECT
      ****************************************************************
      * MAIN LINE
      ****************************************************************
      *
      * READ AND PRINT ALL DEAD HOGS SELECTED BY OPNQRY
      *
     C     *IN90         DOUEQ     *ON                                          MAIN DO
     C                   READ      HSJ068A                                90
     C     *IN90         IFEQ      *OFF                                         IF NOT EOF
      *
     C     FIRST         IFEQ      YES                                          IF FIRST
     C                   EXSR      $PTDS
     C                   EXSR      $PPDS
     C                   EXSR      $HRHDR
     C                   MOVEL     HGPTCD        SVPTCD
     C                   MOVEL     HGPPCD        SVPPCD
     C                   MOVEL     HGHGCD        SVHGCD
     C                   Z-ADD     KDKDDT        SVKDDT
     C                   Z-ADD     KDFSCD        SVFSCD
     C                   Z-ADD     KDHGSN        SVHGSN
     C                   MOVEL     NO            FIRST
     C                   ENDIF                                                  IF FIRST
      *
      * CHECK FOR CONTROL BREAKS
      *
     C                   SELECT
     C     HGPTCD        WHENNE    SVPTCD                                       PROD TYPE
     C                   EXSR      $HGTOT
     C                   EXSR      $FSTOT
     C                   EXSR      $PPTOT
     C                   EXSR      $PTTOT
     C                   EXSR      $PTDS
     C                   EXSR      $PPDS
     C                   EXSR      $HRHDR
      *
     C     HGPPCD        WHENNE    SVPPCD                                       PROD PHASE
     C                   EXSR      $HGTOT
     C                   EXSR      $FSTOT
     C                   EXSR      $DDTOT
     C                   EXSR      $PPTOT
     C                   EXSR      $PPDS
     C                   EXSR      $HRHDR
      *
     C     KDKDDT        WHENNE    SVKDDT                                       DEAD DATE
     C                   EXSR      $HGTOT
     C                   EXSR      $FSTOT
     C                   EXSR      $DDTOT
     C                   EXCEPT    BLANK
      *
     C     KDFSCD        WHENNE    SVFSCD                                       FARM
     C                   EXSR      $HGTOT
     C                   EXSR      $FSTOT
     C                   EXCEPT    BLANK
      *
     C     KDHGSN        WHENNE    SVHGSN                                       FARM
     C                   EXSR      $HGTOT
     C                   EXCEPT    BLANK
     C                   ENDSL
      *
      * FLIP DEAD DATE TO MDY
      *
     C                   Z-ADD     KDKDDT        WKCYMD
     C                   EXSR      $TOMDY
     C                   Z-ADD     WKMDY         DTKDDT
     C                   Z-ADD     WKMDY         TDMDY
      *
      * RETRIEVE DEATH REASON DESCRIPTION
      *
     C     KDDRCD        CHAIN     HSP041                             91
     C     *IN91         IFEQ      *OFF
     C                   MOVEL     DRDRDS        DTDRDS
     C                   ELSE
     C                   MOVEL(P)  UNKNWN        DTDRDS
     C                   ENDIF
      *
      * ACCUMULATE TOTALS
     C                   ADD       KDKDLB        LBS
     C                   ADD       KDKDHD        HD
      *
     C     *INOF         IFEQ      *ON
     C                   EXSR      $HRHDR
     C                   ENDIF
      *
      * PRINT DETAIL AND SETOFF INDICATOR THAT CONTROLS PRINTING OF
      * FARM SITE CODE
     C                   EXCEPT    DTLINE
     C                   SETOFF                                       8889
      *
     C                   ENDIF                                                  IF NOT EOF
     C                   ENDDO                                                  MAIN DO
      *
      *----------------------------------------------------------------
      * EOF PROCESSING
      *----------------------------------------------------------------
      *  IF NO DETAIL RECORDS MATCHED THE SELECTION CRITERIA, THEN
      *  PRINT THE STANDARD 'NO DATA...' ERROR MESSAGE
      *
     C     FIRST         IFEQ      YES                                          IF NO RECS
     C                   EXCEPT    HRHDR
     C                   EXCEPT    NODATA
     C                   ELSE
      *
     C                   EXSR      $HGTOT
     C                   EXSR      $FSTOT
     C                   EXSR      $DDTOT
     C                   EXSR      $PPTOT
     C                   EXSR      $PTTOT
     C                   EXCEPT    TRTOT
     C                   ENDIF                                                  IF NO RECS
      *
     C                   SETON                                        LR
      /EJECT
      *---------------------------------------------------------------
      * $TOMDY   FLIP DATE FROM YYMMDD TO MMDDYY
      *---------------------------------------------------------------
      *
     C     $TOMDY        BEGSR
      *
     C                   Z-ADD     WKMON         WKMM
     C                   Z-ADD     WKDAY         WKDD
     C                   Z-ADD     WKYER         WKYY
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * $HRHDR - PRINT REPORT HEADINGS
      *---------------------------------------------------------------
      *
     C     $HRHDR        BEGSR
      *
     C                   EXCEPT    HRHDR
     C                   EXCEPT    HTHDR
     C                   SETON                                        8889
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      *  $PTDS - RETRIEVE THE PRODUCTION TYPE DESCRIPTION
      *----------------------------------------------------------------
      *
     C     $PTDS         BEGSR
      *
     C     HGPTCD        CHAIN     HSP010                             91
     C     *IN91         IFEQ      *OFF                                         IF FOUND
     C                   MOVEL     PTPTDS        HTPTDS
     C                   ELSE
     C                   MOVEL(P)  UNKNWN        HTPTDS
     C                   ENDIF                                                  IF FOUND
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $PPDS - RETRIEVE THE PRODUCTION PHASE DESCRIPTION
      *----------------------------------------------------------------
      *
     C     $PPDS         BEGSR
      *
     C     HGPPCD        CHAIN     HSP011                             91
     C     *IN91         IFEQ      *OFF                                         IF FOUND
     C                   MOVEL     PPPPDS        HTPPDS
     C                   ELSE
     C                   MOVEL(P)  UNKNWN        HTPPDS
     C                   ENDIF                                                  IF FOUND
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      *  $PTTOT - PRODUCTION TYPE TOTALS
      *----------------------------------------------------------------
      *
     C     $PTTOT        BEGSR
      *
     C     *INOF         IFEQ      *ON
     C                   EXSR      $HRHDR
     C                   ENDIF
      *
     C                   EXCEPT    TTTOT
     C                   MOVEL     HGPTCD        SVPTCD
      *
     C                   ENDSR
      *
      *----------------------------------------------------------------
      * $PPTOT - PRODUCTION PHASE TOTALS
      *----------------------------------------------------------------
      *
     C     $PPTOT        BEGSR
      *
     C     *INOF         IFEQ      *ON
     C                   EXSR      $HRHDR
     C                   ENDIF
      *
     C                   EXCEPT    TPTOT
     C                   MOVEL     HGPPCD        SVPPCD
      *
     C                   ENDSR
      *
      *----------------------------------------------------------------
      * $DDTOT - TOTALS FOR A DEAD DATE
      *----------------------------------------------------------------
      *
     C     $DDTOT        BEGSR
      *
     C     *INOF         IFEQ      *ON
     C                   EXSR      $HRHDR
     C                   ENDIF
      *
     C                   EXCEPT    TDTOT
     C                   Z-ADD     KDKDDT        SVKDDT
     C                   SETON                                        89
      *
     C                   ENDSR
      *
      *----------------------------------------------------------------
      * $FSTOT - FARM SITE TOTALS
      *----------------------------------------------------------------
      *
     C     $FSTOT        BEGSR
      *
     C     *INOF         IFEQ      *ON
     C                   EXSR      $HRHDR
     C                   ENDIF
      *
     C                   EXCEPT    TFTOT
     C                   Z-ADD     KDFSCD        SVFSCD
     C                   SETON                                        88
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $HGTOT - HOG GROUP TOTALS
      *----------------------------------------------------------------
      *
     C     $HGTOT        BEGSR
      *
     C                   EXCEPT    UNDER
      *
     C     *INOF         IFEQ      *ON
     C                   EXSR      $HRHDR
     C                   ENDIF
      *
     C                   EXCEPT    TGTOT
      *
     C                   MOVEL     KDHGSN        SVHGSN
     C                   MOVEL     HGHGCD        SVHGCD
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * *INZSR - INITIALIZATION SUBROUTINE
      *---------------------------------------------------------------
      *
     C     *INZSR        BEGSR
      *
      * DEFINE PRINT FIELDS FOR PRODUCTION TYPE HEADING
      *
     C     *LIKE         DEFINE    PTPTDS        HTPTDS
     C     *LIKE         DEFINE    PPPPDS        HTPPDS
      *
      * DEFINE PRINT FIELDS FOR DETAIL LINE
      *
     C                   Z-ADD     0             DTKDDT            6 0
     C     *LIKE         DEFINE    DRDRDS        DTDRDS
      *
      * DEFINE PRINT FIELDS FOR DATE TOTAL LINE
      *
     C                   Z-ADD     0             TDMDY             6 0
      *
      * DEFINE SAVE FIELDS FOR CONTROL BREAKS
      *
     C     *LIKE         DEFINE    HGPTCD        SVPTCD
     C     *LIKE         DEFINE    HGPPCD        SVPPCD
     C     *LIKE         DEFINE    HGHGCD        SVHGCD
     C     *LIKE         DEFINE    KDFSCD        SVFSCD
     C     *LIKE         DEFINE    KDKDDT        SVKDDT
     C     *LIKE         DEFINE    KDHGSN        SVHGSN
      *
      *----------------------------------------------------------------
      * SET UP REPORT HEADINGS
      *----------------------------------------------------------------
      *
      * BUSINESS OFFICE
      *
     C     LDFSBO        IFEQ      *BLANK
     C                   MOVEL     ALL           LDFSBO
     C                   ENDIF                                                  IF ALL TYPE
      *
      * PRODUCTION TYPE
      *
     C     LDPTCD        IFEQ      *BLANK
     C                   MOVEL     ALL           LDPTCD
     C                   ENDIF
      *
      * PRODUCTION PHASE
      *
     C     LDPPCD        IFEQ      *BLANK
     C                   MOVEL     ALL           LDPPCD
     C                   ENDIF
      *
      *  FARM SITE
      *
     C     LDFSCD        IFEQ      0                                            IF ALL FARM
     C                   SETON                                        81
     C                   ENDIF                                                  IF ALL FARM
      *
      * DETERMINE IF USER SELECTED SPECIFIC HOG GROUP OR "ALL"
      *
     C     LDHGCD        IFEQ      *BLANK                                       IF ALL GROUP
     C                   MOVEL     ALL           LDHGCD
     C                   ENDIF                                                  IF ALL GROUP
      *
      * Group type
      *
     C                   if        ldgtcd = *blank
     C                   seton                                        82
     C                   endif
      *
      * SET UP HEADING UNDERLINE FIELD FOR PRINT
      * RETRIEVE TIME FOR REPORT HEADING
      * SET UP A FIRST TIME FLAG
      *
     C                   MOVEL     *ALL'-'       DASH            132
     C                   TIME                    RTIME             6 0
     C                   MOVE      YES           FIRST             1
      *
     C                   ENDSR
      /EJECT
      *-------------------------------------------------------------
      * REPORT HEADING LINES
      *-------------------------------------------------------------
      *
     OQPRINT    E            HRHDR          1 01
     O                       SDPGM               10
     O                                           68 'HOG PRODUCTION SYSTEM'
     O                                          122 'DATE'
     O                       UDATE         Y    132
      *
     O          E            HRHDR          1
     O                       SDUSR               10
     O                                           50 'DAILY'
     O                                           71 'DEATH REPORT BY DATE'
     O                                          122 'TIME'
     O                       RTIME              132 '  :  :  '
      *
     O          E            HRHDR          1
     O                                          122 'PAGE'
     O                       PAGE          Z    132
      *
     O          E            HRHDR          1
     O                                           19 'BUSINESS OFFICE ...'
     O                       LDFSBO              26
     O                       LDBODS              61
      *
     O          E            HRHDR          1
     O                                           19 'FROM DEATH DATE ...'
     O                       LDFMDY        Y     29
      *
     O          E            HRHDR          1
     O                                           19 'TO DEATH DATE .....'
     O                       LDTMDY        Y     29
      *
     O          E            HRHDR          1
     O                                           19 'PRODUCTION TYPE ...'
     O                       LDPTCD              26
     O                       LDPTDS              41
      *
     O          E            HRHDR          1
     O                                           19 'PRODUCTION PHASE ..'
     O                       LDPPCD              26
     O                       LDPPDS              61
      *
      *
     O          e            hrhdr          1
     O                                           19 'GROUP TYPE ........'
     O              n82      ldgtcd              22
     O               82                          24 'ALL'
     O                       ldgtds              46
      *
      *
     O          E            HRHDR          1
     O                                           19 'FARM SITE .........'
     O              N81      LDFSCD        M     27
     O               81                          24 'ALL'
     O                       LDFSNM              56
      *
     O          E            HRHDR          2
     O                                           19 'HOG GROUP .........'
     O                       LDHGCD              28
      *
      *-------------------------------------------------------------
      * COLUMN HEADING LINES
      *-------------------------------------------------------------
      *
     O          E            HRHDR          1
     O                                            7 'DEATH'
     O                                           15 'FARM'
     O                                           69 'DEAD'
     O                                           85 'DEAD'
     O                                          122 '------- DEATH REASON'
     O                                         +  1 ' -------'
      *
     O          E            HRHDR          1
     O                                            6 'DATE'
     O                                           15 'SITE'
     O                                           23 'GROUP'
     O                                           32 'BLDG'
     O                                           39 'ROOM'
     O                                           69 'HEAD'
     O                                           85 'POUNDS'
     O                                           99 'DESTROYED'
     O                                          106 'CODE'
     O                                          120 'DESCRIPTION'
      *
     O          E            HRHDR          1
     O                       DASH               132
      *
      *-------------------------------------------------------------
      * PRODUCTION TYPE / PRODUCTION PHASE HEADING
      *-------------------------------------------------------------
      *
     O          E            HTHDR       1  2
     O                       HTPTDS              18
     O                                           20 '-'
     O                       HTPPDS              51
      *
      *-------------------------------------------------------------
      * DEAD HOG DETAIL LINE
      *-------------------------------------------------------------
      *
     O          E            DTLINE         1
     O               89      DTKDDT        YB     8
     O               88      KDFSCD        MB    16
     O                       HGHGCD         B    25
     O                       HGBLCD         B    33
     O                       HGRMCD         B    40
     O                       KDKDHD        KB    70
     O                       KDKDLB        KB    86
     O                       KDDSFL         B    95
     O                       KDDRCD         B   107
     O                       DTDRDS         B   129
      *
      *-------------------------------------------------------------
      * HOG GROUP TOTAL LINE
      *-------------------------------------------------------------
      *
     O          E            TGTOT          1
     O                                           46 '*'
     O                       SVHGCD              54
     O                       HD(1)         KB    70
     O                       LBS(1)        KB    86
     O                                           87 '*'
      *
      *-------------------------------------------------------------
      * FARM SITE TOTAL LINE
      *-------------------------------------------------------------
      *
     O          E            TFTOT          1
     O                                           46 '**'
     O                       SVFSCD        M     53
     O                       HD(2)         KB    70
     O                       LBS(2)        KB    86
     O                                           88 '**'
      *
      *-------------------------------------------------------------
      * PRODUCTION PHASE TOTAL LINE
      *-------------------------------------------------------------
      *
     O          E            TPTOT          1
     O                                           46 '****'
     O                       SVPPCD              52
     O                       HD(3)         KB    70
     O                       LBS(3)        KB    86
     O                                           90 '****'
      *
      *-------------------------------------------------------------
      * PRODUCTION TYPE TOTAL LINE
      *-------------------------------------------------------------
      *
     O          E            TTTOT          1
     O                                           46 '*****'
     O                       SVPTCD              52
     O                       HD(4)         KB    70
     O                       LBS(4)        KB    86
     O                                           91 '*****'
      *
      *-------------------------------------------------------------
      * REPORT TOTALS
      *-------------------------------------------------------------
      *
     O          E            TRTOT          1
     O                                           54 'REPORT TOTAL'
     O                       HD(5)         KB    70
     O                       LBS(5)        KB    86
      *
      *-------------------------------------------------------------
      * TOTAL FOR A DEAD DATE
      *-------------------------------------------------------------
      *
     O          E            TDTOT          1
     O                                           46 '***'
     O                       TDMDY         Y     55
     O                       HD(6)         KB    70
     O                       LBS(6)        KB    86
     O                                           89 '***'
      *
      *-------------------------------------------------------------
      * PRINT A BLANK LINE
      *-------------------------------------------------------------
      *
     O          E            BLANK          1
      *
      *-------------------------------------------------------------
      * PRINT UNDERSCORE
      *-------------------------------------------------------------
      *
     O          E            UNDER       0  1
     O                                           69 '--------------'
     O                                           85 '--------------'
      *
      *-------------------------------------------------------------
      * NO DATA MESSAGE LINE
      *-------------------------------------------------------------
      *
     O          E            NODATA         1
     O                                           19 'NO DATA MEETS YOUR'
     O                                           38 'SELECTION CRITERIA'
