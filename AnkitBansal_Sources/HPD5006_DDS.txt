     A*%%TS  SD  19971230  081456  ISLRAMS     REL-V3R7M0  5716-PW1
     A*****************************************************************
     A*
     A*  SYSTEM:        HOG PRODUCTION
     A*  DISPLAY FILE:  HPD5006 - SELECT COMPANY
     A*  CREATED BY:    LEANNE FEDOR
     A*  DATE:          12/30/97
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      PRINT
     A                                      CA12(12 'PREVIOUS')
     A N82                                  ROLLUP(25 'PAGE FORWARD')
     A*----------------------------------------------------------------
     A* REFERENCE WINDOW RECORD FORMAT 1
     A*----------------------------------------------------------------
     A          R HP5006R1
     A*%%TS  SD  19971230  081456  ISLRAMS     REL-V3R7M0  5716-PW1
     A                                      TEXT('Reference window')
     A                                      WINDOW(*DFT  9 40)
     A                                      WDWBORDER((*DSPATR RI) (*CHAR '    -
     A                                          '))
     A*----------------------------------------------------------------
     A* RECORD FORMAT ONE
     A*----------------------------------------------------------------
     A          R HP5006R2
     A*%%TS  SD  19940315  135528  ISRCENT     REL-V2R2M0  5738-PW1
     A                                      TEXT('Written in RPG pgm to prevent-
     A                                       screen from clearing')
     A                                      CLRL(*NO)
     A                                      OVERLAY
     A*----------------------------------------------------------------
     A* RECORD FORMAT TWO
     A*----------------------------------------------------------------
     A* THIS FORMAT IS NOT WRITTEN IN THE RPG PGM.  IT PREVENTS THE
     A* CALLING_PROGRAM_SCREEN FROM BEING CLEARED.
     A*----------------------------------------------------------------
     A          R HP5006R3
     A*%%TS  SD  19940315  135528  ISRCENT     REL-V2R2M0  5738-PW1
     A                                      TEXT('Dummy format not written in R-
     A                                      PG pgm.')
     A                                      ASSUME
     A                                 11  1' '
     A*----------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*----------------------------------------------------------------
     A          R HP5006S1                  SFL
     A*%%TS  SD  19940331  151719  ISLRAMS     REL-V2R2M0  5738-PW1
     A                                      TEXT('subfile record format one')
     A            HDRRN1         4S 0H
     A            DFCOCD    R        O  5  1REFFLD(COREC/COCOCD *LIBL/HSP062)
     A            DFCONM    R        O  5  8REFFLD(COREC/COCONM *LIBL/HSP062)
     A*----------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*----------------------------------------------------------------
     A          R HP5006C1                  SFLCTL(HP5006S1)
     A*%%TS  SD  19971230  081316  ISLRAMS     REL-V3R7M0  5716-PW1
     A                                      SFLSIZ(0010)
     A                                      SFLPAG(0002)
     A                                      WINDOW(HP5006R1)
     A                                      TEXT('control format one')
     A                                      OVERLAY
     A                                      SFLCSRRRN(&HDRRN)
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND(*MORE)
     A                                      RMVWDW
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            HDRRN          5S 0H
     A                                  1 12'SELECT COMPANY'
     A                                      DSPATR(HI)
     A                                  2  1'Position Cursor, Press Enter'
     A                                  4  1'Code'
     A                                      DSPATR(HI)
     A                                  4  8'Name'
     A                                      DSPATR(HI)
     A          R HP5006K1
     A*%%TS  SD  19940316  131942  ISRCENT     REL-V2R2M0  5738-PW1
     A                                      WINDOW(HP5006R1)
     A                                      OVERLAY
     A                                  8  1'F12=Previous'
