/***************************************************************** */
/*                                                                 */
/*  SYSTEM:          TRIUMPH FOODS                                 */
/*  PROGRAM NUMBER:  TF4120CL                                      */
/*  PROGRAM NAME:    SPECIFY CLAIM ADJUSTMENTS LISTING OPTIONS     */
/*  PROGRAMMER:      LEANNE FEDOR                                  */
/*  CREATE DATE:     08/16/05                                      */
/*                                                                 */
/*******************************************************************/
/* MODIFIED:                                                       */
/*                                                                     */
/* 05/07/09  LEANNE RAMSEY                                             */
/*           CHANGED PRINT LOGIC TO MATCH MEAT COSTING.                */
/*                                                                     */
/* 01/06/11  LEANNE RAMSEY                                             */
/*           WE HAVE A NEW SELECTION FOR CLAIMS FOR AFFILIATED SALES   */
/*           CUSTOMERS: I=INCLUDE ON LISTING                           */
/*                      E=EXCLUDE FROM LISTING                         */
/*                      O=ONLY PRINT THESE ON LISTING                  */
/*                                                                     */
/* 11/28/17  Danny Nguyen  R12011A-Weekly Product Revenue              */
/*           File TFP022 was changed. Added 2 fields:                  */
/*             CLXRPPC  - STF RESPONSIBILITY %                         */
/*             CLXNCLAM - STF NET CLAIM AMT                            */
/*           Recompile only.                                           */
/***********************************************************************/

             PGM

             DCL        VAR(&QDATA) TYPE(*CHAR) LEN(355)
             DCL        VAR(&RCDCNT) TYPE(*DEC) LEN(10 0)

             DCL        VAR(&OUTQ) TYPE(*CHAR) LEN(10)
             DCL        VAR(&HOLD) TYPE(*CHAR) LEN(4)
             DCL        VAR(&SAVE) TYPE(*CHAR) LEN(4)
             DCL        VAR(&COPY) TYPE(*DEC)  LEN(1)

             DCL        VAR(&LDRDFL)   TYPE(*CHAR) LEN(1)
             DCL        VAR(&LDEMAIL)  TYPE(*CHAR) LEN(50)
             DCL        VAR(&LDRPT)    TYPE(*CHAR) LEN(1)
             DCL        VAR(&LDFCYMD)  TYPE(*DEC) LEN(8)
             DCL        VAR(&LDTCYMD)  TYPE(*DEC) LEN(8)
             DCL        VAR(&LDCONO)   TYPE(*DEC) LEN(3)
             DCL        VAR(&LDCLNO)   TYPE(*DEC) LEN(7)
             DCL        VAR(&LDCLORNO) TYPE(*DEC) LEN(7)
             DCL        VAR(&LDMMNO)   TYPE(*DEC) LEN(7)
             DCL        VAR(&LDPRCD)   TYPE(*DEC) LEN(7)
             DCL        VAR(&LDMMTY)   TYPE(*CHAR) LEN(2)
             DCL        VAR(&LDRPFL)   TYPE(*CHAR) LEN(1)
             DCL        VAR(&LDRPDEPT) TYPE(*CHAR) LEN(3)
             DCL        VAR(&LDRSCD)   TYPE(*CHAR) LEN(3)
             DCL        VAR(&LDDSCD)   TYPE(*CHAR) LEN(1)
             DCL        VAR(&LDIEOFL)  TYPE(*CHAR) LEN(1)

             CHGVAR     VAR(&LDRDFL) VALUE(%SST(*LDA 301 1))
             CHGVAR     VAR(&LDRPT)  VALUE(%SST(*LDA 299 1))
             CHGVAR     VAR(&LDFCYMD) VALUE(%SST(*LDA 52 8))
             CHGVAR     VAR(&LDTCYMD) VALUE(%SST(*LDA 60 8))
             CHGVAR     VAR(&LDCONO) VALUE(%SST(*LDA 68 3))
             CHGVAR     VAR(&LDCLNO) VALUE(%SST(*LDA 71 7))
             CHGVAR     VAR(&LDMMNO) VALUE(%SST(*LDA 78 7))
             CHGVAR     VAR(&LDPRCD) VALUE(%SST(*LDA 85 7))
             CHGVAR     VAR(&LDMMTY) VALUE(%SST(*LDA 92 2))
             CHGVAR     VAR(&LDCLORNO) VALUE(%SST(*LDA 94 7))
             CHGVAR     VAR(&LDRPFL) VALUE(%SST(*LDA 101 1))
             CHGVAR     VAR(&LDRPDEPT) VALUE(%SST(*LDA 102 3))
             CHGVAR     VAR(&LDRSCD) VALUE(%SST(*LDA 105 3))
             CHGVAR     VAR(&LDDSCD) VALUE(%SST(*LDA 108 1))
             CHGVAR     VAR(&LDEMAIL) VALUE(%SST(*LDA 302 50))
             CHGVAR     VAR(&LDIEOFL) VALUE(%SST(*LDA 109 1))

/*---------------------------------------------------------------------*/
/* IF DOWNLOADING, CREATE A FILE FOR THE DOWNLOAD                      */
/*---------------------------------------------------------------------*/

             IF         COND(&LDRDFL *EQ 'D') THEN(DO)
             CRTDUPOBJ  OBJ(TFP022) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP) NEWOBJ(TFP022DWL) DATA(*NO)
             ENDDO

/*---------------------------------------------------------------------*/
/* IF PRINTING THE REPORT, SET UP THE PRINT FILE                       */
/*---------------------------------------------------------------------*/

             IF         COND(&LDRDFL *EQ 'R') THEN(DO)

 OUTQ:       CHGVAR     VAR(&OUTQ) VALUE(%SST(*LDA 401 10))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(*JOB))

 HOLD:       CHGVAR     VAR(&HOLD) VALUE(%SST(*LDA 411 4))
             IF         COND(&HOLD *NE '*YES' *AND &HOLD *NE '*NO ') +
                          THEN(CHGVAR VAR(&HOLD) VALUE(*NO))

 COPY:       CHGVAR     VAR(&COPY) VALUE(%SST(*LDA 419 1))
             IF         COND(&COPY = 0) THEN(CHGVAR VAR(&COPY) +
                          VALUE(1))

 SAVE:       CHGVAR     VAR(&SAVE) VALUE(%SST(*LDA 415 4))
             IF         COND(&SAVE *NE '*YES' *AND &SAVE *NE '*NO ') +
                          THEN(CHGVAR VAR(&SAVE) VALUE(*NO))

             OVRPRTF    FILE(PRINT198) OUTQ(&OUTQ) COPIES(&COPY) +
                          HOLD(&HOLD) SAVE(&SAVE)
             ENDDO

/***********************************************************************/
/* EXTRACT THE DATA FROM THE CLAIMS ADJUSTMENT FILE                    */
/***********************************************************************/

             CHGVAR     VAR(&QDATA) VALUE(' ')
             CHGVAR     VAR(%SST(&QDATA 1 12)) VALUE('CLWBDT *NE 0')

/* IF FROM DATE                                                     */
             IF         COND(&LDFCYMD *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 14 15)) VALUE('*AND CLWEDT *GE')
             CHGVAR     VAR(%SST(&QDATA 30 8)) VALUE(&LDFCYMD)
             ENDDO

/* IF TO DATE                                                       */
             IF         COND(&LDTCYMD *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 40 15)) VALUE('*AND CLWEDT *LE')
             CHGVAR     VAR(%SST(&QDATA 56 8)) VALUE(&LDTCYMD)
             ENDDO

/* IF COMPANY                                                       */
             IF         COND(&LDCONO *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 65 15)) VALUE('*AND CLCONO *EQ')
             CHGVAR     VAR(%SST(&QDATA 81 3)) VALUE(&LDCONO)
             ENDDO

/* IF CLAIM NUMBER                                                  */

             IF         COND(&LDCLNO *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 85 15)) VALUE('*AND CLCLNO *EQ')
             CHGVAR     VAR(%SST(&QDATA 101 7)) VALUE(&LDCLNO)
             ENDDO

/* IF MEMO NUMBER                                                   */

             IF         COND(&LDMMNO *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 109 15)) VALUE('*AND CLMMNO *EQ')
             CHGVAR     VAR(%SST(&QDATA 125 7)) VALUE(&LDMMNO)
             ENDDO

/* IF PRODUCT CODE                                                  */

             IF         COND(&LDPRCD *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 133 15)) VALUE('*AND CLPRCD *EQ')
             CHGVAR     VAR(%SST(&QDATA 149 7)) VALUE(&LDPRCD)
             ENDDO

/* IF MEMO TYPE                                                        */

             IF         COND(&LDMMTY *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 157 17)) VALUE('*AND CLMMTY *EQ "')
             CHGVAR     VAR(%SST(&QDATA 174 2)) VALUE(&LDMMTY)
             CHGVAR     VAR(%SST(&QDATA 176 1)) VALUE('"')
             ENDDO

/* IF CLAIM ORDER NUMBER                                               */

             IF         COND(&LDCLORNO *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 178 17)) VALUE('*AND CLCLORNO *EQ')
             CHGVAR     VAR(%SST(&QDATA 196 7)) VALUE(&LDCLORNO)
             ENDDO

/* IF RESPONSIBILITY FLAG                                              */

             IF         COND(&LDRPFL *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 204 17)) VALUE('*AND CLRPFL *EQ "')
             CHGVAR     VAR(%SST(&QDATA 221 1)) VALUE(&LDRPFL)
             CHGVAR     VAR(%SST(&QDATA 222 1)) VALUE('"')
             ENDDO

/* IF RESPONSIBLE DEPARTMENT                                           */

             IF         COND(&LDRPDEPT *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 224 19)) VALUE('*AND CLRPDEPT *EQ "')
             CHGVAR     VAR(%SST(&QDATA 243 3)) VALUE(&LDRPDEPT)
             CHGVAR     VAR(%SST(&QDATA 246 1)) VALUE('"')
             ENDDO

/* IF REASON CODE                                                      */

             IF         COND(&LDRSCD *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 248 17)) VALUE('*AND CLRSCD *EQ "')
             CHGVAR     VAR(%SST(&QDATA 265 3)) VALUE(&LDRSCD)
             CHGVAR     VAR(%SST(&QDATA 268 1)) VALUE('"')
             ENDDO

/* IF DATA SOURCE COCE                                                 */

             IF         COND(&LDDSCD *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 270 17)) VALUE('*AND CLDSCD *EQ "')
             CHGVAR     VAR(%SST(&QDATA 287 1)) VALUE(&LDDSCD)
             CHGVAR     VAR(%SST(&QDATA 288 1)) VALUE('"')
             ENDDO

/* EXCLUDE CLAIMS FROM AFFILIATED SALES                                */

             IF         COND(&LDIEOFL *EQ 'E') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 290 22)) VALUE('*AND +
                          CLCUTYCD *NE "IS"')
             ENDDO

/* ONLY PRINT CLAIMS FOR AFFILIATED SALES CUSTOMERS                    */

             IF         COND(&LDIEOFL *EQ 'O') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 313 22)) VALUE('*AND +
                          CLCUTYCD *EQ "IS"')
             ENDDO


/***********************************************************************/
/* SORTED BY "CLAIM"                                                   */
/***********************************************************************/

             IF         COND(&LDRPT *EQ 'C') THEN(DO)

             OVRDBF     FILE(TFL022C) SHARE(*YES)
             OPNQRYF    FILE((TFL022C)) OPTION(*INP) QRYSLT(&QDATA) +
                          KEYFLD(*FILE) SEQONLY(*YES)

/* DOWNLOAD (Just grab the query data for later.)                       */

             IF         COND(&LDRDFL *EQ 'D') THEN(DO)
             CPYFRMQRYF FROMOPNID(TFL022C) TOFILE(QTEMP/TFP022DWL) +
                          MBROPT(*REPLACE)
             ENDDO

/* REPORT                                                               */
             IF         COND(&LDRDFL *EQ 'R') THEN(DO)
             CALL       PGM(TF612)
             ENDDO

             CLOF       OPNID(TFL022C)
             DLTOVR     FILE(TFL022C)
             ENDDO

/***********************************************************************/
/* SORTED BY "PRODUCT"                                                 */
/***********************************************************************/

             IF         COND(&LDRPT *EQ 'P') THEN(DO)

             OVRDBF     FILE(TFL022A) SHARE(*YES)
             OPNQRYF    FILE((TFL022A)) OPTION(*INP) QRYSLT(&QDATA) +
                          KEYFLD(*FILE) SEQONLY(*YES)

/* DOWNLOAD (Just grab the query data for later)                       */

             IF         COND(&LDRDFL *EQ 'D') THEN(DO)
             CPYFRMQRYF FROMOPNID(TFL022A) TOFILE(QTEMP/TFP022DWL) +
                          MBROPT(*REPLACE)
             ENDDO

/* REPORT                                                               */
             IF         COND(&LDRDFL *EQ 'R') THEN(DO)
             CALL       PGM(TF613)
             ENDDO

             CLOF       OPNID(TFL022A)
             DLTOVR     FILE(TFL022A)
             ENDDO

/***********************************************************************/
/* DOWNLOAD                                                             */
/***********************************************************************/

             IF         COND(&LDRDFL *EQ 'D') THEN(DO)

/* RETRIEVE THE NUMBER OF RECORDS IN THE DOWNLOAD FILE                  */

             RTVMBRD    FILE(TFP022DWL) NBRCURRCD(&RCDCNT)

             IF         COND(&RCDCNT = 0) THEN(DO)
             ESNDMAIL   RECIPIENT(&LDEMAIL) SUBJECT('Claims Adjustment +
                          Listing Download') MSG('Your selections +
                          on TF4120-Specify Claim Adjustments +
                          Listing Options did not extract +
                          any data for the spreadsheet download. +
                          Please change your selections and submit +
                          the Download again.')
             MONMSG     MSGID(CPF0000)
             ENDDO

             IF         COND(&RCDCNT >= 66000) THEN(DO)
             ESNDMAIL   RECIPIENT(&LDEMAIL) SUBJECT('Claims Adjustment +
                          Listing Download') MSG('Your selections +
                          on TF4120-Specify Claim Adjustments +
                          Listing Options extracted over +
                          66,000 records--which is too many records +
                          for the spreadsheet to handle. Change +
                          your selections to extract fewer records +
                          and submit your Download request again.')
             MONMSG     MSGID(CPF0000)
             ENDDO

             IF         COND(&RCDCNT > 0 *AND &RCDCNT < 66000) THEN(DO)
             EXECUTE    SQL('SELECT * FROM TFP022DWL') NBRRCDS(*ALL) +
                          PCFMT(*XLS) RECIPIENT(&LDEMAIL) +
                          EMLMSG('Claim Adjustments Listing') +
                          TEXT('Claim Adjustments Listing')
             MONMSG     MSGID(CPF0000)
             ENDDO

             ENDDO

/*---------------------------------------------------------------------*/
/* END OF PROCESSING                                                   */
/*---------------------------------------------------------------------*/

             DLTOVR     FILE(*ALL)

             ENDPGM

