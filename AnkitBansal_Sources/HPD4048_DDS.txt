     A*%%TS  SD  20021127  081430  ISLRAMS     REL-V5R1M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD4048
     A*  TITLE:         WORK WITH REPORTED WEEKLY ACTIVITY
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  DATE:          09/25/02
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A N99                                  CF04(04 'PROMPT')
     A N99                                  CF05(05 'REFRESH')
     A N99                                  CF06(06 'CREATE')
     A N99                                  CF07(07 'LISTING')
     A N99                                  CF17(17 'PRINT OPTIONS')
     A N99                                  CF20(20 'WRKSPLF')
     A*
     A*
     A*---------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP4048S1                  SFL
     A*%%TS  SD  20021127  081430  ISLRAMS     REL-V5R1M0  5722-WDS
     A  85                                  SFLNXTCHG
     A            HDRRN1         4Y 0H
     A            HDWEDT    R        H      REFFLD(@@WEDT)
     A            HDHGSN    R        H      REFFLD(@@HGSN)
     A            D1OPT          2A  B 11  2CHGINPDFT(UL)
     A  30                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A N30                                  DSPATR(HI)
     A  99                                  DSPATR(PR)
     A            D1CDYR    R        O 11  7REFFLD(HSRREF/@@CDYR *LIBL/HSPREF)
     A                                      EDTCDE(M)
     A            D1CDWK    R        O 11 14REFFLD(HSRREF/@@CDWK *LIBL/HSPREF)
     A                                      EDTCDE(M)
     A            D1WEMDY   R        O 11 22REFFLD(HSRREF/@@DT06 *LIBL/HSPREF)
     A                                      EDTWRD('  /  /  ')
     A            D1CELL    R        O 11 34REFFLD(FSREC/FSCELL *LIBL/HSP018)
     A                                      EDTCDE(M)
     A            D1FSCD    R        O 11 39REFFLD(FSFSCD)
     A                                      EDTCDE(M)
     A            D1BLCD    R        O 11 47REFFLD(HSRREF/@@BLCD *LIBL/HSPREF)
     A            D1RMCD    R        O 11 54REFFLD(HSRREF/@@RMCD *LIBL/HSPREF)
     A            D1HGCD    R        O 11 61REFFLD(HSRREF/@@HGCD *LIBL/HSPREF)
     A            D1GTCD    R        O 11 72REFFLD(HSRREF/@@GTCD *LIBL/HSPREF)
     A*
     A*---------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP4048C1                  SFLCTL(HP4048S1)
     A*%%TS  SD  20021127  081430  ISLRAMS     REL-V5R1M0  5722-WDS
     A                                      SFLSIZ(0050)
     A                                      SFLPAG(0010)
     A N82N99                               ROLLUP(25 'PAGE FORWARD')
     A                                      SETOF(31)
     A                                      SETOF(51)
     A                                      SETOF(52)
     A                                      SETOF(53)
     A                                      RTNCSRLOC(&RCD &FLD)
     A                                      OVERLAY
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 26'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  2 20'WORK WITH REPORTED WEEKLY ACTIVITY'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  4  2'Business office ..'
     A N99                              4 21'?'
     A            C1FSBO    R        B  4 23REFFLD(FSFSBO)
     A  31
     AO 51                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A N31N99                               DSPATR(HI)
     A  99                                  DSPATR(PR)
     A            C1BODS    R        O  4 30REFFLD(BOBODS)
     A N99                              6  2'2=Revise'
     A N99                              6 12'4=Delete'
     A N99                              7 41'?'
     A                                      DSPATR(HI)
     A                                  7 72'?'
     A                                      DSPATR(HI)
     A                                  8  7'-Calendar-'
     A                                      DSPATR(HI)
     A                                  8 20'Week-Ending'
     A                                      DSPATR(HI)
     A                                  8 70'Group'
     A                                      DSPATR(HI)
     A                                  9  2'Opt'
     A                                      DSPATR(HI)
     A                                  9  7'Year'
     A                                      DSPATR(HI)
     A                                  9 13'Week'
     A                                      DSPATR(HI)
     A                                  9 23'Date'
     A                                      DSPATR(HI)
     A                                  9 33'Cell'
     A                                      DSPATR(HI)
     A                                  9 40'Farm'
     A                                      DSPATR(HI)
     A                                  9 47'Bldg'
     A                                      DSPATR(HI)
     A                                  9 54'Room'
     A                                      DSPATR(HI)
     A                                  9 61'Group'
     A                                      DSPATR(HI)
     A                                  9 70'Type'
     A                                      DSPATR(HI)
     A N99        C1CDYR    R        B 10  7REFFLD(HSRREF/@@CDYR *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A                                      EDTCDE(M)
     A N31N51                               DSPATR(PC)
     A N99        C1CDWK    R        B 10 14REFFLD(HSRREF/@@CDWK *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A                                      EDTCDE(M)
     A N99        C1WEMDY   R        B 10 22REFFLD(@@DT06)
     A                                      DSPATR(HI)
     A                                      EDTWRD('  /  /  -')
     A N99        C1CELL    R        B 10 34REFFLD(FSCELL)
     A                                      EDTCDE(M)
     A                                      DSPATR(HI)
     A N99        C1FSCD    R        B 10 39REFFLD(FSFSCD)
     A                                      EDTCDE(M)
     A                                      DSPATR(HI)
     A  52                                  DSPATR(PC)
     A            C1BLCD    R        B 10 47REFFLD(HSRREF/@@BLCD *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A            C1RMCD    R        B 10 54REFFLD(HSRREF/@@RMCD *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A            C1HGCD    R        B 10 61REFFLD(HSRREF/@@HGCD *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A            C1GTCD    R        B 10 72REFFLD(HSRREF/@@GTCD *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A  53                                  DSPATR(PC)
     A*
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R HP4048K1
     A*%%TS  SD  19970618  135159  ISLRAMS     REL-V3R6M0  5716-PW1
     A  43                             21  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 22  2'F3=Exit'
     A N99                             22 11'F4=Prompt'
     A N99                             22 22'F5=Refresh'
     A N99                             22 34'F6=Create'
     A N99                             22 45'F7=Listing'
     A N99                             22 57'F17=Print Options'
     A N99                             23 57'F20=Spool Files'
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *---------------------------------------------------------------
     A          R HP4048ES                  SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE CONTROL FORMAT
      *---------------------------------------------------------------
     A          R HP4048EC                  SFLCTL(HP4048ES)
      *
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
