      *These two lines here to aid in debugging when needed***************
     H debug(*yes)
     H option(*srcstmt:*nodebugio)
      *****************  RPG PROGRAM HEADING  ************************
      * SYSTEM:      Hog Production System
      * PROGRAM:     HP6025
      * TITLE:       Listing of Nur/Fin Targets
      * PROGRAMMER:  Sharon Zurn
      * CREATED:     01/10/07
      *
      *            This listing is submitted from:
      *                HP4025-Specify Nur/Fin Target Listing Options
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 09/19/07  LeAnne Ramsey
      *           Cleaned up code. Also, we removed 2 fields from the
      *           Nur/Fin Targets file:
      *                  GF Transfer in Head
      *                  NF Transfer in Head
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fhsp123    if   e           k disk
      *   Nur/Fin yearly targets
      *
      *
     Fprint132  o    f  132        printer oflind(*inof)
      *
      /EJECT
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Standard fields
      *
     D dash            s            132    inz(*all'-')
     D rtime           s              6  0
      *
      * Control fields
      *
     D first           s              1    inz('Y')
     D svobcell        s                   like(nyobcell)
     D svobpod         s                   like(nyobpod)
     D svcdyr          s                   like(nycdyr)
      *
      *
      * Print fields
      *
     D l1ntlspr        s                   like(nyntlspr)
     D l1gtlspr        s                   like(nygtlspr)
     D l1gcupr         s                   like(nygcupr)
     D l1ggtlspr       s                   like(nygtlspr)
     D l1ggcupr        s                   like(nygcupr)
      *
     D l2ntlspr        s                   like(nyntlspr)
     D l2gtlspr        s                   like(nygtlspr)
     D l2gcupr         s                   like(nygcupr)
     D l2ggtlspr       s                   like(nygtlspr)
     D l2ggcupr        s                   like(nygcupr)
      *
     D l3ntlspr        s                   like(nyntlspr)
     D l3gtlspr        s                   like(nygtlspr)
     D l3gcupr         s                   like(nygcupr)
     D l3ggtlspr       s                   like(nygtlspr)
     D l3ggcupr        s                   like(nygcupr)
      *
     D l4ntlspr        s                   like(nyntlspr)
     D l4gtlspr        s                   like(nygtlspr)
     D l4gcupr         s                   like(nygcupr)
     D l4ggtlspr       s                   like(nygtlspr)
     D l4ggcupr        s                   like(nygcupr)
      *
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *---------------------------------------------------------------
      * Local data area.
      *---------------------------------------------------------------
      *
     Dlda             uds                  dtaara(*lda)
     D  ldobcell               1      2  0
     D  ldobpod                3      5  0
     D  ldcdyr                 6      9  0
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * MAINLINE
      ****************************************************************
      *
      * Print headings
     C                   exsr      $h1hdr
      *
      * Process all Target records selected by the open query.
      *
     C     *loval        setll     hsp123
      *
     C                   dou       *in90 = *on                                  Main do
     C                   read      hsp123                                 90
     C                   if        *in90 = *off                                 If not EOF
      *
      * Control breaks
     C                   select
     C                   when      first = yes
     C                   move      no            first
     C                   z-add     nyobcell      svobcell
     C                   z-add     nyobpod       svobpod
     C                   z-add     nycdyr        svcdyr
      *
     C                   when      nyobcell <> svobcell or
     C                             nyobpod <> svobpod or
     C                             nycdyr <> svcdyr
     C                   exsr      $l1dtl
     C                   endsl
      *
      * Detail processing (load the quarter print fields).
      *
     C                   select
     C                   when      nycdqr  = 1
     C                   z-add     nyntlspr      l1ntlspr
     C                   z-add     nygtlspr      l1gtlspr
     C                   z-add     nygcupr       l1gcupr
     C                   z-add     nyggtlspr     l1ggtlspr
     C                   z-add     nyggcupr      l1ggcupr
      *
     C                   when      nycdqr  = 2
     C                   z-add     nyntlspr      l2ntlspr
     C                   z-add     nygtlspr      l2gtlspr
     C                   z-add     nygcupr       l2gcupr
     C                   z-add     nyggtlspr     l2ggtlspr
     C                   z-add     nyggcupr      l2ggcupr
      *
     C                   when      nycdqr  = 3
     C                   z-add     nyntlspr      l3ntlspr
     C                   z-add     nygtlspr      l3gtlspr
     C                   z-add     nygcupr       l3gcupr
     C                   z-add     nyggtlspr     l3ggtlspr
     C                   z-add     nyggcupr      l3ggcupr
      *
     C                   when      nycdqr  = 4
     C                   z-add     nyntlspr      l4ntlspr
     C                   z-add     nygtlspr      l4gtlspr
     C                   z-add     nygcupr       l4gcupr
     C                   z-add     nyggtlspr     l4ggtlspr
     C                   z-add     nyggcupr      l4ggcupr
     C                   endsl
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Main do
      *
      *
      *---------------------------------------------------------------
      * EOF processing
      *---------------------------------------------------------------
      * If the file was empty, print the standard 'no data...'
      * error message
      *
     C                   if        first = yes
     C                   except    nodata
     C                   else
     C                   exsr      $l1dtl
     C                   endif
      *
     C                   seton                                        lr
      /eject
      *---------------------------------------------------------------
      * Print the detail line
      *---------------------------------------------------------------
      *
     C     $l1dtl        begsr
      *
     C                   if        *inof = *on
     C                   exsr      $h1hdr
     C                   endif
      *
     C                   except    l1dtl
      *
      *
      * Reset indicators to control printing of Cell/Pod on the next detail line.
      *
     C                   setoff                                       8182
     C                   select
     C                   when      nyobcell <> svobcell
     C                   seton                                        8182
      *
     C                   when      nyobpod <> svobpod
     C                   seton                                        82
     C                   endsl
      *
      * Save control fields
      *
     C                   z-add     nyobcell      svobcell
     C                   z-add     nyobpod       svobpod
     C                   z-add     nycdyr        svcdyr
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Print headings
      *---------------------------------------------------------------
      *
     C     $h1hdr        begsr
      *
     C                   except    h1hdr
      *
      * Set indicator to control printing of Cell/Pod on the next detail line.
      *
     C                   seton                                        8182
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Retrieve time for report heading.
      *
     C                   time                    rtime
      *
      * Setup the user selections in the headings.
      *
      * Cell
     C                   if        ldobcell = 0
     C                   seton                                        89
     C                   endif
      * Pod
     C                   if        ldobpod = 0
     C                   seton                                        87
     C                   endif
      * Year
     C                   if        ldcdyr = 0
     C                   seton                                        88
     C                   endif
      *
     C                   endsr
      /EJECT
      *-------------------------------------------------------------
      * Report heading lines
      *-------------------------------------------------------------
      *
     Oprint132  e            h1hdr          1 01
     O                       sdpgm               10
     O                                           85 'HOG PRODUCTION SYSTEM'
     O                                          122 'DATE'
     O                       udate         y    132
      *
     O          e            h1hdr          1
     O                       sdusr               10
     O                                           88 'LISTING OF NUR/FIN TARGETS'
     O                                          122 'TIME'
     O                       rtime              132 '  :  :  '
      *
     O          e            h1hdr          2
     O                                          122 'PAGE'
     O                       page          z    132
      *
     O          e            h1hdr          1
     O                                           16 'Origin BGF Cell:'
     O                       ldobcell      z     20
     O               89                          21 'All'
      *
     O          e            h1hdr          1
     O                                           16 'Origin BGF Pod:'
     O              n87      ldobpod       z     22
     O               87                          21 'All'
      *
     O          e            h1hdr          2
     O                                           16 'Year:'
     O              N88      ldcdyr        z     22
     O               88                          21 'All'
      *
     O          e            h1hdr          1
     O                                            6 'ORIGIN'
     O                                           14 'ORIGIN'
      *
     O          e            h1hdr          1
     O                                            5 'BGF'
     O                                           12 'BGF'
      *
     O          e            h1hdr          1
     O                                            5 'CELL'
     O                                           12 'POD'
     O                                           20 'YEAR'
     O                                           59 'QTR1'
     O                                           69 'QTR2'
     O                                           79 'QTR3'
     O                                           89 'QTR4'
      *
     O          e            h1hdr          1
     O                       dash               132
      *
      *-------------------------------------------------------------
      * Detail line for target table
      *-------------------------------------------------------------
      *
     O          e            l1dtl          1
     O               81      svobcell      z      4
     O               82      svobpod       z     12
     O                       svcdyr        z     20
     O                                           49 'NUR Total Loss %'
     O                       l1ntlspr      kb    60
     O                       l2ntlspr      kb    70
     O                       l3ntlspr      kb    80
     O                       l4ntlspr      kb    90
      *
     O          e            l1dtl          1
     O                                           49 'GF Normal Total Loss %'
     O                       l1gtlspr      kb    60
     O                       l2gtlspr      kb    70
     O                       l3gtlspr      kb    80
     O                       l4gtlspr      kb    90
      *
     O          e            l1dtl          1
     O                                           49 'GF Normal Cull %'
     O                       l1gcupr       kb    60
     O                       l2gcupr       kb    70
     O                       l3gcupr       kb    80
     O                       l4gcupr       kb    90
      *
     O          e            l1dtl          1
     O                                           49 'GF GDU Total Loss %'
     O                       l1ggtlspr     kb    60
     O                       l2ggtlspr     kb    70
     O                       l3ggtlspr     kb    80
     O                       l4ggtlspr     kb    90
      *
     O          e            l1dtl          2
     O                                           49 'GF GDU Cull %'
     O                       l1ggcupr      kb    60
     O                       l2ggcupr      kb    70
     O                       l3ggcupr      kb    80
     O                       l4ggcupr      kb    90
      *
      *-------------------------------------------------------------
      * No data message line
      *-------------------------------------------------------------
      *
     O          e            nodata      1
     O                                           19 'NO DATA MEETS YOUR'
     O                                           38 'SELECTION CRITERIA'
