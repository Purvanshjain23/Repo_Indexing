     A*%%TS  SD  20040218  080033  ISLRAMS     REL-V5R2M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD7706
     A*  TITLE:         VIEW UNLINKED FARMS
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  DATE:          02/18/04
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A                                      CF04(04 'PROMPT')
     A                                      CF05(05 'REFRESH')
     A*
     A*
     A*---------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP7706S1                  SFL
     A*%%TS  SD  20040218  080033  ISLRAMS     REL-V5R2M0  5722-WDS
     A  85                                  SFLNXTCHG
     A            HDRRN1         4Y 0H
     A            D1FSCD    R        O  8  3REFFLD(@@FSCD)
     A                                      EDTCDE(M)
     A            D1FSNM    R        O  8 11REFFLD(HSRREF/FSFSNM *LIBL/HSPREF)
     A            D1CELL    R        O  8 40REFFLD(FSREC/FSCELL *LIBL/HSP018)
     A                                      EDTCDE(M)
     A            D1TFCD    R        O  8 47REFFLD(@@TFCD)
     A            D1PPCD    R        O  8 58REFFLD(@@PPCD)
     A            D1PTCD    R        O  8 66REFFLD(@@PTCD)
     A*
     A*
     A*---------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP7706C1                  SFLCTL(HP7706S1)
     A*%%TS  SD  20040218  080033  ISLRAMS     REL-V5R2M0  5722-WDS
     A                                      SFLSIZ(0050)
     A                                      SFLPAG(0014)
     A N82                                  ROLLUP(25 'PAGE FORWARD')
     A                                      SETOF(52)
     A                                      SETOF(53)
     A                                      SETOF(54)
     A                                      SETOF(55)
     A                                      RTNCSRLOC(&RCD &FLD)
     A                                      OVERLAY
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 26'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  2 27'VIEW UNLINKED FARMS'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  4  6'?'
     A                                      DSPATR(HI)
     A                                  4 49'?'
     A                                      DSPATR(HI)
     A                                  4 64'?'
     A                                      DSPATR(HI)
     A                                  4 70'?'
     A                                      DSPATR(HI)
     A                                  5  4'Farm'
     A                                      DSPATR(HI)
     A                                  5 46'Type of'
     A                                      DSPATR(HI)
     A                                  5 57'--Production--'
     A                                      DSPATR(HI)
     A                                  6  4'Site'
     A                                      DSPATR(HI)
     A                                  6 11'Name'
     A                                      DSPATR(HI)
     A                                  6 39'Cell'
     A                                      DSPATR(HI)
     A                                  6 47'Farm'
     A                                      DSPATR(HI)
     A                                  6 58'Phase'
     A                                      DSPATR(HI)
     A                                  6 66'Type'
     A                                      DSPATR(HI)
     A            C1FSCD    R        B  7  3REFFLD(@@FSCD)
     A                                      EDTCDE(M)
     A                                      DSPATR(HI)
     A  53                                  DSPATR(PC)
     A            C1FSNM    R        B  7 11REFFLD(HSRREF/FSFSNM *LIBL/HSPREF)
     A                                      CHGINPDFT(UL LC FE)
     A                                      DSPATR(HI)
     A            C1CELL    R        B  7 40REFFLD(HSRREF/@@CELL *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A            C1TFCD    R        B  7 47REFFLD(@@TFCD)
     A                                      DSPATR(HI)
     A  54                                  DSPATR(PC)
     A            C1PPCD    R        B  7 58REFFLD(@@PPCD)
     A                                      DSPATR(HI)
     A  55                                  DSPATR(PC)
     A            C1PTCD    R        B  7 66REFFLD(@@PTCD)
     A                                      DSPATR(HI)
     A  52                                  DSPATR(PC)
     A*
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R HP7706K1
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A                                 23 11'F4=Prompt'
     A                                 23 22'F5=Refresh'
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *---------------------------------------------------------------
     A          R HP7706ES                  SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE CONTROL FORMAT
      *---------------------------------------------------------------
     A          R HP7706EC                  SFLCTL(HP7706ES)
      *
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
