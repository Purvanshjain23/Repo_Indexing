/********************************************************************/
/*                                                                  */
/*  SYNOPSIS - PRINT OPERATING MARGIN SUMMARY BY AS OF DATE         */
/*                                                                  */
/*                                                                  */
/*                                                                  */
/*  COMPANY   - SEABOARD COPRORATION                                */
/*  SYSTEM    - ORDER MANAGEMENT                                    */
/*  USER NAME - SHEILA SCHERTZ                                      */
/*  DATE      - 06/15/95                                            */
/*                                                                  */
/*      PARAMETERS RECEIVED:                                        */
/*                                                                  */
/*          &OUTQ       - OUTPUT QUEUE                              */
/*          &COPIESL    - NUMBER OF COPIES                          */
/*          &NIGHTQ     - NIGHT QUEUE                               */
/*          &HOLD       - HOLD PRINT FLAG                           */
/*          &SAVE       - SAVE PRINT FLAG                           */
/*          &PGM        - PROGRAM NAME                              */
/*          &COMPL      - COMPANY NUMBER - BLANK FOR ALL            */
/*          &FDATEL     - FROM DATE                                 */
/*          &TDATEL     - TO DATE                                   */
/*          &PRDLV      - PRODUCT LEVEL                             */
/*          &TYPE       - SALES TYPE                                */
/*          &GRUP       - SALES GROUP                               */
/*          &CLSS       - SALES CLASS                               */
/********************************************************************/

 OMMSPCLP:   PGM        PARM(&PGM &COMPL &FDATEL &TDATEL &PRDLV +
                          &TYPEL &GRUPL &CLSSL &OUTQ &HOLD &SAVE +
                          &COPIESL &NIGHTQ)

        DCL    &COMP      *DEC    (3 0)           /*  COMPANY #     */
        DCL    &COMPL     *DEC                    /*  FOR PASSING   */
        DCL    &FDATE     *DEC    (7 0)          /*  FROM DATE      */
        DCL    &FDATEL    *DEC                   /*  FOR PASSING    */
        DCL    &TDATE     *DEC    (7 0)          /*  THROUGH DATE   */
        DCL    &TDATEL    *DEC                   /*  FOR PASSING    */
        DCL    &COPIES    *DEC    (3 0)           /*  # OF COPIES   */
        DCL    &COPIESL   *DEC                    /*  FOR PASSING   */
        DCL    &TYPE      *DEC    (3 0)          /*  SALES TYPE     */
        DCL    &TYPEL     *DEC                   /*  FOR PASSING    */
        DCL    &GRUP      *DEC    (3 0)          /*  SALES GROUP    */
        DCL    &GRUPL     *DEC                   /*  FOR PASSING    */
        DCL    &CLSS      *DEC    (3 0)          /*  SALES CLASS    */
        DCL    &CLSSL     *DEC                   /*  FOR PASSING    */
        DCL    &PRDLV     *CHAR   2              /*  PRODUCT LEVEL  */
        DCL    &OUTQ      *CHAR   10             /*  OUTPUT QUEUE  */
        DCL    &NIGHTQ    *CHAR   10             /*  NIGHT JOB QUEUE*/
        DCL    &SAVE      *CHAR   4              /*  SAVE SPOOL     */
        DCL    &HOLD      *CHAR   4              /*  HOLD SPOOL     */
        DCL    &PGM       *CHAR   10              /*  PGM NAME      */
        DCL    &COMPA     *CHAR   3               /*  COMPANY ALPHA */
        DCL    &FDATEA    *CHAR   7               /*  FROM DT ALPHA */
        DCL    &TDATEA    *CHAR   7               /*  TO   DT ALPHA */
        DCL    &TYPEA     *CHAR   3               /*  TYPE ALPHA    */
        DCL    &GRUPA     *CHAR   3               /*  GROUP ALPHA   */
        DCL    &CLSSA     *CHAR   3               /*  CLASS ALPHA   */
        DCL    &COMMAND    *CHAR   (512)

/* SEND PROGRAM MESSAGE PARAMETERS  */

        DCL    &USER      *CHAR   10              /*  USER ID       */
        DCL    &MSGRTN    *CHAR   10              /*  MSG QUEUE     */
        DCL    &PGMO      *CHAR   10              /*  PRINTER FILE  */
        DCL    &RPTCO     *CHAR   14              /* REPORT/CO MSG  */
        DCL     &A        *CHAR   20              /* QRY FROM DATE  */
        DCL     &C        *CHAR   25              /* QRY COMPANY    */
        DCL     &D        *CHAR   25              /* QRY PROD LEVEL */
        DCL     &E        *CHAR   25              /* QRY TYPE       */
        DCL     &F        *CHAR   25              /* QRY GROUP      */
        DCL     &G        *CHAR   25              /* QRY CLASS      */
        DCL     &H        *CHAR  170              /* QRY ALL        */
        DCL     &DATA     *CHAR   10              /* DATA FILES     */
        DCL     &DATAH    *CHAR   10              /* HIST FILES     */

             CHGVAR    VAR(&COMP) VALUE(&COMPL) /* Change to length +
                          for print program */
             CHGVAR    VAR(&COMPA) VALUE(&COMP) /* Change to alpha +
                          for message data */
             CHGVAR    VAR(&FDATE) VALUE(&FDATEL) /* Change to length +
                          for print program */
             CHGVAR    VAR(&FDATEA) VALUE(&FDATE) /* Change to alpha +
                          for query data */
             CHGVAR    VAR(&TYPE) VALUE(&TYPEL) /* Change to length +
                          for print program */
             CHGVAR    VAR(&TYPEA) VALUE(&TYPE) /* Change to alpha +
                          for message data */
             CHGVAR    VAR(&GRUP) VALUE(&GRUPL) /* Change to length +
                          for print program */
             CHGVAR    VAR(&GRUPA) VALUE(&GRUP) /* Change to alpha +
                          for message data */
             CHGVAR    VAR(&CLSS) VALUE(&CLSSL) /* Change to length +
                          for print program */
             CHGVAR    VAR(&CLSSA) VALUE(&CLSS) /* Change to alpha +
                          for message data */
             CHGVAR    VAR(&COPIES) VALUE(&COPIESL) /* change to +
                          length for print program */

/********************************************************************/
/* APPEND $ TO THE PROGRAM NAME                                     */
/********************************************************************/

             CHGVAR     VAR(&PGMO) VALUE(&PGM *TCAT '$')

/********************************************************************/
/* Retrieve Data Library name and Data History library name         */
/********************************************************************/

             CALL       PGM(CAQXXFR) PARM(' ' &DATA &DATAH)

/*********************************************************************/
/*   CREATE WORK FILE IN QTEMP                                       */
/*********************************************************************/
             RMVLIBLE   LIB(QTEMP)
             MONMSG     MSGID(CPF0000)
             ADDLIBLE   LIB(QTEMP)
             MONMSG     MSGID(CPF0000)

             CLRPFM     FILE(OMHSCPP)
             CRTDUPOBJ  OBJ(OMHSCPP) FROMLIB(&DATA) +
                          OBJTYPE(*FILE) TOLIB(QTEMP)
             CRTDUPOBJ  OBJ(OMHSCPL0) FROMLIB(&DATA) +
                          OBJTYPE(*FILE) TOLIB(QTEMP)
             CRTDUPOBJ  OBJ(OMHSCPL1) FROMLIB(&DATA) +
                          OBJTYPE(*FILE) TOLIB(QTEMP)
             CRTDUPOBJ  OBJ(OMHSCPLA) FROMLIB(&DATA) +
                          OBJTYPE(*FILE) TOLIB(QTEMP)
             CRTDUPOBJ  OBJ(OMHSCPL8) FROMLIB(&DATA) +
                          OBJTYPE(*FILE) TOLIB(QTEMP)
             CRTDUPOBJ  OBJ(OMHSCPL9) FROMLIB(&DATA) +
                          OBJTYPE(*FILE) TOLIB(QTEMP)

/*********************************************************************/
/*   CREATE OPEN QUERY FILE TO BE USED FOR AREA/BRK WORK FILE BUILD  */
/*********************************************************************/

             CHGVAR     VAR(&A) VALUE('F4AWDT *GE ' *CAT +
                         &FDATEA)

             CHGVAR     VAR(&C) VALUE('*AND F4AIC3 *EQ ' *CAT +
                         &COMPA)

             IF         (&PRDLV *NE '  ') THEN(DO)
             CHGVAR     VAR(&D) VALUE('*AND F4RBCD *EQ "' *CAT +
                         &PRDLV *CAT '"')
             ENDDO

             IF         (&TYPEA *NE '000') THEN(DO)
             CHGVAR     VAR(&E) VALUE('*AND F4RGNB *EQ ' *CAT +
                         &TYPEA)
             ENDDO

             IF         (&GRUPA *NE '000') THEN(DO)
             CHGVAR     VAR(&F) VALUE('*AND F4RHNB *EQ ' *CAT +
                         &GRUPA)
             ENDDO

             IF         (&CLSSA *NE '000') THEN(DO)
             CHGVAR     VAR(&G) VALUE('*AND F4RINB *EQ ' *CAT +
                         &CLSSA)
             ENDDO

             CHGVAR     VAR(&H) VALUE(&A *BCAT &C *BCAT &D *BCAT &E +
                          *BCAT &F *BCAT &G)
             CHGVAR     (%SST(&RPTCO 1 3)) VALUE(&COMPA)

             OVRDBF     FILE(OMHSTPL1) SHARE(*YES)
             OPNQRYF    FILE(OMHSTPL1) +
                        QRYSLT(&H) +
                        KEYFLD(*FILE)

/* BUILD HISTORY WORK FILE                                  */

             CALL       PGM(OMNRXFR) PARM('')
             CLOF       OPNID(OMHSTPL1)
             DLTOVR     FILE(OMHSTPL1)


/********************************************************************/
/* Override printer file with user specified values                 */
/********************************************************************/

             SBDPRTOVR  CMDVAR(&COMMAND) FILE(&PGMO) OUTQ(&OUTQ) +
                          COPIES(&COPIES) HOLD(&HOLD) SAVE(&SAVE)

             CALL       PGM(QCMDEXC) PARM(&COMMAND 512)
/*********************************************************************/
/*   CALL LISTING PROGRAM                                            */
/*********************************************************************/

             CALL       PGM(&PGM) PARM('' &FDATE &TYPE &GRUP +
                          &CLSS &PRDLV)

/*   DELETE WORK FILE IN QTEMP                                       */

             DLTF       FILE(QTEMP/OMHSCPL0)
             DLTF       FILE(QTEMP/OMHSCPL1)
             DLTF       FILE(QTEMP/OMHSCPLA)
             DLTF       FILE(QTEMP/OMHSCPL8)
             DLTF       FILE(QTEMP/OMHSCPL9)
             DLTF       FILE(QTEMP/OMHSCPP)

 END:        ENDPGM
