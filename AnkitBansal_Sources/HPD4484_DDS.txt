     A*%%TS  SD  20020131  081618  ISLRAMS     REL-V4R4M0  5769-PW1
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION
     A*  DISPLAY FILE:  HPD4484
     A*  TITLE:         SPECIFY QUARTERLY PREDICTION VALUES TO COPY
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  DATE:          01/30/02
     A*
     A*  NOTE:  THIS IS A POP-UP WINDOW. THE WINDOW DEFINITION IS IN
     A*         RECORD FORMAT HP4484EC
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CF03(03 'EXIT')
     A                                      CF04(04 'PROMPT')
     A                                      CF08(08 'SUBMIT')
     A*
     A*----------------------------------------------------------------
     A* RECORD FORMAT ONE
     A*----------------------------------------------------------------
     A          R HP4484R1
     A                                      WINDOW(HP4484EC)
     A                                      TEXT('INPUT SCREEN')
     A                                      SETOF(31)
     A                                      SETOF(32)
     A                                      SETOF(33)
     A                                      SETOF(34)
     A                                      SETOF(35)
     A                                      SETOF(36)
     A                                      SETOF(37)
     A                                      SETOF(38)
     A                                      SETOF(57)
     A                                      SETOF(58)
     A                                      OVERLAY
     A                                      RTNCSRLOC(&RCD &FLD)
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  1REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 14'SPECIFY QUARTERLY PREDICTION VALUE-
     A                                      S TO COPY'
     A                                      DSPATR(HI)
     A                                  3 23'COPY'
     A                                      DSPATR(HI)
     A                                  3 33'COPY'
     A                                      DSPATR(HI)
     A                                  4 23'FROM'
     A                                      DSPATR(HI)
     A                                  4 34'TO'
     A                                      DSPATR(HI)
     A                                  6  1'Calendar year .....'
     A            D1FCDYR   R        B  6 23REFFLD(HSRREF/@@CDYR *LIBL/HSPREF)
     A  31                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A N31                                  DSPATR(HI)
     A            D1TCDYR   R        B  6 33REFFLD(HSRREF/@@CDYR *LIBL/HSPREF)
     A  32                                  DSPATR(PC)
     A  32                                  DSPATR(RI)
     A N32                                  DSPATR(HI)
     A                                  7  1'Calendar quarter ..'
     A            D1FCDQR   R        B  7 23REFFLD(HSRREF/@@CDQR *LIBL/HSPREF)
     A  33                                  DSPATR(PC)
     A  33                                  DSPATR(RI)
     A N33                                  DSPATR(HI)
     A            D1TCDQR   R        B  7 33REFFLD(HSRREF/@@CDQR *LIBL/HSPREF)
     A  34                                  DSPATR(PC)
     A  34                                  DSPATR(RI)
     A N34                                  DSPATR(HI)
     A                                  7 43'(Blank for all)'
     A                                  8  1'Cell ..............'
     A            D1FCELL   R        B  8 23REFFLD(HSRREF/@@CELL *LIBL/HSPREF)
     A                                      EDTCDE(M)
     A  35                                  DSPATR(PC)
     A  35                                  DSPATR(RI)
     A N35                                  DSPATR(HI)
     A            D1TCELL   R        B  8 33REFFLD(HSRREF/@@CELL *LIBL/HSPREF)
     A                                      EDTCDE(M)
     A  36                                  DSPATR(PC)
     A  36                                  DSPATR(RI)
     A N36                                  DSPATR(HI)
     A                                  8 43'(Blank for all)'
     A                                  9  1'Group type.........'
     A                                  9 21'?'
     A            D1FGTCD   R        B  9 23REFFLD(HSRREF/GTGTCD *LIBL/HSPREF)
     A  37
     AO 57                                  DSPATR(PC)
     A  37                                  DSPATR(RI)
     A N37                                  DSPATR(HI)
     A            D1TGTCD   R        B  9 33REFFLD(HSRREF/GTGTCD *LIBL/HSPREF)
     A  38
     AO 58                                  DSPATR(PC)
     A  38                                  DSPATR(RI)
     A N38                                  DSPATR(HI)
     A                                  9 43'(Blank for all)'
     A*
     A*----------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*----------------------------------------------------------------
     A          R HP4484K1
     A*%%TS  SD  20020131  081618  ISLRAMS     REL-V4R4M0  5769-PW1
     A                                      WINDOW(HP4484EC)
     A                                      TEXT('COMMAND KEYS')
     A                                      OVERLAY
     A  43                             11  2'Message program failed. Call IS.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 12  1'F3=Exit'
     A                                 12 10'F4=Prompt'
     A                                 12 21'F8=Submit'
      *
      *----------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *----------------------------------------------------------------
     A          R HP4484ES
     A                                      TEXT('Error message subfile')
     A                                      SFL SFLMSGRCD(13)
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
     A*
     A*----------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT WITH WINDOW DEFINITION
     A*----------------------------------------------------------------
     A          R HP4484EC                  SFLCTL(HP4484ES)
     A*%%TS  SD  20000628  103649  ISLRAMS     REL-V4R2M0  5769-PW1
     A                                      TEXT('Error message subfile')
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A                                      WINDOW(*DFT 14 60)
     A                                      WDWBORDER((*DSPATR RI) (*CHAR '    -
     A                                          '))
     A            MSGPGM                    SFLPGMQ(10)
     A*
     A*----------------------------------------------------------------
     A* ASSUME FORMAT - REQUIRED FOR WINDOWS
     A*----------------------------------------------------------------
     A*
     A          R ASSUME
     A*%%TS  SD  20000627  095625  ISLRAMS     REL-V4R2M0  5769-PW1
     A                                      TEXT('ASSUME FORMAT FOR WINDOWS')
     A                                      ASSUME
     A                                      OVERLAY
     A                                 24 79' '
