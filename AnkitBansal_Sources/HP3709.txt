      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Hog Production System
      * PROGRAM:     HP3709-Workfile: Order Requests On Hold Report
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     07/21/04
      *
      * Function:    This function builds the workfile.
      *
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 10/13/04  LeAnne Fedor
      *           Recompile only. Cost Per Unit Amount was changed from
      *           9,2 to 9,4.
      *
      * 03/21/05  LeAnne Fedor
      *           Added Production Manager Code/Name.
      *
      * 10/15/13  LeAnne Ramsey (E2831)
      *           Recompile only. Added field 'MTech Reference'.
      /eject
      *****************************************************************************
      * FILE SPECIFICATIONS
      *****************************************************************************
      *
     Fhsp016    if   e           k disk
      *    Manager
      *
      *
     Fhsp018    if   e           k disk
      *    Farms  (records selected with open query)
      *
      *
     Fhsp181    if   e           k disk
      *    Item
      *
      *
     Fhsp188    if   e           k disk
      *    Farm budget header
      *
      *
     Fhsp192    if   e           k disk
      *    Request header (records selected with open query)
      *
      *
     Fhsp193    if   e           k disk
      *    Request detail (records selected with open query)
      *
      *
     Fhsp3709   o    e           k disk
      *
      /eject
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      *----------------------------------------------------------------
      * CONSTANTS
      *----------------------------------------------------------------
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      *----------------------------------------------------------------
      * STANDALONE FIELDS
      *----------------------------------------------------------------
      *
      * Workfields for date manipulation
      *
     D wkisodate       s               D   datfmt(*iso)
      *
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * Mainline
      ****************************************************************
      *
      * Read each record selected by the open query from the Request Header.
      *
     C     *loval        setll     hsp192
      *
     C                   dou       *in90 = *on                                  Main do
     C                   read      hsp192                                 90
     C                   if        *in90 = *off                                 If not EOF
      *
      * Retrieve the Farm info for this record. If there is no Farm record,
      * then the Farm does not meet the user's selection criteria; and, you
      * should omit this request from processing.
      *
     C     qhfscd        chain     hsp018                             92
     C                   if        *in92 = *off                                 If continue
     C                   exsr      $setup
     C                   exsr      $detail
     C                   endif                                                  If continue
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Main do
      *
      * EOF processing
     C                   seton                                        lr
      /eject
      *---------------------------------------------------------------
      * Setup for header/farm info
      *---------------------------------------------------------------
      *
     C     $setup        begsr
      *
     C                   clear                   w1rec
      *
     C                   move      fsfsnm        w1fsnm
     C                   move      fsppcd        w1ppcd
     C                   z-add     fscell        w1cell
      *
      * Cell manager
     C                   move      fscmgcd       w1cmgcd
     C     fscmgcd       chain     hsp016                             92
     C                   if        *in92 = *off
     C                   move      mgmgnm        w1cmgnm
     C                   endif
      *
      * Production manager
     C                   move      fspmgcd       w1pmgcd
     C     fspmgcd       chain     hsp016                             92
     C                   if        *in92 = *off
     C                   move      mgmgnm        w1pmgnm
     C                   endif
      *
     C                   z-add     qhfscd        w1fscd
     C                   move      qhwo          w1wo
     C                   z-add     qhrqsn        w1rqsn
     C                   z-add     qhrqdt        w1rqdt
      *
      * Flip Request Date from CCYYMMDD to MMDDYY.
      *
     C     *iso          test(d)                 qhrqdt                 92
     C                   if        *in92 = *off                                 If OK date
     C                   move      qhrqdt        wkisodate
     c     *mdy          move      wkisodate     w1rqmdy
     C                   endif                                                  If OK date
      *
     C                   endsr
      /eject
      *-------------------------------------------------------------------------------------
      * Process all 'on-hold' detail lines for this request
      *-------------------------------------------------------------------------------------
      *
     C     $detail       begsr
      *
     C     qhrqsn        setll     hsp193
      *
     C                   dou       *in91 = *on                                  Do detail
     C     qhrqsn        reade     hsp193                                 91
     C                   if        *in91 = *off                                 If not EOF
      *
     C                   z-add     qdlnno        w1lnno
     C                   move      qditcd        w1itcd
     C                   move      qdrqscd       w1rqscd
     C                   move      qdcom         w1com
     C                   z-add     qdrqqt        w1rqqt
     C                   z-add     qdfbsn        w1fbsn
      *
      * Clear/retrieve Farm Budget Header fields.
      *
     C                   move      *blank        w1btcd
     C                   z-add     0             w1bfdt
     C                   z-add     0             w1bfmdy
     C                   z-add     0             w1btdt
     C                   z-add     0             w1btmdy
      *
     C     qdfbsn        chain     hsp188                             92
     C                   if        *in92 = *off                                 If hit
      * Budget template
     C                   move      jhbtcd        w1btcd
      *
      * Budget from date
     C                   z-add     jhbfdt        w1bfdt
     C     *iso          test(d)                 jhbfdt                 92
     C                   if        *in92 = *off                                 If OK date
     C                   move      jhbfdt        wkisodate
     c     *mdy          move      wkisodate     w1bfmdy
     C                   endif                                                  If OK date
      *
      * Budget to date
     C                   z-add     jhbtdt        w1btdt
     C     *iso          test(d)                 jhbtdt                 92
     C                   if        *in92 = *off                                 If OK date
     C                   move      jhbtdt        wkisodate
     c     *mdy          move      wkisodate     w1btmdy
     C                   endif                                                  If OK date
     C                   endif                                                  If hit
      *
      *
      * Retrieve Budget Item for this Item
      *
     C     qditcd        chain     hsp181                             92
     C                   if        *in92 = *off                                 If item hit
     C                   move      imbgit        w1bgit
     C                   z-add     imvol         w1vol
     C                   eval      w1itds = imitds
     C                   else
     C                   move      *blank        w1bgit
     C                   move      *blank        w1itds
     C                   z-add     0             w1vol
     C                   endif                                                  If item hit
      *
      * Write workfile record
      *
     C                   write     w1rec
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Do detail
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Parm lists
      *   None
      *
      * Key lists
      *   None
     C                   endsr
      /eject
