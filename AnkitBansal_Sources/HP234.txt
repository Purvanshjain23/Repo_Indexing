      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Hog Production
      * PROGRAM:     HP234-Write Feed Editing Records to HPS Feed Ticket Files
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     05/05/97
      *
      * FUNCTION: This program is the final step in the interface of Feed Tickets from
      *           the PC System to the HPS System.  This program creates the Feed Ticket
      *           Header and Detail records in the HPS System from the Feed Ticket Editing
      *           files.
      *
      *           This program is submitted by HP434CL which is called from HP434 thru
      *           QCMDEXC.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      * 06/25/97  LEANNE FEDOR
      *           NEW FIELD 'COST THROUGH SYSTEM' ADDED TO FEED TICKET
      *           HEADER. LOGIC ORIGINALLY BASED ON 'PRODUCED FLAG' IS
      *           NOW BASED ON NEW FIELD.
      *
      * 03/09/98  LEANNE FEDOR
      *           RECOMPILE ONLY. FEED POUNDS AND FEED AMOUNT FIELDS
      *           INCREASED IN SIZE PER NATHAN MALONE.
      *
      * 01/30/01  LeAnne Fedor
      *           Production type was removed from the feed ticket files.
      *           So, removed all production type logic from program.
      *
      * 07/31/02  LeAnne Fedor
      *           Production Phase was removed from the feed ticket header file in HPS.
      *           It is still in the Feed Ticket Detail file and will be populated with:
      *             1) the group's phase if there is a group on the detail record
      *             2) the farm's phase if there is no group on the detail record
      *
      * 12/09/02  LeAnne Fedor
      *           Production Phase was removed from the feed ticket detail file.
      *
      * 04/23/09  LeAnne Ramsey
      *           added logic to populate new Costed Time/User fields in the Feed Ticket Header.
      *
      * 05/12/09  LeAnne Ramsey
      *           Recompile only. PickUp Ticket Type added to Feed Ticket Header.
      *
      * 09/21/09  LeAnne Ramsey
      *           Debbie Deen wanted us to add edits to the Delivery Feed Ticket program (HP120).
      *           These edits are only for tickets that were originally uploaded....not entered
      *           manually.  So, we needed some way to differentiate uploaded/manual delivery
      *           tickets....I added an Upload Date. (It will be zero for manual tickets.)
      *
      * 10/26/09  LeAnne Ramsey
      *           Rewrote to incorporate David Weaver's enhancements from his Grain Accounting
      *           Project (where he rewrote the pc side of things.)
      *
      * 03/26/20  Danny Nguyen - P405 - Farm Number Increase
      *           Recompile only. Increased Bin Code (@@BNCD) in HSPREF file from 5A to 6A.
      *
      * 05/10/22 Eric L SDN736 Recompiled ticket nbr increase (TKNO & RTNO 7.0 TO 9.0)
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fhsp037    o    e           k disk
      *  Feed ticket header
      *
      *
     Fhsp038    o    e           k disk
      *  Feed ticket detail
      *
      *
     Fhsp237    uf   e           k disk
      *  Uploaded feed tickets: Edit file-Header
      *
      *
     Fhsp238    uf   e           k disk
      *  Uploaded feed tickets: Edit file-Detail
      *
      /EJECT
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      *---------------------------------------------------------------
      * CONSTANTS
      *---------------------------------------------------------------
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      *---------------------------------------------------------------
      * Standalone fields
      *---------------------------------------------------------------
      *
      * Workfields for date manipulation
      *
     D wkisodate       s               d   datfmt(*iso)
      *
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * Mainline
      ****************************************************************
      *
      * Process each record in the Editing Header file.
      *
     C     *start        setll     hsp237
      *
     C                   dou       *in90 = *on                                  Main do
     C                   read      hsp237                                 90
     C                   if        *in90 = *off                                 If not EOF
      *
      * Write an HPS Feed Ticket Header record
      *
     C                   exsr      $wrt037
      *
      * Write each Feed Ticket Detail record for this Header record.
      *
     C     uhtkno        setll     hsp238
     C                   dou       *in91 = *on
     C     uhtkno        reade     hsp238                                 91
     C                   if        *in91 = *off
     C                   exsr      $wrt038
     C                   endif
     C                   enddo
      *
      *
      * Delete the Uploaded Editing File Header Record
      *
     C                   delete    uhrec
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Main do
      *
      * EOF
     C                   seton                                        lr
      /EJECT
      *---------------------------------------------------------------
      * Write Feed Ticket Header record
      *---------------------------------------------------------------
      *
     C     $wrt037       begsr
      *
     C                   clear                   fhrec
      *
     C                   z-add     uhtkno        fhrtno
     C                   z-add     uhtkno        fhtkno
     C                   z-add     uhtkdt        fhtkdt
     C                   z-add     uhtkdt        fhrtdt
     C                   z-add     uhfscd        fhfscd
     C                   movel     uhrgcd        fhrgcd
     C                   movel     uhfmbo        fhfmbo
     C                   movel     uhprfl        fhprfl
     C                   movel     uhcsfl        fhcsfl
     C                   movel     uhvino        fhvino
      *
      * Set up defaults
     C                   move      'D'           fhtrcd
      *
     C     *mdy          move      udate         wkisodate
     C                   move      wkisodate     fhuldt
      *
      * For Tickets where the user has entered dollars (ie: Cost-thru-System is NO):
      *    set the status to 'CO=Costed'
      *    set the Costed Date equal to System Date
      *
     C                   if        fhcsfl = no                                  If dollars keyed
     C                   move      'CO'          fhtscd
     C                   move      sdusr         fhcousr
     C                   time                    fhcotm
     C                   move      wkisodate     fhcodt
     C                   else
      *
      * Tickets that are being costed through the system.
      *
     C                   move      'EN'          fhtscd
     C                   endif                                                  If dollars keyed
      *
     C                   write     fhrec
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Write Feed Ticket Detail records
      *---------------------------------------------------------------
      *
     C     $wrt038       begsr
      *
     C                   clear                   fdrec
      *
     C                   z-add     fhtkno        fdrtno
     C                   z-add     fhtkno        fdtkno
     C                   z-add     fhtkdt        fdtkdt
     C                   z-add     fhtkdt        fdrtdt
      *
     C                   move      fhtrcd        fdtrcd
     C                   move      fhrgcd        fdrgcd
     C                   z-add     fhfscd        fdfscd
     C                   move      fhfmbo        fdfmbo
      *
     C                   z-add     udlnno        fdlnno
     C                   z-add     udhgsn        fdhgsn
     C                   move      udblcd        fdblcd
     C                   move      udrmcd        fdrmcd
     C                   move      udbncd        fdbncd
     C                   move      udalfl        fdalfl
      *
     C                   move      udrncd        fdrncd
     C                   move      udadcd        fdadcd
     C                   move      udftcd        fdftcd
      *
     C                   z-add     udfdlb        fdfdlb
     C                   z-add     udinam        fdinam
     C                   z-add     udmfam        fdmfam
     C                   z-add     udmdam        fdmdam
      *
      * Write Feed Ticket Detail record
      *
     C                   write     fdrec
      *
      * Delete Editing record
      *
     C                   delete    udrec
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Initialization
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
     C                   endsr
      /EJECT
