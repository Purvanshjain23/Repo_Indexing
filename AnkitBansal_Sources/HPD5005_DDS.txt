     A*%%TS  SD  19970623  155400  ISLRAMS     REL-V3R6M0  5716-PW1
     A*****************************************************************
     A*
     A*  SYSTEM:        HOG PRODUCTION
     A*  DISPLAY FILE:  HPD5005 - SELECT DOLLAR AMOUNT FORMAT
     A*  RPG PROGRAM:   HP5005  - SELECT DOLLAR AMOUNT FORMAT
     A*  CREATED BY:    LEANNE FEDOR
     A*  DATE:          06/23/97
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      PRINT
     A                                      CA12(12 'PREVIOUS')
     A N82                                  ROLLUP(25 'PAGE FORWARD')
     A*----------------------------------------------------------------
     A* REFERENCE WINDOW RECORD FORMAT 1
     A*----------------------------------------------------------------
     A          R HP5005R1
     A*%%TS  SD  19940316  140733  ISRCENT     REL-V2R2M0  5738-PW1
     A                                      TEXT('Reference window')
     A                                      WINDOW(*DFT 12 28)
     A                                      WDWBORDER((*DSPATR RI) (*CHAR '    -
     A                                          '))
     A*----------------------------------------------------------------
     A* RECORD FORMAT ONE
     A*----------------------------------------------------------------
     A          R HP5005R2
     A*%%TS  SD  19940315  135528  ISRCENT     REL-V2R2M0  5738-PW1
     A                                      TEXT('Written in RPG pgm to prevent-
     A                                       screen from clearing')
     A                                      CLRL(*NO)
     A                                      OVERLAY
     A*----------------------------------------------------------------
     A* RECORD FORMAT TWO
     A*----------------------------------------------------------------
     A* THIS FORMAT IS NOT WRITTEN IN THE RPG PGM.  IT PREVENTS THE
     A* CALLING_PROGRAM_SCREEN FROM BEING CLEARED.
     A*----------------------------------------------------------------
     A          R HP5005R3
     A*%%TS  SD  19940315  135528  ISRCENT     REL-V2R2M0  5738-PW1
     A                                      TEXT('Dummy format not written in R-
     A                                      PG pgm.')
     A                                      ASSUME
     A                                 11  1' '
     A*----------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*----------------------------------------------------------------
     A          R HP5005S1                  SFL
     A*%%TS  SD  19940331  151719  ISLRAMS     REL-V2R2M0  5738-PW1
     A                                      TEXT('subfile record format one')
     A            HDRRN1         4S 0H
     A            DFAFCD    R        O  5  1REFFLD(AFREC/AFAFCD *LIBL/HSP043)
     A            DFAFDS    R        O  5  8REFFLD(AFREC/AFAFDS *LIBL/HSP043)
     A*----------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*----------------------------------------------------------------
     A          R HP5005C1                  SFLCTL(HP5005S1)
     A*%%TS  SD  19970623  155400  ISLRAMS     REL-V3R6M0  5716-PW1
     A                                      SFLSIZ(0050)
     A                                      SFLPAG(0004)
     A                                      WINDOW(HP5005R1)
     A                                      TEXT('control format one')
     A                                      OVERLAY
     A                                      SFLCSRRRN(&HDRRN)
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND(*MORE)
     A                                      RMVWDW
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            HDRRN          5S 0H
     A                                  1  1'SELECT DOLLAR AMOUNT FORMAT'
     A                                      DSPATR(HI)
     A                                  2  1'Position Cursor, Press Enter'
     A                                  4  1'Code'
     A                                      DSPATR(HI)
     A                                  4  8'Description'
     A                                      DSPATR(HI)
     A          R HP5005K1
     A*%%TS  SD  19940316  131942  ISRCENT     REL-V2R2M0  5738-PW1
     A                                      WINDOW(HP5005R1)
     A                                      OVERLAY
     A                                 11  1'F12=Previous'
