     A*%%TS  SD  20050908  151708  ISLRAMS     REL-V5R3M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD7712
     A*  TITLE:         CONFIRM REMOVAL OF LINKS TO FARMS WITH NO BUDGETS
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  DATE:          09/08/05
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A                                      CF11(11 'DELETE')
     A*
     A*--------------------------------------------------------------*
     A* RECORD FORMAT ONE
     A*--------------------------------------------------------------*
     A          R HP7712R1
     A*%%TS  SD  20050908  151708  ISLRAMS     REL-V5R3M0  5722-WDS
     A                                      OVERLAY
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 29'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  2 14'CONFIRM REMOVAL OF LINKS TO FARMS -
     A                                      WITH NO BUDGETS'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  4  3'If you are sure that you want to r-
     A                                      emove/break all links to farms with-
     A                                       no'
     A                                  5  3'budgets for this template, take'
     A                                  5 35'F11=Delete;'
     A                                      DSPATR(HI)
     A                                  5 48'otherwise, take'
     A                                  5 64'F3=Exit.'
     A                                      DSPATR(HI)
     A                                  5 80' '
     A                                  6  3'                       '
     A                                  7 80' '
     A                                  8  3'Template:'
     A                                      DSPATR(HI)
     A            D1BTCD    R        O  8 14REFFLD(HSRREF/@@BTCD *LIBL/HSPREF)
     A            D1BTDS    R        O  8 22REFFLD(HSRREF/@@BTDS *LIBL/HSPREF)
     A*--------------------------------------------------------------*
     A* COMMAND KEY RECORD 1
     A*--------------------------------------------------------------*
     A          R HP7712K1
     A*%%TS  SD  20050908  151708  ISLRAMS     REL-V5R3M0  5722-WDS
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A                                 23 11'F11=Delete'
      *
      *--------------------------------------------------------------*
      * ERROR MESSAGE SUBFILE FORMAT
      *--------------------------------------------------------------*
     A          R HP7712ES                  SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
     A*
     A*--------------------------------------------------------------*
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*--------------------------------------------------------------*
     A*
     A          R HP7712EC                  SFLCTL(HP7712ES)
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
