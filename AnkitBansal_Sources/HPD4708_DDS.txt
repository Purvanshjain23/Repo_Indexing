     A*%%TS  SD  20040719  092245  ISLRAMS     REL-V5R2M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION
     A*  DISPLAY FILE:  HPD4708
     A*  TITLE:         SPECIFY ITEM LISTING
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  DATE:          07/19/04
     A*
     A*  NOTE:  THIS IS A POP-UP WINDOW. THE WINDOW DEFINITION IS IN
     A*         RECORD FORMAT HP4708EC
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CF03(03 'EXIT')
     A                                      CF04(04 'PROMPT')
     A                                      CF08(08 'SUBMIT')
     A                                      CF17(17 'PRINT OPTIONS')
     A*
     A*----------------------------------------------------------------
     A* RECORD FORMAT ONE
     A*----------------------------------------------------------------
     A          R HP4708R1
     A*%%TS  SD  20040719  092158  ISLRAMS     REL-V5R2M0  5722-WDS
     A                                      WINDOW(HP4708EC)
     A                                      TEXT('INPUT SCREEN')
     A                                      SETOF(31)
     A                                      SETOF(32)
     A                                      SETOF(33)
     A                                      SETOF(34)
     A                                      SETOF(35)
     A                                      SETOF(36)
     A                                      SETOF(37)
     A                                      SETOF(51)
     A                                      SETOF(52)
     A                                      SETOF(53)
     A                                      SETOF(54)
     A                                      SETOF(55)
     A                                      SETOF(56)
     A                                      SETOF(57)
     A                                      OVERLAY
     A                                      RTNCSRLOC(&RCD &FLD)
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  1REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 22'SPECIFY ITEM LISTING OPTIONS'
     A                                      DSPATR(HI)
     A                                  3  1'Version ..........'
     A                                  3 20'?'
     A            D1VER     R        B  3 22REFFLD(HSRREF/@@AIST *LIBL/HSPREF)
     A  31
     AO 51                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A N31                                  DSPATR(HI)
     A            D1VERDS       30A  O  3 25
     A                                  4  1'Item number ......'
     A                                  4 20'?'
     A            D1ITCD    R        B  4 22REFFLD(HSRREF/@@ITCD *LIBL/HSPREF)
     A  32
     AO 52                                  DSPATR(PC)
     A  32                                  DSPATR(RI)
     A N32                                  DSPATR(HI)
     A                                  4 55'(Blank for all)'
     A                                  5  1'Budget item ......'
     A                                  5 20'?'
     A            D1BGIT    R        B  5 22REFFLD(HSRREF/@@BGIT *LIBL/HSPREF)
     A  33
     AO 53                                  DSPATR(PC)
     A  33                                  DSPATR(RI)
     A N33                                  DSPATR(HI)
     A                                  5 55'(Blank for all)'
     A                                  6  1'Item type code ...'
     A                                  6 20'?'
     A            D1ITTYCD  R        B  6 22REFFLD(HSRREF/@@ITTYCD *LIBL/HSPREF)
     A  34
     AO 54                                  DSPATR(PC)
     A  34                                  DSPATR(RI)
     A N34                                  DSPATR(HI)
     A                                  6 55'(Blank for all)'
     A                                  7  1'Active/inactive ..'
     A                                  7 20'?'
     A            D1AIST    R        B  7 22REFFLD(HSRREF/@@AIST *LIBL/HSPREF)
     A  35
     AO 55                                  DSPATR(PC)
     A  35                                  DSPATR(RI)
     A N35                                  DSPATR(HI)
     A                                  7 55'(Blank for all)'
     A                                  8  1'Budget flag ......'
     A                                  8 20'?'
     A            D1BGFL    R        B  8 22REFFLD(HSRREF/@@BGFL *LIBL/HSPREF)
     A  36
     AO 56                                  DSPATR(PC)
     A  36                                  DSPATR(RI)
     A N36                                  DSPATR(HI)
     A                                  8 55'(Blank for all)'
     A                                  9  1'Critical flag ....'
     A                                  9 20'?'
     A            D1CRITFL  R        B  9 22REFFLD(HSRREF/@@CRITFL *LIBL/HSPREF)
     A  37
     AO 57                                  DSPATR(PC)
     A  37                                  DSPATR(RI)
     A N37                                  DSPATR(HI)
     A                                  9 55'(Blank for all)'
     A*
     A*----------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*----------------------------------------------------------------
     A          R HP4708K1
     A*%%TS  SD  20020131  142842  ISLRAMS     REL-V4R4M0  5769-PW1
     A                                      WINDOW(HP4708EC)
     A                                      TEXT('COMMAND KEYS')
     A                                      OVERLAY
     A  43                             10  2'Message program failed. Call IS.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 11  1'F3=Exit'
     A                                 11 10'F4=Prompt'
     A                                 11 21'F8=Submit'
     A                                 11 32'F17-Print Options'
      *
      *----------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *----------------------------------------------------------------
     A          R HP4708ES
     A                                      TEXT('Error message subfile')
     A                                      SFL SFLMSGRCD(12)
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
     A*
     A*----------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT WITH WINDOW DEFINITION
     A*----------------------------------------------------------------
     A          R HP4708EC                  SFLCTL(HP4708ES)
     A*%%TS  SD  20000628  103649  ISLRAMS     REL-V4R2M0  5769-PW1
     A                                      TEXT('Error message subfile')
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A                                      WINDOW(*DFT 13 70)
     A                                      WDWBORDER((*DSPATR RI) (*CHAR '    -
     A                                          '))
     A            MSGPGM                    SFLPGMQ(10)
     A*
     A*----------------------------------------------------------------
     A* ASSUME FORMAT - REQUIRED FOR WINDOWS
     A*----------------------------------------------------------------
     A*
     A          R ASSUME
     A*%%TS  SD  20000627  095625  ISLRAMS     REL-V4R2M0  5769-PW1
     A                                      TEXT('ASSUME FORMAT FOR WINDOWS')
     A                                      ASSUME
     A                                      OVERLAY
     A                                 24 79' '
