      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Hog Production System
      * PROGRAM:     HP279 - Synon: Check Delivery Schedule Header
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     01/22/03
      *
      * FUNCTION:    In January 2003, a SYNON/WEB system was written that allowed users to
      *              create/update movements into SYNON via the WEB.  The SYNON/WEB data is
      *              interfaced into HPS.  So, data will exist in multiple systems.
      *
      *              Alice hopes to keep the data 'in synch' in the HPS/Synon/WEB systems up
      *              to the point that movements are received at the plant.
      *
      *              So, this program is called from multiple HPS Sales Movement functions.
      *              We are simply trying to make the determination of whether the user
      *              is allowed to mess with movements in HPS or should be going at them
      *              from the SYNON/WEB side.
      *
      *
      *************************************************************************************
      * MODIFICATIONS:
      *************************************************************************************
      * DATE      PROGRAMMER
      *
      * 04/08/09  LeAnne Ramsey
      *           Users could not delete Scheduled Movements with a Scheduled Kill Date of
      *           03/28/09. Using ViewPoint, I verified that none of the movements were in
      *           HPE. Then, I temporarily changed this edit to allow Garna Elston to delete
      *           the movements.
      *
      * 03/21/13  LeAnne Ramsey  (C2517)
      *           When the office was closed back in February, the plant missed some Kill
      *           Dates. HPE was all screwed up; Susan Mason fixed HPE enough to get HPE
      *           going. But, now, the HPS users have movements from that screw up that they
      *           need to set to FP=Fully Paid. The edits in this program are keeping them
      *           to set the movements to Fully Paid.
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS:
      ****************************************************************
      *
     Fpladcpl0  if   e           k disk
      *  Synon: Delivery schedule header
      *
      /eject
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      *
      ****************************************************************
      * ARRAYS AND TABLES
      ****************************************************************
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Parm fields
      *
     D xxokforhps      s              1
     D xxccyymmdd      s              8  0
     D xxsynondt       s              7  0
      *
      *
     D wkcono          s              3  0 inz(360)
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * MAINLINE
      ****************************************************************
      *
      * SYNON has to have the primary key of 'company'. So, we have created
      * a bogus 'company' key field and set it to 360.  Our true check is
      * for records with a match on the incoming 'date' value.
      *
      * Per Alice B. on 2/03/03,
      *        1) if there is NO record in the SYNON file,
      *           it is OK for the user to mess with stuff in HPS
      *
      *        2) if there is a record and it has a status of 10 or greater,
      *           it is OK for the user to mess with stuff in HPS
      *
      *        3) if there is a record but the status is less than 10,
      *           the user has to mess with stuff in SYNON...not HPS
      *
     C     key01         chain     pladcpl0                           92
      *
     C                   select
     C                   when      *in92 = *on
     C                   move      'Y'           xxokforhps
      *
     C                   when      adwxst >= '10'
     C                   move      'Y'           xxokforhps
      *
     C                   other
     C                   move      'N'           xxokforhps
     C                   endsl
      *
     C                   seton                                        lr
      /eject
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
     C     *entry        plist
     C                   parm                    xxccyymmdd
     C                   parm                    xxokforhps
      *
      *
     C     key01         klist
     C                   kfld                    wkcono
     C                   kfld                    xxsynondt
      *
      *
      * Format incoming date into Synon format.
      *
     C                   call      'HP8008'
     C                   parm                    xxccyymmdd
     C                   parm                    xxsynondt
      *
     C                   endsr
      /eject
