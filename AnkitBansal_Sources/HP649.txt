      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      HOG PRODUCTION
      * PROGRAM:     HP649 - DOA REPORT BY FARM
      * PROGRAMMER:  LEANNE FEDOR
      * CREATED:     10/19/95
      *
      * FUNCTION:    PRINTS THE DOA REPORT BY FARM--SUMMARY.
      *              IT PRINTS A LINE FOR EACH FARM.
      *
      *              (NOTE: THE USERS HAVE NOT YET REQUESTED A 'DETAIL'
      *              VERSION OF THIS REPORT.)
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 04/13/98  LeAnne Fedor
      *           Recompile only. New field 'state' added to farm site file.
      *
      * 06/05/01  LeAnne Fedor
      *           Recompile only. New field 'multisite' added to Farm Site file.
      *
      * 06/28/01  LeAnne Fedor
      *           Recompile only.
      *           Three fields (manager/supervisor/multisite) renamed in Farm Site file.
      *
      * 09/06/01  LeAnne Fedor
      *           'Group type' added as a new report option. Also, new 'square feet'
      *           fields added to Farm Site file requiring a recompile.
      *
      * 10/16/13  LeAnne Ramsey (E2831)
      *           Recompile only. Added field 'MTech Reference'.
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     FHSP010    IF   E           K DISK
      *    PRODUCTION TYPE           (KEY: PTPTCD)
      *
     FHSP011    IF   E           K DISK
      *    PRODUCTION PHASE          (KEY: PPPPCD)
      *
     FHSP018    IF   E           K DISK
      *    FARM SITE                 (KEY: FSFSCD)
      *
     FHSP349    IF   E           K DISK
      *    WORKFILE                  (KEY: WFPTCD WFPPCD WFFSCD)
      *
     FQPRINT    O    F  132        PRINTER OFLIND(*INOF)
      /EJECT
      ****************************************************************
      * TABLE AND ARRAY SPECIFICATIONS
      ****************************************************************
      *
      *  PRINT ARRAYS FOR TOTALS
      *
     D LBS             S             11  0 DIM(4)
     D HD              S             11  0 DIM(4)
      /EJECT
      ****************************************************************
      * INPUT SPECS
      ****************************************************************
      *
      *---------------------------------------------------------------
      * STANDARD PROGRAM STATUS DATA STRUCTURE
      *---------------------------------------------------------------
      *    EXTERNALLY DEFINED AS UTPGFR (RECORD FORMAT: PGMDSR)
     D PGMDS         ESDS                  EXTNAME(UTPGFR)
      *
      *---------------------------------------------------------------
      *  NAMED CONSTANTS
      *---------------------------------------------------------------
      *
     D YES             C                   CONST('Y')
     D NO              C                   CONST('N')
     D UNKNWN          C                   CONST('UNKNOWN')
     D ALL             C                   CONST('ALL')
      *
      *----------------------------------------------------------------
      * LDA - REPORT SELECTIONS
      *----------------------------------------------------------------
      *
     D                UDS
     D  LDFSBO                 1      5
     D  LDFYMD                 6     13  0
     D  LDTYMD                14     21  0
     D  LDPPCD                22     26
     D  LDFSCD                27     31  0
     D  LDSDFL                32     32
     D  LDGTCD                47     47
     D  LDPTCD                48     52
     D  LDHGSN                53     59  0
     D  LDFMDY                60     65  0
     D  LDTMDY                66     71  0
      *
     D  LDBODS                72    101
     D  LDPTDS               102    111
     D  LDPPDS               112    141
     D  LDFSNM               142    166
     D  LDHGCD               167    173
     D  LDRPFL               174    174
     D  LDSDDS               175    181
     D  LDGTDS               182    196
      *
     D  LDOUTU               401    410
     D  LDHOLD               411    411
     D  LDCOPY               412    412
     D  LDOUTQ               413    422
      /EJECT
      ****************************************************************
      * MAIN LINE
      ****************************************************************
      *
      * READ AND PRINT ALL WORKFILE RECORDS
      *
     C     *IN90         DOUEQ     *ON                                          MAIN DO
     C                   READ      HSP349                                 90
     C     *IN90         IFEQ      *OFF                                         IF NOT EOF
      *
     C     FIRST         IFEQ      YES                                          IF FIRST
     C                   EXSR      $PTDS
     C                   EXSR      $PPDS
     C                   EXSR      $HRHDR
     C                   MOVEL     WFPTCD        SVPTCD
     C                   MOVEL     WFPPCD        SVPPCD
     C                   MOVEL     NO            FIRST
     C                   ENDIF                                                  IF FIRST
      *
      *------------------------------------------------------------
      * CONTROL BREAK LOGIC
      *------------------------------------------------------------
      *
     C                   SELECT
     C     WFPTCD        WHENNE    SVPTCD                                       PROD TYPE
     C                   EXSR      $PPTOT
     C                   EXSR      $PTTOT
     C                   EXSR      $PTDS
     C                   EXSR      $PPDS
     C                   EXSR      $HRHDR
      *
     C     WFPPCD        WHENNE    SVPPCD                                       PROD PHASE
     C                   EXSR      $PPTOT
     C                   EXSR      $PPDS
     C                   EXSR      $HRHDR
     C                   ENDSL
      *
      *------------------------------------------------------------
      * DETAIL PROCESSING
      *------------------------------------------------------------
      *
      * ACCUMULATE TOTALS
     C                   ADD       WFDOLB        LBS
     C                   ADD       WFDOHD        HD
      *
      * RETRIEVE FARM NAME
      *
     C     WFFSCD        CHAIN     HSP018                             92
     C     *IN92         IFEQ      *OFF
     C                   MOVEL(P)  FSFSNM        WKFSNM
     C                   ELSE
     C                   MOVEL(P)  UNKNWN        WKFSNM
     C                   ENDIF
      *
     C     *INOF         IFEQ      *ON
     C                   EXSR      $HRHDR
     C                   ENDIF
     C                   EXCEPT    DTLINE
      *
     C                   ENDIF                                                  IF NOT EOF
     C                   ENDDO                                                  MAIN DO
      *
      *----------------------------------------------------------------
      * EOF PROCESSING
      *----------------------------------------------------------------
      *  IF NO DETAIL RECORDS MATCHED THE SELECTION CRITERIA, THEN
      *  PRINT THE STANDARD 'NO DATA...' ERROR MESSAGE
      *
     C     FIRST         IFEQ      YES                                          IF NO RECS
     C                   EXCEPT    HRHDR
     C                   EXCEPT    NODATA
     C                   ELSE
      *
     C                   EXSR      $PPTOT
     C                   EXSR      $PTTOT
     C                   EXCEPT    TRTOT
     C                   ENDIF                                                  IF NO RECS
      *
     C                   SETON                                        LR
      /EJECT
      *---------------------------------------------------------------
      * $HRHDR - PRINT REPORT HEADINGS
      *---------------------------------------------------------------
      *
     C     $HRHDR        BEGSR
      *
     C                   EXCEPT    HRHDR
     C                   EXCEPT    HTHDR
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      *  $PTDS - RETRIEVE THE PRODUCTION TYPE DESCRIPTION
      *----------------------------------------------------------------
      *
     C     $PTDS         BEGSR
      *
     C     WFPTCD        CHAIN     HSP010                             91
     C     *IN91         IFEQ      *OFF                                         IF FOUND
     C                   MOVEL     PTPTDS        HTPTDS
     C                   ELSE
     C                   MOVEL(P)  UNKNWN        HTPTDS
     C                   ENDIF                                                  IF FOUND
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $PPDS - RETRIEVE THE PRODUCTION PHASE DESCRIPTION
      *----------------------------------------------------------------
      *
     C     $PPDS         BEGSR
      *
     C     WFPPCD        CHAIN     HSP011                             91
     C     *IN91         IFEQ      *OFF                                         IF FOUND
     C                   MOVEL     PPPPDS        HTPPDS
     C                   ELSE
     C                   MOVEL(P)  UNKNWN        HTPPDS
     C                   ENDIF                                                  IF FOUND
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      *  $PTTOT - PRODUCTION TYPE TOTALS
      *----------------------------------------------------------------
      *
     C     $PTTOT        BEGSR
      *
     C     *INOF         IFEQ      *ON
     C                   EXSR      $HRHDR
     C                   ENDIF
      *
     C                   EXCEPT    TTTOT
     C                   MOVEL     WFPTCD        SVPTCD
      *
     C                   ENDSR
      *
      *----------------------------------------------------------------
      * $PPTOT - PRODUCTION PHASE TOTALS
      *----------------------------------------------------------------
      *
     C     $PPTOT        BEGSR
      *
     C     *INOF         IFEQ      *ON
     C                   EXSR      $HRHDR
     C                   ENDIF
      *
     C                   EXCEPT    TPTOT
     C                   MOVEL     WFPPCD        SVPPCD
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * *INZSR - INITIALIZATION SUBROUTINE
      *---------------------------------------------------------------
      *
     C     *INZSR        BEGSR
      *
      * DEFINE PRINT FIELDS FOR PRODUCTION TYPE HEADING
      *
     C     *LIKE         DEFINE    PTPTDS        HTPTDS
     C     *LIKE         DEFINE    PPPPDS        HTPPDS
      *
      * DEFINE PRINT FIELD FOR FARM NAME ON SUMMARY VERSION
      *
     C     *LIKE         DEFINE    FSFSNM        WKFSNM
      *
      * DEFINE SAVE FIELDS FOR CONTROL BREAKS
      *
     C     *LIKE         DEFINE    WFPTCD        SVPTCD
     C     *LIKE         DEFINE    WFPPCD        SVPPCD
      *
      *----------------------------------------------------------------
      * SET UP REPORT HEADINGS
      *----------------------------------------------------------------
      *
      * BUSINESS OFFICE
      *
     C     LDFSBO        IFEQ      *BLANK
     C                   MOVEL     ALL           LDFSBO
     C                   ENDIF
      *
      * PRODUCTION TYPE
      *
     C     LDPTCD        IFEQ      *BLANK
     C                   MOVEL     ALL           LDPTCD
     C                   ENDIF
      *
      * ALL PRODUCTION PHASES
      *
     C     LDPPCD        IFEQ      *BLANK
     C                   MOVEL     ALL           LDPPCD
     C                   ENDIF
      *
      *  ALL FARM SITES
      *
     C     LDFSCD        IFEQ      0
     C                   SETON                                        81
     C                   ENDIF
      *
      * DETERMINE IF USER SELECTED SPECIFIC HOG GROUP OR "ALL"
      *
     C     LDHGCD        IFEQ      *BLANK                                       IF ALL GROUP
     C                   MOVEL     ALL           LDHGCD
     C                   ENDIF                                                  IF ALL GROUP
      *
      * Group type
     C                   if        ldgtcd = *blank
     C                   seton                                        82
     C                   endif
      *
      * SET UP HEADING UNDERLINE FIELD FOR PRINT
      * RETRIEVE TIME FOR REPORT HEADING
      * SET UP A FIRST TIME FLAG
      *
     C                   MOVEL     *ALL'-'       DASH            132
     C                   TIME                    RTIME             6 0
     C                   MOVEL     YES           FIRST             1
      *
     C                   ENDSR
      /EJECT
      *-------------------------------------------------------------
      * REPORT HEADING LINES
      *-------------------------------------------------------------
      *
     OQPRINT    E            HRHDR          1 01
     O                       SDPGM               10
     O                                           68 'HOG PRODUCTION SYSTEM'
     O                                          122 'DATE'
     O                       UDATE         Y    132
      *
     O          E            HRHDR          1
     O                       SDUSR               10
     O                                           66 'DOA REPORT BY FARM'
     O                                          122 'TIME'
     O                       RTIME              132 '  :  :  '
      *
     O          E            HRHDR          1
     O                       LDSDDS              61
     O                                          122 'PAGE'
     O                       PAGE          Z    132
      *
     O          E            HRHDR       1  1
     O                                           21 'BUSINESS OFFICE .....'
     O                       LDFSBO              28
     O                       LDBODS              63
      *
     O          E            HRHDR          1
     O                                           21 'FROM RECEIVED DATE ..'
     O                       LDFMDY        Y     31
      *
     O          E            HRHDR          1
     O                                           21 'TO RECEIVED DATE ....'
     O                       LDTMDY        Y     31
      *
     O          E            HRHDR          1
     O                                           21 'PRODUCTION TYPE .....'
     O                       LDPTCD              28
     O                       LDPTDS              43
      *
     O          E            HRHDR          1
     O                                           21 'PRODUCTION PHASE ....'
     O                       LDPPCD              28
     O                       LDPPDS              63
      *
      *
     O          e            hrhdr          1
     O                                           21 'GROUP TYPE ..........'
     O              n82      ldgtcd              24
     O               82                          26 'ALL'
     O                       ldgtds              48
      *
      *
     O          E            HRHDR          1
     O                                           21 'FARM SITE ...........'
     O              N81      LDFSCD        M     29
     O               81                          26 'ALL'
     O                       LDFSNM              58
      *
     O          E            HRHDR          2
     O                                           21 'HOG GROUP ...........'
     O                       LDHGCD              30
      *
      *-------------------------------------------------------------
      * COLUMN HEADING LINES
      *-------------------------------------------------------------
      *
     O          E            HRHDR          1
     O                                            5 'FARM'
     O                                           52 'DOA'
     O                                           74 'DOA'
      *
     O          E            HRHDR          1
     O                                            5 'SITE'
     O                                           52 'HEAD'
     O                                           74 'POUNDS'
      *
     O          E            HRHDR          1
     O                       DASH               132
      *
      *-------------------------------------------------------------
      * DETAIL LINE FOR FARM
      *-------------------------------------------------------------
      *
     O          E            DTLINE         1
     O                       WFFSCD        MB     6
     O                       WKFSNM         B    33
     O                       WFDOHD        KB    53
     O                       WFDOLB        KB    75
      *
      *-------------------------------------------------------------
      * PRODUCTION TYPE / PRODUCTION PHASE HEADING
      *-------------------------------------------------------------
      *
     O          E            HTHDR       1  2
     O                       HTPTDS              15
     O                                           17 '-'
     O                       HTPPDS              48
      *
      *-------------------------------------------------------------
      * PRODUCTION PHASE TOTAL LINE
      *-------------------------------------------------------------
      *
     O          E            TPTOT       1  1
     O                                           21 '***'
     O                       SVPPCD              27
     O                       HD(2)         KB    53
     O                       LBS(2)        KB    75
     O                                           79 '***'
      *
      *-------------------------------------------------------------
      * PRODUCTION TYPE TOTAL LINE
      *-------------------------------------------------------------
      *
     O          E            TTTOT          1
     O                                           21 '****'
     O                       SVPTCD              27
     O                       HD(3)         KB    53
     O                       LBS(3)        KB    75
     O                                           80 '****'
      *
      *-------------------------------------------------------------
      * REPORT TOTALS
      *-------------------------------------------------------------
      *
     O          E            TRTOT          1
     O                                           29 'REPORT TOTAL'
     O                       HD(4)         KB    53
     O                       LBS(4)        KB    75
      *
      *-------------------------------------------------------------
      * NO DATA MESSAGE LINE
      *-------------------------------------------------------------
      *
     O          E            NODATA         1
     O                                           19 'NO DATA MEETS YOUR'
     O                                           38 'SELECTION CRITERIA'
