/***************************************************************** */
/*                                                                 */
/*  PROGRAM NUMBER:  HP7001CL                                      */
/*  PROGRAM NAME:    LISTING OF BGF FARROWING AND PREGNANCY DATA   */
/*                   FARM SITE                                     */
/*  PROGRAMMER:      LEANNE FEDOR                                  */
/*  CREATE DATE:     09/05/00                                      */
/*                                                                 */
/*  FUNCTION:        THIS CL IS SUBMITTED WITH QCMDEXC FROM        */
/*                   HP7001-VIEW BGF FARROWING & PREGNANCY DATA.   */
/*                   OPTIONS ARE PASSED THROUGH THE LDA.           */
/*                                                                 */
/*******************************************************************/
/*  MODIFIED:                                                      */
/*******************************************************************/

             PGM

/* DEFINE THE VARIABLE REQUIRED IN THE COMMAND TO OVERRIDE THE     */
/* PRINT FILE TO A LASER PRINTER                                   */

             DCL        VAR(&CMD) TYPE(*CHAR) LEN(512)
             DCL        VAR(&QDATA) TYPE(*CHAR) LEN(134)

             DCL        VAR(&LDEVYR) TYPE(*DEC) LEN(4)
             DCL        VAR(&LDEVWK) TYPE(*DEC) LEN(2)
             DCL        VAR(&LDEVFL) TYPE(*CHAR) LEN(1)
             DCL        VAR(&LDFSCD) TYPE(*DEC) LEN(5)
             DCL        VAR(&LDSVYR) TYPE(*DEC) LEN(4)
             DCL        VAR(&LDSVWK) TYPE(*DEC) LEN(2)

             DCL        VAR(&HOLD) TYPE(*CHAR) LEN(10)
             DCL        VAR(&COPYS) TYPE(*DEC) LEN(2)
             DCL        VAR(&OUTQ) TYPE(*CHAR) LEN(10)

             CHGVAR     VAR(&LDEVYR) VALUE(%SST(*LDA 1 4))
             CHGVAR     VAR(&LDEVWK) VALUE(%SST(*LDA 5 2))
             CHGVAR     VAR(&LDEVFL) VALUE(%SST(*LDA 7 1))
             CHGVAR     VAR(&LDFSCD) VALUE(%SST(*LDA 8 5))
             CHGVAR     VAR(&LDSVYR) VALUE(%SST(*LDA 15 4))
             CHGVAR     VAR(&LDSVWK) VALUE(%SST(*LDA 19 2))


/*------------------------------------------------------------------*/
/* BUILD QUERY                                                      */
/*------------------------------------------------------------------*/

/*  SET UP A DEFAULT STARTING POINT FOR THE QUERY                */

             CHGVAR     VAR(%SST(&QDATA 1 12)) VALUE('BEFSCD *NE 0')

/*  IF FARM                                                      */

             IF         COND(&LDFSCD *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 14 15)) VALUE('*AND BEFSCD +
                          *EQ')
             CHGVAR     VAR(%SST(&QDATA 30 5)) VALUE(&LDFSCD)
             ENDDO

/*  IF EVENT YEAR                                                */

             IF         COND(&LDEVYR *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 36 15)) VALUE('*AND BEEVYR +
                          *EQ')
             CHGVAR     VAR(%SST(&QDATA 52 4)) VALUE(&LDEVYR)
             ENDDO

/*  IF EVENT WEEK                                                */

             IF         COND(&LDEVWK *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 57 15)) VALUE('*AND BEEVWK +
                          *EQ')
             CHGVAR     VAR(%SST(&QDATA 73 2)) VALUE(&LDEVWK)
             ENDDO

/*  IF SERVICE YEAR                                              */

             IF         COND(&LDSVYR *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 76 15)) VALUE('*AND BESVYR +
                          *EQ')
             CHGVAR     VAR(%SST(&QDATA 92 4)) VALUE(&LDSVYR)
             ENDDO

/*  IF SERVICE WEEK                                              */

             IF         COND(&LDSVWK *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 97 15)) VALUE('*AND BESVWK +
                          *EQ')
             CHGVAR     VAR(%SST(&QDATA 113 2)) VALUE(&LDSVWK)
             ENDDO

/*  IF EVENT FLAG                                                */

             IF         COND(&LDEVFL *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 116 17)) VALUE('*AND BEEVFL *EQ "')
             CHGVAR     VAR(%SST(&QDATA 133 1)) VALUE(&LDEVFL)
             CHGVAR     VAR(%SST(&QDATA 134 1)) VALUE('"')
             ENDDO


/*------------------------------------------------------------------*/
/* RUN QUERY OVER THE BGF WEEKLY PRODUCTION PREVIOUS EVENT FILE      */
/*------------------------------------------------------------------*/

             OVRDBF     FILE(HSP093) SHARE(*YES)
             OPNQRYF    FILE((HSP093)) QRYSLT(&QDATA) KEYFLD(*FILE) +
                          SEQONLY(*YES)

/*------------------------------------------------------------------*/
/*  SET UP PRINT FILE OVERRIDES                                      */
/*------------------------------------------------------------------*/

/* SET PARAMETERS FROM LDA                                           */

 HOLD:       IF         COND(%SST(*LDA 411 1) = 'Y') THEN(CHGVAR +
                          VAR(&HOLD) VALUE(*YES))
             ELSE       CMD(CHGVAR VAR(&HOLD) VALUE(*NO))

 COPYS:      CHGVAR     VAR(&COPYS) VALUE(%SST(*LDA 412 1))
             IF         COND(&COPYS = 0) THEN(CHGVAR VAR(&COPYS) +
                          VALUE(1))

 OUTQ:       CHGVAR     VAR(&OUTQ) VALUE(%SST(*LDA 413 10))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(%SST(*LDA 401 10)))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(*JOB))


/*------------------------------------------------------------------*/
/* OVERRIDE PRINT FILE                                              */
/*------------------------------------------------------------------*/

/* USE THE CUSTOMIZED SEABOARD COMMAND TO OVERRIDE THE PRINT FILE.  */
/* THIS COMMAND ALLOWS THE REPORT TO BE GENERATED ON A STANDARD LINE*/
/* PRINTER OR A LASER PRINTER. TO USE THIS OVERRIDE YOU MUST DECLARE*/
/* A COMMAND VARIABLE AND FOLLOW THE OVERRIDE WITH A CALL TO        */
/* QCMDEXC.                                                         */

/* THIS REPORT IS CONDENSED PRINT AT 15 CPI; SO, ON A LASER PRINTER */
/* FORCE IT TO 'LANDSCAPE' ON LETTER PAPER                          */

             SBDPRTOVR  CMDVAR(&CMD) FILE(QPRINT) OUTQ(&OUTQ) +
                          WIDTH(80) LANDSCAPE(*NO) DRAWER(*PRTF) +
                          COPIES(&COPYS) HOLD(&HOLD)

             CALL       PGM(QCMDEXC) PARM(&CMD 512)


/*------------------------------------------------------------------*/
/*  CALL REPORT PROGRAM                                              */
/*------------------------------------------------------------------*/

             CALL       PGM(HP6001)

             CLOF       OPNID(HSP093)
             DLTOVR     FILE(*ALL)

             ENDPGM

