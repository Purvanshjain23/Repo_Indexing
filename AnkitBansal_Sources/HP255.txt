      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Hog Production System
      * PROGRAM:     HP255
      * TITLE:       Combine Kill Data before HPE to HPS Copy
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     05/26/04
      *
      * FUNCTION:  When there are holdovers, HPE has 2 records with different kill dates:
      *
      *            The first record has the DOA/Yard Dead/Condemned head. This record has
      *            no 'live weight' and the 'kill date' is actually the 'arrival' date.
      *
      *            The second record has the head that were actually slaughtered on the next
      *            day. This record has 'live weight' and the 'kill date' is truly the 'kill
      *            date'.
      *
      *            Alice does not want 2 separate records in HPS; she wants to combine the
      *            a) HPE no live weight record with the b) HPE live weight record before we
      *            write to HPS.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      * 11/02/20  ISE  (H16853)
      *           Increased the length of Buy Order Number field from 5 to 7.
      ****************************************************************
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fhsl311a   uf   e           k disk    rename(wdrec:wdreca)
      *   Workfile: HPE/HPS Interface--Movement Detail
      *   (Logical selects only records with 'live weight' of zero)
      *
      *
     Fhsl311b   uf   e           k disk    rename(wdrec:wdrecb) prefix(p1)
      *   Workfile: HPE/HPS Interface--Movement Detail
      *   (Logical omits all records with 'live weight' of zero)
      *
      *
     Fhsl313a   uf   e           k disk    rename(warec:wareca)
      *   Workfile: HPE/HPS Interface--Deduction/Add-ons
      *
      *
     Fhsl313b   uf a e           k disk    rename(warec:warecb) prefix(p2)
      *   Workfile: HPE/HPS Interface--Deduction/Add-ons
      *
      /EJECT
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      ****************************************************************
      * ARRAYS AND TABLES
      ****************************************************************
      *
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * MAINLINE
      ****************************************************************
      *
      * Read/process all Movement Detail workfile records with 'live weight' of zero.
      * (The logical file selects out these records.)
      *
     C     *loval        setll     hsl311a
      *
     C                   dou       *in90 = *on                                  Do zeros
     C                   read      hsl311a                                90
     C                   if        *in90 = *off                                 If read
      *
      * See if you have a record with Live Weight for this same check/
      * movement/hog group/tattoo. If so, update this 'live weight'
      * record with the values from the zero weight record and
      * then delete the zero weight record.
      *
     C     key01         chain     hsl311b                            92
     C                   if        *in92 = *off                                 If hit
      * HEAD:
      *    live
     C                   add       wdcwnb        p1wdcwnb
      *    DOA
     C                   add       wdcxnb        p1wdcxnb
      *    yard dead
     C                   add       wdfcnb        p1wdfcnb
      *    condemned
     C                   add       wdfdnb        p1wdfdnb
      *
      * AMOUNTS:
      *    base market price
     C                   add       wdanpr        p1wdanpr
      *    sort discount
     C                   add       wdayva        p1wdayva
      *    yield gain/loss
     C                   add       wdawva        p1wdawva
      *    lean add/deduct
     C                   add       wdfdnb        p1wdfdnb
      *    payment gross
     C                   add       wdcbpr        p1wdcbpr
      *    payment net
     C                   add       wdccpr        p1wdccpr
      *
      * Update the 'live weight' record
      *
     C                   update    wdrecb
      *
      * Before you delete the 'zero weight' record, go combine
      * any/all Deduction/add-on records----(we don't expect any!)
      *
     C                   exsr      $dedadd
      *
      * Now, delete the 'zero weight' record.
      *
     C                   delete    wdreca
     C                   endif                                                  If hit
      *
     C                   endif                                                  If read
     C                   enddo                                                  Do zeros
      *
      * EOF processing
     C                   seton                                        lr
      /eject
      *---------------------------------------------------------------
      * Combine deduction/add-on records
      *---------------------------------------------------------------
      *
      * There is hardly EVER a Deduction record for a 'zero weight' record.
      * Infact, I'm not sure there EVER is for Internal hogs. But, just to be safe,
      * we will find/combine any/all deduction records.
      *
     C     $dedadd       begsr
      *
      * Read all deduction records for the 'zero weight' record and combine
      * each with the corresponding Deduction for the 'live weight' record.
      * (The only difference between the zero weight/live weight records
      *  would be the Kill Date--we will use the Kill Date from the Live Weight
      *  record when combining.)
      *
     C     key02         setll     hsl313a
      *
     C                   dou       *in91 = *on                                  Do loop
     C     key02         reade     hsl313a                                91
     C                   if        *in91 = *off                                 If read
      *
      * Combine with deductions for 'live weight' record
      *
     C     key03         chain     hsl313b                            92
     C                   if        *in92 = *off                                 If hit
     C                   add       waa0va        p2waa0va
     C                   update    warecb
     C                   else
      *
     C                   z-add     p1wdb0dt      p2wab0dt
     C                   move(p)   wabqcd        p2wabqcd
     C                   z-add     wahgsn        p2wahgsn
     C                   z-add     wahmnb        p2wahmnb
     C                   z-add     wabnnb        p2wabnnb
     C                   z-add     wabonb        p2wabonb
     C                   z-add     wacvnb        p2wacvnb
     C                   z-add     waa0va        p2waa0va
      *
     C                   write     warecb
     C                   endif                                                  If hit
      *
      * Delete the Deduction record for the 'zero weight' record.
      *
     C                   delete    wareca
      *
     C                   endif                                                  If read
     C                   enddo                                                  Do loop
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Key lists
      *
     C     key01         klist
     C                   kfld                    wdlpnb                         Check number
     C                   kfld                    wdoxnb                         Movement
     C                   kfld                    wdhgcd                         Hog group
     C                   kfld                    wdcvnb                         Tattoo
      *
     C     key02         klist
     C                   kfld                    wdhgsn                         Hog group
     C                   kfld                    wdbnnb                         Buy order nbr
     C                   kfld                    wdbonb                         Load number
     C                   kfld                    wdcvnb                         Tattoo number
     C                   kfld                    wdb0dt                         Kill date
      *
     C     key03         klist
     C                   kfld                    p1wdhgsn                       Hog group
     C                   kfld                    p1wdbnnb                       Buy order nbr
     C                   kfld                    p1wdbonb                       Load number
     C                   kfld                    p1wdcvnb                       Tattoo number
     C                   kfld                    p1wdb0dt                       Kill date
     C                   kfld                    wabqcd                         Ded/add code
      *
     C                   endsr
      /EJECT
