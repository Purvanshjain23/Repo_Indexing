/********************************************************************/
/*                                                                  */
/*  SYNOPSIS - BUILD CUSTOMER/ITEM WORK FILE                        */
/*                                                                  */
/*                                                                  */
/*  COMPANY   - SEABOARD COPRORATION                                */
/*  SYSTEM    - ORDER MANAGEMENT                                    */
/*  USER NAME - MARK ARENSBERG                                      */
/*  DATE      - 06/30/98                                            */
/*                                                                  */
/*      PARAMETERS RECEIVED:                                        */
/*                                                                  */
/*          &COMPL      - COMPANY NUMBER - BLANK FOR ALL            */
/*          &FDATEL     - FROM DATE                                 */
/*          &TDATEL     - TO DATE                                   */
/*          &CUSTL      - CUSTOMER                                  */
/*          &SHPCL      - SHIP TO CUSTOMER                          */
/*          &SLSPR      - SALESPERSON                               */
/*          &EXPSLS     - EXPORT SALES                              */
/*          &FLAG1      - INCLUDE ALL PRODUCTS                      */
/*          &FLAG2      - INCLUDE CM/DM                             */
/********************************************************************/

 PDRCPCLP:   PGM        PARM(&COMPL &FDATEL &TDATEL &CUSTL +
                          &EXPSLS &SHPCL &SLSPR +
                          &FLAG1 &FLAG2 &STATE)

        DCL    &COMP      *DEC    (3 0)           /*  COMPANY #     */
        DCL    &COMPL     *DEC                    /*  FOR PASSING   */
        DCL    &FDATE     *DEC    (7 0)          /*  FROM DATE      */
        DCL    &FDATEL    *DEC                   /*  FOR PASSING    */
        DCL    &TDATE     *DEC    (7 0)          /*  THROUGH DATE   */
        DCL    &TDATEL    *DEC                   /*  FOR PASSING    */
        DCL    &COPIES    *DEC    (3 0)           /*  # OF COPIES   */
        DCL    &COPIESL   *DEC                    /*  FOR PASSING   */
        DCL    &CUST      *DEC    (7 0)          /*  A/R CUSTOMER   */
        DCL    &CUSTL     *DEC                   /*  FOR PASSING    */
        DCL    &SHPCS     *DEC    (7 0)          /*  SHIP TO CUST   */
        DCL    &SHPCL     *DEC                   /*  FOR PASSING    */
        DCL    &SLSPR     *CHAR   3              /*  SALESPERSON    */
        DCL    &COMPA     *CHAR   3               /*  COMPANY ALPHA */
        DCL    &FDATEA    *CHAR   7               /*  FROM DT ALPHA */
        DCL    &TDATEA    *CHAR   7               /*  TO   DT ALPHA */
        DCL    &CUSTA     *CHAR   7               /*  CUSTOMER      */
        DCL    &SHPCA     *CHAR   7               /*  SHIP TO CUST  */
        DCL    &EXPSLS    *CHAR   1               /*  EXPORT SALES  */
        DCL    &FLAG1     *CHAR   1               /*  INCL. ALL PROD*/
        DCL    &FLAG2     *CHAR   1               /*  INCL. CM/DM   */
        DCL    &STATE     *CHAR   2
        DCL    &COMMAND    *CHAR   (512)

/* SEND PROGRAM MESSAGE PARAMETERS  */

        DCL    &USER      *CHAR   10              /*  USER ID       */
        DCL    &MSGRTN    *CHAR   10              /*  MSG QUEUE     */
        DCL     &A        *CHAR   20              /* QRY FROM DATE  */
        DCL     &B        *CHAR   25              /* QRY TO DATE    */
        DCL     &C        *CHAR   25              /* QRY COMPANY    */
        DCL     &D        *CHAR   25              /* QRY EXPORT SLS */
        DCL     &E        *CHAR   25              /* QRY CUSTOMER   */
        DCL     &F        *CHAR   25              /* QRY SHIP CUST  */
        DCL     &G        *CHAR   25              /* QRY SLSPERSON  */
        DCL     &H        *CHAR  195              /* QRY ALL        */
        DCL     &I        *CHAR   25              /* QRY STATE      */
        DCL     &DATA     *CHAR   10              /* DATA FILES     */
        DCL     &DATAH    *CHAR   10              /* HIST FILES     */

             CHGVAR    VAR(&COMP) VALUE(&COMPL) /* Change to length +
                          for print program */
             CHGVAR    VAR(&COMPA) VALUE(&COMP) /* Change to alpha +
                          for message data */
             CHGVAR    VAR(&FDATE) VALUE(&FDATEL) /* Change to length +
                          for print program */
             CHGVAR    VAR(&FDATEA) VALUE(&FDATE) /* Change to alpha +
                          for query data */
             CHGVAR    VAR(&TDATE) VALUE(&TDATEL) /* Change to length +
                          for print program */
             CHGVAR    VAR(&TDATEA) VALUE(&TDATE) /* Change to alpha +
                          for query data */
             CHGVAR    VAR(&CUST) VALUE(&CUSTL) /* Change to length +
                          for print program */
             CHGVAR    VAR(&CUSTA) VALUE(&CUST) /* Change to alpha +
                          for query data */
             CHGVAR    VAR(&SHPCS) VALUE(&SHPCL) /* Change to length +
                          for print program */
             CHGVAR    VAR(&SHPCA) VALUE(&SHPCS) /* Change to alpha +
                          for query data */

/*********************************************************************/
/*   CLEAR CUSTOMER/ITEM WORK FILE                                  */
/*********************************************************************/

             CLRPFM     FILE(PDMWCPP)

/*********************************************************************/
/*   CREATE OPEN QUERY FILE TO BE USED FOR CUSTOMER/ITEM WORK FILE   */
/*********************************************************************/

             CHGVAR     VAR(&A) VALUE('F4GNDT *GE ' *CAT +
                         &FDATEA)

             CHGVAR     VAR(&B) VALUE('*AND F4GNDT *LE ' *CAT +
                         &TDATEA)

             CHGVAR     VAR(&C) VALUE('*AND F4AIC3 *EQ ' *CAT +
                         &COMPA)

             IF         COND(&EXPSLS *EQ 'N') THEN(DO)
             CHGVAR     VAR(&D) VALUE('*AND F4U2ST *NE ' *CAT +
                         '"Y"')
             ENDDO
             IF         COND(&EXPSLS *EQ 'E') THEN(DO)
             CHGVAR     VAR(&D) VALUE('*AND F4U2ST *EQ ' *CAT +
                         '"Y"')
             ENDDO

             IF         COND(&CUSTA *NE '0000000') THEN(DO)
             CHGVAR     VAR(&E) VALUE('*AND F4ANC7 *EQ ' *CAT +
                         &CUSTA)
             ENDDO

             IF         COND(&SHPCA *NE '0000000') THEN(DO)
             CHGVAR     VAR(&F) VALUE('*AND F4BKC7 *EQ ' *CAT +
                         &SHPCA)
             ENDDO

             IF         COND(&SLSPR *NE '   ') THEN(DO)
             CHGVAR     VAR(&G) VALUE('*AND F4R4CD *EQ "' *CAT +
                         &SLSPR *CAT '"')
             ENDDO
             IF         COND(&STATE *NE '  ') THEN(DO)
             CHGVAR     VAR(&I) VALUE('*AND F4DBCD *EQ "' *CAT +
                         &STATE *CAT '"')
             ENDDO

             CHGVAR     VAR(&H) VALUE(&A *BCAT &B *BCAT &C *BCAT &D +
                                      *BCAT &E *BCAT &F *BCAT &G +
                                      *BCAT &I)

             OVRDBF     FILE(OMHSTPM2) TOFILE(OMHSTPM2) +
                          SHARE(*YES)

             OPNQRYF    FILE((OMHSTPM2)) OPTION(*ALL) +
                          QRYSLT(&H) KEYFLD(*FILE)

/*********************************************************************/
/*   CALL PROGRAM TO BUILD WORK FILE                                 */
/*********************************************************************/

             CALL       PGM(PDRDPFR) PARM('' &FDATE &TDATE +
                          &FLAG1 &FLAG2 &EXPSLS &CUST +
                          &SHPCS &SLSPR &STATE)

             CLOF       OPNID(OMHSTPM2)
             DLTOVR     FILE(OMHSTPM2)

 END:        ENDPGM
