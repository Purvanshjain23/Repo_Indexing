/********************************************************************/
/*                                                                  */
/*  SYNOPSIS - PRINT SALES TAX  REPORT                              */
/*                                                                  */
/*  COMPANY   - SEABOARD COPRORATION                                */
/*  SYSTEM    - ORDER MANAGEMENT                                    */
/*  USER NAME - ROSE CENTONZE                                       */
/*  DATE      - 11/03/97                                            */
/*                                                                  */
/*      PARAMETERS RECEIVED:                                        */
/*                                                                  */
/*          &COMPL      - COMPANY NUMBER                            */
/*          &FDATEL     - FROM DATE                                 */
/*          &TDATEL     - THROUGH DATE                              */
/*          &OUTQ       - OUTPUT QUEUE                              */
/*          &HOLD       - HOLD PRINT FLAG                           */
/*          &SAVE       - SAVE PRINT FLAG                           */
/*          &COPIESL    - NUMBER OF COPIES                          */
/*          &NIGHTQ     - FORMS TYPE                                */
/********************************************************************/

 PDGEPCLP:   PGM        PARM(&COMPL &FDATEL &TDATEL &OUTQ +
                        &HOLD &SAVE &COPIESL &NIGHTQ)

        DCL    &COMP      *DEC    (3 0)           /*  COMPANY #      */
        DCL    &COMPL     *DEC                    /*  FOR PASSING    */
        DCL    &FDATE     *DEC    (7 0)           /*  FROM DATE      */
        DCL    &FDATEL    *DEC                    /*  FOR PASSING    */
        DCL    &TDATE     *DEC    (7 0)           /*  THROUGH DATE   */
        DCL    &TDATEL    *DEC                    /*  FOR PASSING    */
        DCL    &COPIES    *DEC    (3 0)           /*  # OF COPIES    */
        DCL    &COPIESL   *DEC                    /*  FOR PASSING    */
        DCL    &OUTQ      *CHAR   10              /*  OUTPUT QUEUE   */
        DCL    &NIGHTQ    *CHAR   10              /*  NIGHT JOB QUEUE*/
        DCL    &HOLD       *CHAR   (4)            /* Hold Status     */
        DCL    &SAVE       *CHAR   (4)            /* Save Status     */
        DCL    &PGM       *CHAR   10              /*  PGM NAME       */
        DCL    &COMPA     *CHAR   3               /*  COMPANY ALPHA  */
        DCL    &TDATEA    *CHAR   7               /*  TO DATE ALPHA  */
        DCL    &FDATEA    *CHAR   7               /*  FROM DT ALPHA  */
        DCL    &COMMAND    *CHAR   (512)

/* SEND PROGRAM MESSAGE PARAMETERS  */

        DCL     &PGMO     *CHAR   10              /*  PRINTER FILE   */
        DCL     &A        *CHAR   20              /* QRY FRM DATE    */
        DCL     &B        *CHAR   25              /* QRY TO DATE     */
        DCL     &C        *CHAR   25              /* QRY COMPANY     */
        DCL     &H        *CHAR  145              /* QRY ALL         */

             CHGVAR    VAR(&COMP) VALUE(&COMPL) /* Change to length +
                          for print program */
             CHGVAR    VAR(&COMPA) VALUE(&COMP) /* Change to alpha +
                          for message data */
             CHGVAR    VAR(&FDATE) VALUE(&FDATEL) /* Change to length +
                          for print program */
             CHGVAR    VAR(&FDATEA) VALUE(&FDATE) /* Change to alpha +
                          for query data */
             CHGVAR    VAR(&TDATE) VALUE(&TDATEL) /* Change to length +
                          for print program */
             CHGVAR    VAR(&TDATEA) VALUE(&TDATE) /* Change to alpha +
                          for query data */
             CHGVAR    VAR(&COPIES) VALUE(&COPIESL) /* change to +
                          length for print program */

/********************************************************************/
/* APPEND $ TO THE PROGRAM NAME                                     */
/********************************************************************/
             CHGVAR     VAR(&PGM)  VALUE('PDEOPFR')
             CHGVAR     VAR(&PGMO) VALUE(&PGM *TCAT '$')

/*********************************************************************/
/*   CREATE OPEN QUERY FILE TO BE USED FOR REPORT                    */
/*********************************************************************/

             CHGVAR     VAR(&A) VALUE('F4GNDT *GE ' *CAT +
                         &FDATEA)

             CHGVAR     VAR(&B) VALUE('*AND F4GNDT *LE ' *CAT +
                         &TDATEA)

             CHGVAR     VAR(&C) VALUE('*AND F4AIC3 *EQ ' *CAT +
                         &COMPA)

             CHGVAR     VAR(&H) VALUE(&A *BCAT &B *BCAT &C)

             OVRDBF     FILE(OMHSTPLJ) SHARE(*YES)
             OPNQRYF    FILE(OMHSTPLJ) +
                        QRYSLT(&H) +
                        KEYFLD(*FILE)

/********************************************************************/
/* Override printer file with user specified values                 */
/********************************************************************/

             SBDPRTOVR  CMDVAR(&COMMAND) FILE(&PGMO) OUTQ(&OUTQ) +
                          COPIES(&COPIES) HOLD(&HOLD) SAVE(&SAVE)

             CALL       PGM(QCMDEXC) PARM(&COMMAND 512)

/*********************************************************************/
/*   CALL LISTING PROGRAM                                            */
/*********************************************************************/

             CALL      PGM(&PGM) PARM('' &COMP &FDATE &TDATE)
             CLOF       OPNID(OMHSTPLJ)
             DLTOVR     FILE(OMHSTPLJ)

 END:        ENDPGM
