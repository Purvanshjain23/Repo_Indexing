      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      HOG PRODUCTION
      * PROGRAM:     HP628
      * TITLE:       LISTING OF AUTHORIZED FUNCTIONS FOR A USER
      * PROGRAMMER:  LEANNE FEDOR
      * CREATED:     02/13/95
      *
      * FUNCTION: SUBMITTED FROM HP426-WORK WITH USERS.
      *           A SEPARATE REPORT WILL PRINT FOR EACH USER
      *           SELECTED.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 03/27/96  LEANNE FEDOR
      *           RECOMPILE ONLY. A NEW FIELD 'DELETE MOVEMENTS FLAG'
      *           WAS ADDED TO THE AUTHORIZED USERS FILE.
      *
      * 04/30/96  LEANNE FEDOR
      *           RECOMPILE ONLY. A NEW FIELD 'MOVEMENT STATUS'
      *           WAS ADDED TO THE AUTHORIZED USERS FILE TO SUPPORT
      *           THE LOGIC FOR DELETING MOVEMENTS.
      *
      * 05/14/97  LEANNE FEDOR
      *           RECOMPILE ONLY.  OUTQUEUE WAS ADDED TO THE
      *           AUTHORIZED USERS FILE. THIS FIELD WILL NOW BE THE
      *           USER'S DEFAULT PRINTER IN HPS INSTEAD OF THE OUTQ
      *           ON THE USER'S PROFILE.
      *
      * 06/18/97  LEANNE FEDOR
      *           REPLACED ENTRY PARM LOGIC WITH LDA VALUES.
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     FHSP001    IF   E           K DISK
      *    AUTHORIZED USERS          (KEY: AUUSCD)
      *
     FHSP002    IF   E           K DISK
      *    FUNCTIONS                 (KEY: FNFNCD)
      *
     FHSP003    IF   E           K DISK
      *    FUNCTION AUTHORIZATION    (KEY: FAUSCD FAFNCD)
      *
     FQPRINT    O    F  132        PRINTER OFLIND(*INOF)
      *
      /EJECT
      ****************************************************************
      * INPUT SPECS
      ****************************************************************
      *
      *---------------------------------------------------------------
      * STANDARD PROGRAM STATUS DATA STRUCTURE
      *---------------------------------------------------------------
      *    EXTERNALLY DEFINED AS UTPGFR (RECORD FORMAT: PGMDSR)
     D PGMDS         ESDS                  EXTNAME(UTPGFR)
      *
      *---------------------------------------------------------------
      *  NAMED CONSTANTS
      *---------------------------------------------------------------
      *
     D YES             C                   CONST('Y')
     D NO              C                   CONST('N')
      *
      *
      *----------------------------------------------------------------
      * LDA - REPORT SELECTIONS
      *----------------------------------------------------------------
      *
     D                UDS
     D  LDUSCD                 2     11
     D                 DS
     D  DUMMYD                 1      1
      *  THE PRECEDING DUMMY DATA STRUCTURE IS ONLY TO KEEP THE I-SPEC
      *  INFO FROM RUNNING OVER INTO THE 'CALC SPECS' ON THE COMPILE
      *  LISTING.  IT SERVES NO OTHER PURPOSE AND CAN BE DELETED
      *  AT ANY TIME!!!
      /EJECT
      ****************************************************************
      * MAIN LINE
      ****************************************************************
      *
     C                   EXCEPT    HRHDR
     C     LDUSCD        SETLL     HSP003
     C                   SETOFF                                       92
      *
      * READ AND PRINT FUNCTIONS FOR THE SELECTED USER
      *
     C     *IN92         DOUEQ     *ON                                          MAIN DO
     C     LDUSCD        READE     HSP003                                 92
     C     *IN92         IFEQ      *OFF                                         IF NOT EOF
      *
      * RETRIEVE FUNCTION DESCRIPTION FOR PRINTING
      *
     C     FAFNCD        CHAIN     HSP002                             95
     C     *IN95         IFEQ      *OFF
     C                   MOVEL     FNFNDS        DTFNDS
     C                   MOVEL     FNFNTY        DTFNTY
     C                   MOVEL     FNBOFL        DTBOFL
     C                   MOVEL     FNDVCD        DTDVCD
     C                   ELSE
     C                   MOVEL     'UNKNOWN'     DTFNDS
     C                   ENDIF
      *
     C     *INOF         IFEQ      *ON
     C                   EXCEPT    HRHDR
     C                   ENDIF
     C                   EXCEPT    DTLINE
      *
     C                   MOVEL     YES           PRTFLG
     C                   ENDIF                                                  IF NOT EOF
     C                   ENDDO                                                  MAIN DO
      *
      *---------------------------------------------------------------
      * EOF PROCESSING
      *---------------------------------------------------------------
      * IF NO DATA WAS PRINTED, PRINT THE STANDARD 'NO DATA...' ERROR
      * MESSAGE
      *
     C     PRTFLG        IFEQ      NO
     C                   EXCEPT    NODATA
     C                   ENDIF
      *
     C                   SETON                                        LR
      /EJECT
      *---------------------------------------------------------------
      * *INZSR - INITIALIZATION SUBROUTINE
      *---------------------------------------------------------------
      *
     C     *INZSR        BEGSR
      *
      * DEFINE PRINT FIELDS FOR HEADING
      *
     C     *LIKE         DEFINE    AUUSNM        HRUSNM
      *
      * DEFINE PRINT FIELDS FOR DETAIL LINE
      *
     C     *LIKE         DEFINE    FNFNDS        DTFNDS
     C     *LIKE         DEFINE    FNFNTY        DTFNTY
     C     *LIKE         DEFINE    FNBOFL        DTBOFL
     C     *LIKE         DEFINE    FNDVCD        DTDVCD
      *
      * RETRIEVE USER NAME FOR HEADING
      *
     C     LDUSCD        CHAIN     HSP001                             93
     C     *IN93         IFEQ      *ON
     C                   MOVEL     'UNKNOWN'     HRUSNM
     C                   ELSE
     C                   MOVEL     AUUSNM        HRUSNM
     C                   ENDIF
      *
      * SET PRINT FLAG TO DETERMINE IF ANY RECORDS WERE PRINTED
      * SET UP HEADING UNDERLINE FIELD
      * RETRIEVE TIME FOR HEADING
      *
     C                   MOVEL     NO            PRTFLG            1
     C                   MOVE      *ALL'-'       DASH             90
     C                   TIME                    RTIME             6 0
      *
     C                   ENDSR
      /EJECT
      *-------------------------------------------------------------
      * REPORT HEADING
      *-------------------------------------------------------------
      *
     OQPRINT    E            HRHDR          1 01
     O                       SDPGM               10
     O                                           58 'HOG PRODUCTION SYSTEM'
     O                                           80 'DATE'
     O                       UDATE         Y     90
      *
     O          E            HRHDR          1
     O                       SDUSR               10
     O                                           48 'LISTING OF AUTHORIZED'
     O                                           69 'FUNCTIONS FOR A USER'
     O                                           80 'TIME'
     O                       RTIME               90 '  :  :  '
      *
     O          E            HRHDR          1
     O                                           80 'PAGE'
     O                       PAGE          Z     90
      *
     O          E            HRHDR          2
     O                                            5 'USER:'
     O                       LDUSCD              17
     O                       HRUSNM              69
      *
     O          E            HRHDR          1
     O                                           50 'FUNCTION'
     O                                           58 'B/O'
     O                                           70 'DIVISION'
      *
     O          E            HRHDR          1
     O                                            8 'FUNCTION'
     O                                           23 'DESCRIPTION'
     O                                           48 'TYPE'
     O                                           60 'REQUIRED'
     O                                           68 'CODE'
      *
     O          E            HRHDR          1
     O                       DASH                90
      *
      *-------------------------------------------------------------
      * DETAIL
      *-------------------------------------------------------------
      *
     O          E            DTLINE      1
     O                       FAFNCD         B    10
     O                       DTFNDS         B    42
     O                       DTFNTY         B    47
     O                       DTBOFL         B    56
     O                       DTDVCD         B    69
      *
      *-------------------------------------------------------------
      * NO DATA MESSAGE LINE
      *-------------------------------------------------------------
      *
     O          E            NODATA      1
     O                                           19 'NO DATA MEETS YOUR'
     O                                           38 'SELECTION CRITERIA'
