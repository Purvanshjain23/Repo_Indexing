/********************************************************************/
/*                                                                  */
/*  SYNOPSIS -  PRINT COMPANY DOCUMENT DEFAULTS LIST                */
/*  SYSTEM    - ORDER MANAGEMENT SYSTEM                             */
/*  DATE      - 06/05/2007                                          */
/*                                                                  */
/*  PARAMETERS:                                                     */
/*                                                                  */
/* O    &RETURN     -  RETURN CODE                                  */
/* B    &COPYN      -  NO. OF COPIES (PASSED)                       */
/* B    &OUTQ       -  OUTPUT QUEUE                                 */
/* B    &HOLD       -  HOLD STATUS                                  */
/* B    &SAVE       -  SAVE STATUS                                  */
/* B    &COMP       -  COMPANY NUMBER                               */
/* B    &TYPE       -  DOCUMENT TYPE                                */
/* B    &DEPT       -  DOCUMENT DEPARTMENT                          */
/* B    &RECSTS     -  RECORD STATUS                                */
/* B    &DTLSUM     -  SELECT DETAIL OR SUMMARY OPTION              */
/*                     1 = DETAIL REPORT   2 = SUMMARY REPORT       */
/********************************************************************/
/* 99/99/9999 XXX XXX999 DESCRIPTION                                */
/********************************************************************/
 PMHUUPC:    PGM        PARM(&RETURN &OUTQ &HOLD &SAVE &COPYN +
                          &COMP &TYPE &DEPT &RECSTS &DTLSUM)
/* PARMS                                                            */
               DCL    &RETURN     *CHAR   (7)   /* RETURN CODE      */
               DCL    &COPYN      *DEC          /* NO. OF COPIES    */
               DCL    &OUTQ       *CHAR   (10)  /* OUTPUT QUEUE     */
               DCL    &HOLD       *CHAR   (4)   /* HOLD STATUS      */
               DCL    &SAVE       *CHAR   (4)   /* SAVE STATUS      */
               DCL    &COMP       *DEC    (3 0) /* COMPANY NUMBER   */
               DCL    &TYPE       *CHAR   (2)   /* DOC TYPE         */
               DCL    &DEPT       *CHAR   (6)   /* DOC DEPT         */
               DCL    &RECSTS     *CHAR   (1)   /* RECORD STATUS    */
               DCL    &DTLSUM     *CHAR   (1)   /* DETAIL / SUMMARY */

/* INTERNAL FIELDS                                                  */
               DCL    &COPY       *DEC    (2 0) /* NUMBER OF COPIES */

/* ERROR MESSAGE FIELDS                                             */
               DCL    &MSG        *CHAR   (512) /* MESSAGE          */
               DCL    &MSGID      *CHAR   (7)           /* message id         */
               DCL    &MSGL       *DEC    (5 0)

/********************************************************************/
/*   INITIALIZE VARIABLES                                           */
/********************************************************************/
             CHGVAR     VAR(&RETURN) VALUE('       ')
             CHGVAR     VAR(&COPY) VALUE(&COPYN)

/********************************************************************/
/*   MONITOR FOR ALL MESSAGES AT PROGRAM LEVEL                      */
/********************************************************************/
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(MSGTRAP))

/********************************************************************/
/*   PRINT DETAIL REPORT                                            */
/********************************************************************/
             IF         COND(&DTLSUM *EQ '1') THEN(DO)

             OVRPRTF    FILE(PPITPFR$) OUTQ(&OUTQ) COPIES(&COPY) +
                          HOLD(&HOLD) SAVE(&SAVE)

             CALL       PGM(PPITPFR) PARM(' ' &COMP &TYPE &DEPT +
                          &RECSTS)
             GOTO       CMDLBL(END)
             ENDDO

/********************************************************************/
/*   PRINT SUMMARY REPORT                                           */
/********************************************************************/
             IF         COND(&DTLSUM *EQ '2') THEN(DO)

             OVRPRTF    FILE(PMHVPFR$) OUTQ(&OUTQ) COPIES(&COPY) +
                          HOLD(&HOLD) SAVE(&SAVE)

             CALL       PGM(PMHVPFR) PARM(' ' &COMP &TYPE &DEPT +
                          &RECSTS)
             GOTO       CMDLBL(END)
             ENDDO
/*----------------------RECEIVE MESSAGES----------------------------*/
 MSGTRAP:    RCVMSG     MSGTYPE(*EXCP) MSGDTA(&MSG) MSGDTALEN(&MSGL) +
                          MSGID(&MSGID)
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(END))

             SNDPGMMSG  MSGID(&MSGID) MSGF(QCPFMSG) MSGDTA(%SST(&MSG +
                          1 &MSGL)) MSGTYPE(*ESCAPE)
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(END))

 END:        ENDPGM
