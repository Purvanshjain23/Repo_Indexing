     A*%%TS  SD  19980302  090022  ISLRAMS     REL-V3R7M0  5716-PW1
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION
     A*  DISPLAY FILE:  HPD706
     A*  TITLE:         VIEW RECEIVING DETAIL FOR SALES MOVEMENT
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  DATE:          06/21/95
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(FE)
     A                                      PRINT
     A*---------------------------------------------------------------
     A* SUBFILE FORMAT 1
     A*---------------------------------------------------------------
     A*
     A          R HP706S1                   SFL
     A*%%TS  SD  19980302  090022  ISLRAMS     REL-V3R7M0  5716-PW1
     A  85                                  SFLNXTCHG
     A            HDRRN1         4Y 0H
     A            DFHGCD    R        O 12  2REFFLD(HGHGCD)
     A            DFLVHD    R        O 12 10REFFLD(CDLVHD)
     A                                      EDTCDE(K)
     A            DFLVLB    R        O 12 18REFFLD(CDLVLB)
     A                                      EDTCDE(K)
     A            DFDOHD    R        O 12 26REFFLD(CDDOHD)
     A                                      EDTCDE(K)
     A            DFDOLB    R        O 12 34REFFLD(CDDOLB)
     A                                      EDTCDE(K)
     A            DFCHNO    R        O 12 42REFFLD(CDCHNO)
     A                                      EDTCDE(Z)
     A            DFCMDY    R     Y  O 12 51REFFLD(@@DT06)
     A                                      EDTCDE(Y)
     A            DFTPCD    R        O 12 62REFFLD(CDTPCD)
     A            DFTANO    R        O 12 66REFFLD(CDTANO)
     A                                      EDTCDE(Z)
     A            DFCRLB    R        O 12 73REFFLD(CDCRLB)
     A                                      EDTCDE(K)
     A            DFCDHD    R        O 13 10REFFLD(CDCDHD)
     A                                      EDTCDE(K)
     A            DFCDLB    R        O 13 18REFFLD(CDCDLB)
     A                                      EDTCDE(K)
     A            DFYDHD    R        O 13 26REFFLD(CDYDHD)
     A                                      EDTCDE(K)
     A            DFYDLB    R        O 13 34REFFLD(CDYDLB)
     A                                      EDTCDE(K)
     A            DFLNPR    R        O 13 49REFFLD(CDLNPR)
     A                                      EDTCDE(K)
     A            DFBFAM    R        O 13 59REFFLD(CDBFAM)
     A                                      EDTCDE(K)
     A            DFLEAM    R        O 13 70REFFLD(CDLEAM)
     A                                      EDTCDE(K)
     A*
     A*----------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT  1
     A*----------------------------------------------------------------
     A*
     A          R HP706C1                   SFLCTL(HP706S1)
     A*%%TS  SD  19980302  090022  ISLRAMS     REL-V3R7M0  5716-PW1
     A                                      SFLSIZ(0050)
     A                                      SFLPAG(0005)
     A                                      CF03(03 'EXIT')
     A                                      CF05(05 'REFRESH')
     A                                      CF12(12 'PREVIOUS')
     A                                      SFLCSRRRN(&HDRR1)
     A                                      OVERLAY
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            HDRR1          5S 0H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 26'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  2 17'VIEW RECEIVING DETAIL FOR A SALES -
     A                                      MOVEMENT'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  4  2'Sale type ........:'
     A            C1STCD    R        O  4 23REFFLD(STSTCD)
     A            C1STDS    R        O  4 33REFFLD(STSTDS)
     A                                  5  2'Movement number ..:'
     A            C1MVSN    R     Y  O  5 23REFFLD(CDMVSN)
     A                                      EDTCDE(Z)
     A                                  6  2'Customer number ..:'
     A            C1CVNO    R        O  6 23REFFLD(CHCVNO)
     A                                      EDTCDE(Z)
     A            C1CNAM    R        O  6 33REFFLD(@@DS30)
     A                                  8 12'Head'
     A                                      DSPATR(HI)
     A                                  8 18'Pounds'
     A                                      DSPATR(HI)
     A                                  8 28'Head'
     A                                      DSPATR(HI)
     A                                  8 34'Pounds'
     A                                      DSPATR(HI)
     A                                  8 43'Check'
     A                                      DSPATR(HI)
     A                                  8 53'Check'
     A                                      DSPATR(HI)
     A                                  8 60'Pay'
     A                                      DSPATR(HI)
     A                                  8 73'Carcass'
     A                                      DSPATR(HI)
     A                                  9  2'Group'
     A                                      DSPATR(HI)
     A                                  9 10'---- Live ----'
     A                                      DSPATR(HI)
     A                                  9 26'---- DOA -----'
     A                                      DSPATR(HI)
     A                                  9 43'Number'
     A                                      DSPATR(HI)
     A                                  9 53'Date'
     A                                      DSPATR(HI)
     A                                  9 60'Type'
     A                                      DSPATR(HI)
     A                                  9 66'Tattoo'
     A                                      DSPATR(HI)
     A                                  9 73'Pounds'
     A                                      DSPATR(HI)
     A                                 10 10'-- Condemned -'
     A                                      DSPATR(HI)
     A                                 10 26'-- Yard Dead -'
     A                                      DSPATR(HI)
     A                                 10 49'Lean %'
     A                                      DSPATR(HI)
     A                                 10 59'Backfat'
     A                                      DSPATR(HI)
     A                                 10 68'Loin Eye'
     A                                      DSPATR(HI)
     A*
     A*----------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*----------------------------------------------------------------
     A          R HP706K1
     A*%%TS  SD  19950829  091241  ISLRAMS     REL-V3R0M5  5763-PW1
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A                                 23 11'F5=Refresh'
     A                                 23 23'F12=Previous'
      *----------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *----------------------------------------------------------------
     A          R HP706ES                   SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
     A*----------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*----------------------------------------------------------------
     A*
     A          R HP706EC                   SFLCTL(HP706ES)
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
