     A*%%TS  SD  20100224  102104  ISLRAMS     REL-V5R4M0  5722-WDS
     A**************************************************************************
     A*
     A*  ENVIRONMENT:   PORK DIVISION
     A*  SYSTEM:        RESOURCE OPTIMIZATION
     A*  DISPLAY FILE:  ROD401
     A*  TITLE:         WORK WITH RESOURCE OPTIMIZATION HEADERS
     A*  PROGRAMMER:    LEANNE RAMSEY
     A*  DATE:          11/08/06
     A*****************************************************************
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/ROPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A                                      CF04(04 'PROMPT')
     A                                      CF05(05 'REFRESH')
     A                                      CF06(06 'CREATE')
     A                                      CF17(17 'PRINT OPTIONS')
     A                                      CF20(20 'WRKSPLF')
     A                                      CF24(24 'WWOVERRIDES')
     A*---------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*---------------------------------------------------------------
     A          R RO401S1                   SFL
     A*%%TS  SD  20100224  101650  ISLRAMS     REL-V5R4M0  5722-WDS
     A  85                                  SFLNXTCHG
     A            HDRRN1         4Y 0H
     A            HDWEDT    R        H      REFFLD(@@WEDT)
     A            D1OPT          2A  B 13  2CHGINPDFT(UL)
     A  30                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A N30                                  DSPATR(HI)
     A            D1WEMDY   R        O 13  5REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  ')
     A            D1CONO    R        O 13 15REFFLD(@@CONO)
     A                                      EDTCDE(Z)
     A            D1CBSTPC  R        O 13 20REFFLD(ROREF/@@CBSTPC *LIBL/ROPREF)
     A                                      EDTCDE(K)
     A                                      COLOR(GRN)
     A*
     A            D1BICRVL  R        O 13 30REFFLD(OHREC/OHBICRVL *LIBL/ROP100)
     A                                      EDTCDE(K)
     A            D1ALTRPC  R        O 13 42REFFLD(OHREC/OHALTRPC *LIBL/ROP100)
     A                                      EDTCDE(K)
     A            D1BICAPC  R        O 13 54REFFLD(OHREC/OHBICAPC *LIBL/ROP100)
     A                                      EDTCDE(K)
     A            D1BLCAPC  R        O 13 67REFFLD(OHREC/OHBLCAPC *LIBL/ROP100)
     A                                      EDTCDE(K)
     A            D1RCSTCD  R        O 13 78REFFLD(@@RCSTCD)
     A            D1CBRTPC  R        O 14 20REFFLD(ROREF/@@CBRTPC *LIBL/ROPREF)
     A                                      COLOR(BLU)
     A                                      EDTCDE(K)
     A            D1SMOKYPC R        O 14 32REFFLD(ROREF/@@SMOKYPC *LIBL/ROPREF)
     A                                      EDTCDE(K)
     A                                      COLOR(BLU)
     A            D1BITRPC  R        O 14 42REFFLD(OHREC/OHBITRPC *LIBL/ROP100)
     A                                      EDTCDE(K)
     A                                      COLOR(BLU)
     A            D1BIBYPC  R        O 14 54REFFLD(OHREC/OHBIBYPC *LIBL/ROP100)
     A                                      EDTCDE(K)
     A                                      COLOR(BLU)
     A            D1BLBYPC  R        O 14 67REFFLD(OHREC/OHBLBYPC *LIBL/ROP100)
     A                                      EDTCDE(K)
     A                                      COLOR(BLU)
     A*---------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*---------------------------------------------------------------
     A          R RO401C1                   SFLCTL(RO401S1)
     A*%%TS  SD  20100224  102104  ISLRAMS     REL-V5R4M0  5722-WDS
     A                                      SFLSIZ(0050)
     A                                      SFLPAG(0004)
     A N82                                  ROLLUP(25 'PAGE FORWARD')
     A                                      SETOF(30)
     A                                      SETOF(51)
     A                                      RTNCSRLOC(&RCD &FLD)
     A                                      OVERLAY
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            RCD       R        H      REFFLD(@@RCD)
     A            FLD       R        H      REFFLD(@@FLD)
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 19'WORK WITH RESOURCE OPTIMIZATION HE-
     A                                      ADERS'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USR)
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@JOB)
     A                                      DSPATR(HI)
     A                                  3  2'RO Header      '
     A                                      DSPATR(UL)
     A                                  3 22'Item Control Data'
     A                                      DSPATR(UL)
     A                                  4  2'2=Revise'
     A                                  4 22'10=Maintain'
     A                                  4 52'8=Weekly Calculation'
     A                                  5  2'4=Delete'
     A                                  5 22'15=View'
     A                                  5 51'14=W/W Meat Costs'
     A                                  6  2'5=View'
     A                                  6 22'16=Print'
     A                                  6 51'18=W/W ByProduct Values'
     A                                  7  2'9=Change Status'
     A                                  8 43'Sirloin'
     A                                      DSPATR(HI)
     A                                  8 54'Sirloin'
     A                                      DSPATR(HI)
     A                                  8 67'Sirloin'
     A                                      DSPATR(HI)
     A                                  8 78'?'
     A                                      DSPATR(HI)
     A                                  9 20'Cap&Blade'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  9 43'  Trim '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  9 52'  Bone-in  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  9 65' Bone-less '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10  5'Week-End'
     A                                      DSPATR(HI)
     A                                 10 15'Com'
     A                                      DSPATR(HI)
     A                                 10 20'SpecTrim%'
     A                                      DSPATR(HI)
     A                                      COLOR(GRN)
     A                                 10 31'BICredVal'
     A                                      DSPATR(HI)
     A                                      COLOR(GRN)
     A                                 10 43'Alloc %'
     A                                      DSPATR(HI)
     A                                      COLOR(GRN)
     A                                 10 52'Comp Allot%'
     A                                      DSPATR(HI)
     A                                      COLOR(GRN)
     A                                 10 65'Comp Allot%'
     A                                      DSPATR(HI)
     A                                      COLOR(GRN)
     A                                 11  2'Op'
     A                                      DSPATR(HI)
     A                                 11  7'Date'
     A                                      DSPATR(HI)
     A                                 11 15'Nbr'
     A                                      DSPATR(HI)
     A                                 11 20'RegTrim%'
     A                                      COLOR(BLU)
     A                                 11 30'SmokerYld%'
     A                                      DSPATR(HI)
     A                                      COLOR(BLU)
     A                                 11 43'100% BI'
     A                                      DSPATR(HI)
     A                                      COLOR(BLU)
     A                                 11 52'ByProd Yld%'
     A                                      DSPATR(HI)
     A                                      COLOR(BLU)
     A                                 11 65'ByProd Yld%'
     A                                      DSPATR(HI)
     A                                      COLOR(BLU)
     A                                 11 78'St'
     A                                      DSPATR(HI)
     A            C1WEMDY   R        B 12  5REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  ')
     A                                      DSPATR(HI)
     A            C1CONO    R        B 12 15REFFLD(@@CONO)
     A                                      EDTCDE(Z)
     A                                      DSPATR(HI)
     A            C1RCSTCD  R        B 12 78REFFLD(@@RCSTCD)
     A  51                                  DSPATR(PC)
     A                                      DSPATR(HI)
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R RO401K1
     A*%%TS  SD  20080731  144755  ISLRAMS     REL-V5R4M0  5722-WDS
     A  43                             21  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 22  2'F3=Exit'
     A N99                             22 11'F4=Prompt'
     A                                 22 22'F5=Refresh'
     A                                 22 34'F6=Create'
     A                                 22 45'F24-W/W Meat Cost Overrides'
     A                                 23 45'F17=Print Options'
     A                                 23 64'F20=Spool Files'
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *---------------------------------------------------------------
     A          R RO401ES                   SFL SFLMSGRCD(24)
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE CONTROL FORMAT
      *---------------------------------------------------------------
     A          R RO401EC                   SFLCTL(RO401ES)
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
