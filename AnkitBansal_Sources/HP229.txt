      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      HOG PRODUCTION
      * PROGRAM:     HP229 - UPDATE THE EXPENSE INTERFACE STATUS
      * PROGRAMMER:  LEANNE FEDOR
      * CREATED:     12/30/97
      *
      * FUNCTION: THIS PROGRAM REVISES THE INTERFACE STATUS FOR
      *           THE G/L EXPENSE INTERFACE INTO HPS EXPENSE TICKETS.
      *           THE COMPANY/YEAR/PERIOD AND STATUS ARE PASSED INTO
      *           THE PROGRAM AS PARMS AND THE CONTROL FILE IS UPDATED.
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      * 99/99/99
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     FHSP061    UF A E           K DISK
      *  CONTROL FILE: EXPENSE INTERFACE FROM G/L TO HPS
      *
      /EJECT
      ****************************************************************
      * INPUT SPECS
      ****************************************************************
      *
      *---------------------------------------------------------------
      * STANDARD PROGRAM STATUS DATA STRUCTURE
      *---------------------------------------------------------------
      *    EXTERNALLY DEFINED AS UTPGFR (RECORD FORMAT: PGMDSR)
     D PGMDS         ESDS                  EXTNAME(UTPGFR)
      *
      *
      *
     D                 DS
     D  DUMMYD                 1      1
      *  THE PRECEDING DUMMY DATA STRUCTURE IS ONLY TO KEEP THE I-SPEC
      *  INFO FROM RUNNING OVER INTO THE 'CALC SPECS' ON THE COMPILE
      *  LISTING.  IT SERVES NO OTHER PURPOSE AND CAN BE DELETED
      *  AT ANY TIME!!!
      /EJECT
      ****************************************************************
      * MAIN LINE
      ****************************************************************
      *
     C     KEY01         CHAIN     HSP061                             92
      *
     C                   SELECT
     C     *IN92         WHENEQ    *ON
     C                   MOVEL     XXCOCD        EICOCD
     C                   Z-ADD     XXACYR        EIACYR
     C                   Z-ADD     XXACPE        EIACPE
     C                   MOVEL     XXISCD        EIISCD
     C                   WRITE     EIREC
     C                   OTHER
      *
     C                   MOVEL     XXISCD        EIISCD
     C                   UPDATE    EIREC
     C                   ENDSL
      *
     C                   SETON                                        LR
      /EJECT
      *----------------------------------------------------------------
      *  INZSR - INITIALIZATION SUBROUTINE
      *----------------------------------------------------------------
      *
     C     *INZSR        BEGSR
      *
      * PARM LISTS
      *
     C     *ENTRY        PLIST
     C                   PARM                    XXCOCD
     C                   PARM                    XXACYR
     C                   PARM                    XXACPE
     C                   PARM                    XXISCD
      *
      * KEY LISTS
      *
     C     KEY01         KLIST
     C                   KFLD                    XXCOCD
     C                   KFLD                    XXACYR
     C                   KFLD                    XXACPE
      *
      * PARM FIELDS
      *
     C     *LIKE         DEFINE    EICOCD        XXCOCD
     C     *LIKE         DEFINE    EIACYR        XXACYR
     C     *LIKE         DEFINE    EIACPE        XXACPE
     C     *LIKE         DEFINE    EIISCD        XXISCD
      *
     C                   ENDSR
      /EJECT
