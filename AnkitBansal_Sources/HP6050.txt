      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Hog Production System
      * PROGRAM:     HP6050
      * TITLE:       Listing of Farms with Daily Production Entries but No Weekly Entry
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     01/09/02
      *
      * FUNCTION:  Batch program to list the farms with records in the Daily Production file
      *            but no record in the Weekly Production file.
      *            This listing is submitted from HP4051-Specify Options for BGF
      *            Production Edit Listings.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 06/30/04  LeAnne Fedor
      *           Recompile only.
      *
      * 02/03/05  LeAnne Fedor
      *           Recompile only. New field "Transferred In Open Gilts" add
      *           to BGF Daily and Weekly Production files.
      *
      * 08/23/05  LeAnne Fedor
      *           Recompile only. New field "Man Hours" added to BGF Daily and Weekly
      *           Production files.
      *
      * 06/15/06  LeAnne Ramsey
      *           Recompile only. New field "Avg Gilt Weight @ 28 Weeks" added
      *           to BGF Weekly Production file.
      *
      * 08/04/08  LeAnne Ramsey
      *           Recompile only. Fields deleted from both Daily and Weekly
      *           Production files.
      *
      * 10/16/13  LeAnne Ramsey (E2831)
      *           Recompile only. Added field 'MTech Reference'.
      *
      * 01/21/21  ISE            TSN593
      *           Replaced *loval with *start
      *
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fhsp018    if   e           k disk
      *   Farm Site
      *
      *
     Fhsl082a   if   e           k disk
      *    BGF Daily Production    (records selected by open query)
      *
      *
     Fhsl092a   if   e           k disk
      *    BGF Weekly Production
      *
      *
     Fqprint    o    f   80        printer oflind(*inof)
      *
      /EJECT
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Standard fields
      *
     D prtfl           s              1    inz('N')
     D dash            s             80
     d rtime           s              6  0
      *
      * Control fields
      *
     D svfscd          s                   like(bdfscd)
      *
      *
      * Print fields
      *
     D d1fsnm          s                   like(fsfsnm)
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *
      *---------------------------------------------------------------
      * Local data area.
      *---------------------------------------------------------------
      *
     Dlda             uds                  dtaara(*lda)
     D  ldcdyr                 1      4  0
     D  ldcdwk                 5      6  0
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * MAINLINE
      ****************************************************************
      *
      * Prepare to read the file and print headings the first
      * time through.
      *
593  C*    *loval        setll     hsl082a
593  C     *start        setll     hsl082a
     C                   except    h1hdr
      *
      * Read each farm, when farm site changes check to see if there is a
      * weekly record.
     C                   dou       *in90 = *on                                  Main do
     C                   read      hsl082a                                90
     C                   if        *in90 = *off                                 If not EOF
      *
     C                   if        bdfscd <> svfscd                             If new farm
     C                   z-add     bdfscd        svfscd
      *
      * Check for a weekly record
      *
     C     key01         chain     hsl092a                            92
     C                   if        *in92 = *on                                  If no weekly
      *
      * Retrieve farm name
      *
     C     bdfscd        chain     hsp018                             92
     C                   if        *in92 = *off                                 If name hit
     C                   move      fsfsnm        d1fsnm
     C                   else
     C                   eval      d1fsnm = 'Unknown farm'
     C                   endif                                                  If name hit
      *
      * Print detail line
     C                   if        *inof = *on
     C                   except    h1hdr
     C                   endif
     C                   except    l1dtl
      *
     C                   movel     yes           prtfl
     C                   endif                                                  If no weekly
     C                   endif                                                  If new farm
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Main do
      *
      *---------------------------------------------------------------
      * EOF processing
      *---------------------------------------------------------------
      *
     C                   select
     C                   when      svfscd = 0
     C                   except    nodata
     C                   when      prtfl = no
     C                   except    allmatch
     C                   endsl
      *
     C                   seton                                        lr
      /EJECT
      *---------------------------------------------------------------
      * *inzsr - initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Set up a line of underscores and retrieve time for report heading.
      *
     C                   movel     *all'-'       dash
     C                   time                    rtime
      *
      * Key lists
      *
     C     key01         klist
     C                   kfld                    bdfscd
     C                   kfld                    ldcdyr
     C                   kfld                    ldcdwk
      *
     C                   endsr
      /EJECT
      *-------------------------------------------------------------
      * Report heading lines
      *-------------------------------------------------------------
      *
     Oqprint    e            h1hdr          1 01
     O                       sdpgm               10
     O                                           50 'HOG PRODUCTION SYSTEM'
     O                                           70 'DATE'
     O                       udate         y     80
      *
     O          e            h1hdr          1
     O                       sdusr               10
     O                                           53 'LISTING OF BGF FARMS WITH '
     O                                           70 'TIME'
     O                       rtime               80 '  :  :  '
      *
     O          e            h1hdr          2
     O                                           43 'DAILY PRODUCTION ENTRIES '
     O                                           62 'BUT NO WEEKLY ENTRY'
     O                                           70 'PAGE'
     O                       page          z     80
      *
      *
     O          e            h1hdr          1
     O                                           17 'Calendar Year ..:'
     O                       ldcdyr        z     22
      *
      *
     O          e            h1hdr          2
     O                                           17 'Calendar Week ..:'
     O                       ldcdwk        z     22
      *
      *
     O          e            h1hdr          1
     O                                            5 'FARM'
     O          e            h1hdr          1
     O                                            5 'SITE'
      *
     O          e            h1hdr          1
     O                       dash                80
      *
      *-------------------------------------------------------------
      * Detail line
      *-------------------------------------------------------------
      *
     O          e            l1dtl       1
     O                       bdfscd        z      5
     O                       d1fsnm              35
      *
      *-------------------------------------------------------------
      * Blank line
      *-------------------------------------------------------------
      *
     O          e            blank       1
      *
      *-------------------------------------------------------------
      * No data message line
      *-------------------------------------------------------------
      *
     O          e            allmatch    1
     O                                           21 'All farms with Daily '
     O                                           45 'Production entries also '
     O                                           69 'had a Weekly Production '
     O                                           75 'entry.'
      *-------------------------------------------------------------
      * No data message line
      *-------------------------------------------------------------
      *
     O          e            nodata      1
     O                                           20 'There were no Daily '
     O                                           43 'Production entries for '
     O                                           66 'any BGF farms for this '
     O                                           76 'year/week.'
      /eject
