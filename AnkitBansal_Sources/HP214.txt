      *
      * SYSTEM:      HPS Data Mart
      * PROGRAM:     HP214 - Update Group Header with In Before Out
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     08/28/00
      *
      * FUNCTION:    This program updates the Commercial/Grow Finish groups only
      *              in the Datamart group header with a value for 'in before out flag'.
      *
      *              All other groups will retain the defaulted value of 'NO'.
      *
      *              This function attempts to determine if a new group was populated
      *              on a farm before all the other buildings were emptied on the farm.
      *
      *              Making this determination is really guessing on our part since the
      *              'groups' arriving/departing a farm are not 'batched' or 'tied' together
      *              in the system (or in the real world!).
      *
      *              So, we use the 'open' and 'dispose' dates of groups with an arbitrary
      *              60-day time frame to attempt to populate this flag.
      *
      /eject
      *****************************************************************************************
      * MODIFICATIONS:
      *****************************************************************************************
      * DATE      PROGRAMMER
      *
      * 06/11/01  LeAnne Fedor
      *           Recompile only. New field 'transferred out head to sow farm' added
      *           to Datamart Group Header file.
      *
      * 06/26/01  LeAnne Fedor
      *           Recompile only.
      *           Manager and supervisor fields removed from Hog Group file.
      *
      * 07/17/01  LeAnne Fedor
      *           Recompile only.  Added 7 ration fields to group header.
      *
      * 06/03/02  LeAnne Fedor
      *           Added History vs Current processing logic.
      *
      * 02/01/05  LeAnne Fedor
      *           Recompile only. New fields (Sire Line Company and Prime Line) added
      *           to Datamart Group Header file.
      *
      * 08/02/06  LeAnne Fedor
      *           Recompile only. Two new fields (ration 8 feed pounds, ration paylean flag)
      *           added to Datamart Group Header file.
      *
      * 08/10/07  LeAnne Ramsey
      *           Recompile only. New "target" fields renamed in the Datamart Group
      *           Header file.
      *
      * 09/21/07  LeAnne Ramsey
      *           Recompile only. New "target" fields added/removed in the Datamart Group
      *           Header file.
      *
      * 07/06/09  LeAnne Ramsey
      *           Recompile only. Added new field 'Continuous Flow Flag' to Hog Group file.
      *
      * 10/18/10  LeAnne Ramsey  (E1079)
      *           Recompile only. Sire Line Company is now 2-characters instead
      *           of one.
      *
      * 10/14/16  Barb Gutierrez
      *           Recompile only. Added fields Sold days in phase, Avg Sold days in phase,
      *           Wgt open date, and company number to HPPF034.         E6136
      /eject
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fhsl034e   if   e           k disk
      *    HPS Hog Group (records selected and keyed in open query)
      *
      *
     Fhppf034   uf   e           k disk    rename(hgrec: hgrec1) prefix(p1)
      *    Datamart Group Header (records selected with open query)
      *
      /eject
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      *
      ****************************************************************
      * ARRAYS AND TABLES
      ****************************************************************
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      *
      * Workfields for date manipulation
      *
     D wk60opdt        s                   like(p1hgopdt)
     D wkisoopdt       s                   like(p1hgopdt)
     D wkisodsdt       s                   like(p1hgdsdt)
     D wknulldt        s                   like(p1hgdsdt) inz(D'0001-01-01')
     D wkwkbegdt       s                   like(p1hgcldt)
      *
      *
      * Parm fields
      *
     D xxcurbegdt      s              8  0
     D xxcurhist       s              1
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * MAINLINE
      ****************************************************************
      *
      * NOTE: All dates in this Datamart file are already in date format.
      *
      * Read each group in the Datamart Group Header that has an open date.
      * (An open query was used to select out only Commercial/Grow Finish groups).
      *
     C                   dou       *in90 = *on                                  Main do
     C                   read      hppf034                                90
     C                   if        *in90 = *off and                             If not EOF
     C                             p1hgopdt <> wknulldt
      *
      * Process this group when this is:
      * 1) a History run or
      * 2) a Current run and the group is either:
      *    a) not closed or
      *    b) closed on/after the 'current' week-beginnning date from the
      *       data area.
      *
     C                   if        xxcurhist = 'H' or                           If continue
     C                             (p1hgcldt = wknulldt or
     C                              p1hgcldt >= wkwkbegdt)
      *
      * Using the HPS group file,
      * determine if other groups on the farm opened 60 days before this
      * group and are a) still open or b) disposed after this group opened.
      *
     C                   exsr      $groups
      *
     C                   endif                                                  If continue
     C                   endif                                                  If not EOF
     C                   enddo                                                  Main do
      *
     C                   seton                                        lr
      /eject
      *-------------------------------------------------------------------------
      * $groups - Read all groups on the farm
      *-------------------------------------------------------------------------
      *
     C     $groups       begsr
      *
      * Determine a comparison open date that is 60 days prior to the open date
      * of the group that you are checking for 'in before out'.
      *
     C     p1hgopdt      subdur    60:*days      wk60opdt
      *
      *
      * Read the HPS groups on the farm until:
      *    1) you determine that Datamart group should have 'yes' for the in before
      *       out flag, or
      *    2) the open dates on the HPS groups you are reading are now later
      *       than the comparison cutoff open date, or
      *    3) you have read all the HPS groups on the farm
      *
     C     p1hgfscd      setll     hsl034e
      *
     C                   dou       *in91 = *on or                               Do groups
     C                             p1hginoufl = yes or
     C                             wkisoopdt > wk60opdt
     C     p1hgfscd      reade     hsl034e                                91
     C                   if        *in91 = *off and                             If not EOF
     C                             p1hghgsn <> hghgsn
      *
      * Get the HPS date fields into ISO formats.
      *
     C     *iso          test(d)                 hgopdt                 92
     C                   if        *in92 = *off
     C                   movel     hgopdt        wkisoopdt
     C                   else
     C                   movel     wknulldt      wkisoopdt
     C                   endif
      *
     C     *iso          test(d)                 hgdsdt                 92
     C                   if        *in92 = *off
     C                   movel     hgdsdt        wkisodsdt
     C                   else
     C                   movel     wknulldt      wkisodsdt
     C                   endif
      *
     C                   if        wkisoopdt < wk60opdt and                     If in before
     C                             (wkisodsdt = wknulldt or
     C                              wkisodsdt > p1hgopdt)
     C                   move      yes           p1hginoufl
     C                   endif                                                  If in before
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Do groups
      *
      * Update the Datamart group header record
      *
     C                   update    hgrec1
      *
     C                   endsr
      /eject
      *----------------------------------------------------------------
      * Initialization subroutine
      *----------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
     C     *entry        plist
     C                   parm                    xxcurhist
     C                   parm                    xxcurbegdt
      *
      * If this is a 'Current' run, you will ultimately need the incoming date in ISO format
      * date comparison purposes. So, always just get it ready!
      *
     C     *iso          movel     xxcurbegdt    wkwkbegdt
      *
     C                   endsr
      /eject
