      *****************  RPG PROGRAM HEADING  ************************
      *
      * ENVIRONMENT: Pork Division
      * SYSTEM:      HPS---Datamart
      * PROGRAM:     HP221 Build Weekly Farm/Building/Room
*     *                    (Nursery/Grow Finish Only)
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     03/08/01
      *
      *  FUNCTION:   This function writes a weekly record for each farm/building/room. While
      *              the initial purpose of the function was to create 7-week Average Dead
      *              data. The function has been enhanced to also calculate/store Room Capacity
      *              data.
      *
      *              If the build function was submitted with a parm value of H=History,
      *                 this program rebuilds the entire 'weekly' file starting
      *                 with the 'history' dates in the data area DAFIN
      *
      *              If the build function was submitted with a parm value of ' '=Current
      *                 this program writes additional 'weekly' records to the file starting
      *                 with the 'current' dates in the data area DAFIN
      *
      ****************************************************************************************
      * Modifications:
      ****************************************************************************************
      * Date      Programmer
      *
      * 06/11/01  LeAnne Fedor
      *           Recompile only. New field 'accounting quarter' added to Datamart
      *           Weekly file.
      *
      * 07/02/01  LeAnne Fedor
      *           Instead of using 'daily dead' only, 'dead head' will now include daily dead,
      *           DOA, and inventory adjustment head.
      *
      * 10/11/01  LeAnne Fedor
      *           Recompile only. New field 'cull sale amount' added to Datamart
      *           Weekly file.
      *
      * 02/28/02  LeAnne Fedor
      *           Increased length of data area DAFIN.
      *
      * 08/01/02  LeAnne Fedor
      *           Increased DAFIN data area by 1. 'Days' is now 3 long instead of 2.
      *
      * 03/04/04  Alice
      *           Changed to calculate the weekly target capacity for each room/week.
      *           ---  new fields added to HPPF175 ---
      *
      * 10/11/04  LeAnne Fedor
      *           Added a 'submitted flag' position to the data area DAFIN.
      *
      * 10/27/04  LeAnne Fedor
      *           Modified Capacity logic added in March 2004.
      *
      * 10/28/04  LeAnne Fedor
      *           Added new field '3-week average dead' to files:
      *              Datamart FIN: Weekly Group Detail
      *              Datamart FIN: Weekly Farm/Building/Room
      *
      *10/29/04 & 11/02/04   Alice Brownfield
      *          Modified logic to calculate the Capacity Head logic again.  Ended up
      *          writing a NEW pgm (HP2021) to handle the problems we were having with
      *          not getting capacity for rooms with NO groups in it for an entire week.
      *
      * 02/01/05  LeAnne Fedor
      *           Recompile only. New fields (Sire Line Company and Prime Line) added
      *           to Datamart Group Header file.
      *
      * 08/25/05  LeAnne Fedor
      *           Recompile only.
      *           Added a new field "Man Hours" to Weekly Group Detail file.
      *
      * 08/02/06  LeAnne Fedor
      *           Recompile only. Two new fields (ration 8 feed pounds, ration paylean flag)
      *           added to Datamart Group Header file.
      *
      * 08/10/07  LeAnne Ramsey
      *           Recompile only. New "target" fields renamed in the Datamart Group
      *           Header file.
      *
      * 09/21/07  LeAnne Ramsey
      *           Recompile only. New "target" fields added/removed in the Datamart Group
      *           Header file.
      *
      * 05/24/10  LeAnne Ramsey
      *           Recompile only. Added 2 fields to HPPF075-Weekly Group Detail:
      *              WBWEDTMDCY-Week Ending Date in date format mm/dd/ccyy
      *              WBPAJTIHD -Placement Adjusted Transfer In Head
      *
      * 10/18/10  LeAnne Ramsey  (E1079)
      *           Recompile only. Sire Line Company is now 2-characters instead
      *           of one.
      *
      * 10/15/13  LeAnne Ramsey (E2831)
      *           Recompile only. Added field 'MTech Reference'.
      *
      * 10/14/16  Barb Gutierrez
      *           Recompile only. Added fields Sold days in phase, Avg Sold days in phase,
      *           Wgt open date, and company number to HPPF034.         E6136
      /eject
      ****************************************************************************************
      * File Specifications
      ****************************************************************************************
      *
     Fhplf075a  if   e           k disk
      *    DataMart FIN: Weekly Group Detail
      *
      *
     Fhplf075b  if   e           k disk    rename(wbrec:wbrecb) prefix(p1)
      *    DataMart FIN: Weekly Group Detail
      *
      *
     Fhppf034   if   e           k disk
      *    DataMart FIN: Group Header
      *
      *
     Fhsp018    if   e           k disk
      *    Farm site
      *
      *
     Fhppf175   o    e           k disk
      *  Datamart FIN: Weekly Farm/Building/Room
      *
      /EJECT
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      *---------------------------------------------------------------
      *  Named Constants
      *---------------------------------------------------------------
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      *
      ****************************************************************
      * Arrays and Tables
      ****************************************************************
      *
      ****************************************************************
      * Standalone fields
      ****************************************************************
      *
      *
      * Parm fields
      *
     D xxprocfl        s              1a
      *
      *
      * Control fields
      *
     D first           s              1    inz('Y')
     D svfscd          s                   like(wbfscd)
     D svblcd          s                   like(wbblcd)
     D svrmcd          s                   like(wbrmcd)
     D svwedt          s                   like(wbwedt)
     D svhgcd          s                   like(wbhgcd)
     D svcdyr          s                   like(wbcdyr)
     D svcdwk          s                   like(wbcdwk)
      *
      *
      * Workfields/accumulators
      *
     D wkhd            s              9  0
     D wkcphd          s              7  0
     D wkcppd          s             15  0
     D wkcphdtot       s              7  0
     D wkcppdtot       s             15  0
     D wkgpcnt         s              5  0
     D wkeihd          s                   like(wbeihd)
     D wkpigday        s                   like(wbpigday)
      *
      *
      * Workfields for dates
      *
     D wkwedt          s               D   datfmt(*iso)
     D wkstartdt       s               D   datfmt(*iso)
      *
      *
      ****************************************************************
      * Data Structures
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *---------------------------------------------------------------
      * Standard database file information data structure
      *---------------------------------------------------------------
      *    externally defined as UTDBGR (record format: FDBCKD)
     D dbfeed        e ds                  extname(utdbfr)
      *
      *---------------------------------------------------------------
      * Definition for external data area
      *---------------------------------------------------------------
     D
     Ddafin            ds                  dtaara(dafin)
     D  dahwkenddt            15     22  0
     D  dacwkenddt            38     45  0
     D  dadays                47     49  0
     D  dasbmfl               62     62
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * MAINLINE
      ****************************************************************
      *
      * Read the Datamart Weekly Group Detail file. You will ultimately write one
      * record to the file for each unique farm/bldg/room/week-ending date.
      *
     C     wkstartdt     setll     hplf075a
      *
     C                   dou       *in90 = *on                                  Main do
     C                   read      hplf075a                               90
     C                   if        *in90 = *off                                 If not EOF
      *
      * Control break
     C                   select
     C                   when      first = yes
     C                   exsr      $savefields
     C                   move      no            first
      *
     C                   when      wbwedt <> svwedt or
     C                             wbfscd <> svfscd or
     C                             wbblcd <> svblcd or
     C                             wbrmcd <> svrmcd
     C                   exsr      $7wkdead
     C                   exsr      $3wkdead
     C                   exsr      $wrt175
     C                   exsr      $savefields
     C                   endsl
      *
      * Detail processing for each record:
      *  1) save the Hog Group so that you ultimately have the last group
      *  2) accumulate Ending Inventory Head
      *  3) accumulate Pig Days
      *  4) calculate Capacity
      *
     C                   move      wbhgcd        svhgcd
     C                   add       wbeihd        wkeihd
     C                   add       wbpigday      wkpigday
      *
     C                   exsr      $capacity
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Main do
      *
      *-----------------
      * EOF processing
      *-----------------
      *
      * Last record processing
      *
     C                   if        first = no
     C                   exsr      $7wkdead
     C                   exsr      $3wkdead
     C                   exsr      $wrt175
     C                   endif
      *
     C                   seton                                        lr
      /EJECT
      *----------------------------------------------------------------------------------
      * Calculate the capacity for the farm/building/room and accumulate
      *----------------------------------------------------------------------------------
      *
     C     $capacity     begsr
      *
      * Initialize workfields:
      *  1) capacity head
      *  2) capacity pig days
      *
     C                   z-add     0             wkcphd
     C                   z-add     0             wkcppd
      *
      * Retrieve from the Farm Master:
      *   1) Target Square Feet Per Pig
      *
     C     wbfscd        chain     hsp018                             92
     C                   if        *in92 = *off                                 If farm hit
      *
      * Retrieve from Datamart Fin: Group Header
      *   1) Group type
      *
     C     wbhgcd        chain     hppf034                            92
     C                   if        *in92 = *off                                 If group hit
      *
      * Calculate a default Capacity Head based on square feet.
      *
     C                   if        fshdsqft <> 0
     C     hgrmsqft      div       fshdsqft      wkcphd
     C                   endif
      *
      * Increment the counter of groups in the room
      *
     C                   add       1             wkgpcnt
      *
      * Now, override/adjust this Capacity Head for all NOT Normal groups.
      *
     C                   if        hggtcd <> 'N'
     C                   exsr      $capadj
     C                   endif
      *
      * Calculate Capacity Pig Days using Capacity Head
      *
     C     wkcphd        mult      7             wkcppd
      *
      * Accumulate totals for the Room for:
      *  1) capacity head
      *  2) capacity pig days
      *
     C                   add       wkcphd        wkcphdtot
     C                   add       wkcppd        wkcppdtot
      *
     C                   endif                                                  If group hit
     C                   endif                                                  If farm hit
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Override the Head Capacity for all Not Normal groups
      *---------------------------------------------------------------
      *
     C     $capadj       begsr
      *
      * Gilt development - originally they wanted to increase the capacity for GDU
      *   ---  as of 10/27/2004 they don't want to increase capacity so I changed the
      *        multiplier to 1.00 but kept the logic in place...   amb
      *   1) increase capacity head by a rate of 1.00
      *
     C                   select                                                 Select type
     C                   when      hggtcd = 'G'
     C                   mult      1.00          wkcphd
      *
      * Wean-to-finish
      *   1) For Grow Finish, hard-code a capacity head
      *   2) For Nursery, hard-code a capacity head and then
      *      adjust for Double Stock.
      *
     C                   when      hggtcd = 'W'
      *
     C                   select                                                 Select phase
     C                   when      hgppcd = 'GF '
     C                   z-add     1040          wkcphd
      *
     C                   when      hgppcd = 'NUR'
     C                   z-add     1080          wkcphd
      * Double Stock
     C                   if        hgsdcd = 'D'
     C                   mult      2             wkcphd
     C                   endif
     C                   endsl                                                  Select phase
     C                   endsl                                                  Select type
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Retrieve/calculate 7-week average dead head
      *---------------------------------------------------------------
      *
     C     $7wkdead      begsr
      *
      * You will need the Saturday that is 6 weeks prior to this week-ending date.
      *
     C     svwedt        subdur    42:*days      wkwedt
      *
     C                   z-add     0             wkhd
      *
      * Sum daily dead and DOA/KOAs then subtract inventory adjustment head.
      *
     C     key01         setll     hplf075b
     C                   dou       *in91 = *on                                  Do dead
     C     key02         reade     hplf075b                               91
     C                   if        *in91 = *off and                             If not EOF
     C                             p1wbwedt <= svwedt
     C                   add       p1wbwkdhd     wkhd
     C                   add       p1wbwsdohd    wkhd
     C                   add       p1wbwtdohd    wkhd
     C                   sub       p1wbwiahd     wkhd
     C                   endif                                                  If not EOF
     C                   enddo                                                  Do dead
      *
     C     wkhd          div(h)    7             wfavddhd
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Retrieve/calculate 3-week average dead head
      *---------------------------------------------------------------
      *
     C     $3wkdead      begsr
      *
      * You will need the Saturday that is 2 weeks prior to this week-ending date.
      *
     C     svwedt        subdur    14:*days      wkwedt
      *
     C                   z-add     0             wkhd
      *
      * Sum daily dead and DOA/KOAs then subtract inventory adjustment head.
      *
     C     key01         setll     hplf075b
     C                   dou       *in91 = *on                                  Do dead
     C     key02         reade     hplf075b                               91
     C                   if        *in91 = *off and                             If not EOF
     C                             p1wbwedt <= svwedt
     C                   add       p1wbwkdhd     wkhd
     C                   add       p1wbwsdohd    wkhd
     C                   add       p1wbwtdohd    wkhd
     C                   sub       p1wbwiahd     wkhd
     C                   endif                                                  If not EOF
     C                   enddo                                                  Do dead
      *
     C     wkhd          div(h)    3             wfa3ddhd
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Write a record for the farm/building/room/week-ending date
      *---------------------------------------------------------------
      *
     C     $wrt175       begsr
      *
     C                   z-add     svfscd        wffscd
     C                   move      svblcd        wfblcd
     C                   move      svrmcd        wfrmcd
     C                   move      svwedt        wfwedt
     C                   z-add     svcdyr        wfcdyr
     C                   z-add     svcdwk        wfcdwk
     C                   move      svhgcd        wfhgcd
      *
     C                   z-add     wkeihd        wfeihd
     C                   z-add     wkpigday      wfpigday
     C                   z-add     wkgpcnt       wfgpcnt
      *
      * If at least 1 group in the room that week = divide the total
      * capacity by the # of groups, otherwise use the capacity
      *
     C                   if        wkgpcnt <> 0
     C     wkcphdtot     div       wkgpcnt       wfcphd
     C     wkcppdtot     div       wkgpcnt       wfcppd
      *
      * else 0 groups = use default for the farm/room
      *
     C                   else
     C                   z-add     wkcphdtot     wfcphd
     C                   z-add     wkcppdtot     wfcppd
     C                   endif
      *
     C                   write     wfrec
     C                   clear                   wfrec
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Save break fields and initialize workfields
      *---------------------------------------------------------------
      *
     C     $savefields   begsr
      *
     C                   z-add     wbfscd        svfscd
     C                   move      wbblcd        svblcd
     C                   move      wbrmcd        svrmcd
     C                   move      wbwedt        svwedt
     C                   z-add     wbcdyr        svcdyr
     C                   z-add     wbcdwk        svcdwk
      *
      * Initialize workfields
      *
     C                   z-add     0             wkcphdtot
     C                   z-add     0             wkcppdtot
     C                   z-add     0             wkgpcnt
     C                   z-add     0             wkeihd
     C                   z-add     0             wkpigday
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Parm lists
      *
     C     *entry        plist
     C                   parm                    xxprocfl
      *
      *
      * Key lists
      *
     C     key01         klist
     C                   kfld                    svfscd
     C                   kfld                    svblcd
     C                   kfld                    svrmcd
     C                   kfld                    wkwedt
      *
     C     key02         klist
     C                   kfld                    svfscd
     C                   kfld                    svblcd
     C                   kfld                    svrmcd
      *
      *
      * Bring in the data area that contains the week-ending date for the first week.
      * Move the retrieved date to a date format field.
      *
     C                   in        dafin
      *
     C                   select
     C                   when      xxprocfl = 'H'
     C                   movel     dahwkenddt    wkstartdt
      *
     C                   when      xxprocfl = ' '
     C                   movel     dacwkenddt    wkstartdt
     C                   endsl
      *
     C                   endsr
      /EJECT
