     A*%%TS  SD  20020122  151635  ISLRAMS     REL-V4R4M0  5769-PW1
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD152
     A*  TITLE:         MAINTAIN ROOMS
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  CREATED:       03/18/94
     A*
     A*  INDICATOR 60 IS ON WHEN IN REVISE MODE
     A*  INDICATOR 61 IS ON WHEN IN CREATE MODE
     A*
     A*  INDICATOR 62 IS ON WHEN USER HAS DELETED OR CREATED A RECORD
     A*               IN THE SUBFILE
     A*  INDICATOR 90 IS ON WHEN THE BUILDING THE USER HAS SELECTED
     A*               FROM THE WORK WITH HAS BEEN DELETED BY ANOTHER
     A*               USER
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A                                      CF04(04 'PROMPT')
     A N90                                  CF05(05 'REFRESH')
     A  60N90                               CF06(06 'CREATE')
     A                                      CA12(12 'PREVIOUS')
     A N82N90                               ROLLUP(25 'PAGE FORWARD')
     A*
     A*--------------------------------------------------------------*
     A* SUBFILE FORMAT ONE
     A*--------------------------------------------------------------*
     A          R HP152S1                   SFL
     A*%%TS  SD  20020122  151635  ISLRAMS     REL-V4R4M0  5769-PW1
     A                                      SETOF(30)
     A                                      SETOF(31)
     A                                      SETOF(34)
     A                                      SETOF(35)
     A                                      SETOF(36)
     A                                      SETOF(37)
     A                                      SETOF(62)
     A  85                                  SFLNXTCHG
     A            HDRRN1         4Y 0H
     A            DFOPT          2A  B 11  2CHGINPDFT(UL)
     A  61
     AO 62
     AO 90                                  DSPATR(PR)
     A  61
     AO 90                                  DSPATR(ND)
     A  30                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A N61N62N90
     A N30                                  DSPATR(HI)
     A N61N62N90
     A N30 60                               DSPATR(UL)
     A            DFRMCD    R        B 11  8REFFLD(BRRMCD)
     A  60
     AO 62
     AO 90                                  DSPATR(PR)
     A  61 90                               DSPATR(ND)
     A  31                                  DSPATR(RI)
     A  31                                  DSPATR(PC)
     A N60N62N90
     A N31 61                               DSPATR(HI)
     A N60N62N90
     A N31 61                               DSPATR(UL)
     A            DFRMST    R        B 11 18REFFLD(@@FLAG)
     A  62
     AO 90                                  DSPATR(PR)
     A  61 90                               DSPATR(ND)
     A  34                                  DSPATR(RI)
     A  54
     AO 34                                  DSPATR(PC)
     A N34N62N90                            DSPATR(HI)
     A N34N62N90                            DSPATR(UL)
     A            DFRMSQFT  R        B 11 28REFFLD(HSRREF/@@RMSQFT *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A  62
     AO 90                                  DSPATR(PR)
     A  61 90                               DSPATR(ND)
     A  35                                  DSPATR(RI)
     A  35                                  DSPATR(PC)
     A N35N62N90                            DSPATR(HI)
     A N35N62N90                            DSPATR(UL)
     A            DFWANINO  R        B 11 44REFFLD(HSRREF/@@WANINO *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A  62
     AO 90                                  DSPATR(PR)
     A  61 90                               DSPATR(ND)
     A  36                                  DSPATR(RI)
     A  36                                  DSPATR(PC)
     A N36N62N90                            DSPATR(HI)
     A            DFFDSPIN  R        B 11 60REFFLD(HSRREF/@@FDSPIN *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A  62
     AO 90                                  DSPATR(PR)
     A  61 90                               DSPATR(ND)
     A  37                                  DSPATR(RI)
     A  37                                  DSPATR(PC)
     A N37N62N90                            DSPATR(HI)
     A            DFACT          8A  O 11 72
     A*
     A*--------------------------------------------------------------*
     A* SUBFILE CONTROL FORMAT ONE
     A*--------------------------------------------------------------*
     A          R HP152C1                   SFLCTL(HP152S1)
     A*%%TS  SD  20020122  151635  ISLRAMS     REL-V4R4M0  5769-PW1
     A                                      SFLSIZ(0100)
     A                                      SFLPAG(0011)
     A                                      SFLCSRRRN(&HDRR1)
     A                                      OVERLAY
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A                                      RTNCSRLOC(&RCD &FLD)
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            HDRR1          5S 0H
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 25'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A            DFMODE    R        O  2  2REFFLD(@@MODE)
     A                                      DSPATR(HI)
     A                                  2 28'MAINTAIN ROOMS'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  4  2'Farm site ..:'
     A            C1FSCD    R        O  4 17REFFLD(FSFSCD)
     A                                      EDTCDE(M)
     A            C1FSNM    R        O  4 24REFFLD(FSFSNM)
     A                                  5  2'Building ...:'
     A            C1BLCD    R        O  5 17REFFLD(FBBLCD)
     A            C1BLDS    R        O  5 24REFFLD(FBBLDS)
     A N61                              7  2'4=Delete'
     A N61                              7 12' 7=Maintain Equipment'
     A                                  8 18'?'
     A                                      DSPATR(HI)
     A                                  9 25'Total Square'
     A                                      DSPATR(HI)
     A                                  9 42'Number of'
     A                                      DSPATR(HI)
     A                                  9 57'Total Inches'
     A                                      DSPATR(HI)
     A N61                             10  2'Opt'
     A                                      DSPATR(HI)
     A                                 10  8'Room'
     A                                      DSPATR(HI)
     A                                 10 16'Status'
     A                                      DSPATR(HI)
     A                                 10 25'Feet in Room'
     A                                      DSPATR(HI)
     A                                 10 40'Water Nipples'
     A                                      DSPATR(HI)
     A                                 10 56'of Feeder Space'
     A                                      DSPATR(HI)
     A*
     A*--------------------------------------------------------------*
     A* COMMAND KEY RECORD 1
     A*--------------------------------------------------------------*
     A          R HP152K1
     A*%%TS  SD  19940408  113712  ISRCENT     REL-V2R2M0  5738-PW1
     A                                      SETOF(60)
     A                                      SETOF(61)
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A                                      DSPATR(HI)
     A                                 23  2'F3=Exit'
     A                                 23 11'F4=Prompt'
     A N90                             23 22'F5=Refresh'
     A  60N90                          23 34'F6=Create'
     A                                 23 45'F12=Previous'
     A*
     A*--------------------------------------------------------------*
     A* ERROR MESSAGE SUBFILE FORMAT
     A*--------------------------------------------------------------*
     A          R HP152ES                   SFL SFLMSGRCD(24)
     A*
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
     A*                                                                *********
     A*--------------------------------------------------------------*
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*--------------------------------------------------------------*
     A          R HP152EC                   SFLCTL(HP152ES)
     A*
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
