     A*%%TS  SD  20050317  090717  ISLRAMS     REL-V5R2M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        TRIUMPH FOODS
     A*  DISPLAY FILE:  TFD133
     A*  TITLE:         REVISE INVOICE
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  DATE:          03/02/05
     A*
     A*  INDICATOR 60 IS ON WHEN IN REVISE MODE
     A*  INDICATOR 61 IS ON WHEN IN CREATE MODE
     A*  INDICATOR 62 IS ON WHEN IN DELETE MODE
     A*  INDICATOR 63 IS ON WHEN THE STATUS OF THE INVOICE IS "ASSIGNED"
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/TFPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A N62N63                               CF04(04 'PROMPT')
     A  60N62                               CF05(05 'REFRESH')
     A  60N62                               CF11(11 'DELETE')
     A                                      CA12(12 'PREVIOUS')
     A*
     A*--------------------------------------------------------------
     A* RECORD FORMAT ONE
     A*--------------------------------------------------------------
     A          R TF133R1
     A*%%TS  SD  20050317  084417  ISLRAMS     REL-V5R2M0  5722-WDS
     A                                      SETOF(30)
     A                                      SETOF(31)
     A                                      SETOF(35)
     A                                      SETOF(55)
     A                                      RTNCSRLOC(&RCD &FLD)
     A                                      OVERLAY
     A            RCD       R        H      REFFLD(@@RCD)
     A            FLD       R        H      REFFLD(@@FLD)
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 30'REVISE INVOICE'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USR)
     A                                      DSPATR(HI)
     A            D1MODE         6A  O  2  2DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USR)
     A                                      DSPATR(HI)
     A                                  4  2'Invoice date ......'
     A            D1INDTMDY R        B  4 24REFFLD(TFREF/@@DT06 *LIBL/TFPREF)
     A                                      EDTWRD('  /  /  -')
     A  30                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A N62N30                               DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(PR)
     A N61                              4 46'Invoice number:'
     A                                      COLOR(PNK)
     A N61        D1INSN    R        O  4 62REFFLD(TFREF/@@INSN *LIBL/TFPREF)
     A                                      EDTCDE(Z)
     A                                      COLOR(PNK)
     A                                  5  2'Due date ..........'
     A            D1DUDTMDY R        B  5 24REFFLD(TFREF/@@DT06 *LIBL/TFPREF)
     A                                      EDTWRD('  /  /  -')
     A  31                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A N62N31                               DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(PR)
     A N61                              5 46'Invoice status:'
     A                                      COLOR(PNK)
     A N61        D1STDS    R        O  5 62REFFLD(TFREF/@@STDS *LIBL/TFPREF)
     A                                      COLOR(PNK)
     A                                  6  2'Charge frequency ..'
     A N63                              6 22'?'
     A            D1CFQCD   R        B  6 24REFFLD(TFREF/@@CFQCD *LIBL/TFPREF)
     A  35
     AO 55                                  DSPATR(PC)
     A  35                                  DSPATR(RI)
     A N62N63N35                            DSPATR(HI)
     A N62N63                               DSPATR(UL)
     A  62
     AO 63                                  DSPATR(PR)
     A            D1CFQDS   R        O  6 33REFFLD(TFREF/@@CFQDS *LIBL/TFPREF)
     A*--------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*--------------------------------------------------------------
     A          R TF133K1
     A*%%TS  SD  20050317  090717  ISLRAMS     REL-V5R2M0  5722-WDS
     A  43                             22  2'Message ''F4gram failed.  Call MIS-
     A                                       user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A N62N63                          23 11'F4=Prompt'
     A N62N61                          23 22'F5=Refresh'
     A N62N61                          23 34'F11=Delete'
     A                                 23 46'F12=Previous'
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *---------------------------------------------------------------
     A          R TF133ES                   SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE CONTROL FORMAT
      *---------------------------------------------------------------
     A          R TF133EC                   SFLCTL(TF133ES)
      *
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
