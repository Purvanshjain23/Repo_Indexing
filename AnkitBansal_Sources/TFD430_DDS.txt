     A*%%TS  SD  20050303  154023  ISLRAMS     REL-V5R2M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        TRIUMPH FOODS
     A*  DISPLAY FILE:  TFD430
     A*  TITLE:         WORK WITH CHARGE CODES
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  DATE:          03/01/05
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/TFPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A                                      CF04(04 'PROMPT')
     A                                      CF05(05 'REFRESH')
     A                                      CF06(06 'CREATE')
     A                                      CF07(07 'LISTING')
     A                                      CF13(13 'FOLD/DROP')
     A                                      CF17(17 'PRINT OPTIONS')
     A                                      CF20(20 'WRKSPLF')
     A*
     A*
     A*---------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*---------------------------------------------------------------
     A          R TF430S1                   SFL
     A*%%TS  SD  20050302  150955  ISLRAMS     REL-V5R2M0  5722-WDS
     A  85                                  SFLNXTCHG
     A            HDRRN1         4Y 0H
     A            D1OPT          2A  B  9  2CHGINPDFT(UL)
     A  30                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A N30                                  DSPATR(HI)
     A            D1CCCD    R        O  9  6REFFLD(@@CCCD)
     A            D1CONO    R        O  9 18REFFLD(TFREF/@@CONO *LIBL/TFPREF)
     A                                      EDTCDE(Z)
     A            D1GLDBCD  R        O  9 23REFFLD(TFREF/@@GLDBCD *LIBL/TFPREF)
     A            D1GLCRCD  R        O  9 33REFFLD(TFREF/@@GLCRCD *LIBL/TFPREF)
     A            D1CFQCD   R        O  9 45REFFLD(TFREF/@@CFQCD *LIBL/TFPREF)
     A            D1UCFL    R        O  9 53REFFLD(TFREF/@@UCFL *LIBL/TFPREF)
     A            D1INFL    R        O  9 62REFFLD(TFREF/@@INFL *LIBL/TFPREF)
     A            D1SMCD    R        O  9 70REFFLD(TFREF/@@SMCD *LIBL/TFPREF)
     A            D1AIST    R        O  9 78REFFLD(@@AIST)
     A                                 10 46'Desc:'
     A                                      COLOR(PNK)
     A            D1CCDS    R        O 10 52REFFLD(@@CCDS)
     A                                      COLOR(PNK)
     A*
     A*---------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*---------------------------------------------------------------
     A          R TF430C1                   SFLCTL(TF430S1)
     A*%%TS  SD  20050303  154023  ISLRAMS     REL-V5R2M0  5722-WDS
     A                                      SFLSIZ(0050)
     A                                      SFLPAG(0006)
     A N82                                  ROLLUP(25 'PAGE FORWARD')
     A                                      SETOF(30)
     A                                      SETOF(51)
     A                                      SETOF(52)
     A                                      SETOF(53)
     A                                      SETOF(54)
     A                                      SETOF(55)
     A                                      SETOF(56)
     A                                      SETOF(57)
     A                                      RTNCSRLOC(&RCD &FLD)
     A                                      OVERLAY
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A  78                                  SFLFOLD(CF13)
     A  79                                  SFLDROP(CF13)
     A                                      SFLMODE(&FOLD)
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            FOLD           1A  H
     A            RCD       R        H      REFFLD(@@RCD)
     A            FLD       R        H      REFFLD(@@FLD)
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 30'WORK WITH CHARGE CODES'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USR)
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@JOB)
     A                                      DSPATR(HI)
     A                                  3  2'2=Revise'
     A                                  3 12'4=Delete'
     A                                  3 22'5=View'
     A                                  4 27'?'
     A                                      DSPATR(HI)
     A                                  4 36'?'
     A                                      DSPATR(HI)
     A                                  4 45'?'
     A                                      DSPATR(HI)
     A                                  4 53'?'
     A                                      DSPATR(HI)
     A                                  4 62'?'
     A                                      DSPATR(HI)
     A                                  4 70'?'
     A                                      DSPATR(HI)
     A                                  4 78'?'
     A                                      DSPATR(HI)
     A                                  5 59'Include'
     A                                      DSPATR(HI)
     A                                  5 68'System'
     A                                      DSPATR(HI)
     A                                  6  6'Charge'
     A                                      DSPATR(HI)
     A                                  6 18'Com'
     A                                      DSPATR(HI)
     A                                  6 23'--- G/L Codes ----'
     A                                      DSPATR(HI)
     A                                  6 43'Freq'
     A                                      DSPATR(HI)
     A                                  6 51'User'
     A                                      DSPATR(HI)
     A                                  6 59'In Inv'
     A                                      DSPATR(HI)
     A                                  6 68'Module'
     A                                      DSPATR(HI)
     A                                  6 77'A/I'
     A                                      DSPATR(HI)
     A                                  7  2'Opt'
     A                                      DSPATR(HI)
     A                                  7  6'Code'
     A                                      DSPATR(HI)
     A                                  7 18'Nbr'
     A                                      DSPATR(HI)
     A                                  7 23'Debit'
     A                                      DSPATR(HI)
     A                                  7 33'Credit'
     A                                      DSPATR(HI)
     A                                  7 43'Code'
     A                                      DSPATR(HI)
     A                                  7 50'Control'
     A                                      DSPATR(HI)
     A                                  7 60'Total'
     A                                      DSPATR(HI)
     A                                  7 69'Code'
     A                                      DSPATR(HI)
     A                                  7 77'St'
     A                                      DSPATR(HI)
     A            C1CCCD    R        B  8  6REFFLD(@@CCCD)
     A                                      DSPATR(HI)
     A            C1CONO    R        B  8 18REFFLD(TFREF/@@CONO *LIBL/TFPREF)
     A                                      DSPATR(HI)
     A                                      EDTCDE(K)
     A            C1GLDBCD  R        B  8 23REFFLD(TFREF/@@GLDBCD *LIBL/TFPREF)
     A                                      DSPATR(HI)
     A  51                                  DSPATR(PC)
     A            C1GLCRCD  R        B  8 33REFFLD(TFREF/@@GLCRCD *LIBL/TFPREF)
     A                                      DSPATR(HI)
     A  52                                  DSPATR(PC)
     A            C1CFQCD   R        B  8 45REFFLD(TFREF/@@CFQCD *LIBL/TFPREF)
     A                                      DSPATR(HI)
     A  53                                  DSPATR(PC)
     A            C1UCFL    R        B  8 53REFFLD(TFREF/@@UCFL *LIBL/TFPREF)
     A                                      DSPATR(HI)
     A  54                                  DSPATR(PC)
     A            C1INFL    R        B  8 62REFFLD(TFREF/@@INFL *LIBL/TFPREF)
     A                                      DSPATR(HI)
     A  55                                  DSPATR(PC)
     A            C1SMCD    R        B  8 70REFFLD(TFREF/@@SMCD *LIBL/TFPREF)
     A                                      DSPATR(HI)
     A  56                                  DSPATR(PC)
     A            C1AIST    R        B  8 78REFFLD(@@AIST)
     A  57                                  DSPATR(PC)
     A                                      DSPATR(HI)
     A*
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R TF430K1
     A*%%TS  SD  20050301  161851  ISLRAMS     REL-V5R2M0  5722-WDS
     A  43                             21  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 22  2'F3=Exit'
     A N99                             22 11'F4=Prompt'
     A                                 22 22'F5=Refresh'
     A                                 22 34'F6=Create'
     A                                 22 45'F7=Listing'
     A                                 22 45'F13=Fold/Unfold'
     A                                 22 57'F13=Fold/Drop'
     A                                 23 34'F17=Print Options'
     A                                 23 57'F20=Spool Files'
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *---------------------------------------------------------------
     A          R TF430ES                   SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE CONTROL FORMAT
      *---------------------------------------------------------------
     A          R TF430EC                   SFLCTL(TF430ES)
      *
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
