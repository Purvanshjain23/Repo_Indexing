      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      HOG PRODUCTION
      * PROGRAM:     HP601
      * TITLE:       LISTING OF EQUIPMENT TYPE/EQUIPMENT
      * PROGRAMMER:  LEANNE FEDOR
      * CREATED:     3/09/94
      *
      * FUNCTION: BATCH PROGRAM TO LIST THE RECORDS IN THE
      *           EQUIPMENT TYPE MASTER FILE ALONG WITH THE
      *           EQUIPMENT ASSIGNED TO EACH TYPE FROM THE
      *           EQUIPMENT FILE
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      * 06/16/97  LEANNE FEDOR
      *           ADDED USER ID TO REPORT HEADING.
      *
      * 01/22/98  LEANNE FEDOR
      *           Test Implementer install of ILE converted program.
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     FHSP021    IF   E           K DISK
      *    EQUIPMENT TYPE MASTER   (KEY: ETETCD)
      *
     FHSL022A   IF   E           K DISK
      *    EQUIPMENT MASTER        (KEY: EQETCD EQEQCD)
      *
     FQPRINT    O    F  132        PRINTER OFLIND(*INOF)
      *
      /EJECT
      ****************************************************************
      * INPUT SPECS
      ****************************************************************
      *
      *---------------------------------------------------------------
      * STANDARD PROGRAM STATUS DATA STRUCTURE
      *---------------------------------------------------------------
      *    EXTERNALLY DEFINED AS UTPGFR (RECORD FORMAT: PGMDSR)
     D PGMDS         ESDS                  EXTNAME(UTPGFR)
      *
      *---------------------------------------------------------------
      *  NAMED CONSTANTS
      *---------------------------------------------------------------
      *
     D YES             C                   CONST('Y')
     D NO              C                   CONST('N')
      *
      /EJECT
      ****************************************************************
      * MAIN LINE
      ****************************************************************
      *
      * PREPARE TO READ THE EQUIPMENT TYPE MASTER FILE AND
      * PRINT HEADINGS FOR FIRST TIME THROUGH
      *
     C     *LOVAL        SETLL     HSP021
     C                   EXCEPT    HDRPT
      *
      * READ AND PRINT ALL RECORDS IN THE EQUIPMENT TYPE MASTER.
      * FOR EACH RECORD READ, RETRIEVE THE EQUIPMENT ASSIGNED TO THIS
      * TYPE FROM THE EQUIPMENT MASTER.
      *
     C     *IN92         DOUEQ     *ON                                          MAIN DO
     C                   READ      HSP021                                 92
      *
     C     *IN92         IFEQ      *OFF                                         IF NOT EOF
      *
     C     *INOF         IFEQ      *ON
     C                   EXCEPT    HDRPT
     C                   ENDIF
      *
      * PRINT EQUIPMENT TYPE LINE
      *
     C                   EXCEPT    DTTYPE
     C                   MOVEL     YES           PRTFLG
      *
      * RETRIEVE/PRINT ALL EQUIPMENT FOR THIS EQUIPMENT TYPE
      *
     C                   EXSR      $EQUIP
     C                   ENDIF                                                  IF NOT EOF
      *
      *  IF THE MASTER FILE WAS EMPTY AND NO RECORDS WERE PRINTED,
      *  PRINT THE STANDARD 'NO DATA...' ERROR MESSAGE
      *
     C     PRTFLG        IFEQ      NO
     C                   EXCEPT    NODATA
     C                   ENDIF
      *
     C                   ENDDO                                                  MAIN DO
      *
     C                   SETON                                        LR
      /EJECT
      ****************************************************************
      * SUBROUTINES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * $EQUIP - RETRIEVE/PRINT EQUIPMENT
      *---------------------------------------------------------------
      *
     C     $EQUIP        BEGSR
      *
     C     ETETCD        SETLL     EQREC
      *
     C     *IN94         DOUEQ     *ON
     C     ETETCD        READE     HSL022A                                94
     C     *IN94         IFEQ      *OFF
      *
     C     *INOF         IFEQ      *ON
     C                   EXCEPT    HDRPT
     C                   EXCEPT    DTTYPE
     C                   ENDIF
      *
     C                   EXCEPT    DTEQ
     C                   ENDIF
     C                   ENDDO
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * *INZSR - INITIALIZATION SUBROUTINE
      *---------------------------------------------------------------
      *
     C     *INZSR        BEGSR
      *
      * SET PRINT FLAG TO DETERMINE IF ANY RECORDS WERE PRINTED
      *
     C                   MOVEL     NO            PRTFLG            1
      *
     C                   MOVE      *ALL'-'       DASH             76
      *
      * RETRIEVE TIME FOR REPORT HEADING
      *
     C                   TIME                    RTIME             6 0
      *
     C                   ENDSR
      *
      /EJECT
      *-------------------------------------------------------------
      * REPORT LAYOUT
      *-------------------------------------------------------------
      *
      *-------------------------------------------------------------
      * REPORT HEADING LINES
      *-------------------------------------------------------------
      *
     OQPRINT    E            HDRPT          1 01
     O                       SDPGM               10
     O                                           48 'HOG PRODUCTION SYSTEM'
     O                                           66 'DATE'
     O                       UDATE         Y     76
      *
     O          E            HDRPT          1
     O                       SDUSR               10
     O                                           41 'LISTING OF EQUIPMENT '
     O                                           55 'TYPE/EQUIPMENT'
     O                                           66 'TIME'
     O                       RTIME               76 '  :  :  '
      *
     O          E            HDRPT          2
     O                                           66 'PAGE'
     O                       PAGE          Z     71
      *
     O          E            HDRPT          1
     O                                            9 'EQUIPMENT'
     O                                           24 'EQUIPMENT'
      *
     O          E            HDRPT          1
     O                                            6 'TYPE'
     O                                           21 'TYPE'
     O                                           40 'EQUIPMENT'
     O                                           60 'EQUIPMENT'
      *
     O          E            HDRPT          1
     O                                            6 'CODE'
     O                                           25 'DESCRIPTION'
     O                                           37 'CODE'
     O                                           61 'DESCRIPTION'
      *
     O          E            HDRPT          1
     O                       DASH                76
      *
      *-------------------------------------------------------------
      * EQUIPMENT TYPE DETAIL LINE
      *-------------------------------------------------------------
      *
     O          E            DTTYPE      1
     O                       ETETCD               6
     O                       ETETDS              34
      *
      *-------------------------------------------------------------
      * EQUIPMENT DETAIL LINE
      *-------------------------------------------------------------
      *
     O          E            DTEQ        1
     O                       EQEQCD              40
     O                       EQEQDS              70
      *
      *-------------------------------------------------------------
      * NO DATA MESSAGE LINE
      *-------------------------------------------------------------
      *
     O          E            NODATA      1
     O                                           19 'NO DATA MEETS YOUR'
     O                                           38 'SELECTION CRITERIA'
