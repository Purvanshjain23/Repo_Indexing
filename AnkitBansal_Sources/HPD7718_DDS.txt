     A*%%TS  SD  20050215  102933  ISLRAMS     REL-V5R2M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD7718
     A*  TITLE:         VIEW REQUEST
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  CREATED:       03/15/04
     A*
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A                                      CF05(05 'REFRESH')
     A*
     A*-------------------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*-------------------------------------------------------------------------
     A          R HP7718S1                  SFL
     A*%%TS  SD  20050215  102933  ISLRAMS     REL-V5R2M0  5722-WDS
     A  85                                  SFLNXTCHG
     A            HDRRN1    R        H      REFFLD(@@RRN)
     A            HDRQSN    R        H      REFFLD(@@RQSN)
     A            D1ITCD         6A  O 12  2
     A            D1BTCD    R        O 12 10REFFLD(HSRREF/@@BTCD *LIBL/HSPREF)
     A            D1FBSN    R        O 12 19REFFLD(HSRREF/@@FBSN *LIBL/HSPREF)
     A                                      EDTCDE(Z)
     A            D1RQQT    R        O 12 28REFFLD(@@RQQT)
     A                                      EDTCDE(K)
     A            D1APQT    R        O 12 44REFFLD(HSRREF/@@APQT *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A            D1USQT    R        O 12 60REFFLD(HSRREF/@@USQT *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A            D1RQSCD   R        O 12 78REFFLD(@@RQSCD)
     A            D1ITDS    R        O 13 10REFFLD(HSRREF/@@ITDS *LIBL/HSPREF)
     A                                      COLOR(PNK)
     A                                 13 64'Over Rsn:'
     A                                      DSPATR(HI)
     A            D1ORCD    R        O 13 74REFFLD(@@ORCD)
     A                                      COLOR(PNK)
     A*
     A*
     A*-------------------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*-------------------------------------------------------------------------
     A          R HP7718C1                  SFLCTL(HP7718S1)
     A*%%TS  SD  20050215  102933  ISLRAMS     REL-V5R2M0  5722-WDS
     A                                      SFLSIZ(0050)
     A                                      SFLPAG(0005)
     A N82                                  ROLLUP(25 'PAGE FORWARD')
     A                                      OVERLAY
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 28'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USR)
     A                                      DSPATR(HI)
     A                                  2 33'VIEW REQUEST'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@JOB)
     A                                      DSPATR(HI)
     A                                  4  2'Work order number:'
     A            C1WO      R        O  4 21REFFLD(@@WO)
     A                                  4 35'System request number:'
     A            C1RQSN    R        O  4 58REFFLD(HSRREF/@@RQSN *LIBL/HSPREF)
     A                                      EDTCDE(Z)
     A                                  5  2'Request status ..:'
     A            C1RQSDS   R        O  5 21REFFLD(@@RQSDS)
     A                                  6  2'Request date ....:'
     A            C1MDY          6Y 0O  6 21EDTWRD('  /  /  ')
     A                                  7  2'Farm site .......:'
     A            C1FSCD    R        O  7 21REFFLD(@@FSCD)
     A                                      EDTCDE(Z)
     A            C1FSNM    R        O  7 35REFFLD(FSFSNM)
     A                                  8  2'Comment .........:'
     A            C1COM     R        O  8 21REFFLD(HSRREF/QHCOM *LIBL/HSPREF)
     A                                 10  2'Item'
     A                                      DSPATR(HI)
     A                                 11 29'Requested Qty'
     A                                      DSPATR(HI)
     A                                 11 46'Approved Qty'
     A                                      DSPATR(HI)
     A                                 11 66'Used Qty'
     A                                      DSPATR(HI)
     A                                 11 78'St'
     A                                      DSPATR(HI)
     A                                 11  2'Code'
     A                                      DSPATR(HI)
     A                                 10 20'Budget'
     A                                      DSPATR(HI)
     A                                 10 10'Budget'
     A                                      DSPATR(HI)
     A                                 11  9'Template'
     A                                      DSPATR(HI)
     A                                 11 20'Number'
     A                                      DSPATR(HI)
     A*-------------------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*-------------------------------------------------------------------------
     A          R HP7718K1
     A*%%TS  SD  20040315  133452  ISLRAMS     REL-V5R2M0  5722-WDS
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A                                 23 11'F5=Refresh'
      *
     A*-------------------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
     A*-------------------------------------------------------------------------
     A          R HP7718ES                  SFL SFLMSGRCD(24)
      *
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
     A*-------------------------------------------------------------------------
      * ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*-------------------------------------------------------------------------
     A          R HP7718EC                  SFLCTL(HP7718ES)
      *
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
