     A*%%TS  SD  20080804  092014  ISLRAMS     REL-V5R4M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION
     A*  DISPLAY FILE:  HPD1000
     A*  TITLE:         REVISE BGF WEEKLY PRODUCTION
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  DATE:          05/30/00
     A*
     A*  INDICATOR 60 IS ON WHEN IN REVISE MODE
     A*  INDICATOR 61 IS ON WHEN IN CREATE MODE
     A*  INDICATOR 62 IS ON WHEN IN DELETE MODE
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CF03(03 'EXIT')
     A*
     A*-----------------------------------------------------------------
     A* RECORD FORMAT ONE
     A*-----------------------------------------------------------------
     A          R HP1000R1
     A*%%TS  SD  20080804  085411  ISLRAMS     REL-V5R4M0  5722-WDS
     A N90                                  CF04(04 'PROMPT')
     A                                      SETOF(30)
     A                                      SETOF(31)
     A                                      SETOF(32)
     A                                      SETOF(33)
     A                                      SETOF(51)
     A                                      SETOF(53)
     A                                      RTNCSRLOC(&RCD &FLD)
     A                                      OVERLAY
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 29'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A            D1MODE         6A  O  2  2DSPATR(HI)
     A                                  2 26'REVISE BGF WEEKLY PRODUCTION'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  4  2'Week-ending date ..'
     A            D1WEMDY   R        B  4 24REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A  32                                  DSPATR(PC)
     A  32                                  DSPATR(RI)
     A N32 61                               DSPATR(HI)
     A  61                                  DSPATR(UL)
     A  60
     AO 62                                  DSPATR(PR)
     A                                  4 37'Calendar year/week .:'
     A            D1CDYR    R        O  4 59REFFLD(HSRREF/@@CDYR *LIBL/HSPREF)
     A                                      EDTCDE(M)
     A            D1CDWK    R        O  4 65REFFLD(HSRREF/@@CDWK *LIBL/HSPREF)
     A                                      EDTCDE(M)
     A                                  5 37'Pic date ...........:'
     A            D1PICDT   R        O  5 59REFFLD(HSRREF/@@PICDT *LIBL/HSPREF)
     A                                      EDTWRD('  -   ')
     A                                  6  2'Farm site .........'
     A  61                              6 22'?'
     A            D1FSCD    R        B  6 24REFFLD(HGFSCD)
     A                                      EDTCDE(M)
     A  31
     AO 51                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A N31 61                               DSPATR(HI)
     A  61                                  DSPATR(UL)
     A  60
     AO 62                                  DSPATR(PR)
     A            D1FSNM    R        O  6 37REFFLD(FSFSNM)
     A                                  7  2'Population code ...'
     A                                  7 22'?'
     A            D1POCD    R        B  7 24REFFLD(HSRREF/@@POCD *LIBL/HSPREF)
     A  33
     AO 53                                  DSPATR(PC)
     A  33                                  DSPATR(RI)
     A N62N33                               DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(PR)
     A            D1PODS    R        O  7 37REFFLD(HSRREF/@@PODS *LIBL/HSPREF)
     A                                 10  2'Percent'
     A                                 11  2'Breed'
     A                                 12  2'Back'
     A            D1BREDPR       4Y 1B 13  2EDTCDE(K)
     A  30                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A N62N30                               DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(PR)
     A*-----------------------------------------------------------------
     A* RECORD FORMAT TWO
     A*-----------------------------------------------------------------
     A          R HP1000R2
     A*%%TS  SD  20080804  092014  ISLRAMS     REL-V5R4M0  5722-WDS
     A N90                                  CF09(09 'ACCEPT')
     A N90N61                               CF11(11 'DELETE')
     A N90                                  CF12(12 'PREVIOUS')
     A                                      SETOF(34)
     A                                      SETOF(35)
     A                                      SETOF(37)
     A                                      SETOF(38)
     A                                      SETOF(39)
     A                                      SETOF(40)
     A                                      SETOF(42)
     A                                      OVERLAY
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 29'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A            DFMODE         6A  O  2  2DSPATR(HI)
     A                                  2 26'REVISE BGF WEEKLY PRODUCTION'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  4  2'--Preg Checked Positive--'
     A                                  5 15'Gilt'
     A                                  5 22'Group'
     A                                  5 34'Conception'
     A                                  6  2'Year'
     A                                  6  8'Week'
     A                                  6 15'Head'
     A                                  6 23'Head'
     A                                  6 37'Rate'
     A            D2PRYR    R        O  7  2REFFLD(HSRREF/@@CDYR *LIBL/HSPREF)
     A                                      EDTCDE(M)
     A            D2PRWK    R        O  7  9REFFLD(HSRREF/@@CDWK *LIBL/HSPREF)
     A                                      EDTCDE(M)
     A            D2GLPRHD  R        B  7 13REFFLD(HSRREF/@@PRHD *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A  39                                  DSPATR(PC)
     A  39                                  DSPATR(RI)
     A N62N39                               DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(PR)
     A            D2PRHD    R        B  7 21REFFLD(HSRREF/@@PRHD *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A  38                                  DSPATR(PC)
     A  38                                  DSPATR(RI)
     A N62N38                               DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(PR)
     A            D2CRPR    R        O  7 36REFFLD(HSRREF/@@CRPR *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A                                 10  2'----Total Group Farrowed----'
     A                                 10 35'Farrowing'
     A                                 11 15'Gilt'
     A                                 11 22'Group'
     A                                 11 38'Rate'
     A                                 12  2'Year'
     A                                 12  8'Week'
     A                                 12 15'Head'
     A                                 12 23'Head'
     A                                 12 35'For Group'
     A            D2FAYR    R        O 13  2REFFLD(HSRREF/@@CDYR *LIBL/HSPREF)
     A                                      EDTCDE(M)
     A            D2FAWK    R        O 13  9REFFLD(HSRREF/@@CDWK *LIBL/HSPREF)
     A                                      EDTCDE(M)
     A            D2GLFAHD  R        B 13 13REFFLD(HSRREF/@@FAHD *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A  34                                  DSPATR(PC)
     A  34                                  DSPATR(RI)
     A N62N34                               DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(PR)
     A            D2FAHD    R        B 13 21REFFLD(HSRREF/@@FAHD *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A  35                                  DSPATR(PC)
     A  35                                  DSPATR(RI)
     A N62N35                               DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(PR)
     A            D2FRPR    R        O 13 37REFFLD(HSRREF/@@FRPR *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A                                 16  5'Ending'
     A                                 16 17'Ending'
     A                                 16 30'Ending'
     A                                 17  5'Piglet'
     A                                 17 15'Gilt & Sow'
     A                                 17 31'Boar'
     A                                 18  3'Inventory'
     A                                 18 16'Inventory'
     A                                 18 29'Inventory'
     A            D2ENPLHD  R        B 19  5REFFLD(HSRREF/@@ENPLHD *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A  42                                  DSPATR(PC)
     A  42                                  DSPATR(RI)
     A N62N42                               DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(PR)
     A            D2GLSOHD  R        B 19 17REFFLD(HSRREF/@@GLSOHD *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A  37                                  DSPATR(PC)
     A  37                                  DSPATR(RI)
     A N62N37                               DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(PR)
     A            D2BOHD    R        B 19 30REFFLD(HSRREF/@@BOHD *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A  40                                  DSPATR(PC)
     A  40                                  DSPATR(RI)
     A N62N40                               DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(PR)
     A*
     A*-----------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*-----------------------------------------------------------------
     A          R HP1000K1
     A*%%TS  SD  20000601  071703  ISLRAMS     REL-V4R2M0  5769-PW1
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A  61N90                          23 12'F4=Prompt'
     A*
     A*-----------------------------------------------------------------
     A* COMMAND KEY RECORD 2
     A*-----------------------------------------------------------------
     A          R HP1000K2
     A*%%TS  SD  20010227  091702  ISLRAMS     REL-V4R4M0  5769-PW1
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A N62N90                          23 12'F9=Accept'
     A N90N61                          23 24'F11=Delete'
     A N90                             23 37'F12=Previous'
      *
      *------------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *-----------------------------------------------------------------
     A          R HP1000ES                  SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
      *------------------------------------------------------------------
      * ERROR MESSAGE SUBFILE CONTROL FORMAT
      *-----------------------------------------------------------------
     A          R HP1000EC                  SFLCTL(HP1000ES)
      *
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
