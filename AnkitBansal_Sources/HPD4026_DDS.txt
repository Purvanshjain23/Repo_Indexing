     A*%%TS  SD  20250708  090430  ISSGOYA     REL-V7R5M0  5770-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD4026
     A*  TITLE:         WORK WITH COMPANY
     A*  PROGRAMMER:    SHEFALI GOYAL(PIO)
     A*  DATE:          06/17/25
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A                                      CF05(05 'REFRESH')
     A                                      CF06(06 'CREATE')
     A                                      CF07(07 'PRINT')
     A                                      CF17(17 'PRINT OPTIONS')
     A                                      CF20(20 'WRKSPLF')
     A*
     A*
     A*---------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP4026S1                  SFL
     A*%%TS  SD  20250626  063350  ISSGOYA     REL-V7R5M0  5770-WDS
     A  85                                  SFLNXTCHG
     A            HDRRN1         4Y 0H
     A            D1OPT          2A  B  9  2CHGINPDFT(UL)
     A  30                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A N30                                  DSPATR(HI)
     A            D1COCD    R        O  9  9REFFLD(COREC/COCOCD *LIBL/HSP062)
     A            D1CONM    R        O  9 17REFFLD(COREC/COCONM *LIBL/HSP062)
     A*
     A*---------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP4026C1                  SFLCTL(HP4026S1)
     A*%%TS  SD  20250630  074246  ISSGOYA     REL-V7R5M0  5770-WDS
     A                                      SFLSIZ(0050)
     A                                      SFLPAG(0012)
     A N82                                  ROLLUP(25 'PAGE FORWARD')
     A                                      SETOF(30)
     A                                      SETOF(51)
     A                                      SETOF(52)
     A                                      RTNCSRLOC(&RCD &FLD)
     A                                      OVERLAY
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            RCD           10A  H
     A            FLD           10A  H
     A                                  1 27'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A                                  2 29'WORK WITH COMPANY '
     A                                      DSPATR(HI)
     A                                  4  2'2=Revise'
     A                                  6  7'Company'
     A                                      DSPATR(HI)
     A                                  7  2'Opt'
     A                                      DSPATR(HI)
     A                                  7  8'Code'
     A                                      DSPATR(HI)
     A                                  7 17'Name'
     A                                      DSPATR(HI)
     A                                  6 17'Company'
     A                                      COLOR(WHT)
     A            C1COCD    R        B  8  9REFFLD(COREC/COCOCD *LIBL/HSP062)
     A                                      COLOR(WHT)
     A            C1CONM    R        B  8 17REFFLD(COREC/COCONM *LIBL/HSP062)
     A                                      COLOR(WHT)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      TEXT('USER CODE')
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      TEXT('USER JOB')
     A                                      DSPATR(HI)
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      TEXT('PROGRAM NAME')
     A                                      DSPATR(HI)
     A*
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R HP4026K1
     A*%%TS  SD  20250708  090059  ISSGOYA     REL-V7R5M0  5770-WDS
     A  43                             21  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 22  2'F3=Exit  F5=Refresh  F6=Create  F7-
     A                                      =Print'
     A                                 22 44'F17=Print Options  F20=Spool Files'
     A*---------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE FORMAT
     A*---------------------------------------------------------------
     A*
     A          R HP4026ES                  SFL
     A*%%TS  SD  20250626  054734  ISSGOYA     REL-V7R5M0  5770-WDS
     A                                      TEXT('Error message subfile')
     A                                      SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ(10)
     A*
     A*---------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*---------------------------------------------------------------
     A*
     A          R HP4026EC                  SFLCTL(HP4026ES)
     A*%%TS  SD  20250626  054734  ISSGOYA     REL-V7R5M0  5770-WDS
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ(10)
