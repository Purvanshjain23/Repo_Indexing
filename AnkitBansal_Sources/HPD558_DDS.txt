     A*%%TS  SD  20030107  140811  ISLRAMS     REL-V5R1M0  5722-WDS
     A*****************************************************************
     A*
     A*  SYSTEM:        HOG PRODUCTION
     A*  DISPLAY FILE:  HPD558 - SELECT SCHEDULED SALES MOVEMENT
     A*  CREATED BY:    LEANNE FEDOR
     A*  DATE:          02/02/96
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA12(12 'PREVIOUS')
     A N82                                  ROLLUP(25 'PAGE FORWARD')
     A*---------------------------------------------------------------
     A* REFERENCE WINDOW RECORD FORMAT 1
     A*---------------------------------------------------------------
     A          R HP558R1
     A*%%TS  SD  19960202  083414  ISLRAMS     REL-V3R1M0  5763-PW1
     A                                      TEXT('Reference window')
     A                                      WINDOW(*DFT 17 75)
     A                                      WDWBORDER((*DSPATR RI) (*CHAR '    -
     A                                          '))
     A*---------------------------------------------------------------
     A* RECORD FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP558R2
     A                                      TEXT('Written in RPG pgm to prevent-
     A                                       screen from clearing')
     A                                      CLRL(*NO)
     A                                      OVERLAY
     A*---------------------------------------------------------------
     A* RECORD FORMAT TWO
     A*---------------------------------------------------------------
     A* THIS FORMAT IS NOT WRITTEN IN THE RPG PGM.  IT PREVENTS THE
     A* CALLING_PROGRAM_SCREEN FROM BEING CLEARED.
     A*---------------------------------------------------------------
     A          R HP558R3
     A                                      TEXT('Dummy format not written in R-
     A                                      PG pgm.')
     A                                      ASSUME
     A                                 11  1' '
     A*----------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*----------------------------------------------------------------
     A          R HP558S1                   SFL
     A*%%TS  SD  20000830  103600  ISLRAMS     REL-V4R4M0  5769-PW1
     A                                      TEXT('subfile record format one')
     A            HDRRN1         4S 0H
     A            DFSMDY    R        O  7  1REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  ')
     A            DFFSCD    R        O  7 11REFFLD(@@FSCD)
     A                                      EDTCDE(Z)
     A            DFMVSN    R        O  7 18REFFLD(SHMVSN)
     A                                      EDTCDE(M)
     A            DFSTCD    R        O  7 27REFFLD(SHSTCD)
     A            DFTICD    R        O  7 33REFFLD(@@TICD)
     A            DFCVNO    R        O  7 39REFFLD(SHCVNO)
     A                                      EDTCDE(M)
     A            DFCNAM    R        O  7 49REFFLD(@@DS15)
     A            DFFSBO    R        O  7 65REFFLD(@@BOCD)
     A            DFPTCT    R        O  7 72REFFLD(HSRREF/@@PTCT *LIBL/HSPREF)
     A                                      EDTCDE(Z)
     A*--------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*--------------------------------------------------------------
     A          R HP558C1                   SFLCTL(HP558S1)
     A*%%TS  SD  20030107  140811  ISLRAMS     REL-V5R1M0  5722-WDS
     A                                      SFLSIZ(0050)
     A                                      SFLPAG(0008)
     A                                      WINDOW(HP558R1)
     A                                      TEXT('control format one')
     A                                      OVERLAY
     A                                      SFLCSRRRN(&HDRRN)
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND(*MORE)
     A                                      RMVWDW
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            HDRRN          5S 0H
     A                                  1 22'SELECT SCHEDULED SALES MOVEMENT'
     A                                      DSPATR(HI)
     A                                  2  1'Position Cursor, Press Enter'
     A                                  4  1'Scheduled'
     A                                      DSPATR(HI)
     A                                  4 12'Farm'
     A                                      DSPATR(HI)
     A                                  4 27'Sales'
     A                                      DSPATR(HI)
     A                                  4 33'Trckr'
     A                                      DSPATR(HI)
     A                                  4 39'Customer'
     A                                      DSPATR(HI)
     A                                  4 66'Bus'
     A                                      DSPATR(HI)
     A                                  4 71'Times'
     A                                      DSPATR(HI)
     A                                  5  1'Ship Date'
     A                                      DSPATR(HI)
     A                                  5 12'Site'
     A                                      DSPATR(HI)
     A                                  5 18'Movement'
     A                                      DSPATR(HI)
     A                                  5 28'Type'
     A                                      DSPATR(HI)
     A                                  5 34'Code'
     A                                      DSPATR(HI)
     A                                  5 40'Number'
     A                                      DSPATR(HI)
     A                                  5 49'Customer Name'
     A                                      DSPATR(HI)
     A                                  5 65'Offic'
     A                                      DSPATR(HI)
     A                                  5 71'Print'
     A                                      DSPATR(HI)
     A            C1SMDY    R        B  6  1REFFLD(@@DT06)
     A                                      DSPATR(HI)
     A                                      EDTWRD('  /  /  -')
     A            C1FSCD    R        B  6 11REFFLD(@@FSCD)
     A                                      EDTCDE(M)
     A                                      DSPATR(HI)
     A            C1STCD    R        B  6 27REFFLD(FSPTCD)
     A                                      DSPATR(HI)
     A            C1TICD    R        B  6 33REFFLD(@@TICD)
     A                                      DSPATR(HI)
     A            C1CVNO    R        B  6 39REFFLD(SHCVNO)
     A                                      DSPATR(HI)
     A                                      EDTCDE(M)
     A            C1FSBO    R        B  6 65REFFLD(@@BOCD)
     A                                      DSPATR(HI)
     A*----------------------------------------------------------------
     A          R HP558K1
     A*----------------------------------------------------------------
     A                                      OVERLAY
     A                                      WINDOW(HP558R1)
     A                                 16  1'F12=Previous'
