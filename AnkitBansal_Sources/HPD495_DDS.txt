     A*%%TS  SD  20030717  092844  ISLRAMS     REL-V5R2M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD495
     A*  TITLE:         SPECIFY OPTIONS FOR INVENTORY RECAP BY GROUP
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  CREATED:       06/26/03
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A N99                                  CF04(04 'PROMPT')
     A  44N99                               CF08(08 'SUBMIT')
     A N99                                  CF17(17 'PRINT OPTIONS')
     A N99                                  CF20(20 'WRKSPLF')
     A*
     A*---------------------------------------------------------------
     A* RECORD FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP495R1
     A*%%TS  SD  20030717  092844  ISLRAMS     REL-V5R2M0  5722-WDS
     A                                      SETOF(30)
     A                                      SETOF(31)
     A                                      SETOF(32)
     A                                      SETOF(33)
     A                                      SETOF(34)
     A                                      SETOF(35)
     A                                      SETOF(36)
     A                                      SETOF(37)
     A                                      SETOF(38)
     A                                      SETOF(39)
     A                                      SETOF(52)
     A                                      SETOF(53)
     A                                      SETOF(54)
     A                                      SETOF(56)
     A                                      SETOF(57)
     A                                      SETOF(58)
     A                                      SETOF(59)
     A                                      OVERLAY
     A                                      RTNCSRLOC(&RCD &FLD)
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 25'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USR)
     A                                      DSPATR(HI)
     A                                  2 14'SPECIFY OPTIONS FOR INVENTORY RECA-
     A                                      P BY GROUP'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@JOB)
     A                                      DSPATR(HI)
     A                                  4  2'Accounting year .......'
     A N99        D1ACYR    R        B  4 28REFFLD(@@CDYR)
     A                                      EDTCDE(M)
     A N30                                  DSPATR(HI)
     A  30                                  DSPATR(RI)
     A  30                                  DSPATR(PC)
     A                                  5  2'Accounting period .....'
     A N99        D1ACPE    R        B  5 28REFFLD(@@CDWK)
     A                                      EDTCDE(M)
     A N31                                  DSPATR(HI)
     A  31                                  DSPATR(RI)
     A  31                                  DSPATR(PC)
     A                                  6  2'Sequenced by ..........'
     A                                  6 26'?'
     A N99        D1SEQCD   R        B  6 28REFFLD(@@FLAG)
     A N39                                  DSPATR(HI)
     A  39                                  DSPATR(RI)
     A  39
     AO 59                                  DSPATR(PC)
     A            D1SEQDS       44A  O  6 36
     A*
     A                                  7  2'Business office .......'
     A                                  7 26'?'
     A            D1FSBO    R        B  7 28REFFLD(HSRREF/@@BOCD *LIBL/HSPREF)
     A N32                                  DSPATR(HI)
     A  32                                  DSPATR(RI)
     A  32
     AO 52                                  DSPATR(PC)
     A  99                                  DSPATR(PR)
     A            D1BODS    R        O  7 36REFFLD(HSRREF/BOBODS *LIBL/HSPREF)
     A                                  7 69'(Blank=all)'
     A                                  8  2'Production type .......'
     A                                  8 26'?'
     A            D1PTCD    R        B  8 28REFFLD(PTPTCD)
     A N33                                  DSPATR(HI)
     A  33                                  DSPATR(RI)
     A  33
     AO 53                                  DSPATR(PC)
     A  99                                  DSPATR(PR)
     A            D1PTDS    R        O  8 36REFFLD(PTPTDS)
     A                                  8 69'(Blank=all)'
     A                                  9  2'Production phase ......'
     A                                  9 26'?'
     A            D1PPCD    R        B  9 28REFFLD(PPPPCD)
     A N34                                  DSPATR(HI)
     A  34                                  DSPATR(RI)
     A  34
     AO 54                                  DSPATR(PC)
     A  99                                  DSPATR(PR)
     A            D1PPDS    R        O  9 36REFFLD(PPPPDS)
     A                                  9 69'(Blank=all)'
     A                                 10  2'Cell ..................'
     A            D1CELL    R        B 10 28REFFLD(HSRREF/@@CELL *LIBL/HSPREF)
     A                                      EDTCDE(M)
     A N35                                  DSPATR(HI)
     A  35                                  DSPATR(RI)
     A  35                                  DSPATR(PC)
     A  99                                  DSPATR(PR)
     A                                 10 69'(Blank=all)'
     A                                 11  2'Company relationship ..'
     A                                 11 26'?'
     A            D1CRCD    R        B 11 28REFFLD(HSRREF/@@CRCD *LIBL/HSPREF)
     A N36                                  DSPATR(HI)
     A  36                                  DSPATR(RI)
     A  36
     AO 56                                  DSPATR(PC)
     A  99                                  DSPATR(PR)
     A            D1CRDS    R        O 11 36REFFLD(CRREC/CRCRDS *LIBL/HSP012)
     A                                 11 69'(Blank=all)'
     A                                 12  2'Farm site .............'
     A                                 12 26'?'
     A            D1FSCD    R        B 12 28REFFLD(FSFSCD)
     A                                      EDTCDE(M)
     A N37                                  DSPATR(HI)
     A  37                                  DSPATR(RI)
     A  37
     AO 57                                  DSPATR(PC)
     A  99                                  DSPATR(PR)
     A            D1FSNM    R        O 12 36REFFLD(FSFSNM)
     A                                 12 69'(Blank=all)'
     A                                 13  2'Group type ............'
     A                                 13 26'?'
     A            D1GTCD    R        B 13 28REFFLD(@@GTCD)
     A N38                                  DSPATR(HI)
     A  38                                  DSPATR(RI)
     A  38
     AO 58                                  DSPATR(PC)
     A  99                                  DSPATR(PR)
     A            D1GTDS    R        O 13 36REFFLD(GTGTDS)
     A                                 13 69'(Blank=all)'
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R HP495K1
     A*%%TS  SD  20030626  073932  ISLRAMS     REL-V5R2M0  5722-WDS
     A  43                             21  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A N99                             23 11'F4=Prompt'
     A  44N99                          23 22'F8=Submit'
     A N99                             23 33'F17=Print Options'
     A N99                             23 52'F20=Spool Files'
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *---------------------------------------------------------------
     A          R HP495ES                   SFL SFLMSGRCD(24)
      *
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
     A*---------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*---------------------------------------------------------------
     A*
     A          R HP495EC                   SFLCTL(HP495ES)
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
