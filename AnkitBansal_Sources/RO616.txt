      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Meat Costing
      * PROGRAM:     RO616
      * TITLE:       Listing of Meat Cost Overrides
      * PROGRAMMER:  LeAnne Ramsey
      * CREATED:     07/31/08
      *
      * FUNCTION:  Batch program to print records in the Meat Cost Overrides file.
      *            This listing is submitted from W/W Meat Cost Overrides when the user
      *            takes:
      *                1) F7=Listing
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fombyrel1  if   e           k disk
      *  Item default
      *
      *
     Frop016    if   e           k disk
      *  Meat cost overrides
      *
      *
     Fprint198  o    f  198        printer oflind(*inof)
      *
      /EJECT
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Standard fields
      *
     D dash            s            198    inz(*all'-')
     d rtime           s              6  0
      *
      *
      * Control fields
      *
     D first           s              1    inz('Y')
      *
      *
      * Workfields for date manipulation
      *
     D wkisodate       s               D   datfmt(*iso)
      *
      *
      * Print fields
      *
     D l1mdy           s              6  0
     D l1itds          s             30
     D l1ovitds        s             30
      *
      ****************************************************************
      * ARRAYS AND TABLES
      ****************************************************************
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ***************************************************************************************
      * MAINLINE
      ***************************************************************************************
      *
      * Print headings
     C                   exsr      $h1hdr
      *
      * Read/print each record in the Meat Cost Overrides file (there are
      * no selections).
      *
     C                   dou       *in90 = *on                                  Main do
     C                   read      rop016                                 90
     C                   if        *in90 = *off                                 If not EOF
     C                   move      no            first
      *
      * Print record
     C                   exsr      $l1dtl
     C                   endif                                                  If not EOF
     C                   enddo                                                  Main do
      *
      *---------------------------------------------------------------
      * EOF processing
      *---------------------------------------------------------------
      * If the file was empty, print the standard 'no data...' message
      *
     C                   if        first = yes
     C                   except    nodata
     C                   endif
      *
     C                   seton                                        lr
      /EJECT
      *---------------------------------------------------------------
      * Print detail line
      *---------------------------------------------------------------
      *
     C     $l1dtl        begsr
      *
      * Effective date
      *
     C     *iso          test(d)                 moefdt                 92
     C                   if        *in92 = *off                                 If OK
     C                   move      moefdt        wkisodate
     C     *mdy          move      wkisodate     l1mdy
     C                   endif                                                  If OK
      *
      * Retrieve Item Description
      *
     C     moitcd        chain     ombyrel1                           92
     C                   if        *in92 = *off
     C                   eval      l1itds  = byavna
     C                   else
     C                   eval      l1itds = 'Unknown'
     C                   endif
      *
      * Retrieve Override Item Description
      *
     C     moovitcd      chain     ombyrel1                           92
     C                   if        *in92 = *off
     C                   eval      l1ovitds  = byavna
     C                   else
     C                   eval      l1ovitds = 'Unknown'
     C                   endif
      *
      *
     C                   if        *inof = *on
     C                   exsr      $h1hdr
     C                   endif
     C                   except    l1dtl
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Print report heading
      *---------------------------------------------------------------
      *
     C     $h1hdr        begsr
      *
     C                   except    h1hdr
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Retrieve time for report heading.
      *
     C                   time                    rtime
      *
     C                   endsr
      /EJECT
      *-------------------------------------------------------------
      * Report heading lines
      *-------------------------------------------------------------
      *
     Oprint198  e            h1hdr          1 01
     O                       sdpgm               10
     O                                          101 'Resource Optimization '
     O                                          107 'System'
     O                                          188 'DATE'
     O                       udate         y    198
      *
     O          e            h1hdr          1
     O                       sdusr               10
     O                                          100 'Listing of Meat Cost '
     O                                          109 'Overrides'
     O                                          188 'TIME'
     O                       rtime              198 '  :  :  '
      *
      *
     O          e            h1hdr          2
     O                                          188 'PAGE'
     O                       page          z    198
      *
      *
      *-------------------------------------------------------------
      * Column headings
      *-------------------------------------------------------------
      *
     O          e            h1hdr       0  1
     O                                           50 'Override'
     O                                          108 'Active/'
     O          e            h1hdr       0  1
     O                                            7 'Item'
     O                                           50 'Item'
     O                                           96 'Effective'
     O                                          108 'Inactive'
      *
     O          e            h1hdr       0  1
     O                                            7 'Code'
     O                                           21 'Description'
     O                                           50 'Code'
     O                                           63 'Description'
     O                                           94 'Date'
     O                                          108 'Status'
      *
     O          e            h1hdr          1
     O                       dash               198
      *
      *-------------------------------------------------------------
      * Detail line
      *-------------------------------------------------------------
      *
     O          e            l1dtl          1
     O                       moitcd        z      7
     O                       l1itds              40
     O                       moovitcd      z     50
     O                       l1itds              82
     O                       l1mdy          b    95 '  /  /  '
     O                       moaist             105
      *
      *-------------------------------------------------------------
      * No data message line
      *-------------------------------------------------------------
      *
     O          e            nodata      1
     O                                           19 'No data meets your'
     O                                           39 'selection criteria.'
