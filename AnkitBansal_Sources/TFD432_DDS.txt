     A*%%TS  SD  20050316  145417  ISLRAMS     REL-V5R2M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        TRIUMPH FOODS
     A*  DISPLAY FILE:  TFD432
     A*  TITLE:         WORK WITH INVOICE CHARGES
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  DATE:          03/07/05
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/TFPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A                                      CF04(04 'PROMPT')
     A                                      CF05(05 'REFRESH')
     A N87                                  CF06(06 'CREATE')
     A                                      CF07(07 'LISTING')
     A                                      CF13(13 'FOLD/DROP')
     A                                      CF17(17 'PRINT OPTIONS')
     A                                      CF20(20 'WRKSPLF')
     A*
     A*-------------------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*-------------------------------------------------------------------------
     A          R TF432S1                   SFL
     A*%%TS  SD  20050316  145417  ISLRAMS     REL-V5R2M0  5722-WDS
     A  85                                  SFLNXTCHG
     A            HDRRN1    R        H      REFFLD(@@RRN)
     A            D1OPT     R        B 11  2REFFLD(@@OPT)
     A                                      CHGINPDFT(UL)
     A  30                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A N30                                  DSPATR(HI)
     A            D1ICDTMDY R        O 11  6REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  ')
     A            D1INSTCD  R        O 11 17REFFLD(@@INSTCD)
     A            D1INFL    R        O 11 26REFFLD(TFREF/@@INFL *LIBL/TFPREF)
     A            D1SMCD    R        O 11 32REFFLD(TFREF/@@SMCD *LIBL/TFPREF)
     A            D1CCCD    R        O 11 37REFFLD(TFREF/@@CCCD *LIBL/TFPREF)
     A            D1INSN    R        O 11 49REFFLD(TFREF/@@INSN *LIBL/TFPREF)
     A                                      EDTCDE(M)
     A            D1CFQCD   R        O 11 61REFFLD(TFREF/@@CFQCD *LIBL/TFPREF)
     A            D1UCFL    R        O 11 67REFFLD(TFREF/@@UCFL *LIBL/TFPREF)
     A            D1ICSN    R        O 11 72REFFLD(TFREF/@@ICSN *LIBL/TFPREF)
     A                                      EDTCDE(M)
     A                                 12 15'Amt:'
     A                                      COLOR(PNK)
     A            D1ICAM    R        O 12 20REFFLD(TFREF/@@ICAM *LIBL/TFPREF)
     A                                      EDTCDE(K)
     A                                      COLOR(PNK)
     A            D1COMTEXT      8A  O 12 45COLOR(PNK)
     A            D1ICCOM   R        O 12 54REFFLD(TFREF/@@ICCOM *LIBL/TFPREF)
     A                                      COLOR(PNK)
     A*
     A*-------------------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*-------------------------------------------------------------------------
     A          R TF432C1                   SFLCTL(TF432S1)
     A*%%TS  SD  20050316  144019  ISLRAMS     REL-V5R2M0  5722-WDS
     A                                      SFLSIZ(0050)
     A                                      SFLPAG(0005)
     A N82                                  ROLLUP(25 'PAGE FORWARD')
     A                                      SETOF(30)
     A                                      SETOF(31)
     A                                      SETOF(51)
     A                                      SETOF(52)
     A                                      SETOF(53)
     A                                      SETOF(54)
     A                                      SETOF(55)
     A                                      SETOF(56)
     A                                      SETOF(57)
     A                                      SETOF(58)
     A                                      RTNCSRLOC(&RCD &FLD)
     A                                      OVERLAY
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A  78                                  SFLFOLD(CF13)
     A  79                                  SFLDROP(CF13)
     A                                      SFLMODE(&FOLD)
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            FOLD           1A  H
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 27'WORK WITH INVOICE CHARGES'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USR)
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@JOB)
     A                                      DSPATR(HI)
     A                                  3  2'Include posted charges ..'
     A                                  3 28'?'
     A            C1YNFL    R        B  3 30REFFLD(@@FLAG)
     A  31
     AO 51                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A N31                                  DSPATR(HI)
     A                                  5  2'2=Revise'
     A                                  5 12'4=Delete'
     A                                  5 22'5=View Invoice Charge'
     A                                  5 45'7=View Invoice'
     A                                  7 17'?'
     A                                      DSPATR(HI)
     A                                  7 26'?'
     A                                      DSPATR(HI)
     A                                  7 33'?'
     A                                      DSPATR(HI)
     A                                  7 40'?'
     A                                      DSPATR(HI)
     A                                  7 52'?'
     A                                      DSPATR(HI)
     A                                  7 61'?'
     A                                      DSPATR(HI)
     A                                  7 67'?'
     A                                      DSPATR(HI)
     A                                  8  5'-Invoice Charge-'
     A                                      DSPATR(HI)
     A                                  8 23'Incl In'
     A                                      DSPATR(HI)
     A                                  8 32'Sys'
     A                                      DSPATR(HI)
     A                                  8 37'Charge'
     A                                      DSPATR(HI)
     A                                  8 49'Invoice'
     A                                      DSPATR(HI)
     A                                  8 59'Freq'
     A                                      DSPATR(HI)
     A                                  8 65'User'
     A                                      DSPATR(HI)
     A                                  8 73'Charge'
     A                                      DSPATR(HI)
     A                                  9  2'Op'
     A                                      DSPATR(HI)
     A                                  9  7'Date'
     A                                      DSPATR(HI)
     A                                  9 14'Status'
     A                                      DSPATR(HI)
     A                                  9 23'Inv Tot'
     A                                      DSPATR(HI)
     A                                  9 32'Mod'
     A                                      DSPATR(HI)
     A                                  9 37'Code'
     A                                      DSPATR(HI)
     A                                  9 50'Number'
     A                                      DSPATR(HI)
     A                                  9 59'Code'
     A                                      DSPATR(HI)
     A                                  9 65'Contr'
     A                                      DSPATR(HI)
     A                                  9 73'Number'
     A                                      DSPATR(HI)
     A            C1ICDTMDY R        B 10  6REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  ')
     A                                      DSPATR(HI)
     A N31N51N52
     A N53N54N55
     A N56N57N58                            DSPATR(PC)
     A            C1INSTCD  R        B 10 17REFFLD(TFREF/@@INSTCD *LIBL/TFPREF)
     A  52                                  DSPATR(PC)
     A                                      DSPATR(HI)
     A            C1INFL    R        B 10 26REFFLD(TFREF/@@INFL *LIBL/TFPREF)
     A  58                                  DSPATR(PC)
     A                                      DSPATR(HI)
     A            C1SMCD    R        B 10 32REFFLD(TFREF/@@SMCD *LIBL/TFPREF)
     A  55                                  DSPATR(PC)
     A                                      DSPATR(HI)
     A            C1CCCD    R        B 10 37REFFLD(TFREF/@@CCCD *LIBL/TFPREF)
     A  53                                  DSPATR(PC)
     A                                      DSPATR(HI)
     A            C1INSN    R        B 10 49REFFLD(TFREF/@@INSN *LIBL/TFPREF)
     A  56                                  DSPATR(PC)
     A                                      DSPATR(HI)
     A                                      EDTCDE(M)
     A            C1CFQCD   R        B 10 61REFFLD(TFREF/@@CFQCD *LIBL/TFPREF)
     A  54                                  DSPATR(PC)
     A                                      DSPATR(HI)
     A            C1UCFL    R        B 10 67REFFLD(TFREF/@@UCFL *LIBL/TFPREF)
     A  57                                  DSPATR(PC)
     A                                      DSPATR(HI)
     A            C1ICSN    R        B 10 72REFFLD(TFREF/@@ICSN *LIBL/TFPREF)
     A                                      DSPATR(HI)
     A                                      EDTCDE(M)
     A*
     A*-------------------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*-------------------------------------------------------------------------
     A          R TF432K1
     A*%%TS  SD  20040223  175559  ISLRAMS     REL-V5R2M0  5722-WDS
     A  43                             21  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 22  2'F3=Exit'
     A                                 22 11'F4=Prompt'
     A                                 22 22'F5=Refresh'
     A                                 22 34'F6=Create'
     A                                 22 45'F7=Listing'
     A                                 22 57'F13=Fold/Unfold'
     A                                 23 34'F17-Print Options'
     A                                 23 57'F20=Spool Files'
      *
     A*-------------------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
     A*-------------------------------------------------------------------------
     A          R TF432ES                   SFL SFLMSGRCD(24)
      *
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
     A*-------------------------------------------------------------------------
      * ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*-------------------------------------------------------------------------
     A          R TF432EC                   SFLCTL(TF432ES)
      *
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
