      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      HOG PRODUCTION
      * PROGRAM:     HP6041
      * TITLE:       LISTING OF SHIP TO SBD CUSTOMERS
      * PROGRAMMER:  BRAD BADEN
      * CREATED:     11/02/2017
      *
      * FUNCTION: BATCH PROGRAM TO LIST THE RECORDS IN THE
      *           SHIP TO SBD CUSTOMER FILE HSP195
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      * xx/xx/xx  xxxxxxxxxxxxxxxxxxx
      *           xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
      *           xxxxxxxxxxxxxxxxxxxxx.
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     FHSL195A   IF   E           K DISK
      *    SHIP TO SBD CUSTOMER        (KEY: CTAJD)
      *
     FQPRINT    O    F  132        PRINTER OFLIND(*INOF)
      *
      /EJECT
      ****************************************************************
      * INPUT SPECS
      ****************************************************************
      *
      *---------------------------------------------------------------
      * STANDARD PROGRAM STATUS DATA STRUCTURE
      *---------------------------------------------------------------
      *    EXTERNALLY DEFINED AS UTPGFR (RECORD FORMAT: PGMDSR)
     D PGMDS         ESDS                  EXTNAME(UTPGFR)
      *
      *---------------------------------------------------------------
      *  NAMED CONSTANTS
      *---------------------------------------------------------------
      *
     D YES             C                   CONST('Y')
     D NO              C                   CONST('N')
      *
      /EJECT
      ****************************************************************
      * MAIN LINE
      ****************************************************************
      *
      * PREPARE TO READ THE SHIP TO SBD CUSTOMER REF FILE AND
      * PRINT HEADINGS FOR FIRST TIME THROUGH
      *
     C     *LOVAL        SETLL     HSL195A
     C                   EXCEPT    HRHDR
      *
      * READ AND PRINT ALL RECORDS IN THE SHIP TO SBD CUSTOMER REF FILE
      *
     C     *IN92         DOUEQ     *ON                                          MAIN DO
     C                   READ      HSL195A                                92
     C     *IN92         IFEQ      *OFF                                         IF NOT EOF
      *
     C     *INOF         IFEQ      *ON
     C                   EXCEPT    HRHDR
     C                   ENDIF
      *
      * PRINT FEED TYPE DETAIL LINE
      *
     C                   EXCEPT    DTLINE
     C                   MOVEL     YES           PRTFLG
     C                   ENDIF                                                  IF NOT EOF
     C                   ENDDO                                                  MAIN DO
      *
      *---------------------------------------------------------------
      * EOF PROCESSING
      *---------------------------------------------------------------
      *  IF THE MASTER FILE WAS EMPTY AND NO RECORDS WERE PRINTED,
      *  PRINT THE STANDARD 'NO DATA...' ERROR MESSAGE
      *
     C     PRTFLG        IFEQ      NO
     C                   EXCEPT    NODATA
     C                   ENDIF
      *
     C                   EXCEPT    ENDRPT
      *
     C                   SETON                                        LR
      /EJECT
      *---------------------------------------------------------------
      * *INZSR - INITIALIZATION SUBROUTINE
      *---------------------------------------------------------------
      *
     C     *INZSR        BEGSR
      *
      * SET PRINT FLAG TO DETERMINE IF ANY RECORDS WERE PRINTED
      * RETRIEVE TIME FOR REPORT HEADING
      *
     C                   MOVEL     NO            PRTFLG            1
     C                   MOVEL     *ALL'-'       DASH             80
     C                   TIME                    RTIME             6 0
      *
     C                   ENDSR
      /EJECT
      *-------------------------------------------------------------
      * REPORT HEADING LINES
      *-------------------------------------------------------------
      *
     OQPRINT    E            HRHDR          1 01
     O                       SDPGM               10
     O                                           48 'HOG PRODUCTION SYSTEM'
     O                                           70 'DATE'
     O                       UDATE         Y     80
      *
     O          E            HRHDR          1
     O                       SDUSR               10
     O                                           48 'LISTING OF SHIP TO SBD CUS'
     O                                           54 'TOMERS'
     O                                           70 'TIME'
     O                       RTIME               80 '  :  :  '
      *
     O          E            HRHDR          2
     O                                           70 'PAGE'
     O                       PAGE          Z     80
      *
     O          E            HRHDR          1
     O                                           55 'CREATE HPE'
      *
     O          E            HRHDR          1
     O                                           54 'DELIVERY'
      *
     O          E            HRHDR          1
     O                                            8 'CUSTOMER'
     O                                           14 'NAME'
     O                                           54 'SCHEDULE'
     O                                           65 'STATUS'
      *
     O          E            HRHDR          1
     O                       DASH                80
      *
      *-------------------------------------------------------------
      * FEED TYPES DETAIL LINE
      *-------------------------------------------------------------
      *
     O          E            DTLINE      1
     O                       CUAJD                8
     O                       CUCNM               40
     O                       CUPSTHPE            50
     O                       CUCUST              62
      *
      *-------------------------------------------------------------
      * END OF REPORT LINE
      *-------------------------------------------------------------
      *
     O          E            ENDRPT      2
     O                                           21 '*** END OF REPORT ***'
      *
      *-------------------------------------------------------------
      * NO DATA MESSAGE LINE
      *-------------------------------------------------------------
      *
     O          E            NODATA      1
     O                                           19 'NO DATA MEETS YOUR'
     O                                           38 'SELECTION CRITERIA'
