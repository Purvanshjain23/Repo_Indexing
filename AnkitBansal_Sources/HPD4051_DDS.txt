     A*%%TS  SD  20210622  155706  ISSMASO     REL-V7R3M0  5770-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD4051
     A*  TITLE:         SPECIFY OPTIONS FOR BGF PRODUCTION EDIT LISTINGS
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  CREATED:       02/20/01
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A  44N99                               CF08(08 'SUBMIT')
     A N99                                  CF17(17 'PRINT OPTIONS')
     A N99                                  CF20(20 'WRKSPLF')
     A*
     A*---------------------------------------------------------------
     A* RECORD FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP4051R1
     A*%%TS  SD  20020109  080004  ISLRAMS     REL-V4R4M0  5769-PW1
     A                                      SETOF(30)
     A                                      SETOF(31)
     A                                      OVERLAY
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 25'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USR)
     A                                      DSPATR(HI)
     A                                  2 14'SPECIFY OPTIONS FOR BGF PRODUCTION-
     A                                       EDIT LISTINGS'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@JOB)
     A                                      DSPATR(HI)
     A                                  4  2'Calendar year ..'
     A N99        D1CDYR    R        B  4 20REFFLD(@@CDYR)
     A                                      EDTCDE(M)
     A N30                                  DSPATR(HI)
     A  30                                  DSPATR(RI)
     A  30                                  DSPATR(PC)
     A                                  5  2'Calendar week ..'
     A N99        D1CDWK    R        B  5 20REFFLD(@@CDWK)
     A                                      EDTCDE(M)
     A N31                                  DSPATR(HI)
     A  31                                  DSPATR(RI)
     A  31                                  DSPATR(PC)
     A                                  7  2'This function will submit 3 edit l-
     A                                      istings over the weekly production -
     A                                      data:'
     A                                  9 12'2) Weekly BGF Datamart Production -
     A                                      Listing'
     A                                  8 12'1) Weekly BGF Datamart Variance Re-
     A                                      port'
     A                                 10 12'3) Listing of Farms with Daily Ent-
     A                                      ries But No Weekly Entries'
     A*
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R HP4051K1
     A*%%TS  SD  20210622  084543  ISSMASO     REL-V7R3M0  5770-WDS
     A  43                             21  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A  44N99                          23 11'F8=Validate/Submit'
     A N99                             23 32'F17=Print Options'
     A N99                             23 52'F20=Spool Files'
     A*
     A          R HP4051E1
     A*---------------------------------------------------------------
     A* SCREEN - USED TO REPORT ERROR TO USERS INER-ACTIVELY
     A*            TEST FOR FILE BEING LOCKED AND CAN NOT BE USED
     A*            AT THIS TIME
     A*---------------------------------------------------------------
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 25'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USR)
     A                                      DSPATR(HI)
     A                                  2 14'SPECIFY OPTIONS FOR BGF PRODUCTION-
     A                                       EDIT LISTINGS'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@JOB)
     A                                      DSPATR(HI)
     A                                  4  2'Calendar year ..'
     A            D1CDYR    R        O  4 20REFFLD(@@CDYR)
     A                                      EDTCDE(M)
     A                                      DSPATR(PC)
     A                                  5  2'Calendar week ..'
     A            D1CDWK    R        O  5 20REFFLD(@@CDWK)
     A                                      EDTCDE(M)
     A                                      DSPATR(PC)
     A                                 19 05'BGFEDITS Did not run because the -
     A                                      data mart files are currently in -
     A                                      use.'
     A                                 20 05'Resubmit your report at a -
     A                                      later time.'
     A                                      DSPATR(HI)
     A                                 23  2'F3=Exit'
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *---------------------------------------------------------------
     A          R HP4051ES                  SFL SFLMSGRCD(24)
      *
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
     A*---------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*---------------------------------------------------------------
     A*
     A          R HP4051EC                  SFLCTL(HP4051ES)
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
