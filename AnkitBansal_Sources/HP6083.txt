      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Hog Production System
      * PROGRAM:     HP6083
      * TITLE:       Listing of Quarterly Prediction Values
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     01/31/02
      *
      * FUNCTION:  Batch program to list the records in the Quarterly Prediction Values file.
      *            This listing is submitted from HP4419-Specify Options for
      *            Quarterly Prediction Values which is called from HP4012-W/W Quarterly
      *            Prediction Values.
      *
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
      *
     Fhsp025    if   e           k disk
      *    Group type
      *
      *
     Fhsp083    if   e           k disk
      *   Quarterly prediction values
      *   (records selected by query in submission CL)
      *
      *
     Fqprint    o    f  137        printer oflind(*inof)
      *
      /EJECT
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      *---------------------------------------------------------------
      * CONSTANTS
      *---------------------------------------------------------------
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Standard fields
      *
     D prtfl           s              1    inz('N')
     D dash            s            137    inz(*all'-')
     d rtime           s              6  0
      *
      *
      * Print fields
      *
     d h1gtds          s                   like(gtgtds)
      *
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *---------------------------------------------------------------
      * Local data area.
      *---------------------------------------------------------------
      *
     Dlda             uds                  dtaara(*lda)
     D  ldcdyr                 1      4  0
     D  ldcdqr                 5      5  0
     D  ldcell                 6      7  0
     D  ldgtcd                 8      8
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * MAINLINE
      ****************************************************************
      *
      * Prepare to read the file and print headings the first time through.
      *
     C                   except    h1hdr
      *
     C                   dou       *in90 = *on                                  Main do
     C                   read      hsp083                                 90
     C                   if        *in90 = *off                                 If not EOF
      *
     C                   if        *inof = *on
     C                   except    h1hdr
     C                   endif
      *
     C                   except    l1dtl
     C                   movel     yes           prtfl
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Main do
      *
      *---------------------------------------------------------------
      * EOF processing
      *---------------------------------------------------------------
      * If the file was empty, print the standard 'no data...'
      * error message
      *
     C                   if        prtfl = no
     C                   except    nodata
     C                   endif
      *
     C                   seton                                        lr
      /EJECT
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Rretrieve time for report heading.
      *
     C                   time                    rtime
      *
      * Setup the user selections in the headings.
      *
      * Calendar year
     C                   if        ldcdyr = 0
     C                   seton                                        88
     C                   endif
      *
      * Calendar quarter
     C                   if        ldcdqr = 0
     C                   seton                                        87
     C                   endif
      *
      * Cell
     C                   if        ldcell = 0
     C                   seton                                        86
     C                   endif
      *
      * Group type
     C                   if        ldgtcd = *blank
     C                   seton                                        85
     C                   else
     C     ldgtcd        chain     hsp025                             92
     C                   if        *in92 = *off
     C                   move      gtgtds        h1gtds
     C                   else
     C                   eval      h1gtds = 'Unknown'
     C                   endif
     C                   endif
      *
     C                   endsr
      /EJECT
      *-------------------------------------------------------------
      * Report heading lines
      *-------------------------------------------------------------
      *
     Oqprint    e            h1hdr          1 01
     O                       sdpgm               10
     O                                           68 'HOG PRODUCTION SYSTEM'
     O                                          127 ' DATE'
     O                       udate         y    137
      *
     O          e            h1hdr          1
     O                       sdusr               10
     O                                           60 'QUARTERLY PREDICTION'
     O                                           75 ' VALUES LISTING'
     O                                          127 'TIME'
     O                       rtime              137 '  :  :  '
      *
     O          e            h1hdr          1
     O                                          127 'PAGE'
     O                       page          z    137
      *
     O          e            h1hdr          1
     O                                           17 'Calendar Year ..:'
     O              N88      ldcdyr        z     24
     O               88                          24 'All'
      *
      *
     O          e            h1hdr          1
     O                                           17 'Calendar Quarter:'
     O              N87      ldcdqr        z     24
     O               87                          24 'All'
      *
      *
     O          e            h1hdr          1
     O                                           17 'Cell ...........:'
     O              n86      ldcell        z     24
     O               86                          24 'All'
      *
      *
     O          e            h1hdr          2
     O                                           17 'Group type .....:'
     O              n85      ldgtcd              24
     O              n85      h1gtds              41
     O               85                          24 'All'
      *
      *-------------------------------------------------------------
      * Column headings
      *-------------------------------------------------------------
      *
      *
     O          e            h1hdr          1
     O                                           97 'Piglet'
     O                                          106 'Nursery'
     O                                          116 'Finisher'
     O                                          127 'Genetic'
      *
     O          e            h1hdr          1
     O                                           21 'Group'
     O                                           41 'Total'
     O                                           53 'Conception'
     O                                           64 'Farrowing'
     O                                           74 'Fall-out'
     O                                           86 'Born Alive'
     O                                           97 'Mortality'
     O                                          106 'Removal'
     O                                          116 'Removal'
     O                                          127 'Selection'
     O                                          137 'Sort-off'
      *
      *
     O          e            h1hdr          1
     O                                            8 'Year/Qtr'
     O                                           14 'Cell'
     O                                           20 'Type'
      *
     O                                           41 'Services'
     O                                           53 'Rate %'
     O                                           64 'Rate %'
     O                                           74 'Rate %'
     O                                           86 'Per Litter'
     O                                           97 'Rate %'
     O                                          106 'Rate %'
     O                                          116 'Rate %'
     O                                          127 'Rate %'
     O                                          137 'Rate %'
      *
     O          e            h1hdr          1
     O                       dash               137
      *
      *
      *-------------------------------------------------------------
      * Detail line
      *-------------------------------------------------------------
      *
     O          e            l1dtl       1  1
      *
     O                       qpcdyr        z      4
     O                                            6 '-'
     O                       qpcdqr        z      7
     O                       qpcell        z     13
     O                       qpgtcd              19
      *
     O                                           31 'Low'
     O                       qpltosv       k     41
     O                       qplcrpr       k     53
     O                       qplfrpr       k     63
     O                       qplfopr       k     74
     O                       qplltbahd     k     86
     O                       qplpmpr       k     97
     O                       qplnrmpr      k    106
     O                       qplfrmpr      k    116
     O                       qplgspr       k    127
     O                       qplsopr       k    137
      *
     O          e            l1dtl          1
     O                                           31 'High'
     O                       qphtosv       k     41
     O                       qphcrpr       k     53
     O                       qphfrpr       k     63
     O                       qphfopr       k     74
     O                       qphltbahd     k     86
     O                       qphpmpr       k     97
     O                       qphnrmpr      k    106
     O                       qphfrmpr      k    116
     O                       qphgspr       k    127
     O                       qphsopr       k    137
      *
      *
     O          e            l1dtl          1
     O                                           31 'Override'
     O                       qpotosv       k     41
     O                       qpocrpr       k     53
     O                       qpofrpr       k     63
     O                       qpofopr       k     74
     O                       qpoltbahd     k     86
     O                       qpopmpr       k     97
     O                       qponrmpr      k    106
     O                       qpofrmpr      k    116
     O                       qpogspr       k    127
     O                       qposopr       k    137
      *
      *-------------------------------------------------------------
      * No data message line
      *-------------------------------------------------------------
      *
     O          e            nodata      1
     O                                           19 'NO DATA MEETS YOUR'
     O                                           38 'SELECTION CRITERIA'
