     A*%%TS  SD  19991101  151521  ISLRAMS     REL-V4R2M0  5769-PW1
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION
     A*  DISPLAY FILE:  HPD5007 - SELECT OPEN, DISPOSED OR CLOSED GROUP
     A*                           ON A FARM
     A*  CREATED BY:    LEANNE FEDOR
     A*  DATE:          11/01/99
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA12(12 'PREVIOUS')
     A N82                                  ROLLUP(25 'PAGE FORWARD')
     A*--------------------------------------------------------------*
     A* REFERENCE WINDOW RECORD FORMAT 1
     A*--------------------------------------------------------------*
     A          R HP5007R1
     A*%%TS  SD  19991101  151521  ISLRAMS     REL-V4R2M0  5769-PW1
     A                                      TEXT('Reference window')
     A                                      WINDOW(*DFT 20 50)
     A                                      WDWBORDER((*DSPATR RI) (*CHAR '    -
     A                                          '))
     A*--------------------------------------------------------------*
     A* RECORD FORMAT ONE
     A*--------------------------------------------------------------*
     A          R HP5007R2
     A                                      TEXT('Written in RPG pgm to prevent-
     A                                       screen from clearing')
     A                                      CLRL(*NO)
     A                                      OVERLAY
     A*--------------------------------------------------------------*
     A* RECORD FORMAT TWO
     A*--------------------------------------------------------------*
     A* THIS FORMAT IS NOT WRITTEN IN THE RPG PGM.  IT PREVENTS THE
     A* CALLING_PROGRAM_SCREEN FROM BEING CLEARED.
     A*--------------------------------------------------------------*
     A          R HP5007R3
     A                                      TEXT('Dummy format not written in R-
     A                                      PG pgm.')
     A                                      ASSUME
     A                                 11  1' '
     A*--------------------------------------------------------------*
     A* SUBFILE FORMAT ONE
     A*--------------------------------------------------------------
     A          R HP5007S1                  SFL
     A*%%TS  SD  19991101  151521  ISLRAMS     REL-V4R2M0  5769-PW1
     A                                      TEXT('subfile record format one')
     A            HDRRN1         4S 0H
     A            DFBLCD    R        O  8  1REFFLD(HSRREF/HGBLCD *LIBL/HSPREF)
     A            DFRMCD    R        O  8 11REFFLD(HSRREF/HGRMCD *LIBL/HSPREF)
     A            DFHGCD    R        O  8 19REFFLD(HGHGCD)
     A            DFGSCD    R        O  8 31REFFLD(HSRREF/@@GSCD *LIBL/HSPREF)
     A*--------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*--------------------------------------------------------------
     A          R HP5007C1                  SFLCTL(HP5007S1)
     A*%%TS  SD  19991101  151521  ISLRAMS     REL-V4R2M0  5769-PW1
     A                                      SFLSIZ(0050)
     A                                      SFLPAG(0010)
     A                                      WINDOW(HP5007R1)
     A                                      TEXT('control format one')
     A                                      OVERLAY
     A                                      SFLCSRRRN(&HDRRN)
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND(*MORE)
     A                                      RMVWDW
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            HDRRN          5S 0H
     A                                  1  1'SELECT OPEN, DISPOSED OR CLOSED GR-
     A                                      OUP ON A FARM'
     A                                      DSPATR(HI)
     A                                  2  1'Position Cursor, Press Enter'
     A                                  4  1'Farm site:'
     A            C1FSCD    R        O  4 12REFFLD(HSRREF/@@FSCD *LIBL/HSPREF)
     A                                      EDTCDE(Z)
     A            C1FSNM    R        O  4 19REFFLD(FSREC/FSFSNM *LIBL/HSP018)
     A                                  6  1'Building'
     A                                      DSPATR(HI)
     A                                  6 11'Room'
     A                                      DSPATR(HI)
     A                                  6 19'Group'
     A                                      DSPATR(HI)
     A                                  6 29'Status'
     A                                      DSPATR(HI)
     A            C1BLCD    R        B  7  1REFFLD(HSRREF/@@BLCD *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A            C1RMCD    R        B  7 11REFFLD(HSRREF/@@RMCD *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A            C1GSCD    R        B  7 31REFFLD(HSRREF/@@GSCD *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A*
     A*-------------------------------------------------------------------------
     A* COMMAND LINE
     A*-------------------------------------------------------------------------
     A          R HP5007K1
     A                                      OVERLAY
     A                                      WINDOW(HP5007R1)
     A                                 19  1'F12=Previous'
