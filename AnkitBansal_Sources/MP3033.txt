      *****************  RPG PROGRAM HEADING  ************************
      *ˆNOTE: Programs in E1IPLIB takes precedence over PRKPLIB.
      ****************************************************************
      *
      * ENVIRONMENT:  PORK DIVISION   - moved to E1IDEVMDL
      * SYSTEM:       AS/400
      * PROGRAM:      MP3033
      * DESCRIPTION:  MP2: Process Companies--Post Purchase Orders to JDE
      * PROGRAMMER:   LeAnne Ramsey       /   Rose Centonze
      * DATE:         03/12/14            /   04/06/2020
      *
      *
      * FUNCtion:  We will now be uploading the Guyman MP2 Plant data (company 360)
      *            as well as the Live data (company 350). So, we could no longer
      *            use data area DAMP2CONO in PRKFLIB to control processing.
      *
      *            I created an I.S.-controlled file MPP108 to hold the companies
      *            that will be uploaded in each environment (PRKFLIB and HPBFLIB).
      *
      *            So, this new program will read the "companies" in that file and
      *            process each one...if it has any data in the workfile MPP303.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      * 4/22/2017 Barb Gutierrez
      *           Company 360 will no longer be integrated to JDE since
      *           they will be in M3.  Decision made to not integrate 360
      *           MP2 data into M3.  All entries will be manually entered in M3
      *           360.
      *           TFS# 10220
      *
      * 04/08/19  Brad baden
      *           Only check if Live in M3 when company number changes.
      *           Also, only call MPJDE303 when company number changes.
      *
      * 04/06/20  Rose Centonze (P16169)
      *           Get Live with jde world/Live with E1 - call appropriate function
      *           -- also from company values
      *
      * 05/26/21  Danny Nguyen   (S17068)
      *           DBFC on MPP303 file. PDITEMRT field length changed from 9.4 to 11.4.
      *           Commented out old JDE logic 'JWLIVE'. No longer used.
      *
      /eject
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fmpp108    if   e           k disk
      *  MP2: Upload companies
      *
     Fpdkmrel0  if   e           k disk
      *  Company values file
      *
      *
     Fmpp303    if   e           k disk
      *  MP2: Workfile of receipts to post to JDE
      *
      /eject
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
     D xxacono         s                   like(upacono)
     D xxlivejw        s              1                                          live w JDE Wrld
     D xxlivee1        s              1                                          live w E1
      *
     D*wkv8cd          s                   like(kmv8cd) inz('M3MP2    ')
     D wkv8cd          s                   like(kmv8cd)
     D svacono         s                   like(upacono)
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * MAINLINE
      ****************************************************************
      *
      * Process each record (aka: Accounting Company) in the I.S.-controlled
      * Upload Companies file.  For each company, call the JDE post program
      *
     C     *loval        setll     mpp108
      *
     C                   dou       *in90 = *on                                  Main do
     C                   read      mpp108                                 90
     C                   if        *in90 = *off                                 If not EOF
      *
      * Perform the M3 live check and call to MPJDE303 only when Company Number changes.
      *
     C                   if        upacono <> svacono                           if company# changes
     C                   eval      svacono =  upacono
      *
      * blg: TFS 10220   Check to see if live in M3 or not. Don't process if it is.
      *
     c                   eval      wkv8cd =  'M3MP2     '                        p16169
     C     Key           chain     pdkmrel0                           93
     C                   if        *in93 = *off                                 93 = off
     C***              if        kmbxtx = 'N'                                 not live M3
     C                   if        kmbxtx = 'N' or kmbxtx = 'P'                 not live/parallel
      *
     C     upacono       chain     mpp303                             92
     C                   if        *in92 = *off                                 If data
      *  Live with JW World
      ** S17068 - Commented out old JDE program.
17068c**                 eval      wkv8cd =  'JWLIVE    '                        p16169
  |  C**   Key           chain     pdkmrel0                           95
  |  C**                 if        *in95 = *off                                 If found
  |  C**                 if        kmbxtx = 'Y' or kmbxtx = 'P'                 if live/parallel JW
  |  C**                 call      'MPJDE303'
  |  C**                 parm      upacono       xxacono
  |  C**                 parm                    kmbxtx                         live jw
  |  C**                 endif                                                  live jw
17068C**                 endif                                                  live jw
      *  Live with E1
     c                   eval      wkv8cd =  'E1LIVE    '                        p16169
     C     Key           chain     pdkmrel0                           95
     C                   if        *in95 = *off                                 If found
     C                   if        kmbxtx = 'Y' or kmbxtx = 'P'                 if live/parallel E1
     C                   call      'MPJDE303E1'
     C                   parm      upacono       xxacono
     C                   parm                    kmbxtx                         Live e1
     C                   endif                                                  live e1
     C                   endif
      *
     C                   endif                                                  if data
     C                   endif                                                  not live in m3
     C                   endif                                                  93 = off
      *
     C                   endif                                                  if company# changes
     C                   endif                                                  If not EOF
     C                   enddo                                                  Main do
      *
     C                   seton                                        LR
      /eject
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
     C     key           klist
     C                   kfld                    upacono
     C                   kfld                    wkv8cd
      *
     C                   endsr
      /eject
