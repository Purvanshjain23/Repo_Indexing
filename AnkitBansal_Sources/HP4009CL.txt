/***************************************************************** */
/*                                                                 */
/*  PROGRAM NUMBER:  HP4009CL                                      */
/*  PROGRAM NAME:    LISTING OF SIRE LINES                         */
/*  PROGRAMMER:      LEANNE FEDOR                                  */
/*  CREATE DATE:     10/25/01                                      */
/*                                                                 */
/*  FUNCTION:        THIS CL IS SUBMITTED WITH QCMDEXC FROM        */
/*                   HP4009-WORK WITH SIRE LINES.                  */
/*                   OPTIONS ARE PASSED THROUGH THE LDA.           */
/*                                                                 */
/*                                                                 */
/*******************************************************************/
/* MODIFICATIONS:                                                  */
/*                                                                 */
/* 10/14/10  LeAnne Ramsey (E1079)                                 */
/*           Expanded 'Sire Line Company' from 1 to 2 characters.  */
/*           So, moved it in the LDA to a spot that had 2 places   */
/*           available.                                            */
/*******************************************************************/

             PGM

/* DEFINE THE VARIABLE REQUIRED IN THE COMMAND TO OVERRIDE THE     */
/* PRINT FILE TO A LASER PRINTER                                   */

             DCL        VAR(&CMD) TYPE(*CHAR) LEN(512)

             DCL        VAR(&QDATA) TYPE(*CHAR) LEN(171)
             DCL        VAR(&LDLISTCD) TYPE(*CHAR) LEN(1)
             DCL        VAR(&LDSICD)   TYPE(*CHAR) LEN(5)
             DCL        VAR(&LDSICOMP) TYPE(*CHAR) LEN(2)
             DCL        VAR(&LDAIST)   TYPE(*CHAR) LEN(1)
             DCL        VAR(&LDPRIME)  TYPE(*DEC) LEN(3)
             DCL        VAR(&LDGG0)    TYPE(*CHAR) LEN(5)
             DCL        VAR(&LDGG1)    TYPE(*CHAR) LEN(5)
             DCL        VAR(&LDGG2)    TYPE(*CHAR) LEN(5)

             DCL        VAR(&HOLD) TYPE(*CHAR) LEN(10)
             DCL        VAR(&COPYS) TYPE(*DEC) LEN(2)
             DCL        VAR(&OUTQ) TYPE(*CHAR) LEN(10)

             CHGVAR     VAR(&LDLISTCD) VALUE(%SST(*LDA 1 1))
             CHGVAR     VAR(&LDSICD)   VALUE(%SST(*LDA 37 5))
             CHGVAR     VAR(&LDPRIME)  VALUE(%SST(*LDA 43 3))
             CHGVAR     VAR(&LDAIST)   VALUE(%SST(*LDA 46 1))
             CHGVAR     VAR(&LDGG0)    VALUE(%SST(*LDA 47 5))
             CHGVAR     VAR(&LDGG1)    VALUE(%SST(*LDA 52 5))
             CHGVAR     VAR(&LDGG2)    VALUE(%SST(*LDA 57 5))
             CHGVAR     VAR(&LDSICOMP) VALUE(%SST(*LDA 62 2))

/*------------------------------------------------------------------*/
/*  SET UP OPEN QUERY                                                */
/*------------------------------------------------------------------*/

/*  SET UP A DEFAULT STARTING POINT FOR THE QUERY                */

             CHGVAR     VAR(%SST(&QDATA 1 14)) VALUE('SISICD *NE +
                          " "')

/*  SIRE LINE CODE                                               */

             IF         COND(&LDSICD *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 16 17)) VALUE('*AND SISICD *EQ "')
             CHGVAR     VAR(%SST(&QDATA 33 5)) VALUE(&LDSICD)
             CHGVAR     VAR(%SST(&QDATA 38 1)) VALUE('"')
             ENDDO

/*  SIRE LINE COMPANY                                            */

             IF         COND(&LDSICOMP *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 40 19)) VALUE('*AND SISICOMP *EQ "')
             CHGVAR     VAR(%SST(&QDATA 59 2)) VALUE(&LDSICOMP)
             CHGVAR     VAR(%SST(&QDATA 61 1)) VALUE('"')
             ENDDO

/* PRIME LINE                                                    */

             IF         COND(&LDPRIME *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 63 16)) VALUE('*AND SIPRIME +
                          *EQ')
             CHGVAR     VAR(%SST(&QDATA 80 3)) VALUE(&LDPRIME)
             ENDDO

/* ACTIVE/INACTIVE STATUS                                        */

             IF         COND(&LDAIST *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 84 17)) VALUE('*AND SIAIST *EQ "')
             CHGVAR     VAR(%SST(&QDATA 101 1)) VALUE(&LDAIST)
             CHGVAR     VAR(%SST(&QDATA 102 1)) VALUE('"')
             ENDDO

/* COGNOS REPORT GROUPING                                        */

             IF         COND(&LDGG0 *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 104 16)) VALUE('*AND SIGG0 *EQ "')
             CHGVAR     VAR(%SST(&QDATA 120 5)) VALUE(&LDGG0)
             CHGVAR     VAR(%SST(&QDATA 125 1)) VALUE('"')
             ENDDO

/* ALTERNATE REPORT GROUP 1                                      */

             IF         COND(&LDGG1 *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 127 16)) VALUE('*AND SIGG1 *EQ "')
             CHGVAR     VAR(%SST(&QDATA 143 5)) VALUE(&LDGG1)
             CHGVAR     VAR(%SST(&QDATA 148 1)) VALUE('"')
             ENDDO

/* ALTERNATE REPORT GROUP 2                                      */

             IF         COND(&LDGG2 *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 150 16)) VALUE('*AND SIGG2 *EQ "')
             CHGVAR     VAR(%SST(&QDATA 166 5)) VALUE(&LDGG2)
             CHGVAR     VAR(%SST(&QDATA 171 1)) VALUE('"')
             ENDDO

/*------------------------------------------------------------------*/
/* OVERRIDE FILE AND SET FILE KEY BASED ON SELECTED SORT SEQUENCE   */
/*------------------------------------------------------------------*/

/* BY SIRE LINE                                                     */

             IF         COND(&LDLISTCD *EQ 'S') THEN(DO)
             OVRDBF     FILE(HSP098) SHARE(*YES)
             OPNQRYF    FILE((HSP098)) QRYSLT(&QDATA) KEYFLD(*FILE)
             ENDDO


/* BY COMPANY/PRIME LINE                                            */

             IF         COND(&LDLISTCD *EQ 'C') THEN(DO)
             OVRDBF     FILE(HSP098) SHARE(*YES)
             OPNQRYF    FILE((HSP098)) QRYSLT(&QDATA) +
                          KEYFLD((SISICOMP) (SIPRIME))
             ENDDO

/* BY PRIME LINE/COMPANY                                  */

             IF         COND(&LDLISTCD *EQ 'P') THEN(DO)
             OVRDBF     FILE(HSP098) SHARE(*YES)
             OPNQRYF    FILE((HSP098)) QRYSLT(&QDATA) +
                          KEYFLD((SIPRIME) (SISICOMP))
             ENDDO

/* BY COGNOS REPORT GROUPING/SIRE LINE                    */

             IF         COND(&LDLISTCD *EQ 'R') THEN(DO)
             OVRDBF     FILE(HSP098) SHARE(*YES)
             OPNQRYF    FILE((HSP098)) QRYSLT(&QDATA) KEYFLD((SIGG0) +
                          (SISICD))
             ENDDO


/*------------------------------------------------------------------*/
/*  SET UP PRINT FILE OVERRIDES                                      */
/*------------------------------------------------------------------*/

/* SET PARAMETERS FROM LDA                                           */

 HOLD:       IF         COND(%SST(*LDA 411 1) = 'Y') THEN(CHGVAR +
                          VAR(&HOLD) VALUE(*YES))
             ELSE       CMD(CHGVAR VAR(&HOLD) VALUE(*NO))

 COPYS:      CHGVAR     VAR(&COPYS) VALUE(%SST(*LDA 412 1))
             IF         COND(&COPYS = 0) THEN(CHGVAR VAR(&COPYS) +
                          VALUE(1))

 OUTQ:       CHGVAR     VAR(&OUTQ) VALUE(%SST(*LDA 413 10))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(%SST(*LDA 401 10)))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(*JOB))


/*------------------------------------------------------------------*/
/* OVERRIDE PRINT FILE                                              */
/*------------------------------------------------------------------*/

/* USE THE CUSTOMIZED SEABOARD COMMAND TO OVERRIDE THE PRINT FILE.  */
/* THIS COMMAND ALLOWS THE REPORT TO BE GENERATED ON A STANDARD LINE*/
/* PRINTER OR A LASER PRINTER. TO USE THIS OVERRIDE YOU MUST DECLARE*/
/* A COMMAND VARIABLE AND FOLLOW THE OVERRIDE WITH A CALL TO        */
/* QCMDEXC.                                                         */

/* THIS REPORT IS STANDARD PRINT AT 10 CPI:  SO, ON A LASER PRINTER */
/* IT CAN PRINT 'PORTRAIT' ON LETTER PAPER                          */

             SBDPRTOVR  CMDVAR(&CMD) FILE(QPRINT) OUTQ(&OUTQ) +
                          WIDTH(132) LANDSCAPE(*NO) DRAWER(*PRTF) +
                          COPIES(&COPYS) HOLD(&HOLD)

             CALL       PGM(QCMDEXC) PARM(&CMD 512)


/*------------------------------------------------------------------*/
/*  CALL LISTING PROGRAM                                             */
/*------------------------------------------------------------------*/

             CALL       PGM(HP6009)

             CLOF       OPNID(HSP098)
             DLTOVR     FILE(*ALL)

             ENDPGM

