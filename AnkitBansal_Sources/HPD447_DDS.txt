     A*%%TS  SD  20010514  131740  ISLRAMS     REL-V4R4M0  5769-PW1
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD447
     A*  TITLE:         SPECIFY CLOSED GROUP ANALYSIS REPORT OPTIONS
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  DATE:          06/23/97
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A N99                                  CF04(04 'PROMPT')
     A N99 44                               CF08(08 'SUBMIT')
     A N99                                  CF17(17 'PRINT OPTIONS')
     A N99                                  CF20(20 'WRKSPLF')
     A*
     A*---------------------------------------------------------------
     A* RECORD FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP447R1
     A*%%TS  SD  20010514  131740  ISLRAMS     REL-V4R4M0  5769-PW1
     A                                      SETOF(30)
     A                                      SETOF(31)
     A                                      SETOF(32)
     A                                      SETOF(33)
     A                                      SETOF(34)
     A                                      SETOF(35)
     A                                      SETOF(36)
     A                                      SETOF(37)
     A                                      SETOF(38)
     A                                      SETOF(41)
     A                                      SETOF(45)
     A                                      SETOF(51)
     A                                      SETOF(54)
     A                                      SETOF(55)
     A                                      SETOF(56)
     A                                      SETOF(57)
     A                                      SETOF(58)
     A                                      SETOF(61)
     A                                      SETOF(65)
     A                                      OVERLAY
     A                                      RTNCSRLOC(&RCD &FLD)
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 29'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  2 17'SPECIFY CLOSED GROUP ANALYSIS REPO-
     A                                      RT OPTIONS'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  4  2'Business office .....'
     A                                  4 24'?'
     A            DFFSBO    R        B  4 26REFFLD(BOBOBO)
     A N31                                  DSPATR(HI)
     A  31                                  DSPATR(RI)
     A  31
     AO 51                                  DSPATR(PC)
     A  99                                  DSPATR(PR)
     A            DFBODS    R        O  4 35REFFLD(BOBODS)
     A                                  4 66'(Blank for all)'
     A                                  5  2'From close date .....'
     A            DFFMDY    R        B  5 26REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A N32                                  DSPATR(HI)
     A  32                                  DSPATR(PC)
     A  32                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A                                  6  2'To close date .......'
     A            DFTMDY    R        B  6 26REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A N33                                  DSPATR(HI)
     A  33                                  DSPATR(PC)
     A  33                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A                                  7  2'Production type .....'
     A                                  7 24'?'
     A            DFPTCD    R        B  7 26REFFLD(PTPTCD)
     A N34                                  DSPATR(HI)
     A  34                                  DSPATR(RI)
     A  34
     AO 54                                  DSPATR(PC)
     A  99                                  DSPATR(PR)
     A            DFPTDS    R        O  7 35REFFLD(PTPTDS)
     A                                  7 66'(Blank for all)'
     A                                  8  2'Production phase ....'
     A                                  8 24'?'
     A            DFPPCD    R        B  8 26REFFLD(PPPPCD)
     A N35                                  DSPATR(HI)
     A  35                                  DSPATR(RI)
     A  35
     AO 55                                  DSPATR(PC)
     A  99                                  DSPATR(PR)
     A            DFPPDS    R        O  8 35REFFLD(PPPPDS)
     A                                  8 66'(Blank for all)'
     A                                  9  2'Group type ..........'
     A                                  9 24'?'
     A            DFGTCD    R        B  9 26REFFLD(HSRREF/@@GTCD *LIBL/HSPREF)
     A N37                                  DSPATR(HI)
     A  37
     AO 57                                  DSPATR(PC)
     A  37                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A            DFGTDS    R        O  9 35REFFLD(HSRREF/GTGTDS *LIBL/HSPREF)
     A                                  9 66'(Blank for all)'
     A                                 10  2'Cell ................'
     A            DFCELL    R        B 10 26REFFLD(FSCELL)
     A                                      EDTCDE(M)
     A N30                                  DSPATR(HI)
     A  30                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A                                 10 66'(Blank for all)'
     A                                 11  2'Farm site ...........'
     A                                 11 24'?'
     A            DFFSCD    R        B 11 26REFFLD(FSFSCD)
     A                                      EDTCDE(M)
     A N36                                  DSPATR(HI)
     A  36                                  DSPATR(RI)
     A  36
     AO 56                                  DSPATR(PC)
     A  99                                  DSPATR(PR)
     A            DFFSNM    R        O 11 35REFFLD(FSFSNM)
     A                                 11 66'(Blank for all)'
     A                                 13  2'Include on the report:'
     A                                 14  4'Movements/performance ...'
     A                                 14 30'?'
     A            DFMVPF    R        B 14 32REFFLD(@@FLAG)
     A N41                                  DSPATR(HI)
     A  41                                  DSPATR(RI)
     A  41
     AO 61                                  DSPATR(PC)
     A  99                                  DSPATR(PR)
     A                                 15  4'Sales/costs .............'
     A                                 15 30'?'
     A            DFSLCS    R        B 15 32REFFLD(@@FLAG)
     A N45                                  DSPATR(HI)
     A  45                                  DSPATR(RI)
     A  45
     AO 65                                  DSPATR(PC)
     A  99                                  DSPATR(PR)
     A                                 16  6'Dollar amount format ..'
     A                                 16 30'?'
     A            DFAFCD    R        B 16 32REFFLD(AFAFCD)
     A N38                                  DSPATR(HI)
     A  38                                  DSPATR(RI)
     A  38
     AO 58                                  DSPATR(PC)
     A  99                                  DSPATR(PR)
     A            DFAFDS    R        O 16 35REFFLD(AFREC/AFAFDS *LIBL/HSP043)
     A*
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R HP447K1
     A*%%TS  SD  19950208  104913  ISLRAMS     REL-V3R0M5  5763-PW1
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A N99                             23 11'F4=Prompt'
     A  44N99                          23 22'F8=Submit'
     A N99                             23 33'F17=Print Options'
     A N99                             23 52'F20=Spool Files'
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *---------------------------------------------------------------
     A          R HP447ES                   SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
     A*
     A*---------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*---------------------------------------------------------------
     A*
     A          R HP447EC                   SFLCTL(HP447ES)
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
