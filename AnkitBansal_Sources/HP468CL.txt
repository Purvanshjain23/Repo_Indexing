/***************************************************************** */
/*                                                                 */
/*  PROGRAM NUMBER:  HP468CL                                       */
/*  PROGRAM NAME:    FEED TICKET ALLOCATION                        */
/*  PROGRAMMER:      LEANNE FEDOR                                  */
/*  CREATE DATE:     06/10/94                                      */
/*                                                                 */
/*  FUNCTION:        THIS CL IS SUBMITTED WITH QCMDEXC FROM        */
/*                   HP468-SPECIFY FEED TICKET ALLOCATION.         */
/*                   USER OPTIONS ARE PASSED THROUGH THE LDA.      */
/*                   AN OPNQRY STATEMENT IS USED TO SELECT AND     */
/*                   SEQUENCE RECORDS.                             */
/*                                                                 */
/*******************************************************************/
/*  MODIFIED:                                                      */
/*  08/09/94  R. MARTIN                                            */
/*            ADDED PRINT PARMS FOR HOLD, OUTQ, COPYS, ETC.        */
/*                                                                 */
/*  09/01/94  LEANNE FEDOR                                         */
/*            ADDED MONMSG ON CLRPFM COMMANDS.                     */
/*                                                                 */
/* 09/12/94  LEANNE FEDOR                                          */
/*           RECOMPILE ONLY. FIELDS WERE REMOVED FROM THE FEED     */
/*           TICKET DETAIL FILE.                                   */
/*                                                                 */
/*  12/05/94  LEANNE FEDOR                                         */
/*            CHANGED WORKFILE LOGIC. WE WILL NOW CREATE WORKFILES */
/*            IN QTEMP INSTEAD OF CLEARING THEM IN PRODUCTION.     */
/*                                                                 */
/*  12/13/94  LEANNE FEDOR                                         */
/*            RECOMPILE ONLY DUE TO CHANGES TO FEED DETAIL FILE.   */
/*                                                                 */
/*  04/26/95  LEANNE FEDOR                                         */
/*            CHANGED FARM SITE FROM ALPHA TO NUMERIC.             */
/*                                                                 */
/* 04/23/96  LEANNE FEDOR                                          */
/*           ADDED CUSTOMIZED PRINTER OVERRIDE COMMAND TO FORCE    */
/*           REPORT TO PRINT ON LASER PRINTER.                     */
/*                                                                 */
/* 05/21/96  LEANNE FEDOR                                          */
/*           IN PREPARATION FOR HAVING TWO BOXES (CISC AND RISC),  */
/*           REPLACED STANDARD CREATE DUPLICATE OBJECT COMMAND     */
/*           WITH A COMMAND CUSTOMIZED BY JOHN ERICSON. THIS       */
/*           COMMAND WILL RETRIEVE/USE THE FILE FROM THE APPRO-    */
/*           PRIATE LIBRARY INSTEAD OF USING HARDCODED 'PRKFLIB'.  */
/*           THE CUSTOMIZED COMMAND IS CRTDUPOBJL FOR 'CREATE      */
/*           DUPLICATE OBJECT FROM LIBRARY'.                       */
/*                                                                 */
/* 06/19/97  LEANNE FEDOR                                          */
/*           CHANGED OUT QUEUE LOGIC.                              */
/*                                                                 */
/* 03/10/98  LEANNE FEDOR                                          */
/*           RECOMPILE ONLY. FEED POUNDS/DOLLAR FIELDS WERE        */
/*           INCREASED IN SIZE PER NATHAN MALONE.                  */
/*                                                                 */
/* 02/22/99 LEANNE FEDOR                                           */
/*          ADDED LOGIC TO SET NEW DATA AREA TO BLANK AT           */
/*          COMPLETION OF JOB.                                     */
/*                                                                 */
/* 04/22/08 LEANNE RAMSEY                                          */
/*          REPLACED JOHN ERIKSON'S PRINT FILE OVERRIDE COMMAND    */
/*          WITH THE STANDARD IBM PRINT OVERRIDE COMMAND.          */
/*******************************************************************/

             PGM

             DCL        VAR(&QDATA) TYPE(*CHAR) LEN(75)

             DCL        VAR(&LDCYMD) TYPE(*DEC) LEN(8)
             DCL        VAR(&LDEFFL) TYPE(*CHAR) LEN(1)
             DCL        VAR(&LDFMBO) TYPE(*CHAR) LEN(5)

             DCL        VAR(&HOLD) TYPE(*CHAR) LEN(10)
             DCL        VAR(&COPYS) TYPE(*DEC) LEN(2)
             DCL        VAR(&OUTQ) TYPE(*CHAR) LEN(10)

             CHGVAR     VAR(&LDCYMD) VALUE(%SST(*LDA 1 8))
             CHGVAR     VAR(&LDEFFL) VALUE(%SST(*LDA  9 1))
             CHGVAR     VAR(&LDFMBO) VALUE(%SST(*LDA 10 5))


/*  CREATE WORKFILES                                              */

             CRTDUPOBJL OBJ(HSP301) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)

             CRTDUPOBJL OBJ(HSL301A) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)

             CRTDUPOBJL OBJ(HSP302) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)


/*  SELECT UNALLOCATED FEED TICKET RECORDS FOR CUTOFF DATE    */
/*        AND BUSINESS OFFICE                                 */

             CHGVAR     VAR(%SST(&QDATA 1 10)) VALUE('FDRTDT *LE')
             CHGVAR     VAR(%SST(&QDATA 12 8)) VALUE(&LDCYMD)
             CHGVAR     VAR(%SST(&QDATA 21 20)) VALUE('*AND FDALFL +
                          *EQ "T"')
             CHGVAR     VAR(%SST(&QDATA 42 17)) VALUE('*AND FDFMBO +
                          *EQ "')
             CHGVAR     VAR(%SST(&QDATA 59 5)) VALUE(&LDFMBO)
             CHGVAR     VAR(%SST(&QDATA 64 1)) VALUE('"')


/*  PERFORM OPEN QUERY AND BUILD WORKFILES      */

             OVRDBF     FILE(HSP038) SHARE(*YES)
             OPNQRYF    FILE((HSP038)) QRYSLT(&QDATA) KEYFLD(*FILE) +
                          SEQONLY(*YES)

             CALL       PGM(HP301)

             CLOF       OPNID(HSP038)
             DLTOVR     FILE(HSP038)


/* SET PARAMETERS FROM LDA                         */

 HOLD:       IF         COND(%SST(*LDA 411 1) = 'Y') THEN(CHGVAR +
                          VAR(&HOLD) VALUE(*YES))
             ELSE       CMD(CHGVAR VAR(&HOLD) VALUE(*NO))

 COPYS:      CHGVAR     VAR(&COPYS) VALUE(%SST(*LDA 412 1))
             IF         COND(&COPYS = 0) THEN(CHGVAR VAR(&COPYS) +
                          VALUE(1))

 OUTQ:       CHGVAR     VAR(&OUTQ) VALUE(%SST(*LDA 413 10))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(%SST(*LDA 401 10)))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(*JOB))


/*------------------------------------------------------------------*/
/*  PRINT ALLOCATION REPORT                                         */
/*------------------------------------------------------------------*/

             OVRPRTF    FILE(PRINT132) OUTQ(&OUTQ) COPIES(&COPYS) +
                          HOLD(&HOLD)

             CALL       PGM(HP668)


/*------------------------------------------------------------------*/
/*  IF 'FINAL' RUN, CALL PROGRAM TO UPDATE HSP038 FROM WORKFILES */
/*------------------------------------------------------------------*/

             IF         COND(&LDEFFL *EQ 'F') THEN(DO)
             CALL       PGM(HP302)
             ENDDO

             DLTOVR     FILE(*ALL)

/*----------------------------------------------------------------------*/
/*  SET THE DATA AREA THAT KEEPS THE ALLOCATION FUNCTION FROM BEING     */
/*  SUBMITTED TWICE SIMULTANEOUSLY BACK TO BLANK                        */
/*----------------------------------------------------------------------*/

             CHGDTAARA  DTAARA(DAALFD (1 1)) VALUE(' ')

             ENDPGM

