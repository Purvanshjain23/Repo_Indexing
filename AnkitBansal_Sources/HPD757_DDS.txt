     A*%%TS  SD  19991028  161052  ISLRAMS     REL-V4R2M0  5769-PW1
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD757
     A*  TITLE:         VIEW DEATHS
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  CREATED:       05/30/95
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A                                      CF04(04 'PROMPT')
     A                                      CF05(05 'REFRESH')
     A                                      CA12(12 'PREVIOUS')
     A*
     A*--------------------------------------------------------------*
     A* SUBFILE FORMAT ONE
     A*--------------------------------------------------------------*
     A          R HP757S1                   SFL
     A*%%TS  SD  19991028  161052  ISLRAMS     REL-V4R2M0  5769-PW1
     A  85                                  SFLNXTCHG
     A            HDRRN1         4Y 0H
     A            DFMDY     R        O 11  2REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A            DFBLCD    R        O 11 14REFFLD(HSRREF/@@BLCD *LIBL/HSPREF)
     A            DFRMCD    R        O 11 22REFFLD(HSRREF/@@RMCD *LIBL/HSPREF)
     A            DFHGCD    R        O 11 29REFFLD(HGHGCD)
     A            DFKDHD    R        O 11 38REFFLD(KDKDHD)
     A                                      EDTCDE(M)
     A            DFKDLB    R        O 11 46REFFLD(KDKDLB)
     A                                      EDTCDE(M)
     A            DFDSFL    R        O 11 55REFFLD(KDDSFL)
     A            DFDRCD    R        O 11 61REFFLD(KDDRCD)
     A            DFMSCD    R        O 11 69REFFLD(KDMSCD)
     A            DFMVSN    R     Y  O 11 73REFFLD(KDMVSN)
     A                                      EDTCDE(M)
     A*
     A*--------------------------------------------------------------*
     A* SUBFILE CONTROL FORMAT ONE
     A*--------------------------------------------------------------*
     A          R HP757C1                   SFLCTL(HP757S1)
     A*%%TS  SD  19991028  161052  ISLRAMS     REL-V4R2M0  5769-PW1
     A                                      SFLSIZ(0100)
     A                                      SFLPAG(0010)
     A                                      SETOF(50)
     A                                      SETOF(51)
     A                                      SETOF(52)
     A                                      SETOF(54)
     A                                      SETOF(55)
     A                                      SETOF(56)
     A N82                                  ROLLUP(25 'PAGE FORWARD')
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND(*MORE)
     A                                      OVERLAY
     A                                      RTNCSRLOC(&RCD &FLD)
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 25'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  2 30'VIEW DEATHS'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  4  2'Farm site ..:'
     A            C1FSCD    R        O  4 17REFFLD(FSFSCD)
     A                                      EDTCDE(M)
     A            C1FSNM    R        O  4 24REFFLD(FSFSNM)
     A                                  6 16'?'
     A                                      DSPATR(HI)
     A                                  6 24'?'
     A                                      DSPATR(HI)
     A                                  6 32'?'
     A                                      DSPATR(HI)
     A                                  6 55'?'
     A                                      DSPATR(HI)
     A                                  6 63'?'
     A                                      DSPATR(HI)
     A                                  6 69'?'
     A                                      DSPATR(HI)
     A                                  7  3'Date of'
     A                                      DSPATR(HI)
     A                                  7 52'Destroy'
     A                                      DSPATR(HI)
     A                                  8  4'Death'
     A                                      DSPATR(HI)
     A                                  8 12'Building'
     A                                      DSPATR(HI)
     A                                  8 22'Room'
     A                                      DSPATR(HI)
     A                                  8 29'Group'
     A                                      DSPATR(HI)
     A                                  8 39'Head'
     A                                      DSPATR(HI)
     A                                  8 45'Pounds'
     A                                      DSPATR(HI)
     A                                  8 54'Y/N'
     A                                      DSPATR(HI)
     A                                  8 61'Reason'
     A                                      DSPATR(HI)
     A                                  8 69'St'
     A                                      DSPATR(HI)
     A                                  8 73'Movement'
     A                                      DSPATR(HI)
     A            C1MDY     R        B  9  2REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A            C1BLCD    R        B  9 14REFFLD(HSRREF/@@BLCD *LIBL/HSPREF)
     A  55                                  DSPATR(PC)
     A                                      DSPATR(HI)
     A            C1RMCD    R        B  9 22REFFLD(HSRREF/@@RMCD *LIBL/HSPREF)
     A  56                                  DSPATR(PC)
     A                                      DSPATR(HI)
     A            C1HGCD    R        B  9 29REFFLD(HGHGCD)
     A  50                                  DSPATR(PC)
     A                                      DSPATR(HI)
     A            C1DSFL    R        B  9 55REFFLD(KDDSFL)
     A  51                                  DSPATR(PC)
     A                                      DSPATR(HI)
     A            C1DRCD    R        B  9 61REFFLD(DRDRCD)
     A  52                                  DSPATR(PC)
     A                                      DSPATR(HI)
     A            C1MSCD    R        B  9 69REFFLD(KDMSCD)
     A  54                                  DSPATR(PC)
     A                                      DSPATR(HI)
     A            C1MVSN    R        B  9 73REFFLD(KDMVSN)
     A                                      DSPATR(HI)
     A*
     A*
     A*--------------------------------------------------------------*
     A* COMMAND KEY RECORD 1
     A*--------------------------------------------------------------*
     A          R HP757K1
     A*%%TS  SD  19940407  133822  ISLRAMS     REL-V2R2M0  5738-PW1
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A                                      DSPATR(HI)
     A                                 23  2'F3=Exit'
     A                                 23 11'F4=Prompt'
     A                                 23 22'F5=Refresh'
     A                                 23 34'F12=Previous'
     A*
     A*--------------------------------------------------------------*
     A* ERROR MESSAGE SUBFILE FORMAT
     A*--------------------------------------------------------------*
     A          R HP757ES                   SFL SFLMSGRCD(24)
     A*
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
     A*                                                                *********
     A*--------------------------------------------------------------*
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*--------------------------------------------------------------*
     A          R HP757EC                   SFLCTL(HP757ES)
     A*
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
