     A*%%TS  SD  20090429  155234  ISLRAMS     REL-V5R4M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD7033
     A*  TITLE:         VIEW HPS-TO-JDE UPLOAD STATUS CHANGES
     A*  PROGRAMMER:    LEANNE RAMSEY
     A*  DATE:          11/11/08
     A*****************************************************************
     A* MODIFICATIONS:
     A*----------------------------------------------------------------
     A* 10/05/18 DN  S13682 - Added Company Code to subfile control.
     A*    Shifted existing fields down 1 row starting with 'Year'.
     A*    Decrease subfile page from 9 to 8.
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A                                      CF05(05 'REFRESH')
     A*
     A*---------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP7033S1                  SFL
     A*%%TS  SD  20090429  125719  ISLRAMS     REL-V5R4M0  5722-WDS
     A  85                                  SFLNXTCHG
     A            HDRRN1         4Y 0H
     A            D1UPLSCD  R        O 14  6REFFLD(HSRREF/@@UPLSCD *LIBL/HSPREF)
     A            D1CRDTMDY R        O 14 12REFFLD(HSRREF/@@DT06 *LIBL/HSPREF)
     A                                      EDTWRD('  /  /  ')
     A            D1CRTM    R        O 14 22REFFLD(HSRREF/@@CRTM *LIBL/HSPREF)
     A                                      EDTWRD('  :  :  ')
     A            D1CRUSR   R        O 14 33REFFLD(HSRREF/@@CRUSR *LIBL/HSPREF)
     A            D1PGM     R        O 14 45REFFLD(HSRREF/@@PGM *LIBL/HSPREF)
     A*
     A*---------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP7033C1                  SFLCTL(HP7033S1)
     A*%%TS  SD  20090429  155234  ISLRAMS     REL-V5R4M0  5722-WDS
     A                                      SFLSIZ(0050)
13682A**                                    SFLPAG(0009)
13682A                                      SFLPAG(0008)
     A N82                                  ROLLUP(25 'PAGE FORWARD')
     A                                      OVERLAY
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 26'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  2 18'VIEW HPS-TO-JDE UPLOAD STATUS CHAN-
     A                                      GES'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A  88
     AO 89                              3  2'Alert! Important Message(s):'
     A                                      COLOR(RED)
     A                                      DSPATR(UL)
     A  88        C1MSG1        60A  O  4  2COLOR(RED)
     A  89        C1MSG2        60A  O  5  2COLOR(RED)
     A*
     A  89                              5 64'    Costed      '
     A                                      DSPATR(UL)
     A                                      COLOR(RED)
     A  89                              6 64'Date:'
     A  89        C1COMDY   R        O  6 70REFFLD(HSRREF/@@DT06 *LIBL/HSPREF)
     A                                      EDTWRD('  /  /  ')
     A                                  7  2'Upload code:'
     A            C1HUCD    R        O  7 16REFFLD(HSRREF/@@HUCD *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A            C1HUDS    R        O  7 22REFFLD(HSRREF/@@HUDS *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A  89                              7 64'Time:'
     A  89        C1COTM    R        O  7 70REFFLD(HSRREF/@@COTM *LIBL/HSPREF)
     A                                      EDTWRD('  :  :  ')
13682A                                  8 11'Co:'
  |  A            C1COCD    R        O  8 16REFFLD(HSRREF/@@COCD *LIBL/HSPREF)
13682A                                      DSPATR(HI)
     A                                  9  9'Year:'
     A            C1ACYR    R        O  9 16REFFLD(HSRREF/@@ACYR *LIBL/HSPREF)
     A                                      EDTCDE(M)
     A                                      DSPATR(HI)
     A  89                              9 64'User:'
     A  89        C1COUSR   R        O  9 70REFFLD(HSRREF/@@COUSR *LIBL/HSPREF)
     A                                 10  7'Period:'
     A            C1ACPE    R        O 10 18REFFLD(HSRREF/@@ACPE *LIBL/HSPREF)
     A                                      EDTCDE(M)
     A                                      DSPATR(HI)
     A            C1BPMDY   R     Y  O 10 22REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A                                      DSPATR(HI)
     A  87                             10 32'TO'
     A            C1EPMDY   R     Y  O 10 35REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A                                      DSPATR(HI)
     A                                 12  4'Upload'
     A                                      DSPATR(HI)
     A                                 12 12'------------- Status Changes ------
     A                                      ---------'
     A                                      DSPATR(HI)
     A                                 13  4'Status'
     A                                      DSPATR(HI)
     A                                 13 14'Date'
     A                                      DSPATR(HI)
     A                                 13 24'Time'
     A                                      DSPATR(HI)
     A                                 13 33'User'
     A                                      DSPATR(HI)
     A                                 13 45'Program'
     A                                      DSPATR(HI)
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R HP7033K1
     A*%%TS  SD  20081112  091738  ISLRAMS     REL-V5R4M0  5722-WDS
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A                                 23 11'F5=Refresh'
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *---------------------------------------------------------------
     A          R HP7033ES                  SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE CONTROL FORMAT
      *---------------------------------------------------------------
     A          R HP7033EC                  SFLCTL(HP7033ES)
      *
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
