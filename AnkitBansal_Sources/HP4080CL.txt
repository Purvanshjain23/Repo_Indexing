/***************************************************************** */
/*                                                                 */
/*  PROGRAM NUMBER:  HP4080CL                                      */
/*  PROGRAM NAME:    ACTIVITY VARIANCE REPORT                      */
/*  PROGRAMMER:      LEANNE FEDOR                                  */
/*  CREATE DATE:     10/02/02                                      */
/*                                                                 */
/*  FUNCTION:        THIS CL IS SUBMITTED WITH QCMDEXC FROM        */
/*                   HP4080-SPECIFY OPTIONS FOR WEEKLY REPORTED    */
/*                   ACTIVITY VARIANCE REPORT.                     */
/*                                                                 */
/*                   OPTIONS ARE PASSED THROUGH THE LDA.           */
/*                                                                 */
/*******************************************************************/
/*  MODIFIED:                                                      */
/* 05/17/10  LeAnne Ramsey                                         */
/*           Added logic to handle the new XLS download.           */
/*******************************************************************/

             PGM

/* DEFINE THE VARIABLE REQUIRED IN THE COMMAND TO OVERRIDE THE     */
/* PRINT FILE TO A LASER PRINTER                                   */

             DCL        VAR(&CMD)      TYPE(*CHAR) LEN(512)
             DCL        VAR(&QDATA)    TYPE(*CHAR) LEN(274)
             DCL        VAR(&RCDCNT)   TYPE(*DEC)  LEN(10 0)
             DCL        VAR(&LDEMAIL)  TYPE(*CHAR) LEN(50)

             DCL        VAR(&LDCDYR) TYPE(*DEC) LEN(4)
             DCL        VAR(&LDCDWK) TYPE(*DEC) LEN(2)
             DCL        VAR(&LDFYMD) TYPE(*DEC) LEN(8)
             DCL        VAR(&LDTYMD) TYPE(*DEC) LEN(8)
             DCL        VAR(&LDRPFL) TYPE(*CHAR) LEN(1)
             DCL        VAR(&LDSRFL) TYPE(*CHAR) LEN(1)

             DCL        VAR(&LDFSBO) TYPE(*CHAR) LEN(5)
             DCL        VAR(&LDPTCD) TYPE(*CHAR) LEN(5)
             DCL        VAR(&LDPPCD) TYPE(*CHAR) LEN(5)
             DCL        VAR(&LDGTCD) TYPE(*CHAR) LEN(1)
             DCL        VAR(&LDFSCD) TYPE(*DEC) LEN(5)
             DCL        VAR(&LDHGSN) TYPE(*DEC) LEN(7)

             DCL        VAR(&HOLD) TYPE(*CHAR) LEN(10)
             DCL        VAR(&COPYS) TYPE(*DEC) LEN(2)
             DCL        VAR(&OUTQ) TYPE(*CHAR) LEN(10)

             CHGVAR     VAR(&LDEMAIL)   VALUE(%SST(*LDA 350 50))

             CHGVAR     VAR(&LDCDYR) VALUE(%SST(*LDA 1 4))
             CHGVAR     VAR(&LDCDWK) VALUE(%SST(*LDA 5 2))

             CHGVAR     VAR(&LDFYMD) VALUE(%SST(*LDA 7 8))
             CHGVAR     VAR(&LDTYMD) VALUE(%SST(*LDA 15 8))

             CHGVAR     VAR(&LDRPFL) VALUE(%SST(*LDA 23 1))
             CHGVAR     VAR(&LDSRFL) VALUE(%SST(*LDA 300 1))

             CHGVAR     VAR(&LDFSBO) VALUE(%SST(*LDA 31 5))
             CHGVAR     VAR(&LDPTCD) VALUE(%SST(*LDA 36 5))
             CHGVAR     VAR(&LDPPCD) VALUE(%SST(*LDA 41 5))
             CHGVAR     VAR(&LDFSCD) VALUE(%SST(*LDA 48 5))
             CHGVAR     VAR(&LDHGSN) VALUE(%SST(*LDA 60 7))
             CHGVAR     VAR(&LDGTCD) VALUE(%SST(*LDA 67 1))

/*-------------------------------------------------------------------*/
/* CREATE WORKFILE                                                   */
/*-------------------------------------------------------------------*/

             CRTDUPOBJL OBJ(HSP3080) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)

/*-------------------------------------------------------------------*/
/*  SET UP AND RUN OPEN QUERY OVER HOG GROUP                         */
/*-------------------------------------------------------------------*/

             CHGVAR     VAR(&QDATA) VALUE(' ')

/*  ALWAYS OMIT HOG GROUPS WITH A STATUS OF VOIDED               */

             CHGVAR     VAR(%SST(&QDATA 1 15)) VALUE('HGGSCD +
                          *NE "VD"')

/*  ALWAYS OMIT BGF HOG GROUPS                                   */

             CHGVAR     VAR(%SST(&QDATA 38 23)) VALUE('*AND HGPPCD +
                          *NE "BGF  "')

/*  GROUP HAD TO HAVE 'OPENED' ON OR BEFORE THE WEEK-ENDING DATE */

             CHGVAR     VAR(%SST(&QDATA 62 15)) VALUE('*AND HGOPDT +
                          *LE')
             CHGVAR     VAR(%SST(&QDATA 78 8)) VALUE(&LDTYMD)


/*  ALWAYS OMIT HOG GROUPS WITH A DISPOSED DATE PRIOR TO BEGINNING DATE */

             CHGVAR     VAR(%SST(&QDATA 87 16)) VALUE('*AND (HGDSDT +
                          *EQ')
             CHGVAR     VAR(%SST(&QDATA 104 8)) VALUE(00000000)
             CHGVAR     VAR(%SST(&QDATA 113 14)) VALUE('*OR HGDSDT +
                          *GE')
             CHGVAR     VAR(%SST(&QDATA 128 8)) VALUE(&LDFYMD)
             CHGVAR     VAR(%SST(&QDATA 136 1)) VALUE(')')


/*  IF BUSINESS OFFICE                                           */

             IF         COND(&LDFSBO *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 138 17)) VALUE('*AND HGFSBO *EQ "')
             CHGVAR     VAR(%SST(&QDATA 155 5)) VALUE(&LDFSBO)
             CHGVAR     VAR(%SST(&QDATA 160 1)) VALUE('"')
             ENDDO

/*  IF PRODUCTION TYPE                                            */

             IF         COND(&LDPTCD *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 162 17)) VALUE('*AND HGPTCD +
                          *EQ "')
             CHGVAR     VAR(%SST(&QDATA 179 5)) VALUE(&LDPTCD)
             CHGVAR     VAR(%SST(&QDATA 184 1)) VALUE('"')
             ENDDO

/*  IF PRODUCTION PHASE                                            */

             IF         COND(&LDPPCD *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 186 17)) VALUE('*AND HGPPCD +
                          *EQ "')
             CHGVAR     VAR(%SST(&QDATA 203 5)) VALUE(&LDPPCD)
             CHGVAR     VAR(%SST(&QDATA 208 1)) VALUE('"')
             ENDDO

/*  IF GROUP TYPE                                                  */

             IF         COND(&LDGTCD *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 210 17)) VALUE('*AND HGGTCD +
                          *EQ "')
             CHGVAR     VAR(%SST(&QDATA 227 1)) VALUE(&LDGTCD)
             CHGVAR     VAR(%SST(&QDATA 228 1)) VALUE('"')
             ENDDO

/*  IF FARM SITE                                                 */

             IF         COND(&LDFSCD *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 230 15)) VALUE('*AND HGFSCD +
                          *EQ')
             CHGVAR     VAR(%SST(&QDATA 246 5)) VALUE(&LDFSCD)
             ENDDO

/*  IF HOG GROUP                                                    */

             IF         COND(&LDHGSN *NE 0000000) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 252 15)) VALUE('*AND HGHGSN +
                          *EQ')
             CHGVAR     VAR(%SST(&QDATA 268 7)) VALUE(&LDHGSN)
             ENDDO


             OVRDBF     FILE(HSP034) SHARE(*YES)
             OPNQRYF    FILE((HSP034)) QRYSLT(&QDATA) KEYFLD(*FILE) +
                          SEQONLY(*YES)


/*-------------------------------------------------------------------*/
/*  SET UP AND RUN OPEN QUERY OVER KILLED/DEAD FILE FOR DATE RANGE   */
/*-------------------------------------------------------------------*/

             CHGVAR     VAR(&QDATA) VALUE(' ')

             CHGVAR     VAR(%SST(&QDATA 1 10)) VALUE('KDKDDT *GE')
             CHGVAR     VAR(%SST(&QDATA 12 8)) VALUE(&LDFYMD)
             CHGVAR     VAR(%SST(&QDATA 21 15)) VALUE('*AND KDKDDT +
                          *LE')
             CHGVAR     VAR(%SST(&QDATA 37 8)) VALUE(&LDTYMD)


             OVRDBF     FILE(HSL068D) SHARE(*YES)
             OPNQRYF    FILE((HSL068D)) QRYSLT(&QDATA) KEYFLD(*FILE) +
                          SEQONLY(*YES)


/*-------------------------------------------------------------------*/
/*  SET UP AND RUN OPEN QUERY OVER INVENTORY ADJUSTMENT FILE         */
/*-------------------------------------------------------------------*/

             CHGVAR     VAR(&QDATA) VALUE(' ')

             CHGVAR     VAR(%SST(&QDATA 1 10)) VALUE('IAIADT *GE')
             CHGVAR     VAR(%SST(&QDATA 12 8)) VALUE(&LDFYMD)
             CHGVAR     VAR(%SST(&QDATA 21 15)) VALUE('*AND IAIADT +
                          *LE')
             CHGVAR     VAR(%SST(&QDATA 37 8)) VALUE(&LDTYMD)


             OVRDBF     FILE(HSL069A) SHARE(*YES)
             OPNQRYF    FILE((HSL069A)) QRYSLT(&QDATA) KEYFLD(*FILE) +
                          SEQONLY(*YES)

/*-------------------------------------------------------------------*/
/*  SET UP AND RUN OPEN QUERY OVER REPORTED WEEKLY ACTIVITY          */
/*-------------------------------------------------------------------*/

             CHGVAR     VAR(&QDATA) VALUE(' ')

             CHGVAR     VAR(%SST(&QDATA 1 10)) VALUE('RWCDYR *EQ')
             CHGVAR     VAR(%SST(&QDATA 12 4)) VALUE(&LDCDYR)
             CHGVAR     VAR(%SST(&QDATA 17 15)) VALUE('*AND RWCDWK +
                          *EQ')
             CHGVAR     VAR(%SST(&QDATA 33 2)) VALUE(&LDCDWK)

             OVRDBF     FILE(HSP102) SHARE(*YES)
             OPNQRYF    FILE((HSP102)) QRYSLT(&QDATA) KEYFLD(*FILE) +
                          SEQONLY(*YES)

/*-------------------------------------------------------------------*/
/*  BUILD THE ACTUAL/REPORTED WORKFILE                               */
/*-------------------------------------------------------------------*/

             CALL PGM(HP3080)

             CLOF       OPNID(HSP034)
             CLOF       OPNID(HSL068D)
             CLOF       OPNID(HSL069A)
             CLOF       OPNID(HSP102)

             DLTOVR     FILE(*ALL)


/*-------------------------------------------------------------------*/
/* SET UP PRINT OVERRIDES                                            */
/*-------------------------------------------------------------------*/

 HOLD:       IF         COND(%SST(*LDA 411 1) = 'Y') THEN(CHGVAR +
                          VAR(&HOLD) VALUE(*YES))
             ELSE       CMD(CHGVAR VAR(&HOLD) VALUE(*NO))

 COPYS:      CHGVAR     VAR(&COPYS) VALUE(%SST(*LDA 412 1))
             IF         COND(&COPYS = 0) THEN(CHGVAR VAR(&COPYS) +
                          VALUE(1))

OUTQ:        CHGVAR     VAR(&OUTQ) VALUE(%SST(*LDA 413 10))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(%SST(*LDA 401 10)))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(*JOB))

/*------------------------------------------------------------------*/
/* OVERRIDE PRINT FILE                                              */
/*------------------------------------------------------------------*/

/* USE THE CUSTOMIZED SEABOARD COMMAND TO OVERRIDE THE PRINT FILE.  */
/* THIS COMMAND ALLOWS THE REPORT TO BE GENERATED ON A STANDARD LINE*/
/* PRINTER OR A LASER PRINTER. TO USE THIS OVERRIDE YOU MUST DECLARE*/
/* A COMMAND VARIABLE AND FOLLOW THE OVERRIDE WITH A CALL TO        */
/* QCMDEXC.                                                         */

/* THIS REPORT IS STANDARD PRINT AT 10 CPI:  SO, ON A LASER PRINTER */
/* IT CAN PRINT 'PORTRAIT' ON LETTER PAPER                          */

             SBDPRTOVR  CMDVAR(&CMD) FILE(QPRINT) OUTQ(&OUTQ) +
                          WIDTH(198) LANDSCAPE(*PRTF) DRAWER(*PRTF) +
                          COPIES(&COPYS) HOLD(&HOLD)

             CALL       PGM(QCMDEXC) PARM(&CMD 512)

/*-------------------------------------------------------------------*/
/* PRINT THE REPORT                                                  */
/*-------------------------------------------------------------------*/

/* REPORT BY PHASE/REPORTED ENDING INVENTORY VARIANCE                */

             IF         COND(&LDRPFL *EQ 'V' *AND &LDSRFL *EQ 'R') +
                          THEN(DO)
             OVRDBF     FILE(HSP3080) SHARE(*YES)
             OPNQRYF    FILE((HSP3080)) KEYFLD((W1PPCD) (W1RVENINHD +
                          *DESCEND) (W1WEDT)) SEQONLY(*YES)
             ENDDO

/* REPORT BY PHASE/SHIPPED ENDING INVENTORY VARIANCE                */

             IF         COND(&LDRPFL *EQ 'V' *AND &LDSRFL *EQ 'S') +
                          THEN(DO)
             OVRDBF     FILE(HSP3080) SHARE(*YES)
             OPNQRYF    FILE((HSP3080)) KEYFLD((W1PPCD) (W1SVENINHD +
                          *DESCEND) (W1WEDT)) SEQONLY(*YES)
             ENDDO

/* REPORT BY PHASE/CELL/FARM                                         */

             IF         COND(&LDRPFL *EQ 'C') +
                          THEN(DO)
             OVRDBF     FILE(HSP3080) SHARE(*YES)
             OPNQRYF    FILE((HSP3080)) KEYFLD((W1PPCD) (W1CELL) +
                          (W1FSCD) (W1BLCD) (W1RMCD) (W1CDYR) +
                          (W1CDWK)) SEQONLY(*YES)
             ENDDO


/* XLS DOWNLOAD BY PHASE/CELL/FARM                                         */

             IF         COND(&LDRPFL = 'D') THEN(DO)
             OVRDBF     FILE(HSP3080) SHARE(*YES)
             OPNQRYF    FILE((HSP3080)) KEYFLD((W1PPCD) (W1CELL) +
                          (W1FSCD) (W1BLCD) (W1RMCD) (W1CDYR) +
                          (W1CDWK)) SEQONLY(*YES)

             CRTDUPOBJ  OBJ(HSP3080) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP) NEWOBJ(HSP3080DWL) DATA(*NO)
             CPYFRMQRYF FROMOPNID(HSP3080) TOFILE(QTEMP/HSP3080DWL) +
                          MBROPT(*REPLACE)
             ENDDO


/*-------------------------------------------------------------------*/
/* EMAIL XLS OR PRINT REPORT                                         */
/*-------------------------------------------------------------------*/

/* REPORT                                                            */
             IF         COND(&LDRPFL *NE 'D') THEN(DO)
             CALL       PGM(HP6080)
             ENDDO

/* DOWNLOAD XLS                                                               */
             IF         COND(&LDRPFL *EQ 'D') THEN(DO)              /* Do DWL */

/* Retrieve the number of records in the DWL workfile                         */

             RTVMBRD    FILE(HSP3080DWL) NBRCURRCD(&RCDCNT)

             IF         COND(&RCDCNT = 0) THEN(DO)
             ESNDMAIL   RECIPIENT(&LDEMAIL) SUBJECT('HPS: +
                          HP6080-Activity Variance XLS Download') +
                          MSG('Your selections on HP4080-Specify +
                          Options for Activity Variance Report did +
                          not extract any data for the spreadsheet +
                          download. Please change your selections +
                          and submit the Download again.')
             MONMSG     MSGID(CPF0000)
             ENDDO

             IF         COND(&RCDCNT >= 66000) THEN(DO)
             ESNDMAIL   RECIPIENT(&LDEMAIL) SUBJECT('HPS: +
                          HP6080-Activity Variance XLS Download') +
                          MSG('Your selections on HP4080-Specify +
                          Options for Activity Variance Report +
                          extracted over 66,000 records--which is +
                          too many records for the spreadsheet to +
                          handle. Change your selections to extract +
                          fewer records and submit your Download +
                          request again.')
             MONMSG     MSGID(CPF0000)
             ENDDO

             IF         COND(&RCDCNT > 0 *AND &RCDCNT < 66000) THEN(DO)
             EXECUTE    SQL('SELECT * FROM HSP3080DWL') +
                          NBRRCDS(*ALL) PCFMT(*XLS) +
                          RECIPIENT(&LDEMAIL) EMLMSG('Activity +
                          Variance XLS Download--Data is sequenced +
                          by Phase/Cell/Farm.') TEXT('HPS: +
                          HP6080-Activity Variance XLS Download')
             MONMSG     MSGID(CPF0000)
             ENDDO
             ENDDO


             CLOF       OPNID(HSP3080)
             DLTOVR     FILE(*ALL)

             ENDPGM

