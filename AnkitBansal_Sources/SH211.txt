      *****************  RPG PROGRAM HEADING  ************************
     h option(*SRCSTMT:*NODEBUGIO)
      ****************************************************************
      *
      * ENVIRONMENT: Pork Division
      * SYSTEM:      Datamart--Sales
      * PROGRAM:     SH211 - Datamart Sales: Build Accrual Expense
      * PROGRAMMER:  LeAnne Ramsey
      * CREATED:     10/22/08
      *
      * FUNCTION: This program builds Accrual Expenses data from Dailys files.
      *           It is called twice in the Datamart build:
      *             1) first over JRDATA (Missoula; company 363)
      *             2) then over DFDATA (Salt Lake; company 364)
      *
      *
      *           For each run of the Datamart build, we will delete/rewrite
      *           records for the system "year". These records are deleted in
      *           a previous step/program.
      *
      ****************************************************************
      * Date      Programmer
      *
      * 08/06/09  LeAnne Ramsey
      *           Added logic for multiple "buckets" for Dailys Accrual Amounts.
      *           Prior to this we had a single amount, now we have 4 different
      *           accrual amounts.
      *
      * 04/26/10  LeAnne Ramsey
      *           Recompile only. New fields were added to the file:
      *             Total Market Accrual Amt
      *             Non-Ledgered Market Accrual Amt
      *             Ledgered Market Accrual Amt
      *             Weight Billed
      *
      * 07/23/15  Lara Buser  (E004044)
      *           Recompile only. Field added to SHP203-Datamart: Customer
      *               Field vs. In-House Managed Flag (CUMANAGED)
      *
      * 07/08/21  Brad Baden WHD80179 - Add Parent Customer Name field
      *           Recompile only. Parent Customer Name field added to
      *           file SHP203.
      *
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Faccrhist  if   e           k disk
      *  Customer accruals history (file keyed/records selected in OPNQRY)
      *
      *
     Faccruals  if   e           k disk
      *  Customer accruals transaction
      *
      *
     Fshp203    if   e           k disk
      *    Datamart: Customer
      *
      *
     Fshp211    uf a e           k disk
      *  Datamart: Accrual expense
      *
      /EJECT
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      *---------------------------------------------------------------
      *  Named Constants
      *---------------------------------------------------------------
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      *---------------------------------------------------------------
      * Standalone fields
      *---------------------------------------------------------------
      *
      * Control fields
      *
     D first           s              1    inz('Y')
     D svcust          s                   like(cust)
      *
      *
      * Workfields
      *
     D wkcorg          s                   like(aecorg) inz('D')
     D wk5am           s                   like(ae5accam)
     D wk6am           s                   like(ae6accam)
     D wk7am           s                   like(ae7accam)
     D wk8am           s                   like(ae8accam)
      *
      *
      * Parms
      *
     D xxcono          s                   like(aecono)
      *
      ****************************************************************
      * Data Structures
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * Mainline
      ****************************************************************
      *
      * Read/process the History file.
      *
     C                   exsr      $history
      *
      * Read/process the Monthly file.
      *
     C                   exsr      $monthly
      *
      *
     C                   seton                                        lr
      /eject
      *---------------------------------------------------------------
      * Process "history" file
      *---------------------------------------------------------------
      *
     C     $history      begsr
      *
     C                   dou       *in91 = *on                                  Do history
     C                   read      accrhist                               91
     C                   if        *in91 = *off                                 If not EOF
      *
     C                   select
     C                   when      first = yes
     C                   move      no            first
     C                   z-add     cust          svcust
      *
     C                   when      cust <> svcust
     C                   exsr      $wrt211
     C                   endsl
      * Detail
     C                   select
     C                   when      rec = '5'
     C                   add       accamt        wk5am
     C                   when      rec = '6'
     C                   add       accamt        wk6am
     C                   when      rec = '7'
     C                   add       accamt        wk7am
     C                   when      rec = '8'
     C                   add       accamt        wk8am
     C                   endsl
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Do history
      *
      * Last record
     C                   if        first = no
     C                   exsr      $wrt211
     C                   endif
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Process "monthly" file
      *---------------------------------------------------------------
      *
     C     $monthly      begsr
      *
     C                   move      yes           first
      *
     C                   dou       *in91 = *on                                  Do monthly
     C                   read      accruals                               91
     C                   if        *in91 = *off and                             If not EOF
     C                             (rec = '5' or rec = '6' or
     C                              rec = '7' or rec = '8')
      *
     C                   select
     C                   when      first = yes
     C                   move      no            first
     C                   z-add     cust          svcust
      *
     C                   when      cust <> svcust
     C     key01         chain     shp211                             92
     C                   if        *in92 = *off
     C                   exsr      $upd211
     C                   else
     C                   exsr      $wrt211
     C                   endif
     C                   endsl
      * Detail
     C                   select
     C                   when      rec = '5'
     C                   add       accamt        wk5am
     C                   when      rec = '6'
     C                   add       accamt        wk6am
     C                   when      rec = '7'
     C                   add       accamt        wk7am
     C                   when      rec = '8'
     C                   add       accamt        wk8am
     C                   endsl
     C                   endif                                                  If not EOF
     C                   enddo                                                  Do monthly
      *
      * Last record
     C                   if        first = no                                   If got something
      *
     C     key01         chain     shp211                             92
     C                   if        *in92 = *off
     C                   exsr      $upd211
     C                   else
     C                   exsr      $wrt211
     C                   endif
      *
     C                   endif                                                  If got something
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Write a record to Datamart Accrual Expense file.
      *---------------------------------------------------------------
      *
     C     $wrt211       begsr
      *
     C                   move      wkcorg        aecorg
     C                   z-add     xxcono        aecono
     C                   z-add     svcust        aeshcuno
     C                   z-add     *year         aeacyr
     C                   z-add     wk5am         ae5accam
     C                   z-add     wk6am         ae6accam
     C                   z-add     wk7am         ae7accam
     C                   z-add     wk8am         ae8accam
      *
      * Retrieve some "customer" fields from the Datamart Customer file.
      *
     C     key02         chain     shp203                             92
     C                   if        *in92 = *off
     C                   move      cucmmkcd      aecmmkcd
     C                   move      cusrcd        aesrcd
     C                   move      cumkcd        aemkcd
     C                   move      cumkbkcd      aemkbkcd
     C                   move      cumkspcd      aemkspcd
     C                   endif
      *
     C                   write     aerec
     C                   clear                   aerec
      *
     C                   z-add     cust          svcust
     C                   z-add     0             wk5am
     C                   z-add     0             wk6am
     C                   z-add     0             wk7am
     C                   z-add     0             wk8am
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Update a Datamart Accrual Expense record.
      *---------------------------------------------------------------
      *
     C     $upd211       begsr
      *
     C                   add       wk5am         ae5accam
     C                   add       wk6am         ae6accam
     C                   add       wk7am         ae7accam
     C                   add       wk8am         ae8accam
     C                   update    aerec
      *
     C                   z-add     cust          svcust
     C                   z-add     0             wk5am
     C                   z-add     0             wk6am
     C                   z-add     0             wk7am
     C                   z-add     0             wk8am
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      *  Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
     C     *entry        plist
     C                   parm                    xxcono
      *
      * Key lists
      *
     C     key01         klist
     C                   kfld                    wkcorg
     C                   kfld                    xxcono
     C                   kfld                    *year
     C                   kfld                    svcust
      *
     C     key02         klist
     C                   kfld                    aeshcuno
     C                   kfld                    aecorg
      *
     C                   endsr
      /EJECT
