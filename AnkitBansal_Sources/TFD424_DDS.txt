     A*%%TS  SD  20080814  104409  ISLRAMS     REL-V5R4M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        TRIUMPH FOODS
     A*  DISPLAY FILE:  TFD424
     A*  TITLE:         SPECIFY WIP USAGE
     A*  PROGRAMMER:    LEANNE RAMSEY
     A*  CREATED:       07/12/07
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/TFPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A                                      CF17(17 'PRINT OPTIONS')
     A                                      CF20(20 'WRKSPLF')
     A*
     A*---------------------------------------------------------------
     A* RECORD FORMAT ONE
     A*---------------------------------------------------------------
     A          R TF424R1
     A*%%TS  SD  20080814  104409  ISLRAMS     REL-V5R4M0  5722-WDS
     A                                      SETOF(30)
     A                                      SETOF(31)
     A                                      SETOF(32)
     A                                      SETOF(33)
     A                                      OVERLAY
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 30'SPECIFY WIP USAGE'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USR)
     A                                      DSPATR(HI)
     A  89                              2  2'VIEW ONLY VERSION'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@JOB)
     A                                      DSPATR(HI)
     A                                  4 26'Week-ending date'
     A                                      DSPATR(UL)
     A                                      COLOR(BLU)
     A                                  4 44'Year'
     A                                      DSPATR(UL)
     A                                      COLOR(BLU)
     A                                  4 50'Week'
     A                                      DSPATR(UL)
     A                                      COLOR(BLU)
     A                                  5  2'Date ....'
     A            D1MDY     R        B  5 13REFFLD(TFREF/@@DT06 *LIBL/TFPREF)
     A                                      EDTWRD('  /  /  -')
     A  30                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A N30                                  DSPATR(HI)
     A            D1WEMDY   R        O  5 30REFFLD(TFREF/@@DT06 *LIBL/TFPREF)
     A                                      EDTWRD('  /  /  ')
     A            D1YR      R        O  5 44REFFLD(TFREF/@@YR *LIBL/TFPREF)
     A                                      EDTCDE(Z)
     A            D1WK      R        O  5 51REFFLD(TFREF/@@WK *LIBL/TFPREF)
     A                                      EDTCDE(Z)
     A                                  7  2'Option ..'
     A            D1OPT          1A  B  7 13
     A  31                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A N31                                  DSPATR(HI)
     A N89                              7 26'(M=Maintenance, V=View, R=Report)'
     A  89                              7 26'(V=View, R=Report)'
     A                                 10  2'When you have selected the V=View -
     A                                      or R=Report function, you must ente-
     A                                      r:'
     A                                      COLOR(BLU)
     A                                 11  6'Company ....'
     A            D1CONO    R        B 11 20REFFLD(TFREF/@@CONO *LIBL/TFPREF)
     A  32                                  DSPATR(PC)
     A  32                                  DSPATR(RI)
     A N32                                  DSPATR(HI)
     A                                 12  6'Warehouse ..'
     A            D1WHS          3   B 12 20
     A  33                                  DSPATR(PC)
     A  33                                  DSPATR(RI)
     A N33                                  DSPATR(HI)
     A*
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R TF424K1
     A*%%TS  SD  20070824  102003  ISLRAMS     REL-V5R4M0  5722-WDS
     A  43                             21  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A                                 23 42'F17=Print Options'
     A                                 23 61'F20=Spool Files'
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *---------------------------------------------------------------
     A          R TF424ES                   SFL SFLMSGRCD(24)
      *
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
     A*---------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*---------------------------------------------------------------
     A*
     A          R TF424EC                   SFLCTL(TF424ES)
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
