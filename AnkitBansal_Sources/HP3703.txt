      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Hog Production System
      * PROGRAM:     HP3703-Remove Farm Links to Template
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     02/05/04
      *
      * Function:    Depending on the incoming parm, this function:
      *                1) deletes all records from the Budget Template/Farm Links file for
      *                   a template
      *                2) deletes only the linking records for farms that have no budget header
      *                   records for a template
      *
      *              This program is called from HP4700-Work With Budget Templates.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      /eject
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fhsp187    uf   e           k disk
      *    Budget template/farm links
      *
      *
     Fhsl188b   if   e           k disk
      *    Farm budget header
      *
      /eject
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      *----------------------------------------------------------------
      * CONSTANTS
      *----------------------------------------------------------------
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      *----------------------------------------------------------------
      * STANDALONE FIELDS
      *----------------------------------------------------------------
      *
      * Standard fields
      *
     D rtncd           s              2
      *
      *
      * Parms
      *
     D xxbtcd          s                   like(tlbtcd)
     D xxonly0         s              1
      *
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * Mainline
      ****************************************************************
      *
      * Process each Budget template/farm link record for the incoming Budget
      * Template code.
      *
     C     xxbtcd        setll     hsp187
      *
     C                   dou       *in90 = *on                                  Main do
     C     xxbtcd        reade     hsp187                                 90
     C                   if        *in90 = *off                                 If not EOF
      *
      * Unconditionally delete the farm link when the incoming parm
      * indicates that the user wants all linking records deleted.
      *
     C                   select
     C                   when      xxonly0 = no
     C                   delete    tlrec
     C                   other
      *
      * Delete the farm link only if the farm does not have any budgets
      * for this template.
      *
     C     key01         chain     hsl188b                            92
     C                   if        *in92 = *on                                  If no budgets
     C                   delete    tlrec
     C                   endif                                                  If no budgets
     C                   endsl
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Main do
      *
     C                   seton                                        lr
      /eject
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Parm lists
      *
     C     *entry        plist
     C                   parm                    xxbtcd
     C                   parm                    xxonly0
     C                   parm                    rtncd
      *
      * Key lists
      *
     C     key01         klist
     C                   kfld                    xxbtcd
     C                   kfld                    tlfscd
      *
     C                   endsr
      /eject
