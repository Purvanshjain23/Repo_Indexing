/********************************************************************/
/*                                                                  */
/* PPX0UPC   - PRT SBM INVC EXCPT RPT CL                            */
/* SYNOPSIS  - PROMPT JOB CONTROL FOR PRINTER OVERRIDE INFORMATION  */
/*             TO PRINT INVOICE EXCEPTION REPORT FOR THIRD          */
/*             PARTY OR INTER-COMPANY CUSTOMERS.                    */
/* SYSTEM    - ORDER MANAGEMENT SYSTEM                              */
/* DATE      - 04/27/2006                                           */
/* PROGRAMMER- LARA BUSER                                           */
/*                                                                  */
/* PARAMETERS RECEIVED:                                             */
/*   &RETURN     - RETURN CODE                                      */
/*   &APPLIC     - APPLICATION                                      */
/*   &COMPNYN    - COMPANY NUMBER - BLANK FOR ALL                   */
/*   &ORDRN      - ORDE NUMBER                                      */
/*   &THRDATN    - PROCESS ORDERS THRU ACTUAL SHIP DATE             */
/*   &LOADN      - PROCESS ORDERS ON LOAD                           */
/*   &BILTYP     - BILLING TYPE (INVOICE,MEMO)                      */
/*   &DESC       - PRINT PROGRAM DESCRIPTION (FOR PROMPT SCREEN)    */
/*   &OUTQ       - OUTPUT QUEUE                                     */
/*   &HOLD       - HOLD PRINT                                       */
/*   &SAVE       - SAVE PRINT                                       */
/*   &NQSTAT     - NIGHT QUEUE STATUS                               */
/*   &NIGHTQ     - NIGHT OUTPUT QUEUE                               */
/*   &COPYN      - NUMBER OF COPIES                                 */
/*   &PRCUSTT    - THRID PARTY OR INTER-COMPANY                     */
/*   &JOBNME     - JOB NAME                                         */
/*   &STATUSYN   - PRINT AMOUNTS ON REPORT-YES OR NO                */
/********************************************************************/
/*  MODIFICATIONS:                                                  */
/* ---------------------------------------------------------------- */
/*                                                                  */
/********************************************************************/
 PPX0UPC:    PGM        PARM(&RETURN &APPLIC &COMPNYN &ORDRN +
                          &THRDATN &LOADN &BILTYP &OUTQ &HOLD &SAVE +
                          &COPYN &NQSTAT &NIGHTQ &STATUSYN &JOBNME +
                          &PRCUSTT)

/* PARMS                                                                      */
             DCL        VAR(&RETURN) TYPE(*CHAR) LEN(7) /* RETURN +
                          CODE              */
             DCL        VAR(&APPLIC) TYPE(*CHAR) LEN(6) /* +
                          APPLICATION              */
             DCL        VAR(&COMPNYN) TYPE(*DEC) /* Company Number +
                          (Passed)   */
             DCL        VAR(&LOADN) TYPE(*DEC) /* Load  Number   +
                          (Passed)   */
             DCL        VAR(&THRDATN) TYPE(*DEC) /* Thru Ship Date +
                          (Passed)   */
             DCL        VAR(&ORDRN) TYPE(*DEC) /* Order Number   +
                          (Passed)   */
             DCL        VAR(&BILTYP) TYPE(*CHAR) LEN(1) /* Bill Type +
                          (Invoice,Memo)  */
             DCL        VAR(&DESC) TYPE(*CHAR) LEN(25) +
                          VALUE('Invoice Exception Report') /* PRT +
                          PGM DESCRIPTION      */
             DCL        VAR(&OUTQ) TYPE(*CHAR) LEN(10) /* Output +
                          Queue              */
             DCL        VAR(&HOLD) TYPE(*CHAR) LEN(4) /* Hold +
                          Status               */
             DCL        VAR(&SAVE) TYPE(*CHAR) LEN(4) /* Save +
                          Status               */
             DCL        VAR(&COPYN) TYPE(*DEC) /* No. of Copies  +
                          (Passed)   */
             DCL        VAR(&NQSTAT) TYPE(*CHAR) LEN(1) /* If MUST +
                          run in night q.   */
             DCL        VAR(&NIGHTQ) TYPE(*CHAR) LEN(10) /* Night +
                          Job Queue           */
             DCL        VAR(&JOBNME) TYPE(*CHAR) LEN(10) /* Job +
                          Name                  */
             DCL        VAR(&PRCUSTT) TYPE(*CHAR) LEN(02) /* PROCESS +
                          CUST TYPE (PASSED)*/
             DCL        VAR(&STATUSYN) TYPE(*CHAR) LEN(02) /* Print +
                          Amounts Control Flag */

             DCL        VAR(&COPY) TYPE(*DEC) LEN(2 0) /* NUMBER OF +
                          COPIES         */
             DCL        VAR(&COMMAND) TYPE(*CHAR) LEN(512) /* For +
                          Trans. Reg. override  */

/* ERROR MESSAGE FIELDS                                              */
             DCL        VAR(&MSG) TYPE(*CHAR) LEN(512) /* +
                          MESSAGE                  */
             DCL        VAR(&MSGID) TYPE(*CHAR) LEN(7) /* MESSAGE +
                          ID               */
             DCL        VAR(&MSGL) TYPE(*DEC) LEN(5 0)

/********************************************************************/
/*   INITIALIZE VARIABLES                                           */
/********************************************************************/
             CHGVAR     VAR(&RETURN) VALUE('       ')
             CHGVAR     VAR(&COPY) VALUE(&COPYN)

/********************************************************************/
/*   MONITOR FOR ALL MESSAGES AT PROGRAM LEVEL                      */
/********************************************************************/
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(MSGTRAP))

/********************************************************************/
/* PROMPT FOR PRINTER OVERRIDE INFORMATION                          */
/********************************************************************/
 PRINTER:    CALL       PGM(PDG0PVR) PARM(&RETURN &DESC &OUTQ &HOLD +
                          &SAVE &COPY &NQSTAT &NIGHTQ &APPLIC)
             IF         COND(&RETURN *NE ' ') THEN(GOTO +
                          CMDLBL(USERQUIT))

             CHGVAR     VAR(&COPYN) VALUE(&COPY)

/*********************************************************************/
/* CALL THE PROGRAM TO SUBMIT THE EXCEPTION REPORT                   */
/*********************************************************************/
             SBMJOB     CMD(CALL PGM(PPXZUPC) PARM(&RETURN &APPLIC +
                          &BILTYP &DESC &OUTQ &HOLD &SAVE &NQSTAT +
                          &NIGHTQ &COMPNYN &THRDATN &ORDRN &LOADN +
                          &COPYN &STATUSYN &PRCUSTT &JOBNME)) +
                          JOB(&JOBNME) OUTQ(&NIGHTQ)

             GOTO       CMDLBL(END)

/*-------------------------USER QUIT--------------------------------*/
/* USER QUIT REQUESTED                                              */
/*------------------------------------------------------------------*/
 USERQUIT:   CHGVAR     VAR(&RETURN) VALUE('Y2U9999')
             GOTO       CMDLBL(END)

/*----------------------RECEIVE MESSAGES----------------------------*/

 MSGTRAP:    RCVMSG     MSGTYPE(*EXCP) MSGDTA(&MSG) MSGDTALEN(&MSGL) +
                          MSGID(&MSGID)
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(END))

             CHGJOB     LOG(4 20 *SECLVL) LOGCLPGM(*YES)

             SNDPGMMSG  MSGID(&MSGID) MSGF(QCPFMSG) MSGDTA(%SST(&MSG +
                          1 &MSGL)) MSGTYPE(*ESCAPE)
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(END))

 END:        ENDPGM
