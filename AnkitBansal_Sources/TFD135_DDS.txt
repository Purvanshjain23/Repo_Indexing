     A*%%TS  SD  20050406  153853  ISLRAMS     REL-V5R2M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        TRIUMPH FOODS
     A*  DISPLAY FILE:  TFD135
     A*  TITLE:         SELECT CHARGES TO ASSIGN TO AN INVOICE
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  DATE:          03/10/05
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/TFPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A                                      CF04(04 'PROMPT')
     A                                      CF05(05 'REFRESH')
     A*
     A*
     A*---------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*---------------------------------------------------------------
     A          R TF135S1                   SFL
     A*%%TS  SD  20050406  152350  ISLRAMS     REL-V5R2M0  5722-WDS
     A  85                                  SFLNXTCHG
     A            HDRRN1         4Y 0H
     A            D1OPT          2A  B 11  2CHGINPDFT(UL)
     A  30                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A N30                                  DSPATR(HI)
     A  90                                  DSPATR(PR)
     A  90                                  DSPATR(ND)
     A N90                                  DSPATR(UL)
     A            D1ICDTMDY R        O 11  5REFFLD(TFREF/@@DT06 *LIBL/TFPREF)
     A                                      EDTWRD('  /  /  ')
     A            D1INFL    R        O 11 14REFFLD(TFREF/@@INFL *LIBL/TFPREF)
     A            D1SMCD    R        O 11 17REFFLD(TFREF/@@SMCD *LIBL/TFPREF)
     A            D1CCCD    R        O 11 20REFFLD(TFREF/@@CCCD *LIBL/TFPREF)
     A            D1CFQCD   R        O 11 32REFFLD(TFREF/@@CFQCD *LIBL/TFPREF)
     A            D1ICSN    R        O 11 35REFFLD(@@ICSN)
     A                                      EDTCDE(Z)
     A            D1CCDS        20A  O 11 44
     A            D1ICAM    R        O 11 65REFFLD(TFREF/@@ICAM *LIBL/TFPREF)
     A                                      EDTCDE(K)
     A*
     A*---------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*---------------------------------------------------------------
     A          R TF135C1                   SFLCTL(TF135S1)
     A*%%TS  SD  20050406  153853  ISLRAMS     REL-V5R2M0  5722-WDS
     A                                      SFLSIZ(0050)
     A                                      SFLPAG(0011)
     A N82N90                               ROLLUP(25 'PAGE FORWARD')
     A                                      SETOF(30)
     A                                      SETOF(51)
     A                                      SETOF(52)
     A                                      SETOF(53)
     A                                      RTNCSRLOC(&RCD &FLD)
     A                                      OVERLAY
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 18'SELECT CHARGES TO ASSIGN TO AN INV-
     A                                      OICE'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USR)
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@JOB)
     A                                      DSPATR(HI)
     A                                  3  2'Invoice number :'
     A                                      DSPATR(HI)
     A            C1INSN    R        O  3 19REFFLD(TFREF/@@INSN *LIBL/TFPREF)
     A                                      EDTCDE(Z)
     A                                  3 33'Charge frequency:'
     A                                      DSPATR(HI)
     A            C1CFQCD   R        O  3 51REFFLD(TFREF/@@CFQCD *LIBL/TFPREF)
     A            C1CFQDS   R        O  3 53REFFLD(TFREF/@@CFQDS *LIBL/TFPREF)
     A                                  4  2'Invoice date ..:'
     A                                      DSPATR(HI)
     A            C1INDTMDY R        O  4 19REFFLD(TFREF/@@DT06 *LIBL/TFPREF)
     A                                      EDTWRD('  /  / 0')
     A                                  4 33'Invoice status .:'
     A                                      DSPATR(HI)
     A            C1INSTCD  R        O  4 51REFFLD(TFREF/@@INSTCD *LIBL/TFPREF)
     A            C1STDS    R        O  4 53REFFLD(TFREF/@@STDS *LIBL/TFPREF)
     A N90                              6  2'1=Select'
     A                                  7 14'?'
     A                                      DSPATR(HI)
     A                                  7 18'?'
     A                                      DSPATR(HI)
     A                                  7 23'?'
     A                                      DSPATR(HI)
     A                                  8  6'Charge'
     A                                      DSPATR(HI)
     A                                  8 14'In'
     A                                      DSPATR(HI)
     A                                  8 17'Sy'
     A                                      DSPATR(HI)
     A                                  8 20'Charge'
     A                                      DSPATR(HI)
     A                                  8 31'Chg'
     A                                      DSPATR(HI)
     A                                  8 36'Charge'
     A                                      DSPATR(HI)
     A                                  9  2'Op'
     A                                      DSPATR(HI)
     A                                  9  7'Date'
     A                                      DSPATR(HI)
     A                                  9 14'Iv'
     A                                      DSPATR(HI)
     A                                  9 17'Md'
     A                                      DSPATR(HI)
     A                                  9 20'Code'
     A                                      DSPATR(HI)
     A                                  9 31'Frq'
     A                                      DSPATR(HI)
     A                                  9 36'Number'
     A                                      DSPATR(HI)
     A                                  9 44'Description'
     A                                      DSPATR(HI)
     A                                  9 66'Charge Amount'
     A                                      DSPATR(HI)
     A            C1ICDTMDY R        B 10  5REFFLD(TFREF/@@DT06 *LIBL/TFPREF)
     A                                      EDTWRD('  /  / 0')
     A                                      DSPATR(HI)
     A            C1INFL    R        B 10 14REFFLD(TFREF/@@INFL *LIBL/TFPREF)
     A                                      DSPATR(HI)
     A  53                                  DSPATR(PC)
     A            C1SMCD    R        B 10 17REFFLD(TFREF/@@SMCD *LIBL/TFPREF)
     A  52                                  DSPATR(PC)
     A                                      DSPATR(HI)
     A            C1CCCD    R        B 10 20REFFLD(TFREF/@@CCCD *LIBL/TFPREF)
     A  51                                  DSPATR(PC)
     A                                      DSPATR(HI)
     A            C1ICSN    R        B 10 35REFFLD(@@ICSN)
     A                                      EDTCDE(Z)
     A                                      DSPATR(HI)
     A*
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R TF135K1
     A*%%TS  SD  20050310  160501  ISLRAMS     REL-V5R2M0  5722-WDS
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A N90                             23 11'F4=Prompt'
     A N90                             23 22'F5=Refresh'
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *---------------------------------------------------------------
     A          R TF135ES                   SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE CONTROL FORMAT
      *---------------------------------------------------------------
     A          R TF135EC                   SFLCTL(TF135ES)
      *
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
