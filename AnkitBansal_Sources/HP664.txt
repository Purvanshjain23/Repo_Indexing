      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      HOG PRODUCTION
      * PROGRAM:     HP664
      * TITLE:       FEED TICKET EXCEPTION REPORT  (NOT ALLOCATED)
      * PROGRAMMER:  MARY PALMER
      * CREATED:     4/22/94
      *
      *  FUNCTION:   THIS PROGRAM PRINTS A SINGLE LINE REPORT
      *              OF FEED TICKET HEADER RECORDS FOR A SELECTED
      *              BUSINESS OFFICE AND CUT-OFF DATE.
      *
      *              THIS REPORT RUNS WHEN THE USER HAS SELECTED:
      *                      NOT ALLOCATED
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 01/30/01  LeAnne Fedor
      *           Recompile only.  Production type was removed from feed files.
      *
      * 07/16/02  LeAnne Fedor
      *           Recompile only. Production Phase was removed from the Feed
      *           Ticket Header file.
      *
      * 04/23/09  LeAnne Ramsey
      *           Recompile only. Costed Time/User added to Feed Ticket Header.
      *
      * 05/12/09  LeAnne Ramsey
      *           Recompile only. PickUp Ticket Type added to Feed Ticket Header.
      *
      * 09/21/09  LeAnne Ramsey
      *           Recompile only. Added new field 'Upload Date' to Feed Ticket Header.
      *
      * 03/26/20  Danny Nguyen - P405 - Farm Number Increase
      *           Increased Bin Code (@@BNCD) in HSPREF file from 5A to 6A.
      *           Shifted 1 space to the right for Bin Code on report.
      *
      * 05/10/22 Eric L SDN736 Recompiled ticket nbr increase (TKNO & RTNO 7.0 TO 9.0)
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     FHSP037    IF   E           K DISK
      *    FEED TICKET HEADER
      *
     FHSP038    IF   E           K DISK
      *    FEED TICKET DETAIL
      *    (FILE KEYED IN OPNQRY)
      *
     FQPRINT    O    F  132        PRINTER OFLIND(*INOF)
      *
      /EJECT
      ****************************************************************
      * INPUT SPECS
      ****************************************************************
      *
      *---------------------------------------------------------------
      * STANDARD PROGRAM STATUS DATA STRUCTURE
      *---------------------------------------------------------------
      *    EXTERNALLY DEFINED AS UTPGFR (RECORD FORMAT: PGMDSR)
     D PGMDS         ESDS                  EXTNAME(UTPGFR)
      *
      *---------------------------------------------------------------
      *  NAMED CONSTANTS
      *---------------------------------------------------------------
      *
     D YES             C                   CONST('Y')
     D NO              C                   CONST('N')
      *
      *---------------------------------------------------------------
      *  DATE FORMATS
      *---------------------------------------------------------------
      *
      * DATE IN MMDDYY FORMAT
      *
     D                 DS
     D  WKMDY                  1      6  0
     D  WKMM                   1      2  0
     D  WKDD                   3      4  0
     D  WKYY                   5      6  0
      *
      * DATE IN CCYYMMDD FORMAT
      *
     D                 DS
     D  WKCYMD                 1      8  0
     D  WKCEN                  1      2  0
     D  WKYER                  3      4  0
     D  WKMON                  5      6  0
     D  WKDAY                  7      8  0
      *
      *---------------------------------------------------------------
      * LDA - REPORT SELECTIONS
      *---------------------------------------------------------------
      *
     D                UDS
     D  LDFMBO                 1      5
     D  LDCYMD                 6     13  0
     D  LDROCD                14     14
     D  LDRODS                15     44
     D  LDCMDY                45     50  0
     D  LDBODS                51     80
      /EJECT
      ****************************************************************
      * MAIN LINE
      ****************************************************************
      *
      * PROCESS ALL FEED DETAIL RECORDS SELECTED BY THE OPNQRY
      *
     C     *IN92         DOUEQ     *ON                                          MAIN DO
     C                   READ      HSP038                                 92
     C     *IN92         IFEQ      *OFF                                         IF NOT EOF
      *
      * RETRIEVE TICKET HEADER INFO
      *
     C     K1P037        CHAIN     HSP037                             93
     C     *IN93         IFEQ      *OFF                                         IF FOUND
     C                   MOVEL     FHPRFL        DTPRFL
     C                   MOVEL     FHCSFL        DTCSFL
     C                   ELSE
     C                   MOVE      *BLANK        DTPRFL
     C                   MOVE      *BLANK        DTCSFL
     C                   ENDIF                                                  IF FOUND
      *
      * FLIP TICKET DATE
     C                   Z-ADD     FDTKDT        WKCYMD
     C                   EXSR      $TOMDY
     C                   Z-ADD     WKMDY         DTTMDY
      *
     C     *INOF         IFEQ      *ON
     C                   EXCEPT    HRHDR
     C                   ENDIF
      *
      * PRINT FEED TICKET DETAIL LINE
      *
     C                   EXCEPT    DTLINE
     C                   MOVEL     YES           PRTFLG
      *
     C                   ENDIF                                                  IF NOT EOF
     C                   ENDDO                                                  MAIN DO
      *
      *---------------------------------------------------------------
      * EOF PROCESSING
      *---------------------------------------------------------------
      *  IF NO TICKET DETAIL RECORDS MATCHED THE SELECTION CRITERIA,
      *  PRINT THE STANDARD 'NO DATA...' ERROR MESSAGE
      *
     C     PRTFLG        IFEQ      NO
     C                   EXCEPT    NODATA
     C                   ENDIF
      *
     C                   SETON                                        LR
      /EJECT
      *---------------------------------------------------------------
      * $TOMDY:  FLIP DATE FROM YYMMDD TO MMDDYY
      *---------------------------------------------------------------
      *
     C     $TOMDY        BEGSR
      *
     C                   Z-ADD     WKMON         WKMM
     C                   Z-ADD     WKDAY         WKDD
     C                   Z-ADD     WKYER         WKYY
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * *INZSR - INITIALIZATION SUBROUTINE
      *---------------------------------------------------------------
      *
     C     *INZSR        BEGSR
      *
      * KEY FOR FEED TICKET HEADER
      *
     C     K1P037        KLIST
     C                   KFLD                    FDFMBO
     C                   KFLD                    FDTKNO
     C                   KFLD                    FDTRCD
      *
      * DEFINE PRINT FIELDS FOR DETAIL LINE
      *
     C     *LIKE         DEFINE    FHPRFL        DTPRFL
     C     *LIKE         DEFINE    FHCSFL        DTCSFL
     C                   Z-ADD     0             DTTMDY            6 0
      *
      * SET PRINT FLAG TO DETERMINE IF ANY RECORDS WERE PRINTED
      * SET UP HEADING UNDERLINE FIELD FOR PRINT
      * RETRIEVE TIME FOR REPORT HEADING
      *
     C                   MOVEL     NO            PRTFLG            1
     C                   MOVEL     *ALL'-'       DASH             89
     C                   TIME                    RTIME             6 0
      *
      * PRINT HEADINGS FOR FIRST TIME THROUGH
      *
     C                   EXCEPT    HRHDR
      *
     C                   ENDSR
      /EJECT
      *-------------------------------------------------------------
      * REPORT HEADING LINES
      *-------------------------------------------------------------
      *
     OQPRINT    E            HRHDR          1 01
     O                       SDPGM               10
     O                                           53 'HOG PRODUCTION SYSTEM'
     O                                           79 'DATE'
     O                       UDATE         Y     89
      *
     O          E            HRHDR          1
     O                       SDUSR               10
     O                                           52 'FEED TICKET EXCEPTION RE'
     O                                           56 'PORT'
     O                                           79 'TIME'
     O                       RTIME               89 '  :  :  '
      *
     O          E            HRHDR          1
     O                                           79 'PAGE'
     O                       PAGE          Z     89
      *
     O          E            HRHDR          1
     O                                           18 'BUSINESS OFFICE ..'
     O                       LDFMBO              25
     O                       LDBODS              60
      *
     O          E            HRHDR          1
     O                                           18 'CUT-OFF DATE .....'
     O                       LDCMDY        Y     28
      *
     O          E            HRHDR          2
     O                                           18 'REPORT OPTION ....'
     O                       LDROCD              21
     O                       LDRODS              60
      *
     O          E            HRHDR          1
     O                                            9 'REFERENCE'
     O                                           23 'TRN'
     O                                           30 'TICKET'
     O                                           38 'TICKET'
     O                                           44 'PROD'
     O                                           50 'COST'
     O                                           56 'FARM'
     O                                           79 'ADDITIVE'
      *
     O          E            HRHDR          1
     O                                            7 'TICKET#'
     O                                           17 'TICKET#'
     O                                           23 'COD'
     O                                           29 'DATE'
     O                                           38 'STATUS'
     O                                           43 'Y/N'
     O                                           49 'Y/N'
     O                                           56 'SITE'
     O                                           61 'BIN'
     O                                           71 'RATION'
     O                                           88 'POUNDS'
      *
     O          E            HRHDR          1
     O                       DASH                89
      *
      *-------------------------------------------------------------
      * FEED TICKET DETAIL LINE
      *-------------------------------------------------------------
      *
     O          E            DTLINE      1
     O                       FDRTNO        Z      9
     O                       FDTKNO        Z     19
     O                       FDTRCD              22
     O                       DTTMDY        Y     31
     O                       FHTSCD              36
     O                       DTPRFL              42
     O                       DTCSFL              49
     O                       FDFSCD        M     57
P405 O**                     FDBNCD              63
P405 O                       FDBNCD              64
     O                       FDRNCD              70
     O                       FDADCD              77
     O                       FDFDLB        K     89
      *
      *-------------------------------------------------------------
      * NO DATA MESSAGE LINE
      *-------------------------------------------------------------
      *
     O          E            NODATA      1
     O                                           19 'NO DATA MEETS YOUR'
     O                                           38 'SELECTION CRITERIA'
