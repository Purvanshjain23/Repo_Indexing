      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      HOG PRODUCTION
      * PROGRAM:     HPCONFAR
      * TITLE:       Generic Program to Retrieve/Calculate the BGF Conception and
      *              Farrowing Rates
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     10/11/00
      *
      * FUNCTION:  This program retrieves the conception and farrowing data for a given
      *            farm/year/week; then retrieves the associated services data; then
      *            calculates the conception rate and farrowing rate.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 10/18/00  LeAnne Fedor
      *           Renamed farrowing and conception fields.
      *
      * 12/11/00  LeAnne Fedor
      *           Recompile only. New field 'de-pop flag' added to
      *           Weekly Production File.
      *
      * 02/27/01  LeAnne Fedor
      *           Recompile only. Added 4 new fields to Weekly Production File:
      *              gilts transferred in
      *              gilts transferred out
      *              sows transferred in
      *              sows transferred out
      *
      * 12/18/01  LeAnne Fedor
      *           Recompile only. Added many new fields to Weekly Production File.
      *
      * 02/19/02  LeAnne Fedor
      *           Recompile only. 'Weeking ending pounds of supplement used' changed to
      *           7,0 from 9,2.
      *
      * 02/03/05  LeAnne Fedor
      *           Recompile only. New field "Transferred In Open Gilts" add
      *           to BGF Weekly Production file.
      *
      * 08/23/05  LeAnne Fedor
      *           Recompile only. New field "Man Hours" added to BGF Daily and Weekly
      *           Production files.
      *
      * 06/15/06  LeAnne Ramsey
      *           Recompile only. New field "Avg Gilt Weight @ 28 Weeks" added
      *           to BGF Weekly Production file.
      *
      * 08/01/08  LeAnne Ramsey
      *           Recompile only. Removed fields from BGF Weekly Production file.
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
      *
     Fhsl092a   if   e           k disk
      *    BGF weekly production
      *
      *
     Fhsp093    if   e           k disk
      *    BGF farrowing and pregnancy check events
      *
      *
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      *
      ****************************************************************
      * ARRAYS AND TABLES
      ****************************************************************
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      *
      * Work fields
      *
     D wkdtfl          s              1
     D wkfld15         s             15  0
     D wkfld155        s             15  5
     D wkwk            s                   like(bpcdwk)
     D wkyr            s                   like(bpcdyr)
      *
      *
      * Parms
      *
     D xxfscd          s                   like(befscd)
     D xxcdyr          s                   like(beevyr)
     D xxcdwk          s                   like(beevwk)
      *
     D xxpryr          s                   like(beevyr)
     D xxprwk          s                   like(beevwk)
     D xxglprhd        s                   like(beglevhd)
     D xxprhd          s                   like(beevhd)
     D xxcrpr          s              5  2
      *
     D xxfayr          s                   like(beevyr)
     D xxfawk          s                   like(beevwk)
     D xxglfahd        s                   like(beglevhd)
     D xxfahd          s                   like(beevhd)
     D xxfrpr          s              5  2
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    ex ernally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * Mainline
      ****************************************************************
      *
      *
      * Retrieve pregnancy checked positive data
      *
     C                   move      'P'           wkdtfl
     C     key01         chain     hsp093                             92        If preg data
     C                   if        *in92 = *off
     C                   z-add     besvyr        xxpryr
     C                   z-add     besvwk        xxprwk
     C                   z-add     beglevhd      xxglprhd
     C                   z-add     beevhd        xxprhd
     C                   endif                                                  If preg data
      *
      * Retrieve farrowing rate data
      *
     C                   move      'F'           wkdtfl
     C     key01         chain     hsp093                             92        If farrow data
     C                   if        *in92 = *off
     C                   z-add     besvyr        xxfayr
     C                   z-add     besvwk        xxfawk
     C                   z-add     beglevhd      xxglfahd
     C                   z-add     beevhd        xxfahd
     C                   endif                                                  If farrow data
      *
      *
      * Calculate conception rate percent after retrieving the 'services' values
      * for the specified year/week. (Place key values in generic workfields
      * so that a common key list can be used.)
      *
     C                   z-add     xxpryr        wkyr
     C                   z-add     xxprwk        wkwk
      *
     C     key02         chain     hsl092a                            92
     C                   if        *in92 = *on                                  If no hit
     C                   z-add     0             xxcrpr
     C                   else
     C                   eval      wkfld15 =
     C                             (bpwnsv + bplwnsv + bprtsv + bpglsv)
     C                   if        wkfld15 <> 0
     C     xxprhd        div       wkfld15       wkfld155
     C     wkfld155      mult(h)   100           xxcrpr
     C                   endif
     C                   endif                                                  If no hit
      *
      *
      * Calculate farrowing rate after retrieving the 'services' values for
      * the specified year/week. (Place key values in generic workfields
      * so that a common key list can be used.)
      *
     C                   z-add     xxfayr        wkyr
     C                   z-add     xxfawk        wkwk
      *
     C     key02         chain     hsl092a                            92
     C                   if        *in92 = *on                                  If not found
     C                   z-add     0             xxfrpr
     C                   else
     C                   eval      wkfld15 =
     C                             bpwnsv + bplwnsv + bprtsv + bpglsv
     C                   if        wkfld15 <> 0
     C     xxfahd        div       wkfld15       wkfld155
     C     wkfld155      mult(h)   100           xxfrpr
     C                   endif
     C                   endif                                                  If not found
      *
     C                   seton                                        lr
      /EJECT
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Parm lists
      *
     C     *entry        plist
     C                   parm                    xxfscd
     C                   parm                    xxcdyr
     C                   parm                    xxcdwk
     C                   parm                    xxpryr
     C                   parm                    xxprwk
     C                   parm                    xxglprhd
     C                   parm                    xxprhd
     C                   parm                    xxcrpr
     C                   parm                    xxfayr
     C                   parm                    xxfawk
     C                   parm                    xxglfahd
     C                   parm                    xxfahd
     C                   parm                    xxfrpr
      *
      * Key lists
      *
     C     key01         klist
     C                   kfld                    xxfscd
     C                   kfld                    xxcdyr
     C                   kfld                    xxcdwk
     C                   kfld                    wkdtfl
      *
      *
     C     key02         klist
     C                   kfld                    befscd
     C                   kfld                    wkyr
     C                   kfld                    wkwk
      *
     C                   endsr
      /EJECT
