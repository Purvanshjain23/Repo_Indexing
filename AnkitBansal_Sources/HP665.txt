      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      HOG PRODUCTION
      * PROGRAM:     HP665 - FEED TICKET DETAIL REPORT BY PRODUCTION PHASE
      * PROGRAMMER:  MARY PALMER
      * CREATED:     4/28/94
      *
      * FUNCTION:    FEED TICKET DETAIL REPORT OF TRANSACTION POUNDS &
      *              COSTS FOR A SELECTED DATE RANGE.
      *
      *              With our changes of 9/13/01, we now print 2 versions thru this
      *              program. The difference in versions is the sort sequence:
      *                         By Production Phase
      *                         By Production Manager
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 01/18/01  LeAnne Fedor
      *           Rick Crouse requested that a selection on 'cell' be
      *           added for this report.
      *
      * 01/31/01  LeAnne Fedor
      *           Production type was removed from the feed files. So, we
      *           a) removed it from the report selection screen and b) removed
      *           it from this program. This report, which had production type as
      *           one of the sort fields, no longer sorts/breaks on production type.
      *
      * 06/12/01  LeAnne Fedor
      *           Business office was removed from sort sequence.
      *           Business office was added as a column on the report.
      *           Both Manufacturing Amount and Medication Amount columns were removed.
      *
      * 08/13/01  LeAnne Fedor
      *           Added new optional selection on 'transaction code'.
      *
      * 09/13/01  LeAnne Fedor
      *           Changed logic to use a new workfile as the primary file. This allows
      *           us to run the existing report sorted by Production Phase as well as
      *           the new report sorted by Production Manager.
      *
      * 04/20/05  LeAnne Fedor
      *           Added logic for new user selections:
      *               Company relationship
      *               Feed delivery zone
      *               Produced flag
      *
      * 04/23/09  LeAnne Ramsey
      *           Recompile only. Costed Time/User added to Feed Ticket Header.
      *
      * 05/12/09  LeAnne Ramsey
      *           Recompile only. PickUp Ticket Type added to Feed Ticket Header.
      *
      * 07/06/09  LeAnne Ramsey
      *           Recompile only. Added new field 'Continuous Flow Flag' to Hog Group file.
      *
      * 09/21/09  LeAnne Ramsey
      *           Recompile only. Added new field 'Upload Date' to Feed Ticket Header.
      *
      * 10/16/13  LeAnne Ramsey (E2831)
      *           Recompile only. Added field 'MTech Reference'.
      *
      * 03/26/20  Danny Nguyen - P405 - Farm Number Increase
      *           Recompile only. Increased Bin Code (@@BNCD) in HSPREF file from 5A to 6A.
      *           File change on HSP338.
      *
      * 05/10/22 Eric L SDN736 Recompiled ticket nbr increase (TKNO & RTNO 7.0 TO 9.0)
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     FHSP018    IF   E           K DISK
      *    FARM SITE
      *
      *
     FHSL034D   IF   E           K DISK
      *    HOG GROUP
      *
      *
     FHSP037    IF   E           K DISK
      *    FEED TICKET HEADER
      *
      *
     FHSP338    IF   E           K DISK
      *    Workfile: Feed ticket detail
      *
      *
     FQPRINT    O    F  198        PRINTER OFLIND(*INOF)
     F                                     INFDS(DS#PRT)
      /EJECT
      ****************************************************************
      * Definition Specifications
      ****************************************************************
      *
      *  PRINT ARRAYS FOR TOTALS
      *
     D LBS             S             12  0 DIM(5)
     D ING             S             13  2 DIM(5)
     D AMT             S             13  2 DIM(5)
      *
      *---------------------------------------------------------------
      * STANDARD PROGRAM STATUS DATA STRUCTURE
      *---------------------------------------------------------------
      *    EXTERNALLY DEFINED AS UTPGFR (RECORD FORMAT: PGMDSR)
     D PGMDS         ESDS                  EXTNAME(UTPGFR)
      *
      *---------------------------------------------------------------
      *  DATE FORMATS
      *---------------------------------------------------------------
      *
      * DATE IN MMDDYY FORMAT
      *
     D                 DS
     D  WKMDY                  1      6  0
     D  WKMM                   1      2  0
     D  WKDD                   3      4  0
     D  WKYY                   5      6  0
      *
      * DATE IN CCYYMMDD FORMAT
      *
     D                 DS
     D  WKCYMD                 1      8  0
     D  WKCEN                  1      2  0
     D  WKYER                  3      4  0
     D  WKMON                  5      6  0
     D  WKDAY                  7      8  0
      *
      *---------------------------------------------------------------
      *  NAMED CONSTANTS
      *---------------------------------------------------------------
      *
     D YES             C                   CONST('Y')
     D NO              C                   CONST('N')
      *
      *----------------------------------------------------------------
      *  DS#PRT: PRINTER CONTROL FIELDS...
      *----------------------------------------------------------------
     D DS#PRT          DS
     D  OVFLIN               188    189B 0
     D  CURLIN               367    368B 0
      *
      *----------------------------------------------------------------
      * LDA - REPORT SELECTIONS
      *----------------------------------------------------------------
      *
     D                UDS
     D  LDFMBO                 1      5
     D  LDFYMD                 6     13  0
     D  LDTYMD                14     21  0
     D  LDPMGCD               22     26
     D  LDPPCD                27     31
     D  LDFSCD                32     36  0
     D  LDRNCD                37     41
     D  LDHGSN                42     48  0
     D  LDTRCD                49     49
     D  LDBODS                50     79
     D  LDRPFL                80     80
     D  LDPPDS                90    119
     D  LDFSNM               120    144
     D  LDRNDS               145    164
     D  LDFMDY               165    170  0
     D  LDTMDY               171    176  0
      *
     D  LDPBFL               178    178
     D  LDCELL               179    180  0
     D  LDTRDS               181    190
     D  LDPMGNM              191    215
     D  LDRPDS               216    236
      *
     D  LDPRFL               237    237
     D  LDCRCD               238    242
     D  LDCRDS               243    252
     D  LDDZCD               253    257
     D  LDDZDS               258    277
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * MAINLINE
      ****************************************************************
      *
      * READ AND PRINT ALL FEED TICKET DETAIL RECORDS FROM THE WORKFILE
      *
     C     *IN90         DOUEQ     *ON                                          MAIN DO
     C                   READ      hsp338                                 90
     C     *IN90         IFEQ      *OFF                                         IF NOT EOF
      *
      *
     C     FIRST         IFEQ      YES                                          IF FIRST
     C                   EXSR      $OFLOW
      *
     C                   select
     C                   when      ldrpfl = 'P'
     C                   movel     wdppcd        svsort
     C                   when      ldrpfl = 'M'
     C                   movel     wdpmgcd       svsort
     C                   endsl
      *
     C                   Z-ADD     WDFSCD        SVFSCD
     C                   MOVEL     WDRNCD        SVRNCD
     C                   MOVEL     NO            FIRST
     C                   ENDIF                                                  IF FIRST
      *
      * CHECK FOR CONTROL BREAKS
      *
     C                   SELECT
     C                   when      (ldrpfl = 'P' and wdppcd <> svsort) or
     C                             (ldrpfl = 'M' and wdpmgcd <> svsort)
     C                   EXSR      $sortbrk
      *
     C     WDFSCD        WHENNE    SVFSCD                                       FARM
     C                   EXSR      $FSBRK
      *
     C     WDRNCD        WHENNE    SVRNCD                                       RATION
     C                   EXSR      $RABRK
     C                   ENDSL
      *
      * IF THERE IS A CHANGE IN THE ADDITIVE CODE,
      *       SETON INDICATOR TO PRINT FIELD ON DETAIL
      *
     C     WDADCD        IFNE      SVADCD
     C                   SETON                                        87
     C                   MOVEL     WDADCD        SVADCD
     C                   ENDIF
      *
      * FLIP TICKET DATE TO MDY
      *
     C                   Z-ADD     WDTKDT        WKCYMD
     C                   EXSR      $TOMDY
     C                   Z-ADD     WKMDY         DTTKDT
      *
      *
      * RETRIEVE FEED TICKET HEADER INFO
      *
     C     key01         CHAIN     HSP037                             98
     C     *IN98         IFEQ      *OFF                                         IF FOUND
     C                   MOVEL     FHTRCD        DTTRCD
     C                   MOVEL     FHPRFL        DTPRFL
     C                   MOVEL     FHCSFL        DTCSFL
     C                   MOVEL     FHTSCD        DTTSCD
     C                   ELSE
     C                   MOVE      *BLANK        DTTRCD
     C                   MOVE      *BLANK        DTPRFL
     C                   MOVE      *BLANK        DTCSFL
     C                   MOVE      *BLANK        DTTSCD
     C                   ENDIF                                                  IF FOUND
      *
      * SET INDICATOR TO PRINT REFERENCE TICKET NUMBER IF
      * THIS TICKET IS A PICKUP OR ADJUSTMENT TICKET
      *
     C     DTTRCD        IFEQ      'A'                                          IF A OR P
     C     DTTRCD        OREQ      'P'
     C                   SETON                                        85
     C                   ELSE
     C                   SETOFF                                       85
     C                   ENDIF                                                  IF A OR P
      *
      * ACCUMULATE FEED DOLLARS FOR DETAIL LINE
      *
     C     WDINAM        ADD       WDMFAM        DTTFAM
     C                   ADD       WDMDAM        DTTFAM
      *
      *
      * CALCULATE FEED COST PER TON
      *
     C     WDFDLB        DIV       2000          WKF152
     C     WKF152        IFNE      0
     C     DTTFAM        DIV(H)    WKF152        DTTNRT
     C                   ENDIF
      *
      *
      * ACCUMULATE TOTALS
     C                   ADD       WDFDLB        LBS
     C                   ADD       WDINAM        ING
     C                   ADD       DTTFAM        AMT
      *
     C     *INOF         IFEQ      *ON
     C                   EXSR      $OFLOW
     C                   ENDIF
      *
      * PRINT DETAIL AND SETOFF PRINT CONTROL INDICATORS
      *
     C                   EXCEPT    DTLINE
     C                   SETOFF                                       878889
      *
     C                   ENDIF                                                  IF NOT EOF
     C                   ENDDO                                                  MAIN DO
      *
      *----------------------------------------------------------------
      * EOF PROCESSING
      *----------------------------------------------------------------
      *  IF NO DETAIL RECORDS MATCHED THE SELECTION CRITERIA, THEN
      *  PRINT THE STANDARD 'NO DATA...' ERROR MESSAGE
      *
     C     FIRST         IFEQ      YES                                          IF NO RECS
     C                   EXCEPT    HRHDR
     C                   EXCEPT    NODATA
     C                   ELSE
      *
      *  CHECK CURRENT LINE FOR ROOM TO PRINT ALL TOTALS
      *
     C                   ADD       5             CURLIN
     C     CURLIN        IFGT      OVFLIN
     C                   SETON                                        OF
     C                   ENDIF
      *
     C     *INOF         IFEQ      *ON
     C                   EXSR      $OFLOW
     C                   ELSE
     C                   EXCEPT    BLANK
     C                   ENDIF
      *
     C                   EXSR      $RATOT
     C                   EXSR      $FSTOT
     C                   EXSR      $SORTTOT
     C                   EXSR      $RTTOT
     C                   ENDIF                                                  IF NO RECS
      *
     C                   SETON                                        LR
      /EJECT
      *---------------------------------------------------------------
      * $TOMDY   FLIP DATE FROM YYMMDD TO MMDDYY
      *---------------------------------------------------------------
      *
     C     $TOMDY        BEGSR
      *
     C                   Z-ADD     WKMON         WKMM
     C                   Z-ADD     WKDAY         WKDD
     C                   Z-ADD     WKYER         WKYY
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * $OFLOW - OVERFLOW SUBROUTINE
      *---------------------------------------------------------------
      *
     C     $OFLOW        BEGSR
      *
     C                   EXCEPT    HRHDR
     C                   EXCEPT    H1HDR
     C                   SETON                                        878889
      *
     C                   ENDSR
      /EJECT
      *****************************************************************
      * $RABRK - CONTROL BREAK IN RATION CODE
      *****************************************************************
      *
     C     $RABRK        BEGSR
      *
      *  CHECK CURRENT LINE FOR ROOM TO PRINT ALL TOTALS
      *
     C                   ADD       1             CURLIN
     C     CURLIN        IFGT      OVFLIN
     C                   SETON                                        OF
     C                   ENDIF
      *
     C     *INOF         IFEQ      *ON
     C                   EXSR      $OFLOW
     C                   ENDIF
      *
     C                   EXSR      $RATOT
     C                   EXCEPT    BLANK
      *
      * SET INDICATORS TO CONTROL PRINTING
      *
     C                   SETON                                        8788
      *
     C                   ENDSR
      /EJECT
      *****************************************************************
      * $FSBRK - CONTROL BREAK IN FARM SITE CODE
      *****************************************************************
      *
     C     $FSBRK        BEGSR
      *
      *  CHECK CURRENT LINE FOR ROOM TO PRINT ALL TOTALS
      *
     C                   ADD       2             CURLIN
     C     CURLIN        IFGT      OVFLIN
     C                   SETON                                        OF
     C                   ENDIF
      *
     C     *INOF         IFEQ      *ON
     C                   EXSR      $OFLOW
     C                   ENDIF
      *
     C                   EXSR      $RATOT
     C                   EXSR      $FSTOT
      *
      * IF THE USER IS NOT PRINTING ONE FARM PER PAGE, THEN
      * PRINT A BLANK LINE.
      *
     C     LDPBFL        IFEQ      NO
     C                   EXCEPT    BLANK
     C                   ENDIF
      *
      * SET INDICATORS TO CONTROL PRINTING OF DETAIL FIELDS
      *
     C                   SETON                                        878889
      *
     C                   ENDSR
      /EJECT
      *****************************************************************
      * CONTROL BREAK IN PRIMARY SORT VALUE----EITHER PHASE OR MANAGER
      *****************************************************************
      *
     C     $sortbrk      BEGSR
      *
      *  CHECK CURRENT LINE FOR ROOM TO PRINT ALL TOTALS
      *
     C                   ADD       3             CURLIN
     C     CURLIN        IFGT      OVFLIN
     C                   SETON                                        OF
     C                   ENDIF
      *
     C     *INOF         IFEQ      *ON
     C                   EXSR      $OFLOW
     C                   ENDIF
      *
     C                   EXSR      $RATOT
     C                   EXSR      $FSTOT
     C                   EXSR      $SORTTOT
      *
      * Print report headings
      *
     C                   EXSR      $OFLOW
      *
     C                   ENDSR
      /EJECT
      *****************************************************************
      * Totals for 'sort' field---either phase or manager
      *****************************************************************
      *
     C     $SORTTOT      BEGSR
      *
      * CALCULATE FEED COST PER TON
      *
     C     LBS(3)        DIV       2000          WKF152
     C     WKF152        IFNE      0
     C     AMT(3)        DIV(H)    WKF152        DTTNRT
     C                   ENDIF
      *
     C     *INOF         IFEQ      *ON
     C                   EXSR      $OFLOW
     C                   ENDIF
      *
     C                   EXCEPT    TSTOT
      *
     C                   select
     C                   when      ldrpfl = 'P'
     C                   movel     wdppcd        svsort
     C                   when      ldrpfl = 'M'
     C                   movel     wdpmgcd       svsort
     C                   endsl
      *
     C                   ENDSR
      *
      *****************************************************************
      * $FSTOT - FARM SITE TOTALS
      *****************************************************************
      *
     C     $FSTOT        BEGSR
      *
      *
      * CALCULATE FEED COST PER TON
      *
     C     LBS(2)        DIV       2000          WKF152
     C     WKF152        IFNE      0
     C     AMT(2)        DIV(H)    WKF152        DTTNRT
     C                   ENDIF
      *
     C                   EXCEPT    TFTOT
     C                   Z-ADD     WDFSCD        SVFSCD
      *
      *
      * IF THE USER IS PRINTING ONE FARM PER PAGE, SET ON
      * OVERFLOW TO FORCE A PAGE BREAK.
      *
     C     LDPBFL        IFEQ      YES
     C                   SETON                                        OF
     C                   ENDIF
      *
     C                   ENDSR
      *
      *****************************************************************
      * $RATOT - RATION TOTALS
      *****************************************************************
      *
     C     $RATOT        BEGSR
      *
      * CALCULATE FEED COST PER TON
      *
     C     LBS(1)        DIV       2000          WKF152
     C     WKF152        IFNE      0
     C     AMT(1)        DIV(H)    WKF152        DTTNRT
     C                   ENDIF
      *
     C                   EXCEPT    RATOT
     C                   MOVEL     WDRNCD        SVRNCD
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * $RTTOT - REPORT TOTALS
      *---------------------------------------------------------------
      *
     C     $RTTOT        BEGSR
      *
      * CALCULATE FEED COST PER TON
      *
     C     LBS(5)        DIV       2000          WKF152
     C     WKF152        IFNE      0
     C     AMT(5)        DIV(H)    WKF152        DTTNRT
     C                   ENDIF
      *
     C                   EXCEPT    TRTOT
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * INITIALIZATION SUBROUTINE
      *---------------------------------------------------------------
      *
     C     *INZSR        BEGSR
      *
      *  KEY FOR FEED TICKET HEADER FILE
      *
     C     key01         KLIST
     C                   KFLD                    WDFMBO
     C                   KFLD                    WDTKNO
     C                   KFLD                    WDTRCD
      *
      *
      * WORKFIELDS
     C                   Z-ADD     0             WKF152           15 2
      *
      *
      * DEFINE PRINT FIELDS FOR THE REPORT HEADING
      *
     C     *LIKE         DEFINE    WDPMGCD       HRPMGCD
     C     *LIKE         DEFINE    LDPPCD        HRPPCD
     C     *LIKE         DEFINE    WDRNCD        HRRNCD
     C     *LIKE         DEFINE    HGHGCD        HRHGCD
     C     *LIKE         DEFINE    HGBLCD        HRBLCD
     C     *LIKE         DEFINE    HGRMCD        HRRMCD
     C                   MOVE      *BLANK        HRTRDS           10
     C     *LIKE         DEFINE    FSDZCD        HRDZCD
     C     *LIKE         DEFINE    FSCRCD        HRCRCD
     C                   MOVE      *BLANK        HRPRFL            3
      *
      *
      * DEFINE PRINT FIELDS FOR DETAIL LINE
      *
     C                   Z-ADD     0             DTTKDT            6 0
     C     *LIKE         DEFINE    FHTRCD        DTTRCD
     C     *LIKE         DEFINE    FHPRFL        DTPRFL
     C     *LIKE         DEFINE    FHCSFL        DTCSFL
     C     *LIKE         DEFINE    FHTSCD        DTTSCD
     C     *LIKE         DEFINE    WDINAM        DTTFAM
     C                   Z-ADD     0             DTTNRT            5 2
      *
      *
      * DEFINE SAVE FIELDS FOR CONTROL BREAKS
      *
     C     *LIKE         DEFINE    WDPPCD        SVSORT
     C     *LIKE         DEFINE    WDFSCD        SVFSCD
     C     *LIKE         DEFINE    WDRNCD        SVRNCD
     C     *LIKE         DEFINE    WDADCD        SVADCD
      *
      *
      * SET UP REPORT HEADINGS
      *
      * BUSINESS OFFICE
      *
     C     LDFMBO        IFEQ      *BLANK
     C                   MOVEL     'All'         LDFMBO
     C                   ENDIF
      *
      *
      * Feed delivery zone
      *
     C                   if        lddzcd = *blank
     C                   movel     'All'         hrdzcd
     C                   else
     C                   movel     lddzcd        hrdzcd
     C                   endif
      *
      *
      * Company relationship
      *
     C                   if        ldcrcd = *blank
     C                   movel     'All'         hrcrcd
     C                   else
     C                   movel     ldcrcd        hrcrcd
     C                   endif
      *
      *
      * PRODUCTION PHASE
      *
     C     LDPPCD        IFEQ      *BLANK
     C                   MOVEL     'All'         HRPPCD
     C                   ELSE
     C                   MOVEL     LDPPCD        HRPPCD
     C                   ENDIF
      *
      * Page break on farm
      *
     C     LDPBFL        IFEQ      YES
     C                   MOVEL     'Yes'         HRPBFL            3
     C                   ELSE
     C                   MOVEL     'No '         HRPBFL
     C                   ENDIF
      *
      *
      * PRODUCTION MANAGER
      *
     C     LDPMGCD       IFEQ      *BLANK
     C                   MOVEL     'All'         HRPMGCD
     C                   ELSE
     C                   MOVEL     LDPMGCD       HRPMGCD
     C                   ENDIF
      *
      *  Cell
      *
     C     ldcell        IFEQ      0
     C                   SETON                                        82
     C                   ENDIF
      *
      *
      *  FARM SITE
      *
     C     LDFSCD        IFEQ      0
     C                   SETON                                        81
     C                   ENDIF
      *
      *
      * Produced flag
     C                   select
     C                   when      ldprfl = *blank
     C                   movel     'All'         hrprfl
      *
     C                   when      ldprfl = yes
     C                   movel     'Yes'         hrprfl
      *
     C                   when      ldprfl = no
     C                   movel     'No '         hrprfl
     C                   endsl
      *
      *
      * DETERMINE IF USER SELECTED SPECIFIC TRANSACTION CODE OR ALL
      *
     C     LDTRCD        IFEQ      *BLANK
     C                   MOVEL     'All'         HRTRDS
     C                   ELSE
     C                   MOVEL     LDTRDS        HRTRDS
     C                   ENDIF
      *
      * DETERMINE IF USER SELECTED SPECIFIC RATION OR "ALL"
      *
     C     LDRNCD        IFEQ      *BLANK
     C                   MOVEL     'All'         HRRNCD
     C                   ELSE
     C                   MOVEL     LDRNCD        HRRNCD
     C                   ENDIF
      *
      * DETERMINE IF USER SELECTED SPECIFIC HOG GROUP OR "ALL"
      *
     C     LDHGSN        IFEQ      0                                            IF ALL GROUP
     C                   MOVEL     'All'         HRHGCD
     C                   ELSE
     C     LDHGSN        CHAIN     HSL034D                            97
     C     *IN97         IFEQ      *OFF                                         IF FOUND
     C                   MOVEL     HGHGCD        HRHGCD
     C                   MOVEL     HGBLCD        HRBLCD
     C                   MOVEL     HGRMCD        HRRMCD
     C                   SETON                                        96
     C                   ELSE
     C                   eval      hrhgcd = 'Unknown'
     C                   MOVE      *BLANK        HRBLCD
     C                   MOVE      *BLANK        HRRMCD
     C                   ENDIF                                                  IF FOUND
     C                   ENDIF                                                  IF ALL GROUP
      *
      *
     C                   select
     C                   when      ldrpfl = 'P'
     C                   seton                                        83
     C                   when      ldrpfl = 'M'
     C                   seton                                        84
     C                   endsl
      *
      * SET UP HEADING UNDERLINE FIELD FOR PRINT
      * RETRIEVE TIME FOR REPORT HEADING
      * SET UP A FIRST TIME FLAG
      *
     C                   MOVEL     *ALL'-'       DASH            198
     C                   TIME                    RTIME             6 0
     C                   MOVE      YES           FIRST             1
      *
     C                   ENDSR
      /EJECT
      *-------------------------------------------------------------
      * REPORT HEADING LINES
      *-------------------------------------------------------------
      *
     OQPRINT    E            HRHDR          1 01
     O                       SDPGM               10
     O                                          107 'HOG PRODUCTION SYSTEM'
     O                                          189 'DATE'
     O                       UDATE         Y    198
      *
     O          E            HRHDR          1
     O                       SDUSR               10
     O                                          108 'FEED TICKET DETAIL REPOR'
     O                                          109 'T'
     O                                          189 'TIME'
     O                       RTIME              198 '  :  :  '
      *
     O          E            HRHDR          1
     O                       ldrpds             108
     O                                          189 'PAGE'
     O                       PAGE          Z    198
      *
     O          E            HRHDR          1
     O                                           22 'From date ............'
     O                                           26 '...:'
     O                       LDFMDY        Y     35
      *
     O          E            HRHDR          1
     O                                           22 'To date ..............'
     O                                           26 '...:'
     O                       LDTMDY        Y     35
      *
     O          E            HRHDR          1
     O                                           22 'Feed mill business off'
     O                                           26 'ice:'
     O                       LDFMBO              33
     O                       LDBODS              66
      *
      *
     O          E            HRHDR          1
     O                                           22 'Production phase .....'
     O                                           26 '...:'
     O                       HRPPCD              33
     O                       LDPPDS              66
      *
      *
     O          E            HRHDR          1
     O                                           22 'Production manager ...'
     O                                           26 '...:'
     O                       HRPMGCD             33
     O                       LDPMGNM             61
      *
     O          E            HRHDR          1
     O                                           22 'Cell .................'
     O                                           26 '...:'
     O              N82      LDCELL        M     34
     O               82                          31 'All'
      *
     O          E            HRHDR          1
     O                                           22 'Farm site ............'
     O                                           26 '...:'
     O              N81      LDFSCD        M     34
     O               81                          31 'All'
     O              N81      LDFSNM              61
      *
      *
     O          e            hrhdr          1
     O                                           22 'Company relationship .'
     O                                           26 '...:'
     O                       hrcrcd              33
     O                       ldcrds              46
      *
      *
     O          e            hrhdr          1
     O                                           22 'Feed delivery zone ...'
     O                                           26 '...:'
     O                       hrdzcd              33
     O                       lddzds              56
      *
      *
     O          E            HRHDR          1
     O                                           22 'Page break on farm ...'
     O                                           26 '...:'
     O                       HRPBFL              31
      *
      *
     O          E            HRHDR          1
     O                                           22 'Ration code ..........'
     O                                           26 '...:'
     O                       HRRNCD              33
      *
      *
     O          e            hrhdr          1
     O                                           22 'Produced flag ........'
     O                                           26 '...:'
     O                       hrprfl              31
      *
      *
     O          E            HRHDR          1
     O                                           23 'Hog group/building/room'
     O                                           26 '.:'
     O                       HRHGCD              35
     O               96                          37 '/'
     O                       HRBLCD              43
     O               96                          45 '/'
     O                       HRRMCD              51
      *
      *
     O          E            HRHDR          2
     O                                           22 'Transaction code .....'
     O                                           26 '...:'
     O                       HRTRDS              38
      *
      *-------------------------------------------------------------
      * COLUMN HEADING LINES
      *-------------------------------------------------------------
      *
     O          E            HRHDR          1
     O                                            5 'FARM'
     O                                           13 'RATION/'
     O                                           29 'TR'
     O                                           35 'PROD'
     O                                           41 'COST'
     O                                           53 'TICKET'
     O                                           60 'BIN'
     O                                           88 'TICKET'
     O                                          110 'INGREDIENT'
     O                                          119 'COST'
     O                                          138 'TOTAL FEED'
     O                                          151 'VENDOR'
     O                                          166 'REFER'
     O                                          182 'FEED MILL'
      *
     O          E            HRHDR          1
     O                                            5 'SITE'
     O                                           14 'ADDITIVE'
     O                                           25 'TICKET#'
     O                                           29 'CD'
     O                                           34 'Y/N'
     O                                           40 'Y/N'
     O                                           44 'ST'
     O                                           52 'DATE'
     O                                           61 'CODE'
     O                                           69 'GROUP'
     O                                           88 'POUNDS'
     O                                          108 'AMOUNT'
     O                                          119 'PER TON'
     O                                          136 'AMOUNT'
     O                                          153 'INVOICE#'
     O                                          166 'TICKET#'
     O                                          185 'BUSINESS OFFICE'
      *
     O          E            HRHDR          1
     O                       DASH               198
      *
      *-------------------------------------------------------------
      * Sort Heading---either phase or manager
      *-------------------------------------------------------------
      *
     O          E            H1HDR       1  2
     O               83      WDPPDS              30
     O               84      WDPMGNM             25
      *
      *-------------------------------------------------------------
      * FEED TICKET DETAIL LINE
      *-------------------------------------------------------------
      *
     O          E            DTLINE         1
     O               89      WDFSCD        M      6
     O               88      WDRNCD              11
     O               87      WDADCD              17
     O                       WDTKNO        Z     25
     O                       DTTRCD              29
     O                       DTPRFL              34
     O                       DTCSFL              40
     O                       DTTSCD              44
     O                       DTTKDT        Y     54
     O                       WDBNCD              62
     O                       WDHGCD              71
     O                       WDFDLB        K     89
     O                       WDINAM        K    111
     O                       DTTNRT        KB   120
     O                       DTTFAM        K    139
     O                       FHVINO             155
     O               85      WDRTNO        Z    166
     O                       WDFMBO             180
      *
      *-------------------------------------------------------------
      * RATION CODE TOTAL LINE
      *-------------------------------------------------------------
      *
     O          E            RATOT       1  1
     O                                           66 '*'
     O                       SVRNCD              72
     O                       LBS(1)        KB    89
     O                       ING(1)        KB   111
     O                       DTTNRT        KB   120
     O                       AMT(1)        KB   139
     O                                          198 '*'
      *
      *-------------------------------------------------------------
      * FARM SITE TOTAL LINE
      *-------------------------------------------------------------
      *
     O          E            TFTOT          1
     O                                           66 '**'
     O                       SVFSCD        M     73
     O                       LBS(2)        KB    89
     O                       ING(2)        KB   111
     O                       DTTNRT        KB   120
     O                       AMT(2)        KB   139
     O                                          198 '**'
      *
      *-------------------------------------------------------------
      * TOTAL LINE FOR PRIMARY SORT FIELD---EITHER PHASE OR MANAGER
      *-------------------------------------------------------------
      *
     O          E            TSTOT          1
     O                                           66 '***'
     O                       SVSORT              72
     O                       LBS(3)        KB    89
     O                       ING(3)        KB   111
     O                       DTTNRT        KB   120
     O                       AMT(3)        KB   139
     O                                          198 '***'
      *
      *-------------------------------------------------------------
      * REPORT TOTALS
      *-------------------------------------------------------------
      *
     O          E            TRTOT          1
     O                                           57 'REPORT TOTAL'
     O                                           66 '******'
     O                       LBS(5)        KB    89
     O                       ING(5)        KB   111
     O                       DTTNRT        KB   120
     O                       AMT(5)        KB   139
     O                                          198 '******'
      *
      *-------------------------------------------------------------
      * PRINT A BLANK LINE
      *-------------------------------------------------------------
      *
     O          E            BLANK          1
      *
      *-------------------------------------------------------------
      * NO DATA MESSAGE LINE
      *-------------------------------------------------------------
      *
     O          E            NODATA         1
     O                                           19 'NO DATA MEETS YOUR'
     O                                           38 'SELECTION CRITERIA'
