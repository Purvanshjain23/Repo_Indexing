      *****************  RPG PROGRAM HEADING  ************************
     h option(*SRCSTMT:*NODEBUGIO)
      ****************************************************************
      *
      * SYSTEM:      Triumph Foods
      * PROGRAM:     TF616
      * TITLE:       Margin: Volume Adjustment Report
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     01/19/05
      *
      * FUNCTION:  Batch program to print the Volume Adjustment report.
      *
      *            A Volume Adjustment is only applicable to records with a
      *            TF Class Group Code of CV=Converted.
      *
      *            This 'report' is generated:
      *               1) On-demand
      *               2) From the Margin Adjustment Close function
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 02/15/07  LeAnne Ramsey
      *           Changed layout to show all "cost" fields.
      *
      * 03/21/07  LeAnne Ramsey
      *           Changed layout again. The users want to print an NVA section
      *           at the top--after the CV section. This new section will print
      *           from our new Volume Summary Extension file---created to handle
      *           the NVAs.
      *           Also, in the "remaining line" section, we:
      *            1) removed some lines
      *            2) repositioned some lines
      *            3) changed the final Margin per/Lb to use a combined CV and NVA
      *               value.
      *
      * 05/02/07  LeAnne Ramsey
      *           Recompile only. Synon file PPAMREP was changed.
      *
      * 11/21/08  LeAnne Ramsey
      *           As part of synchronizing the LDAs between the TFS Margin Adjustment Close
      *           and the Meat Costing, we changed the LDA positions.
     ‚* 09/05/24  Jagdish Mistry (JM-P310149),Service request number-3014062
     ‚*                          Consider NVZ in Header PRODUCED Pounds of
     ‚*                          NVA Products.
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Ftfp318    if   e           k disk
      *    Volume summary
      *    (records selected with open query)
      *
      *
     Ftfp331    if   e           k disk
      *    Volume summary extension
      *
      *
     Fome8rel0  if   e           k disk
      *    Product types
      *
      *
     Fppamrel1  if   e           k disk
      *    TF Classification
      *
      *
     ‚* Project-JM-P310149-START
     FTFL331A   IF   E           K Disk    Rename(ESREC:R331A)
     F                                     Prefix(A1:2)
     ‚* Project-JM-P310149-END
     Fprint198  o    f  198        printer oflind(*inof)
      *
      /EJECT
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Standard fields
      *
     d rtime           s              6  0
      *
      *
      * Control fields
      *
     D gotdata         s              1    inz('N')
      *
      *
      * Work fields
      *
     D wktfclcd        s                   like(vstfclcd) inz('NVA')
     D wkwkpe          s                   like(eswkpe)
      *
      *
      * Print fields
      *
     D dash            s            198    inz(*all'-')
     D dash2           s             26    inz(*all'-')
      *
     d h1demand        s             35
     D h1istyds        s             30
     D h1tfclds        s             30
      *
     D l1desc          s              8
     D l1puypc         s                   like(vsspuypc)
     D l1pumco         s                   like(vsspumco)
     D l1pulco         s                   like(vsspulco)
     D l1pukco         s                   like(vsspukco)
     D l1puico         s                   like(vsspuico)
     D l1puoco         s                   like(vsspuoco)
     D l1pupco         s                   like(vsspupco)
     D l1pupr          s                   like(vsspupr)
     D l1pu1mg         s                   like(vsspu1mg)
     D l1pumgam        s                   like(vsspumgam)
     D l1pulb          s                   like(vsspulb)
      *
     D l1pu2mg         s                   like(vsspu2mg)
     D l1cpuslb        s                   like(vsscpuslb)
     ‚* Project-JM-P310149-START
     ‚* Used to validate if NVZ is present
     D wktfclcdNVZ     s              3    inz('NVZ')
     ‚* Project-JM-P310149-END
      *
      ****************************************************************
      * ARRAYS AND TABLES
      ****************************************************************
      *
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *
      *
      *---------------------------------------------------------------
      * Local data area.
      *---------------------------------------------------------------
      *
     Dlda             uds                  dtaara(*lda)
     D  ldpfcd                 1      1
     D  ldyr                   2      5  0
     D  ldwk                   6      7  0
      *
     D  ldwemdy               44     49  0
      *
     D  ldpe                  51     52  0
     D  ldpbmdy               61     66  0
     D  ldpemdy               75     80  0
      *
     D  ldwpfl               134    134
      *
     D  ldtfcgcd             139    140
     D  ldtfcgds             141    155
      *
     D  ldtfclcd             156    158
     D  ldtfclds             159    188
      *
     D  ldistycd             189    191  0
     D  ldistyds             192    221
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ***************************************************************************************
      * MAINLINE
      ***************************************************************************************
      *
      * A Volume Adjustment is only applicable to records with a TF Class Group Code of
      * CV=Converted. Only "CV" records are written to the Volume Summary file.
      *
     C     *loval        setll     tfp318
      *
     C                   dou       *in90 = *on                                  Main do
     C                   read      tfp318                                 90
     C                   if        *in90 = *off                                 If not EOF
      *
     C                   move      yes           gotdata
      *
      *
      * Retrieve description for TF Classification Code
      *
     C     vstfclcd      chain     ppamrel1                           92
     C                   if        *in92 = *off
     C                   eval      h1tfclds = amq0na
     C                   else
     C                   move      *blank        h1tfclds
     C                   endif
      *
      * Retrieve description for Item Structure Type Code
      *
     C     vsistycd      chain     ome8rel0                           92
     C                   if        *in92 = *off
     C                   eval      h1istyds = e8dctx
     C                   else
     C                   move      *blank        h1istyds
     C                   endif
      *
      * Print headings
      *  1) Report
      *  2) Column
     C                   exsr      $h1hdr
      *
      * Print 3 detail lines in the Pounds Section for the Item Structure Type.
      *   Seaboard
      *   Triumph
      *   Total
     C                   exsr      $h2hdr
     C                   exsr      $sbd1
     C                   exsr      $tf1
     C                   exsr      $agg1
      *
      * If you have a NAV record in the Volume Summary Extension file for this
      * Item Structure Type/Date, print 3 detail lines in the Pounds Section for
      * the NVA associated with the Item Structure Type.
      *   Seaboard
      *   Triumph
      *   Total
      *
     C     key01         chain     tfp331                             92
     C                   if        *in92 = *off
     C                   except    blank
     C                   seton                                        87
     ‚* Project-JM-P310149-START
     C                   ExSr      $getNVZforHead
     C***                exsr      $h2hdr
     ‚* Project-JM-P310149-END
     C                   setoff                                       87
     C                   exsr      $sbd2
     C                   exsr      $tf2
     C                   exsr      $agg2
     C                   endif
      *
      *
      * Print all the remaining lines.
      *
     C                   except    blank
     C                   except    l3dtl
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Main do
      *
      *---------------------------------------------------------------
      * EOF processing
      *---------------------------------------------------------------
      * If the file was empty, print the standard 'no data...' message
      *
     C                   if        gotdata = no
     C                   except    nodata
     C                   endif
      *
     C                   seton                                        lr
      /EJECT
      *----------------------------------------------------------------------------------------
      * Set up/print the Seaboard detail line in the Pounds Section for the Item Structure Type
      *----------------------------------------------------------------------------------------
      *
     C     $sbd1         begsr
      *
     C                   eval      l1desc = 'Seaboard'
      *
      * Produced pounds
     c                   z-add     vsspuypc      l1puypc
     c                   z-add     vsspumco      l1pumco
     c                   z-add     vsspulco      l1pulco
     c                   z-add     vsspukco      l1pukco
     c                   z-add     vsspuico      l1puico
     c                   z-add     vsspuoco      l1puoco
     c                   z-add     vsspupco      l1pupco
     c                   z-add     vsspupr       l1pupr
     c                   z-add     vsspu1mg      l1pu1mg
     c                   z-add     vsspumgam     l1pumgam
     c                   z-add     vsspulb       l1pulb
      *
      * Starting pounds
     c                   z-add     vsspu2mg      l1pu2mg
     c                   z-add     vsscpuslb     l1cpuslb
      *
     C                   exsr      $l1dtl
      *
     C                   endsr
      /EJECT
      *-----------------------------------------------------------------------------------------
      * Set up/print the Triumph detail line in the Pounds Section for the Item Structure Type
      *-----------------------------------------------------------------------------------------
      *
     C     $tf1          begsr
      *
     C                   eval      l1desc = 'Triumph'
      *
      * Produced pounds
     C                   z-add     vstpuypc      l1puypc
     C                   z-add     vstpumco      l1pumco
     C                   z-add     vstpulco      l1pulco
     C                   z-add     vstpukco      l1pukco
     C                   z-add     vstpuico      l1puico
     C                   z-add     vstpuoco      l1puoco
     C                   z-add     vstpupco      l1pupco
     c                   z-add     vstpupr       l1pupr
     c                   z-add     vstpu1mg      l1pu1mg
     c                   z-add     vstpumgam     l1pumgam
     c                   z-add     vstpulb       l1pulb
      *
      * Starting pounds
     c                   z-add     vstpu2mg      l1pu2mg
     c                   z-add     vstcpuslb     l1cpuslb
      *
     C                   exsr      $l1dtl
     C                   except    underscore
      *
     C                   endsr
      /eject
      *-----------------------------------------------------------------------------------------
      * Set up/print the Aggregate detail line in the Pounds Section for the Item Structure Type
      *-----------------------------------------------------------------------------------------
      *
     C     $agg1         begsr
      *
     C                   eval      l1desc = 'TOTAL'
      * Produced pounds
      *
     C                   z-add     vsapuypc      l1puypc
     C                   z-add     vsapumco      l1pumco
     C                   z-add     vsapulco      l1pulco
     C                   z-add     vsapukco      l1pukco
     C                   z-add     vsapuico      l1puico
     C                   z-add     vsapuoco      l1puoco
     C                   z-add     vsapupco      l1pupco
     C                   z-add     vsapupr       l1pupr
     C                   z-add     vsapu1mg      l1pu1mg
     C                   z-add     vsapumgam     l1pumgam
     C                   z-add     vsapulb       l1pulb
      *
      * Starting pounds
     c                   z-add     vsapu2mg      l1pu2mg
     c                   z-add     vsacpuslb     l1cpuslb
      *
     C                   exsr      $l1dtl
      *
     C                   endsr
      /EJECT
      *----------------------------------------------------------------------------------------
      * Set up/print the Seaboard detail line in the Pounds Section for the associated NVA
      *----------------------------------------------------------------------------------------
      *
     C     $sbd2         begsr
      *
     C                   eval      l1desc = 'Seaboard'
      *
      * Produced pounds
     c                   z-add     esspuypc      l1puypc
     c                   z-add     esspumco      l1pumco
     c                   z-add     esspulco      l1pulco
     c                   z-add     esspukco      l1pukco
     c                   z-add     esspuico      l1puico
     c                   z-add     esspuoco      l1puoco
     c                   z-add     esspupco      l1pupco
     c                   z-add     esspupr       l1pupr
     c                   z-add     esspu1mg      l1pu1mg
     c                   z-add     esspumgam     l1pumgam
     c                   z-add     esspulb       l1pulb
      *
      * Starting pounds
     c                   z-add     esspu2mg      l1pu2mg
     c                   z-add     esspuslb      l1cpuslb
      *
     C                   exsr      $l1dtl
      *
     C                   endsr
      /EJECT
      *-----------------------------------------------------------------------------------------
      * Set up/print the Triumph detail line in the Pounds Section for the associated NVA
      *-----------------------------------------------------------------------------------------
      *
     C     $tf2          begsr
      *
     C                   eval      l1desc = 'Triumph'
      *
      * Produced pounds
     C                   z-add     estpuypc      l1puypc
     C                   z-add     estpumco      l1pumco
     C                   z-add     estpulco      l1pulco
     C                   z-add     estpukco      l1pukco
     C                   z-add     estpuico      l1puico
     C                   z-add     estpuoco      l1puoco
     C                   z-add     estpupco      l1pupco
     c                   z-add     estpupr       l1pupr
     c                   z-add     estpu1mg      l1pu1mg
     c                   z-add     estpumgam     l1pumgam
     c                   z-add     estpulb       l1pulb
      *
      * Starting pounds
     c                   z-add     estpu2mg      l1pu2mg
     c                   z-add     estpuslb      l1cpuslb
      *
     C                   exsr      $l1dtl
     C                   except    underscore
      *
     C                   endsr
      /EJECT
      *-----------------------------------------------------------------------------------------
      * Set up/print the Aggregate detail line in the Pounds Section for the associated NVA
      *-----------------------------------------------------------------------------------------
      *
     C     $agg2         begsr
      *
     C                   eval      l1desc = 'TOTAL'
      * Produced pounds
      *
     C                   z-add     esapuypc      l1puypc
     C                   z-add     esapumco      l1pumco
     C                   z-add     esapulco      l1pulco
     C                   z-add     esapukco      l1pukco
     C                   z-add     esapuico      l1puico
     C                   z-add     esapuoco      l1puoco
     C                   z-add     esapupco      l1pupco
     C                   z-add     esapupr       l1pupr
     C                   z-add     esapu1mg      l1pu1mg
     C                   z-add     esapumgam     l1pumgam
     C                   z-add     esapulb       l1pulb
      *
      * Starting pounds
     c                   z-add     esapu2mg      l1pu2mg
     c                   z-add     esapuslb      l1cpuslb
      *
     C                   exsr      $l1dtl
      *
     C                   endsr
     ‚* Project-JM-P310149-START
     C     $getNVZforHeadBegSr
     C     Key02         Setll     TFL331A
     C                   If        %Equal(TFL331A)
     C                   except    h3hdr
     C                   Else
     C                   ExSr      $h2hdr
     C                   EndIf
     C                   EndSr
     ‚* Project-JM-P310149-END
      /EJECT
      *---------------------------------------------------------------
      * Print detail line in Produced Pounds section
      *---------------------------------------------------------------
      *
     C     $l1dtl        begsr
      *
     C                   except    l1dtl
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Print report headings
      *---------------------------------------------------------------
      *
     C     $h1hdr        begsr
      *
     C                   except    h1hdr
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Print Pound Headings
      *---------------------------------------------------------------
      *
     C     $h2hdr        begsr
      *
     C                   except    h2hdr
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Key lists
      *
     C     key01         klist
     C                   kfld                    vsistycd
     C                   kfld                    wktfclcd
     C                   kfld                    ldyr
     C                   kfld                    wkwkpe
     ‚* Project-JM-P310149-START
     C     key02         klist
     C                   kfld                    vsistycd
     C                   kfld                    wktfclcdNVZ
     C                   kfld                    ldyr
     C                   kfld                    wkwkpe
     ‚* Project-JM-P310149-END
      *
      * Retrieve time for report heading.
      *
     C                   time                    rtime
      *
      * This report can be generated for "weekly" data or "period" data.
      *
     C                   select
     C                   when      ldwpfl = 'W'
     C                   z-add     ldwk          wkwkpe
     C                   seton                                        88
     C                   when      ldwpfl = 'P'
     C                   z-add     ldpe          wkwkpe
     C                   seton                                        89
     C                   endsl
      *
      * This report is generated on-demand and also from the
      * Margin Adjustment Close function.
      *
     C                   select
     C                   when      ldpfcd = 'D'
     C                   eval      %subst(h1demand:14:9) = 'On-Demand'
      *
     C                   when      ldpfcd = 'F'
     C                   eval      %subst(h1demand:4:29) =
     C                             'Final Margin Adjustment Close'
      *
     C                   when      ldpfcd = 'P'
     C                   eval      %subst(h1demand:1:35) =
     C                             'Preliminary Margin Adjustment Close'
     C                   endsl
      *
      * When the report is generated on-demand, the user can enter selections.
      *
      * TF class group code
      *
     C                   if        ldtfcgcd = *blank
     C                   seton                                        95
     C                   endif
      *
      * TF classification code
      *
     C                   if        ldtfclcd = *blank
     C                   seton                                        96
     C                   endif
      *
      * Item structure type code
      *
     C                   if        ldistycd = 0
     C                   seton                                        97
     C                   endif
      *
     C                   endsr
      /eject
      *-------------------------------------------------------------
      * Report lines
      *-------------------------------------------------------------
      *
     Oprint198  e            h1hdr          1 01
     O                       sdpgm               10
     O                                          108 'Volume Adjustment Report'
     O                                          188 'DATE'
     O                       udate         y    198
      *
      *
     O          e            h1hdr          1
     O                       sdusr               10
     O                       h1demand           113
     O                                          188 'TIME'
     O                       rtime              198 '  :  :  '
      *
     O          e            h1hdr          1
     O                                          188 'PAGE'
     O                       page          z    198
      *
     O          e    88      h1hdr          1
     O                                           25 'Year/week:'
     O                       ldyr          z     31
     O                       ldwk          z     35
      *
     O          e    88      h1hdr          1
     O                                           25 'Week-ending date:'
     O                       ldwemdy             35 '  /  /  '
      *
      *
     O          e    89      h1hdr          1
     O                                           25 'Year/period:'
     O                       ldyr          z     31
     O                       ldpe          z     35
     O                       ldpbmdy             46 '  /  /  '
     O                                           49 'to'
     O                       ldpemdy             58 '  /  /  '
      *
     O          e            h1hdr          1
     O                                           25 'TF classification code:'
     O                       vstfclcd            30
     O                       h1tfclds            68
      *
     O          e            h1hdr          1
     O                                           25 'Item structure type code:'
     O                       vsistycd      z     30
     O                       h1istyds            68
      *
      *-------------------------------------------------------------
      * Column headings for Produced Pounds section
      *-------------------------------------------------------------
      *
     O          e            h2hdr          0
     O              n87                          99 'ALL Based on PRODUCED '
     O              n87                         120 'Pounds of CV Products'
     O               87                          99 'ALL Based on PRODUCED '
     O               87                         121 'Pounds of NVA Products'
     O                                          177 'Starting Pounds'
     O          e            h2hdr          1
     O              n87                          99 'ALL Based on PRODUCED '
     O              n87                         120 'Pounds of CV Products'
     O               87                          99 'ALL Based on PRODUCED '
     O               87                         121 'Pounds of NVA Products'
     O                                          177 'Starting Pounds'
     O          e            h2hdr          1
     O                       dash2              182
      *
     O          e            h2hdr          1
     O                                           19 'STD'
     O                                           34 'STD'
     O                                           47 'STD'
     O                                           59 'STD'
     O                                           72 'STD'
     O                                           85 'STD'
     O                                           98 'STD'
     O                                          110 'Prod Value'
     O                                          122 'Margin'
     O                                          138 'Prod Wgt'
     O                                          154 'Produced'
     O                                          170 'Produced'
     O                                          182 'Margin'
      *
     O          e            h2hdr          1
     O                                           19 'Yld'
     O                                           34 'Meat Cost'
     O                                           47 'Labor Cost'
     O                                           59 'Pkg Cost'
     O                                           72 'Ing Cost'
     O                                           85 'Other Cost'
     O                                           98 'Total Cost'
     O                                          110 'per lb'
     O                                          122 'per lb'
     O                                          138 'Margin $$'
     O                                          154 'Pounds'
     O                                          170 'Start Lbs'
     O                                          182 'per lb'
      *
     O          e            h2hdr          0
     O                                           19 '--------'
     O                                           34 '----------'
     O                                           47 '----------'
     O                                           59 '----------'
     O                                           72 '----------'
     O                                           85 '----------'
     O                                           98 '----------'
     O                                          110 '----------'
     O                                          122 '----------'
     O                                          138 '--------------'
     O                                          154 '--------------'
     O                       dash2              182
     ‚* Project-JM-P310149-START
     O          e            h3hdr          0
     O              n87                          99 'ALL Based on PRODUCED '
     O              n87                         120 'Pounds of CV Products'
     O               87                          99 'ALL Based on PRODUCED '
     O               87                          +0 'Pounds of NVA,NVZ Products'
     O                                          177 'Starting Pounds'
     O          e            h3hdr          0
     O              n87                          99 'ALL Based on PRODUCED '
     O              n87                         120 'Pounds of CV Products'
     O               87                          99 'ALL Based on PRODUCED '
     O               87                          +0 'Pounds of NVA,NVZ Products'
     O                                          177 'Starting Pounds'
     O          e            h3hdr       1  1
     O                       dash2              182
      *
     O          e            h3hdr          1
     O                                           19 'STD'
     O                                           34 'STD'
     O                                           47 'STD'
     O                                           59 'STD'
     O                                           72 'STD'
     O                                           85 'STD'
     O                                           98 'STD'
     O                                          110 'Prod Value'
     O                                          122 'Margin'
     O                                          138 'Prod Wgt'
     O                                          154 'Produced'
     O                                          170 'Produced'
     O                                          182 'Margin'
      *
     O          e            h3hdr          1
     O                                           19 'Yld'
     O                                           34 'Meat Cost'
     O                                           47 'Labor Cost'
     O                                           59 'Pkg Cost'
     O                                           72 'Ing Cost'
     O                                           85 'Other Cost'
     O                                           98 'Total Cost'
     O                                          110 'per lb'
     O                                          122 'per lb'
     O                                          138 'Margin $$'
     O                                          154 'Pounds'
     O                                          170 'Start Lbs'
     O                                          182 'per lb'
      *
     O          e            h3hdr          0
     O                                           19 '--------'
     O                                           34 '----------'
     O                                           47 '----------'
     O                                           59 '----------'
     O                                           72 '----------'
     O                                           85 '----------'
     O                                           98 '----------'
     O                                          110 '----------'
     O                                          122 '----------'
     O                                          138 '--------------'
     O                                          154 '--------------'
     O                       dash2              182
     ‚* Project-JM-P310149-END
      *
      *-------------------------------------------------------------
      * Produced/Starting Pounds Section
      *-------------------------------------------------------------
      *
     O          e            l1dtl       1
     O                       l1desc               8
      * Produced pounds
     O                       l1puypc             20 '   .    %'
     O                       l1pumco       k     35
     O                       l1pulco       k     48
     O                       l1pukco       k     60
     O                       l1puico       k     73
     O                       l1puoco       k     86
     O                       l1pupco       k     99
     O                       l1pupr        k    111
     O                       l1pu1mg       k    123
     O                       l1pumgam      k    139
     O                       l1pulb        k    155
      * Starting pounds
     O                       l1cpuslb      k    171
     O                       l1pu2mg       k    183
      *
      *-------------------------------------------------------------
      * Underscores for Produced Pounds section
      *-------------------------------------------------------------
      *
     O          e            underscore  1  0
     O                                           19 '--------'
     O                                           34 '----------'
     O                                           47 '----------'
     O                                           59 '----------'
     O                                           72 '----------'
     O                                           85 '----------'
     O                                           98 '----------'
     O                                          110 '----------'
     O                                          122 '----------'
     O                                          138 '--------------'
     O                                          154 '--------------'
     O                                          170 '--------------'
     O                                          182 '----------'
      *
      *-------------------------------------------------------------
      * Remaining print lines
      *-------------------------------------------------------------
      *
      *
      * Seaboard
      *
     O          e            l3dtl       3  0
     O                                            8 'SEABOARD'
      *
     O          e            l3dtl          1
     O                                            8 'SEABOARD'
      *
     O          e            l3dtl          0
     O                                            8 '--------'
      *
     O          e            l3dtl          1
     O                                            8 '--------'
      *
     O          e            l3dtl          1
     O                                           25 'Unconverted Raw Products '
     O                                           41 '(Yielded Weight)'
     O                       vsspurslb     k     81
      *
     O          e            l3dtl          1
     O                                           19 'Converted Products '
     O                                           35 '(Yielded Weight)'
     O                       vsscpuslb     k     81
      *
     O          e            l3dtl          1
     O                                           80 '--------------'
      *
     O          e            l3dtl          1
     O                                           21 '     Total Weight of '
     O                                           40 'Unconverted Product'
     O                       vssputslb     k     81
      *
     O          e            l3dtl          2
     O                                           20 '     % to Conversion'
     O                       vsspucpc            81 ' 0 .    %'
      *
      *
      * Triumph
      *
     O          e            l3dtl          0
     O                                            7 'TRIUMPH'
      *
     O          e            l3dtl          1
     O                                            7 'TRIUMPH'
      *
     O          e            l3dtl          0
     O                                            7 '-------'
      *
     O          e            l3dtl          1
     O                                            7 '-------'
      *
      *
     O          e            l3dtl          1
     O                                           25 'Unconverted Raw Products '
     O                                           41 '(Yielded Weight)'
     O                       vstpurslb     k     81
      *
     O          e            l3dtl          1
     O                                           19 'Converted Products '
     O                                           35 '(Yielded Weight)'
     O                       vstcpuslb     k     81
      *
      *
     O          e            l3dtl          1
     O                                           80 '--------------'
      *
     O          e            l3dtl          1
     O                                           21 '     Total Weight of '
     O                                           40 'Unconverted Product'
     O                       vstputslb     k     81
      *
     O          e            l3dtl          2
     O                                           20 '     % to Conversion'
     O                       vstpucpc            81 ' 0 .    %'
      *
      *
      *
      *
      * Volume Adjustment
      *
     O          e            l3dtl          0
     O                                           17 'VOLUME ADJUSTMENT'
      *
     O          e            l3dtl          1
     O                                           17 'VOLUME ADJUSTMENT'
      *
     O          e            l3dtl          0
     O                                           17 '-----------------'
      *
     O          e            l3dtl          1
     O                                           17 '-----------------'
      *
     O          e            l3dtl          1
     O                                           25 'Actual Converted Product '
     O                                           38 '% for Triumph'
     O                       vstpucpc            81 ' 0 .    %'
      *
     O          e            l3dtl          1
     O                                           16 'Total Weight of '
     O                                           34 'Converted Product '
     O                                           59 'Both Plants (Yielded Wgt)'
     O                       vsacpuslb     k     81
      *
     O          e            l3dtl          1
     O                                           16 'Total Weight of '
     O                                           36 'Unconverted Product '
     O                                           47 'Both Plants'
     O                       vsaputslb     k     81
      *
      *
     O          e            l3dtl          1
     O                                           17 'Avg Converting % '
     O                                           33 'from Both Plants'
     O                       vsapucpc            81 ' 0 .    %'
      *
     O          e            l3dtl          2
     O                                           15 'Triumph Volume '
     O                                           34 'Adjustment % Excess'
     O                       vstevolpc           81 ' 0 .    %'
      *
      *
     O          e            l3dtl          1
     O                                           18 'Triumph Pounds of '
     O                                           37 'Unconverted Product'
     O                       vstputslb     k     81
      *
     O          e            l3dtl          1
     O                                           15 'Triumph Volume '
     O                                           34 'Adjustment % Excess'
     O                       vstevolpc           81 ' 0 .    %'
      *
     O          e            l3dtl          1
     O                                           20 'Unconverted Wgt Avg '
     O                                           29 'Margin/lb'
     O                       vsapu3mg      k     81
      *
     O          e            l3dtl          0
     O                                           38 'SBF Payment to TF '
     O                                           59 'for Volume Adjustment'
      *
     O          e            l3dtl          2
     O                                           38 'SBF Payment to TF '
     O                                           59 'for Volume Adjustment'
     O                       vstvolam      j     81
      *
     O          e            l3dtl          1
     O                                           24 'Seaboard % to Conversion'
     O                       vsspucpc            81 ' 0 .    %'
      *
     O          e            l3dtl          1
     O                                           16 'Total Weight of '
     O                                           34 'Converted Product '
     O                                           59 'Both Plants (Yielded Wgt)'
     O                       vsacpuslb     k     81
      *
     O          e            l3dtl          1
     O                                           16 'Total Weight of '
     O                                           36 'Unconverted Product '
     O                                           47 'Both Plants'
     O                       vsaputslb     k     81
      *
     O          e            l3dtl          1
     O                                           17 'Avg Converting % '
     O                                           33 'from Both Plants'
     O                       vsapucpc            81 ' 0 .    %'
      *
     O          e            l3dtl          2
     O                                           16 'Seaboard Volume '
     O                                           35 'Adjustment % Excess'
     O                       vssevolpc           81 ' 0 .    %'
      *
     O          e            l3dtl          1
     O                                           19 'Seaboard Pounds of '
     O                                           38 'Unconverted Product'
     O                       vssputslb     k     81
      *
     O          e            l3dtl          1
     O                                           16 'Seaboard Volume '
     O                                           35 'Adjustment % Excess'
     O                       vssevolpc           81 ' 0 .    %'
      *
     O          e            l3dtl          1
     O                                           16 'Unconverted Wgt '
     O                                           29 'Avg Margin/lb'
     O                       vsapu3mg      k     81
      *
     O          e            l3dtl          0
     O                                           38 'TF Payment to SBF '
     O                                           59 'for Volume Adjustment'
      *
     O          e            l3dtl          2
     O                                           38 'TF Payment to SBF '
     O                                           59 'for Volume Adjustment'
     O                       vssvolam      j     81
      *
      /eject
      *-------------------------------------------------------------
      * Blank line
      *-------------------------------------------------------------
      *
     O          e            blank       3  1
      /eject
      *-------------------------------------------------------------
      * No data message line
      *-------------------------------------------------------------
      *
     O          e            nodata         1 01
     O                       sdpgm               10
     O                                           78 'Volume Adjustment Report'
     O                                          122 'DATE'
     O                       udate         y    132
      *
      *
     O          e            nodata         1
     O                       sdusr               10
     O                       h1demand            83
     O                                          122 'TIME'
     O                       rtime              132 '  :  :  '
      *
     O          e            nodata         1
     O                                          122 'PAGE'
     O                       page          z    132
      *
     O          e    88      nodata         1
     O                                           25 'Year/week:'
     O                       ldyr          z     31
     O                       ldwk          z     35
     O                                           55 'Week-ending date:'
     O                       ldwemdy             64 '  /  /  '
      *
     O          e    89      nodata         1
     O                                           25 'Year/period:'
     O                       ldyr          z     31
     O                       ldpe          z     35
     O                       ldpbmdy             46 '  /  /  '
     O                                           49 'to'
     O                       ldpemdy             58 '  /  /  '
      *
     O          e            nodata         1
     O                                           25 'TF class group code:'
     O               95                          30 'All'
     O              n95      ldtfcgcd            29
     O              n95      ldtfcgds            53
      *
     O          e            nodata         1
     O                                           25 'TF classification code:'
     O               96                          30 'All'
     O              n96      ldtfclcd            30
     O              n96      ldtfclds            68
      *
     O          e            nodata         2
     O                                           25 'Item structure type code:'
     O               97                          30 'All'
     O              n97      ldistycd      z     30
     O              n97      ldistyds            68
      *
     O          e            nodata         1
     O                       dash               198
      *
      *
      *
     O          e            nodata      1
     O                                           19 'No data meets your'
     O                                           39 'selection criteria.'
      /eject
