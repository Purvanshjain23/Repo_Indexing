     A*%%TS  SD  20121109  112525  ISLRAMS     REL-V6R1M0  5761-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD464
     A*  TITLE:         SPECIFY FEED TICKET DETAIL REPORT OPTIONS
     A*  PROGRAMMER:    MARY PALMER
     A*  DATE:          04/28/94
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A N99                                  CF04(04 'PROMPT')
     A N99 44                               CF08(08 'SUBMIT')
     A N99                                  CF17(17 'PRINT OPTIONS')
     A N99                                  CF20(20 'WRKSPLF')
     A*
     A*--------------------------------------------------------------*
     A* RECORD FORMAT ONE
     A*--------------------------------------------------------------*
     A          R HP464R1
     A*%%TS  SD  20121109  112525  ISLRAMS     REL-V6R1M0  5761-WDS
     A                                      SETOF(30)
     A                                      SETOF(31)
     A                                      SETOF(32)
     A                                      SETOF(33)
     A                                      SETOF(34)
     A                                      SETOF(35)
     A                                      SETOF(36)
     A                                      SETOF(37)
     A                                      SETOF(38)
     A                                      SETOF(39)
     A                                      SETOF(40)
     A                                      SETOF(42)
     A                                      SETOF(47)
     A                                      SETOF(48)
     A                                      SETOF(49)
     A                                      SETOF(50)
     A                                      SETOF(51)
     A                                      SETOF(52)
     A                                      SETOF(53)
     A                                      SETOF(54)
     A                                      SETOF(55)
     A                                      SETOF(56)
     A                                      SETOF(57)
     A                                      SETOF(58)
     A                                      SETOF(59)
     A                                      SETOF(78)
     A                                      SETOF(79)
     A                                      OVERLAY
     A                                      RTNCSRLOC(&RCD &FLD)
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 27'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  2 18'SPECIFY FEED TICKET DETAIL REPORT -
     A                                      OPTIONS'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  4  2'From ticket date ....'
     A            DFFMDY    R     Y  B  4 26REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A N32                                  DSPATR(HI)
     A  32                                  DSPATR(PC)
     A  32                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A                                  5  2'To ticket date ......'
     A            DFTMDY    R     Y  B  5 26REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A N33                                  DSPATR(HI)
     A  33                                  DSPATR(PC)
     A  33                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A                                  6  2'Report ..............'
     A                                  6 24'?'
     A            DFRPFL    R        B  6 26REFFLD(@@FLAG)
     A N47                                  DSPATR(HI)
     A  47
     AO 57                                  DSPATR(PC)
     A  47                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A            DFRPDS        21A  O  6 37
     A                                  7  2'Page break on farm ..'
     A                                  7 24'?'
     A            DFPBFL    R        B  7 26REFFLD(@@FLAG)
     A N30                                  DSPATR(HI)
     A  30
     AO 50                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A                                  8  2'Instead of printing the report, em-
     A                                      ail the data as a spreadsheet to:'
     A                                      COLOR(BLU)
     A                                  9  6'E-mail address ..'
     A            DFEMAIL   R        B  9 26REFFLD(TFREF/@@EMAIL *LIBL/TFPREF)
     A                                      CHGINPDFT(UL LC FE)
     A                                      DSPATR(HI)
     A                                 10  2'Optional selections'
     A                                      DSPATR(UL)
     A                                 11  2'Feed mill business office ..'
     A                                 11 31'?'
     A            DFFMBO    R        B 11 33REFFLD(BOBOBO)
     A N31                                  DSPATR(HI)
     A  31
     AO 51                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A            DFBODS    R        O 11 41REFFLD(BOBODS)
     A                                 12  2'Production phase ...........'
     A                                 12 31'?'
     A            DFPPCD    R        B 12 33REFFLD(PPPPCD)
     A N35                                  DSPATR(HI)
     A  35
     AO 53                                  DSPATR(PC)
     A  35                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A            DFPPDS    R        O 12 41REFFLD(PPPPDS)
     A                                 13  2'Production manager .........'
     A                                 13 31'?'
     A            DFPMGCD   R        B 13 33REFFLD(HSRREF/@@MGCD *LIBL/HSPREF)
     A N34                                  DSPATR(HI)
     A  54
     AO 34                                  DSPATR(PC)
     A  34                                  DSPATR(RI)
     A                                      DSPATR(UL)
     A            DFPMGNM   R        O 13 41REFFLD(HSRREF/MGMGNM *LIBL/HSPREF)
     A                                 14  2'Cell .......................'
     A            DFCELL    R        B 14 33REFFLD(@@CELL)
     A                                      EDTCDE(M)
     A N40                                  DSPATR(HI)
     A  40                                  DSPATR(PC)
     A  40                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A                                 15  2'Farm site ..................'
     A                                 15 31'?'
     A            DFFSCD    R        B 15 33REFFLD(FSFSCD)
     A                                      EDTCDE(M)
     A N36                                  DSPATR(HI)
     A  36
     AO 56                                  DSPATR(PC)
     A  36                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A            DFFSNM    R        O 15 41REFFLD(FSFSNM)
     A                                 16  2'Company relationship .......'
     A                                 16 31'?'
     A            DFCRCD    R        B 16 33REFFLD(HSRREF/@@CRCD *LIBL/HSPREF)
     A N48                                  DSPATR(HI)
     A  48
     AO 78                                  DSPATR(PC)
     A  48                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A            DFCRDS    R        O 16 41REFFLD(HSRREF/CRCRDS *LIBL/HSPREF)
     A                                 17  2'Feed delivery zone .........'
     A                                 17 31'?'
     A            DFDZCD    R        B 17 33REFFLD(HSRREF/@@DZCD *LIBL/HSPREF)
     A N42                                  DSPATR(HI)
     A  42
     AO 52                                  DSPATR(PC)
     A  42                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A            DFDZDS    R        O 17 41REFFLD(HSRREF/DZDZDS *LIBL/HSPREF)
     A                                 18  2'Ration code ................'
     A                                 18 31'?'
     A            DFRNCD    R        B 18 33REFFLD(RNRNCD)
     A N37                                  DSPATR(HI)
     A  37
     AO 55                                  DSPATR(PC)
     A  37                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A            DFRNDS    R        O 18 41REFFLD(RNRNDS)
     A                                 19  2'Produced flag ..............'
     A                                 19 31'?'
     A            DFPRFL    R        B 19 33REFFLD(HSRREF/@@PRFL *LIBL/HSPREF)
     A N49                                  DSPATR(HI)
     A  49
     AO 79                                  DSPATR(PC)
     A  49                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A                                 20  2'Hog group ..................'
     A                                 20 31'?'
     A            DFHGCD    R        B 20 33REFFLD(HGHGCD)
     A N38                                  DSPATR(HI)
     A  38
     AO 58                                  DSPATR(PC)
     A  38                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A                                 21  2'Transaction code ...........'
     A                                 21 31'?'
     A            DFTRCD    R        B 21 33REFFLD(HSRREF/@@TRCD *LIBL/HSPREF)
     A N39                                  DSPATR(HI)
     A  39
     AO 59                                  DSPATR(PC)
     A  39                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A            DFTRDS    R        O 21 41REFFLD(@@DS10)
     A*
     A*--------------------------------------------------------------*
     A* COMMAND KEY RECORD 1
     A*--------------------------------------------------------------*
     A          R HP464K1
     A*%%TS  SD  19941028  131416  ISLRAMS     REL-V3R0M5  5763-PW1
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A N99                             23 11'F4=Prompt'
     A  44N99                          23 22'F8=Submit'
     A N99                             23 33'F17=Print Options'
     A N99                             23 52'F20=Spool Files'
      *
      *--------------------------------------------------------------*
      * ERROR MESSAGE SUBFILE FORMAT
      *--------------------------------------------------------------*
     A          R HP464ES                   SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
     A*
     A*--------------------------------------------------------------*
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*--------------------------------------------------------------*
     A*
     A          R HP464EC                   SFLCTL(HP464ES)
     A*%%TS  SD  19940415  103325  ISRCENT     REL-V2R2M0  5738-PW1
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
