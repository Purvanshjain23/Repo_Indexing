/***************************************************************** */
/*                                                                 */
/*  PROGRAM NUMBER:  HP467CL                                       */
/*  PROGRAM NAME:    EXPENSE TICKET ALLOCATION                     */
/*  PROGRAMMER:      LEANNE FEDOR                                  */
/*  CREATE DATE:     06/03/94                                      */
/*                                                                 */
/*  FUNCTION:        THIS CL IS SUBMITTED WITH QCMDEXC FROM        */
/*                   HP467-SPECIFY EXPENSE TICKET ALLOCATION.      */
/*                   USER OPTIONS ARE PASSED THROUGH THE LDA.      */
/*                   AN OPNQRY STATEMENT IS USED TO SELECT AND     */
/*                   SEQUENCE RECORDS.                             */
/*  MOFIFIED BY:     BARB GUTIERREZ    3/4/16                      */
/*                   ADDED COMPANY NUMBER TO OPEN QUERY TO         */
/*                   ACCOMODATE FOR MULTIPLE COMPANIES NOW.        */
/*                   (E5290)                                       */
/*******************************************************************/

             PGM

/* DEFINE THE VARIABLE REQUIRED IN THE COMMAND TO OVERRIDE THE     */
/* PRINT FILE TO A LASER PRINTER                                   */

             DCL        VAR(&CMD) TYPE(*CHAR) LEN(512)

             DCL        VAR(&QDATA) TYPE(*CHAR) LEN(70)
             DCL        VAR(&QDATA1) TYPE(*CHAR) LEN(70)

             DCL        VAR(&DFCYMD) TYPE(*DEC) LEN(8)
             DCL        VAR(&DFEFFL) TYPE(*CHAR) LEN(1)
             DCL        VAR(&DFCOCD) TYPE(*CHAR) LEN(3)

             DCL        VAR(&HOLD) TYPE(*CHAR) LEN(10)
             DCL        VAR(&COPYS) TYPE(*DEC) LEN(2)
             DCL        VAR(&OUTQ) TYPE(*CHAR) LEN(10)

             CHGVAR     VAR(&DFCYMD) VALUE(%SST(*LDA 1 8))
             CHGVAR     VAR(&DFEFFL) VALUE(%SST(*LDA  9 1))
             CHGVAR     VAR(&DFCOCD) VALUE(%SST(*LDA  200 3))

/*  CREATE WORKFILES                                              */


             CRTDUPOBJL OBJ(HSP366) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)

             CRTDUPOBJL OBJ(HSP367) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)

             CRTDUPOBJL OBJ(HSL367A) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)

             CRTDUPOBJL OBJ(HSP368) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)


/*  SELECT UNALLOCATED EXPENSE TICKET RECORDS FOR CUTOFF DATE    */

             CHGVAR     VAR(%SST(&QDATA 1 10)) VALUE('XTXBDT *LE')
             CHGVAR     VAR(%SST(&QDATA 12 8)) VALUE(&DFCYMD)
             CHGVAR     VAR(%SST(&QDATA 21 20)) VALUE('*AND XTALFL +
                          *EQ "T"')
             CHGVAR     VAR(%SST(&QDATA 41 16)) VALUE('*AND XTCOCD +
                          *EQ')
             CHGVAR     VAR(%SST(&QDATA 57 3)) VALUE(&DFCOCD)

/*  PERFORM OPEN QUERY AND BUILD WORKFILES      */

             OVRDBF     FILE(HSL048I) SHARE(*YES)
             OPNQRYF    FILE((HSL048I)) QRYSLT(&QDATA) +
                          KEYFLD(*FILE) SEQONLY(*YES)


/*  SELECT ONLY GROUPS FOR COMPANY SELECTED                      */

             CHGVAR     VAR(%SST(&QDATA1 1 10)) VALUE('HGCOCD *EQ')
             CHGVAR     VAR(%SST(&QDATA1 12 3)) VALUE(&DFCOCD)

/*  PERFORM OPEN QUERY                          */

             OVRDBF     FILE(HSL034F) SHARE(*YES)
             OPNQRYF    FILE((HSL034F)) QRYSLT(&QDATA1) +
                          KEYFLD(*FILE) SEQONLY(*YES)

             CALL       PGM(HP367)

             CLOF       OPNID(HSL048I)
             DLTOVR     FILE(HSL048I)
             CLOF       OPNID(HSL034F)
             DLTOVR     FILE(HSL034F)


/* SET PARAMETERS FROM LDA                         */

 HOLD:       IF         COND(%SST(*LDA 411 1) = 'Y') THEN(CHGVAR +
                          VAR(&HOLD) VALUE(*YES))
             ELSE       CMD(CHGVAR VAR(&HOLD) VALUE(*NO))

 COPYS:      CHGVAR     VAR(&COPYS) VALUE(%SST(*LDA 412 1))
             IF         COND(&COPYS = 0) THEN(CHGVAR VAR(&COPYS) +
                          VALUE(1))

 OUTQ:       CHGVAR     VAR(&OUTQ) VALUE(%SST(*LDA 413 10))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(%SST(*LDA 401 10)))
             IF COND(&OUTQ = '          ') THEN(CHGVAR VAR(&OUTQ) +
                          VALUE(*JOB))

/*------------------------------------------------------------------*/
/* OVERRIDE PRINT FILE                                              */
/*------------------------------------------------------------------*/

/* USE THE CUSTOMIZED SEABOARD COMMAND TO OVERRIDE THE PRINT FILE.  */
/* THIS COMMAND ALLOWS THE REPORT TO BE GENERATED ON A STANDARD LINE*/
/* PRINTER OR A LASER PRINTER. TO USE THIS OVERRIDE YOU MUST DECLARE*/
/* A COMMAND VARIABLE AND FOLLOW THE OVERRIDE WITH A CALL TO        */
/* QCMDEXC.                                                         */

/* THIS REPORT IS CONDENSED PRINT AT 15 CPI; SO, ON A LASER PRINTER */
/* FORCE IT TO DRAWER '2' FOR LEGAL PAPER AND FORCE 'LANDSCAPE'     */

             SBDPRTOVR  CMDVAR(&CMD) FILE(QPRINT) OUTQ(&OUTQ) +
                          WIDTH(198) LANDSCAPE(*YES) DRAWER(2) +
                          COPIES(&COPYS) HOLD(&HOLD)

             CALL       PGM(QCMDEXC) PARM(&CMD 512)

/*------------------------------------------------------------------*/
/*  CALL PROGRAM TO PRINT ALLOCATION REPORT    */
/*------------------------------------------------------------------*/

             CALL       PGM(HP667)

/*  IF 'FINAL' RUN, CALL PROGRAM TO UPDATE HSP048 FROM WORKFILES */

             IF         COND(&DFEFFL *EQ 'F') THEN(DO)
             CALL       PGM(HP368)
             ENDDO

             DLTOVR QPRINT

/*  SET THE DATA AREA THAT KEEPS THE ALLOCATION FUNCTION FROM BEING     */
/*  SUBMITTED TWICE SIMULTANEOUSLY BACK TO BLANK                        */

             CHGDTAARA  DTAARA(DAALEX (1 1)) VALUE(' ')

             ENDPGM

