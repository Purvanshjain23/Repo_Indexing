      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Hog Production System
      * PROGRAM:     HP5047
      * TITLE:       Select Scheduled Transfer Movement
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     09/14/04
      *
      *
      * FUNCTION: This is a select function window display of the records in the
      *           Transfer Movement header file. Only Scheduled transfers are included.
      *
      *           A record is selected by positioning the cursor on the line
      *           and pressing enter.
      *
      *           Restrictors:
      *              none
      *
      *           Positioners:
      *              scheduled ship date
      *
      *           Selectors:
      *              origin transportation office
      *              origin production type
      *              origin production phase
      *              origin cell
      *              origin farm site
      *
      *              destination production type
      *              destination production phase
      *              destination farm site
      *
      * NOTE:    This is a pop-up window program.  Please note that in window
      *          programs the record format containing the window definition
      *          must be written first.  So, we write the error message subfile
      *          control record first followed by the command line record format.
      *          This is opposite of the non-window programs.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 10/16/13  LeAnne Ramsey (E2831)
      *           Recompile only. Added field 'MTech Reference'.
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fhpd5047   cf   e             workstn
     F                                     sfile(hp5047s1:hdrrn1)
     F                                     infds(iofeed)
      *
      *
     Fhsp018    if   e           k disk
      * Farm site
      *
      *
     Fhsl074g   if   e           k disk    infds(dbfeed)
      * Transfer movement header
      *
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
     D set1            c                   'SET1  '
     D edit1           c                   'EDIT1 '
     D scrn1           c                   'SCRN1 '
     D exit            c                   'EXIT  '
      *
      *
      ****************************************************************
      * ARRAYS AND TABLES
      ****************************************************************
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Standard fields
      *
     D pgm             s             10
     D msgfl           s             10
     D maxmsg          s              2  0  inz(20)
     D routne          s              6
     D maxrrn          s              4  0
     D norecs          s              1    inz('N')
     D reset           s              1    inz('N')
     D count           s              2  0
      *
      *
      * Workfields required for subfile paging--see initialization
      * subroutine.
      *
     D wkplin          s              2  0
     D wkrlin          s              2  0
     D wkflow          s                   like(idflow)
      *
      *
      * Control fields
      *
     D procfl          s              1
      *
     D svscmdy         s                   like(c1scmdy)
     D svortocd        s                   like(c1ortocd)
     D svorppcd        s                   like(c1orppcd)
     D svorptcd        s                   like(c1orptcd)
     D svorcell        s                   like(c1orcell)
     D svorfscd        s                   like(c1orfscd)
      *
     D svdnppcd        s                   like(c1orppcd)
     D svdnptcd        s                   like(c1orptcd)
     D svdnfscd        s                   like(c1orfscd)
      *
      *
      * Workfields
      *
     D wkscdt          s                   like(mhscdt)
     D wkisodate       s               d   datfmt(*iso)
      *
      *
      * Parm fields
      *
     D xxmvsn          s                   like(d1mvsn)
     D xxreprint       s              1
      *
     D xxtocd          s                   like(fstocd)
     D xxtonm          s             25
     D xxaist          s              1
      *
     D xxfscd          s                   like(d1orfscd)
     D xxfsnm          s             25
      *
     D xxppcd          s                   like(c1orppcd)
     D xxppds          s             30
      *
     D xxptcd          s                   like(c1orptcd)
     D xxptds          s             10
      *
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * standard error message handling data structures
      *---------------------------------------------------------------
      *
      * For error message handling, a packed index (3) is required.
      * For program readability, define a corresponding error count
      * field called 'error'
      *
     D                 ds                  inz
     D  error                         2p 0
     D   e                            2p 0 overlay(error)
      *
      * This data structure supplies the name of the message file to
      * the message handling CL program.  The field name MSGFIL must be
      * constant. The value in quotes is the name of the specific
      * message file containing the user defined messages.
      *
     D                 ds                  inz
     D  msgfil                       10    inz('HSMSGF    ')
      *
      *---------------------------------------------------------------
      *  Standard message data structures
      *---------------------------------------------------------------
      *
      * The following 3 data structures are used to speed message
      * handling since it is faster to clear data structures than
      * arrays.  Each is associated with a standard message array.
      *
     D mgi             ds                  inz
     D  mgid                          7    dim(20)
      *
     D mgd             ds                  inz
     D  mgdt                         50    dim(20)
      *
     D mgwk            ds                  inz
     D  mgw                           1    dim(50)
      *
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *---------------------------------------------------------------
      * Standard workstation information data structure
      *---------------------------------------------------------------
      *    externally defined as UTWSFR (record format: UTIDFRR)
     D iofeed        e ds                  extname(utwsfr)
      *
      *
      *---------------------------------------------------------------
      * Standard database file information data structure
      *---------------------------------------------------------------
      *    externally defined as UTDBGR (record format: FDBCKD)
     D dbfeed        e ds                  extname(utdbfr)
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * MAINLINE
      ****************************************************************
      *
     C                   eval      routne = set1
      *
     C     *inlr         doweq     *off                                         Main do loop
     C     routne        caseq     set1          $set1
     C     routne        caseq     scrn1         $scrn1
     C     routne        caseq     exit          $exit
     C                   endcs
     C                   enddo                                                  Main do loop
      /EJECT
      *---------------------------------------------------------------
      * Set environment for screen 1
      *---------------------------------------------------------------
      *
     C     $set1         begsr
      *
      * Clear/fill the subfile
      *
     C                   exsr      $clr1
     C                   exsr      $frsh1
      *
      * Set routine to display screen
      *
     C                   eval      routne = scrn1
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Perform operator I/O for screen 1
      *---------------------------------------------------------------
      *
     C     $scrn1        begsr
      *
      * Write the error messages from the error arrays to the
      * error message subfile
      *
     C                   exsr      $wrmsg
      *
      * Write screen 1 to crt
      *
     C                   write     hp5047ec
     C                   write     hp5047k1
     C                   exfmt     hp5047c1
      *
      * Clear the messages from the message data structures and
      * program message queue
      *
     C                   exsr      $clmsg
      *
      * Get user's entry and set next routine
      *
     C                   select
     C                   when      *in12 = *on                                  F12=previous
     C                   eval      routne = exit
      *
     C                   when      *in04 = *on                                  F4-prompt
     C                   exsr      $f4s1
     C                   exsr      $chksv
      *
      * Reposition the subfile if the user has changed any of the
      * positioners or selectors in the control record.
      *
     C                   when      c1scmdy  <> svscmdy  or                      Positioners
     C                             c1ortocd <> svortocd or
     C                             c1orppcd <> svorppcd or
     C                             c1orptcd <> svorptcd or
     C                             c1orfscd <> svorfscd or
     C                             c1orcell <> svorcell or
     C                             c1dnppcd <> svdnppcd or
     C                             c1dnptcd <> svdnptcd or
     C                             c1dnfscd <> svdnfscd
     C                   exsr      $chksv
      *
     C                   when      *in25 = *on                                  F25-rollup
     C                   exsr      $load1
     C                   eval      routne = scrn1
     C                   other
      *
      * User has pressed enter
      *
     C                   if        norecs = yes                                 If empty sfl
     C                   eval      routne = set1
     C                   else
     C                   exsr      $proc1
     C                   endif                                                  If empty sfl
     C                   endsl
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * F4 prompt on screen 1
      *---------------------------------------------------------------
      *
     C     $f4s1         begsr
      *
      * Origin Transportation Office
      *
     C                   select
     C                   when      fld = 'C1ORTOCD'
     C                   call      'HP5026'
     C                   parm      *blank        xxaist
     C                   parm      *blank        xxtocd
     C                   parm      *blank        xxtonm
     C                   seton                                        51
     C                   if        xxtocd <> *blank
     C                   movel     xxtocd        c1ortocd
     C                   endif
      *
      * Origin Production Type
      *
     C                   when      fld = 'C1ORPTCD'
     C                   call      'HP504'
     C                   parm      *blank        xxptcd
     C                   parm      *blank        xxptds
     C                   seton                                        52
     C                   if        xxptcd <> *blank
     C                   movel     xxptcd        c1orptcd
     C                   endif
      *
      * Origin Production Phase
      *
     C                   when      fld = 'C1ORPPCD'
     C                   call      'HP507'
     C                   parm      *blank        xxppcd
     C                   parm      *blank        xxppds
     C                   seton                                        53
     C                   if        xxppcd <> *blank
     C                   movel     xxppcd        c1orppcd
     C                   endif
      *
      * Origin Farm Site
      *
     C                   when      fld = 'C1ORFSCD'
     C                   call      'HP5040'
     C                   parm      *blank        xxaist
     C                   parm      0             xxfscd
     C                   parm      *blank        xxfsnm
     C                   seton                                        54
     C                   if        xxfscd <> 0
     C                   z-add     xxfscd        c1orfscd
     C                   endif
      *
      * Destination Production Type
      *
     C                   when      fld = 'C1DNPTCD'
     C                   call      'HP504'
     C                   parm      *blank        xxptcd
     C                   parm      *blank        xxptds
     C                   seton                                        55
     C                   if        xxptcd <> *blank
     C                   movel     xxptcd        c1dnptcd
     C                   endif
      *
      * Destination Production phase
      *
     C                   when      fld = 'C1DNPPCD'
     C                   call      'HP507'
     C                   parm      *blank        xxppcd
     C                   parm      *blank        xxppds
     C                   seton                                        56
     C                   if        xxppcd <> *blank
     C                   movel     xxppcd        c1dnppcd
     C                   endif
      *
      * Destination Farm Site
      *
     C                   when      fld = 'C1DNFSCD'
     C                   call      'HP5040'
     C                   parm      *blank        xxaist
     C                   parm      0             xxfscd
     C                   parm      *blank        xxfsnm
     C                   seton                                        57
     C                   if        xxfscd <> 0
     C                   z-add     xxfscd        c1dnfscd
     C                   endif
     C*                  other
      *
      * F4 not valid for this field...issue message
      *
     C                   if        error < maxmsg
     C                   add       1             error                                        013
     C                   movel     'HS09011'     mgid(e)
     C                   endif
      *
     C                   endsl
      *
     C                   endsr
      /EJECT
      *----------------------------------------------------------------
      * Process user's selection
      *----------------------------------------------------------------
      *
     C     $proc1        begsr
      *
      * If cursor location returned is the rrn of a record in the
      * subfile, chain to the subfile record and load the data into
      * the parms to return to the calling program.
      *
     C                   if        hdrrn <> 0
     C                   eval      hdrrn1 = hdrrn
     C     hdrrn1        chain     hp5047s1                           92
     C                   z-add     d1mvsn        xxmvsn
     C                   endif
      *
      * If the user selected a record, set the routine to exit.  If no record
      * was selected, redisplay the screen.
      *
     C                   if        xxmvsn = 0
     c                   eval      routne = set1
     C                   else
     c                   eval      routne = exit
     C                   endif
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Clear subfile for screen 1
      *---------------------------------------------------------------
      *
     C     $clr1         begsr
      *
     C                   clear                   hp5047s1
     C                   seton                                        83        sflclr
     C                   write     hp5047c1
     C                   setoff                                       818283
      *                                                    81=sfldsp
      *                                                    82=sflend
      *                                                    83=sflclr
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Refresh/fill the subfile for screen 1
      *---------------------------------------------------------------
      *
     C     $frsh1        begsr
      *
      * Set position in file
      *
     C     wkscdt        setll     hsl074g
      *
      * The lowest subfile RRN on the screen from the DSPF feedback area (IDFLOW)
      * was retrieved earlier and put in WKFLOW. Add to WKFLOW the number of lines
      * on your subfile page or the number less 1 to determine a subfile RRN to
      * reposition to. RESET is a flag field to indicate whether you are attempting
      * to reposition the user close to where he was in the subfile. If RESET is
      * 'NO' don't reposition--just start over.
      *
     C                   if        reset = yes
     C     wkflow        add       wkrlin        maxrrn
     C                   else
     C                   z-add     wkplin        maxrrn
     C                   endif
      *
      * Load the subfile until you have the required number of records.
      * The number will depend on whether uou are repositioning the
      * subfile or not. Stop if you get to the end of the subfile.
      *
     C                   dou       hdrrn1 >= maxrrn or *in82 = *on
     C                   exsr      $load1
     C                   enddo
      *
     C                   endsr
      /EJECT
      *----------------------------------------------------------------
      * Save the control record positioners/selectors
      *----------------------------------------------------------------
      *                                                                        z
     C     $chksv        begsr
      *
     C                   eval      routne = set1
     C                   eval      reset = no
      *
     C                   move      c1ortocd      svortocd
     C                   move      c1orppcd      svorppcd
     C                   move      c1orptcd      svorptcd
     C                   z-add     c1orfscd      svorfscd
     C                   z-add     c1orcell      svorcell
      *
     C                   move      c1dnppcd      svdnppcd
     C                   move      c1dnptcd      svdnptcd
     C                   z-add     c1dnfscd      svdnfscd
      *
     C                   z-add     c1scmdy       svscmdy
      *
      * Flip date selector from mmddyy to ccyymmdd format
      *
     C     *mdy          test(d)                 c1scmdy                92
     C                   if        *in92 = *on                                  If bad date
     C                   z-add     0             wkscdt
     C                   else
     C     *mdy          move      c1scmdy       wkisodate
     C                   move      wkisodate     wkscdt
     C                   endif                                                  If bad date
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Load subfile for screen 1
      *---------------------------------------------------------------
      *
     C     $load1        begsr
      *
      * Set the count of records on the subfile page to zero.
      *
     C                   z-add     0             count
      *
      * Fill the subfile until the end of the database file or
      * you have filled all the lines on the subfile page.
      *
     C                   setoff                                       82
     C                   dou       *in82 = *on or count >= wkplin               Do fill
      *
     C                   read      hsl074g                                82
     C                   if        *in82 = *off                                 If read
      *
     C                   move      yes           procfl
      *
      * Are you supposed to show movements that have already been printed or
      * not?
     C                   select
     C                   when      xxreprint = yes and mhptct = 0
     C                   move      no            procfl
     C                   when      xxreprint = no and mhptct <> 0
     C                   move      no            procfl
     C                   endsl
      *
      * Retrieve origin farm data
      *
     C     mhorfs        chain     hsp018                             92
     C                   if        *in92 = *off
     C                   z-add     fscell        d1orcell
     C                   move      fstocd        d1ortocd
     C                   else
     C                   z-add     0             d1orcell
     C                   move      *blank        d1ortocd
     C                   endif
      *
      * Only include records that match the user's selectors
      *
     C                   if        c1ortocd <> *blank and
     C                             c1ortocd <> d1ortocd
     C                   move      no            procfl
     C                   endif
      *
     C                   if        c1orcell <> 0 and
     C                             c1orcell <> d1orcell
     C                   move      no            procfl
     C                   endif
      *
     C                   if        c1orptcd <> *blank and
     C                             c1orptcd <> mhorpt
     C                   move      no            procfl
     C                   endif
      *
     C                   if        c1orppcd <> *blank and
     C                             c1orppcd <> mhorpp
     C                   move      no            procfl
     C                   endif
      *
     C                   if        c1orfscd <> 0 and
     C                             c1orfscd <> mhorfs
     C                   move      no            procfl
     C                   endif
      *
     C                   if        c1dnptcd <> *blank and
     C                             c1dnptcd <> mhdnpt
     C                   move      no            procfl
     C                   endif
      *
     C                   if        c1dnppcd <> *blank and
     C                             c1dnppcd <> mhdnpp
     C                   move      no            procfl
     C                   endif
      *
     C                   if        c1dnfscd <> 0 and
     C                             c1dnfscd <> mhdnfs
     C                   move      no            procfl
     C                   endif
      *
      *
     C                   if        procfl = yes                                 If process
      *
     C                   add       1             count
     C                   add       1             hdrrn1
     C                   z-add     mhmvsn        d1mvsn
     C                   move      mhorpt        d1orptcd
     C                   move      mhorpp        d1orppcd
     C                   z-add     mhorfs        d1orfscd
     C                   move      mhdnpt        d1dnptcd
     C                   move      mhdnpp        d1dnppcd
     C                   z-add     mhdnfs        d1dnfscd
     C                   z-add     mhptct        d1ptct
      *
      * Flip Closed Date from CCYYMMDD to MMDDYY
      *
     C     *iso          test(d)                 mhscdt                 92
     C                   if        *in92 = *off                                 If OK date
     C                   move      mhscdt        wkisodate
     C     *mdy          move      wkisodate     d1scmdy
     C                   else
     C                   z-add     0             d1scmdy
     C                   endif                                                  If OK date
      *
     C                   write     hp5047s1
     C                   seton                                        81
      *
      * For positioning, place the subfile relative record number into the
      * sflrcdnbr field (HDPAG1)
      *
     C                   z-add     hdrrn1        hdpag1
     C                   endif                                                  If process
     C                   endif                                                  If read
     C                   enddo                                                  Do fill
      *
      * If no records were written to the subfile, then issue
      * the standard message 'No data to Display'.
      *
     C                   if        hdrrn1 = 0                                   If no recs
     C                   eval      norecs = yes
      *
     C                   if        error < maxmsg
     C                   add       1             error
     C                   eval      mgid(e) = 'HS09002'
     C                   endif
     C                   else
      *
     C                   eval      norecs = no
     C                   endif                                                  If no recs
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * End job
      *---------------------------------------------------------------
      *
     C     $exit         begsr
     C                   seton                                        LR
     C                   endsr
      *
      *---------------------------------------------------------------
      * Write error messages
      *---------------------------------------------------------------
      *
     C     $wrmsg        begsr
      *
     C                   call      'UT80045J'                           43
     C                   parm                    mgid
     C                   parm                    mgdt
     C                   parm                    error
     C                   parm      sdpgm         pgm
     C                   parm      msgfil        msgfl
      *
      * If call to UT80045J failed, a message hardcoded in the DSPF
      * command line format will be set on. So, redisplay screen.
      *
     C                   if        *in43 = *on
     C                   eval      routne = set1
     C                   endif
      *
     C                   endsr
      *
      *----------------------------------------------------------------
      * Clear messages
      *----------------------------------------------------------------
      *
     C     $clmsg        begsr
      *
     C                   call      'UT80045J'                           43
     C                   parm      *blank        mgi
     C                   parm      *blank        mgd
     C                   parm      *zero         error
     C                   parm      sdpgm         pgm
     C                   parm      msgfil        msgfl
      *
      * If call to UT80045J failed, a message hardcoded in the DSPF
      * command line format will be set on; so, redisplay screen.
      *
     C                   if        *in43 = *on
     C                   eval      routne = set1
     C                   endif
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Key lists
      *     None
      *
      * Parm lists
      *
     C     *entry        plist
     C                   parm                    xxreprint
     C                   parm                    xxmvsn
      *
      *
      * To support subfile paging, logic requires the number of lines on
      * the subfile page.
      *
     C                   z-add     9             wkplin                         sflpag
     C     wkplin        sub       1             wkrlin
      *
      *
      * The following standard code must be included to make the
      * standard error message handling program function properly.
      * This code sets and clears the program message queue.
      *
     C                   movel     '*'           msgpgm
     C                   exsr      $clmsg
      *
      * Execute required window format
      *
     C                   write     assume
      *
     C                   endsr
      /EJECT
