     A*%%TS  SD  20020611  161019  ISLRAMS     REL-V5R1M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD7083
     A*  TITLE:         VIEW QUARTERLY AVERAGES
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  DATE:          06/11/02
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A                                      CF05(05 'REFRESH')
     A*
     A*
     A*---------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP7083S1                  SFL
     A*%%TS  SD  20020611  080312  ISLRAMS     REL-V5R1M0  5722-WDS
     A  85                                  SFLNXTCHG
     A            HDRRN1         4Y 0H
     A            D1FSCD    R        O 12  2REFFLD(HSRREF/@@FSCD *LIBL/HSPREF)
     A                                      EDTCDE(Z)
     A            D1TOSV    R        O 12 10REFFLD(HSRREF/@@TOSV *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A            D1CRPR    R        O 12 19REFFLD(HSRREF/@@CRPR *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A            D1FRPR    R        O 12 28REFFLD(HSRREF/@@FRPR *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A            D1FOPR    R        O 12 37REFFLD(HSRREF/@@FOPR *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A            D1LTBAHD  R        O 12 46REFFLD(HSRREF/@@LTBAHD *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A            D1PMPR    R        O 12 54REFFLD(HSRREF/@@PMPR *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A            D1NRMPR   R        O 12 63REFFLD(HSRREF/@@NRMPR *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A            D1FRMPR   R        O 12 73REFFLD(HSRREF/@@FRMPR *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A*
     A*
     A*---------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP7083C1                  SFLCTL(HP7083S1)
     A*%%TS  SD  20020611  161019  ISLRAMS     REL-V5R1M0  5722-WDS
     A                                      SFLSIZ(0050)
     A                                      SFLPAG(0010)
     A N82                                  ROLLUP(25 'PAGE FORWARD')
     A                                      OVERLAY
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 26'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  2 25'VIEW QUARTERLY AVERAGES'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  4  2'Year/quarter:'
     A                                      DSPATR(HI)
     A            C1ACYR    R        O  4 16REFFLD(@@CDYR)
     A                                      EDTCDE(M)
     A            C1ACQR    R        O  4 22REFFLD(@@CDQR)
     A                                      EDTCDE(M)
     A                                  4 28'Cell:'
     A                                      DSPATR(HI)
     A            C1CELL    R        O  4 34REFFLD(@@CELL)
     A                                      EDTCDE(M)
     A                                  4 41'Group type:'
     A                                      DSPATR(HI)
     A            C1GTCD    R        O  4 53REFFLD(@@GTCD)
     A            C1GTDS    R        O  4 56REFFLD(GTREC/GTGTDS *LIBL/HSP025)
     A                                  6 39'Fall'
     A                                      DSPATR(HI)
     A                                  6 47'Born'
     A                                      DSPATR(HI)
     A                                  6 54'Piglet'
     A                                      DSPATR(HI)
     A                                  6 62'Nursery'
     A                                      DSPATR(HI)
     A                                  6 71'Finisher'
     A                                      DSPATR(HI)
     A                                  7  3'Farm'
     A                                      DSPATR(HI)
     A                                  7 19'Concpt'
     A                                      DSPATR(HI)
     A                                  7 28'Farrow'
     A                                      DSPATR(HI)
     A                                  7 40'Out'
     A                                      DSPATR(HI)
     A                                  7 46'Alive/'
     A                                      DSPATR(HI)
     A                                  7 55'Mort'
     A                                      DSPATR(HI)
     A                                  7 62'Removal'
     A                                      DSPATR(HI)
     A                                  7 72'Removal'
     A                                      DSPATR(HI)
     A                                  8  3'Site'
     A                                      DSPATR(HI)
     A                                  8  9'Services'
     A                                      DSPATR(HI)
     A                                  8 20'Rate'
     A                                      DSPATR(HI)
     A                                  8 29'Rate'
     A                                      DSPATR(HI)
     A                                  8 39'Rate'
     A                                      DSPATR(HI)
     A                                  8 46'Litter'
     A                                      DSPATR(HI)
     A                                  8 55'Rate'
     A                                      DSPATR(HI)
     A                                  8 64'Rate'
     A                                      DSPATR(HI)
     A                                  8 74'Rate'
     A                                      DSPATR(HI)
     A                                  9 21'%'
     A                                      DSPATR(HI)
     A                                  9 30'%'
     A                                      DSPATR(HI)
     A                                  9 40'%'
     A                                      DSPATR(HI)
     A                                  9 56'%'
     A                                      DSPATR(HI)
     A                                  9 65'%'
     A                                      DSPATR(HI)
     A                                  9 75'%'
     A                                      DSPATR(HI)
     A            C1FSCD    R        B 10  2REFFLD(HSRREF/@@FSCD *LIBL/HSPREF)
     A                                      EDTCDE(M)
     A                                      DSPATR(HI)
     A            C1TOSV    R        O 10 10REFFLD(HSRREF/@@TOSV *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A            C1CRPR    R        O 10 19REFFLD(HSRREF/@@CRPR *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A            C1FRPR    R        O 10 28REFFLD(HSRREF/@@FRPR *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A            C1FOPR    R        O 10 37REFFLD(HSRREF/@@FOPR *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A            C1LTBAHD  R        O 10 46REFFLD(HSRREF/@@LTBAHD *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A            C1PMPR    R        O 10 54REFFLD(HSRREF/@@PMPR *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A            C1NRMPR   R        O 10 63REFFLD(HSRREF/@@NRMPR *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A            C1FRMPR   R        O 10 73REFFLD(HSRREF/@@FRMPR *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A*
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R HP7083K1
     A*%%TS  SD  20000919  104024  ISLRAMS     REL-V4R4M0  5769-PW1
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A                                 23 11'F5=Refresh'
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *---------------------------------------------------------------
     A          R HP7083ES                  SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE CONTROL FORMAT
      *---------------------------------------------------------------
     A          R HP7083EC                  SFLCTL(HP7083ES)
      *
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
