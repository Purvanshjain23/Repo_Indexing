      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      HOG PRODUCTION
      * PROGRAM:     HP254 - RETRIEVAL OF ALPHA HOG GROUP CODE FOR
      *                      HOG PROCUREMENT AND EVALUATION SYSTEM
      * PROGRAMMER:  LEANNE FEDOR
      * CREATED:     09/11/95
      *
      * FUNCTION: THIS PROGRAM INTERFACES THE HOG PROCUREMENT AND
      *           EVALUATION SYSTEM TO THE HOG PRODUCTION SYSTEM.
      *
      *           ITS PURPOSE IS TO RETRIEVE THE ALPHA HOG GROUP CODE
      *           ASSOCIATED WITH THE NUMERIC HOG GROUP NUMBER.
      *           THIS PROGRAM IS CALLED BY A SYNON PROGRAM IN HPES.
      *
      *           A RETURN CODE IS INCLUDED AS A PARM BUT NEVER USED IN
      *           THIS PROGRAM.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 06/03/97  LEANNE FEDOR
      *           RECOMPILE ONLY. NEW FIELDS 'PURGE FLAG' AND 'DATE OF
      *           FIRST PURGE' ADDED TO HOG GROUP FILE.
      *
      * 06/28/01  LeAnne Fedor
      *           Recompile only.
      *           Manager and supervisor fields removed from Hog Group file.
      *
      * 07/06/09  LeAnne Ramsey
      *           Recompile only. Added new field 'Continuous Flow Flag' to Hog Group file.
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     FHSL034D   IF   E           K DISK
      *    HOG GROUP
      *
      /EJECT
      ****************************************************************
      * Definition Specifications
      ****************************************************************
      *
      *---------------------------------------------------------------
      * STANDARD PROGRAM STATUS DATA STRUCTURE
      *---------------------------------------------------------------
      *    EXTERNALLY DEFINED AS UTPGFR (RECORD FORMAT: PGMDSR)
     D PGMDS         ESDS                  EXTNAME(UTPGFR)
      *
      *---------------------------------------------------------------
      * DATA STRUCTURE FOR BREAKING THE 10-ALPHA HPES FIELD INTO
      * THE 7-ALPHA HOG GROUP CODE REQUIRED BY HPS.
      *---------------------------------------------------------------
      *
     D                 DS
     D  WK10                   1     10
     D  WK3                    1      3
     D  WKHGSN                 4     10
     D  WKNUM                  4     10  0
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * MAIN LINE
      ****************************************************************
      *
      * THE FIRST THREE POSITIONS OF THE INCOMING 10-ALPHA
      * FIELD MUST BE BLANK OR ZERO.
      *
     C     WK3           IFEQ      *BLANK                                       IF LEAD OK
     C     WK3           OREQ      '000'
      *
      * THE LAST SEVEN POSITIONS OF THE INCOMING 10-ALPHA
      * FIELD MUST BE NUMERIC. IF THE VALUE IS NUMERIC, USE IT TO
      * RETRIEVE THE HOG GROUP RECORD. THEN, MOVE THE ALPHA
      * HOG GROUP CODE TO THE LEADING SEVEN POSITIONS OF THE
      * 10-ALPHA PARM.
      *
     C                   TESTN                   WKHGSN               91
     C     *IN91         IFEQ      *ON                                          IF NUMERIC
      *
     C     WKNUM         CHAIN     HSL034D                            92
     C     *IN92         IFEQ      *OFF
     C                   MOVEL(P)  HGHGCD        XXCODE
     C                   ENDIF
      *
     C                   ENDIF                                                  IF NUMERIC
     C                   ENDIF                                                  IF LEAD OK
      *
      * EOF PROCESSING
     C                   SETON                                        LR
      /EJECT
      *---------------------------------------------------------------
      * INITIALIZATION SUBROUTINE
      *---------------------------------------------------------------
      *
     C     *INZSR        BEGSR
      *
      *  NOTE: THE HPES PROGRAM WILL SEND IN A 10-ALPHA FIELD WITH
      *        THE HOG GROUP NUMBER IN THE LAST 7 POSITIONS. THIS
      *        PROGRAM WILL RETRIEVE THE CORRESPONDING ALPHA HOG GROUP
      *        CODE AND SEND IT BACK IN THE FIRST 7 POSITIONS OF THE
      *        SAME 10-CHARACTER FIELD. IF THIS PROGRAM CANNOT RETRIEVE
      *        AN ALPHA CODE, THE PARM WILL SIMPLY RETURN WITH THE
      *        SAME VALUE THAT IT CAME IN WITH.
      *
      *        THE PARM 'RETURN' WILL ALWAYS BE BLANK.
      *
     C     *ENTRY        PLIST
     C                   PARM                    RETURN
     C     WK10          PARM                    XXCODE           10            INPUT/OUTPUT
      *
     C                   MOVE      *BLANK        RETURN            7
      *
     C                   ENDSR
      /EJECT
