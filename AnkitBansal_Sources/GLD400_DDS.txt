     A*%%TS  SD  20100122  081828  ISBGUTI     REL-V5R4M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        SEABOARD
     A*  DISPLAY FILE:  GLD400
     A*  TITLE:         SPECIFY G/L DOWNLOAD FOR TAX RETURNS
     A*  PROGRAMMER:    LEANNE RAMSEY
     A*  CREATED:       07/17/08
     A*
     A*  MODIFIED BY:   BARB GUTIERREZ   01/22/2010
     A*                 ADDED FM ENVIRONMENT AS A SELECTION.
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A                                      CF04(04 'PROMPT')
     A                                      CF08(08 'SUBMIT')
     A                                      CF20(20 'WRKSPLF')
     A*
     A*---------------------------------------------------------------
     A* RECORD FORMAT ONE
     A*---------------------------------------------------------------
     A          R GL400R1
     A*%%TS  SD  20100122  081828  ISBGUTI     REL-V5R4M0  5722-WDS
     A                                      SETOF(31)
     A                                      SETOF(32)
     A                                      SETOF(33)
     A                                      SETOF(34)
     A                                      SETOF(35)
     A                                      SETOF(36)
     A                                      SETOF(52)
     A                                      OVERLAY
     A                                      RTNCSRLOC(&RCD &FLD)
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM         10A  O  1  2DSPATR(HI)
     A                                  1 29'SEABOARD CORPORATION'
     A                                      DSPATR(HI)
     A            SDUSR         10A  O  1 70DSPATR(HI)
     A                                  2 21'SPECIFY G/L DOWNLOAD FOR TAX RETUR-
     A                                      NS'
     A                                      DSPATR(HI)
     A            SDJOB         10A  O  2 70DSPATR(HI)
     A                                  4  2'Environment ...'
     A            D1ENV          2A  B  4 20
     A  31                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A N31                                  DSPATR(HI)
     A                                  4 34'(FD=Foods, TR=Transportation, FM=F-
     A                                      oods Mexico)'
     A                                  5  2'Company .......'
     A                                  5 18'?'
     A            D1CONO         5Y 0B  5 20EDTCDE(M)
     A  32
     AO 52                                  DSPATR(PC)
     A  32                                  DSPATR(RI)
     A N32                                  DSPATR(HI)
     A                                  5 34'(Blank for All)'
     A                                  7  2'Thru quarter ..'
     A            D1QR           1Y 0B  7 20EDTCDE(M)
     A  33                                  DSPATR(PC)
     A  33                                  DSPATR(RI)
     A N33                                  DSPATR(HI)
     A                                  8  2'Year (CCYY) ...'
     A            D1YR           4Y 0B  8 20EDTCDE(M)
     A  34                                  DSPATR(PC)
     A  34                                  DSPATR(RI)
     A N34                                  DSPATR(HI)
     A                                 10  2'From account ..'
     A            D1FACT        15A  B 10 20
     A  35                                  DSPATR(PC)
     A  35                                  DSPATR(RI)
     A N35                                  DSPATR(HI)
     A                                 10 36'(0000 for All Accounts)'
     A                                 11  2'To account ....'
     A            D1TACT        15A  B 11 20
     A  36                                  DSPATR(PC)
     A  36                                  DSPATR(RI)
     A N36                                  DSPATR(HI)
     A                                 11 36'(9999 for All Accounts)'
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R GL400K1
     A*%%TS  SD  20080718  104344  ISLRAMS     REL-V5R4M0  5722-WDS
     A  43                             21  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A                                 23 11'F4=Prompt'
     A                                 23 22'F8=Submit'
     A                                 23 64'F20=Spool Files'
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *---------------------------------------------------------------
     A          R GL400ES                   SFL SFLMSGRCD(24)
      *
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
     A*---------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*---------------------------------------------------------------
     A*
     A          R GL400EC                   SFLCTL(GL400ES)
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
