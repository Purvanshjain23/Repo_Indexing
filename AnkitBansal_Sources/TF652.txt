      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Triumph Foods
      * PROGRAM:     TF652
      * TITLE:       Inventory: Freezer Charges Listing
      * PROGRAMMER:  LeAnne Ramsey
      * CREATED:     04/03/07
      *
      * FUNCTION:  Batch program to print a record from the Freezer Charges file.
      *
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 05/22/07  LeAnne Ramsey
      *           Changed the layout to have 2 columns:
      *             1) Not Co-Owned
      *             2) Co-Owned
      *
      * 12/12/07  LeAnne Ramsey
      *           Add new "freezer interest" section.
      *
      * 08/15/08  LeAnne Ramsey
      *           Removed the Holding Charges and Inflow Charges sections.
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Ftfp052    if   e           k disk
      *  Freezer charges
      *
      *
     Fprint132  o    f  132        printer oflind(*inof)
      *
      /EJECT
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Standard fields
      *
     d rtime           s              6  0
      *
      * Print fields
      *
     d h1demand        s             27
      *
      ****************************************************************
      * ARRAYS AND TABLES
      ****************************************************************
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *
      *---------------------------------------------------------------
      * Local data area.
      *---------------------------------------------------------------
      *
     Dlda             uds                  dtaara(*lda)
     D  ldpfcd                 1      1
     D  ldyr                   2      5  0
     D  ldwk                   6      7  0
      *
     D  ldwedt                29     36  0
     D  ldwemdy               44     49  0
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ***************************************************************************************
      * MAINLINE
      ***************************************************************************************
      *
     C                   except    h1hdr
      *
      * This function prints a single record from the file.
      *
     C     ldwedt        chain     tfp052                             92
     C                   if        *in92 = *on                                  If no hit
     C                   except    nodata
     C                   else
     C                   except    l1dtl
     C                   endif                                                  If no hit
      *
     C                   seton                                        lr
      /EJECT
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Retrieve time for report heading.
      *
     C                   time                    rtime
      *
      * This report is generated on-demand and also from the Inventory Close
      * function.
      *
     C                   select
     C                   when      ldpfcd = 'D'
     C                   eval      %subst(h1demand:10:9) = 'On-Demand'
      *
     C                   when      ldpfcd = 'F'
     C                   eval      %subst(h1demand:4:21) =
     C                             'Final Inventory Close'
      *
     C                   when      ldpfcd = 'P'
     C                   eval      %subst(h1demand:1:27) =
     C                             'Preliminary Inventory Close'
     C                   endsl
      *
     C                   endsr
      /EJECT
      *-------------------------------------------------------------
      * Report heading lines
      *-------------------------------------------------------------
      *
     Oprint132  e            h1hdr          1 01
     O                       sdpgm               10
     O                                           67 'FREEZER CHARGES LISTING'
     O                                          105 'DATE'
     O                       udate         y    115
      *
     O          e            h1hdr          1
     O                       sdusr               10
     O                       h1demand            69
     O                                          105 'TIME'
     O                       rtime              115 '  :  :  '
      *
      *
     O          e            h1hdr          1
     O                                          105 'PAGE'
     O                       page          z    115
      *
     O          e            h1hdr          1
     O                                           17 'Week-ending date:'
     O                       ldwemdy             27 '  /  /  '
     O          e            h1hdr          2
     O                                           17 'Year/week:'
     O                       ldyr          z     23
     O                       ldwk          z     27
      *
      *
      *-------------------------------------------------------------
      * Column Headings
      *-------------------------------------------------------------
      *
     O          e            h1hdr          1
     O                                           77 'Without'
     O                                           95 'Only'
      *
     O          e            h1hdr          1
     O                                           77 'Co-Owned'
     O                                           95 'Co-Owned'
      *
     O          e            h1hdr          1
     O                                           77 'Products'
     O                                           95 'Products'
      *
     O          e            h1hdr          1
     O                                           77 '--------------'
     O                                           95 '--------------'
      *-------------------------------------------------------------
      * Detail lines
      *-------------------------------------------------------------
      *
      * Holding Pounds
      *
     O          e            l1dtl       1  0
     O                                           19 'Holding'
     O          e            l1dtl          1
     O                                           19 'Holding'
     O          e            l1dtl          1
     O                                           29 'SBF Pounds'
     O                       fzsnhllb      k     78
     O                       fzsyhllb      k     96
      *
     O          e            l1dtl          1
     O                                           28 'TF Pounds'
     O                       fztnhllb      k     78
     O                       fztyhllb      k     96
      *
      * Inflow Pounds
      *
     O          e            l1dtl       1  0
     O                                           19 'Inflow'
     O          e            l1dtl          1
     O                                           19 'Inflow'
      *
     O          e            l1dtl          1
     O                                           29 'SBF Pounds'
     O                       fzsniflb      k     78
     O                       fzsyiflb      k     96
      *
     O          e            l1dtl          1
     O                                           28 'TF Pounds'
     O                       fztniflb      k     78
     O                       fztyiflb      k     96
      *
      * Cold Carcass
     O          e            l1dtl       1  0
     O                                           19 'Cold Carcass'
     O          e            l1dtl          1
     O                                           19 'Cold Carcass'
      *
     O          e            l1dtl          1
     O                                           29 'SBF Pounds'
     O                       fzscclb       k     60
      *
     O          e            l1dtl          1
     O                                           28 'TF Pounds'
     O                       fztcclb       k     60
      *
     O          e            l1dtl          1
     O                                           24 'SBF %'
     O                       fzsccpc             60 '   .    %'
      *
     O          e            l1dtl          1
     O                                           23 'TF %'
     O                       fztccpc             60 '   .    %'
      *
      * Freezer Interest
      *
     O          e            l1dtl       1  0
     O                                           19 'Freezer Interest'
     O          e            l1dtl          1
     O                                           19 'Freezer Interest'
      *
     O          e            l1dtl          1
     O                                           32 'Interest Rate'
     O                       fzfipc              62 '  0.    %'
      *
     O          e            l1dtl          1
     O                                           38 'SBF Inventory Value'
     O                       fzseiam       k     62
      *
     O          e            l1dtl          1
     O                                           37 'TF Inventory Value'
     O                       fzteiam       k     62
      *
     O          e            l1dtl          1
     O                                           43 'SBF Actual Interest Cost'
     O                       fzsfiactam    k     62
      *
     O          e            l1dtl          1
     O                                           42 'TF Actual Interest Cost'
     O                       fztfiactam    k     62
      *
     O          e            l1dtl          1
     O                                           42 'SBF Allocated Interest '
     O                                           46 'Cost'
     O                       fzsfiallam    k     62
      *
     O          e            l1dtl          2
     O                                           45 'TF Allocated Interest Cost'
     O                       fztfiallam    k     62
      *
     O          e            l1dtl          2
     O                                           34 'Due to SBF (TF)'
     O                       fzdufiam      k     62
      *
      *-------------------------------------------------------------
      * No data message line
      *-------------------------------------------------------------
      *
     O          e            nodata      1
     O                                           19 'No data meets your'
     O                                           39 'selection criteria.'
