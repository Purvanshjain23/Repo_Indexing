      *****************  RPG PROGRAM HEADING  ************************
      *
      * ENVIRONMENT:  PORK DIVISION
      * SYSTEM:       AS/400
      * PROGRAM:      MP302
      * DESCRIPTION:  MP2: Batch Edit Listing of Receipts
      * PROGRAMMER:   LeAnne Fedor
      * DATE:         03/19/02
      *
      *
      * FUNCTION:    This program edits the records in the purchase order header/detail files.
      *              There are  error flag fields in both files.
      *
      *              QPRINT is used for the output file since the command 'CLPRINTA'
      *              prints to QPRINT.  QPRINT is overridden in the submission CLP
      *              to 'SHARE = YES' to allows both this RPG program and the CLP
      *              UT80046A to write to the same print file.
      *
      *              Since the 'CLPRINTA' command logic in UT80046A is not being used
      *              to print headings, count lines or page break, these functions are
      *              controlled from this RPG program by counting the lines.
      *
      *              This program page-breaks when the line count reaches 62 lines
      *              because this listing is designed to run on paper that is 68 lines
      *              long.  Therefore, if the users generate this report on various
      *              laser printers with different page lengths, the page breaks may
      *              be goofy.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 03/12/08  LeAnne Ramsey
      *           We gave all functionality to High Plains Biodiesel. So,
      *           we now run over two different library lists. We added a Data Area
      *           holding Company Number. We will now print Company Number/Name at
      *           the top of the Report.
      *
      * 03/04/10  LeAnne Ramsey
      *           Barb G. added a new parm XXPEC (the posting edit code associated with the
      *           account number) to ARACTEDT.
      *
      * 03/30/11  Barb Gutierrez
      *           Recompile only due to field size changes in database.  E001398
      *
      * 03/05/14  LeAnne Ramsey (E2992)
      *           We will now be uploading MP2 data for the plant (Company 360). So, I am adding
      *           the Accounting Company to the PO Header (MPP102) and PO Detail (MPP103) files.
      *           We will use this new Accounting Company in all the places that, to date, we
      *           have used the Company that is in data area DAMP2CONO.
      *
      * 03/26/19  Brad Baden  (E14631)
      *           Recompile only due to field size changes in database.
      *
      * 10/08/19  Brad Baden  (E15590)
      *           After call to program ARACTEDT, if the return code is blank,
      *           set the error flag to 'N'.  This will allow the users to not
      *           have to update every record that was not posted to JDE. The
      *           user can run the Post Inventory Item to JDE for each company
      *           to automaticallu send records to JDE that have already been fixed.
      *
      * 05/24/21  Danny Nguyen   (S17068)
      *           DBFC on MPP103 file. PDITEMRT field length changed from 9.4 to 11.4.
      *           Recompile only.
      *
      /eject
      ******************************************************************************************
      * FILE SPECIFICATIONS
      ******************************************************************************************
      *
     Fcaabrel1  if   e           k disk
      *    Company
      *
      *
     Fmpp102    uf   e           k disk
      *  MP2: Purchase order header
      *
      *
     Fmpl103d   uf   e           k disk
      *  MP2: Purchase order detail  (records selected by open query)
      *
      *
     Fqprint    o    f  198        printer oflind(*inof)
      *
      /eject
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      ****************************************************************
      * ARRAYS AND TABLES
      ****************************************************************
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Standard fields
      *
     d dash            s            159    inz(*all'-')
     d rtime           s              6  0
     D maxmsg          s              2  0  inz(20)
     D msgfl           s             10
      *
      *
      * Control fields
      *
     D errorfl         s              1    inz('N')
     D first           s              1    inz('Y')
     d svacono         s                   like(pdacono)
      *
      *
      * Work fields for counting lines
      *
     d wkovfl          s              3  0 inz(62)
     d wkcurl          s              3  0
     d wkdifl          s              3  0
      *
      * Parm fields
      *
     d xxajd           s                   like(phmpvnno)
     D xxname          s                   like(phmpvnnm)
     D xxtype          s              3    inz('V  ')
     D xxerror         s              1    inz('N')
      *
     d xxactid         s              8
     d xxdesc          s             30
     d xxpec           s              1
     d xxreturn        s              7
      *
      * Print fields
      *
     d h1acono         s                   like(ldacono)
     d h1conm          s                   like(abadtx)
      *
      ****************************************************************
      * DATA AREA
      ****************************************************************
      *
      * Local data area.
      *
     Dlda             uds                  dtaara(*lda)
      *
     D  ldacono               86     88  0
     D  ldhold               411    411
     D  ldcopy               412    412  0
     D  ldoutq               413    422
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * standard error message handling data structures
      *---------------------------------------------------------------
      *
      * For error message handling, a packed index (3) is required.
      * For program readability, define a corresponding error count
      * field called 'error'
      *
     D                 ds                  inz
     D  error                         2p 0
     D   e                            2p 0 overlay(error)
      *
      * This data structure supplies the name of the message file to
      * the message handling CL program.  The field name MSGFIL must be
      * constant. The value in quotes is the name of the specific
      * message file containing the user defined messages.
      *
     D                 ds                  inz
     D  msgfil                       10    inz('HSMSGF    ')
      *
      *---------------------------------------------------------------
      *  Standard message data structures
      *---------------------------------------------------------------
      *
      * The following 3 data structures are used to speed message
      * handling since it is faster to clear data structures than
      * arrays.  Each is associated with a standard message array.
      *
     D mgi             ds                  inz
     D  mgid                          7    dim(20)
      *
     D mgd             ds                  inz
     D  mgdt                         50    dim(20)
      *
     D mgwk            ds                  inz
     D  mgw                           1    dim(50)
      *
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * MAINLINE
      ****************************************************************
      *
      * Process each record in the Purchase Order Detail file that has not been
      * posted to JDE...for the incoming Accounting Company. (records selected
      * by open query).
      *
     C     *loval        setll     mpl103d
     C                   dou       *in90 = *on                                  Main do
     C                   read      mpl103d                                90
     C                   if        *in90 = *off                                 If not EOF
      *
      * Control break
     C                   select
     C                   when      first = yes
     C                   exsr      $name
     C                   exsr      $prthdr
     C                   move      no            first
      *
     C                   when      svacono <> pdacono
     C                   if        errorfl = no
     C                   except    noerror
     C                   endif
     C                   exsr      $name
     C                   exsr      $prthdr
     C                   endsl
      *
      * Edit record and then print message(s) if it has errors.
      *
     C                   exsr      $edit
      *
     C                   if        error <> 0
     C                   exsr      $prtrec
     C                   exsr      $prtmsg
     C                   endif
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Main do
      *
      * EOF processing
      *
     C                   select
     C                   when      first = yes
     C                   exsr      $prthdr
     C                   except    nodata
      *
     C                   when      errorfl = no
     C                   except    noerror
     C                   endsl
      *
     C                   seton                                        lr
      /eject
      *---------------------------------------------------------------
      * Edit record
      *---------------------------------------------------------------
      *
     C     $edit         begsr
      *
      * Validate:
      *  1) accounting company (and cost center/account) in the detail record
      *  2) vendor number and accounting company in header record
      *
     C                   exsr      $acono
     C                   exsr      $vnno
      *
     C                   endsr
      /eject
      *-----------------------------------------------------------------------------------------
      * Validate accounting company
      *-----------------------------------------------------------------------------------------
      *
     C     $acono        begsr
      *
     C                   if        pdacono = 0
     C                   move      yes           pderfl
     C                   update    pdrec
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS09241'     mgid(e)
     C                   endif
     C                   else
     C                   exsr      $mcu
     C                   endif
      *
     C                   endsr
      /eject
      *-----------------------------------------------------------------------------------------
      * Validate account and cost center
      *-----------------------------------------------------------------------------------------
      *
     C     $mcu          begsr
      *
     C                   call      'ARACTEDT'
     C                   parm                    pdcono
     C                   parm                    pdmcu
     C                   parm                    pdobj
     C                   parm                    pdsub
     C                   parm      *blank        xxactid
     C                   parm      *blank        xxpec
     C                   parm      *blank        xxdesc
     C                   parm      *blank        xxreturn
      *
     C                   if        xxreturn <> *blank
     C                   move      yes           pderfl
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS09181'     mgid(e)
     C                   endif
      *
     ‚** 10/08/19 JBB E15590 - If returnxx is blank, set error flag to 'N'
     c                   else
     c                   eval      pderfl = no
     C                   endif                                                  fi xxreturn <> blank
      *
     C                   update    pdrec
      *
     C                   endsr
      /eject
      *-----------------------------------------------------------------------------------------
      * Validate Vendor Number
      *-----------------------------------------------------------------------------------------
      *
      * Validations:
      *    1) must be valid vendor in JDE
      *
     C     $vnno         begsr
      *
     C     pdposn        chain     mpp102                             92
     C                   if        *in92 = *off                                 If header
      *
      * Vendor Number
     C                   call      'HPJDEV'
     C                   parm                    mgi
     C                   parm                    mgd
     C                   parm                    error
     C                   parm      phjdvnno      xxajd
     C                   parm      'V  '         xxtype
     C     pherfl        parm      no            xxerror
     C                   parm      *blank        xxname
      *
     C                   if        xxerror = no
     C                   z-add     xxajd         phjdvnno
     C                   move      xxname        phjdvnnm
     C                   else
     C                   z-add     phmpvnno      phjdvnno
     C                   move      phmpvnnm      phjdvnnm
     C                   endif
      *
     C                   if        phacono = 0
     C                   move      yes           pherfl
     C                   endif
      *
     C                   update    phrec
     C                   endif                                                  If header
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Print report headings
      *---------------------------------------------------------------
      *
     C     $prthdr       begsr
      *
     C                   z-add     0             wkcurl
     C                   except    h1hdr
     C                   add       8             wkcurl
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Print record
      *---------------------------------------------------------------
      *
     C     $prtrec       begsr
      *
     C                   move      yes           errorfl
      *
     C                   if        wkcurl > wkovfl
     C                   exsr      $prthdr
     C                   endif
      *
      * Program UT80046A does not print headings or check for page breaks.
      * Before calling  UT80046A, determine if all 8 lines required for the
      * record will fit on the current page. Calculate the remaining lines
      * on the page using the overflow line and the current line.
      *
     C     wkovfl        sub       wkcurl        wkdifl
      *
      * If you have more messages than will fit on the page, then page break
      * and print headings.
      *
     C                   if        wkdifl < 8
     C                   exsr      $prthdr
     C                   endif
      *
     C                   except    l1dtl
     C                   add       1             wkcurl
      *
     C                   endsr
      /eject
      *-------------------------------------------------------------------
      * Print error messages
      *-------------------------------------------------------------------
      *
     C     $prtmsg       begsr
      *
     C                   if        error <> 0
     C                   exsr      $wrmsg
     C                   z-add     0             error
     C                   exsr      $clmsg
     C                   endif
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Write error messages
      *---------------------------------------------------------------
      *
     C     $wrmsg        begsr
      *
      * If overflow, print headings.
      *
     C                   if        wkcurl > wkovfl
     C                   exsr      $prthdr
     C                   endif
      *
      * Program UT80046A does not print headings or check for page breaks.
      * Before calling  UT80046A, determine if all the error messages can be
      * printed on the current page.  Calculate the remaining print lines
      * on the page using the overflow line and the current line.
      *
     C     wkovfl        sub       wkcurl        wkdifl
      *
      * If you have more messages than will fit on the page, then page break
      * and print headings.
      *
     C                   if        error > wkdifl
     C                   exsr      $prthdr
     C                   endif
      *
      * Increment the current line by the number of error messages you will be
      * printing in UT80046A plus one for the space after.  This sets the current
      * line number properly for when you return from UT80046A.
      *
     C     wkcurl        add       error         wkcurl
     C                   add       1             wkcurl
      *
     C                   call      'UT80046A'                           43
     C                   parm                    mgid
     C                   parm                    mgdt
     C                   parm                    error
     C                   parm      msgfil        msgfl
      *
     C                   endsr
      /eject
      *----------------------------------------------------------------
      * Clear messages
      *----------------------------------------------------------------
      *
     C     $clmsg        begsr
      *
     C                   call      'UT80046A'                           43
     C                   parm      *blank        mgi
     C                   parm      *blank        mgd
     C                   parm      *zero         error
     C                   parm      msgfil        msgfl
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Retrieve company name
      *---------------------------------------------------------------
      *
     C     $name         begsr
      *
     C                   if        pdacono = 0
     C                   eval      h1conm = 'Company is zero'
     C                   else
      *
     C     pdacono       chain     caabrel1                           92
     C                   if        *in92 = *off
     C                   eval      h1conm = abadtx
     C                   else
     C                   eval      h1conm = 'Unknown Company'
     C                   endif
     C                   endif
      *
     C                   z-add     pdacono       svacono
     C                   z-add     pdacono       h1acono
     C                   move      no            errorfl
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
     C                   time                    rtime
      *
     C                   if        ldacono <> 0
     C                   z-add     ldacono       h1acono
     C     ldacono       chain     caabrel1                           92
     C                   if        *in92 = *off
     C                   eval      h1conm = abadtx
     C                   else
     C                   eval      h1conm = 'Unknown Company'
     C                   endif
     C                   endif
      *
     C                   endsr
      /eject
      *-------------------------------------------------------------
      * Report heading lines
      *-------------------------------------------------------------
      *
     Oqprint    e            h1hdr          1 01
     O                       sdpgm               10
     O                                           85 'MP2 INTERFACE TO JDE'
     O                                          149 'DATE'
     O                       udate         y    159
      *
     O          e            h1hdr          1
     O                       sdusr               10
     O                                           82 'BATCH EDIT LISTING OF '
     O                                           90 'RECEIPTS'
     O                                          149 'TIME'
     O                       rtime              159 '  :  :  '
      *
     O          e            h1hdr          2
     O                                          149 'PAGE'
     O                       page          z    159
      *
      *
     O          e            h1hdr          2
     O                                           19 'Accounting Company:'
     O                       h1acono       z     24
     O                       h1conm              56
      *
     O          e            h1hdr          1
     O                                            8 'PURCHASE'
     O                                           18 'COMPANY'
     O                                           43 'ACCOUNT'
     O                                           69 'TRANSACTION'
     O                                           79 'RECEIPT'
     O                                           92 'SERIAL'
     O                                          105 'RECEIPT'
     O                                          117 'JDE VENDOR'
      *
     O          e            h1hdr          1
     O                                            5 'ORDER'
     O                                           18 'NUMBER'
     O                                           32 'COST CENTER'
     O                                           42 'OBJECT'
     O                                           56 'SUBSIDIARY'
     O                                           69 'TYPE'
     O                                           79 'NUMBER'
     O                                           92 'NUMBER'
     O                                          105 'DATE'
     O                                          115 'NUMBER'
     O                                          134 'JDE VENDOR NAME'
      *
     O          e            h1hdr          1
     O                       dash               159
      *
      *
      *-------------------------------------------------------------
      * Detail line
      *-------------------------------------------------------------
      *
     O          e            l1dtl       1
     O                       phpono               8
     O                       pdcono              18
     O                       pdmcu               33
     O                       pdobj               42
     O                       pdsub               54
     O                       pdttcd              67
     O                       pdrcno        z     79
     O                       pdserno       z     92
     O                       pdtrdt             105 '    -  -  '
     O                       phjdvnno           117
     O                       phjdvnnm           159
      *
      *-------------------------------------------------------------
      * No data message line
      *-------------------------------------------------------------
      *
     O          e            nodata      1
     O                                           20 'There was no receipt'
     O                                           34 ' data to edit.'
      *-------------------------------------------------------------
      * There were no errors
      *-------------------------------------------------------------
      *
     O          e            noerror     1
     O                                           21 'There were no errors.'
      /eject
