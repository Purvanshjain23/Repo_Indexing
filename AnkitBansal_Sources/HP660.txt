      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      HOG PRODUCTION
      * PROGRAM:     HP660
      * TITLE:       LISTING OF RATION ADDITIVE COSTS
      * PROGRAMMER:  LEANNE FEDOR
      * CREATED:     4/14/94
      *
      * FUNCTION: BATCH PROGRAM SUBMITTED FROM SPECIFY RATION ADDITIVE
      *           COSTS TO MAINTAIN (HP460).   COSTS WILL BE PRINTED
      *           FOR A SPECIFIC FEED MILL BUSINESS OFFICE AND
      *           EFFECTIVE DATE.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 06/18/97  LEANNE FEDOR
      *           REPLACED ENTRY PARM LOGIC WITH LDA VALUES.
      *
      * 05/31/07  LeAnne Ramsey
      *           Some users are having printing problems. So, we are switching
      *           from QPRINT to PRINT132 for the printer file and replacing
      *           John Erikson's customized printer override command in the CL with
      *           the standard override command.
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     FHSP009    IF   E           K DISK
      *    BUSINESS OFFICE
      *
     FHSL029A   IF   E           K DISK
      *    RATION COSTS
      *
     FHSP071    IF   E           K DISK
      *    RATIONS
      *
     FHSP072    IF   E           K DISK
      *    RATION ADDITIVE
      *
     Fprint132  O    F  132        PRINTER OFLIND(*INOF)
      *
      /EJECT
      ****************************************************************
      * INPUT SPECS
      ****************************************************************
      *
      *---------------------------------------------------------------
      * STANDARD PROGRAM STATUS DATA STRUCTURE
      *---------------------------------------------------------------
      *    EXTERNALLY DEFINED AS UTPGFR (RECORD FORMAT: PGMDSR)
     D PGMDS         ESDS                  EXTNAME(UTPGFR)
      *
      *---------------------------------------------------------------
      *  NAMED CONSTANTS
      *---------------------------------------------------------------
      *
     D YES             C                   CONST('Y')
     D NO              C                   CONST('N')
      *
      *
      *----------------------------------------------------------------
      * LDA - REPORT SELECTIONS
      *----------------------------------------------------------------
      *
     D                UDS
     D  LDRCDT                 1      8  0
     D  LDMDY                  9     14  0
     D  LDFMBO                15     19
      /EJECT
      ****************************************************************
      * MAIN LINE
      ****************************************************************
      *
      * PREPARE TO READ ALL RATION ADDITIVE COST RECORDS FOR THE
      * SELECTION CRITIERIA; PRINT HEADINGS FOR FIRST TIME THROUGH
      *
     C     K1L029        SETLL     HSL029A
     C                   EXCEPT    HDRPT
      *
     C     *IN92         DOUEQ     *ON                                          MAIN DO
     C     K1L029        READE     HSL029A                                92
     C     *IN92         IFEQ      *OFF                                         IF NOT EOF
      *
      * RETRIEVE RATION DESCRIPTION FROM RATION MASTER.
      *
     C     RCRNCD        CHAIN     HSP071                             93
     C     *IN93         IFEQ      *OFF
     C                   MOVEL     RNRNDS        WKRNDS
     C                   ELSE
     C                   MOVEL(P)  'UNKNOWN'     WKRNDS
     C                   ENDIF
      *
      * RETRIEVE RATION ADDITIVE DESCRIPTION FROM RATION/ADDITIVE
      * FILE--IF THERE IS AN ADDITIVE CODE.
      *
     C                   MOVE      *BLANK        WKADDS
     C     RCADCD        IFNE      *BLANK
     C     K2P072        CHAIN     HSP072                             94
     C     *IN94         IFEQ      *OFF
     C                   MOVEL     RAADDS        WKADDS
     C                   ELSE
     C                   MOVEL(P)  'UNKNOWN'     WKADDS
     C                   ENDIF
     C                   ENDIF
      *
      * TEST FOR OVERFLOW, AND PRINT HEADINGS IF ON
      *
     C     *INOF         IFEQ      *ON
     C                   EXCEPT    HDRPT
     C                   ENDIF
      *
      * PRINT DETAIL LINE
      *
     C                   EXCEPT    DTCOST
     C                   MOVE      YES           PRTFLG
      *
     C                   ENDIF                                                   IF NOT EOF
      *
      * PRINT STANDARD 'NO DATA...' ERROR MESSAGE IF NO DATA MET THE
      * USER'S SELECTION CRITERIA
      *
     C     PRTFLG        IFEQ      NO
     C                   EXCEPT    NODATA
     C                   ENDIF
      *
     C                   ENDDO                                                  MAIN DO
      *
     C                   SETON                                        LR
      /EJECT
      ****************************************************************
      * SUBROUTINES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * *INZSR - INITIALIZATION SUBROUTINE
      *---------------------------------------------------------------
      *
     C     *INZSR        BEGSR
      *
      *
      * KEY FOR RATION COST FILE
      *
     C     K1L029        KLIST
     C                   KFLD                    LDFMBO
     C                   KFLD                    LDRCDT
      *
      * KEY FOR RATION ADDITIVE FILE
      *
     C     K2P072        KLIST
     C                   KFLD                    RCRNCD
     C                   KFLD                    RCADCD
      *
      *
     C     *LIKE         DEFINE    BOBODS        WKBODS
     C     *LIKE         DEFINE    RNRNDS        WKRNDS
     C     *LIKE         DEFINE    RAADDS        WKADDS
      *
      * CHAIN TO BUSINESS OFFICE FILE FOR DESCRIPTION IN HEADING
      *
     C     LDFMBO        CHAIN     HSP009                             91
     C     *IN91         IFEQ      *OFF
     C                   MOVEL     BOBODS        WKBODS
     C                   ELSE
     C                   MOVEL(P)  'UNKNOWN'     WKBODS
     C                   ENDIF
      *
      * SET PRINT FLAG TO DETERMINE IF ANY RECORDS WERE PRINTED
      * SET UP HEADING UNDERLINE FIELD FOR PRINT
      * RETRIEVE TIME FOR REPORT HEADING
      *
     C                   MOVE      NO            PRTFLG            1
     C                   MOVE      *ALL'-'       DASH            132
     C                   TIME                    RTIME             6 0
      *
     C                   ENDSR
      /EJECT
      *-------------------------------------------------------------
      * REPORT HEADING LINES
      *-------------------------------------------------------------
      *
     Oprint132  E            HDRPT          1 01
     O                       SDPGM               10
     O                                           78 'HOG PRODUCTION SYSTEM'
     O                                          122 'DATE'
     O                       UDATE         Y    132
      *
     O          E            HDRPT          1
     O                       SDUSR               10
     O                                           70 'LISTING OF RATION '
     O                                           84 'ADDITIVE COSTS'
     O                                          122 'TIME'
     O                       RTIME              132 '  :  :  '
      *
     O          E            HDRPT          1
     O                                          122 'PAGE'
     O                       PAGE          Z    132
      *
     O          E            HDRPT          1
     O                                           16 'BUSINESS OFFICE:'
     O                       LDFMBO              23
     O                       BOBODS              55
      *
     O          E            HDRPT          2
     O                                           15 'EFFECTIVE DATE:'
     O                       LDMDY         Y     26
      *
     O          E            HDRPT          1
     O                                           83 '--- INGREDIENT COST ----'
     O                                          114 '--- MEDICATION COST ----'
      *
     O          E            HDRPT          1
     O                                            6 'RATION'
     O                                           38 'ADDITIVE'
     O                                           73 'BATCH'
     O                                           83 'POUND'
     O                                          104 'BATCH'
     O                                          114 'POUND'
      *
     O          E            HDRPT          1
     O                       DASH               132
      *
      *-------------------------------------------------------------
      * RATION COST DETAIL LINE
      *-------------------------------------------------------------
      *
     O          E            DTCOST      1
     O                       RCRNCD               5
     O                       WKRNDS              27
     O                       RCADCD              35
     O                       WKADDS              57
     O                       RCIBRT        K     74
     O                       RCILRT        K     84
     O                       RCMBRT        K    105
     O                       RCMLRT        K    115
      *
      *-------------------------------------------------------------
      * NO DATA MESSAGE LINE
      *-------------------------------------------------------------
      *
     O          E            NODATA      1
     O                                           19 'NO DATA MEETS YOUR'
     O                                           38 'SELECTION CRITERIA'
