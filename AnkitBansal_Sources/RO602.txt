      *****************  RPG PROGRAM HEADING  ************************
     h option(*SRCSTMT:*NODEBUGIO)
      ****************************************************************
      *
      * ENVIRONMENT: Pork Division
      * SYSTEM:      Resource Optimization
      * PROGRAM:     RO602 - Listing of Item Control Data
      * PROGRAMMER:  LeAnne Ramsey
      * CREATED:     09/25/06
      *
      * FUNCTION:    Print records from the Item Control Data file.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 11/21/08  LeAnne Ramsey
      *           As part of synchronizing the LDAs between the TFS Margin Adjustment Close
      *           and the Meat Costing, we changed the LDA positions.
      /eject
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fombyrel1  if   e           k disk
      *    Item Default
      *
      *
      *
     Frop012    if   e           k disk
      *  Item control data
      *
      *
     Fprint198  o    f  198        printer oflind(*inof)
      *
      /eject
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Standard fields
      *
     d rtime           s              6  0
     D dash            s            162    inz(*all'-')
      *
      *
      * Control fields
      *
     D first           s              1    inz('Y')
      *
      *
      * Print fields
      *
     d h1pfds          s             11
      *
     D l1itds          s                   like(byavna)
     D l1sritds        s                   like(byavna)
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *
      *---------------------------------------------------------------
      *  Local data area
      *---------------------------------------------------------------
      *
     d lda            uds                  dtaara(*lda)
     D  ldpfcd                 1      1
      *
     D  ldwedt                29     36  0
     D  ldwemdy               44     49  0
      *
     D  ldcono                82     84  0
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * Mainline
      ****************************************************************
      *
      * Print headings
     C                   exsr      $h1hdr

      * Process all Item Control Data records for the selected Company/Week.
      *
     C     key01         setll     rop012
      *
     C                   dou       *in90 = *on                                  Main do
     C     key01         reade     rop012                                 90
     C                   if        *in90 = *off                                 If not EOF
      *
     C                   if        first = yes
     C                   move      no            first
     C                   endif
      *
     C                   exsr      $l1dtl
     C                   endif                                                  If not EOF
     C                   enddo                                                  Main do
      *
      * EOF processing
      *
     C                   if        first = yes
     C                   except    nodata
     C                   endif
      *
     C                   seton                                        lr
      /eject
      *---------------------------------------------------------------
      * Print detail line
      *---------------------------------------------------------------
      *
     C     $l1dtl        begsr
      *
      * Retrieve Item Description
      *
     C     icitcd        chain     ombyrel1                           92
     C                   if        *in92 = *off
     C                   eval      l1itds  = byavna
     C                   else
     C                   eval      l1itds = 'Unknown'
     C                   endif
      *
      * Retrieve Source Item Description
      *
     C                   if        icsritcd <> 0
     C     icsritcd      chain     ombyrel1                           92
     C                   if        *in92 = *off
     C                   eval      l1sritds  = byavna
     C                   else
     C                   eval      l1sritds = 'Unknown'
     C                   endif
     C                   endif
      *
     C                   if        *inof = *on
     C                   exsr      $h1hdr
     C                   endif
     C                   except    l1dtl
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Print report headings
      *---------------------------------------------------------------
      *
     C     $h1hdr        begsr
      *
     C                   except    h1hdr
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Key lists
      *
     C     key01         klist
     C                   kfld                    ldcono
     C                   kfld                    ldwedt
      *
     C                   time                    rtime
      *
      * Retrieve 'description'
      *
     C                   select
     C                   when      ldpfcd = 'D'
     C                   eval      h1pfds = ' On-Demand '
      *
     C                   when      ldpfcd = 'P'
     C                   eval      h1pfds = 'Preliminary'
      *
     C                   when      ldpfcd = 'F'
     C                   eval      h1pfds = '   Final   '
     C                   endsl
      *
     C                   endsr
      /eject
      *-------------------------------------------------------------
      * Report lines
      *-------------------------------------------------------------
      *
     Oprint198  e            h1hdr          1 01
     O                       sdpgm               10
     O                                           90 'Resource Optimization '
     O                                           96 'System'
     O                                          152 'DATE'
     O                       udate         y    162
      *
      *
     O          e            h1hdr          1
     O                       sdusr               10
     O                                           95 'Item Control Data Listing'
     O                                          152 'TIME'
     O                       rtime              162 '  :  :  '
      *
     O          e            h1hdr          1
     O                       h1pfds              88
     O                                          152 'PAGE'
     O                       page          z    162
      *
      *
     O          e            h1hdr          1
     O                                           17 'Company:'
     O                       ldcono        z     22
      *
     O          e            h1hdr          2
     O                                           17 'Week-ending date:'
     O                       ldwemdy             27 '  /  /  '
      *
      *
      *-------------------------------------------------------------
      * Column headings
      *-------------------------------------------------------------
      *
     O          e            h1hdr          1
     O                                           48 'Source'
      *
     O          e            h1hdr          1
     O                                            7 'Item'
     O                                           48 'Item'
     O                                           97 'Difference'
     O                                          116 'Default/Override'
     O                                          129 'Multiplier'
     O                                          150 'Fresh Product'
      *
     O          e            h1hdr          1
     O                                            7 'Code'
     O                                           25 'Item Description'
     O                                           48 'Code'
     O                                           73 'Source Item Description'
     O                                           97 'Price/LB'
     O                                          104 'Code'
     O                                          116 'Base Price'
     O                                          128 'Percent'
     O                                          150 'Exception Flag'
      *
     O          e            h1hdr          0
     O                       dash               162
      *
      *-------------------------------------------------------------
      * Detail line
      *-------------------------------------------------------------
      *
     O          e            l1dtl       1
     O                       icitcd        m      8
     O                       l1itds              39
     O                       icsritcd      m     49
     O                       l1sritds            80
     O                       icdfpr        k     98
     O                       icdocd             104
     O                       icdobspr      k    117
     O                       icmupc        k    129
     O                       icfrxfl            142
      *
      /eject
      *-------------------------------------------------------------
      * No data line
      *-------------------------------------------------------------
      *
     O          e            nodata      2
     O                                           21 'No Item Control Data.'
      /eject
