      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      HOG PRODUCTION
      * PROGRAM:     HP369
      * TITLE:       BUILD WORKFILE FOR INVENTORY REPORTS
      * PROGRAMMER:  MARY PALMER
      * CREATED:     9/20/94
      *
      *  FUNCTION:   THIS BATCH PROGRAM IS CALLED FROM HP469CL.
      *              IT BUILDS WORKFILE HSP369 THAT IS USED IN REPORT
      *              PROGRAMS HP669 AND HP679.
      *
      *              THE USER'S SUBMISSION CRITERIA ARE PASSED IN THE
      *              LDA FROM HP469.  AN OPEN QUERY IS USED IN THE
      *              CL TO MAKE AN INITIAL SELECTION OF DATA.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 12/13/01  LeAnne Fedor
      *           Changed logic is use incoming parms instead of the LDA so that
      *           I build the workfile from the units upload function.
      *
      * 07/06/09  LeAnne Ramsey
      *           Recompile only. Added new field 'Continuous Flow Flag' to Hog Group file.
      *
      * 10/15/13  LeAnne Ramsey (E2831)
      *           Recompile only. Added field 'MTech Reference'.
      *
      * 03/26/20  Danny Nguyen - P405 - Farm Number Increase
      *           Recompile only. Increased Bin Code (@@BNCD) in HSPREF file from 5A to 6A.
      *
      * 05/10/22 Eric L SDN736 Recompiled ticket nbr increase (TKNO & RTNO 7.0 TO 9.0)
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     FHSP018    IF   E           K DISK
      *    FARM SITE
      *
      *
     FHSL034K   IF   E           K DISK
      *    HOG GROUP
      *
      *
     FHSL038E   IF   E           K DISK
      *   FEED TICKET DETAIL
      *
      *
     FHSP066    IF   E           K DISK
      *    SALE TYPE CODES
      *
      *
     FHSP084    IF   E           K DISK
      *  SALE MOVEMENT HEADER
      *
      *
     FHSL085B   IF   E           K DISK
      *  SALE MOVEMENT DETAIL
      *
      *
     FHSP369    O    E           K DISK
      *    WORKFILE
      /EJECT
      ****************************************************************
      * INPUT SPECS
      ****************************************************************
      *---------------------------------------------------------------
      *  NAMED CONSTANTS
      *---------------------------------------------------------------
      *
     D YES             C                   CONST('Y')
     D NO              C                   CONST('N')
      *
      *
      *---------------------------------------------------------------
      * STANDARD PROGRAM STATUS DATA STRUCTURE
      *---------------------------------------------------------------
      *    EXTERNALLY DEFINED AS UTPGFR (RECORD FORMAT: PGMDSR)
     D PGMDS         ESDS                  EXTNAME(UTPGFR)
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * MAINLINE
      ****************************************************************
      *
      * READ EACH HOG GROUP SELECTED BY THE OPNQRY
      *
     C     *IN90         DOUEQ     *ON                                          MAIN DO
     C                   READ      HSL034K                                90
     C     *IN90         IFEQ      *OFF                                         IF NOT EOF
      *
     C                   MOVEL     YES           PROCFL
      *
      * RETRIEVE THE CELL OF THE FARM THAT THIS GROUP IS ON;
      * AND, IF THE USER ELECTED TO SEE ONLY GROUPS FROM A
      * SPECIFIC CELL, ONLY CONTINUE PROCESSING IF THE GROUP IS
      * IN THAT CELL.
      *
     C     HGFSCD        CHAIN     HSP018                             92
     C     *IN92         IFEQ      *ON                                          IF BAD FARM
     C                   Z-ADD     0             WGCELL
     C                   MOVE      *BLANK        WGCNTY
     C                   MOVEL     NO            PROCFL
     C                   ELSE
     C                   Z-ADD     FSCELL        WGCELL
     C                   MOVE      FSCNTY        WGCNTY
      *
     C     XXCELL        IFNE      0                                            IF MISMATCH
     C     FSCELL        ANDNE     XXCELL
     C                   MOVEL     NO            PROCFL
     C                   ENDIF                                                  IF MISMATCH
     C                   ENDIF                                                  IF BAD FARM
      *
     C     PROCFL        IFEQ      YES                                          IF PROCESS
      *
      * RETRIEVE MOVEMENT INFO
      *
     C                   Z-ADD     HGHGSN        ZZHGSN
     C                   Z-ADD     XXCYMD        ZZCYMD
      *
     C                   CALL      'HPMOVE'
     C                   PARM                    ZZHGSN
     C                   PARM                    ZZCYMD
      * HEAD
     C                   PARM                    GPIHD
     C                   PARM                    PINHD
     C                   PARM                    TINHD
     C                   PARM                    TISPHD
     C                   PARM                    TIDPHD
     C                   PARM                    POUHD
     C                   PARM                    TOUHD
     C                   PARM                    TOSPHD
     C                   PARM                    TODPHD
     C                   PARM                    RINHD
     C                   PARM                    ROUHD
     C                   PARM                    QINHD
     C                   PARM                    QOUHD
     C                   PARM                    MORHD
     C                   PARM                    INAHD
      * POUNDS
     C                   PARM                    PINLB
     C                   PARM                    TINLB
     C                   PARM                    TISPLB
     C                   PARM                    TIDPLB
     C                   PARM                    POULB
     C                   PARM                    TOULB
     C                   PARM                    TOSPLB
     C                   PARM                    TODPLB
     C                   PARM                    RINLB
     C                   PARM                    ROULB
     C                   PARM                    QINLB
     C                   PARM                    QOULB
     C                   PARM                    MORLB
      *
      * CALCULATE THE ACTUAL DAYS IN THE PHASE
      *
     C                   EXSR      $DATE
      *
      * IF THE USER IS RUNNING THE REPORT 'BY AGE',
      *   IF AVERAGE DAYS OLD IN FOR THE GROUP IS NOT ZERO
      *     1) ADD AVERAGE DAYS OLD IN TO THE DAYS IN PHASE
      *   IF AVERAGE DAYS OLD IN FOR THE GROUP IS ZERO
      *     1) SET PHASE DAYS TO ZERO SO THE USER WILL
      *        ULTIMATELY SEE AN AGE OF ZERO ON THE REPORT
      *
     C     XXRPFL        IFEQ      'A'                                          IF AGE
     C     HGADOI        IFEQ      0
     C                   Z-ADD     0             PDAYS
     C                   ELSE
     C                   ADD       HGADOI        PDAYS
     C                   ENDIF
     C                   ENDIF                                                  IF AGE
      *
      * CONVERT ELAPSED DAYS IN THE PHASE TO WEEKS AND DAYS
      *
     C                   Z-ADD     0             WGWK
     C                   Z-ADD     0             WGDAY
     C     PDAYS         IFGT      0
     C     PDAYS         DIV       7             WGWK
     C                   MVR                     WGDAY
     C                   ENDIF
      *
      * EXECUTE A SUBROUTINE TO ACCUMULATE SALES FOR THE GROUP.
      *
     C                   EXSR      $SALE
      *
      *
      * EXECUTE A SUBROUTINE TO ACCUMULATE FEED POUNDS FOR A GROUP.
      *
     C                   EXSR      $FEED
      *
      * CALCULATE AVERAGE POUNDS TRANSFERRED IN
      *
     C     TINHD         IFNE      0
     C     TINLB         DIV(H)    TINHD         WGIALB
     C                   ELSE
     C                   Z-ADD     0             WGIALB
     C                   ENDIF
      *
      * MOVE FIELDS TO WORKFILE RECORD
      *
     C                   Z-ADD     HGHGSN        WGHGSN
     C                   MOVEL     HGHGCD        WGHGCD
     C                   MOVEL     HGBLCD        WGBLCD
     C                   MOVEL     HGRMCD        WGRMCD
     C                   MOVEL     HGFSBO        WGFSBO
     C                   MOVEL     HGPTCD        WGPTCD
     C                   MOVEL     HGPPCD        WGPPCD
     C                   MOVEL     HGGTCD        WGGTCD
     C                   Z-ADD     HGFSCD        WGFSCD
     C                   MOVEL     HGGNCD        WGGNCD
      *
     C                   Z-ADD     TISPHD        WGISHD
     C                   Z-ADD     TIDPHD        WGIDHD
     C                   Z-ADD     TISPLB        WGISLB
     C                   Z-ADD     TIDPLB        WGIDLB
     C                   Z-ADD     MORHD         WGDDHD
     C                   Z-ADD     TOSPHD        WGOSHD
     C                   Z-ADD     TODPHD        WGODHD
     C                   Z-ADD     INAHD         WGAJHD
     C                   Z-ADD     GPIHD         WGEIHD
      *
     C                   Z-ADD     WKCUHD        WGCUHD
     C                   Z-ADD     WKMKHD        WGSLHD
      *
     C                   WRITE     WGREC
     C                   CLEAR                   WGREC
      *
     C                   ENDIF                                                  IF PROCESS
     C                   ENDIF                                                  IF NOT EOF
     C                   ENDDO                                                  MAIN DO
      *
      * CALL UTILITY PROGRAM TO RECLAIM RESOURCES--CLOSE FILES
      * AND CLEANUP AFTER THE GENERIC PROGRAMS THAT DO NOT SETON
      * THE LAST RECORD INDICATOR.
      *
     C                   CALL      'UTRCRSC'
     C                   SETON                                        LR
      /EJECT
      ****************************************************************
      * $FEED - ACCUMULATE FEED POUNDS FOR A GROUP
      ****************************************************************
      *
      * ONLY ACCUMULATE FEED IF THE FEED TICKET DATE IS ON OR BEFORE
      * THE CUTOFF DATE SELECED BY THE USER.
      *
     C     $FEED         BEGSR
      *
     C                   Z-ADD     0             WGFDLB
      *
     C     HGHGSN        SETLL     HSL038E
     C                   SETOFF                                       96
      *
     C     *IN96         DOUEQ     *ON                                          DO FEED
     C     HGHGSN        READE     HSL038E                                96
     C     *IN96         IFEQ      *OFF                                         IF NOT EOF
     C     FDTKDT        ANDLE     XXCYMD
     C                   ADD       FDFDLB        WGFDLB
     C                   ENDIF                                                  IF NOT EOF
     C                   ENDDO                                                  DO FEED
      *
      * CALCULATE AVERAGE FEED POUNDS CONSUMED PER
      * HEAD TRANSFERRED IN.
      *
     C     TINHD         IFNE      0
     C     WGFDLB        DIV(H)    TINHD         WGFALB
     C                   ELSE
     C                   Z-ADD     0             WGFALB
     C                   ENDIF
      *
     C                   ENDSR
      /EJECT
      ****************************************************************
      * $SALE - RETRIEVE SALES INFORMATION FOR THE GROUP
      ****************************************************************
      *
     C     $SALE         BEGSR
      *
     C                   Z-ADD     0             WKCUHD
     C                   Z-ADD     0             WKMKHD
      *
      * READ ALL SALES MOVEMENT DETAIL RECORDS FOR THE SELECTED HOG
      * GROUP. (THERE WILL ONLY BE ONE RECORD PER GROUP PER MOVEMENT.)
      *
     C     HGHGSN        SETLL     HSL085B
      *
     C     *IN93         DOUEQ     *ON                                          DO SALES
     C     HGHGSN        READE     HSL085B                                93
     C     *IN93         IFEQ      *OFF                                         IF NOT EOF
      *
      * RETRIEVE THE HEADER FOR THIS SALES MOVEMENT TO GET
      * THE MOVEMENT STATUS, SHIPPED DATE AND RECEIVED DATE.
      *
     C     SGMVSN        CHAIN     HSP084                             92
     C     *IN92         IFEQ      *OFF                                         IF HEADER
      *
      * DETERMINE WHETHER TO PROCESS THIS SALES DETAIL
      * RECORD BY COMPARING EITHER THE SHIP DATE OR THE RECEIVED
      * DATE TO THE CUT-OFF DATE--DEPENDING ON THE MOVEMENT STATUS.
      *
     C                   SELECT
      *
     C     SHMSCD        WHENEQ    'SC'
      *
     C     SHMSCD        WHENEQ    'SH'
     C     SHSHDT        ANDLE     XXCYMD
     C                   EXSR      $SCFL
     C     COSTFL        IFEQ      'C'
     C                   ADD       SGSHHD        WKCUHD
     C                   ELSE
     C                   ADD       SGSHHD        WKMKHD
     C                   ENDIF
      *
     C     SHMSCD        WHENEQ    'NC'
     C     SHRCDT        ANDLE     XXCYMD
     C                   EXSR      $SCFL
     C     COSTFL        IFEQ      'C'
     C                   ADD       SGSHHD        WKCUHD
     C                   ELSE
     C                   ADD       SGSHHD        WKMKHD
     C                   ENDIF
      *
     C     SHMSCD        WHENEQ    'FP'
     C     SHMSCD        OREQ      'PP'
     C     SHMSCD        OREQ      'PG'
      *
     C     SHRCDT        IFLE      XXCYMD
     C                   EXSR      $SCFL
     C     COSTFL        IFEQ      'C'
     C                   ADD       SGLVHD        WKCUHD
     C                   ELSE
     C                   ADD       SGLVHD        WKMKHD
     C                   ENDIF
     C                   ENDIF
     C                   ENDSL
      *
     C                   ENDIF                                                  IF HEADER
     C                   ENDIF                                                  IF NOT EOF
     C                   ENDDO                                                  DO SALES
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * $SCFL - RETRIEVE SALE/COST FLAG ASSOCIATED WITH SALE TYPE CODE
      *---------------------------------------------------------------
      *
     C     $SCFL         BEGSR
      *
      * TO ACCUMULATE HEAD AND POUNDS APPROPRIATELY, YOU
      * MUST KNOW WHETHER THE USER VIEWS THIS SALE TYPE AS A REDUCTION
      * IN THE 'COST OF SALES' OR AS 'SALES REVENUE'. YOU DETERMINE
      * THIS BY RETRIEVING THE SALE/COST FLAG FOR THIS SALE
      * TYPE CODE.
      *
     C     SHSTCD        CHAIN     HSP066                             92
     C     *IN92         IFEQ      *OFF
     C                   MOVEL     STSCFL        COSTFL
     C                   ELSE
     C                   MOVE      *BLANK        COSTFL
     C                   ENDIF
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * $DATE -  MANIPULATE DATES WITH DATE UTILITY
      *---------------------------------------------------------------
      *
     C     $DATE         BEGSR
      *
     C                   Z-ADD     HGOPDT        PFRM8
     C                   Z-ADD     XXCYMD        PTO8
     C                   MOVEL     'CYMD'        PFRFMT
     C                   MOVEL     'CYMD'        PTOFMT
     C                   Z-ADD     0             PDAYS
     C                   MOVEL     'W'           PCODE
     C                   MOVE      *BLANK        PRTRN
      *
     C                   CALL      'UT80060R'
     C                   PARM                    PFRM8             8 0
     C                   PARM                    PTO8              8 0
     C                   PARM                    PFRFMT            4
     C                   PARM                    PTOFMT            4
     C                   PARM                    PDAYS             4 0
     C                   PARM                    PCODE             1
     C                   PARM                    PRTRN             1
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * *INZSR - INITIALIZATION SUBROUTINE
      *---------------------------------------------------------------
      *
     C     *INZSR        BEGSR
      *
      *
     C     *entry        plist
     C                   parm                    xxrpfl            1
     C                   parm                    xxcell            2 0
     C                   parm                    xxcymd            8 0
      *
      *
      * CONTROL PROCESSING FLAG
      *
     C                   MOVEL     YES           PROCFL            1
     C                   MOVE      *BLANK        COSTFL            1
      *
      * WORKFIELDS
     C     *LIKE         DEFINE    SGSHHD        WKCUHD
     C     *LIKE         DEFINE    SGSHHD        WKMKHD
      *
      * DEFINE PARAMETERS FOR THE MOVEMENT PROGRAM (HPMOVE)
      *
     C     *LIKE         DEFINE    HGHGSN        ZZHGSN
     C     *LIKE         DEFINE    XXCYMD        ZZCYMD
      * HEAD
     C                   Z-ADD     0             @@NU50            5 0
     C     *LIKE         DEFINE    @@NU50        GPIHD
     C     *LIKE         DEFINE    @@NU50        PINHD
     C     *LIKE         DEFINE    @@NU50        TINHD
     C     *LIKE         DEFINE    @@NU50        TISPHD
     C     *LIKE         DEFINE    @@NU50        TIDPHD
     C     *LIKE         DEFINE    @@NU50        POUHD
     C     *LIKE         DEFINE    @@NU50        TOUHD
     C     *LIKE         DEFINE    @@NU50        TOSPHD
     C     *LIKE         DEFINE    @@NU50        TODPHD
     C     *LIKE         DEFINE    @@NU50        RINHD
     C     *LIKE         DEFINE    @@NU50        ROUHD
     C     *LIKE         DEFINE    @@NU50        QINHD
     C     *LIKE         DEFINE    @@NU50        QOUHD
     C     *LIKE         DEFINE    @@NU50        MORHD
     C     *LIKE         DEFINE    @@NU50        INAHD
      *
     C                   Z-ADD     0             @@NU92            9 2
     C     *LIKE         DEFINE    @@NU92        PINLB
     C     *LIKE         DEFINE    @@NU92        TINLB
     C     *LIKE         DEFINE    @@NU92        TISPLB
     C     *LIKE         DEFINE    @@NU92        TIDPLB
     C     *LIKE         DEFINE    @@NU92        POULB
     C     *LIKE         DEFINE    @@NU92        TOULB
     C     *LIKE         DEFINE    @@NU92        TOSPLB
     C     *LIKE         DEFINE    @@NU92        TODPLB
     C     *LIKE         DEFINE    @@NU92        RINLB
     C     *LIKE         DEFINE    @@NU92        ROULB
     C     *LIKE         DEFINE    @@NU92        QINLB
     C     *LIKE         DEFINE    @@NU92        QOULB
     C     *LIKE         DEFINE    @@NU92        MORLB
      *
     C                   ENDSR
      /EJECT
