     A*%%TS  SD  20090922  162015  ISLRAMS     REL-V5R4M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION
     A*  DISPLAY FILE:  HPD4131
     A*  TITLE:         WORK WITH FARM FEED TICKET OVERRIDES
     A*  PROGRAMMER:    LEANNE RAMSEY
     A*  CREATED:       09/23/09
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A                                      CF05(05 'REFRESH')
     A                                      CF06(06 'CREATE')
     A                                      CF20(20 'WRKSPLF')
     A*
     A*---------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP4131S1                  SFL
     A*%%TS  SD  20090922  151716  ISLRAMS     REL-V5R4M0  5722-WDS
     A  85                                  SFLNXTCHG
     A            HDRRN1         4Y 0H
     A            D1OPT          2A  B  9  2CHGINPDFT(UL)
     A  30                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A N30                                  DSPATR(HI)
     A            D1FFSCD   R        O  9  8REFFLD(@@FFSCD)
     A                                      EDTCDE(M)
     A            D1TFSCD   R        O  9 18REFFLD(@@TFSCD)
     A                                      EDTCDE(M)
     A            D1BNCD    R        O  9 26REFFLD(HSRREF/@@BNCD *LIBL/HSPREF)
     A*
     A*---------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP4131C1                  SFLCTL(HP4131S1)
     A*%%TS  SD  20090922  153649  ISLRAMS     REL-V5R4M0  5722-WDS
     A                                      SFLSIZ(0050)
     A                                      SFLPAG(0013)
     A N82                                  ROLLUP(25 'PAGE FORWARD')
     A                                      OVERLAY
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 26'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  2 18'WORK WITH FARM FEED TICKET OVERRID-
     A                                      ES'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  4  2'2=Revise'
     A                                  4 12'4=Delete'
     A                                  6  2'Opt'
     A                                      DSPATR(HI)
     A                                  6 18' Override To '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  7  9'Farm'
     A                                      DSPATR(HI)
     A                                  7 19'Farm'
     A                                      DSPATR(HI)
     A                                  8  9'Site'
     A                                      DSPATR(HI)
     A                                  8 19'Site'
     A                                      DSPATR(HI)
     A                                  8 26'Bin'
     A                                      DSPATR(HI)
     A*
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R HP4131K1
     A*%%TS  SD  20090922  162015  ISLRAMS     REL-V5R4M0  5722-WDS
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A                                 23 11'F5=Refresh'
     A                                 23 23'F6=Create'
     A                                 23 64'F20=Spool Files'
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *---------------------------------------------------------------
     A          R HP4131ES                  SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE CONTROL FORMAT
      *---------------------------------------------------------------
     A          R HP4131EC                  SFLCTL(HP4131ES)
      *
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
