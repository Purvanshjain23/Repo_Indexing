     **
     **  PROGRAM NUMBER  -> EC201
     **
     **  PROGRAM NAME    -> DELETE INVOICE WORK FILES
     **
     **  PROGRAM PURPOSE -> THIS PROGRAM DELETES THE INVOICE FROM THE
     **                     WORK FILE FOR RESEND
     **
     **  PROGRAMMER      -> PURVA DROGE             DATE  -> 11/06/00
     **
     **  MODIFIED BY        SHARON ZURN  04/11/08
     **                     CHANGED RCD FORMAT ECR003
     **
     **  MODIFIED BY        SHARON ZURN  10/16/13
     **                     CHANGED LOGICAL ECP001L1 TO ECP001L7.
     **                     A NEW SELECT WAS PUT ON ECP001L1 TO SELECT
     **                     ONLY RCDS WITH A BLANK FILE SET NAME.
     **
     **  MODIFIED BY        SHARON ZURN  02/10/16
     **                     CHANGED LOGICAL ECP001L7 TO ECP001LC.
     **                     ECP001L7 SELECTED ONLY NOT PROCESSED.  IT
     **                     NEEDS TO SELECT ANY HEADER THAT IS ALREADY
      *                     IN ECP001 REGARDLESS OF WHETHER IT IS
      *                     PROCESSED OR NOT.  IT WAS CAUSING TWO
      *                     HEADER RCDS TO BE IN ECP001.
     **
     **  MODIFIED BY        SHARON ZURN  04/11/16
     **                     ADDED CODE TO DELETE THE ALLOW/CHARGES THAT
     **                     GO WITH THIS INVOICE (IF ANY).
     **
     **  MODIFIED BY        DANNY NGUYEN 06/25/18 S13326
     **                     DBF CHANGE ON EDI SAC/G72 ALLOW/CHG DTL (ECP002D).
     **                     ADDED THE FOLLOWING AS PRIMARY KEYS:
     **                       IT101-PO LINE ITEM0049049
     **                       ITD SEQUENCE NUMBER
     ******************************************************************
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     FECP001LCUF  E           K        DISK
      *EDI INVOICE HEADER WORK FILE
      *
     FECP002L1UF  E           K        DISK
      *EDI INVOICE DETAIL WORK FILE
      *
     FECP003L1UF  E           K        DISK
      *EDI INVOICE NAME WORK FILE
      *
     FECP001D0UF  E           K        DISK
      *EDI INVOICE HEADER ALLOWANCE/CHARGES WORK FILE
      *
     FECP002D0UF  E           K        DISK
      *EDI INVOICE DETAIL ALLOWANCE/CHARGES WORK FILE
      *
      ****************************************************************
      * MAIN LINE
      ****************************************************************
      *
      *DELETE INVOICE HEADER
      *
     C                     Z-ADDPPOSUR    BPH9NY
      *
     C           KEYEC1    CHAINECP001LC             99
     C           *IN99     IFEQ *OFF
     C                     DELET@P001TW
     C                     ENDIF
      *
      *DELETE INVOICE DETAIL
      *
     C                     Z-ADDPPOSUR    DPOSUR
     C           KEYEC2    SETLLECP002L1
     C           *IN99     DOUEQ*ON                        DO READE
     C           KEYEC2    READEECP002L1                 99
     C           *IN99     IFEQ *OFF                       IF REC READ
     C                     DELETECR002
     C                     ENDIF
     C                     ENDDO                           END DO
      *
      *DELETE INVOICE SUMMARY
      *
     C                     Z-ADDPPOSUR    NPOSUR
     C           KEYEC3    SETLLECP003L1
     C           *IN99     DOUEQ*ON                        DO READE
     C           KEYEC3    READEECP003L1                 99
     C           *IN99     IFEQ *OFF                       IF REC READ
     C                     DELETECR003
     C                     ENDIF
     C                     ENDDO                           END DO
      *
      *DELETE INVOICE HEADER ALLOW/CHARGES
      *
     C                     Z-ADDPPOSUR    KYH9NY  90
      *
     C           KYH9NY    SETLLECP001D0
     C           *IN99     DOUEQ*ON                        DO READE
     C           KYH9NY    READEECP001D0                 99
     C           *IN99     IFEQ *OFF
     C                     DELET@AXCPPH
     C                     ENDIF
     C                     ENDDO                           END DO
      *
      *DELETE INVOICE DETAIL ALLOW/CHARGES
      *
     C           KYH9NY    SETLLECP002D0
     C           *IN99     DOUEQ*ON                        DO READE
     C           KYH9NY    READEECP002D0                 99
     C           *IN99     IFEQ *OFF                       IF REC READ
     C                     DELET@AYCPPK
     C                     ENDIF
     C                     ENDDO                           END DO
      *END OF PROGRAM
     C                     RETRN
      *---------------------------------------------------------------
      * *INZSR - INITIALIZATION SUBROUTINE
      *---------------------------------------------------------------
      *
     C           *INZSR    BEGSR
      *
      * KEY FOR FILE ECP001
      *
     C           KEYEC1    KLIST
     C                     KFLD           BPH9NY
      *
      * KEY FOR FILE ECP002
      *
     C           KEYEC2    KLIST
     C                     KFLD           DPOSUR
      *
      * KEY FOR FILE ECP003
      *
     C           KEYEC3    KLIST
     C                     KFLD           NPOSUR  90
      *
      * PARM LISTS
      *
      *
     C           *ENTRY    PLIST
     C                     PARM           PCO     30
     C                     PARM           PPOSUR  70
      *
     C                     ENDSR
