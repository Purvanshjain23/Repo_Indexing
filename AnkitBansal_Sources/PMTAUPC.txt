/********************************************************************/
/*                                                                  */
/*  SYNOPSIS -  PRINT BROKER PERFORMANCE REPORTS                    */
/*              CALLED FROM JOB SCHEDULER ONCE FOR SLC AND THEN MSL */
/*  SYSTEM    - ORDER MANAGEMENT SYSTEM                             */
/*  DATE      - 03/27/08 SHARON ZURN                                */
/*                                                                  */
/*      PARAMETERS:                                                 */
/*     O    &RETURN     - RETURN CODE                               */
/*     I    &OUTQ       -  OUTPUT QUEUE                             */
/*     I    &HOLD       -  HOLD STATUS                              */
/*     I    &SAVE       -  SAVE STATUS                              */
/*     I    &COPYN      -  NO. OF COPIES (PASSED)                   */
/*     I    &CO         -  COMPANY BEING PROCESSED                  */
/*                                                                  */
/********************************************************************/
/* MODIFICATIONS                                                    */
/********************************************************************/
 PMTAUPC:    PGM        PARM(&RETURN &OUTQ &HOLD &SAVE &COPY &CO)
/* PARMS                                                                      */
       DCL    &BDATE       *DEC     (7 0)
       DCL    &EDATE       *DEC     (7 0)
       DCL    &EDATEA      *CHAR    7              /*  TO   DT ALPHA */
       DCL    &BDATEA      *CHAR    7              /*  FROM DT ALPHA */
       DCL    &CURDAT      *CHAR   (6)
       DCL    &CURDATA     *CHAR   (7)
       DCL    &WEEK        *DEC    (3 0)
       DCL    &ACTPER      *DEC    (2 0)            /* ACCTNG MO   */
       DCL    &ACTYR       *DEC    (4 0)            /* ACCTNG YR    */

       DCL    &RETURN      *CHAR   (7)           /* Return Code               */
       DCL    &COPY        *DEC    (2 0)         /* Number of copies          */
       DCL    &OUTQ        *CHAR   (10)          /* Output Queue    */
       DCL    &HOLD        *CHAR   (4)           /* Hold Status     */
       DCL    &SAVE        *CHAR   (4)           /* Save Status     */

       DCL    &ACCR        *CHAR   (3) /* ACCRUAL CODE*/
       DCL    &BROKER      *CHAR   (6) /* BROKER CODE*/
       DCL    &PAYEE       *CHAR   (2)             /* PAYEE TYPE*/
       DCL    &CO          *DEC    (3 0)           /* COMPANY        */
       DCL    &COA         *CHAR   (3)             /* COMPANY ALPHA  */
       DCL    &SHIP        *DEC    (7 0)           /*  SHIP TO       */
       DCL    &SHIPTOA     *CHAR   7               /* SHIP TO ALPHA  */
       DCL    &PRINTYN     *CHAR   (1)             /* PRINT OR HIDE*/
       DCL    &PTYP        *CHAR   (1)             /* PRT TYP */
       DCL    &ACCRTYP     *CHAR   2               /* ACCRUAL TYPE   */

       DCL    &A           *CHAR   20              /* QRY ACT SHIP FROM*/
       DCL    &B           *CHAR   25              /* QRY ACT SHP TO */
       DCL    &C           *CHAR   25              /* QRY COMPANY    */
       DCL    &E           *CHAR   25              /* QRY ACCRL TYPE */
       DCL    &F           *CHAR   25              /* QRY CUSTOMER   */
       DCL    &D           *CHAR   120             /* QRY ALL        */
/* ERROR MESSAGE FIELDS                                                       */
        DCL        VAR(&MSG) TYPE(*CHAR) LEN(512) /* +
                          message                   */
        DCL        VAR(&MSGID) TYPE(*CHAR) LEN(7) /* message +
                      id                */
        DCL        VAR(&MSGL) TYPE(*DEC) LEN(5 0)
        DCL        VAR(&COMMAND) TYPE(*CHAR) LEN(512)

/********************************************************************/
/*   INITIALIZE VARIABLES                                           */
/********************************************************************/
             CHGVAR     VAR(&PRINTYN) VALUE('N')
             CHGVAR     VAR(&ACCRTYP) VALUE('CR')
             CHGVAR     VAR(&PTYP) VALUE('M') /* Date calc */
             CHGVAR     VAR(&RETURN) VALUE('       ')
/*********************************************************************/
/*   MONITOR FOR ALL MESSAGES AT PROGRAM LEVEL                       */
/*********************************************************************/

             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(MSGTRAP))

/********************************************************************/
/*GET BEGIN AND END DATES FOR PERIOD                                 */
/********************************************************************/

             RTVSYSVAL  SYSVAL(QDATE) RTNVAR(&CURDAT)
             CVTDAT     DATE(&CURDAT) TOVAR(&CURDATA) FROMFMT(*MDY) +
                          TOFMT(*CYMD) TOSEP(*NONE)

             CALL       PGM(PMI2XFR) PARM(' ' &CO &PTYP &BDATE &EDATE)

             CALL       PGM(PDT3XFR) PARM(' ' &CO &BDATE &ACTYR +
                          &ACTPER &WEEK)
/********************************************************************/
/*   CALL OVRPRTF                                                  */
/********************************************************************/
             SBDPRTOVR  CMDVAR(&COMMAND) FILE(PPBEPFR$) +
                          OUTQ(&OUTQ) COPIES(&COPY) HOLD(&HOLD) +
                          SAVE(&SAVE)
             CALL       PGM(QCMDEXC) PARM(&COMMAND 512)
/********************************************************************/
/*CALL BROKER STATEMENTS PER PAYEE TYPE USING DRIVER OVER BROKER FILE*/
/********************************************************************/
/*PRINT BUYER GROUP                                                 */
/********************************************************************/

             CHGVAR     VAR(&PAYEE) VALUE('BG')

             CALL       PGM(PMLYXFR) PARM(' '  &BROKER &PAYEE +
                           &BDATE &EDATE &SHIP &ACCR &PRINTYN &CO)

/********************************************************************/
/*PRINT BROKER PERFORMANCE                                          */
/********************************************************************/
             CHGVAR     VAR(&PAYEE) VALUE('BR')

             CALL       PGM(PMLYXFR) PARM(' '  &BROKER &PAYEE +
                           &BDATE &EDATE &SHIP &ACCR &PRINTYN &CO)
/********************************************************************/
/*PRINT CUSTOMER ACCRUAL STATEMENT                                  */
/********************************************************************/
             CHGVAR     VAR(&BDATEA) VALUE(&BDATE) /* Change numeric +
                          date to alpha */
             CHGVAR     VAR(&EDATEA) VALUE(&EDATE) /* Change numeric +
                          to date to alpha */
             CHGVAR     VAR(&SHIPTOA) VALUE(&SHIP) /* Change +
                          numeric ship to to alpha */
             CHGVAR     VAR(&COA) VALUE(&CO) /* Change +
                          numeric CO to to alpha */

/*********************************************************************/
/*   CREATE OPEN QUERY FILE TO BE USED FOR REPORT                    */
/*********************************************************************/

             CHGVAR     VAR(&A) VALUE('ICPTDT *GE ' *CAT +
                         &BDATEA)

             CHGVAR     VAR(&B) VALUE('*AND ICPTDT *LE ' *CAT +
                         &EDATEA)

             CHGVAR     VAR(&C) VALUE('*AND ICAIC3 *EQ ' *CAT +
                         &COA)

             CHGVAR     VAR(&E) VALUE('*AND ICOKCD *EQ "' *CAT +
                         &ACCRTYP *CAT '"')

             IF         COND(&SHIP *GT 0) THEN(DO)
             CHGVAR     VAR(&F) VALUE('*AND ICBKC7 *EQ ' *CAT +
                         &SHIPTOA)
             ENDDO

             CHGVAR     VAR(&D) VALUE(&A *BCAT &B *BCAT &C *BCAT &E +
                          *BCAT &F)

/*********************************************************************/
/*   SELECT QUERY BY REPORT BEING RUN                                */
/*********************************************************************/
 ENDQRYSTMT: OVRDBF     FILE(PDICCPL7) SHARE(*YES)
             OPNQRYF    FILE((PDICCPL7)) QRYSLT(&D) KEYFLD(*FILE)
/*********************************************************************/
/*   CALL LISTING PROGRAM                                            */
/*********************************************************************/

             OVRPRTF    FILE(PMLOPFR$) OUTQ(&OUTQ) COPIES(&COPY) +
                          HOLD(&HOLD) SAVE(&SAVE)

             CALL       PGM(PMLOPFR) PARM('' &CO &ACCRTYP &ACTPER +
                          &ACTYR)

             CLOF       OPNID(PDICCPL7)

/********************************************************************/
/*----------------------RECEIVE MESSAGES-----------------------------*/

 MSGTRAP:    RCVMSG     MSGTYPE(*EXCP) MSGDTA(&MSG) MSGDTALEN(&MSGL) +
                          MSGID(&MSGID)
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(END))

             SNDPGMMSG  MSGID(&MSGID) MSGF(QCPFMSG) MSGDTA(%SST(&MSG +
                          1 &MSGL)) MSGTYPE(*ESCAPE)
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(END))

 END:        ENDPGM

