     A*%%TS  SD  20050125  143507  ISLRAMS     REL-V5R2M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION
     A*  DISPLAY FILE:  HPD746
     A*  TITLE:         VIEW SALE
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  DATE:          06/09/95
     A*
     A*----------------------------------------------------------------
     A*  INDICATORS:
     A*  75  SCHEDULED KILL DATE IS POPULATED, SO DISPLAY TEXT
     A*  90  THE SALE THE USER HAS SELECTED FROM THE WORK WITH HAS
     A*      BEEN DELETED BY ANOTHER USER SINCE SELECTED
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A N90                                  CF05(05 'REFRESH')
     A                                      CA12(12 'PREVIOUS')
     A N90                                  CF13(13 'VIEW')
     A*---------------------------------------------------------------
     A* SUBFILE FORMAT 1
     A*---------------------------------------------------------------
     A*
     A          R HP746S1                   SFL
     A*%%TS  SD  20050125  143507  ISLRAMS     REL-V5R2M0  5722-WDS
     A  85                                  SFLNXTCHG
     A            HDRRN1         4Y 0H
     A            DFBLCD    R        O 18  2REFFLD(HSRREF/@@BLCD *LIBL/HSPREF)
     A            DFRMCD    R        O 18  8REFFLD(HSRREF/@@RMCD *LIBL/HSPREF)
     A            DFHGCD    R        O 18 14REFFLD(HGHGCD)
     A            DFGSCD    R        O 18 24REFFLD(GSGSCD)
     A            DFSCHD    R        O 18 34REFFLD(SGSCHD)
     A                                      EDTCDE(K)
     A            DFSHHD    R     Y  O 18 42REFFLD(SGSHHD)
     A                                      EDTCDE(K)
     A            DFLVHD    R     S  O 18 50REFFLD(SGLVHD)
     A                                      EDTCDE(K)
     A            DFLVLB    R        O 18 58REFFLD(SGLVLB)
     A                                      EDTCDE(K)
     A            DFDOHD    R     S  O 18 66REFFLD(SGDOHD)
     A                                      EDTCDE(M)
     A            DFDOLB    R        O 18 73REFFLD(SGDOLB)
     A                                      EDTCDE(K)
     A*
     A*----------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT  1
     A*----------------------------------------------------------------
     A*
     A          R HP746C1                   SFLCTL(HP746S1)
     A*%%TS  SD  20050125  143507  ISLRAMS     REL-V5R2M0  5722-WDS
     A                                      SFLSIZ(0100)
     A                                      SFLPAG(0004)
     A                                      SFLCSRRRN(&HDRR1)
     A                                      OVERLAY
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A                                      SETOF(75)
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            HDRR1          5S 0H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 26'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  2 30'VIEW SALE'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  4  2'Movement number ......:'
     A            C1MVSN    R        O  4 27REFFLD(@@MVSN)
     A                                      EDTCDE(Z)
     A                                  4 41'Status ...............:'
     A            C1MSCD    R     A  O  4 66REFFLD(SHMSCD)
     A                                  5  2'Scheduled ship date ..:'
     A            C1MDY     R     S  O  5 27REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  ')
     A                                  5 41'Sale type ............:'
     A            C1STCD    R        O  5 66REFFLD(STSTCD)
     A  75                              6  2'Scheduled kill date ..:'
     A            C1SCKLMDY R     S  O  6 27REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  ')
     A                                  6 41'Load type ............:'
     A            C1LTCD    R        O  6 66REFFLD(HSRREF/@@LTCD *LIBL/HSPREF)
     A                                  7  2'Shipped date .........:'
     A            C1SMDY    R     S  O  7 27REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  ')
     A                                  7 41'Trucker ..............:'
     A            C1TICD    R        O  7 66REFFLD(SHTICD)
     A                                  8  2'Received date ........:'
     A            C1RMDY    R     S  O  8 27REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  ')
     A                                  8 41'Customer number ......:'
     A            C1CVNO    R        O  8 66REFFLD(SHCVNO)
     A                                      EDTCDE(M)
     A                                  9  2'Farm .................:'
     A            C1FSCD    R        O  9 27REFFLD(FSFSCD)
     A                                      EDTCDE(M)
     A            C1CNAM    R        O  9 50REFFLD(@@DS30)
     A            C1FSNM    R        O 10  7REFFLD(FSFSNM)
     A                                 11  2'Scheduled'
     A                                      DSPATR(HI)
     A                                 11 15'** Shipped **'
     A                                      DSPATR(HI)
     A                                 11 32'*** Arrived ***'
     A                                      DSPATR(HI)
     A                                 11 49'**** Live ****'
     A                                      DSPATR(HI)
     A                                 11 65'***** DOA *****'
     A                                      DSPATR(HI)
     A                                 12  5'Head'
     A                                 12 16'Head'
     A                                 12 22'Pounds'
     A                                 12 35'Head'
     A                                 12 41'Pounds'
     A                                 12 51'Head'
     A                                 12 57'Pounds'
     A                                 12 68'Head'
     A                                 12 74'Pounds'
     A            C1SCHD    R        O 13  3REFFLD(SGSCHD)
     A                                      EDTCDE(K)
     A            C1SHHD    R        O 13 14REFFLD(SGSHHD)
     A                                      EDTCDE(K)
     A            C1SHLB    R        O 13 22REFFLD(SGSHLB)
     A                                      EDTCDE(K)
     A            C1ARHD    R        O 13 33REFFLD(SGSHHD)
     A                                      EDTCDE(K)
     A            C1ARLB    R        O 13 41REFFLD(SGSHLB)
     A                                      EDTCDE(K)
     A            C1LVHD    R        O 13 49REFFLD(SGSHHD)
     A                                      EDTCDE(K)
     A            C1LVLB    R        O 13 57REFFLD(SGSHLB)
     A                                      EDTCDE(K)
     A            C1DOHD    R        O 13 66REFFLD(SGDOHD)
     A                                      EDTCDE(K)
     A            C1DOLB    R        O 13 74REFFLD(SGDOLB)
     A                                      EDTCDE(K)
     A                                 14  2'----------- SHIPPING INFORMATION --
     A                                      ------------ ---- RECEIVING INFORMA-
     A                                      TION ----'
     A                                      DSPATR(HI)
     A                                 16 35'Sched'
     A                                      DSPATR(HI)
     A                                 16 42'Shipped'
     A                                      DSPATR(HI)
     A                                 16 52'Live'
     A                                      DSPATR(HI)
     A                                 16 60'Live'
     A                                      DSPATR(HI)
     A                                 16 68'DOA'
     A                                      DSPATR(HI)
     A                                 16 76'DOA'
     A                                      DSPATR(HI)
     A                                 17  2'Bldg'
     A                                      DSPATR(HI)
     A                                 17  8'Room'
     A                                      DSPATR(HI)
     A                                 17 14'Group'
     A                                      DSPATR(HI)
     A                                 17 22'Status'
     A                                      DSPATR(HI)
     A                                 17 36'Head'
     A                                      DSPATR(HI)
     A                                 17 44'Head'
     A                                      DSPATR(HI)
     A                                 17 52'Head'
     A                                      DSPATR(HI)
     A                                 17 58'Pounds'
     A                                      DSPATR(HI)
     A                                 17 67'Head'
     A                                      DSPATR(HI)
     A                                 17 73'Pounds'
     A                                      DSPATR(HI)
     A*----------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*----------------------------------------------------------------
     A          R HP746K1
     A*%%TS  SD  19950622  110238  ISLRAMS     REL-V3R0M5  5763-PW1
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A N90                             23 11'F5=Refresh'
     A                                 23 23'F12=Previous'
     A N90                             23 38'F13=View Receiving Detail for Move-
     A                                      ment'
      *----------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *----------------------------------------------------------------
     A          R HP746ES                   SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
     A*----------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*----------------------------------------------------------------
     A*
     A          R HP746EC                   SFLCTL(HP746ES)
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
