     A*%%TS  SD  20170613  141632  ISLBUSE     REL-V7R1M0  5770-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD100
     A*  TITLE:         REVISE FEED MILL
     A*  PROGRAMMER:    LARA BUSER
     A*  CREATED:       06/01/2017
     A*
     A*
     A*  INDICATOR 60 is on when in REVISE mode
     A*  INDICATOR 61 is on when in CREATE mode
     A*  INDICATOR 62 is on when in Inactivate mode
     A*****************************************************************
     A* 10/30/19 JBB E15736 - Upload Toll Mill Receipts
     A*  1. Add Cost Through System Flag field after Manf Type Code.
     A*  2. Make Cost Through System Flag promptable with Y/N.
     A*  3. Validate flag as Y or N, display error message HS09006.
     A*  4. Protect Feed Mill Code in REVISE MODE.
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A N62                                  CF04(04 'PROMPT')
     A  60N62                               CF05(05 'REFRESH')
     A  60N62                               CF11(11 'INACTIVATE')
     A                                      CA12(12 'PREVIOUS')
     A*---------------------------------------------------------------
     A* RECORD FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP100R1
     A*%%TS  SD  20170613  141632  ISLBUSE     REL-V7R1M0  5770-WDS
     A                                      SETOF(30)
     A                                      SETOF(31)
     A                                      SETOF(32)
     A                                      SETOF(33)
     A                                      SETOF(34)
     A                                      SETOF(35)
     A                                      SETOF(36)
     A                                      SETOF(37)
     A                                      SETOF(38)
     A                                      SETOF(39)
     A                                      SETOF(41)
     A                                      SETOF(50)
     A                                      SETOF(51)
     A                                      SETOF(52)
     A                                      SETOF(53)
     A                                      SETOF(54)
     A                                      SETOF(55)
     A                                      SETOF(56)
     A                                      SETOF(57)
     A                                      SETOF(58)
     A                                      SETOF(59)
     A                                      RTNCSRLOC(&RCD &FLD)
     A                                      OVERLAY
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 30'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USR)
     A                                      DSPATR(HI)
     A            D1MODE        10A  O  2  2DSPATR(HI)
     A                                  2 32'MAINTAIN FEED MILL'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@JOB)
     A                                      DSPATR(HI)
     A                                  4  3'Feed Mill Code ............'
     A            D1FMBO    R        B  4 35REFFLD(HSRREF/@@FMBO *LIBL/HSPREF)
     A  35                                  DSPATR(RI)
     A  55
     AO 35                                  DSPATR(PC)
     A*N62                                  DSPATR(UL)
     A* 62                                  DSPATR(PR)
     A N60N62                               DSPATR(UL)
     A  60
     AO 62                                  DSPATR(PR)
     A N62N35                               DSPATR(HI)
     A                                  5  3'Feed Mill Name ............'
     A            D1FMALNM  R        B  5 35REFFLD(HSRREF/@@FMALNM *LIBL/HSPREF)
     A                                      CHECK(LC)
     A  34                                  DSPATR(RI)
     A  54
     AO 34                                  DSPATR(PC)
     A  62                                  DSPATR(PR)
     A N62                                  DSPATR(UL)
     A N62N34                               DSPATR(HI)
     A                                  6  3'Feed Mill Type Code .......'
     A                                  6 33'?'
     A            D1MLTCD   R        B  6 35REFFLD(HSRREF/@@MLTCD *LIBL/HSPREF)
     A  37                                  DSPATR(RI)
     A  37
     AO 57                                  DSPATR(PC)
     A  62                                  DSPATR(PR)
     A N62                                  DSPATR(UL)
     A N62N37                               DSPATR(HI)
     A*
     A                                  7  3'Mill Type Description .....'
     A            D1MLTDS   R        B  7 35REFFLD(HSRREF/@@MLTDS *LIBL/HSPREF)
     A                                      CHECK(LC)
     A  38                                  DSPATR(RI)
     A  38
     AO 58                                  DSPATR(PC)
     A  62                                  DSPATR(PR)
     A N62                                  DSPATR(UL)
     A N62N38                               DSPATR(HI)
     A*
     A                                  8  3'2 Char G/L Sub ............'
     A            D12SUB    R        B  8 35REFFLD(HSRREF/@@2SUB *LIBL/HSPREF)
     A  39                                  DSPATR(RI)
     A  39
     AO 59                                  DSPATR(PC)
     A  62                                  DSPATR(PR)
     A N62                                  DSPATR(UL)
     A N62N39                               DSPATR(HI)
     A*
     A                                  9  3'Mapping Mill Number .......'
     A            D1MMNO    R        B  9 35REFFLD(HSRREF/@@MMNO *LIBL/HSPREF)
     A                                      EDTCDE(3)
     A  32                                  DSPATR(RI)
     A  32
     AO 52                                  DSPATR(PC)
     A  62                                  DSPATR(PR)
     A N62                                  DSPATR(UL)
     A N62N32                               DSPATR(HI)
     A*
     A                                 10  3'Manf Type Code ............'
     A                                 10 33'?'
     A            D1FTCD    R        B 10 35REFFLD(HSRREF/@@FTCD *LIBL/HSPREF)
     A  33                                  DSPATR(RI)
     A  33
     AO 53                                  DSPATR(PC)
     A  62                                  DSPATR(PR)
     A N62                                  DSPATR(UL)
     A N62N33                               DSPATR(HI)
     A*
     A                                 11  3'Cost Through System Flag ..'
     A                                 11 33'?'
     A            D1CSFL    R        B 11 35REFFLD(HSRREF/@@FLAG *LIBL/HSPREF)
     A  41                                  DSPATR(RI)
     A  41
     AO 51                                  DSPATR(PC)
     A  62                                  DSPATR(PR)
     A N62                                  DSPATR(UL)
     A N62N41                               DSPATR(HI)
     A                                 12  3'Active/Inactive status ....'
     A                                 12 33'?'
     A            D1AIST    R        B 12 35REFFLD(HSRREF/@@AIST *LIBL/HSPREF)
     A  36                                  DSPATR(RI)
     A  36
     AO 56                                  DSPATR(PC)
     A  62                                  DSPATR(PR)
     A N62                                  DSPATR(UL)
     A N62N36                               DSPATR(HI)
     A                                 14  3'BUSINESS OFFICE FIELDS'
     A                                      DSPATR(HI)
     A                                 15  3'Company Number ............'
     A            D1COCD    R        B 15 35REFFLD(HSRREF/@@COCD *LIBL/HSPREF)
     A  39                                  DSPATR(RI)
     A  39
     AO 59                                  DSPATR(PC)
     A  62                                  DSPATR(PR)
     A N62                                  DSPATR(UL)
     A N62N39                               DSPATR(HI)
     A                                 16  3'Division Code .............'
     A            D1DVCD    R        O 16 35REFFLD(HSRREF/@@DVCD *LIBL/HSPREF)
     A N62N29                               DSPATR(HI)
     A                                 17  3'Region Code ...............'
     A            D1RGCD    R        B 17 35REFFLD(HSRREF/@@RGCD *LIBL/HSPREF)
     A  30                                  DSPATR(RI)
     A  30
     AO 50                                  DSPATR(PC)
     A  62                                  DSPATR(PR)
     A N62                                  DSPATR(UL)
     A N62N30                               DSPATR(HI)
     A                                 18  3'Security Loc. Code ........'
     A            D1SLCD    R        O 18 35REFFLD(HSRREF/@@SLCD *LIBL/HSPREF)
     A N62N31                               DSPATR(HI)
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R HP100K1
     A*%%TS  SD  20170602  104019  ISLBUSE     REL-V7R1M0  5770-WDS
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A N62                             23 11'F4=Prompt'
     A N62N61                          23 23'F5=Refresh'
     A N62N61                          23 35'F11=Inactivate'
     A                                 23 51'F12=Previous'
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *---------------------------------------------------------------
     A          R HP100ES                   SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE CONTROL FORMAT
      *---------------------------------------------------------------
     A          R HP100EC                   SFLCTL(HP100ES)
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
