/***************************************************************** */
/*  PROGRAM NUMBER --> HP201CL                                     */
/*  PROGRAM NAME ----> DOWNLOAD: BGF TRANSFERS AND CULL SALES      */
/*  DATE ------------> 06/13/01                                    */
/*  PROGRAMMER ------> LEANNE FEDOR                                */
/*                                                                 */
/*  FUNCTION:  ANNE MARIE DUSHAY REQUESTED THAT WE SEND HER DATA   */
/*             LIKE WHAT SHE SEES ON THE ACTUAL MOVEMENT LOG REPORT*/
/*                                                                 */
/*             SHE SET THE FOLLOWING RULES:                        */
/*              1) ONLY MOVEMENTS WHERE THE ORIGIN FARM WAS BGF    */
/*              2) FOR TRANSFERS, ONLY MOVEMENTS INTO NURSERIES    */
/*              3) FOR SALES, ONLY CULL SALES                      */
/*              4) FOR A DATE RANGE, USE THE BEGINNING DATE OF     */
/*                 THE CURRENTLY OPEN ACCOUNTING PERIOD AS THE     */
/*                 FROM DATE. USE THE SYSTEM DATE FOR THE TO DATE. */
/*                                                                 */
/*       !!!! I'M NOT SURE ABOUT RULE 4 ABOVE. ALICE B. IS HAVING  */
/*       !!!! ME CHANGE THE BEGIN DATE TO 1/1/01.....BUT I DON'T   */
/*       !!!! KNOW IF IT IS TEMPORARY OR NOT.                      */
/*                                                                 */
/*            WE CREATE THE ACTUAL MOVEMENT LOG WORKFILE (USING    */
/*            THE EXISTING WORKFILE PROGRM). THEN, WE USE SEQUEL   */
/*            TO EXTRACT THE DATA FROM THE WORKFILE AND FTP IT     */
/*            TO THE PORK INTRANET DOWNLOAD SITE.                  */
/*                                                                 */
/*            THIS CL IS THE ONLY OBJECT WE CREATED FOR THIS       */
/*            DOWNLOAD.  THE OTHER FILES/PROGRAMS ALREADY EXISTED  */
/*            IN HPS.                                              */
/*                                                                 */
/*******************************************************************/
/*  MODIFICATIONS                                                  */
/*                                                                 */
/*  06/15/01  BARB GUTIERREZ                                       */
/*            CHANGED FILES BGFTRAN AND BFFCULL FROM CSV TO XLS    */
/*                                                                 */
/*  06/18/01  LEANNE FEDOR                                         */
/*            CHANGED VALUE OF PCFMT TO *XLS FROM *DELIMTED.       */
/*                                                                 */
/*  06/20/01  LEANNE FEDOR                                         */
/*            CHANGE FROM DATE TO BE JANUARY 1, 2001.              */
/*‚06/11/24  Jagdish Mistry (JM-WI628),                            */
/*‚          Project 628-RabApp Bypass to Agview                   */
/*‚          a. Added parameter to get count for HP370             */
/*                                                                 */
/*******************************************************************/

             PGM

             DCL        VAR(&QDATA) TYPE(*CHAR) LEN(250)

/* FTP VARIABLES */

             DCL        VAR(&ALREADY) TYPE(*CHAR) LEN(1)
             DCL        VAR(&DOCUMENT) TYPE(*CHAR) LEN(10)
             DCL        VAR(&PCFILE) TYPE(*CHAR) LEN(32)

/* CREATE ALPHA AND NUMERIC FIELDS FOR FROM AND TO DATES */

             DCL        VAR(&DTFROM) TYPE(*CHAR) LEN(8)
             DCL        VAR(&DTFROM08) TYPE(*DEC) LEN(8 0)

             DCL        VAR(&DTTO) TYPE(*CHAR) LEN(8)
             DCL        VAR(&DTTO08) TYPE(*DEC) LEN(8 0)
/*‚JM-WI628-START                                                        */
             DCL        VAR(&TOTALCNT) TYPE(*DEC) LEN(8 0)
/*‚JM-WI628-END                                                          */

/*-------------------------------------------------------------------*/
/* RETRIEVE HPS DATA AREA THAT HOLDS THE CURRENT ACCOUNTING PERIOD   */
/*         RETURNING THE BEGINNING DATE OF THE OPEN PERIOD.          */
/* CONVERT THE DATE FROM ALPHA TO A NUMERIC VALUE AND USE IT AS THE  */
/*         "FROM" DATE.                                              */
/*-------------------------------------------------------------------*/

             RTVDTAARA  DTAARA(DAAPER (7 8)) RTNVAR(&DTFROM)
             CHGVAR     VAR(&DTFROM08) VALUE(&DTFROM)

/* POSSIBLE TEMPORARY OVERRIDE OF FROM DATE TO JANUARY 1, 2001       */

             CHGVAR     VAR(&DTFROM08) VALUE(20010101)

/*-------------------------------------------------------------------*/
/* RETRIEVE SYSTEM DATE AND CONVERT TO NUMERIC 8,0 DATE FIELD        */
/* SYSTEM DATE WILL BE USED FOR THE "TO" DATE FOR DATA EXTRACTION    */
/*-------------------------------------------------------------------*/

             RTVDAT     CCYYMMDD(&DTTO)
             CHGVAR     VAR(&DTTO08) VALUE(&DTTO)


/*-------------------------------------------------------------------*/
/*  SETUP QUERY OVER THE MOVEMENT EVENT FILE:                        */
/*        SELECT ALL SALES AND TRANSFER RECORDS FOR THE DATE RANGE   */
/*-------------------------------------------------------------------*/

             CHGVAR     VAR(%SST(&QDATA 1 10)) VALUE('MEMEDT *GE')
             CHGVAR     VAR(%SST(&QDATA 12 8)) VALUE(&DTFROM08)
             CHGVAR     VAR(%SST(&QDATA 21 15)) VALUE('*AND MEMEDT +
                          *LE')
             CHGVAR     VAR(%SST(&QDATA 37 8)) VALUE(&DTTO08)
             CHGVAR     VAR(%SST(&QDATA 46 40)) VALUE('*AND (MEMTCD +
                          *EQ "T" *OR MEMTCD *EQ "S")')

             OVRDBF     FILE(HSP058) SHARE(*YES)
             OPNQRYF    FILE((HSP058)) QRYSLT(&QDATA) KEYFLD(*FILE) +
                          SEQONLY(*YES)

/*-------------------------------------------------------------------*/
/*  CREATE AND THEN POPULATE WORKFILE                                */
/*-------------------------------------------------------------------*/

             CRTDUPOBJL OBJ(HSP370) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)
/*‚JM-WI628-START                                                        */
/*           CALL PGM(HP370)                                             */
             CALL       PGM(HP370) PARM((&TOTALCNT))
/*‚JM-WI628-END                                                          */

             CLOF       OPNID(HSP058)
             DLTOVR     FILE(HSP058)


/*-------------------------------------------------------------------*/
/* ADD SEQUEL LIBRARY TO LIBRARY LIST */
/*-------------------------------------------------------------------*/

             CHGVAR VAR(&ALREADY) VALUE('N')
             ADDLIBLE   LIB(SEQUEL) POSITION(*LAST)
             MONMSG     MSGID(CPF2103) EXEC(DO) /* already in libl */
                CHGVAR VAR(&ALREADY) VALUE('Y')
             ENDDO

/*-------------------------------------------------------------------*/
/* EXTRACT TRANSFER DATA FROM WORKFILE AND FTP TO PORK INTRANET SITE */
/* ONLY SELECT TRANSFERS OUT OF BGF GROUPS AND IN TO NURSERY GROUPS  */
/*-------------------------------------------------------------------*/

             EXECUTE SQL('SELECT +
                           WFMVSN COLHDG("MOVEMENT" "NUMBER"), +
                           CVTDATE(WFRCDT,YMD1) COLHDG("RECEIVED" "DATE"), +
                           WFORFS COLHDG("ORIGIN" "FARM" "SITE"), +
                           WFORBL COLHDG("ORIGIN" "BUILDING"), +
                           WFORRM COLHDG("ORIGIN" "ROOM"), +
                           WFORHG COLHDG("ORIGIN" "GROUP"), +
                           WFDNFS COLHDG("DESTINATION" "FARM" "SITE"), +
                           WFDNBL COLHDG("DESTINATION" "BUILDING"), +
                           WFDNRM COLHDG("DESTINATION" "ROOM"), +
                           WFDNHG COLHDG("DESTINATION" "GROUP"), +
                           WFSCHD COLHDG("SCHEDULED" "HEAD"), +
                           WFSHHD COLHDG("SHIPPED" "HEAD"), +
                           WFQLHD + WFRJHD NAME(WFRCHD) +
                           COLHDG("RECEIVED" "HEAD"), +
                           WFDOHD COLHDG("DOA" "HEAD") +
                     FROM HSP370   +
                     WHERE WFORPP="BGF  " AND WFDNPP="NUR  " +
                          AND WFCODE="TRAN "') +
                     TOFLR(GUY.IS) +
                     TODOC(BGFTRAN) +
                     PCFMT(*XLS) +
                     TEXT('BGF TRANSFERS TO NURSERIES') +
                     REPLACE(*YES)


/* FTP DOCUMENTS TO URL FOR PORK INTRANET PAGE */

             CHGVAR     VAR(&DOCUMENT) VALUE('BGFTRAN')
             CHGVAR     VAR(&PCFILE) VALUE('BGFTRAN.XLS')
             CALL       PGM(FTPPRKSRVR) PARM(&DOCUMENT &PCFILE)

/*-------------------------------------------------------------------*/
/* EXTRACT CULL SALES FROM WORKFILE AND FTP TO PORK INTRANET SITE    */
/* ONLY SELECT CULLS SALES OUT OF BGF GROUPS                         */
/*-------------------------------------------------------------------*/

             EXECUTE SQL('SELECT +
                           WFMVSN COLHDG("MOVEMENT" "NUMBER"), +
                           CVTDATE(WFRCDT,YMD1) COLHDG("RECEIVED" "DATE"), +
                           WFORFS COLHDG("ORIGIN" "FARM" "SITE"), +
                           WFORBL COLHDG("ORIGIN" "BUILDING"), +
                           WFORRM COLHDG("ORIGIN" "ROOM"), +
                           WFORHG COLHDG("ORIGIN" "GROUP"), +
                           WFSCHD COLHDG("SCHEDULED" "HEAD"), +
                           WFSHHD COLHDG("SHIPPED" "HEAD"), +
                           WFQLHD + WFRJHD NAME(WFRCHD) +
                           COLHDG("RECEIVED" "HEAD"), +
                           WFDOHD COLHDG("DOA" "HEAD") +
                     FROM HSP370   +
                     WHERE WFORPP="BGF  " AND WFCODE="CULLS"') +
                     TOFLR(GUY.IS) +
                     TODOC(BGFCULL) +
                     PCFMT(*XLS) +
                     TEXT('BGF CULL SALES') +
                     REPLACE(*YES)


/* FTP DOCUMENTS TO URL FOR PORK INTRANET PAGE */

             CHGVAR     VAR(&DOCUMENT) VALUE('BGFCULL')
             CHGVAR     VAR(&PCFILE) VALUE('BGFCULL.XLS')
             CALL       PGM(FTPPRKSRVR) PARM(&DOCUMENT &PCFILE)

/*-------------------------------------------------------------------*/
/* REMOVE SEQUEL LIBRARY FROM LIBRARY LIST */
/*-------------------------------------------------------------------*/

             IF         COND(&ALREADY *EQ 'N') THEN(DO)
                RMVLIBLE   LIB(SEQUEL)
             ENDDO

             ENDPGM

