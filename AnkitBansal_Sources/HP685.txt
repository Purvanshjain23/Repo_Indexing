      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      HOG PRODUCTION
      * PROGRAM:     HP685 - DAILY DEATHS, DOAS AND REJECTS REPORT
      * PROGRAMMER:  ROSE CENTONZE & MARY PALMER
      * CREATED:     7/06/94          8/05/94
      *
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      * 07/29/94  R. CENTONZE
      *           USE HSL064B A LIEU HSL064C - REMOVE B/O
      *
      * 09/12/94  LEANNE FEDOR
      *           RECOMPILE ONLY. REMOVED NEXT MOVEMENT NUMBER FROM
      *           BUSINESS OFFICE FILE.
      *
      * 09/27/94  LEANNE FEDOR
      *           RECOMPILE ONLY. REMOVED THE FOLLOWING UNUSED
      *           DATABASE FIELDS FROM THE CHECK FILES:
      *             HSP063-CHECK HEADER        CHCHBO CHHRDT
      *             HSP064-CHECK DETAIL        CDCHBO CDCSAM
      *             HSP065-CHECK ADJUSTMENTS   CJCHBO
      *
      * 01/13/95  LEANNE FEDOR
      *           RECOMPILE ONLY. SEX ADDED TO HOG GROUP FILE.
      *
      * 02/02/95  LEANNE FEDOR
      *           INCREASED SIZES OF WORKFIELDS, ARRAYS, PRINT
      *           FIELDS.
      *
      * 04/25/95  LEANNE FEDOR
      *           CHANGED FARM SITE CODE FROM ALPHA TO NUMERIC.
      *
      * 07/07/95  LEANNE FEDOR
      *           REWRITTEN FOR MOVEMENT REWRITE.
      *
      * 09/06/01  LeAnne Fedor
      *           'Group type' added as a new report option. Also, new 'square feet'
      *           fields added to Farm Site file requiring a recompile.
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     FHSP010    IF   E           K DISK
      *    PRODUCTION TYPE           (KEY: PTPTCD)
      *
     FHSP011    IF   E           K DISK
      *    PRODUCTION PHASE          (KEY: PPPPCD)
      *
     FHSP385    IF   E           K DISK
      *    WORKFILE                  (KEY: WFPTCD WFPPCD WFDRCD)
      *
     FQPRINT    O    F  132        PRINTER OFLIND(*INOF)
      /EJECT
      ****************************************************************
      * TABLE AND ARRAY SPECIFICATIONS
      ****************************************************************
      *
      *  ARRAYS FOR TOTALS
      *
     D LBS             S             11  0 DIM(3)                               DEAD POUNDS
     D DHD             S             11  0 DIM(3)                               DEAD HEAD
     D RJHD            S             11  0 DIM(3)                               REJECT OUT HD
      /EJECT
      ****************************************************************
      * INPUT SPECS
      ****************************************************************
      *
      *---------------------------------------------------------------
      * STANDARD PROGRAM STATUS DATA STRUCTURE
      *---------------------------------------------------------------
      *    EXTERNALLY DEFINED AS UTPGFR (RECORD FORMAT: PGMDSR)
     D PGMDS         ESDS                  EXTNAME(UTPGFR)
      *
      *---------------------------------------------------------------
      *  NAMED CONSTANTS
      *---------------------------------------------------------------
      *
     D YES             C                   CONST('Y')
     D NO              C                   CONST('N')
     D UNKNWN          C                   CONST('UNKNOWN')
     D ALL             C                   CONST('ALL')
      *
      *----------------------------------------------------------------
      * LDA - REPORT SELECTIONS
      *----------------------------------------------------------------
      *
     D                UDS
     D  LDFSBO                 1      5
     D  LDFYMD                 6     13  0
     D  LDTYMD                14     21  0
     D  LDPPCD                22     26
     D  LDFSCD                27     31  0
     D  LDGTCD                47     47
     D  LDPTCD                48     52
     D  LDHGSN                53     59  0
     D  LDFMDY                60     65  0
     D  LDTMDY                66     71  0
      *
     D  LDBODS                72    101
     D  LDPTDS               102    111
     D  LDPPDS               112    141
     D  LDFSNM               142    166
     D  LDHGCD               167    173
     D  LDRPFL               172    172
     D  LDGTDS               182    196
      *
     D  LDOUTU               401    410
     D  LDHOLD               411    411
     D  LDCOPY               412    412
     D  LDOUTQ               413    422
      /EJECT
      ****************************************************************
      * MAIN LINE
      ****************************************************************
      *
      * READ EACH RECORD IN THE WORKFILE. THE DATA IN THE WORKFILE IS
      * SUMMARIZED AT THE PRODUCTION TYPE/PHASE/REASON CODE LEVEL.
      *
     C     *IN90         DOUEQ     *ON                                          MAIN DO
     C                   READ      HSP385                                 90
     C     *IN90         IFEQ      *OFF                                         IF NOT EOF
      *
     C     FIRST         IFEQ      YES                                          IF FIRST
     C                   EXSR      $PTDS
     C                   EXSR      $PPDS
     C                   EXSR      $HRHDR
     C                   MOVEL     WFPTCD        SVPTCD
     C                   MOVEL     WFPPCD        SVPPCD
     C                   MOVEL     NO            FIRST
     C                   ENDIF                                                  IF FIRST
      *
      * CHECK FOR CONTROL BREAKS
      *
     C                   SELECT
     C     WFPTCD        WHENNE    SVPTCD                                       PROD TYPE
     C                   EXSR      $PPTOT
     C                   EXSR      $PTTOT
     C                   EXSR      $PTDS
     C                   EXSR      $PPDS
     C                   EXSR      $HRHDR
      *
     C     WFPPCD        WHENNE    SVPPCD                                       PROD PHASE
     C                   EXSR      $PPTOT
     C                   EXSR      $PPDS
     C                   EXSR      $HRHDR
     C                   ENDSL
      *
      * ACCUMULATE HEAD AND POUNDS FOR CONTROL BREAK TOTALS
      *
     C                   ADD       WFKDLB        LBS
     C                   ADD       WFKDHD        DHD
     C                   ADD       WFRJHD        RJHD
      *
      * CALCULATE AVERAGE DEAD POUNDS
      *
     C     WFKDHD        IFNE      0
     C     WFKDLB        DIV(H)    WFKDHD        WKAVLB
     C                   ENDIF
      *
      * PRINT THE DETAIL LINE
      *
     C     *INOF         IFEQ      *ON
     C                   EXSR      $HRHDR
     C                   ENDIF
      *
     C                   EXCEPT    DTLINE
      *
     C                   ENDIF                                                  IF NOT EOF
     C                   ENDDO                                                  MAIN DO
      *
      *----------------------------------------------------------------
      * EOF PROCESSING
      *----------------------------------------------------------------
      *  IF NO DETAIL RECORDS MATCHED THE SELECTION CRITERIA, THEN
      *  PRINT THE STANDARD 'NO DATA...' ERROR MESSAGE
      *
     C     FIRST         IFEQ      YES                                          IF NO RECS
     C                   EXCEPT    HRHDR
     C                   EXCEPT    NODATA
     C                   ELSE
      *
     C                   EXSR      $PPTOT
     C                   EXSR      $PTTOT
      *
     C     DHD(3)        IFNE      0
     C     LBS(3)        DIV(H)    DHD(3)        WKAVLB
     C                   ENDIF
     C                   EXCEPT    TRTOT
     C                   ENDIF                                                  IF NO RECS
      *
     C                   SETON                                        LR
      /EJECT
      *---------------------------------------------------------------
      * $HRHDR - PRINT REPORT HEADINGS
      *---------------------------------------------------------------
      *
     C     $HRHDR        BEGSR
      *
     C                   EXCEPT    HRHDR
     C                   EXCEPT    HTHDR
      *
     C                   ENDSR
      *
      *----------------------------------------------------------------
      *  $PTDS - RETRIEVE THE PRODUCTION TYPE DESCRIPTION
      *----------------------------------------------------------------
      *
     C     $PTDS         BEGSR
      *
     C     WFPTCD        CHAIN     HSP010                             91
     C     *IN91         IFEQ      *OFF                                         IF FOUND
     C                   MOVEL     PTPTDS        HTPTDS
     C                   ELSE
     C                   MOVEL(P)  UNKNWN        HTPTDS
     C                   ENDIF                                                  IF FOUND
      *
     C                   ENDSR
      *
      *----------------------------------------------------------------
      * $PPDS - RETRIEVE THE PRODUCTION PHASE DESCRIPTION
      *----------------------------------------------------------------
      *
     C     $PPDS         BEGSR
      *
     C     WFPPCD        CHAIN     HSP011                             91
     C     *IN91         IFEQ      *OFF                                         IF FOUND
     C                   MOVEL     PPPPDS        HTPPDS
     C                   ELSE
     C                   MOVEL(P)  UNKNWN        HTPPDS
     C                   ENDIF                                                  IF FOUND
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      *  $PTTOT - PRODUCTION TYPE TOTALS
      *----------------------------------------------------------------
      *
     C     $PTTOT        BEGSR
      *
     C     DHD(2)        IFNE      0
     C     LBS(2)        DIV(H)    DHD(2)        WKAVLB
     C                   ENDIF
      *
     C     *INOF         IFEQ      *ON
     C                   EXSR      $HRHDR
     C                   ENDIF
     C                   EXCEPT    TTTOT
      *
     C                   MOVEL     WFPTCD        SVPTCD
     C                   Z-ADD     0             LBS(2)
      *
     C                   ENDSR
      *
      *----------------------------------------------------------------
      * $PPTOT - PRODUCTION PHASE TOTALS
      *----------------------------------------------------------------
      *
     C     $PPTOT        BEGSR
      *
     C     DHD(1)        IFNE      0
     C     LBS(1)        DIV(H)    DHD(1)        WKAVLB
     C                   ENDIF
      *
     C     *INOF         IFEQ      *ON
     C                   EXSR      $HRHDR
     C                   ENDIF
     C                   EXCEPT    TPTOT
      *
     C                   MOVEL     WFPPCD        SVPPCD
     C                   Z-ADD     0             LBS(1)
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * *INZSR - INITIALIZATION SUBROUTINE
      *---------------------------------------------------------------
      *
     C     *INZSR        BEGSR
      *
      * WORKFILDS
      *
     C                   Z-ADD     0             WKAVLB            5 0
      *
      * DEFINE PRINT FIELDS FOR PRODUCTION TYPE HEADING
      *
     C     *LIKE         DEFINE    PTPTDS        HTPTDS
     C     *LIKE         DEFINE    PPPPDS        HTPPDS
      *
      * DEFINE SAVE FIELDS FOR CONTROL BREAKS
      *
     C     *LIKE         DEFINE    WFPTCD        SVPTCD
     C     *LIKE         DEFINE    WFPPCD        SVPPCD
      *
      *---------------------------------------------------------------
      * SET UP REPORT HEADINGS
      *---------------------------------------------------------------
      *
      * ALL BUSINESS OFFICES
      *
     C     LDFSBO        IFEQ      *BLANK
     C                   MOVEL     ALL           LDFSBO
     C                   ENDIF
      *
      * ALL PRODUCTION TYPES
      *
     C     LDPTCD        IFEQ      *BLANK
     C                   MOVEL     ALL           LDPTCD
     C                   ENDIF
      *
      * ALL PRODUCTION PHASES
      *
     C     LDPPCD        IFEQ      *BLANK
     C                   MOVEL     ALL           LDPPCD
     C                   ENDIF
      *
      *  ALL FARM SITES
      *
     C     LDFSCD        IFEQ      0
     C                   SETON                                        81
     C                   ENDIF
      *
      * DETERMINE IF USER SELECTED SPECIFIC HOG GROUP OR "ALL"
      *
     C     LDHGCD        IFEQ      *BLANK
     C                   MOVEL     ALL           LDHGCD
     C                   ENDIF
      *
      *
      * Group type
      *
     C                   if        ldgtcd = *blank
     C                   seton                                        82
     C                   endif
      *
      * SET UP HEADING UNDERLINE FIELD FOR PRINT
      * RETRIEVE TIME FOR REPORT HEADING
      * SET UP A FIRST TIME FLAG
      *
     C                   MOVEL     *ALL'-'       DASH             80
     C                   TIME                    RTIME             6 0
     C                   MOVEL     YES           FIRST             1
      *
     C                   ENDSR
      /EJECT
      *-------------------------------------------------------------
      * REPORT HEADING LINES
      *-------------------------------------------------------------
      *
     OQPRINT    E            HRHDR          1 01
     O                       SDPGM               10
     O                                           44 'HOG PRODUCTION SYSTEM'
     O                                           70 'DATE'
     O                       UDATE         Y     80
      *
     O          E            HRHDR          1
     O                       SDUSR               10
     O                                           39 'DAILY DEATHS, DOAS AND '
     O                                           53 'REJECTS REPORT'
     O                                           70 'TIME'
     O                       RTIME               80 '  :  :  '
      *
     O          E            HRHDR          1
     O                                           70 'PAGE'
     O                       PAGE          Z     80
      *
     O          E            HRHDR          1
     O                                           19 'BUSINESS OFFICE ...'
     O                       LDFSBO              26
     O                       LDBODS              61
      *
     O          E            HRHDR          1
     O                                           19 'FROM DATE .........'
     O                       LDFMDY        Y     29
      *
     O          E            HRHDR          1
     O                                           19 'TO DATE ...........'
     O                       LDTMDY        Y     29
      *
     O          E            HRHDR          1
     O                                           19 'PRODUCTION TYPE ...'
     O                       LDPTCD              26
     O                       LDPTDS              41
      *
     O          E            HRHDR          1
     O                                           19 'PRODUCTION PHASE ..'
     O                       LDPPCD              26
     O                       LDPPDS              61
      *
      *
     O          e            hrhdr          1
     O                                           19 'GROUP TYPE ........'
     O              n82      ldgtcd              22
     O               82                          24 'ALL'
     O                       ldgtds              46
      *
      *
     O          E            HRHDR          1
     O                                           19 'FARM SITE .........'
     O              N81      LDFSCD        M     27
     O               81                          24 'ALL'
     O                       LDFSNM              56
      *
     O          E            HRHDR          2
     O                                           19 'HOG GROUP .........'
     O                       LDHGCD              28
      *
      *-------------------------------------------------------------
      * COLUMN HEADING LINES
      *-------------------------------------------------------------
      *
     O          E            HRHDR          1
     O                                           47 'DEAD'
     O                                           60 'AVERAGE'
     O                                           80 'REJECTED'
     O          E            HRHDR          1
     O                                            6 'REASON'
     O                                           19 'DESCRIPTION'
     O                                           47 'HEAD'
     O                                           60 'POUNDS'
     O                                           80 'HEAD'
      *
     O          E            HRHDR          1
     O                       DASH                80
      *
      *-------------------------------------------------------------
      * PRODUCTION TYPE / PRODUCTION PHASE HEADING
      *-------------------------------------------------------------
      *
     O          E            HTHDR       1  2
     O                       HTPTDS              15
     O                                           17 '-'
     O                       HTPPDS              48
      *
      *-------------------------------------------------------------
      * DETAIL LINE
      *-------------------------------------------------------------
      *
     O          E            DTLINE         2
     O                       WFDRCD               5
     O                       WFDRDS              33
     O                       WFKDHD        KB    48
     O                       WKAVLB        KB    61
     O                       WFRJHD        KB    80
      *
      *-------------------------------------------------------------
      * PRODUCTION PHASE TOTAL LINE
      *-------------------------------------------------------------
      *
     O          E            TPTOT          1
     O                                           21 '***'
     O                       SVPPCD              27
     O                       DHD(1)        KB    48
     O                       WKAVLB        KB    61
     O                       RJHD(1)       KB    80
      *
      *-------------------------------------------------------------
      * PRODUCTION TYPE TOTAL LINE
      *-------------------------------------------------------------
      *
     O          E            TTTOT          1
     O                                           21 '****'
     O                       SVPTCD              27
     O                       DHD(2)        KB    48
     O                       WKAVLB        KB    61
     O                       RJHD(2)       KB    80
      *
      *-------------------------------------------------------------
      * REPORT TOTALS
      *-------------------------------------------------------------
      *
     O          E            TRTOT          1
     O                                           29 'REPORT TOTAL'
     O                       DHD(3)        KB    48
     O                       WKAVLB        KB    61
     O                       RJHD(3)       KB    80
      *
      *-------------------------------------------------------------
      * NO DATA MESSAGE LINE
      *-------------------------------------------------------------
      *
     O          E            NODATA         1
     O                                           19 'NO DATA MEETS YOUR'
     O                                           38 'SELECTION CRITERIA'
