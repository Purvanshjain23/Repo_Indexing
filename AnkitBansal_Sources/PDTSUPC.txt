/***************************************************************** */
/*                                                                 */
/*  PROGRAM NUMBER:  PDTSUPC                                       */
/*  PROGRAM NAME:    Print PRT LOT/LOCATION TRANS UP               */
/*  PROGRAMMER:      JOHN TREMPER                                  */
/*  CREATE DATE:     06/23/98                                      */
/*                                                                 */
/*  FUNCTION:        This CLP is submitted PRINTS THE              */
/*                   ITEM LOT LOCATION TRANSACTIONS.             */
/*                   RUN AN OPEN QUERY FILE TO MINIMIZE RECORDS.   */
/*                                                                 */
/*                   USER OPTIONS ARE PASSED THROUGH THE PARMS.    */
/*                                                                 */
/*******************************************************************/
/*  Modification History                                           */
/*  RMC 11/10/05 ADDED REPORT TYPE SUMMARY FOR NEW REPORT         */
/*                                                                 */
/*  RMC 05/24/22 DDSDDL REPLACE OPNQRYF WITH OPNSQLF        */
/*                      ADDED NEEDED OVRDBF AROUND THE COMMAND.              */
/*                      CHANGE *AND TO AND IN QRY,CHG *EQ/*GE TO =,>=  */
/*                                                                 */
/*******************************************************************/
             PGM        PARM(&COMP &ITEM &WHSE &TYPE &FDATE &TDATE +
                          &DTLSUM)
/*-----------------------------------------------------------------*/
/*                      PARAMETER                                  */
/*-----------------------------------------------------------------*/
/* INPUT PARAMETERS                                                */
             DCL        VAR(&COMP)  TYPE(*DEC)  LEN(3 0)
             DCL        VAR(&ITEM)  TYPE(*DEC)  LEN(7 0)
             DCL        VAR(&WHSE)  TYPE(*CHAR) LEN(3)
             DCL        VAR(&FDATE) TYPE(*DEC)
             DCL        VAR(&TDATE) TYPE(*DEC)
             DCL        VAR(&TYPE)  TYPE(*CHAR) LEN(2)
             DCL        VAR(&DTLSUM)  TYPE(*CHAR) LEN(1)

/*-----------------------------------------------------------------*/
/* PROGRAM VARIABLES                                               */
             DCL        VAR(&FDTE)     TYPE(*DEC)  LEN(7 0)
             DCL        VAR(&TDTE)     TYPE(*DEC)  LEN(7 0)
             DCL        VAR(&QRYCOMP)  TYPE(*CHAR) LEN(3)
             DCL        VAR(&QRYFDATE) TYPE(*CHAR) LEN(7)
             DCL        VAR(&QRYTDATE) TYPE(*CHAR) LEN(7)
             DCL        VAR(&QRYWHSE)  TYPE(*CHAR) LEN(3)
             DCL        VAR(&QRYITEM)  TYPE(*CHAR) LEN(7)
             DCL        VAR(&QRYTYPE)  TYPE(*CHAR) LEN(2)
             DCL        VAR(&A)        TYPE(*CHAR) LEN(25)
             DCL        VAR(&B)        TYPE(*CHAR) LEN(25)
             DCL        VAR(&C)        TYPE(*CHAR) LEN(25)
             DCL        VAR(&D)        TYPE(*CHAR) LEN(25)
             DCL        VAR(&E)        TYPE(*CHAR) LEN(25)
             DCL        VAR(&F)        TYPE(*CHAR) LEN(200)

/*-----------------------------------------------------------------*/
/* MOVES INPUT TO QUERY FIELDS                                     */
             CHGVAR     VAR(&QRYCOMP)  VALUE(&COMP)
             CHGVAR     VAR(&FDTE)     VALUE(&FDATE)
             CHGVAR     VAR(&TDTE)     VALUE(&TDATE)
             CHGVAR     VAR(&QRYFDATE) VALUE(&FDTE)
             CHGVAR     VAR(&QRYTDATE) VALUE(&TDTE)
             CHGVAR     VAR(&QRYITEM)  VALUE(&ITEM)
             CHGVAR     VAR(&QRYTYPE)  VALUE(&TYPE)

/*-----------------------------------------------------------------*/
/* OPEN QUERY FILE CONCATS                                         */
             CHGVAR     VAR(&A) VALUE('L7AIC3 = ' *CAT &QRYCOMP)
             CHGVAR     VAR(&B) VALUE('AND L7LRDT >= ' +
                                       *CAT &QRYFDATE)
             CHGVAR     VAR(&C) VALUE('AND L7LRDT <= ' +
                                       *CAT &QRYTDATE)
             IF         COND(&QRYITEM *NE '0000000') THEN(DO)
               CHGVAR   VAR(&D) VALUE('AND L7HGCD = ' +
                                       *CAT &QRYITEM)
             ENDDO
             IF         COND(&QRYTYPE *NE '  ') THEN(DO)
               CHGVAR   VAR(&E) VALUE('AND L7LTRT = ' +
                                       *CAT '"' *CAT &QRYTYPE +
                                       *CAT '"')
             ENDDO
             CHGVAR     VAR(&F) VALUE(&A *BCAT &B *BCAT &C *BCAT &D +
                          *BCAT &E)


/*-----------------------------------------------------------------*/
/* OPEN QUERY                                                      */
/*-----------------------------------------------------------------*/
/* PLEASE NOTE:                                                    */
/*   THE 'OVRSCOPE' PARAMETER FOR THE OVRDBF MUST BE SET TO *JOB   */
/*   IN ORDER FOR THIS FUNCTION TO WORK PROPERLY.  AFTER THE       */
/*   PRINT FILE HAS COMPLETED AN EXECUTABLE MESSAGE WILL WILL      */
/*   DELETE THE OVERRIDE.  THE 'OVERSCOPE' ON THE EXECUTABLE       */
/*   MUST BE SET TO *JOB AS WELL.                                  */
/*      JOHN TREMPER, JUNE 24, 1998                                */
/*-----------------------------------------------------------------*/
IF COND(&DTLSUM *EQ 'D') THEN(DO)
  /*         OVRDBF     FILE(PDL7CPL5) OVRSCOPE(*JOB) SHARE(*YES)   */
  /*         OPNQRYF    FILE((PDL7CPL5)) QRYSLT(&F) KEYFLD(*FILE)   */

/*   OPEN SQL                                                      */
             OVRDBF     FILE(PDL7CPP) TOFILE(PDL7CPP) LVLCHK(*NO)
             OPNSQLF    SQL('SELECT * FROM PDL7CPP WHERE' *BCAT &F +
                          *BCAT 'ORDER BY  L7AIC3 ASC,L7AJCD +
                          ASC,L7HGCD ASC, L7LTRT ASC, L7LRDT +
                          ASC,L7LSDT ASC, L7YMCD ASC') +
                          OPNID(PDL7CPL5) RCDFMT(@L7CPYZ) +
                          ALWCPY(*IFRQD)
   /*  Fromfile is the file in the program, tofile is 1st file in the opnsqlf */
     /*  For the override to be SEEN BY ALL PROGRAMS, THE OVRDBF +
                command must specify OVRSCOPE(*JOB) and OPNSCOPE(*JOB). */

             OVRDBF     FILE(PDL7CPL5) TOFILE(PDL7CPP) LVLCHK(*NO) +
                          OVRSCOPE(*JOB) SHARE(*YES) OPNSCOPE(*JOB)

         ENDDO
IF COND(&DTLSUM *EQ 'S') THEN(DO)
/*           OVRDBF     FILE(PDL7CPLA) OVRSCOPE(*JOB) SHARE(*YES)      */
/*           OPNQRYF    FILE((PDL7CPLA)) QRYSLT(&F) KEYFLD(*FILE)      */

/*   OPEN SQL                                                      */
             OVRDBF     FILE(PDL7CPP) TOFILE(PDL7CPP) LVLCHK(*NO)
             OPNSQLF    SQL('SELECT * FROM PDL7CPP WHERE' *BCAT &F +
                          *BCAT 'ORDER BY  L7AIC3 ASC,L7AJCD +
                          ASC,L7HGCD ASC, L7LTRT ASC, L7LRDT +
                          ASC,L7LSDT ASC, L7YMCD ASC,L7C4NB ASC') +
                          OPNID(PDL7CPLA) RCDFMT(@L7CPEZ) +
                          ALWCPY(*IFRQD)
   /*  Fromfile is the file in the program, tofile is 1st file in the opnsqlf */
     /*  For the override to be SEEN BY ALL PROGRAMS, THE OVRDBF +
                command must specify OVRSCOPE(*JOB) and OPNSCOPE(*JOB). */

             OVRDBF     FILE(PDL7CPLA) TOFILE(PDL7CPP) LVLCHK(*NO) +
                          OVRSCOPE(*JOB) SHARE(*YES) OPNSCOPE(*JOB)

         ENDDO
/*-----------------------------------------------------------------*/
/*                      PROGRAM END                                */
/*-----------------------------------------------------------------*/
 ENDPGM:

             ENDPGM
