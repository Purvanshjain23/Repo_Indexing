      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      HOG PRODUCTION
      * PROGRAM:     HPPMKT
      * TITLE:       GENERIC PIG DAYS CALCULATION FOR MARKET SALES ONLY
      * PROGRAMMER:  LEANNE FEDOR
      * CREATED:     07/01/97
      *
      * FUNCTION: GENERIC PROGRAM TO BE CALLED FROM MULTIPLE PROGRAMS
      *           TO CALCULATE THE PIG DAYS ASSOCIATED WITH MARKET HOG
      *           SALES.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 02/16/98  LeAnne Fedor
      *           Recompile only. Added the following fields to the
      *           database for sales movement detail and check detail:
      *                     condemned head and pounds
      *                     yard dead head and pounds
      *
      * 01/11/99  LeAnne Fedor
      *           Recompile only. New field 'scheduled kill date' added
      *           to sales movement header file.
      *
      * 10/17/00  LeAnne Fedor
      *           Recompile only. New field 'load type' added to the sales
      *           movement head file.
      *
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     FHSL058D   IF   E           K DISK
      *    MOVEMENT EVENT            (KEY: MEHGSN MEMEDT)
      *
     FHSP084    IF   E           K DISK
      *    SALES MOVEMENT HEADER     (KEY: SHMVSN)
      *
     FHSP085    IF   E           K DISK
      *    SALES MOVEMENT DETAIL     (KEY: SGMVSN SGHGSN)
      *
      /EJECT
      ****************************************************************
      * INPUT SPECS
      ****************************************************************
      *
     D                 DS
     D  DUMMYD                 1      1
      *  THE PRECEDING DUMMY DATA STRUCTURE IS ONLY TO KEEP THE I-SPEC
      *  INFO FROM RUNNING OVER INTO THE 'CALC SPECS' ON THE COMPILE
      *  LISTING.  IT SERVES NO OTHER PURPOSE AND CAN BE DELETED
      *  AT ANY TIME!!!
     C/EJECT
      ****************************************************************
      * MAIN LINE
      ****************************************************************
      *
      *
      * PROCESS ALL MARKET SALES MOVEMENTS FOR THE HOG GROUP
      *
     C     XXHGSN        SETLL     HSL058D
      *
     C     *IN90         DOUEQ     *ON                                          MAIN DO
     C     XXHGSN        READE     HSL058D                                90
     C     *IN90         IFEQ      *OFF                                         IF SALE
     C     MEMTCD        ANDEQ     'S'
     C                   EXSR      $PROC
     C                   ENDIF                                                  IF SALE
     C                   ENDDO                                                  MAIN DO
      *
     C                   SETON                                        LR
      /EJECT
      *----------------------------------------------------------------
      * $PROC - PROCESS THE MARKET SALES MOVEMENT
      *----------------------------------------------------------------
      *
     C     $PROC         BEGSR
      *
     C     MEMVSN        CHAIN     HSP084                             92
     C     *IN92         IFEQ      *OFF                                         IF MARKET SALE
     C     SHSTCD        ANDEQ     'MRKTS'
      *
      * READ THE DETAIL RECORDS FOR THIS MOVEMENT AND
      * ACCUMULATE THE HEAD THAT ARRIVED ALIVE AT THE
      * CUSTOMER.
      *
     C                   Z-ADD     0             WKLVHD
     C     KEY01         SETLL     HSP085
      *
     C     *IN91         DOUEQ     *ON                                          DO SALES DETAIL
     C     KEY01         READE     HSP085                                 91
     C     *IN91         IFEQ      *OFF                                         IF DETAIL
     C                   ADD       SGLVHD        WKLVHD
     C                   ENDIF                                                  IF DETAIL
     C                   ENDDO                                                  DO SALES DETAIL
      *
      * DETERMINE THE NUMBER OF DAYS BETWEEN THE
      * ADJUSTED OPEN DATE AND THE SHIPPED DATE OF THIS
      * MOVEMENT.
      *
     C                   Z-ADD     WKDT          PFRM8
     C                   Z-ADD     SHSHDT        PTO8
     C                   Z-ADD     0             PDAYS
     C                   MOVEL     'W'           PCODE
     C                   EXSR      $DATE
      *
      *
      * CALCULATE PIG DAYS FOR THIS MARKET SALES AS:
      *   DAYS * MARKET HEAD
      *
     C     PDAYS         MULT      WKLVHD        WKPDAY
      *
      * ACCUMULATE TOTAL PIG DAYS
      *
     C                   ADD       WKPDAY        XXPDAY
      *
     C                   ENDIF                                                  IF MARKET SALE
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * $DATE - MANIPULATE DATES WITH DATE UTILITY
      *---------------------------------------------------------------
      *
     C     $DATE         BEGSR
      *
     C                   MOVEL     'CYMD'        PFRFMT
     C                   MOVEL     'CYMD'        PTOFMT
     C                   MOVE      *BLANK        PRTRN
     C                   CALL      'UT80060R'
     C                   PARM                    PFRM8             8 0
     C                   PARM                    PTO8              8 0
     C                   PARM                    PFRFMT            4
     C                   PARM                    PTOFMT            4
     C                   PARM                    PDAYS             4 0
     C                   PARM                    PCODE             1
     C                   PARM                    PRTRN             1
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * *INZSR - INITIALIZATION SUBROUTINE
      *---------------------------------------------------------------
      *
     C     *INZSR        BEGSR
      *
     C     *ENTRY        PLIST
     C                   PARM                    XXHGSN
     C                   PARM                    XXOPDT
     C                   PARM                    XXPDAY           15 2
      *
      * KEY LISTS
      *
     C     KEY01         KLIST
     C                   KFLD                    MEMVSN
     C                   KFLD                    XXHGSN
      *
      * DEFINE PARMS
      *
     C     *LIKE         DEFINE    MEHGSN        XXHGSN
     C     *LIKE         DEFINE    MEMEDT        XXOPDT
      *
      *
      * DEFINE WORKFIELDS
      *
     C     *LIKE         DEFINE    MEMEDT        WKDT
     C     *LIKE         DEFINE    XXPDAY        WKPDAY
     C     *LIKE         DEFINE    SGLVHD        WKLVHD
      *
      * USING THE OPEN DATE OF THE GROUP, CALCULATE AN
      * ADJUSTED OPEN DATE BY ADDING 3 DAYS!
      *
     C                   Z-ADD     XXOPDT        PFRM8
     C                   Z-ADD     0             PTO8
     C                   Z-ADD     3             PDAYS
     C                   MOVEL     'I'           PCODE
     C                   EXSR      $DATE
     C     PRTRN         IFNE      *BLANK                                       IF BAD
     C                   Z-ADD     XXOPDT        WKDT
     C                   ELSE
     C                   Z-ADD     PTO8          WKDT
     C                   ENDIF                                                  IF BAD
      *
     C                   ENDSR
      /EJECT
