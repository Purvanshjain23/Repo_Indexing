PGM  (&LGLVNM &OBJATR &RPGPFX &SYSPFX &PHYVNM &RETRY)
/*T: YALCVNM Allocate DBF logical file name. */
/*Z: CRTCLPGM LOG(*NO) */
/*H: SYSTEM     : SYNON/2 ROUTINE                                      */
/*H: PROGRAMMER : P.WILSON                                             */
/*H: COPYRIGHT (C) 1986 SYNON.                                         */
/*H: SYNOPSIS : ALlocates a CPF LF name for a Synon/2 access path      */
/*W: CPF file names must be unique within a model.                     */

/*M: 06/04/88  PV Replace hardcoded suffix of L by calling program */
/*M:              to retrieve the substitution allocation character*/

DCL &LGLVNM *CHAR 10 /*O: Allocated DBF file name. */
                     /*   Blank = give up auto allocate */
DCL &OBJATR *CHAR  3 /*I: Synon/2 object attribute. */
DCL &RPGPFX *CHAR  2 /*I: Synon/2 file RPG prefix. */
DCL &SYSPFX *CHAR  2 /*I: Synon/2 system prefix. */
DCL &PHYVNM *CHAR 10 /*I: Synon/2 PHY member name. */
DCL &RETRY  *DEC   3 /*I: Retry count, 1-on first entry */
                     /*   incremented by 1 for each retry.*/

DCL &TBL    *CHAR 36 VALUE('0123456789ABCDEFGHIJKLMNOPQRSTUVWXYZ')
                     /*  Table of suffices: 0-Z */
DCL &SFX    *CHAR 1  /* Allocation character suffix for logical file */
DCL &HLLERR *CHAR 7  /* Name validator return code,non-blank = error */
DCL &OBJSGT *DEC  7 VALUE(0) /* Object surrogate to pass for name    */
                             /* checking always zero for new objects */

/*H: 1. If no physical file name, give up.*/
            IF (&PHYVNM *EQ ' ') DO
               CHGVAR &LGLVNM ' '
               RETURN
            ENDDO

/*H: 2. If all 36 qualifiers have been tried, give up*/
            IF (&RETRY *GT 36) DO
               CHGVAR &LGLVNM ' '
               RETURN
            ENDDO

/*H: 3. Construct logical file name: Physical file name chars 1-6 + */
/*                                   Logical file suffix + */
/*                                   Series suffix */
            CALL YGENTYPR1I ('LGL' '' &SFX)
            CHGVAR &LGLVNM (%SST(&PHYVNM 1 6) |< +
                            &SFX |< +
                            %SST(&TBL &RETRY 1))
ENDPGM
