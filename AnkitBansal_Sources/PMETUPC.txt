/********************************************************************/
/*  PROGRAM   - PMETUPC CUSTOMER A/R ACTIVITY                       */
/*  SYNOPSIS  - JOB CONTROL PROMPT FOR PRINTER OVERRIDE INFORMATION */
/*              GENERIC PROMPTING FUNCTION USED BY ANY PROGRAM      */
/*  SYSTEM    - ORDER MANAGEMENT SYSTEM                             */
/*  DATE      - 02/06/97                                            */
/*  DESCRIPTION - PROMPTS FOR PRINTER OVERRIDES, SUBMITS THE        */
/*              CUSTOMER A/R ACTIVITY REPORT                        */
/*                                                                  */
/* PARAMETERS:                                                      */
/*                                                                  */
/* &RETURN     - RETURN CODE                                        */
/* &APPLIC     - APPLICATION CODE                                   */
/* &DESC       - PRINT PROGRAM DESCRIPTION (FOR PROMPT              */
/*                SCREEN)                                           */
/* &OUTQ       -  OUTPUT QUEUE                                      */
/* &HOLD       -  HOLD STATUS                                       */
/* &SAVE       -  SAVE STATUS                                       */
/* &NQSTAT     -  INDICATOR WHETHER MUST RUN IN NIGHT Q             */
/* &NIGHTQ     -  NIGHT JOB QUEUE                                   */
/* &FRINV      -  COMPANY#      (PASSED AS 15.5)                    */
/* &TOINV      -  ITEM CODE PASS(PASSED AS 15.5)                    */
/* &CUSTN      -  CUSTOMER#     (PASSED AS 15.5)                    */
/* &COPYN      -  NO. OF COPIES (PASSED AS 15.5)                    */
/* &FRDATEN    -  FROM DATE (PASSED AS 15.5)                        */
/* &TODATEN    -  TO DATE   (PASSED AS 15.5)                        */
/*                                                                  */
/********************************************************************/
/* XXXXXX FTXXX 99/99/9999 XXXXXXXXXXXXXXXXXXXXXX                   */
/********************************************************************/
 PMETUPC:    PGM        PARM(&RETURN &APPLIC &DESC &OUTQ &HOLD &SAVE +
                          &NQSTAT &NIGHTQ &FRINV &CUSTN &FRDATEN +
                          &TODATEN &COPYN &TOINV)

/* PARMS                                                            */
        DCL    &RETURN     *CHAR   (7)  /* RETURN CODE              */
        DCL    &APPLIC     *CHAR   (06) /* APPLICATION CODE         */
        DCL    &DESC       *CHAR   (25) /* PRT PGM DESCRIPTION      */
        DCL    &COPYN      *DEC         /* NO. OF COPIES            */
        DCL    &OUTQ       *CHAR   (10) /* OUTPUT QUEUE             */
        DCL    &HOLD       *CHAR   (4)  /* HOLD STATUS              */
        DCL    &SAVE       *CHAR   (4)  /* SAVE STATUS              */
        DCL    &NQSTAT     *CHAR  (01)  /* IF MUST +
                                             RUN IN NIGHT Q         */
        DCL    &NIGHTQ     *CHAR   (10) /* NIGHT JOB QUEUE          */
        DCL    &YEARA      *CHAR   (04) /* FISCAL YEAR IN ALPHA     */
        DCL    &CUSTN      *DEC  /* CUSTOMER NUMBER (PASSED 15.5)   */
        DCL    &COPYN      *DEC  /* NO. OF COPIES   (PASSED 15.5)   */
        DCL    &FRDATEN    *DEC  /* FROM DATE       (PASSED 15.5)   */
        DCL    &TODATEN    *DEC  /* TO DATE         (PASSED 15.5)   */
        DCL    &FRINV      *DEC  /* FROM INVOICE #  (PASSED 15.5)   */
        DCL    &TOINV      *DEC  /* TO INVOICE #    (PASSED 15.5)   */

/* INTERNAL FIELDS                                                  */
        DCL    &COPY       *DEC    (2 0) /* NUMBER OF COPIES        */

/* ERROR MESSAGE FIELDS                                             */
        DCL    &MSG        *CHAR   (512)         /* message         */
        DCL    &MSGID      *CHAR   (7)           /* message id      */
        DCL    &MSGL       *DEC    (5 0)

/********************************************************************/
/*   INITIALIZE VARIABLES                                           */
/********************************************************************/
             CHGVAR     VAR(&RETURN) VALUE('       ')
             CHGVAR     VAR(&COPY) VALUE(&COPYN)
             CHGVAR     VAR(&NQSTAT) VALUE('N')

/********************************************************************/
/*   MONITOR FOR ALL MESSAGES AT PROGRAM LEVEL                      */
/********************************************************************/
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(MSGTRAP))

/********************************************************************/
/*   CALL PROMPT PROGRAM                                            */
/********************************************************************/
             CALL       PGM(PDG0PVR) PARM(&RETURN &DESC &OUTQ &HOLD +
                          &SAVE &COPY &NQSTAT &NIGHTQ &APPLIC)
             CHGVAR     VAR(&COPYN) VALUE(&COPY)
             IF         COND(&RETURN *NE ' ') THEN(GOTO +
                          CMDLBL(USERQUIT))

/********************************************************************/
/*   SUBMIT PRINT CL PROGRAM                                        */
/********************************************************************/
       SUBMIT: SBMJOB   CMD(CALL PGM(PMEQUPC) PARM(' ' &APPLIC &DESC +
                          &OUTQ &HOLD &SAVE &NQSTAT &NIGHTQ &CUSTN +
                          &COPYN &FRDATEN &TODATEN &FRINV &TOINV)) +
                          JOB(CSTACTVTY)
/***** +
 SUBMIT:     CALL       PGM(PMEQUPC) PARM(' ' &APPLIC &DESC &OUTQ +
                          &HOLD &SAVE &NQSTAT &NIGHTQ &CUSTN &COPYN +
                          &FRDATEN &TODATEN &FRINV &TOINV)         +
                                        ************************/
             GOTO       CMDLBL(END)

/*-------------------------USER QUIT--------------------------------*/
 USERQUIT:   CHGVAR     VAR(&RETURN) VALUE('Y2U0005')
             GOTO       CMDLBL(END)

/*----------------------RECEIVE MESSAGES----------------------------*/

 MSGTRAP:    RCVMSG     MSGTYPE(*EXCP) MSGDTA(&MSG) MSGDTALEN(&MSGL) +
                          MSGID(&MSGID)
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(END))

             SNDPGMMSG  MSGID(&MSGID) MSGF(QCPFMSG) MSGDTA(%SST(&MSG +
                          1 &MSGL)) MSGTYPE(*ESCAPE)
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(END))

 END:        ENDPGM
