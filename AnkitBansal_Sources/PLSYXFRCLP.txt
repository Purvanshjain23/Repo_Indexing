/***************************************************************** */
/*                                                                 */
/*  PROGRAM NUMBER:  PLSUYXFRCLP                                   */
/*  PROGRAM NAME:    Purge Tattoo Pending                          */
/*  PROGRAMMER:      SUSAN MASON                                   */
/*  CREATE DATE:     08/09/13                                      */
/*                                                                 */
/*  FUNCTION:        This CL is called from the job scheduler      */
/*                   1. save a version of the tattoo pending       */
/*                   2. run a report that counts records by day    */
/*                   3. purge records based on job date and        */
/*                   prior number of days.  this job will not allow*/
/*                   to get less then 5 days, just in case number  */
/*                   days were less then.                          */
/*                   4. run a report that counts records by day    */
/*                   and shows the number of delete records at     */
/*                   final total count.                            */
/*                   4. RGZPFM tattoo pending (PKFJCPP)            */
/*                                                                 */
/*******************************************************************/
/*  Modification History                                           */
/*                                                                 */
/*   Date     Pgmr     Description                                 */
/* -------- ---------- --------------------------------------------*/
/* 11/05/20   ISE      Recompiled due to change in Buy Order Number*/
/*******************************************************************/
             PGM        PARM(&COALPHA &NOALPHA)

             DCL        VAR(&COALPHA) TYPE(*CHAR) LEN(3)
             DCL        VAR(&NOALPHA) TYPE(*CHAR) LEN(5)
             DCL        VAR(&FLAG) TYPE(*CHAR) LEN(1) VALUE('0')

             DCL        VAR(&FILBGS) TYPE(*CHAR) LEN(7) +
                          VALUE('PKFJCPP')
             DCL        VAR(&FILENS) TYPE(*CHAR) LEN(3)

             DCL        VAR(&FILSAV) TYPE(*CHAR) LEN(10)

             CHGVAR     VAR(&FILENS) VALUE(&COALPHA)
             CHGVAR     VAR(&FILSAV) VALUE(&FILBGS *CAT &FILENS)

             CHKOBJ     OBJ(PRKFHST/&FILSAV) OBJTYPE(*FILE)
             MONMSG     MSGID(CPF9801) EXEC(DO)
             CHGVAR     VAR(&FLAG) VALUE('1')
             ENDDO

/*******************************************************************/
/** Create version of file PRKFHST Crtfile(*yes) or              **/
/** save version of file in PRKFHST *replace                      **/
/*******************************************************************/
             IF         (&FLAG = '1') THEN(DO)

             /*  File is not found so create file.           */
             CPYF       FROMFILE(*LIBL/PKFJCPP) +
                          TOFILE(PRKFHST/&FILSAV) MBROPT(*REPLACE) +
                          CRTFILE(*YES)
             GOTO       CMDLBL(CPYEND)
             ENDDO

             /*  File WAS already save so replace what is    */
             /*  there.                                      */
             CPYF       FROMFILE(*LIBL/PKFJCPP) +
                          TOFILE(PRKFHST/&FILSAV) MBROPT(*REPLACE) +
                          CRTFILE(*NO) FMTOPT(*MAP *DROP)
 CPYEND:
/*******************************************************************/
/** Perform report and then delete records and then report        **/
/*******************************************************************/

             CALL       PGM(PLSYXFR) PARM(' ' &COALPHA &NOALPHA)

/*******************************************************************/
/** RGZPFM  Tattoo pending PKFJCPP                                **/
/*******************************************************************/
             RGZPFM     FILE(PKFJCPP)
             MONMSG     MSGID(CPF3202) EXEC(GOTO CMDLBL(ENDPGM))
             MONMSG     MSGID(CPF2981) EXEC(GOTO CMDLBL(ENDPGM))
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(ENDPGM))

/*******************************************************************/
 ENDPGM:     ENDPGM
