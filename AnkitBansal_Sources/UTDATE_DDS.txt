     A*****************************************************************
     A* UTDATE:  INTERACTIVE DATE CONVERSION
     A* CREATED: LEANNE RAMSEY       1/19/93
     A*
     A**PURPOSE: THIS INTERACTIVE SCREEN CALLS THE UTILITY DATE
     A*          CONVERSION PROGRAM UT80060R.  THIS FUNCTION
     A*          IS INTENDED TO BE USED BY PROGRAMMERS DURING TESTING
     A*          TO DETERMINE DAYS BETWEEN DATES, JULIAN DATE
     A*          EQUIVALENTS, ETC.
     A*****************************************************************
     A*
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/CHFREF)
     A                                      CHGINPDFT(CS UL)
     A                                      MSGLOC(24)
     A                                      PRINT
     A                                      HELP
     A                                      CF03(03 'EXIT')
     A*
     A*--------------------------------------------------------------*
     A*  RECORD FORMAT ONE
     A*--------------------------------------------------------------*
     A          R UTDATER1
     A                                      OVERLAY
     A                                  1  3'UTDATE'
     A                                  1 25'INTERACTIVE DATE CONVERSION'
     A                                      DSPATR(HI)
     A                                  1 65'DATE:'
     A                                  1 71DATE
     A                                      EDTCDE(Y)
     A                                  2  3'UTDATER1'
     A                                  2 24'CALLS UTILITY PROGRAM UT80060R'
     A                                      DSPATR(HI)
     A                                  2 65'TIME:'
     A                                  2 71TIME
     A                                  4  3'PFRM8   From Date -------->'
     A                                      DSPATR(HI)
     A            PFRM8          8S 0B  4 32CHECK(FE)
     A                                  5  3'PTO8    To Date ---------->'
     A                                      DSPATR(HI)
     A            PTO8           8S 0B  5 32
     A                                  7  3'PFRFMT  From Date Format ->'
     A                                      DSPATR(HI)
     A            PFRFMT         4A  B  7 32CHECK(FE)
     A                                  7 44'(MDY, DMY, YMD, CYMD, JUL)'
     A                                  8  3'PTOFMT  To Date Format --->'
     A                                      DSPATR(HI)
     A            PTOFMT         4A  B  8 32CHECK(FE)
     A                                  8 44'(MDY, DMY, YMD, CYMD, JUL)'
     A                                 10  3'PDAYS   Number of Days --->'
     A                                      DSPATR(HI)
     A            PDAYS          4S 0B 10 32
     A                                 11  3'PCODE   Function --------->'
     A                                      DSPATR(HI)
     A            PCODE          1A  B 11 32
     A                                 11 44'(INPUT: +, -, B, I, D, W, Blank)'
     A                                 12 44'(OUTPUT: 1-7 for Sunday - Sat)'
     A                                 16  3'PRTRN   Return Code ------>'
     A                                      DSPATR(HI)
     A            PRTRN          1A  O 16 32
     A                                 16 44'blank = valid date'
     A                                 17 44'1 = date cannot be zero'
     A                                 18 44'2 = invalid date'
     A                                 19 44'3 = from date format is invalid'
     A                                 20 44'4 = to date format is invalid'
     A                                 21 44'5 = from date is not in range'
     A                                 22 44'6 = to date is invalid'
      *--------------------------------------------------------------*
      *  COMMAND LINE ONE
      *--------------------------------------------------------------*
     A          R UTDATEK1
     A                                      OVERLAY
     A                                 23 02'F3-Exit'
