     A*%%TS  SD  20040115  095029  ISLRAMS     REL-V5R2M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD472
     A*  TITLE:         SPECIFY HOG MOVEMENT OUT SUMMARY REPORT OPTIONS
     A*  PROGRAMMER:    MARY PALMER
     A*  DATE:          06/24/94
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A N99                                  CF04(04 'PROMPT')
     A N99 44                               CF08(08 'SUBMIT')
     A N99                                  CF17(17 'PRINT OPTIONS')
     A N99                                  CF20(20 'WRKSPLF')
     A*
     A*--------------------------------------------------------------*
     A* RECORD FORMAT ONE
     A*--------------------------------------------------------------*
     A          R HP472R1
     A*%%TS  SD  20040115  095029  ISLRAMS     REL-V5R2M0  5722-WDS
     A                                      SETOF(31)
     A                                      SETOF(32)
     A                                      SETOF(33)
     A                                      SETOF(34)
     A                                      SETOF(35)
     A                                      SETOF(36)
     A                                      SETOF(37)
     A                                      SETOF(38)
     A                                      SETOF(39)
      *
     A                                      SETOF(51)
     A                                      SETOF(52)
     A                                      SETOF(53)
     A                                      SETOF(54)
     A                                      SETOF(57)
     A                                      SETOF(58)
     A                                      SETOF(59)
     A                                      OVERLAY
     A                                      RTNCSRLOC(&RCD &FLD)
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 27'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  2 15'SPECIFY HOG MOVEMENT OUT SUMMARY R-
     A                                      EPORT OPTIONS'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  4  2'Business office ..........'
     A                                  4 29'?'
     A            DFFSBO    R        B  4 31REFFLD(BOBOBO)
     A N31                                  DSPATR(HI)
     A  31
     AO 51                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A            DFBODS    R        O  4 41REFFLD(BOBODS)
     A                                  5  2'From received date .......'
     A            DFFMDY    R     Y  B  5 31REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A N32                                  DSPATR(HI)
     A  32                                  DSPATR(PC)
     A  32                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A                                  6  2'To received date .........'
     A            DFTMDY    R     Y  B  6 31REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A N33                                  DSPATR(HI)
     A  33                                  DSPATR(PC)
     A  33                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A                                  7  2'Origin production type ...'
     A                                  7 29'?'
     A            DFPTCD    R        B  7 31REFFLD(PTPTCD)
     A N34                                  DSPATR(HI)
     A  34
     AO 52                                  DSPATR(PC)
     A  34                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A            DFPTDS    R        O  7 41REFFLD(PTPTDS)
     A                                  8  2'Origin production phase ..'
     A                                  8 29'?'
     A            DFPPCD    R        B  8 31REFFLD(PPPPCD)
     A N35                                  DSPATR(HI)
     A  35
     AO 53                                  DSPATR(PC)
     A  35                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A            DFPPDS    R        O  8 41REFFLD(PPPPDS)
     A                                  9  2'Origin farm site .........'
     A                                  9 29'?'
     A            DFFSCD    R        B  9 31REFFLD(FSFSCD)
     A                                      EDTCDE(M)
     A N36                                  DSPATR(HI)
     A  36
     AO 54                                  DSPATR(PC)
     A  36                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A            DFFSNM    R        O  9 41REFFLD(FSFSNM)
     A                                 11  2'Include on the report:'
     A                                 12  5'Transfer movements ....'
     A                                 12 29'?'
     A            DFTRANFL  R        B 12 31REFFLD(HSRREF/@@FLAG *LIBL/HSPREF)
     A N37                                  DSPATR(HI)
     A  37
     AO 57                                  DSPATR(PC)
     A  37                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A                                 13  5'Sales movements .......'
     A                                 13 29'?'
     A            DFSALEFL  R        B 13 31REFFLD(HSRREF/@@FLAG *LIBL/HSPREF)
     A N38                                  DSPATR(HI)
     A  38
     AO 58                                  DSPATR(PC)
     A  38                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A                                 14  8'Sale type code .....'
     A            DFSTCD    R        B 14 31REFFLD(HSRREF/@@STCD *LIBL/HSPREF)
     A N39                                  DSPATR(HI)
     A  39
     AO 59                                  DSPATR(PC)
     A  39                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A                                 14 29'?'
     A            DFSTDS    R        O 14 41REFFLD(HSRREF/STSTDS *LIBL/HSPREF)
     A*
     A*--------------------------------------------------------------*
     A* COMMAND KEY RECORD 1
     A*--------------------------------------------------------------*
     A          R HP472K1
     A*%%TS  SD  20000208  073741  ISLRAMS     REL-V4R2M0  5769-PW1
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A N99                             23 11'F4=Prompt'
     A  44N99                          23 22'F8=Submit'
     A N99                             23 33'F17=Print Options'
     A N99                             23 52'F20=Spool Files'
      *
      *--------------------------------------------------------------*
      * ERROR MESSAGE SUBFILE FORMAT
      *--------------------------------------------------------------*
     A          R HP472ES                   SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
     A*
     A*--------------------------------------------------------------*
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*--------------------------------------------------------------*
     A*
     A          R HP472EC                   SFLCTL(HP472ES)
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
