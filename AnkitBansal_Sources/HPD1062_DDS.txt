     A*%%TS  SD  20090728  142318  ISLRAMS     REL-V5R4M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION
     A*  DISPLAY FILE:  HPD1062
     A*  TITLE:         UT: EDIT (MAINTAIN) UPLOADED TRANSFERS
     A*  PROGRAMMER:    LEANNE RAMSEY
     A*  DATE:          11/11/99
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A                                      CF04(04 'PROMPT')
     A                                      CF05(05 'REFRESH')
     A                                      CF09(09 'ACCEPT')
     A                                      CF13(13 'FOLD/DROP')
     A*---------------------------------------------------------------
     A* SUBFILE FORMAT 1
     A*---------------------------------------------------------------
     A*
     A          R HP1062S1                  SFL
     A*%%TS  SD  20090728  142318  ISLRAMS     REL-V5R4M0  5722-WDS
     A  85                                  SFLNXTCHG
     A            HDRRN1         4Y 0H
     A*
     A            HDORHGSN  R        H      REFFLD(HGHGSN)
     A            HDORFSNM  R        H      REFFLD(FSFSNM)
     A            HDORGSCD  R        H      REFFLD(HGGSCD)
     A            HDDNHGSN  R        H      REFFLD(HGHGSN)
     A            HDDNFSNM  R        H      REFFLD(FSFSNM)
     A            HDDNGSCD  R        H      REFFLD(HGGSCD)
     A            HDSPLIT   R        H      REFFLD(E1REC/E1SPLIT *LIBL/HSP2062)
     A            HDTYPE    R        H      REFFLD(E1REC/E1TYPE *LIBL/HSP2062)
     A            HDSCDT    R        H      REFFLD(HSRREF/@@DT08 *LIBL/HSPREF)
     A*
     A            D1MDY     R        B  7  2REFFLD(HSRREF/@@DT06 *LIBL/HSPREF)
     A  39                                  DSPATR(PC)
     A  39                                  DSPATR(RI)
     A N39                                  DSPATR(HI)
     A N39                                  DSPATR(UL)
     A                                      EDTWRD('  /  /  ')
     A*
     A            D1ORFSCD  R        B  7 11REFFLD(@@FSCD)
     A  31
     AO 51                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A N31                                  DSPATR(HI)
     A N31                                  DSPATR(UL)
     A            D1ORBLCD  R        B  7 18REFFLD(@@BLCD)
     A  35
     AO 55                                  DSPATR(PC)
     A  35                                  DSPATR(RI)
     A N35                                  DSPATR(HI)
     A N35                                  DSPATR(UL)
     A            D1ORRMCD  R        B  7 24REFFLD(@@RMCD)
     A  40
     AO 50                                  DSPATR(PC)
     A  40                                  DSPATR(RI)
     A N40                                  DSPATR(HI)
     A N40                                  DSPATR(UL)
     A            D1ORHGCD  R        B  7 30REFFLD(HGHGCD)
     A  34
     AO 54                                  DSPATR(PC)
     A  34                                  DSPATR(RI)
     A N34                                  DSPATR(HI)
     A N34                                  DSPATR(UL)
     A            D1SCHD    R        B  7 38REFFLD(MDSCHD)
     A                                      EDTCDE(K)
     A  38                                  DSPATR(PC)
     A  38                                  DSPATR(RI)
     A N38                                  DSPATR(HI)
     A N38                                  DSPATR(UL)
     A            D1DNFSCD  R        B  7 46REFFLD(@@FSCD)
     A  32
     AO 52                                  DSPATR(PC)
     A  32                                  DSPATR(RI)
     A N32                                  DSPATR(HI)
     A N32                                  DSPATR(UL)
     A            D1DNBLCD  R        B  7 53REFFLD(@@BLCD)
     A  37
     AO 57                                  DSPATR(PC)
     A  37                                  DSPATR(RI)
     A N37                                  DSPATR(HI)
     A N37                                  DSPATR(UL)
     A            D1DNRMCD  R        B  7 59REFFLD(@@RMCD)
     A  48
     AO 58                                  DSPATR(PC)
     A  48                                  DSPATR(RI)
     A N48                                  DSPATR(HI)
     A N48                                  DSPATR(UL)
     A            D1DNHGCD  R     A  B  7 65REFFLD(HGHGCD)
     A  36
     AO 56                                  DSPATR(PC)
     A  36                                  DSPATR(RI)
     A N36                                  DSPATR(HI)
     A N36                                  DSPATR(UL)
     A            D1MVSN    R        O  7 73REFFLD(HSRREF/@@MVSN *LIBL/HSPREF)
     A                                      EDTCDE(Z)
     A                                  8 11'Phase/Type:'
     A                                      COLOR(BLU)
     A            D1ORPPCD  R        O  8 23REFFLD(HSRREF/@@PPCD *LIBL/HSPREF)
     A                                      COLOR(BLU)
     A            D1ORPTCD  R        O  8 29REFFLD(HSRREF/@@PTCD *LIBL/HSPREF)
     A                                      COLOR(BLU)
     A                                  8 35'Grp typ:'
     A                                      COLOR(BLU)
     A            D1ORGTCD  R        O  8 44REFFLD(HSRREF/@@GTCD *LIBL/HSPREF)
     A                                      COLOR(BLU)
     A                                  8 46'Phase/Type:'
     A                                      COLOR(BLU)
     A            D1DNPPCD  R        O  8 58REFFLD(HSRREF/@@PPCD *LIBL/HSPREF)
     A                                      COLOR(BLU)
     A            D1DNPTCD  R        O  8 64REFFLD(HSRREF/@@PTCD *LIBL/HSPREF)
     A                                      COLOR(BLU)
     A                                  8 70'Grp typ:'
     A                                      COLOR(BLU)
     A            D1DNGTCD  R        O  8 79REFFLD(HSRREF/@@GTCD *LIBL/HSPREF)
     A                                      COLOR(BLU)
     A            D1ORFSBO  R        H      REFFLD(FSFSBO)
     A            D1DNFSBO  R        H      REFFLD(FSFSBO)
     A*
     A*----------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT  1
     A*----------------------------------------------------------------
     A*
     A          R HP1062C1                  SFLCTL(HP1062S1)
     A*%%TS  SD  20090728  142318  ISLRAMS     REL-V5R4M0  5722-WDS
     A                                      SFLSIZ(0100)
     A                                      SFLPAG(0007)
     A N82                                  ROLLUP(25 'PAGE FORWARD')
     A                                      SFLCSRRRN(&HDRR1)
     A                                      OVERLAY
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND(*MORE)
     A  78                                  SFLFOLD(CF13)
     A  79                                  SFLDROP(CF13)
     A                                      SFLMODE(&FOLD)
     A                                      SETOF(31)
     A                                      SETOF(32)
     A                                      SETOF(34)
     A                                      SETOF(35)
     A                                      SETOF(36)
     A                                      SETOF(37)
     A                                      SETOF(38)
     A                                      SETOF(39)
     A                                      SETOF(40)
     A                                      SETOF(48)
     A                                      SETOF(50)
     A                                      SETOF(51)
     A                                      SETOF(52)
     A                                      SETOF(54)
     A                                      SETOF(55)
     A                                      SETOF(56)
     A                                      SETOF(57)
     A                                      SETOF(58)
     A                                      RTNCSRLOC(&RCD &FLD)
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            HDRR1          5S 0H
     A            FOLD           1A  H
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 26'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A            D1MODE    R        O  2  2REFFLD(@@MODE)
     A                                      DSPATR(HI)
     A                                  2 21'EDIT (MAINTAIN) UPLOADED TRANSFERS'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  3  2'Business Office.....'
     A                                      COLOR(BLU)
     A            SDBOBO         5A  O  3 23
     A                                      DSPATR(HI)
     A            SDBODS        30A  O  3 30
     A                                      COLOR(BLU)
     A                                  4  4'Sched'
     A                                  4 11'------------ ORIGIN -------------'
     A                                      DSPATR(HI)
     A                                  4 46'------ DESTINATION -------'
     A                                      DSPATR(HI)
     A                                  5  4'Ship'
     A                                  5 13'?'
     A                                  5 19'?'
     A                                  5 26'?'
     A                                  5 32'?'
     A                                  5 39'Sched'
     A                                  5 48'?'
     A                                  5 55'?'
     A                                  5 61'?'
     A                                  5 67'?'
     A                                  5 72'Movement'
     A                                  6  4'Date'
     A                                  6 11'Farm'
     A                                  6 18'Bldg'
     A                                  6 24'Room'
     A                                  6 30'Group'
     A                                  6 40'Head'
     A                                  6 46'Farm'
     A                                  6 53'Bldg'
     A                                  6 59'Room'
     A                                  6 65'Group'
     A                                  6 74'Number'
     A*----------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*----------------------------------------------------------------
     A          R HP1062K1
     A*%%TS  SD  19991111  095829  ISLRAMS     REL-V4R2M0  5769-PW1
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A                                 23 11'F4=Prompt'
     A                                 23 22'F5=Refresh'
     A                                 23 34'F9=Accept'
     A                                 23 59'F13=Fold/Unfold'
      *----------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *----------------------------------------------------------------
     A          R HP1062ES                  SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
     A*----------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*----------------------------------------------------------------
     A*
     A          R HP1062EC                  SFLCTL(HP1062ES)
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
