     A*%%TS  SD  20000830  104745  ISLRAMS     REL-V4R4M0  5769-PW1
     A*****************************************************************
     A*
     A*  SYSTEM:        HOG PRODUCTION
     A*  DISPLAY FILE:  HPD500 - SELECT SCHEDULED TRANSFER MOVEMENT
     A*  CREATED BY:    LEANNE FEDOR
     A*  DATE:          02/09/96
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA12(12 'PREVIOUS')
     A N82                                  ROLLUP(25 'PAGE FORWARD')
     A*---------------------------------------------------------------
     A* REFERENCE WINDOW RECORD FORMAT 1
     A*---------------------------------------------------------------
     A          R HP500R1
     A*%%TS  SD  19960202  083414  ISLRAMS     REL-V3R1M0  5763-PW1
     A                                      TEXT('Reference window')
     A                                      WINDOW(*DFT 17 75)
     A                                      WDWBORDER((*DSPATR RI) (*CHAR '    -
     A                                          '))
     A*---------------------------------------------------------------
     A* RECORD FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP500R2
     A                                      TEXT('Written in RPG pgm to prevent-
     A                                       screen from clearing')
     A                                      CLRL(*NO)
     A                                      OVERLAY
     A*---------------------------------------------------------------
     A* RECORD FORMAT TWO
     A*---------------------------------------------------------------
     A* THIS FORMAT IS NOT WRITTEN IN THE RPG PGM.  IT PREVENTS THE
     A* CALLING_PROGRAM_SCREEN FROM BEING CLEARED.
     A*---------------------------------------------------------------
     A          R HP500R3
     A                                      TEXT('Dummy format not written in R-
     A                                      PG pgm.')
     A                                      ASSUME
     A                                 11  1' '
     A*----------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*----------------------------------------------------------------
     A          R HP500S1                   SFL
     A*%%TS  SD  20000830  104745  ISLRAMS     REL-V4R4M0  5769-PW1
     A                                      TEXT('subfile record format one')
     A            HDRRN1         4S 0H
     A            DFSMDY    R        O  7  1REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  ')
     A            DFMVSN    R        O  7 11REFFLD(@@MVSN)
     A                                      EDTCDE(M)
     A            DFORBO    R        O  7 20REFFLD(@@BOCD)
     A            DFORPT    R        O  7 26REFFLD(@@PTCD)
     A            DFORPP    R        O  7 32REFFLD(@@PPCD)
     A            DFORFS    R        O  7 38REFFLD(@@FSCD)
     A                                      EDTCDE(Z)
     A            DFDNBO    R        O  7 45REFFLD(@@BOCD)
     A            DFDNPT    R        O  7 51REFFLD(@@PTCD)
     A            DFDNPP    R        O  7 57REFFLD(@@PPCD)
     A            DFDNFS    R        O  7 63REFFLD(@@FSCD)
     A                                      EDTCDE(Z)
     A            DFPTCT    R        O  7 72REFFLD(HSRREF/@@PTCT *LIBL/HSPREF)
     A                                      EDTCDE(Z)
     A*--------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*--------------------------------------------------------------
     A          R HP500C1                   SFLCTL(HP500S1)
     A*%%TS  SD  20000830  104745  ISLRAMS     REL-V4R4M0  5769-PW1
     A                                      SFLSIZ(0050)
     A                                      SFLPAG(0008)
     A                                      WINDOW(HP500R1)
     A                                      TEXT('control format one')
     A                                      OVERLAY
     A                                      SFLCSRRRN(&HDRRN)
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND(*MORE)
     A                                      RMVWDW
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            HDRRN          5S 0H
     A                                  1 22'SELECT SCHEDULED TRANSFER MOVEMENT'
     A                                      DSPATR(HI)
     A                                  2  1'Position Cursor, Press Enter'
     A                                  4  1'Scheduled'
     A                                      DSPATR(HI)
     A                                  4 20'--------- ORIGIN -------'
     A                                      DSPATR(HI)
     A                                  4 45'----- DESTINATION ------'
     A                                      DSPATR(HI)
     A                                  4 71'Times'
     A                                      DSPATR(HI)
     A                                  5  1'Ship Date'
     A                                      DSPATR(HI)
     A                                  5 11'Movement'
     A                                      DSPATR(HI)
     A                                  5 21'B/O'
     A                                      DSPATR(HI)
     A                                  5 26'Type'
     A                                      DSPATR(HI)
     A                                  5 32'Phase'
     A                                      DSPATR(HI)
     A                                  5 39'Farm'
     A                                      DSPATR(HI)
     A                                  5 46'B/O'
     A                                      DSPATR(HI)
     A                                  5 51'Type'
     A                                      DSPATR(HI)
     A                                  5 57'Phase'
     A                                      DSPATR(HI)
     A                                  5 64'Farm'
     A                                      DSPATR(HI)
     A                                  5 71'Print'
     A                                      DSPATR(HI)
     A            C1SMDY    R        B  6  1REFFLD(@@DT06)
     A                                      DSPATR(HI)
     A                                      EDTWRD('  /  /  -')
     A            C1ORBO    R        B  6 20REFFLD(@@BOCD)
     A                                      DSPATR(HI)
     A            C1ORPT    R        B  6 26REFFLD(@@PTCD)
     A                                      DSPATR(HI)
     A            C1ORPP    R        B  6 32REFFLD(@@PPCD)
     A                                      DSPATR(HI)
     A            C1ORFS    R        B  6 38REFFLD(@@FSCD)
     A                                      DSPATR(HI)
     A                                      EDTCDE(M)
     A            C1DNBO    R        B  6 45REFFLD(@@BOCD)
     A                                      DSPATR(HI)
     A            C1DNPT    R        B  6 51REFFLD(@@PTCD)
     A                                      DSPATR(HI)
     A            C1DNPP    R        B  6 57REFFLD(@@PPCD)
     A                                      DSPATR(HI)
     A            C1DNFS    R        B  6 63REFFLD(@@FSCD)
     A                                      DSPATR(HI)
     A                                      EDTCDE(M)
     A*----------------------------------------------------------------
     A          R HP500K1
     A*----------------------------------------------------------------
     A                                      OVERLAY
     A                                      WINDOW(HP500R1)
     A                                 16  1'F12=Previous'
