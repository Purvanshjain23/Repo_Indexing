     Z* CRTBNDRPG
     Z* DFTACTGRP(*NO) BNDDIR(YBNDDIR) DBGVIEW(*SOURCE)
     Z* CVTOPT(*DATETIME) ACTGRP(*CALLER) OPTIMIZE(*BASIC)
      *****************  RPG PROGRAM HEADING  ************************
      *                                                              *
      * PROGRAM NUMBER..... - VNDFAXRW                               *
      *          DATE....... -  3/20/2020                            *
      *          AUTHOR..... - Brad Baden   SDN465   TFS# 16169      *
      *          DESCRIPTION - THIS PROGRAM IS CALLED FROM THE HPE   *
      *                        (HOG PROCUREMENT) SYSTEM.  IT IS      *
      *                        USED TO RETRIEVE THE NAME AND ADDRESS *
      *                        FOR THE VENDOR NUMBER THAT IS PASSED  *
      *                        IN.                                   *
      ****************************************************************
      *  REVISIONS                                                   *
      ****************************************************************
      *                                                              *
      *  REV # - 00     DATE - MM/DD/YY     PROGRAMMER - XXX         *
      *                                                              *
      *  CHANGES MADE -        10/05/95     BARB GUTIERREZ           *
      *                        ADDED COUNTRY CODE PROCESSING         *
      *                                                              *
      *  CHANGES MADE -        05/09/96     JONI MILLER              *
      *                        JDE RELEASE A7.1 CHANGES              *
      *                                                              *
      *  CHANGES MADE -        02/19/97     BARB GUTIERREZ           *
      *                        ADDED FAX PHONE NUMBER PROCESSING     *
      *  CHANGES MADE -        10/02/97     BARB GUTIERREZ           *
      *                        ADDED ANOTHER FAX LINE                *
      *                                                              *
      *  CHANGES MADE -         3/20/2020   Brad Baden               *
      *       SDN465           Copy source from JFOODCUST and rename *
      *                        VNDFAXRW.                             *
      *                                                              *
      ****************************************************************
      /EJECT
      ****************************************************************
      *   F :             FILE SPECIFICATIONS                        *
      ****************************************************************
      /SPACE 2
     FF0101LA   IF   E           K DISK
      * JDE ADDRESS BOOK CONTROL FILE (KEY: ABAN8)
     FF0116     IF   E           K DISK
      * JDE ADDRESS BOOK - ADDRESSES (KEY: ALAN8 ALEFTB)
     FF0115     IF   E           K DISK
      * JDE ADDRESS BOOK - PHONE #'S (KEY: WPAN8 WPIDLN WPRCK7)
      ****************************************************************
      *   C :             CALC SPECIFICATIONS                        *
      ****************************************************************
      *
      *   CHAIN TO ADDRESS BOOK CONTROL FILE
      *
     C     NUM           CHAIN     I0101                              50
     C     *IN50         IFEQ      *OFF                                         50 = OFF
     C                   MOVE      *BLANKS       WKRETN            7
     C*
     C                   MOVEL     ABTAX         WKSS#            16
     C                   MOVE      ABALPH        WKNAME           40
      *
      *   IF PERSON/CORPORATION FIELD IN JDE HAS A 'P' IN IT, THIS
      *   VENDOR WILL BE PROCESSED FOR A 1099.  THE SOCIAL SECURITY
      *   IS REQUIRED.
      *
     C     ABTAXC        IFEQ      'P'
     C                   MOVE      'Y'           WK1099            1
     C                   ELSE                                                   50 = ON
     C                   MOVE      'N'           WK1099
     C                   ENDIF
      *
      *   CHAIN TO ADDRESS BOOK ADDRESS FILE
      *
     C     NUM           CHAIN     I0116                              51
     C     *IN51         IFEQ      *OFF
     C*
     C                   MOVE      ALADD1        WKADR1           40
     C                   MOVE      ALADD2        WKADR2           40
     C                   MOVE      ALADD3        WKADR3           40
     C*
     C                   MOVEL     ALCTY1        WKCITY           40
     C                   MOVEL     ALADDS        WKSTAT            3
     C                   MOVEL     ALADDZ        WKZIP            12
     C                   MOVE      ALCTR         WKCTR             3
     C*
     C                   ENDIF                                                  51 = OFF    '
      *
      *   CHAIN TO ADDRESS BOOK PHONE NUMBERS FILE
      *
      *   RETREIVE FAX NUMBER 2
      *
     C                   Z-ADD     3             WKRCK7
     C     FILKEY        CHAIN     I0115                              52
     C     *IN52         IFEQ      *OFF
     C*
     C     WPPHTP        IFEQ      'F'
     C                   MOVEL     WPAR1         WKFAR2            6
     C                   MOVEL     WPPH1         WKFPH2           20
     C                   ENDIF
     C*
     C                   ENDIF                                                  52 = OFF    '
      *
      *   RETREIVE FAX NUMBER 2
      *
     C                   Z-ADD     2             WKRCK7
     C     FILKEY        CHAIN     I0115                              52
     C     *IN52         IFEQ      *OFF
     C*
     C     WPPHTP        IFEQ      'F'
     C                   MOVEL     WPAR1         WKFAR             6
     C                   MOVEL     WPPH1         WKFPHO           20
     C                   ENDIF
     C*
     C                   ENDIF                                                  52 = OFF    '
      *
      *   RETREIVE PHONE NUMBER
      *
     C                   Z-ADD     1             WKRCK7
     C     FILKEY        CHAIN     I0115                              52
     C     *IN52         IFEQ      *OFF
     C*
     C                   MOVEL     WPAR1         WKAREA            6
     C                   MOVEL     WPPH1         WKPHON           20
     C*
     C                   ENDIF                                                  52 = OFF    '
     C*
     C*  IF THE VENDOR PASSED IN DOES NOT EXIST IN JDE, MOVE ERR IN
     C*  RETURN FIELD AND BLANKS OUT PARMS.
     C*
     C                   ELSE                                                   50 = ON
     C                   MOVEL     'ERR'         WKRETN
     C                   MOVE      *BLANKS       WKNAME
     C                   MOVE      *BLANKS       WKADR1
     C                   MOVE      *BLANKS       WKADR2
     C                   MOVE      *BLANKS       WKADR3
     C                   MOVE      *BLANKS       WKCITY
     C                   MOVE      *BLANKS       WKSTAT
     C                   MOVE      *BLANKS       WKZIP
     C                   MOVE      *BLANKS       WKAREA
     C                   MOVE      *BLANKS       WKPHON
     C                   MOVE      *BLANKS       WKFAR
     C                   MOVE      *BLANKS       WKFPHO
     C                   MOVE      *BLANKS       WKFAR2
     C                   MOVE      *BLANKS       WKFPH2
     C                   MOVE      *BLANKS       WKSS#
     C                   MOVE      *BLANKS       WK1099
     C                   MOVE      *BLANKS       WKCTR
     C                   ENDIF                                                  50 = OFF
      *
      *  SETUP PARM FIELDS TO PASS BACK TO CALLING PROGRAM.
      *
     C                   MOVE      WKRETN        RETRN
     C                   MOVE      WKNAME        NAME
     C                   MOVE      WKADR1        ADDR1
     C                   MOVE      WKADR2        ADDR2
     C                   MOVE      WKADR3        ADDR3
     C                   MOVE      WKCITY        CITY
     C                   MOVE      WKSTAT        STATE
     C                   MOVE      WKZIP         ZIP
     C                   MOVE      WKAREA        AREA
     C                   MOVE      WKPHON        PHONE
     C                   MOVE      WKFAR         FAREA
     C                   MOVE      WKFPHO        FPHON
     C                   MOVE      WKFAR2        FAREA2
     C                   MOVE      WKFPH2        FPHON2
     C                   MOVE      WK1099        P1099
     C                   MOVE      WKSS#         TAXID
     C                   MOVE      WKCTR         CNTRY
      *
     C                   MOVE      *ON           *INLR
      *
      *--------------------------------------------------------------*
      *   *INZSR   INITIALIZATION ROUTINE                            *
      *--------------------------------------------------------------*
      *
     C     *ENTRY        PLIST
     C                   PARM                    RETRN             7
     C                   PARM                    NUM               8 0
     C                   PARM                    NAME             40
     C                   PARM                    ADDR1            40
     C                   PARM                    ADDR2            40
     C                   PARM                    ADDR3            40
     C                   PARM                    CITY             40
     C                   PARM                    STATE             3
     C                   PARM                    ZIP              12
     C                   PARM                    AREA              6
     C                   PARM                    PHONE            20
     C                   PARM                    FAREA             6
     C                   PARM                    FPHON            20
     C                   PARM                    FAREA2            6
     C                   PARM                    FPHON2           20
     C                   PARM                    P1099             1
     C                   PARM                    TAXID            16
     C                   PARM                    CNTRY             3
     C                   PARM                    PTSTS             1
     C*
      * Complete file key
     C     FILKEY        KLIST
     C                   KFLD                    NUM
     C                   KFLD                    WKIDLN
     C                   KFLD                    WKRCK7
      *
     C     *LIKE         DEFINE    WPIDLN        WKIDLN
     C     *LIKE         DEFINE    WPRCK7        WKRCK7
      *
     C                   Z-ADD     0             WKIDLN
