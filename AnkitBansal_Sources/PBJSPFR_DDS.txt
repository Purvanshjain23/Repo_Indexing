     T* PRT TRAFFIC CARD---GUY PF Print file
     Z* CRTPRTF
     Z* DEVTYPE(*AFPDS)
     Z* FRONTOVL(*LIBL/TRAFCARD) SCHEDULE(*FILEEND)
     Z* THIS PRINTS ON 4" BY 7" CARDSTOCK
     H* MEMBER-ID: PBJSPFR$
      *
     H* Function type : Print file
      *
     H* Company       : OMS Development Model
     H* System        : OMS Development Model
     H* User name     : ISLRAMS
     H* Date          : 08/26/13
      *=========================================================================
     A                                      INDARA
      *=========================================================================
     A          R H1HDR                     TEXT('LOAD INFO')
      *
      * Carrier (kinda)
     A            H1NANA         4        41TEXT('Carrier')
     A                                      SKIPB(2)
‚    A                                      HIGHLIGHT
‚    A                                      FONT(400 (*POINTSIZE 21))
‚    A
      * Trailer ID
     A            H1DXTX        20        54TEXT('Trailer Id')
‚    A                                      HIGHLIGHT
‚    A                                      FONT(400 (*POINTSIZE 21))
      * Load ID
     A            H1RMNB         7  0     41TEXT('Load ID')
     A                                      EDTCDE(Z)
     A                                      SKIPB(3)
‚    A                                      HIGHLIGHT
‚    A                                      FONT(400 (*POINTSIZE 19))
      * Reefer Temp
     A            H1RNNB         5  2     74TEXT('Reefer Temperature')
     A                                      EDTCDE(L)
‚    A                                      HIGHLIGHT
‚    A                                      FONT(400 (*POINTSIZE 19))
      * Deadline Departure Date
     A            H1KZDTMDY      6  0     41TEXT('Deadline Departure Date')
     A                                      EDTWRD('  /  /  ')
     A                                      SKIPB(4)
‚    A                                      HIGHLIGHT
‚    A                                      FONT(400 (*POINTSIZE 19))
      * Deadline Departure Time
     A            H1AXTM         4  0     61TEXT('Deadline Departure Time')
     A                                      EDTWRD('  :  ')
‚    A                                      HIGHLIGHT
‚    A                                      FONT(400 (*POINTSIZE 19))
      * Warehouse
     A            H1VPCD         4        85TEXT('Ship From Warehouse')
‚    A                                      HIGHLIGHT
‚    A                                      FONT(400 (*POINTSIZE 21))
      * Scheduled Ship Date
     A            H1AODTMDY      6  0     41TEXT('Scheduled Ship Date')
     A                                      SKIPB(6)
     A                                      EDTWRD('  /  /  ')
‚    A                                      HIGHLIGHT
‚    A                                      FONT(11)
      * Sequence Number
     A            H1T4NB         4  0     56TEXT('Sequence Number')
     A                                      EDTCDE(L)
‚    A                                      HIGHLIGHT
‚    A                                      FONT(11)
      * On-Yard Date
     A            H1KKDTMDY      6  0     77TEXT('On-Yard Date')
     A                                      EDTWRD('  /  /  ')
‚    A                                      FONT(75)
     A                                      SKIPB(7)
      * On-Yard Time
     A            H1AVTM         4  0     88TEXT('On-Yard Time')
     A                                      EDTWRD('  :  ')
‚    A                                      FONT(75)
      * Requested Delivery Date
     A            H1EIDTMDY      6  0     41TEXT('Requested Delivery Date')
     A                                      SKIPB(8)
     A                                      EDTWRD('  /  /  ')
‚    A                                      HIGHLIGHT
‚    A                                      FONT(11)
      * Ready for Loading Date
     A            H1J4DTMDY      6  0     77TEXT('Ready for Loading Date')
     A                                      EDTWRD('  /  /  ')
‚    A                                      FONT(75)
      * Ready for Loading Time
     A            H1TRTM         4  0     88TEXT('Ready for Loading Time')
     A                                      EDTWRD('  :  ')
‚    A                                      FONT(75)
     A                                      SKIPA(9)
      *=========================================================================
      *
     A          R L1DTL                     TEXT('Stops')
      * Stop
     A            L1RVNB         5  0     39TEXT('Stop/Drop')
‚    A                                      FONT(66)
     A                                      EDTCDE(Z)
     A                                      SPACEB(1)
      * Ship To Cust Name
     A            L1HNNA        24        47TEXT('Ship To Customer Name')
‚    A                                      FONT(66)
      * Ship To City/State
     A            L1CITYST      23        47TEXT('Ship To City/State')
‚    A                                      FONT(66)
     A                                      SPACEB(1)
      *
