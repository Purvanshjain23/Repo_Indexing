      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Hog Production System
      * PROGRAM:     HP6010
      * TITLE:       Listing of Sire Line Sources
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     10/25/01
      *
      * FUNCTION:  Batch program to list the records in the file.
      *            This listing is submitted from HP4010-Work With Sire Line Sources
      *
      *            The listing 'selections' will be the positioner/selector values
      *            in the control record at the time the user submitted the listing.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fhsp099    if   e           k disk
      *    Sire line sources
      *
      *
     Fqprint    o    f   80        printer oflind(*inof)
      *
      /EJECT
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Standard fields
      *
     D prtfl           s              1    inz('N')
     D dash            s             80
     d rtime           s              6  0
      *
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *---------------------------------------------------------------
      * Local data area.
      *---------------------------------------------------------------
      *
     Dlda             uds                  dtaara(*lda)
     D  ldsscd                 1      5
     D  ldaist                 6      6
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * MAINLINE
      ****************************************************************
      *
      * Prepare to read the file and print headings the first
      * time through.
      *
     C     ldsscd        setll     hsp099
     C                   except    h1hdr
      *
      * Read all records in the file. Check the user's selected status with
      * the status in the record before printing.
      *
     C                   dou       *in90 = *on                                  Main do
     C                   read      hsp099                                 90
     C                   if        *in90 = *off and                             If not EOF
     C                             (ldaist = ' ' or ldaist = ssaist)
      *
      * Print detail line
      *
     C                   if        *inof = *on
     C                   except    h1hdr
     C                   endif
      *
     C                   except    l1dtl
     C                   movel     yes           prtfl
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Main do
      *
      *---------------------------------------------------------------
      * EOF processing
      *---------------------------------------------------------------
      * If the file was empty, print the standard 'no data...'
      * error message
      *
     C                   if        prtfl = no
     C                   except    nodata
     C                   endif
      *
     C                   seton                                        lr
      /EJECT
      *---------------------------------------------------------------
      * *inzsr - initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Set up a line of underscores and retrieve time for report heading.
      *
     C                   movel     *all'-'       dash
     C                   time                    rtime
      *
      * Set up indicators to control printing in the header:
      *
      *   User selected all sire line sources
      *
     C                   if        ldsscd = *blank
     C                   seton                                        97
     C                   endif
      *
      *   User selected both active and inactive sources
      *
     C                   if        ldaist = *blank
     C                   seton                                        98
     C                   endif
      *
     C                   endsr
      /EJECT
      *-------------------------------------------------------------
      * Report heading lines
      *-------------------------------------------------------------
      *
     Oqprint    e            h1hdr          1 01
     O                       sdpgm               10
     O                                           52 'HOG PRODUCTION SYSTEM'
     O                                           70 'DATE'
     O                       udate         y     80
      *
     O          e            h1hdr          1
     O                       sdusr               10
     O                                           48 'LISTING OF SIRE LINE '
     O                                           55 'SOURCES'
     O                                           70 'TIME'
     O                       rtime               80 '  :  :  '
      *
     O          e            h1hdr          2
     O                                           70 'PAGE'
     O                       page          z     80
      *
      *
     O          e            h1hdr          1
     O              n97                          25 'Listing begins with sire '
     O              n97                          40 'line source ..:'
     O              n97      ldsscd              47
     O               97                          25 'Listing includes all sire'
     O               97                          38 ' line sources'
      *
      *
     O          e            h1hdr          2
     O              n98                          25 'Active/inactive status ..'
     O              n98                          40 '..............:'
     O              n98      ldaist              43
     O               98                          22 'Listing includes both '
     O               98                          41 'Active and Inactive'
     O               98                          49 ' sources'
      *
      *
     O          e            h1hdr          1
     O                                            4 'SIRE'
      *
     O          e            h1hdr          1
     O                                            4 'LINE'
     O                                           15 'FARM'
     O                                           35 'ACTIVE/INACTIVE'
     O          e            h1hdr          1
     O                                            6 'SOURCE'
     O                                           15 'SITE'
     O                                           31 'STATUS'
     O                                           50 'DESCRIPTION'
      *
     O          e            h1hdr          1
     O                       dash                80
      *
      *-------------------------------------------------------------
      * Detail line
      *-------------------------------------------------------------
      *
     O          e            l1dtl       1
     O                       sssscd               5
     O                       ssfscd        m     16
     O                       ssaist              28
     O                       ssssds              69
      *
      *-------------------------------------------------------------
      * No data message line
      *-------------------------------------------------------------
      *
     O          e            nodata      1
     O                                           19 'NO DATA MEETS YOUR'
     O                                           38 'SELECTION CRITERIA'
