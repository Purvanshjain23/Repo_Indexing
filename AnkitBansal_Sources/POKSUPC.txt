/********************************************************************/
/*                                                                  */
/*  SYNOPSIS -  PRINT THE AS RESTORATION EXCEPTION                  */
/*                                                                  */
/*  SYSTEM    - ORDER MANAGEMENT SYSTEM                             */
/*  PROGRAM   - PRINT REPORTS                                       */
/*  DATE      - 05/23/01                                            */
/*                                                                  */
/*      PARAMETERS:                                                 */
/*                                                                  */
/*     O    &RETURN     - RETURN CODE                               */
/*     B    &OUTQ       -  OUTPUT QUEUE                             */
/*                         IF THIS IS BLANK AT START, PROMPT        */
/*                         PGM IS CALLED; OTHERWISE, THE PRINT CL   */
/*                         IS SUBMITTED WITHOUT PROMPTING           */
/*     B    &HOLD       -  HOLD STATUS                              */
/*     B    &SAVE       -  SAVE STATUS                              */
/*     B    &NQSTAT     -  INDICATOR WHETHER MUST RUN IN NIGHT Q    */
/*     B    &NIGHTQ     -  NIGHT JOB QUEUE                          */
/*     B    &COMPN      - COMPANY NUMBER (PASSED)                   */
/*     B    &COPYN      - NO. OF COPIES  (PASSED)                   */
/*                                                                  */
/********************************************************************/
/* RMC  7/11/01  ADD PARM TO EXCLUDE NEG. QOH AND PHYS. INV EQ 0    */
/* RMC  7/22/02  PRINT ON PRKPRT04 ALSO                             */
/* RMC  1/29/04  PRINT ON PRKPRT02 INSTEAD OF PRKPRT04         */
/********************************************************************/

 POKSUPC:    PGM        PARM(&RETURN &OUTQ &HOLD &SAVE &COMPN &COPYN +
                          &WHS &CYCLE &EXCCF1 &EXCSCL &EXCWIP &EXC999 +
                          &EXCFZ &EXCQOH &MARKEXC)
/* PARMS                                                                      */
        DCL    &RETURN     *CHAR   (7)           /* Return Code               */
        DCL    &COMP       *DEC    (3 0)        /* COMPANY SUBMIT             */
        DCL    &WHS        *CHAR   (3)          /* WAREHOUSE                  */
        DCL    &CYCLE      *CHAR   (3)          /* CYCLE COUNT                */
        DCL    &EXCCF1     *CHAR   (1)          /* EXCLUDE CF1 RETURNS        */
        DCL    &EXCSCL     *CHAR   (1)          /* EXCLUDE SCALE CHK          */
        DCL    &EXCWIP     *CHAR   (1)          /* EXCLUDE WIP ITEM TYP       */
        DCL    &EXC999     *CHAR   (1)          /* EXCLUDE 9XXXXX ITMS        */
        DCL    &EXCFZ      *CHAR   (1)          /* EXCLUDE FZ SCD AS ORDRD    */
        DCL    &EXCQOH     *CHAR   (1)          /* EXCLUDE NEG QOH            */
        DCL    &MARKEXC    *CHAR   (1)          /* MARK EXCEPTION STATUS      */
        DCL    &OUTQ       *CHAR   (10)          /* Output Queue              */
        DCL    &HOLD       *CHAR   (4)           /* Hold Status               */
        DCL    &SAVE       *CHAR   (4)           /* Save Status               */
        DCL    &COPYN      *DEC                  /* No. of Copies             */
        DCL    &COMPN      *DEC                 /* COMPANY SUBMIT             */
/* INTERNAL FIELDS                                                            */
        DCL    &COPIES     *DEC    (2 0)         /* Number of copies          */

        DCL    &COMMAND    *CHAR   (512)
/* ERROR MESSAGE FIELDS                                                       */
        DCL    &MSG        *CHAR   (512)         /* message                   */
        DCL    &MSGID      *CHAR   (7)           /* message id                */
        DCL    &MSGL       *DEC    (5 0)
        DCL    &RTN        *CHAR   (7)           /* message id                */

/********************************************************************/
/*   INITIALIZE VARIABLES                                           */
/********************************************************************/

             CHGVAR     VAR(&RETURN) VALUE('       ')
             CHGVAR     VAR(&COMP) VALUE(&COMPN)
             CHGVAR     VAR(&COPIES) VALUE(&COPYN)
/********************************************************************/
/*   MONITOR FOR ALL MESSAGES AT PROGRAM LEVEL                      */
/********************************************************************/

             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(MSGTRAP))

/********************************************************************/
/*   CALL PRINT PROGRAM                                        */
/********************************************************************/


             SBDPRTOVR  CMDVAR(&COMMAND) FILE(POKRPFR$) OUTQ(&OUTQ) +
                          COPIES(&COPIES) HOLD(&HOLD) SAVE(&SAVE)

             CALL       PGM(QCMDEXC) PARM(&COMMAND 512)

 PRINT:      CALL       PGM(POKRPFR) PARM(&RTN &COMP &WHS &CYCLE +
   &EXCCF1 &EXCSCL &EXCWIP &EXC999 &EXCFZ &EXCQOH &MARKEXC)
             SBDPRTOVR  CMDVAR(&COMMAND) FILE(POKRPFR$) OUTQ(PRKPRT02) +
                          COPIES(&COPIES) HOLD(*YES) SAVE(&SAVE)

             CALL       PGM(QCMDEXC) PARM(&COMMAND 512)

 PRINT2:     CALL       PGM(POKRPFR) PARM(&RTN &COMP &WHS &CYCLE +
   &EXCCF1 &EXCSCL &EXCWIP &EXC999 &EXCFZ &EXCQOH &MARKEXC)
             GOTO       CMDLBL(END)

/*----------------------RECEIVE MESSAGES----------------------------*/

 MSGTRAP:    RCVMSG     MSGTYPE(*EXCP) MSGDTA(&MSG) MSGDTALEN(&MSGL) +
                          MSGID(&MSGID)
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(END))

             SNDPGMMSG  MSGID(&MSGID) MSGF(QCPFMSG) MSGDTA(%SST(&MSG +
                          1 &MSGL)) MSGTYPE(*ESCAPE)
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(END))

 END:        ENDPGM

