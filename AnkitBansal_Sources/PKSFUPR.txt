/********************************************************************/
/*                                                                  */
/*  SYNOPSIS - CL PROGRAM FOR DOWNLOADING WEEKLY PERFORMANCE        */
/*                                                                  */
/*  SYSTEM    - HOG PROCUREMENT AND EVALUATION                      */
/*  DATE      - 08/18/99                                            */
/*  DEVELOPER - PURVA DROGE & LEANNE FEDOR                          */
/*                                                                  */
/********************************************************************/
/* MODIFICATIONS:                                                   */
/* 09/28/99  LEANNE FEDOR                                           */
/*           CHANGED LOGICAL OF TATTOO HEADER AND ADDED AN OPEN     */
/*           QUERY TO SELECT RECORDS FOR A SINGLE PRODUCER OR ALL   */
/* 02/03/00  ROSE CENTONZE                                          */
/*           OUTPUT INSTEAD OF EUF4NB, NEW FIELD EUAGA1 (LEAN% 4.3) */
/*           OUTPUT INSTEAD OF EUAWPC, NEW FIELD EUAFA1 (YIELD% 5.3)*/
/* 08/08/03  ROSE CENTONZE                                          */
/*           OUTPUT INSTEAD OF EUF2NB, NEW FIELD EUMCNF (BACKF 5.2) */
/*           OUTPUT INSTEAD OF EUF3NB, NEW FIELD EUMVNF (LOIN 5.2)*/
/* 04/05/04  ROSE CENTONZE                                          */
/*           CHANGE DEFECT CODES                                    */
/* 11/04/20  ISE                                                    */
/*           RECOMPILE ONLY. INCREASED BUY ORDER NUMBER LENGTH FROM */
/*           5 to 7.                                                */
/********************************************************************/
             PGM        PARM(&CO &PROD &WKSTRT &NOWK &DOC &PCFMT)

/* INPUT FIELDS */
             DCL        VAR(&CO) TYPE(*DEC)  LEN(3)  /*COMPANY NO        */
             DCL        VAR(&PROD)   TYPE(*CHAR) LEN(6)  /*PRODUCER CODE     */
             DCL        VAR(&WKSTRT) TYPE(*DEC) LEN(7)   /*WEEK START DATE   */
             DCL        VAR(&NOWK)   TYPE(*DEC) LEN(2)   /*NO. OF WEEK       */
             DCL        VAR(&WKPASS) TYPE(*DEC)  LEN(7)  /*NO. OF WEEK       */

             DCL        VAR(&DOC)    TYPE(*CHAR) LEN(8)  /*DOCUMENT NAME     */
             DCL        VAR(&PCFMT)  TYPE(*CHAR) LEN(3)  /*PC FORMAT         */

/* WORK FIELDS */
             DCL        VAR(&LIBEXT) TYPE(*CHAR) LEN(10) /*DOCUMENT NAME     */
             DCL        VAR(&DOC2)   TYPE(*CHAR) LEN(10) /*DOCUMENT NAME     */
             DCL        VAR(&PCFMT2) TYPE(*CHAR) LEN(4)  /*PC FORMAT         */
             DCL        VAR(&PCFILE) TYPE(*CHAR) LEN(32) /*PC FILE           */

/* OPEN QUERY  */
             DCL        VAR(&QDATA) TYPE(*CHAR) LEN(50)

/*-----------------------------------------------------------------*/
/* INITILIZATION                                                   */
/*-----------------------------------------------------------------*/
/* ADD SEQUEL TO THE LIBRARY LIST */

             CHGVAR     VAR(&LIBEXT) VALUE('N')
             ADDLIBLE   LIB(SEQUEL) POSITION(*LAST)
             MONMSG     MSGID(CPF2103) EXEC(CHGVAR VAR(&LIBEXT) +
                          VALUE('Y'))

/* SETUP DOCUMENT NAME, PC FILE, AND PC FORMAT                     */

             CHGVAR     VAR(&DOC2) VALUE(&DOC)
             IF         COND(&PCFMT *EQ 'WKS') THEN(DO)
             CHGVAR     VAR(&PCFILE) VALUE(&DOC *TCAT '.WK4')
             ENDDO
             ELSE (DO)
             CHGVAR     VAR(&PCFILE) VALUE(&DOC *TCAT '.XLS')
             ENDDO

             CHGVAR     VAR(&PCFMT2) VALUE('*' *CAT &PCFMT)

/*-----------------------------------------------------------------*/
/* MAINLINE                                                        */
/*-----------------------------------------------------------------*/

/********************************************************************/
/* CREATE DOWNLOAD FILE INTO QTEMP                                  */
/*     USE THE COMMAND CUSTOMIZED BY JOHN ERICSON. THIS COMMAND     */
/*     WILL ELIMINATE HARDCODING OF LIBRARIES. THE CUSTOMIZED       */
/*     COMMAND IS CRTDUPOBJL FOR 'CREATE DUPLICATE OBJECT FROM      */
/*     LIBRARY'.                                                    */
/********************************************************************/

             CRTDUPOBJL OBJ(PKEUCPP) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)

             CRTDUPOBJL OBJ(PKEUCPL0) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)

             CRTDUPOBJL OBJ(PKEUCPL1) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)


/*------------------------------------------------------------------*/
/*  SET UP OPEN QUERY STATEMENT                                     */
/*------------------------------------------------------------------*/

/*  ALWAYS SELECT ONLY THE SPECIFIED COMPANY                     */

             CHGVAR     VAR(%SST(&QDATA 1 10)) VALUE('A1HMNB *EQ')
             CHGVAR     VAR(%SST(&QDATA 12 3)) VALUE(&CO)


/*  IF A SINGLE PRODUCER IS SELECTED                             */

             IF         COND(&PROD *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 16 17)) VALUE('*AND A1ABCD *EQ "')
             CHGVAR     VAR(%SST(&QDATA 33 6)) VALUE(&PROD)
             CHGVAR     VAR(%SST(&QDATA 39 1)) VALUE('"')
             ENDDO


/*------------------------------------------------------------------*/
/*  RUN QUERY OVER TATTOO HEADER                                    */
/*------------------------------------------------------------------*/

             OVRDBF     FILE(PKA1CPLQ) SHARE(*YES)
             OPNQRYF    FILE((PKA1CPLQ)) QRYSLT(&QDATA) KEYFLD(*FILE)

/* CALL PROGRAM TO BUILD DOWNLOAD FILE DATA   */

             CALL       PGM(PKSEUPR) PARM(&CO &PROD &WKSTRT &NOWK)
  /* COMMENTED 4.05.04   */
                     /*   EUBLHD COLHDG("BELLY BUSTS"),  */      +
                   /*     EUGSHD COLHDG("GOOD SOWS"),  */ +
                     /*   EUMNHD COLHDG("MANGE"), */  +
                    /*    EUOTHD COLHDG("ONE TESTICLE BOAR"), */ +
                   /*     EUQSHD COLHDG("POOR QUALITY SOWS"),  */ +
                   /*     EURBHD COLHDG("REAR BUSTS")    */ +
                     /*   EUSCHD COLHDG("SCARBACK") */ +

/* CREATE DOCUMENT IN OFFICE AND FTP TO PORK SERVER */
             EXECUTE SQL('SELECT +
                          EUPCTX COLHDG("WEEK OF"),+
                          EUKDHD COLHDG("NO. KILLED"),+
                          EUF7NB COLHDG("AVG LIVE WT"),+
                          EUHDNB COLHDG("AVG HOT WT"),+
                          EUAFA1 COLHDG("% YIELD"),+
                          EUMCNF COLHDG("AVG BACKFAT"),+
                          EUMVNF COLHDG("AVG LOIN EYE"),+
                          EUAGA1 COLHDG("% LEAN"),+
                          EUDOHD COLHDG("DOA"),+
                          EUDOPD COLHDG("% DOA"),+
                          EUYDHD COLHDG("YARD DEAD"),+
                          EUYDPC COLHDG("% YARD DEAD"),+
                          EUCDHD COLHDG("CONDEMNED"),+
                          EUHCPC COLHDG("% CONDEMNED"),+
                          EUSTHD COLHDG("SORTS TOTAL"),+
                          EUSTPC COLHDG("% SORTS"),+
                          EUBRHD COLHDG("BOARS/1 NUTTER"),+
                          EUHVHD COLHDG("HEAVIES"),+
                          EULTHD COLHDG("LITES"),+
                          EULCHD COLHDG("LITE CULLS"), +
                          EUABHD COLHDG("KOA"),+
                          EUMSHD COLHDG("MISC"),+
                          EUPQHD COLHDG("POOR QUALITY") +
                          FROM PKEUCPP') +
                          TOFLR(GUY.IS) +
                          TODOC(&DOC) +
                          PCFMT(&PCFMT2) +
                          REPLACE(*YES)
             CALL       PGM(FTPPROCURE) PARM(&DOC2 &PCFILE)

/*-----------------------------------------------------------------*/
/* END PROGRAM                                                     */
/*-----------------------------------------------------------------*/
/* REMOVE SEQUAL FROM THE LIBRARY LIST */

             IF COND(&LIBEXT *EQ 'N') THEN(DO)
                RMVLIBLE LIB(SEQUEL)
             ENDDO

             CLOF       OPNID(PKA1CPLQ)
             DLTOVR     FILE(*ALL)

 ENDPGM:     ENDPGM
