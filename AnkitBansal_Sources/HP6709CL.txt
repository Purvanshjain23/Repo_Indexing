/***************************************************************** */
/*                                                                 */
/*  PROGRAM NUMBER:  HP6709CL                                      */
/*  PROGRAM NAME:    GENERATE THE ORDER REQUESTS ON HOLD REPORT    */
/*  PROGRAMMER:      LEANNE FEDOR                                  */
/*  CREATE DATE:     07/21/04                                      */
/*                                                                 */
/*                                                                 */
/*********************************************************************/
/* MODIFICATIONS:                                                  */
/* 07/12/05  LEANNE FEDOR                                          */
/*           IF THE USER IS 'EMAILING', WE NOW OVERRIDE THE REPORT */
/*           TO HOLD=YES---OR THE EMAIL COMMAND ABENDS.            */
/* 08/09/05  BARB GUTIERREZ                                        */
/*           CHANGED EMAIL ADDRESS FROM SEABOARDPORK.COM TO        */
/*           SEABOARDFOODS.COM PER COMPANY NAME CHANGE.            */
/* 09/02/05  BARB GUTIERREZ                                        */
/*           CHANGED PARM &EMAIL FROM 37 POSITIONS TO 38 TO FIX    */
/*           THE PROBLEM OF THE EMAIL NOT GETTING SENT BECAUSE OF  */
/*           THE CHANGE ON 8/9/2005.                               */
/*********************************************************************/

             PGM

/* DEFINE THE VARIABLE REQUIRED IN THE COMMAND TO OVERRIDE THE     */
/* PRINT FILE TO A LASER PRINTER                                   */

             DCL        VAR(&CMD) TYPE(*CHAR) LEN(512)
             DCL        VAR(&QDATA) TYPE(*CHAR) LEN(127)
             DCL        VAR(&EMAIL) TYPE(*CHAR) LEN(38)

             DCL        VAR(&LDVER) TYPE(*CHAR) LEN(1)
             DCL        VAR(&LDEMAIL) TYPE(*CHAR) LEN(1)
             DCL        VAR(&LD1STEMAIL) TYPE(*CHAR) LEN(1)
             DCL        VAR(&LDPPCD) TYPE(*CHAR) LEN(5)
             DCL        VAR(&LDCMGCD) TYPE(*CHAR) LEN(5)
             DCL        VAR(&LDPMGCD) TYPE(*CHAR) LEN(5)
             DCL        VAR(&LDCELL) TYPE(*DEC) LEN(2)
             DCL        VAR(&LDFSCD) TYPE(*DEC) LEN(5)
             DCL        VAR(&LDWO) TYPE(*CHAR) LEN(10)

             DCL        VAR(&HOLD) TYPE(*CHAR) LEN(10)
             DCL        VAR(&COPYS) TYPE(*DEC) LEN(2)
             DCL        VAR(&OUTQ) TYPE(*CHAR) LEN(10)

             CHGVAR     VAR(&LDVER) VALUE(%SST(*LDA 1 1))
             CHGVAR     VAR(&LDEMAIL) VALUE(%SST(*LDA 2 1))
             CHGVAR     VAR(&LD1STEMAIL) VALUE(%SST(*LDA 141 1))
             CHGVAR     VAR(&LDPPCD) VALUE(%SST(*LDA 4 5))
             CHGVAR     VAR(&LDCMGCD) VALUE(%SST(*LDA 9 5))
             CHGVAR     VAR(&LDPMGCD) VALUE(%SST(*LDA 14 5))
             CHGVAR     VAR(&LDCELL) VALUE(%SST(*LDA 19 2))
             CHGVAR     VAR(&LDFSCD) VALUE(%SST(*LDA 21 5))
             CHGVAR     VAR(&LDWO)   VALUE(%SST(*LDA 26 10))

/*------------------------------------------------------------------*/
/* Make print file overrides                                        */
/*------------------------------------------------------------------*/


 HOLD:       IF         COND(%SST(*LDA 411 1) = 'Y' *OR &LDEMAIL = +
                          'Y') THEN(CHGVAR VAR(&HOLD) VALUE(*YES))
             ELSE       CMD(CHGVAR VAR(&HOLD) VALUE(*NO))



 COPYS:      CHGVAR     VAR(&COPYS) VALUE(%SST(*LDA 412 1))
             IF         COND(&COPYS = 0) THEN(CHGVAR VAR(&COPYS) +
                          VALUE(1))

 OUTQ:       CHGVAR     VAR(&OUTQ) VALUE(%SST(*LDA 413 10))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(%SST(*LDA 401 10)))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(*JOB))



/*-------------------------------------------------------------------*/
/*  Set up query over Farm Site Master                               */
/*-------------------------------------------------------------------*/

/* Default starting point                                            */

             CHGVAR     VAR(&QDATA) VALUE(' ')
             CHGVAR     VAR(%SST(&QDATA 1 12)) VALUE('FSFSCD *NE 0')

/* Production phase                                                      */

             IF         COND(&LDPPCD *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 14 17)) VALUE('*AND FSPPCD +
                          *EQ "')
             CHGVAR     VAR(%SST(&QDATA 31 5)) VALUE(&LDPPCD)
             CHGVAR     VAR(%SST(&QDATA 36 1)) VALUE('"')
             ENDDO

/* Cell Manager                                                          */

             IF         COND(&LDCMGCD *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 38 18)) VALUE('*AND FSCMGCD +
                          *EQ "')
             CHGVAR     VAR(%SST(&QDATA 56 5)) VALUE(&LDCMGCD)
             CHGVAR     VAR(%SST(&QDATA 61 1)) VALUE('"')
             ENDDO

/* Cell                                                       */
             IF         COND(&LDCELL *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 63 16)) VALUE('*AND FSCELL +
                           *EQ ')
             CHGVAR     VAR(%SST(&QDATA 79 2)) VALUE(&LDCELL)
             ENDDO

/* Farm Site                                                  */
             IF         COND(&LDFSCD *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 82 16)) VALUE('*AND FSFSCD +
                           *EQ ')
             CHGVAR     VAR(%SST(&QDATA 98 5)) VALUE(&LDFSCD)
             ENDDO

/* Production Manager                                                    */

             IF         COND(&LDPMGCD *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 104 18)) VALUE('*AND FSPMGCD +
                          *EQ "')
             CHGVAR     VAR(%SST(&QDATA 122 5)) VALUE(&LDPMGCD)
             CHGVAR     VAR(%SST(&QDATA 127 1)) VALUE('"')
             ENDDO

             OVRDBF     FILE(HSP018)  SHARE(*YES)
             OPNQRYF    FILE((HSP018)) QRYSLT(&QDATA) KEYFLD(*FILE) +
                          SEQONLY(*YES)


/*-------------------------------------------------------------------*/
/*  Set up query over Order Request Detail file                      */
/*-------------------------------------------------------------------*/

/* Hold lines only  */

             CHGVAR     VAR(&QDATA) VALUE(' ')
             CHGVAR     VAR(%SST(&QDATA 1 15)) VALUE('QDRQSCD *EQ "H"')

             OVRDBF     FILE(HSP193) SHARE(*YES)
             OPNQRYF    FILE((HSP193)) QRYSLT(&QDATA) KEYFLD(*FILE) +
                          SEQONLY(*YES)


/*-------------------------------------------------------------------*/
/*  Set up query over Order Request Header file                      */
/*-------------------------------------------------------------------*/

/* Hold orders only */

             CHGVAR     VAR(&QDATA) VALUE(' ')
             CHGVAR     VAR(%SST(&QDATA 1 15)) VALUE('QHRQSCD *EQ "H"')

/* Farm site                                                  */
             IF         COND(&LDFSCD *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 17 16)) VALUE('*AND QHFSCD +
                           *EQ ')
             CHGVAR     VAR(%SST(&QDATA 33 5)) VALUE(&LDFSCD)
             ENDDO

/* Work order                                                                */

             IF         COND(&LDWO *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 39 15)) VALUE('*AND QHWO +
                          *EQ "')
             CHGVAR     VAR(%SST(&QDATA 54 10)) VALUE(&LDWO)
             CHGVAR     VAR(%SST(&QDATA 64 1)) VALUE('"')
             ENDDO

             OVRDBF     FILE(HSP192) SHARE(*YES)
             OPNQRYF    FILE((HSP192)) QRYSLT(&QDATA) KEYFLD(*FILE) +
                          SEQONLY(*YES)


/*----------------------------------------------------------------------*/
/* Create/build Workfile                                                */
/*----------------------------------------------------------------------*/

             CRTDUPOBJL OBJ(HSP3709) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)
             CALL       PGM(HP3709)

/* Remove file overrides                                             */

             CLOF       OPNID(HSP018)
             CLOF       OPNID(HSP192)
             CLOF       OPNID(HSP193)
             DLTOVR     FILE(*ALL)

/*-------------------------------------------------------------------*/
/* Print the report                                                  */
/*-------------------------------------------------------------------*/

/* Version 1: Sort/Print by Cell Manager                             */

             IF         COND(&LDVER *EQ '1') THEN(DO)
             OVRDBF     FILE(HSP3709) SHARE(*YES)
             OPNQRYF    FILE((HSP3709)) KEYFLD((W1CMGCD) (W1PMGCD) +
                          (W1CELL) (W1FSCD) (W1WO) (W1LNNO)) +
                          SEQONLY(*YES)
             ENDDO

/* Version 2: Sort/Print by Production Manager                       */

             IF         COND(&LDVER *EQ '2') THEN(DO)
             OVRDBF     FILE(HSP3709) SHARE(*YES)
             OPNQRYF    FILE((HSP3709)) KEYFLD((W1PMGCD) (W1CMGCD) +
                          (W1CELL) (W1FSCD) (W1WO) (W1LNNO)) +
                          SEQONLY(*YES)
             ENDDO

/* Version 3: Sort/Print by Workorder                                */

             IF         COND(&LDVER *EQ '3') THEN(DO)
             OVRDBF     FILE(HSP3709) SHARE(*YES)
             OPNQRYF    FILE((HSP3709)) KEYFLD((W1WO) (W1LNNO)) +
                          SEQONLY(*YES)
             ENDDO


/* Override the print file and call the print program                */


             SBDPRTOVR  CMDVAR(&CMD) FILE(QPRINT) OUTQ(&OUTQ) +
                          WIDTH(132) LANDSCAPE(*PRTF) DRAWER(*PRTF) +
                          COPIES(&COPYS) HOLD(&HOLD) SHARE(*YES)
             CALL       PGM(QCMDEXC) PARM(&CMD 512)

             CALL       PGM(HP6709)

             CLOF       OPNID(HSP3709)
             DLTOVR     FILE(HSP3709)
             DLTF       FILE(QTEMP/HSP3709)


/*-------------------------------------------------------------------*/
/* If the report is being E-mailed,                                  */
/* burst the report and E-mail pages to appropriate managers         */
/*-------------------------------------------------------------------*/

             IF         COND(&LDEMAIL *EQ 'Y') THEN(DO)

             ADDLIBLE   LIB(ESEND)
             MONMSG CPF0000

/* If this is the 'first' e-mail (flags in the function control whether */
/* this is the "First" or not) build the ESEND "Rules" file for Managers*/

             IF         COND(&LD1STEMAIL *EQ 'Y') THEN(DO)
             CALL       PGM(HP8709)
             ENDDO

/*------------------------------------------------------------------*/
/* 4/22/05:                                                         */
/* We are changing, temporarily, the "production" version of this   */
/* report to go to Misti Reust and Stephen Trompeter. The "Cell"    */
/* version will continue to go to Managers.                         */
/*                                                                  */
/* To accomplish this temporary deal, we:                           */
/*    created &email                                                */
/*    created a distribution list just for Misti/Stephen            */
/*    conditionally populate &email for the Production Mgr version  */
/*    conditionally &email the Production version                   */
/*    continue to "burst" the Cell Manager version                  */
/* Once Misti/Steve decide to revert back to 'bursting' the         */
/* Production Manager version, we just need to:                     */
/*    delete &email                                                 */
/*    remove the version '2' stuff just below completely            */
/*    remove the conditional IF/ENDDO on the EDISTRIB command       */
/*------------------------------------------------------------------*/

             IF         COND(&LDVER *EQ '2') THEN(DO)
             CHGVAR     VAR(&EMAIL) +
                      VALUE(DLPORKMP2BUDGETADMIN@SEABOARDFOODS.COM)
             ESNDMAIL   RECIPIENT(&EMAIL) SUBJECT('On-Hold Report +
                          (Production Manager Version)') ATTLIST((* +
                          *PDF *N QPRINT *))
             ENDDO

/* Cell Manager Version (continue to burst)                         */

             IF         COND(&LDVER *EQ '1') THEN(DO)
             EDISTRIB   SPLNAME(*ALL) RULESET(HP6709) FILTYP(*PDF) +
                          SUBJECT('Order Requests on Hold Report') +
                          MSG('These orders must be approved before +
                          they can be processed.  Please send your +
                          replies to Misti_Reust@Seaboardfoods.com') +
                          DELETE(*YES)
             ENDDO

             RMVLIBLE   LIB(ESEND)
             ENDDO

/* Delete any remaining file overrides                               */

             DLTOVR     FILE(*ALL)

             ENDPGM

