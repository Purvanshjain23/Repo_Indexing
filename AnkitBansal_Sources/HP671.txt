      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      HOG PRODUCTION
      * PROGRAM:     HP671 - HOG GROUP CLOSING REPORT
      * PROGRAMMER:  LEANNE FEDOR
      * CREATED:     06/23/94
      *
      * FUNCTION: THIS PROGRAM PRINTS A REPORT OF EACH DISPOSED HOG
      *           GROUP TO BE CLOSED FOR THE PERIOD.
      *
      *  ERROR MESSAGES
      *
      *           ERROR MESSAGES ARE PRINTED AT THE END OF EACH HOG
      *           GROUP.  THE MESSAGES ARE RETRIEVED THROUGH A
      *           CALL TO CLP UT80046 USING THE SAME USER MESSAGE
      *           FILE, ARRAYS AND ARRAY LOGIC AS IN THE INTERACTIVE
      *           PROGRAMS.  HOWEVER, THEY ARE PRINTED FROM THE CL
      *           USING THE COMMAND 'CLPRINT' THAT WAS CREATED BY
      *           ERNIE WALLACE.
      *
      *           QPRINT IS USED FOR THE OUTPUT FILE SINCE THE
      *           COMMAND 'CLPRINT' PRINTS TO QPRINT.
      *           QPRINT IS OVERRIDDEN IN THE SUBMISSION CLP
      *           TO 'SHARE = YES' TO ALLOW BOTH THIS RPG PROGRAM AND
      *           CLP UT80046 TO WRITE TO THE SAME PRINT FILE.
      *
      *           SINCE THE 'CLPRINT' COMMAND LOGIC IN UT80046 IS NOT
      *           BEING USED TO PRINT HEADINGS, COUNT LINES OR PAGE
      *           BREAK, THESE FUNCTIONS ARE CONTROLLED FROM THIS RPG
      *           PROGRAM BY COUNTING THE LINES.
      *
      *****************************************************************
      * MODIFICATIONS:
      *****************************************************************
      * DATE      PROGRAMMER
      *
      * 04/13/00  LeAnne Fedor
      *           Recompile only. The 'closed group pounds' field in the
      *           Closed Hog Group file was increased to 11,2.
      *
      * 06/05/01  LeAnne Fedor
      *           Recompile only. New field 'multisite' added to Farm Site file.
      *
      * 06/26/01  LeAnne Fedor
      *           Three fields (manager/supervisor/multisite) renamed in Farm Site file
      *           to be production manager/cell manager/multisite manager.
      *           Multisite and Supervisor files removed from system. All logic is now
      *           based on Manager file.
      *           Manager and supervisor fields removed from Hog Group file.
      *
      * 08/20/01  LeAnne Fedor
      *           We are replacing workfile HSP372-Transfers with a permanent database
      *           file HSP086-Costed Transfers.  So, all logic originally for the
      *           workfile has been rewritten for the new database file.
      *
      * 04/10/03  LeAnne Fedor
      *           We removed HSP086-Costed Transfers. It was only used here to print
      *           transfer movements that 'could not be costed'. We have changed our
      *           entire approach to costing transfers and no longer have movements
      *           that 'cannot be costed'.
      /EJECT
      *****************************************************************
      * FILE SPECIFICATIONS
      *****************************************************************
      *
     FHSP005    IF   E           K DISK
      *    CATAGORY CODES
      *
      *
     FHSP006    IF   E           K DISK
      *    REPORTING CODES
      *
      *
     FHSP016    IF   E           K DISK
      *    MANAGER  MASTER
      *
      *
     FHSP018    IF   E           K DISK
      *    FARM SITE
      *
      *
     FHSL034D   IF   E           K DISK
      *    HOG GROUP
      *
      *
     FHSL371A   IF   E           K DISK
      *    GROUP WORKFILE
      *
      *
     FHSP373    IF   E           K DISK
      *    WORKFILE FOR GROUP HEADER
      *
      *
     FQPRINT    O    F  132        PRINTER OFLIND(*INOF)
      *
      /EJECT
      ****************************************************************
      * Definition specifications
      ****************************************************************
      *
      *---------------------------------------------------------------
      *  NAMED CONSTANTS
      *---------------------------------------------------------------
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      *
      *---------------------------------------------------------------
      * STANDARD PROGRAM STATUS DATA STRUCTURE
      *---------------------------------------------------------------
      *    EXTERNALLY DEFINED AS UTPGFR (RECORD FORMAT: PGMDSR)
     D PGMDS         ESDS                  EXTNAME(UTPGFR)
      *
      *---------------------------------------------------------------
      * STANDARD ERROR MESSAGE HANDLING DATA STRUCTURES
      *---------------------------------------------------------------
      *
      * FOR ERROR MESSAGE HANDLING, A PACKED INDEX (E) IS REQUIRED.
      * FOR PROGRAM READABILITY, DEFINE A CORRESPONDING ERROR COUNT
      * FIELD CALLED 'ERROR'
      *
     D                 DS                  INZ
     D  ERROR                  1      2P 0
     D  E                      1      2P 0
      *
      * THIS DATA STRUCTURE SUPPLIES THE NAME OF THE MESSAGE FILE TO
      * THE MESSAGE HANDLING CL PROGRAM.  THE FIELD NAME MSGFIL MUST BE
      * CONSTANT. THE VALUE IN QUOTES IS THE NAME OF THE SPECIFIC
      * MESSAGE FILE CONTAINING THE USER DEFINED MESSAGES.
      *
     D                 DS                  INZ
     D  MSGFIL                 1     10    INZ('HSMSGF    ')
      *
      * THE FOLLOWING 3 DATA STRUCTURES ARE USED TO SPEED MESSAGE
      * HANDLING SINCE IT IS FASTER TO CLEAR DATA STRUCTURES THAN
      * ARRAYS.  EACH IS ASSOCIATED WITH A STANDARD MESSAGE ARRAY.
      *
     D MGI             DS           140    INZ
     D  MGID                   1    140
     D                                     DIM(20)                              MSG ID ARRAY
      *
     D MGD             DS          1000    INZ
     D  MGDT                   1   1000
     D                                     DIM(20)                              MSG PARMS
      *
     D MGWK            DS            50    INZ
     D  MGW                    1     50
     D                                     DIM(50)                              MSG WORK PARMS
      *
      *
      *----------------------------------------------------------------
      * LDA - REPORT SELECTIONS
      *----------------------------------------------------------------
      *
     D                UDS
     D  LDBPDT                 1     08  0
     D  LDEPDT                 9     16  0
     D  LDPER                 17     18  0
     D  LDEFFL                19     19
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * MAIN LINE
      ****************************************************************
      *
      * READ AND PRINT EACH CLOSED GROUP RECORD FROM THE WORKFILE
      *
     C     *IN90         DOUEQ     *ON                                          MAIN DO
     C                   READ      HSL371A                                90
      *
     C     *IN90         IFEQ      *OFF                                         IF NOT EOF
      *
      * IF THIS IS THE FIRST TIME, PRINT REPORT HEADINGS
      *
     C     FIRST         IFEQ      YES                                          IF FIRST
     C                   EXSR      $FARM
     C                   EXSR      $GROUP
     C                   EXSR      $ERROR
     C                   EXSR      $CACD
     C                   EXSR      $HRHDR
     C                   EXSR      $HDCAT
     C                   MOVEL     NO            FIRST
     C                   ENDIF                                                  IF FIRST
      *
      *----------------------------------------------------------------
      * WHEN HOG GROUP CHANGES
      *
     C     WGHGSN        IFNE      SVHGSN                                       IF NEW GROUP
      *
     C     ERROR         IFGT      0                                            IF ERRORS
     C                   EXSR      $WRMSG
     C                   Z-ADD     0             ERROR
     C                   EXSR      $CLMSG
     C                   ENDIF                                                  IF ERRORS
      *
      * IF FARM SITE CHANGED, RETRIEVE NEW FARM DATA
      *
     C     WGFSCD        IFNE      SVFSCD
     C                   EXSR      $FARM
     C                   ENDIF
      *
      * RETRIEVE INFO FOR NEW GROUP
      *
     C                   EXSR      $GROUP
     C                   EXSR      $ERROR
     C                   EXSR      $CACD
     C                   EXSR      $HRHDR
     C                   EXSR      $HDCAT
     C                   ENDIF                                                  IF NEW GROUP
      *----------------------------------------------------------------
      *
      * WHEN CATEGORY CODE CHANGES
      *
     C     WGCACD        IFNE      SVCACD
     C     WKCURL        IFGT      WKOVFL
     C                   EXSR      $HRHDR
     C                   ENDIF
     C                   EXSR      $CACD
     C                   EXSR      $HDCAT
     C                   ENDIF
      *
      * RETRIEVE REPORT CODE DESCRIPTION AND PRINT DETAIL LINE
      *
     C                   EXSR      $RPCD
      *
     C     WKCURL        IFGT      WKOVFL
     C                   EXSR      $HRHDR
     C                   EXSR      $HDCAT
     C                   ENDIF
      *
     C                   EXCEPT    DTLINE
     C                   ADD       1             WKCURL
      *
     C                   ENDIF                                                  IF NOT EOF
     C                   ENDDO                                                  MAIN DO
      *
      *----------------------------------------------------------------
      * EOF PROCESSING
      *----------------------------------------------------------------
      *
      *  IF NO DETAIL RECORDS MATCHED THE SELECTION CRITERIA, THEN
      *  PRINT THE STANDARD 'NO DATA...' ERROR MESSAGE. OTHERWISE,
      *  PRINT OUT ANY ERROR MESSAGE FOR THE LAST GROUP.
      *
     C     FIRST         IFEQ      YES                                          IF NOTHING
     C                   EXSR      $HRHDR
     C                   EXCEPT    NODATA
     C                   ELSE
      *
      * PRINT OUT DATA FOR LAST GROUP
      *
     C     ERROR         IFGT      0                                            IF ERROR
     C                   EXSR      $WRMSG
     C                   Z-ADD     0             ERROR
     C                   EXSR      $CLMSG
     C                   ENDIF                                                  IF ERROR
      *
     C                   ENDIF                                                  IF NOTHING
      *
     C                   SETON                                        LR
      /EJECT
      ****************************************************************
      * $DATE -  MANIPULATE DATES WITH DATE UTILITY
      ****************************************************************
      *
     C     $DATE         BEGSR
      *
     C                   Z-ADD     0             PTO8
     C                   MOVE      *BLANKS       PFRFMT
     C                   MOVEL     'YMD '        PFRFMT
     C                   MOVE      *BLANKS       PTOFMT
     C                   MOVEL     'MDY '        PTOFMT
      *
     C                   CALL      'UT80060R'
     C                   PARM                    PFRM8             8 0
     C                   PARM                    PTO8              8 0
     C                   PARM                    PFRFMT            4
     C                   PARM                    PTOFMT            4
     C                   PARM                    PDAYS             4 0
     C                   PARM                    PCODE             1
     C                   PARM                    PRTRN             1
      *
     C                   ENDSR
      /EJECT
      ****************************************************************
      * $FARM - RETRIEVE FARM SITE INFORMATION
      ****************************************************************
      *
     C     $FARM         BEGSR
      *
     C                   MOVE      *BLANK        HRFSNM
     C                   MOVE      *BLANK        HRPMGCD
     C                   MOVE      *BLANK        HRPMGNM
     C                   MOVE      *BLANK        HRCMGCD
     C                   MOVE      *BLANK        HRCMGNM
      *
     C     WGFSCD        CHAIN     HSP018                             92
     C     *IN92         IFEQ      *ON                                          IF BAD FARM
     C                   MOVEL     'UNKNOWN'     HRFSNM
     C                   ELSE
     C                   MOVEL     FSFSNM        HRFSNM
      *
      * Production manager
      *
     C     FSPMGCD       IFEQ      *BLANK                                       IF NO PROD MGR
     C                   MOVEL     'NONE'        HRPMGCD
     C                   ELSE
     C                   MOVEL     FSPMGCD       HRPMGCD
     C     FSPMGCD       CHAIN     HSP016                             95
     C     *IN95         IFEQ      *OFF
     C                   MOVEL     MGMGNM        HRPMGNM
     C                   ELSE
     C                   MOVEL     'UNKNOWN'     HRPMGNM
     C                   ENDIF
     C                   ENDIF                                                  IF NO PROD MGR
      *
      * Cell manager
      *
     C     FSCMGCD       IFEQ      *BLANK                                       IF NO CELL MGR
     C                   MOVEL     'NONE'        HRCMGCD
     C                   ELSE
     C                   MOVEL     FSCMGCD       HRCMGCD
     C     FSCMGCD       CHAIN     HSP016                             96
     C     *IN96         IFEQ      *OFF
     C                   MOVEL     MGMGNM        HRCMGNM
     C                   ELSE
     C                   MOVEL     'UNKNOWN'     HRCMGNM
     C                   ENDIF
     C                   ENDIF                                                  IF NO CELL MGR
      *
      * Multisite manager
      *
     C     FSMMGCD       IFEQ      *BLANK                                       IF NO MULT MGR
     C                   MOVEL     'NONE'        HRMMGCD
     C                   ELSE
     C                   MOVEL     FSMMGCD       HRMMGCD
     C     FSMMGCD       CHAIN     HSP016                             95
     C     *IN95         IFEQ      *OFF
     C                   MOVEL     MGMGNM        HRMMGNM
     C                   ELSE
     C                   MOVEL     'UNKNOWN'     HRMMGNM
     C                   ENDIF
     C                   ENDIF                                                  IF NO PROD MGR
      *
     C                   ENDIF                                                  IF BAD FARM
      *
     C                   Z-ADD     WGFSCD        SVFSCD
      *
     C                   ENDSR
      /EJECT
      ****************************************************************
      * $GROUP - RETRIEVE GROUP INFORMATION FROM THE HOG GROUP FILE
      ****************************************************************
      *
     C     $GROUP        BEGSR
      *
     C                   Z-ADD     0             HRCRDT
     C                   Z-ADD     0             HROPDT
     C                   Z-ADD     0             HRDSDT
     C                   Z-ADD     0             HRFSCD
     C                   MOVE      *BLANK        SVHGCD
     C                   MOVE      *BLANK        HRBLCD
     C                   MOVE      *BLANK        HRRMCD
      *
     C     WGHGSN        CHAIN     HSL034D                            91
     C     *IN91         IFEQ      *OFF                                         IF FOUND
      *
      * SAVE FIELDS FOR PRINTING IN HEADINGS
      *
     C                   Z-ADD     HGFSCD        HRFSCD
     C                   MOVEL     HGHGCD        SVHGCD
     C                   MOVEL     HGBLCD        HRBLCD
     C                   MOVEL     HGRMCD        HRRMCD
      *
      * FLIP DATES FROM CCYYMMDD TO MMDDYY
      *
     C                   Z-ADD     HGCRDT        PFRM8
     C                   EXSR      $DATE
     C                   Z-ADD     PTO8          HRCRDT
      *
     C                   Z-ADD     HGOPDT        PFRM8
     C                   EXSR      $DATE
     C                   Z-ADD     PTO8          HROPDT
      *
     C                   Z-ADD     HGDSDT        PFRM8
     C                   EXSR      $DATE
     C                   Z-ADD     PTO8          HRDSDT
      *
     C                   ENDIF                                                  IF FOUND
      *
     C                   Z-ADD     WGHGSN        SVHGSN
      *
     C                   ENDSR
      /EJECT
      ****************************************************************
      * $ERROR - WRITE ANY GROUP ERROR MESSAGES TO ARRAYS
      ****************************************************************
      *
      * A HOG GROUP IS CONSIDERED TO HAVE ERRORS IF ITS STATUS IS
      * STILL 'DS' FOR DISPOSED.  A GROUP MAY HAVE ERRORS AS THE
      * RESULT OF EDITING IN HP371 OR COSTING IN HP372.
      *
      * IF A GROUP STILL HAS A STATUS OF DISPOSED, DO TWO THINGS:
      * 1) CHECK EACH ERROR FLAG VALUE IN THE GROUP HEADER FILE
      *    AND ISSUE APPROPRIATE ERROR MESSAGES.
      * 2) READ ALL THE TRANSFER-IN RECORDS FOR THIS GROUP IN THE
      *    TRANSFER-IN WORKFILE. FOR EACH RECORD THAT HAS A COSTED
      *    FLAG VALUE OF 'NO', WRITE AN ERROR MESSAGE TO THE
      *    ERROR MESSAGE ARRAY.
      *
     C     $ERROR        BEGSR
      *
     C     WGHGSN        CHAIN     HSP373                             97
     C     *IN97         IFEQ      *OFF                                         IF DISPOSED
     C     WHGSCD        ANDEQ     'DS'
      *
      * FEED NEEDS TO BE COSTED
      *
     C     WHFCFL        IFEQ      YES
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS00471'     MGID(E)
     C                   ENDIF
     C                   ENDIF
      *
      * FEED NEEDS TO BE ALLOCATED
      *
     C     WHFAFL        IFEQ      YES
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS00472'     MGID(E)
     C                   ENDIF
     C                   ENDIF
      *
      * INVENTORY HAS NOT COME TO ZERO FOR THE GROUP
      *
     C     WHINFL        IFEQ      YES
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS00473'     MGID(E)
     C                   ENDIF
     C                   ENDIF
      *
      * SCHEDULED TRANSFERS STILL EXIST FOR THE GROUP
      *
     C     WHSTFL        IFEQ      YES
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS00474'     MGID(E)
     C                   ENDIF
     C                   ENDIF
      *
      * SCHEDULED SALES MOVEMENTS STILL EXIST FOR THE GROUP
      *
     C     WHSSFL        IFEQ      YES
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS00693'     MGID(E)
     C                   ENDIF
     C                   ENDIF
      *
      * INCOMPLETE SALES EXIST FOR THE GROUP
      *
     C     WHSLFL        IFEQ      YES
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS00475'     MGID(E)
     C                   ENDIF
     C                   ENDIF
      *
      * DAILY DEAD MOVEMENTS EXIST THAT ARE DATED WITH A DATE PRIOR
      * TO THE 'OPEN' DATE OF THE GROUP
      *
     C     WHPDFL        IFEQ      YES
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS00694'     MGID(E)
     C                   ENDIF
     C                   ENDIF
      *
      * ADJUSTMENT MOVEMENTS EXIST THAT ARE DATED WITH A DATE PRIOR
      * TO THE 'OPEN' DATE OF THE GROUP
      *
     C     WHPAFL        IFEQ      YES
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS00695'     MGID(E)
     C                   ENDIF
     C                   ENDIF
      *
      *
      * SALES MOVEMENTS EXIST THAT ARE DATED WITH A DATE PRIOR
      * TO THE 'OPEN' DATE OF THE GROUP
      *
     C     WHPSFL        IFEQ      YES
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS00696'     MGID(E)
     C                   ENDIF
     C                   ENDIF
      *
     C                   ENDIF                                                  IF DISPOSED
      *
     C                   ENDSR
      /EJECT
      ****************************************************************
      * $CACD - RETRIEVE CATEGORY CODE DESCRIPTION
      ****************************************************************
      *
     C     $CACD         BEGSR
      *
     C                   MOVE      *BLANK        WKCADS
     C     WGCACD        IFNE      *BLANK                                       IF CODE
     C     WGCACD        CHAIN     HSP005                             93
     C     *IN93         IFEQ      *OFF                                         IF GOOD CODE
     C                   MOVEL     CACADS        WKCADS
     C                   ELSE
     C                   MOVEL     'UNKNOWN'     WKCADS
     C                   ENDIF                                                  IF GOOD CODE
     C                   ENDIF                                                  IF CODE
      *
     C                   MOVEL     WGCACD        SVCACD
      *
     C                   ENDSR
      /EJECT
      ****************************************************************
      * $RPCD - RETRIEVE REPORT CODE DESCRIPTION
      ****************************************************************
      *
     C     $RPCD         BEGSR
      *
     C                   MOVE      *BLANK        DTRLDS
     C     WGRPCD        IFNE      *BLANK                                       IF CODE
     C     WGRPCD        CHAIN     HSP006                             94
     C     *IN94         IFEQ      *OFF                                         IF GOOD CODE
     C                   MOVEL     RPRLDS        DTRLDS
     C                   ELSE
     C                   MOVEL     'UNKNOWN'     DTRLDS
     C                   ENDIF                                                  IF GOOD CODE
     C                   ENDIF                                                  IF CODE
      *
     C                   ENDSR
      /EJECT
      ****************************************************************
      * $HRHDR - SUBROUTINE TO PRINT REPORT HEADINGS
      ****************************************************************
      *
      * RESET THE CURRENT LINE TO ZERO
      * PRINT THE HEADING LINE
      * INCREMENT THE CURRENT LINE
      *
     C     $HRHDR        BEGSR
      *
     C                   Z-ADD     0             WKCURL
     C                   EXCEPT    HRHDR
     C                   ADD       14            WKCURL
      *
     C                   ENDSR
      /EJECT
      ****************************************************************
      * $BLANK - SUBROUTINE TO PRINT A BLANK LINE BEFORE MESSAGES
      ****************************************************************
      *
      *  PRINT A BLANK LINE AND INCREMENT LINE COUNTER
      *
     C     $BLANK        BEGSR
      *
     C                   EXCEPT    BLANK
     C                   ADD       1             WKCURL
      *
     C                   ENDSR
      *
      /EJECT
      ****************************************************************
      * $HDCAT - SUBROUTINE TO PRINT THE CATEGORY HEADING
      ****************************************************************
      *
      *  PRINT CATEGORY HEADING AND INCREMENT LINE COUNTER
      *
     C     $HDCAT        BEGSR
      *
     C                   EXCEPT    HDCAT
     C                   ADD       2             WKCURL
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * $WRMSG - SUBROUTINE TO WRITE ERROR MESSAGES TO REPORT
      *---------------------------------------------------------------
      *
     C     $WRMSG        BEGSR
      *
      * PROGRAM UT80046 DOES NOT PRINT HEADINGS OR CHECK FOR PAGE
      * BREAKS.  BEFORE CALLING UT80046, DETERMINE IF ALL THE ERROR
      * MESSAGES CAN BE PRINTED ON THE CURRENT PAGE.
      *
      * PRINT A BLANK LINE BEFORE ANY MESSAGES
      *
     C                   EXSR      $BLANK
      *
      * CALCULATE REMAINING PRINT LINES ON PAGE USING THE OVERFLOW LINE
      * AND THE CURRENT LINE.
      *
     C     WKOVFL        SUB       WKCURL        WKDIFL
      *
      * IF YOU HAVE MORE MESSAGES THAN WILL FIT ON THE PAGE, THEN
      * PAGE BREAK AND PRINT HEADINGS.
      *
     C     ERROR         IFGT      WKDIFL
     C                   EXSR      $HRHDR
     C                   EXSR      $BLANK
     C                   ENDIF
      *
      * INCREMENT THE CURRENT LINE BY THE NUMBER OF ERROR MESSAGES
      * YOU WILL BE PRINTING IN UT80046 PLUS ONE FOR THE SPACE AFTER.
      * THIS SETS THE CURRENT LINE NUMBER PROPERLY FOR WHEN YOU
      * RETURN FROM UT80046.
      *
     C     WKCURL        ADD       ERROR         WKCURL
     C                   ADD       1             WKCURL
      *
     C                   CALL      'UT80046'                            43
     C                   PARM                    MGID
     C                   PARM                    MGDT
     C                   PARM                    ERROR
     C                   PARM      MSGFIL        MSGFL
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $CLMSG - SUBROUTINE TO CLEAR MESSAGES
      *----------------------------------------------------------------
      *
     C     $CLMSG        BEGSR
      *
     C                   CALL      'UT80046'                            43
     C                   PARM      *BLANKS       MGI
     C                   PARM      *BLANKS       MGD
     C                   PARM      *ZEROS        ERROR
     C                   PARM      MSGFIL        MSGFL
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * *INZSR - INITIALIZATION SUBROUTINE
      *---------------------------------------------------------------
      *
     C     *INZSR        BEGSR
      *
      * DEFINE PRINT FIELDS FOR REPORT HEADING LINES
      *
     C                   MOVE      *BLANK        HREF              5
     C                   Z-ADD     0             HRCRDT            6 0
     C                   Z-ADD     0             HROPDT            6 0
     C                   Z-ADD     0             HRDSDT            6 0
     C                   Z-ADD     0             HRBPDT            6 0
     C                   Z-ADD     0             HREPDT            6 0
     C     *LIKE         DEFINE    HGFSCD        HRFSCD
     C     *LIKE         DEFINE    HGBLCD        HRBLCD
     C     *LIKE         DEFINE    HGRMCD        HRRMCD
     C     *LIKE         DEFINE    MGMGCD        HRPMGCD
     C     *LIKE         DEFINE    MGMGNM        HRPMGNM
     C     *LIKE         DEFINE    MGMGCD        HRCMGCD
     C     *LIKE         DEFINE    MGMGNM        HRCMGNM
     C     *LIKE         DEFINE    MGMGCD        HRMMGCD
     C     *LIKE         DEFINE    MGMGNM        HRMMGNM
     C     *LIKE         DEFINE    FSFSNM        HRFSNM
      *
      * DEFINE PRINT FIELDS FOR CATEGORY HEADING LINE
      *
     C     *LIKE         DEFINE    CACADS        WKCADS
      *
      * DEFINE PRINT FIELDS FOR DETAIL LINE
      *
     C     *LIKE         DEFINE    RPRLDS        DTRLDS
      *
      * DEFINE SAVE FIELDS FOR CONTROL BREAKS
      *
     C     *LIKE         DEFINE    WGHGSN        SVHGSN
     C     *LIKE         DEFINE    HGHGCD        SVHGCD
     C     *LIKE         DEFINE    WGCACD        SVCACD
     C     *LIKE         DEFINE    WGRPCD        SVRPCD
     C     *LIKE         DEFINE    WGFSCD        SVFSCD
      *
      * SETUP HEADINGS FOR USER'S REPORT SELECTION
      *
     C     LDEFFL        IFEQ      'E'
     C                   MOVEL(P)  'EDIT'        HREF
     C                   ELSE
     C                   MOVEL(P)  'FINAL'       HREF
     C                   ENDIF
      *
      *
      * CONVERT THE PERIOD DATES FROM CCYYMMDD TO MMDDYY
      *
     C                   Z-ADD     LDBPDT        PFRM8
     C                   EXSR      $DATE
     C                   Z-ADD     PTO8          HRBPDT
      *
     C                   Z-ADD     LDEPDT        PFRM8
     C                   EXSR      $DATE
     C                   Z-ADD     PTO8          HREPDT
      *
      * SET UP A FIRST TIME FLAG
      * SET UP A FIRST TRANSFER-IN ERROR FLAG
      * SET UP A DASHED LINE FOR UNDERLINING THE HEADING
      * RETRIEVE TIME FOR REPORT HEADING
      *
     C                   MOVE      YES           FIRST             1
     C                   MOVEL     *ALL'-'       DASH            132
     C                   TIME                    RTIME             6 0
      *
      * DEFINE WORK FIELDS USED IN CONTROLLING LINE COUNTS FOR
      * PRINTING SINCE DETAIL LINES ARE PRINTED FROM THIS RPG PROGRAM
      * AND ERROR MESSAGES ARE PRINTED FROM CLP UT80046.
      *
     C                   Z-ADD     0             WKDIFL            3 0          LINES LEFT
     C                   Z-ADD     62            WKOVFL            3 0          OVERFLOW LIN
     C                   Z-ADD     0             WKCURL            3 0          CURRENT LINE
      *
      * MESSAGE PARMS
     C                   MOVE      *BLANK        MSGFL            10            MESSAGE FILE
     C                   Z-ADD     20            MAXMSG            2 0
     C                   MOVE      *BLANK        XXMVSN            7
     C                   MOVE      *BLANK        XXFSCD            5
      *
     C                   ENDSR
      /EJECT
      *-------------------------------------------------------------
      * REPORT HEADING LINES
      *-------------------------------------------------------------
      *
     OQPRINT    E            HRHDR          1 01
     O                       SDPGM               10
     O                                           78 'HOG PRODUCTION SYSTEM'
     O                                          122 'DATE'
     O                       UDATE         Y    132
      *
     O          E            HRHDR          1
     O                       SDUSR               10
     O                                           67 'CLOSING HOG'
     O                                           80 'GROUP REPORT'
     O                                          122 'TIME'
     O                       RTIME              132 '  :  :  '
      *
     O          E            HRHDR          2
     O                                           16 'PERIOD:'
     O                       LDPER         M     20
     O                       HRBPDT        Y     34
     O                                           37 'TO'
     O                       HREPDT        Y     46
     O                       HREF                70
     O                                          122 'PAGE'
     O                       PAGE          Z    132
      *
      *
     O          E            HRHDR          1
     O                                           35 'FARM SITE:'
     O                       HRFSCD        M     43
     O                       HRFSNM              69
      *
     O          E            HRHDR          1
     O                                           35 'HOG GROUP/BUILDING/ROOM:'
     O                       SVHGCD              44
     O                       HRBLCD              52
     O                       HRRMCD              60
      *
     O          E            HRHDR          1
     O                                           35 'MGRS: PROD/CELL/MULTI:'
     O                       HRPMGCD             42
     O                       HRPMGNM             68
     O                       HRCMGCD             74
     O                       HRCMGNM            100
     O                       HRCMGCD            106
     O                       HRCMGNM            132
      *
     O          E            HRHDR          1
     O                                           22 'CREATE DATE/OPEN DATE/'
     O                                           35 'DISPOSE DATE:'
     O                       HRCRDT        Y     45
     O                       HROPDT        Y     56
     O                       HRDSDT        Y     67
      *
      *-------------------------------------------------------------
      * COLUMN HEADING LINES
      *-------------------------------------------------------------
      *
     O          E            HRHDR       1  1
     O                                           24 '-------- CATEGORY ------'
     O                                           27 '---'
     O                                           51 '----------- REPORT ---'
     O                                           58 '--------'
      *
     O          E            HRHDR          1
     O                                            4 'CODE'
     O                                           18 'DESCRIPTION'
     O                                           33 'CODE'
     O                                           47 'DESCRIPTION'
     O                                           71 'AMOUNT'
     O                                           81 'HEAD'
     O                                           97 'POUNDS'
     O                                          120 'QUANTITY'
      *
     O          E            HRHDR          0
     O                       DASH               132
      *
      *-------------------------------------------------------------
      * CATEGORY HEADING
      *-------------------------------------------------------------
      *
     O          E            HDCAT       1  1
     O                       WGCACD              05
     O                       WKCADS              27
      *
      *-------------------------------------------------------------
      * DETAIL LINE
      *-------------------------------------------------------------
      *
     O          E            DTLINE      1
     O                       WGRPCD              34
     O                       DTRLDS              56
     O                       WGCGAM        K     72
     O                       WGCGHD        K     82
     O                       WGCGLB        K     98
     O                       WGCGQT        K    121
      *
      *-------------------------------------------------------------
      * A BLANK LINE THAT IS PRINTED BEFORE THE ERROR MESSAGES
      *-------------------------------------------------------------
      *
     O          E            BLANK       1
     O                                            3 '   '
      *-------------------------------------------------------------
      * NO DATA MESSAGE LINE
      *-------------------------------------------------------------
      *
     O                                            3 '   '
     O          E            NODATA      2
     O                                           19 'NO DATA MEETS YOUR'
     O                                           38 'SELECTION CRITERIA'
