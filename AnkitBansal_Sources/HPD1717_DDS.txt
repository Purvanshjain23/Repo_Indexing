     A*%%TS  SD  20060117  151100  ISLRAMS     REL-V5R3M0  5722-WDS
     A*****************************************************************
     A*
     A* ENVIRONMENT:  PORK DIVISION
     A* SYSTEM:       AS/400
     A* FILE:         HPD1717
     A* DESCRIPTION:  MAINTAIN ORDER REQUEST
     A* PROGRAMMER:   LEANNE FEDOR
     A* DATE:         03/11/04
     A*
     A*
     A*  60  REVISE MODE
     A*  61  CREATE MODE
     A*  62  IS ON WHEN THE RECORD HAS BEEN DELETED OUT FROM UNDER THE USER
     A*  64  PROTECT OVERRIDE REASON
     A*  66  PROTECT APPROVED QUANTITY
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A N90                                  CF04(04 'PROMPT')
     A N90                                  CF05(05 'REFRESH')
     A N90                                  CF09(09 'ACCEPT')
     A  60N90                               CF11(11 'DELETE')
     A N90                                  CF13(13 'FOLD/DROP')
     A N90                                  CF14(14 'OPEN HDR')
     A*
     A*----------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*----------------------------------------------------------------
     A          R HP1717S1                  SFL
     A*%%TS  SD  20050209  152023  ISLRAMS     REL-V5R2M0  5722-WDS
     A  85                                  SFLNXTCHG
     A            HDRRN1         4Y 0H
     A            HDLNNO    R        H      REFFLD(@@LNNO)
     A            HDITCD         6A  H
     A            HDRQQT    R        H      REFFLD(@@RQQT)
     A            HDBGIT    R        H      REFFLD(@@BGIT)
     A            HDVOL     R        H      REFFLD(@@VOL)
     A            HDFBSN    R        H      REFFLD(@@FBSN)
     A*
     A            D1ITCD         6A  B  9  2
     A  33
     AO 53                                  DSPATR(PC)
     A  33                                  DSPATR(RI)
     A  62                                  DSPATR(PR)
     A N33N62                               DSPATR(HI)
     A N33N62                               DSPATR(UL)
     A            D1RQQT    R        B  9  9REFFLD(HSRREF/@@RQQT *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A  34                                  DSPATR(PC)
     A  34                                  DSPATR(RI)
     A  62                                  DSPATR(PR)
     A N34N62                               DSPATR(HI)
     A N34N62                               DSPATR(UL)
     A            D1AVQT    R        O  9 26REFFLD(HSRREF/@@RQQT *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A            D1APQT    R        B  9 42REFFLD(HSRREF/@@APQT *LIBL/HSPREF)
     A                                      COLOR(PNK)
     A                                      EDTCDE(K)
     A  36                                  DSPATR(PC)
     A  36                                  DSPATR(RI)
     A  62
     AO 66                                  DSPATR(PR)
     A N36
     A N36N62N66                            DSPATR(HI)
     A N36N62N66                            DSPATR(UL)
     A            D1ORCD    R        B  9 59REFFLD(@@ORCD)
     A  35
     AO 55                                  DSPATR(PC)
     A  35                                  DSPATR(RI)
     A  62
     AO 64                                  DSPATR(PR)
     A N35N62N64                            DSPATR(HI)
     A N35N62N64                            DSPATR(UL)
     A            D1CRITFL  R        O  9 68REFFLD(HSRREF/@@CRITFL *LIBL/HSPREF)
     A            D1BGFL    R        O  9 74REFFLD(HSRREF/@@BGFL *LIBL/HSPREF)
     A            D1RQSCD   R        O  9 78REFFLD(HSRREF/@@RQSCD *LIBL/HSPREF)
     A            D1ITDS    R        O 10  3REFFLD(@@ITDS)
     A                                 10 54'Com:'
     A            D1COM     R        B 10 59REFFLD(HSRREF/@@COM20 *LIBL/HSPREF)
     A                                      CHECK(LC)
     A N62                                  DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(PR)
     A*
     A*---------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*---------------------------------------------------------------
     A*
     A          R HP1717C1                  SFLCTL(HP1717S1)
     A*%%TS  SD  20060117  151100  ISLRAMS     REL-V5R3M0  5722-WDS
     A                                      SFLSIZ(0100)
     A                                      SFLPAG(0006)
     A N90                                  ROLLUP(25 'PAGE FORWARD')
     A                                      SFLCSRRRN(&HDRR1)
     A                                      OVERLAY
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A                                      SETOF(30)
     A                                      SETOF(32)
     A                                      SETOF(33)
     A                                      SETOF(34)
     A                                      SETOF(35)
     A                                      SETOF(36)
     A                                      SETOF(52)
     A                                      SETOF(53)
     A                                      SETOF(55)
     A                                      RTNCSRLOC(&RCD &FLD)
     A  78                                  SFLFOLD(CF13)
     A  79                                  SFLDROP(CF13)
     A                                      SFLMODE(&FOLD)
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            HDRR1          5S 0H
     A            FOLD           1A  H
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM         10A  O  1  2DSPATR(HI)
     A                                  1 27'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR         10A  O  1 70DSPATR(HI)
     A            C1MODE         6A  O  2  2DSPATR(HI)
     A  61                              2 25'CREATE A NEW ORDER REQUEST'
     A                                      DSPATR(HI)
     A  60                              2 26'MAINTAIN ORDER REQUEST'
     A                                      DSPATR(HI)
     A            SDJOB         10A  O  2 70DSPATR(HI)
     A                                  3 62'Request'
     A                                      DSPATR(UL)
     A                                  4  2'Work order number ..'
     A            C1WO      R        B  4 25REFFLD(HSRREF/@@WO *LIBL/HSPREF)
     A N62N30N63                            DSPATR(HI)
     A  30                                  DSPATR(RI)
     A  30                                  DSPATR(PC)
     A N62N63                               DSPATR(UL)
     A  62
     AO 63                                  DSPATR(PR)
     A                                  4 64'Date:'
     A            C1MDY          6Y 0O  4 71EDTWRD('  /  /  -')
     A                                  5  2'Farm site ..........'
     A N62N63                           5 23'?'
     A            C1FSCD    R        B  5 25REFFLD(HSRREF/@@FSCD *LIBL/HSPREF)
     A                                      EDTCDE(M)
     A N62N63N32                            DSPATR(HI)
     A  32                                  DSPATR(RI)
     A  32
     AO 52                                  DSPATR(PC)
     A N62N63                               DSPATR(UL)
     A  62
     AO 63                                  DSPATR(PR)
     A            C1FSNM        20A  O  5 36
     A                                  5 62'Status:'
     A            C1RQSDS   R        O  5 71REFFLD(HSRREF/@@RQSDS *LIBL/HSPREF)
     A                                  6  2'Comment ............'
     A            C1COM     R        B  6 25REFFLD(HSRREF/@@COM50 *LIBL/HSPREF)
     A                                      CHECK(LC)
     A N62N63                               DSPATR(HI)
     A N62N63                               DSPATR(UL)
     A  62
     AO 63                                  DSPATR(PR)
     A                                  7  4'?'
     A                                      DSPATR(HI)
     A                                  7 62'?'
     A                                      DSPATR(HI)
     A                                  8  2'Item'
     A                                      DSPATR(HI)
     A                                  8 10'Requested Qty'
     A                                      DSPATR(HI)
     A                                  8 27'Available Qty'
     A                                      DSPATR(HI)
     A                                  8 44'Approved Qty'
     A                                      DSPATR(HI)
     A                                  8 59'Ovr Rs'
     A                                      DSPATR(HI)
     A                                  8 67'Crit'
     A                                      DSPATR(HI)
     A                                  8 73'Bgt'
     A                                      DSPATR(HI)
     A N61                              8 77'St'
     A                                      DSPATR(HI)
     A*
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R HP1717K1
     A*%%TS  SD  20050210  092758  ISLRAMS     REL-V5R2M0  5722-WDS
     A  43                             21  2'Message program failed.  Call MIS -
     A                                      user support.'
     A                                      DSPATR(HI)
     A                                 22  2'F3=Exit'
     A N90                             22 11'F4=Prompt'
     A N90                             22 22'F5=Refresh'
     A N90                             22 34'F9=Accept'
     A  60N61N90                       22 45'F11=Delete'
     A N90                             22 57'F13=Fold/Unfold'
     A N90                             23 34'F14-Open Header Fields for Input'
     A*
     A*---------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE FORMAT
     A*---------------------------------------------------------------
     A          R HP1717ES                  SFL SFLMSGRCD(24)
     A*
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
     A*
     A*---------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*---------------------------------------------------------------
     A          R HP1717EC                  SFLCTL(HP1717ES)
     A*
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
