     A*%%TS  SD  20040414  080101  ISLRAMS     REL-V5R2M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD7708
     A*  TITLE:         VIEW ALL BUDGET ITEMS/FARM BUDGETS
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  DATE:          03/10/04
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A                                      CF04(04 'PROMPT')
     A                                      CF05(05 'REFRESH')
     A                                      CF13(13 'FOLD/DROP')
     A*
     A*
     A*---------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP7708S1                  SFL
     A*%%TS  SD  20040414  080101  ISLRAMS     REL-V5R2M0  5722-WDS
     A  85                                  SFLNXTCHG
     A            HDRRN1         4Y 0H
     A            D1BGIT    R        O  9  2REFFLD(HSRREF/@@BGIT *LIBL/HSPREF)
     A            D1BGQT    R        O  9 28REFFLD(HSRREF/@@BGQT *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A            D1FSCD    R        O  9 45REFFLD(HSRREF/@@FSCD *LIBL/HSPREF)
     A                                      EDTCDE(Z)
     A            D1FBSN    R        O  9 53REFFLD(HSRREF/@@FBSN *LIBL/HSPREF)
     A                                      EDTCDE(Z)
     A            D1FBSCD   R        O  9 65REFFLD(@@FBSCD)
     A            D1BTCD    R        O  9 72REFFLD(@@BTCD)
     A                                 10 22'Desc:'
     A                                      COLOR(PNK)
     A            D1DESC    R        O 10 28REFFLD(@@ITDS)
     A                                      COLOR(PNK)
     A*
     A*
     A*---------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP7708C1                  SFLCTL(HP7708S1)
     A*%%TS  SD  20040311  100132  ISLRAMS     REL-V5R2M0  5722-WDS
     A                                      SFLSIZ(0050)
     A                                      SFLPAG(0006)
     A N82                                  ROLLUP(25 'PAGE FORWARD')
     A                                      SETOF(51)
     A                                      SETOF(52)
     A                                      SETOF(53)
     A                                      SETOF(54)
     A                                      SETOF(55)
     A                                      RTNCSRLOC(&RCD &FLD)
     A                                      OVERLAY
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A  78                                  SFLFOLD(CF13)
     A  79                                  SFLDROP(CF13)
     A                                      SFLMODE(&FOLD)
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            FOLD           1A  H
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 26'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  2 19'VIEW ALL BUDGET ITEMS/FARM BUDGETS'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  4  9'?'
     A                                      DSPATR(HI)
     A                                  4 48'?'
     A                                      DSPATR(HI)
     A                                  4 56'?'
     A                                      DSPATR(HI)
     A                                  4 65'?'
     A                                      DSPATR(HI)
     A                                  4 75'?'
     A                                      DSPATR(HI)
     A                                  5 31'Farm Budget'
     A                                      DSPATR(HI)
     A                                  5 46'Farm'
     A                                      DSPATR(HI)
     A                                  5 54'Budget'
     A                                      DSPATR(HI)
     A                                  5 63'Budget'
     A                                      DSPATR(HI)
     A                                  5 72'Budget'
     A                                      DSPATR(HI)
     A                                  6  2'Budget Item'
     A                                      DSPATR(HI)
     A                                  6 34'Quantity'
     A                                      DSPATR(HI)
     A                                  6 46'Site'
     A                                      DSPATR(HI)
     A                                  6 54'Number'
     A                                      DSPATR(HI)
     A                                  6 63'Status'
     A                                      DSPATR(HI)
     A                                  6 71'Template'
     A                                      DSPATR(HI)
     A            C1BGIT    R        B  7  2REFFLD(HSRREF/@@BGIT *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A  51                                  DSPATR(PC)
     A            C1FSCD    R        B  7 45REFFLD(HSRREF/@@FSCD *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A                                      EDTCDE(M)
     A  54                                  DSPATR(PC)
     A            C1FBSN    R        B  7 53REFFLD(HSRREF/@@FBSN *LIBL/HSPREF)
     A                                      EDTCDE(M)
     A                                      DSPATR(HI)
     A  55                                  DSPATR(PC)
     A            C1FBSCD   R        B  7 65REFFLD(@@FBSCD)
     A                                      DSPATR(HI)
     A  53                                  DSPATR(PC)
     A            C1BTCD    R        B  7 72REFFLD(@@BTCD)
     A                                      DSPATR(HI)
     A  52                                  DSPATR(PC)
     A                                  8 15'Description:'
     A                                      DSPATR(HI)
     A            C1DESC    R        B  8 28REFFLD(@@ITDS)
     A                                      DSPATR(HI)
     A*
     A*
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R HP7708K1
     A*%%TS  SD  20040311  072529  ISLRAMS     REL-V5R2M0  5722-WDS
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A                                 23 11'F4=Prompt'
     A                                 23 22'F5=Refresh'
     A                                 23 34'F13=Fold/Unfold'
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *---------------------------------------------------------------
     A          R HP7708ES                  SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE CONTROL FORMAT
      *---------------------------------------------------------------
     A          R HP7708EC                  SFLCTL(HP7708ES)
      *
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
