      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      HOG PRODUCTION
      * PROGRAM:     HP612
      * TITLE:       PRINT OF SELECTED MANAGER/FARM SITE REPORT
      * PROGRAMMER:  MARY PALMER
      * CREATED:     3/31/94
      *
      * FUNCTION: BATCH PROGRAM SUBMITTED FROM WORK WITH MANAGERS
      *           PROGRAM HP402.  A SEPARATE REPORT WILL PRINT FOR EACH
      *           MANAGER SELECTED.
      *
      ****************************************************************
      * MODIFICATIONS:
      * DATE      PROGRAMMER
      * 04/20/95  LEANNE FEDOR
      *           CHANGED FARM SITE CODE FROM ALPHA TO NUMERIC
      *           CHANGED TO USE PROGRAM DATA STRUCTURE FOR PROGRAM
      *           ID AND USER NAME IN REPORT HEADING.
      *
      * 06/16/97  LEANNE FEDOR
      *           REPLACED ENTRY PARM LOGIC WITH LDA VALUES.
      *
      * 04/13/98  LeAnne Fedor
      *           Recompile only. New field 'state' added to farm site file.
      *
      * 06/05/01  LeAnne Fedor
      *           Recompile only. New field 'multisite' added to Farm Site file.
      *
      * 06/26/01  LeAnne Fedor
      *           Added 3 columns to layout: production mgr/cell mgr/multisite mgr.
      *           An 'X' will be printed in the appropriate column for the manager.
      *           One or more of the columns may have an X.
      *
      * 09/04/01  LeAnne Fedor
      *           Recompile only. New 'square feet' fields added to Farm Site file.
      *
      * 10/16/13  LeAnne Ramsey (E2831)
      *           Recompile only. Added field 'MTech Reference'.
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fhsp018    IF   E           K DISK
      *    FARM SITE   (records selected by open query)
      *
      *
     FQPRINT    O    F   80        PRINTER OFLIND(*INOF)
      *
      /EJECT
      ****************************************************************
      * INPUT SPECS
      ****************************************************************
      *
      *---------------------------------------------------------------
      * STANDARD PROGRAM STATUS DATA STRUCTURE
      *---------------------------------------------------------------
      *    EXTERNALLY DEFINED AS UTPGFR (RECORD FORMAT: PGMDSR)
     D PGMDS         ESDS                  EXTNAME(UTPGFR)
      *
      *---------------------------------------------------------------
      *  NAMED CONSTANTS
      *---------------------------------------------------------------
      *
     D YES             C                   CONST('Y')
     D NO              C                   CONST('N')
      *
      *----------------------------------------------------------------
      * LDA - REPORT SELECTIONS
      *----------------------------------------------------------------
      *
     D                UDS
     D  LDMGCD                 2      6
     D  LDMGNM                 7     31
      *
      /EJECT
      ****************************************************************
      * MAINLINE
      ****************************************************************
      *
      * PREPARE TO READ ALL FARM SITE RECORDS FOR SELECTED MANAGER AND
      * PRINT HEADINGS FOR FIRST TIME THROUGH
      *
     C                   EXCEPT    HRHDR
      *
      * READ AND PRINT ALL FARM SITE RECORDS FOR CHOSEN MANAGER
      *
     C     *IN90         DOUEQ     *ON                                          MAIN DO
     C                   READ      hsp018                                 90
     C     *IN90         IFEQ      *OFF                                         IF NOT EOF
      *
     C                   setoff                                       878889
      *
      * Production manager
      *
     C                   if        fspmgcd = ldmgcd
     C                   seton                                        87
     C                   endif
      * Cell manager
      *
     C                   if        fscmgcd = ldmgcd
     C                   seton                                        88
     C                   endif
      *
      * Multisite manager
      *
     C                   if        fsmmgcd = ldmgcd
     C                   seton                                        89
     C                   endif
      *
      * TEST FOR OVERFLOW, AND PRINT HEADINGS IF ON
      *
     C     *INOF         IFEQ      *ON
     C                   EXCEPT    HRHDR
     C                   ENDIF
      *
      * PRINT FARM SITE DETAIL LINE AND SET FLAG THAT RECORD WAS READ
      *
     C                   EXCEPT    DTLINE
      *
     C                   MOVEL     YES           PRTFLG
     C                   ENDIF                                                  IF NOT EOF
     C                   ENDDO                                                  MAIN DO
      *
      *--------------------------------------------------------------
      * EOF PROCESSING
      *--------------------------------------------------------------
      *  IF THE MASTER FILE WAS EMPTY OR MANAGER NOT ASSIGNED TO FARM
      *  PRINT THE STANDARD 'NO DATA...' ERROR MESSAGE
      *
     C     PRTFLG        IFEQ      NO
     C                   EXCEPT    NODATA
     C                   ENDIF
      *
     C                   SETON                                        LR
      /EJECT
      *---------------------------------------------------------------
      * *INZSR - INITIALIZATION SUBROUTINE
      *---------------------------------------------------------------
      *
     C     *INZSR        BEGSR
      *
      * SET PRINT FLAG TO DETERMINE IF ANY RECORDS WERE PRINTED
      * SET UP HEADING UNDERLINE FIELD FOR PRINT
      * RETRIEVE TIME FOR REPORT HEADING
      *
     C                   MOVEL     NO            PRTFLG            1
     C                   MOVEL     *ALL'-'       DASH             80
     C                   TIME                    RTIME             6 0
      *
     C                   ENDSR
      /EJECT
      *-------------------------------------------------------------
      * REPORT HEADING LINES
      *-------------------------------------------------------------
      *
     OQPRINT    E            HRHDR          1 01
     O                       SDPGM               10
     O                                           48 'HOG PRODUCTION SYSTEM'
     O                                           70 'DATE'
     O                       UDATE         Y     80
      *
     O          E            HRHDR          1
     O                       SDUSR               10
     O                                           47 'MANAGER LISTING OF FARM '
     O                                           52 'SITES'
     O                                           70 'TIME'
     O                       RTIME               80 '  :  :  '
      *
     O          E            HRHDR          1
     O                                           70 'PAGE'
     O                       PAGE          Z     80
      *
     O          E            HRHDR          2
     O                                            8 'MANAGER:'
     O                       LDMGCD              14
     O                       LDMGNM              41
      *
     O          E            HRHDR          1
     O                                            5 'FARM'
     O                                           49 '- PRODUCTION -'
     O                                           57 'CELL'
     O                                           69 'PRODUCTION'
     O                                           80 'MULTISITE'
      *
     O          E            HRHDR          1
     O                                            5 'SITE'
     O                                           22 'FARM SITE NAME'
     O                                           49 'PHASE     TYPE'
     O                                           56 'MGR'
     O                                           68 'MANAGER'
     O                                           79 'MANAGER'
      *
     O          E            HRHDR          1
     O                       DASH                80
      *
      *-------------------------------------------------------------
      * FARM SITE DETAIL LINE
      *-------------------------------------------------------------
      *
     O          E            DTLINE      1
     O                       FSFSCD        M      6
     O                       FSFSNM              33
     O                       FSPPCD              40
     O                       FSPTCD              50
     O               88                          55 'X'
     O               87                          65 'X'
     O               89                          76 'X'
      *
      *-------------------------------------------------------------
      * NO DATA MESSAGE LINE
      *-------------------------------------------------------------
      *
     O          E            NODATA      1
     O                                           19 'NO DATA MEETS YOUR'
     O                                           38 'SELECTION CRITERIA'
