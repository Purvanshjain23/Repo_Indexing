     A*%%TS  SD  20030113  103143  ISLRAMS     REL-V5R1M0  5722-WDS
     A*****************************************************************
     A*
     A*  SYSTEM:        HOG PRODUCTION
     A*  DISPLAY FILE:  HPD505 - SELECT FARM SITE
     A*  RPG PROGRAM:   HP505  - SELECT FARM SITE
     A*  CREATED BY:    ROSE CENTONZE
     A*  DATE:          03/16/94
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      PRINT
     A                                      CA12(12 'PREVIOUS')
     A N82                                  ROLLUP(25 'PAGE FORWARD')
     A*--------------------------------------------------------------*
     A* REFERENCE WINDOW RECORD FORMAT 1
     A*--------------------------------------------------------------*
     A          R HP505R1
     A*%%TS  SD  20010521  163739  ISLRAMS     REL-V4R4M0  5769-PW1
     A                                      TEXT('Reference window')
     A                                      WINDOW(*DFT 19 56)
     A                                      WDWBORDER((*DSPATR RI) (*CHAR '    -
     A                                          '))
     A*--------------------------------------------------------------*
     A* RECORD FORMAT ONE
     A*--------------------------------------------------------------*
     A          R HP505R2
     A*%%TS  SD  19940315  135528  ISRCENT     REL-V2R2M0  5738-PW1
     A                                      TEXT('Written in RPG pgm to prevent-
     A                                       screen from clearing')
     A                                      CLRL(*NO)
     A                                      OVERLAY
     A*--------------------------------------------------------------*
     A* RECORD FORMAT TWO
     A*--------------------------------------------------------------*
     A* THIS FORMAT IS NOT WRITTEN IN THE RPG PGM.  IT PREVENTS THE
     A* CALLING_PROGRAM_SCREEN FROM BEING CLEARED.
     A*--------------------------------------------------------------*
     A          R HP505R3
     A*%%TS  SD  19940315  135528  ISRCENT     REL-V2R2M0  5738-PW1
     A                                      TEXT('Dummy format not written in R-
     A                                      PG pgm.')
     A                                      ASSUME
     A                                 11  1' '
     A*--------------------------------------------------------------*
     A* SUBFILE FORMAT ONE
     A*--------------------------------------------------------------*
     A          R HP505S1                   SFL
     A*%%TS  SD  20010521  163739  ISLRAMS     REL-V4R4M0  5769-PW1
     A                                      TEXT('subfile record format one')
     A            HDRRN1         4S 0H
     A            DFFSCD    R        O  8  1REFFLD(HSRREF/FSFSCD *LIBL/HSPREF)
     A                                      EDTCDE(M)
     A            DFFSNM    R        O  8  8REFFLD(HSRREF/FSFSNM *LIBL/HSPREF)
     A            DFPPCD    R        O  8 36REFFLD(FSPPCD)
     A            DFPTCD    R        O  8 43REFFLD(FSPTCD)
     A            DFCELL    R        O  8 52REFFLD(HSRREF/@@CELL *LIBL/HSPREF)
     A                                      EDTCDE(Z)
     A*--------------------------------------------------------------*
     A* SUBFILE CONTROL FORMAT ONE
     A*--------------------------------------------------------------*
     A          R HP505C1                   SFLCTL(HP505S1)
     A*%%TS  SD  20030113  103143  ISLRAMS     REL-V5R1M0  5722-WDS
     A                                      SFLSIZ(0050)
     A                                      SFLPAG(0008)
     A                                      WINDOW(HP505R1)
     A                                      TEXT('control format one')
     A                                      OVERLAY
     A                                      SFLCSRRRN(&HDRRN)
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND(*MORE)
     A                                      RMVWDW
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            HDRRN          5S 0H
     A                                  1 17'SELECT FARM SITE'
     A                                      DSPATR(HI)
     A                                  2  1'Position Cursor, Press Enter'
     A                                  4  1'Business office:'
     A            C1FSBO    R        O  4 18REFFLD(HSRREF/FSFSBO *LIBL/HSPREF)
     A                                  5 37'Production'
     A                                      DSPATR(HI)
     A                                  6  2'Code'
     A                                      DSPATR(HI)
     A                                  6  8'Name'
     A                                      DSPATR(HI)
     A                                  6 36'Phase'
     A                                      DSPATR(HI)
     A                                  6 43'Type'
     A                                      DSPATR(HI)
     A                                  6 51'Cell'
     A                                      DSPATR(HI)
     A            C1FSCD    R     Y  B  7  1REFFLD(FSFSCD)
     A                                      DSPATR(HI)
     A                                      EDTCDE(M)
     A            C1PPCD    R        B  7 36REFFLD(FSPPCD)
     A                                      DSPATR(HI)
     A            C1PTCD    R        B  7 43REFFLD(FSPTCD)
     A                                      DSPATR(HI)
     A            C1CELL    R        B  7 52REFFLD(HSRREF/@@CELL *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A                                      EDTCDE(M)
      *
     A          R HP505K1
     A                                      OVERLAY
     A                                      WINDOW(HP505R1)
     A                                 18  1'F12=Previous'
