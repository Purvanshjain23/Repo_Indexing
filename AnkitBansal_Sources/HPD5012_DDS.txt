     A*%%TS  DD  20030220  140005  QUSER       REL-V4.0    WDSCi
     A*****************************************************************
     A*
     A*  SYSTEM:        HOG PRODUCTION
     A*  DISPLAY FILE:  HPD5012 - SELECT LOAD TYPE
     A*  CREATED BY:    LEANNE FEDOR
     A*  DATE:          10/17/00
     A*
     A*****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      PRINT
     A                                      CA12(12 'PREVIOUS')
     A N82                                  PAGEDOWN(25 'PAGE FORWARD')
     A*---------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP5012S1
     A                                      SFL
     A            HDRRN1    R        H      REFFLD(@@RRN)
     A            D1LTCD    R        O  5  1REFFLD(@@LTCD)
     A            D1LTDS    R        O  5  8REFFLD(@@LTDS)
     A*
     A*---------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP5012C1
     A                                      SFLCTL(HP5012S1)
     A                                      SFLSIZ(15)
     A                                      SFLPAG(11)
     A                                      WINDOW(HP5012EC)
     A                                      OVERLAY
     A                                      SFLCSRRRN(&HDRRN)
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A            HDPAG1    R        H      REFFLD(@@PAG)
     A                                      SFLRCDNBR
     A            HDRRN     R        H      REFFLD(@@RRN)
     A                                  1  7'SELECT LOAD TYPE'
     A                                      DSPATR(HI)
     A                                  2  2'Position Cursor, Press Enter'
     A                                  4  1'Code'
     A                                      DSPATR(HI)
     A                                  4  8'Description'
     A                                      DSPATR(HI)
     A*---------------------------------------------------------------
     A* COMMAND LINE LINE
     A*---------------------------------------------------------------
     A          R HP5012K1
     A                                      WINDOW(HP5012EC)
     A                                      OVERLAY
     A  43                             16  1'Message program failed. Call IS.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 17  1'F12=Previous'
     A*
     A*-------------------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE FORMAT
     A*-------------------------------------------------------------------------
     A          R HP5012ES
     A                                      SFL
     A                                      SFLMSGRCD(18)
     A*
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
     A*
     A*-------------------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*-------------------------------------------------------------------------
     A*
     A          R HP5012EC
     A                                      SFLCTL(HP5012ES)
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(20)
     A                                      SFLPAG(1)
     A                                      WINDOW(*DFT 19 35)
     A                                      WDWBORDER((*DSPATR RI) (*CHAR '    -
     A                                          '))
     A            MSGPGM                    SFLPGMQ(10)
     A*
     A*---------------------------------------------------------------
     A* ASSUME FORMAT -- REQUIRED FOR WINDOWS
     A*---------------------------------------------------------------
     A          R ASSUME
     A                                      ASSUME
     A                                      OVERLAY
     A                                 24 79' '
