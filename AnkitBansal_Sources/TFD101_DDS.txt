     A*%%TS  SD  20080602  134853  ISLRAMS     REL-V5R4M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        TRIUMPH FOODS
     A*  DISPLAY FILE:  TFD101
     A*  TITLE:         REVISE CASH DISTRIBUTION DETAIL--USER GENERATED
     A*  PROGRAMMER:    LEANNE RAMSEY
     A*  DATE:          04/24/08
     A*
     A*  INDICATOR 60 IS ON WHEN IN REVISE MODE
     A*  INDICATOR 61 IS ON WHEN IN CREATE MODE
     A*  INDICATOR 62 IS ON WHEN IN DELETE MODE
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/TFPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CF03(03 'EXIT')
     A  60N62                               CF05(05 'REFRESH')
     A N62                                  CF09(09 'ACCEPT')
     A  60N62                               CF11(11 'DELETE')
     A*
     A*--------------------------------------------------------------
     A* RECORD FORMAT ONE
     A*--------------------------------------------------------------
     A          R TF101R1
     A*%%TS  SD  20080602  134853  ISLRAMS     REL-V5R4M0  5722-WDS
     A                                      SETOF(30)
     A                                      SETOF(31)
     A                                      OVERLAY
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 24'REVISE CASH DISTRIBUTION DETAIL'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USR)
     A                                      DSPATR(HI)
     A            D1MODE         6A  O  2  2DSPATR(HI)
     A                                  2 25'(USER-GENERATED RECORDS ONLY)'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USR)
     A                                      DSPATR(HI)
     A                                  4  2'Daily date .............:'
     A            D1DMDY    R        O  4 28REFFLD(TFREF/@@DT06 *LIBL/TFPREF)
     A                                      EDTWRD('  /  /  ')
     A                                  4 42'Batch number:'
     A            D1BTSN    R        O  4 56REFFLD(TFREF/@@BTSN *LIBL/TFPREF)
     A                                      EDTCDE(Z)
     A                                  5  2'Revenue week-ending date:'
     A            D1RMDY    R        O  5 28REFFLD(TFREF/@@DT06 *LIBL/TFPREF)
     A                                      EDTWRD('  /  /  ')
     A                                  6  2'Cash week-ending date ..:'
     A            D1CMDY    R        O  6 28REFFLD(TFREF/@@DT06 *LIBL/TFPREF)
     A                                      EDTWRD('  /  /  ')
     A                                  7  2'Record exempt flag .....:'
     A            D1RCEXFL  R        O  7 28REFFLD(TFREF/@@RCEXFL *LIBL/TFPREF)
     A                                  9  2'Adjustment reason .......'
     A            D1AJRSN   R        B  9 28REFFLD(TFREF/@@AJRSN *LIBL/TFPREF)
     A                                      CHECK(LC)
     A  30                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A N30                                  DSPATR(HI)
     A                                 11 29'System-Generated'
     A                                      COLOR(PNK)
     A                                 11 51'I.S. Manual'
     A                                      COLOR(PNK)
     A                                 12 28'Distributed Amount'
     A                                      COLOR(PNK)
     A                                      DSPATR(UL)
     A                                 12 48'Adjustment Amount'
     A                                      COLOR(PNK)
     A                                      DSPATR(UL)
     A                                 13  2'Agg sales expense .......'
     A            D1ASXCDAM1R        O 13 30REFFLD(TFREF/@@ASXCDAM *LIBL/TFPREF)
     A                                      EDTCDE(K)
     A            D1ASXCDAM2R        B 13 49REFFLD(TFREF/@@ASXCDAM *LIBL/TFPREF)
     A  31                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A N31                                  DSPATR(HI)
     A                                      EDTCDE(K)
     A                                 14  2'TF sales expense ........'
     A            D1TSXCDAM1R        O 14 30REFFLD(TFREF/@@TSXCDAM *LIBL/TFPREF)
     A                                      EDTCDE(K)
     A            D1TSXCDAM2R        B 14 49REFFLD(TFREF/@@TSXCDAM *LIBL/TFPREF)
     A                                      DSPATR(HI)
     A                                      EDTCDE(K)
     A                                 15  2'TF absorbed freight .....'
     A            D1TAFCDAM1R        O 15 30REFFLD(TFREF/@@TAFCDAM *LIBL/TFPREF)
     A                                      EDTCDE(K)
     A            D1TAFCDAM2R        B 15 49REFFLD(TFREF/@@TAFCDAM *LIBL/TFPREF)
     A                                      DSPATR(HI)
     A                                      EDTCDE(K)
     A                                 16  2'TF broker commission ....'
     A            D1TBCCDAM1R        O 16 30REFFLD(TFREF/@@TBCCDAM *LIBL/TFPREF)
     A                                      EDTCDE(K)
     A            D1TBCCDAM2R        B 16 49REFFLD(TFREF/@@TBCCDAM *LIBL/TFPREF)
     A                                      DSPATR(HI)
     A                                      EDTCDE(K)
     A                                 17  2'TF other sales expense ..'
     A            D1TOXCDAM1R        O 17 30REFFLD(TFREF/@@TOXCDAM *LIBL/TFPREF)
     A                                      EDTCDE(K)
     A            D1TOXCDAM2R        B 17 49REFFLD(TFREF/@@TOXCDAM *LIBL/TFPREF)
     A                                      DSPATR(HI)
     A                                      EDTCDE(K)
     A                                 18  2'Seaboard ................'
     A            D1SCDAM1  R        O 18 30REFFLD(TFREF/@@SCDAM *LIBL/TFPREF)
     A                                      EDTCDE(K)
     A            D1SCDAM2  R        B 18 49REFFLD(TFREF/@@SCDAM *LIBL/TFPREF)
     A                                      DSPATR(HI)
     A                                      EDTCDE(K)
     A                                 19  2'Triumph .................'
     A            D1TCDAM1  R        O 19 30REFFLD(TFREF/@@TCDAM *LIBL/TFPREF)
     A                                      EDTCDE(K)
     A            D1TCDAM2  R        B 19 49REFFLD(TFREF/@@TCDAM *LIBL/TFPREF)
     A                                      DSPATR(HI)
     A                                      EDTCDE(K)
     A                                 21 24'Agg adjustment amount ..'
     A            D1AAJAM2  R        B 21 49REFFLD(TFREF/@@TCDAM *LIBL/TFPREF)
     A                                      DSPATR(HI)
     A                                      EDTCDE(K)
     A*--------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*--------------------------------------------------------------
     A          R TF101K1
     A*%%TS  SD  20050608  120008  ISLRAMS     REL-V5R3M0  5722-WDS
     A  43                             22  2'Message program failed.   Call MIS-
     A                                       user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A N62N61                          23 11'F5=Refresh'
     A N62                             23 23'F9=Accept'
     A N61N62                          23 34'F11=Delete'
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *---------------------------------------------------------------
     A          R TF101ES                   SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE CONTROL FORMAT
      *---------------------------------------------------------------
     A          R TF101EC                   SFLCTL(TF101ES)
      *
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
