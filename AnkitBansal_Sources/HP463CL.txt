/***************************************************************** */
/*                                                                 */
/*  PROGRAM NUMBER --> HP463CL                                     */
/*  PROGRAM NAME ----> FEED TICKET EXCEPTION REPORTS               */
/*  PROGRAMMER ------> LEANNE FEDOR                                */
/*  DATE ------------> 1/25/95                                     */
/*                                                                 */
/*  FUNCTION :  THIS CL CALLS THE PROGRAMS TO PRINT THE FEED       */
/*              TICKET EXCEPTION REPORTS.                          */
/*                                                                 */
/*              PROGRAM HP663-FEED TICKET EXCEPTION REPORT IS      */
/*              CALLED IF THE USER SELECTED ONE OF THE FOLLOWING:  */
/*                      NOT COSTED                                 */
/*                      NOT POSTED TO PERIOD END                   */
/*                      NOT POSTED TO GENERAL LEDGER               */
/*                                                                 */
/*              PROGRAM HP664-FEED TICKET EXCEPTION REPORT IS      */
/*              CALLED IF THE USER SELECTED NOT ALLOCATED.         */
/*                                                                 */
/***************************************************************** */
/*  MODIFIED:                                                      */
/*                                                                 */
/* 04/24/96  LEANNE FEDOR                                          */
/*           ADDED CUSTOMIZED PRINTER OVERRIDE COMMAND TO FORCE    */
/*           REPORT TO PRINT ON LASER PRINTER.                     */
/*                                                                 */
/* 06/19/97  LEANNE FEDOR                                          */
/*           CHANGED OUT QUEUE LOGIC.                              */
/*                                                                 */
/* 06/25/97  LEANNE FEDOR                                          */
/*           RECOMPILE ONLY. FIELD ADDED TO FEED TICKET HEADER.    */
/*                                                                 */
/* 03/10/98  LEANNE FEDOR                                          */
/*           INCREASED PRINT LINE FROM 85 TO 89 TO ACCOMMODATE     */
/*           INCREASED SIZE OF FEED POUNDS.                        */
/*******************************************************************/

             PGM

/* DEFINE THE VARIABLE REQUIRED IN THE COMMAND TO OVERRIDE THE     */
/* PRINT FILE TO A LASER PRINTER                                   */

             DCL        VAR(&CMD) TYPE(*CHAR) LEN(512)

             DCL        VAR(&QDATA) TYPE(*CHAR) LEN(180)
             DCL        VAR(&LDFMBO) TYPE(*CHAR) LEN(5)
             DCL        VAR(&LDCYMD) TYPE(*CHAR) LEN(8)
             DCL        VAR(&LDROCD) TYPE(*CHAR) LEN(1)

             DCL        VAR(&HOLD) TYPE(*CHAR) LEN(10)
             DCL        VAR(&COPYS) TYPE(*DEC) LEN(2)
             DCL        VAR(&OUTQ) TYPE(*CHAR) LEN(10)

             CHGVAR     VAR(&LDFMBO) VALUE(%SST(*LDA 1 5))
             CHGVAR     VAR(&LDCYMD) VALUE(%SST(*LDA 6 8))
             CHGVAR     VAR(&LDROCD) VALUE(%SST(*LDA 14 1))

/*---------------------------------------------------------------- */
/* SETUP PRINT PARAMETERS USED BY BOTH REPORTS                     */
/*---------------------------------------------------------------- */

 HOLD:       IF         COND(%SST(*LDA 411 1) = 'Y') THEN(CHGVAR +
                          VAR(&HOLD) VALUE(*YES))
             ELSE       CMD(CHGVAR VAR(&HOLD) VALUE(*NO))

 COPYS:      CHGVAR     VAR(&COPYS) VALUE(%SST(*LDA 412 1))
             IF         COND(&COPYS = 0) THEN(CHGVAR VAR(&COPYS) +
                          VALUE(1))

 OUTQ:       CHGVAR     VAR(&OUTQ) VALUE(%SST(*LDA 413 10))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(%SST(*LDA 401 10)))
             IF COND(&OUTQ = '          ') THEN(CHGVAR VAR(&OUTQ) +
                          VALUE(*JOB))

/*------------------------------------------------------------------*/
/* OVERRIDE PRINT FILE                                              */
/*------------------------------------------------------------------*/

/* USE THE CUSTOMIZED SEABOARD COMMAND TO OVERRIDE THE PRINT FILE.  */
/* THIS COMMAND ALLOWS THE REPORT TO BE GENERATED ON A STANDARD LINE*/
/* PRINTER OR A LASER PRINTER. TO USE THIS OVERRIDE YOU MUST DECLARE*/
/* A COMMAND VARIABLE AND FOLLOW THE OVERRIDE WITH A CALL TO        */
/* QCMDEXC.                                                         */

/* THIS REPORT IS STANDARD 10 CPI WITH 89 COLUMNS OF PRINT          */

             SBDPRTOVR  CMDVAR(&CMD) FILE(QPRINT) OUTQ(&OUTQ) +
                          WIDTH(89) COPIES(&COPYS) HOLD(&HOLD)

             CALL       PGM(QCMDEXC) PARM(&CMD 512)


/*-------------------------------------------------------------------*/
/* SETUP QUERY WHEN USER SELECTED ANYTHING OTHER THAN 'NOT ALLOCATED'*/
/*-------------------------------------------------------------------*/

             IF         COND(&LDROCD *NE 'A') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 1 10)) VALUE('FHTKDT *LE')
             CHGVAR     VAR(%SST(&QDATA 12 8)) VALUE(&LDCYMD)
             CHGVAR     VAR(%SST(&QDATA 21 17)) VALUE('*AND FHFMBO +
                          *EQ "')
             CHGVAR     VAR(%SST(&QDATA 38 5)) VALUE(&LDFMBO)
             CHGVAR     VAR(%SST(&QDATA 43 1)) VALUE('"')

/* CONDITION REST OF &QDATA FOR NOT COSTED OPTION                */

             IF         COND(&LDROCD *EQ 'C') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 45 15)) VALUE('*AND FHTSCD +
                          *EQ')
             CHGVAR     VAR(%SST(&QDATA 61 4)) VALUE('"EN"')
             CHGVAR     VAR(%SST(&QDATA 65 15)) VALUE(' *OR FHTSCD +
                          *EQ')
             CHGVAR     VAR(%SST(&QDATA 81 4)) VALUE('"RC"')
             ENDDO

/* CONDITION REST OF &QDATA FOR NOT POSTED TO PERIOD END OPTION  */

             IF         COND(&LDROCD *EQ 'P') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 45 15)) VALUE('*AND FHTSCD +
                          *EQ')
             CHGVAR     VAR(%SST(&QDATA 61 4)) VALUE('"CO"')
             ENDDO

/* CONDITION REST OF &QDATA FOR NOT POSTED TO GENERAL LEDGER     */

             IF         COND(&LDROCD *EQ 'G') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 45 15)) VALUE('*AND FHTSCD +
                          *EQ')
             CHGVAR     VAR(%SST(&QDATA 61 4)) VALUE('"CO"')
             CHGVAR     VAR(%SST(&QDATA 65 15)) VALUE(' *OR FHTSCD +
                          *EQ')
             CHGVAR     VAR(%SST(&QDATA 81 4)) VALUE('"PP"')
             ENDDO

/* RUN OPEN QUERY OVER THE FEED TICKET HEADER AND CALL PRINT PROGRAM */

             OVRDBF     FILE(HSP037) SHARE(*YES)
             OPNQRYF    FILE((HSP037)) QRYSLT(&QDATA) +
                          KEYFLD((FHTKNO) (FHTRCD) (FHRTNO)) +
                          SEQONLY(*YES)

             CALL       PGM(HP663)
             CLOF       OPNID(HSP037)
             DLTOVR     FILE(HSP037 QPRINT)
             ENDDO

/*---------------------------------------------------------------- */
/* SETUP QUERY WHEN THE USER SELECTED 'NOT ALLOCATED'              */
/*---------------------------------------------------------------- */

             IF         COND(&LDROCD *EQ 'A') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 1 10)) VALUE('FDTKDT *LE')
             CHGVAR     VAR(%SST(&QDATA 12 8)) VALUE(&LDCYMD)
             CHGVAR     VAR(%SST(&QDATA 21 15)) VALUE('*AND FDALFL +
                          *EQ')
             CHGVAR     VAR(%SST(&QDATA 37 3)) VALUE('"T"')
             CHGVAR     VAR(%SST(&QDATA 41 17)) VALUE('*AND FDFMBO +
                          *EQ "')
             CHGVAR     VAR(%SST(&QDATA 58 5)) VALUE(&LDFMBO)
             CHGVAR     VAR(%SST(&QDATA 63 1)) VALUE('"')

/* RUN OPEN QUERY OVER THE FEED TICKET DETAIL AND CALL PRINT PROGRAM */

             OVRDBF     FILE(HSP038) SHARE(*YES)
             OPNQRYF    FILE((HSP038)) QRYSLT(&QDATA) +
                          KEYFLD((FDRTNO) (FDTKNO) (FDTRCD)) +
                          SEQONLY(*YES)

             CALL       PGM(HP664)
             CLOF       OPNID(HSP038)
             DLTOVR     FILE(HSP038 QPRINT)
             ENDDO

             ENDPGM

