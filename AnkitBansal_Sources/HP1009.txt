      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Hog Production System
      * PROGRAM:     HP1009
      * TITLE:       Revise Sire Line
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     10/25/01
      *
      *
      * FUNCTION: Uses an edit record format to add/change/delete sire line records
      *           from the master file.
      *
      *           This program is called from HP4009 - Work with Sire Lines.
      *
      *           The field 'mode' contains the value passed into
      *           this program from the calling program.  The field
      *           'd1mode' is the screen field that displays the
      *           value of 'mode'.  Mode will be CREATE, REVISE or
      *           DELETE.           Once in the program, mode and
      *           d1mode will never change.  However, the action
      *           being performed can change depending on the function
      *           keys the user takes.
      *
      *           The user may delete an existing record by taking
      *           F11=DELETE from this program or option 4=DELETE
      *           from the WORK WITH. If the user is deleting with
      *           4=DELETE, no panel will be displayed from this program
      *           unless there is an error on the attempted deletion.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 02/01/05  LeAnne Fedor
      *           We have added Sire Line Company and Prime Line to the master file.
      *           We will concatenate the 2 together to derive the Sire Line Code.
      *           Also, the Alternate Report Groupings are now optional entries.
      *
      * 07/06/09  LeAnne Ramsey
      *           Recompile only. Added new field 'Continuous Flow Flag' to Hog Group file.
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fhpd1009   cf   e             workstn infds(iofeed)
      *
      *
     Fhsl034v   if   e           k disk
      *    Hog group
      *
      *
     Fhsl081c   if   e           k disk
      *    Weekly sire line doses
      *
      *
     Fhsl098a   uf a e           k disk
      *    Sire Lines
      *
      *
     Fhsl130a   if   e           k disk
      *    Group overrides
      *
      /eject
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D create          c                   'CREATE'
     D revise          c                   'REVISE'
     D delete          c                   'DELETE'
     D view            c                   'VIEW  '
     D select          c                   'SELECT'
     D yes             c                   'Y'
     D no              c                   'N'
     D set1            c                   'SET1  '
     D edit1           c                   'EDIT1 '
     D scrn1           c                   'SCRN1 '
     D exit            c                   'EXIT  '
     D update          c                   'UPDATE'
      *
      *
      ****************************************************************
      * ARRAYS AND TABLES
      ****************************************************************
      *
     D arlet           s              1    dim(26) ctdata perrcd(26)
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Standard fields
      *
     D mode            s              6
     D pgm             s             10
     D msgfl           s             10
     D routne          s              6
     D rtncd           s              2
     D action          s                   like(mode)
     D maxmsg          s              2  0  inz(20)
      *
      *
      * Workfields
      *
     D wkalph          s              3
     D wklet           s              1
      *
      *
      * Parm fields
      *
     D xxaist          s                   like(siaist)
      *
      *
      * Array indexes
      *
     D x               s              3  0
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * standard error message handling data structures
      *---------------------------------------------------------------
      *
      * For error message handling, a packed index (3) is required.
      * For program readability, define a corresponding error count
      * field called 'error'
      *
     D                 ds                  inz
     D  error                         2p 0
     D   e                            2p 0 overlay(error)
      *
      * This data structure supplies the name of the message file to
      * the message handling CL program.  The field name MSGFIL must be
      * constant. The value in quotes is the name of the specific
      * message file containing the user defined messages.
      *
     D                 ds                  inz
     D  msgfil                       10    inz('HSMSGF    ')
      *
      *---------------------------------------------------------------
      *  Standard message data structures
      *---------------------------------------------------------------
      *
      * The following 3 data structures are used to speed message
      * handling since it is faster to clear data structures than
      * arrays.  Each is associated with a standard message array.
      *
     D mgi             ds                  inz
     D  mgid                          7    dim(20)
      *
     D mgd             ds                  inz
     D  mgdt                         50    dim(20)
      *
     D mgwk            ds                  inz
     D  mgw                           1    dim(50)
      *
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *---------------------------------------------------------------
      * Standard workstation information data structure
      *---------------------------------------------------------------
      *    externally defined as UTWSFR (record format: UTIDFRR)
     D iofeed        e ds                  extname(utwsfr)
      *
      *---------------------------------------------------------------
      * Standard database file information data structure
      *---------------------------------------------------------------
      *    externally defined as UTDBGR (record format: FDBCKD)
     D dbfeed        e ds                  extname(utdbfr)
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * MAINLINE
      ****************************************************************
      *
     C                   eval      routne = set1
      *
     C     *inlr         doweq     *off                                         Main do loop
     C     routne        caseq     set1          $set1
     C     routne        caseq     scrn1         $scrn1
     C     routne        caseq     edit1         $edit1
     C     routne        caseq     update        $upd1
     C     routne        caseq     exit          $exit
     C                   endcs
     C                   enddo                                                  Main do loop
      /EJECT
      *----------------------------------------------------------------
      * Set environment for screen 1
      *----------------------------------------------------------------
      *
     C     $set1         begsr
      *
      * Clear fields on display panel if creating
      *
     C                   select
     C                   when      mode = create                                WH create
     C                   move      *blank        d1sicomp
     C                   move      *blank        d1prime
     C                   move      *blank        d1sicd
     C                   move      *blank        d1sids
     C                   move      *blank        d1aadglb
     C                   move      *blank        d1mgart
     C                   move      *blank        d1gg0
     C                   move      *blank        d1gg1
     C                   move      *blank        d1gg2
      *
      * If incoming mode is REVISE or DELETE, then retrieve the database
      * record.  Do not lock the record.  Typically, in DELETE mode, these
      * values will not be displayed. However, if there is an error on the
      * delete action, the panel will be displayed with messages.
      *
     C                   when      mode = revise or mode = delete               WH revise/delete
     C     key01         chain(n)  hsl098a                            92
     C                   if        *in92 = *off                                 If found
     C                   move      sisicd        d1sicd
     C                   move      sisids        d1sids
     C                   z-add     siaadglb      d1aadglb
     C                   z-add     simgart       d1mgart
     C                   move      sigg0         d1gg0
     C                   move      sigg1         d1gg1
     C                   move      sigg2         d1gg2
     C                   move      siaist        d1aist
     C                   else
      *
     C                   seton                                        62
     C                   if        error < maxmsg
     C                   add       1             error
     C                   eval      mgid(e) = 'HS09008'
     C                   endif
      *
     C                   endif                                                  If found
     C                   endsl
      *
      * If the user is deleting and there were no errors when retrieving the
      * record, go on to the update routine without displaying the screen.
      * Otherwise, display the screen.
      *
     C                   if        error = 0 and action = delete                If no error
     C                   eval      routne = update
     C                   else
     C                   eval      routne = scrn1
     C                   endif                                                  If no error
      *
     C                   endsr
      /EJECT
      *----------------------------------------------------------------
      * $scrn1 - perform operator I/O for screen 1
      *----------------------------------------------------------------
      *
     C     $scrn1        begsr
      *
      * Write the error messages from the error arrays to the
      * error message subfile
      *
     C                   exsr      $wrmsg
      *
      *
      * Set indicators to control input/output fields and available
      * function keys.
      *
     c                   select
     C                   when      mode = revise
     C                   seton                                        60
      *
     C                   when      mode = create
     C                   seton                                        61
      *
     C                   when      mode = delete
     C                   seton                                        62
     c                   endsl
      *
      * Write screen 1 to CRT
      *
     C                   write     hp1009k1
     C                   write     hp1009ec
     C                   exfmt     hp1009r1
      *
      * Clear messages
      *
     C                   exsr      $clmsg
      *
      * Reset action to be equal to the incoming mode.  This is required
      * in case the user took F11-Delete and got an error on the
      * deletion and now needs to be able to change the record and take
      * an action other than delete
      *
     C                   eval      action = mode
      *
      * Get user's input and set routine
      *
     C                   select
      *
     C                   when      *in03 = *on                                  F3-exit
     C                   eval      rtncd = '03'
     C                   eval      routne = exit
      *
     C                   when      *in04 = *on                                  F4=prompt
     C                   exsr      $f4s1
      *
     C                   when      *in05 = *on                                  F5-refresh
     C                   eval      routne = set1
      *
     C                   when      *in12 = *on                                  F12-previous
     C                   eval      rtncd = '12'
     C                   eval      routne = exit
      *
     C                   when      *in11 = *on or mode = delete                 F11-delete
     C                   eval      action = delete
     C                   eval      routne = update
     C                   other
      *
      * User has pressed enter
      *
     C                   eval      routne = edit1
     C                   endsl
      *
     C                   endsr
      /EJECT
      *----------------------------------------------------------------
      * Edit input fields for screen 1
      *----------------------------------------------------------------
      *
     C     $edit1        begsr
      *
      * *IN62 will be on if the record the user is trying to access has
      * been deleted by another user.  So, only perform the edits for the
      * fields on the screen if the indicator is off
      *
     C                   if        *in62 = *off                                 If here
      *
      * If creating a new record, validate:
      *   1) sire line company
      *   2) prime line
      *
     C                   if        action = create                              If create
     C                   exsr      $sicomp
     C                   exsr      $prime
     C                   endif                                                  If create
      *
      * Always validate:
      * 1) cognos report grouping
      * 2) alternate report grouping 1
      * 3) alternate report grouping 2
      * 4) active/inactive status
      *
     C                   exsr      $gg0
     C                   exsr      $gg1
     C                   exsr      $gg2
     C                   exsr      $aist
     C                   endif                                                  If here
      *
      * If there are no errors, attempt the write/update; otherwise,
      * redisplay the screen.  If a record has been deleted out from
      * under the user (*IN62 is ON), then the error message will be
      * issued in the update subroutine.  So, always perform this code.
      *
     C                   if        error = 0
     C                   exsr      $upd1
     C                   else
     C                   eval      routne = scrn1
     C                   endif
      *
     C                   endsr
      /EJECT
      *----------------------------------------------------------------
      * Validate Sire Line Company
      *----------------------------------------------------------------
      *
      * Validations:
      *  1) required
      *  2) Must be a letter of the alphabet
      *
      *
     C     $sicomp       begsr
      *
     C                   select
     C                   when      d1sicomp = *blank
     C                   seton                                        33
     C                   if        error < maxmsg
     C                   add       1             error
     C                   eval      mgid(e) = 'HS09001'
     C                   endif
     C                   other
      *
      * The 1st position of Company must be a letter:
      *
     C                   z-add     1             x
     C                   movel     d1sicomp      wklet
     C     wklet         lookup    arlet(x)                               92
     C                   if        *in92 = *off                                 If no hit
     C                   seton                                        33
     C                   endif                                                  If no hit
      *
      * The 2nd position of Company can be a letter or a blank:
      *
     C                   z-add     1             x
     C                   move      d1sicomp      wklet
     C                   if        wklet <> *blank                              If positon2
     C     wklet         lookup    arlet(x)                               92
     C                   if        *in92 = *off                                 If no hit
     C                   seton                                        33
     C                   endif                                                  If no hit
     C                   endif                                                  If position 2
      *
     C                   if        *in33 = *on and error < maxmsg
     C                   add       1             error
     C                   eval      mgid(e) = 'HS09076'
     C                   endif
     C                   endsl
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Validate Prime Line
      *---------------------------------------------------------------
      *
     C     $prime        begsr
      *
     C                   select
     C                   when      d1prime = 0
     C                   seton                                        34
     C                   if        error < maxmsg
     C                   add       1             error
     C                   eval      mgid(e) = 'HS09001'
     C                   endif
      *
     C                   when      d1prime < 0
     C                   seton                                        34
     C                   if        error < maxmsg
     C                   add       1             error
     C                   eval      mgid(e) = 'HS09000'
     C                   endif
     C                   endsl
      *
      * Company/Prime line must be unique in file.
      *
     C                   if        *in33 = *off and *in34 = *off                If OK so far
     C     key01         chain(n)  hsl098a                            92
     C                   if        *in92 = *off                                 If already exist
     C                   seton                                        3334
     C                   if        error < maxmsg
     C                   add       1             error
     C                   eval      mgid(e) = 'HS01019'
     C                   clear                   mgw
     C                   movea     d1sicomp      mgw(1)
     C                   movel     d1prime       wkalph
     C                   movea     wkalph        mgw(3)
     C                   movea     mgw           mgdt(e)
     C                   endif
     C                   endif                                                  If already exist
     C                   endif                                                  If OK so far
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Validate status
      *---------------------------------------------------------------
      *
     C     $aist         begsr
      *
     C                   select
     C                   when      d1aist = *blank
     C                   seton                                        36
     C                   if        error < maxmsg
     C                   add       1             error
     C                   eval      mgid(e) = 'HS09001'
     C                   endif
     C
     C                   when      d1aist <> 'A' and d1aist <> 'I'
     C                   seton                                        36
     C                   if        error < maxmsg
     C                   add       1             error
     C                   eval      mgid(e) = 'HS09105'
     C                   endif
     C                   endsl
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Validate Cognos report grouping
      *---------------------------------------------------------------
      *
     C     $gg0          begsr
      *
     C                   eval      d1gg0 = %triml(d1gg0)
      *
     C                   select
     C                   when      d1gg0 = *blank
     C                   seton                                        37
     C                   if        error < maxmsg
     C                   add       1             error
     C                   eval      mgid(e) = 'HS09001'
     C                   endif
     C                   endsl
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Validate alternate report grouping 1
      *---------------------------------------------------------------
      *
      * This was originally required and is, for now, optional.
      *
     C     $gg1          begsr
      *
     C                   eval      d1gg1 = %triml(d1gg1)
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Validate alternate report grouping 2
      *---------------------------------------------------------------
      *
      * This was originally required and is, for now, optional.
      *
     C     $gg2          begsr
      *
     C                   eval      d1gg2 = %triml(d1gg2)
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      *  F4 prompt on screen 1
      *---------------------------------------------------------------
      *
     C     $f4s1         begsr
      *
      *
     C                   select
      *
      * Active/inactive status
      *
     C                   when      fld = 'D1AIST'
     C                   call      'HP512'
     C                   parm      *blank        xxaist
     C                   seton                                        56
     C                   if        xxaist <> *blank
     C                   move      xxaist        d1aist
     C                   endif
     C                   other
      *
      * F4 not valid for this field...issue message
      *
     C                   if        error < maxmsg
     C                   add       1             error                                        013
     C                   movel     'HS09011'     mgid(e)
     C                   endif
      *
     C                   endsl
      *
     C                   endsr
      /EJECT
      *----------------------------------------------------------------
      * $upd1  - write/update/delete record in database file
      *----------------------------------------------------------------
      *
     C     $upd1         begsr
      *
      * If the user is deleting,
      *   perform deletion integrity checks
      *
     C                   if        action = delete                              If delete
     C                   exsr      $dltck
     C                   endif                                                  If delete
      *
      * If user action is revising or deleting, determine that the
      * record he is accessing still exists prior to attempting to
      * update.  Lock the record for update.
      *
     C                   if        error = 0 and                                If OK
     C                             (action = delete or action = revise)
      *
     C     key01         chain     hsl098a                            92
     C                   if        *in92 = *on                                  If not found
     C                   seton                                        62
     C                   if        error < maxmsg
     C                   add       1             error
     C                   eval      mgid(e) = 'HS09008'
     C                   endif
      *
     C                   endif                                                  If not found
     C                   endif                                                  If OK
      *
      * If there are no errors, delete, revise or create the record.
      *
     C                   if        error = 0                                    If no error
      *
     c                   select
     C                   when      action = delete
     C                   delete    sirec
     C                   other
      *
      * Move DSPF fields to database file fields
      *
     C                   move      d1sicomp      sisicomp
     C                   z-add     d1prime       siprime
      *
     C                   eval      sisicd = d1sicomp +
     C                                      (%editc(d1prime:'Z'))
      *
     C                   move      d1sids        sisids
     C                   z-add     d1aadglb      siaadglb
     C                   z-add     d1mgart       simgart
     C                   move      d1gg0         sigg0
     C                   move      d1gg1         sigg1
     C                   move      d1gg2         sigg2
     C                   move      d1aist        siaist
      *
     C                   if        action = create                              If create
     C                   write     sirec
     C                   else
     C                   update    sirec
     C                   endif                                                  If create
     C                   endsl
     C                   endif                                                  If no error
      *
      * Set next routine
      *
     C                   select
     C                   when      mode = create                                Wh creating
     C                   if        error = 0
     C                   add       1             error
     C                   eval      mgid(e) = 'HS09040'
     C                   eval      routne = set1
     c                   else
     C                   eval      routne = scrn1
     C                   endif
      *
     C                   when      error <> 0                                   Wh errors
     C                   eval      routne = scrn1
     C                   other
      *
     C                   eval      routne = exit
     C                   endsl
      *
     C                   endsr
      /EJECT
      *----------------------------------------------------------------
      * Deletion integrity checks
      *----------------------------------------------------------------
      *
     C     $dltck        begsr
      *
      * A sire line code cannot be deleted if it has been used in the weekly doses
      * file.
      *
     C     d1sicd        chain     hsl081c                            92
     C                   if        *in92 = *off and error < maxmsg
     C                   add       1             error
     C                   eval      mgid(e) = 'HS01156'
     C                   clear                   mgw
     C                   movea     d1sicd        mgw(1)
     C                   movea     mgw           mgdt(e)
     C                   endif
      *
      * A sire line code cannot be deleted if it has been used in the Group Overrides
      * file.
      *
     C     d1sicd        chain     hsl130a                            92
     C                   if        *in92 = *off and error < maxmsg
     C                   add       1             error
     C                   eval      mgid(e) = 'HS01226'
     C                   clear                   mgw
     C                   movea     d1sicd        mgw(1)
     C                   movea     mgw           mgdt(e)
     C                   endif
      *
      *
      * A sire line code cannot be deleted if it has been used in the Hog Group
      * file. (Note: It is called "genetic line" in this file.)
      *
     C     d1sicd        chain     hsl034v                            92
     C                   if        *in92 = *off and error < maxmsg
     C                   add       1             error
     C                   eval      mgid(e) = 'HS01227'
     C                   clear                   mgw
     C                   movea     d1sicd        mgw(1)
     C                   movea     mgw           mgdt(e)
     C                   endif
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * $wrmsg - write error messages
      *---------------------------------------------------------------
      *
     C     $wrmsg        begsr
      *
     C                   call      'UT80045J'                           43
     C                   parm                    mgid
     C                   parm                    mgdt
     C                   parm                    error
     C                   parm      sdpgm         pgm
     C                   parm      msgfil        msgfl
      *
      * If call to UT80045J failed, a message hardcoded in the DSPF
      * command line format will be set on. So, redisplay screen.
      *
     C                   if        *in43 = *on
     C                   eval      routne = set1
     C                   endif
      *
     C                   endsr
      *
      *----------------------------------------------------------------
      * $clmsg - clear messages
      *----------------------------------------------------------------
      *
     C     $clmsg        begsr
      *
     C                   call      'UT80045J'                           43
     C                   parm      *blank        mgi
     C                   parm      *blank        mgd
     C                   parm      *zero         error
     C                   parm      sdpgm         pgm
     C                   parm      msgfil        msgfl
      *
      * If call to UT80045J failed, a message hardcoded in the DSPF
      * command line format will be set on; so, redisplay screen.
      *
     C                   if        *in43 = *on
     C                   eval      routne = set1
     C                   endif
      *
     C                   endsr
      /EJECT
      *----------------------------------------------------------------
      * $exit - set on last record indicator and end job
      *----------------------------------------------------------------
      *
     C     $exit         begsr
     C                   seton                                        lr
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Parm Lists
      *
     C     *entry        plist
     C     d1mode        parm                    mode
     C                   parm                    d1sicomp
     C                   parm                    d1prime
     C                   parm                    rtncd
      *
     C                   eval      action = mode
      *
      * Key lists
      *
     C     key01         klist
     C                   kfld                    d1sicomp
     C                   kfld                    d1prime
      *
      *
      * Default Status to 'active' when Creating.
      *
     C                   if        mode = create
     C                   move      'A'           d1aist
     C                   endif
      *
      * The following standard code must be included to make the
      * standard error message handling program function properly.
      * This code sets and clears the program message queue.
      *
     C                   movel     '*'           msgpgm
     C                   exsr      $clmsg
      *
     C                   endsr
      /EJECT
** LETTERS OF THE ALPHABET
ABCDEFGHIJKLMNOPQRSTUVWXYZ
