/*                                                                  */
/*  SYNOPSIS -  JOB CONTROL PROMPT FOR PRINTER OVERRIDE INFORMATION */
/*              AND PRINT LISTING.                                  */
/*                                                                  */
/*  COMPANY   - SEABOARD CORP                                       */
/*  SYSTEM    - IDEAS/400 INVENTORY MANAGEMENT                      */
/*  PROGRAM   - PRINT TRANSFER BOL                                  */
/*  USER NAME - MARK ARENSBERG                                      */
/*  DATE      -  5/12/95                                            */
/*                                                                  */
/*      PARAMETERS RECEIVED:                                        */
/*                                                                  */
/*          &COMP       - PROGRAM DESCRIPTION                       */
/*          &RTYPE      - REPORT TYPE (IN OR CM)                    */
/*          &ITEM       - ITEM                                      */
/*          &WHSE       - TO WAREHOUSE                              */
/*          &DATE       - TRANS DATE                                */
/*          &BATCH      - TRANS BATCH NUMBER                        */
/*          &WHSE2      - TO WAREHOUSE                              */
/*          &PRTSTS     - PRINT STATUS                              */
/*                                                                  */
/*      PARAMETERS USED:                                            */
/*                                                                  */
/*          &OUTQ1      - OUTPUT QUEUE-TRANS. REGISTER              */
/*          &COPY1      - NUMBER OF COPIES -TRANS. REGISTER         */
/*          &COMP       - COMPANY NUMBER - SPECIFIC ONLY            */
/*                                                                  */
/********************************************************************/

 OPBLLCLP:   PGM        PARM(&COMP &RTYPE &ITEM &WHSE &DATE +
                             &BATCH &WHSE2 &PRTSTS &EXIT)

         DCL     &COMPA      *CHAR  (3)         /*  COMPANY #  ALPHA */
         DCL     &COMP       *DEC   (3 0)         /*  COMPANY #  */
         DCL     &COMPL      *DEC
         DCL     &DATE       *DEC   (7 0)         /*  DATE    #  */
         DCL     &DATEL      *DEC
         DCL     &BATCH      *DEC   (7 0)         /* TRANS BATCH # */
         DCL     &BATCHL     *DEC
         DCL     &MSGL       *DEC   (5 0)
         DCL     &OUTQ1      *CHAR  10           /*  OUTPUT QUEUE 1*/
         DCL     &COPY1      *DEC   (3 0)       /* COPIES - REGISTER */
         DCL     &COPY1L     *DEC
         DCL     &ITEM       *DEC   (7 0)
         DCL     &ITEML      *DEC
         DCL     &WHSE       *CHAR   3
         DCL     &WHSE2      *CHAR   3
         DCL     &HOLD       *CHAR  4            /*  HOLD          */
         DCL     &SAVE       *CHAR  4            /*  SAVE          */
         DCL     &NGHTYN     *CHAR  1            /*  NIGHT (Y/N)   */
         DCL     &NGHTSTS    *CHAR  4            /*  NIGHT Q STS   */
         DCL     &PRTDESC    *CHAR  25           /*  PRINT DESC    */
         DCL     &APPL       *CHAR  3            /*  APPLICATION   */
         DCL     &PRTSTS     *CHAR  1            /*  PRINT STATUS  */
         DCL     &RETURN     *CHAR  7
         DCL     &MSG        *CHAR    512
         DCL     &MSGID      *CHAR    7
         DCL     &RTYPE      *CHAR    1 /* G=REGULAR RUN, R=RERUN */
         DCL     &EXIT       *CHAR    1 /* BOL NOT PRINTED        */

/********************************************************************/
/*   LOAD COMPANY NUMBER NUMERIC FIELDS                             */
/********************************************************************/
             CHGVAR     VAR(&ITEML) VALUE(&ITEM)
             CHGVAR     VAR(&DATEL) VALUE(&DATE)
             CHGVAR     VAR(&BATCHL) VALUE(&BATCH)
             CHGVAR     VAR(&PRTDESC) VALUE('Print Manifest and B.O.L')
             CHGVAR     VAR(&HOLD) VALUE('*NO')
             CHGVAR     VAR(&SAVE) VALUE('*NO')
             CHGVAR     VAR(&COPY1) VALUE(1)
             CHGVAR     VAR(&NGHTYN) VALUE('N')
             CHGVAR     VAR(&APPL) VALUE('I/C')
PASS:

/********************************************************************/
/*   RETRIEVE DEFAULT OUTPUT QUEUE FROM USER PROFILE                */
/********************************************************************/

 JOBCONTROL: RTVUSRPRF  OUTQ(&OUTQ1)
             IF         COND(&OUTQ1 *EQ '*DEV') THEN(DO)
             RTVUSRPRF  PRTDEV(&OUTQ1)
             ENDDO


/********************************************************************/
/*   CALL JOB CONTROL PROGRAM TO PROMPT FOR PRINT INFORMATION.      */
/********************************************************************/

             IF         COND(&PRTSTS *EQ ' ') THEN(DO)
             CALL       PGM(PDG0PVR) PARM(&RETURN &PRTDESC +
                          &OUTQ1 &HOLD &SAVE &COPY1 &NGHTYN &NGHTSTS +
                          &APPL)
             ENDDO

/********************************************************************/
/*   CHECK FOR END OF JOB REQUEST.                                  */
/********************************************************************/

 ENDJOB:     IF         COND(&RETURN *EQ Y2U9999) THEN(DO)
             RCVMSG     MSGTYPE(*LAST) MSG(&MSG) MSGID(&MSGID)
             CHGVAR     VAR(&EXIT) VALUE('R')
             IF         (&MSGID = 'USR0810') THEN(DO)
             SNDPGMMSG  MSGID(CPF9898) +
                        MSGF(QCPFMSG) +
                        MSGDTA(&MSG) +
                        TOPGMQ(*PRV *) +
                        MSGTYPE(*COMP)
             ENDDO
             GOTO         CMDLBL(END)
             ENDDO

/********************************************************************/
/*   DETERMINE IF JOB IS SUBMITTED TO BATCH OR INTERACTIVE.         */
/********************************************************************/

             CHGVAR     VAR(&COMPL)   VALUE(&COMP) /* Change for +
                          passing */
             CHGVAR     VAR(&COPY1L) VALUE(&COPY1) /* Change for +
                          passing */

             SBMJOB     CMD(CALL PGM(OMBLPCLP) PARM(&COMPL &OUTQ1 +
                          &HOLD &SAVE &COPY1L &RTYPE +
                          &ITEML &BATCHL &WHSE +
                          &WHSE2 &DATEL)) JOB(TRANSFBOL)

             GOTO       CMDLBL(END)

/*----------------------RECEIVE MESSAGES----------------------------*/

 MSGTRAP:    RCVMSG     MSGTYPE(*EXCP) MSGDTA(&MSG) MSGDTALEN(&MSGL) +
                          MSGID(&MSGID)
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(END))

             SNDPGMMSG  MSGID(&MSGID) MSGF(QCPFMSG) MSGDTA(%SST(&MSG +
                          1 &MSGL)) MSGTYPE(*ESCAPE)
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(END))

 END:        ENDPGM

