      *****************  RPG PROGRAM HEADING  ************************
     h option(*SRCSTMT:*NODEBUGIO)
      ****************************************************************
      *
      * SYSTEM:      Hog Production System
      * PROGRAM:     HO3071
      * TITLE:       Workfile: Government Census Listing: Year End Farm Inventory NUR/GF
      * PROGRAMMER:  LeAnne Ramsey
      * CREATED:     05/27/08
      *
      *  FUNCTION:   This program builds the workfile for the Ending Inventory Listings.
      *              The workfile is built 3 separate times so that we can generate
      *              separate listings for:
      *                1) Boars only
      *                2) GDU only
      *                3) Not Boars/Not GDU
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 07/02/09  LeAnne Ramsey
      *           Recompile only. Added new field 'Continuous Flow Flag' to Hog Group file.
      *
      * 10/15/13  LeAnne Ramsey (E2831)
      *           Recompile only. Added field 'MTech Reference'.
      *
      * 05/13/16  Barb Gutierrez  (E5752)
      *           Changed to use logical HPLF035a instead of HPPF035.  HPPF035
      *           was changed to add company number to key prior to analyzing this
      *           program.  It was easier to just create a new logical keyed without
      *           company number than to change all the other programs that use HPPF035.
      *           This program doesn't need to run by company number.  All reports are
      *           broken out by the county the farm is in.
      *           No logic changes were made.
      /EJECT
      ****************************************************************
      * File specifications
      ****************************************************************
      *
     Fhsp018    if   e           k disk
      *  Farm site
      *
      *
     Fhsp034    if   e           k disk
      *  Hog Group
      *
      *
     Fhplf035a  if   e           k disk
      *  Datamart FIN: Open Group EOP Header
      *
      *
     Fhsp3071   uf a e           k disk
      *  Workfile: Year End Farm Inventory (NUR/GF)
      *
      /EJECT
      ****************************************************************
      * Definition specifications
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Named Constants
      *---------------------------------------------------------------
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      *
      *---------------------------------------------------------------------
      * Standalone fields
      *---------------------------------------------------------------------
      *
      * Control fields
      *
     D procfl          s              1    inz('Y')
      *
      *
      * Workfields
      *
     D wksacd          s                   like(fssacd)
     D wkcnty          s                   like(fscnty)
      *
      ****************************************************************
      * Data structures
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *---------------------------------------------------------------
      * Local data area.
      *---------------------------------------------------------------
      *
     Dlda             uds                  dtaara(*lda)
     D  ldyr                   1      4  0
     D  ldpe                   5      6  0
      *
     D  ldfdt                  9     16  0
     D  ldtdt                 17     24  0
      *
     D  ldgtcd                25     25
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      *-------------------------------------------------------------------------------------
      * MAINLINE
      *-------------------------------------------------------------------------------------
      *
      * Read all records for Period 12 of the selected Year from the Datamart Fin:
      * Open Group EOP Header file.
      *
     C     key01         setll     hplf035a
      *
     C                   dou       *in90 = *on                                  Main do
     C     key01         reade     hplf035a                               90
     C                   if        *in90 = *off                                 If not EOF
      *
      * Retrieve the Group Type to see if you want to process this Group:
      *
     C                   move      no            procfl
      *
     C     g2hgcd        chain     hsp034                             92
     C                   select
     C                   when      *in92 = *on
      *
     C                   when      ldgtcd = *blank and
     C                             hggtcd <> 'B' and hggtcd <> 'G'
     C                   move      yes           procfl
      *
     C                   when      hggtcd = ldgtcd
     C                   move      yes           procfl
     C                   endsl
      *
      * If procesing
     C                   if        procfl = yes                                 If process
      *
      * Retrieve the County and State of the Farm from the Farm Site Master.
      *
     C     hgfscd        chain     hsp018                             92
     C                   if        *in92 = *off
     C                   move      fscnty        wkcnty
     C                   move      fssacd        wksacd
     C                   else
     C                   move      *blank        wkcnty
     C                   move      *blank        wksacd
     C                   endif
      *
      * Write/update a Workfile record for this State/County/Phase/Farm.
      *
     C                   exsr      $wrt3071
      *
     C                   endif                                                  If process
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Main do
      *
      * EOF processing
      *
     C                   seton                                        lr
      /eject
      *---------------------------------------------------------------
      * Write/Update a workfile record
      *---------------------------------------------------------------
      *
     C     $wrt3071      begsr
      *
      * Retrieve the workfile record for this County/State/Phase/Farm.
      *
     C     key02         chain     hsp3071                            92
     C                   if        *in92 = *off
     C                   add       g2eihd        w1eihd
     C                   update    w1rec
     C                   else
      * write a record
     C                   z-add     g2eihd        w1eihd
     C                   z-add     hgfscd        w1fscd
     C                   move      hgppcd        w1ppcd
     C                   move      wksacd        w1sacd
     C                   move      wkcnty        w1cnty
     C                   write     w1rec
     C                   endif
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Key lists
      *
     C     key01         klist
     C                   kfld                    ldyr
     C                   kfld                    ldpe
      *
     C     key02         klist
     C                   kfld                    wkcnty
     C                   kfld                    wksacd
     C                   kfld                    hgppcd
     C                   kfld                    hgfscd
      *
     C                   endsr
      /eject
