/********************************************************************/
/*                                                                  */
/*  SYNOPSIS -  PRINT INVENTORY VALUE SUMMARY RPT                   */
/*                                                                  */
/*  SYSTEM    - INVENTORY MANAGEMENT SYSTEM                         */
/*  DATE      - 07/15/05                                            */
/*                                                                  */
/*      PARAMETERS:                                                 */
/*                                                                  */
/*     O    &RETURN     - RETURN CODE                               */
/*     B    &COPYN      -  NO. OF COPIES (PASSED)                   */
/*     B    &OUTQ       -  OUTPUT QUEUE                             */
/*                         IF THIS IS BLANK AT START, PROMPT        */
/*                         PGM IS CALLED; OTHERWISE, THE PRINT CL   */
/*                         IS SUBMITTED WITHOUT PROMPTING           */
/*     B    &HOLD       -  HOLD STATUS                              */
/*     B    &SAVE       -  SAVE STATUS                              */
/*     B    &DWNLD      -  DOWNLOAD FILE                            */
/*          &DOCUM      - DOCUMENT NAME                            */
/*          &DOCUMENT   - PARM DOCUMENT                             */
/*          &EMAIL,-2   -  EMAIL ADDR - CONCAT FOR 50 ALPHA         */
/********************************************************************/
/* RMC 01/12/06 ADDED COMPANY, WHSE, PRINT ZEROS PARMS AND SEL IN PRINT */
/* RMC 01/23/06 ADDED &DWNLD STS TO CREATE DOWNLOAD FILE               */
/* RMC 01/24/06 ADDED DOWNLOAD TO FTP SITE OF THE WKENDMMDD FILE       */
/*              AND POST PARM                                          */
/* RMC 06/26/06 ADDED &EMAIL ADDRESS TO EMAIL WORK FILE             */
/* DN  02/20/17 R9083  Added ISC Valuation Grouping & Grouping      */
/*                     description as input parm.                   */
/* DN  11/21/17 R12069 Delete PPB3CPP from QTEMP after job complete.*/
/*                     This job will be called twice since POST     */
/*                     process will now be done from both IVF Group */
/*                     G & S in the same job stream.                */
/********************************************************************/

 PPRBUPC:    PGM        PARM(&RETURN &OUTQ &HOLD &SAVE &COMPN &COPYN +
                          &WHSE &WEEKA &PRTZER &DWNLD &DOCUM &POST +
                          &EMAIL &EMAIL2 &IVG &IVGDSC)
/* PARMS                                                                      */
        DCL    &RETURN     *CHAR   (7)           /* Return Code               */
        DCL    &COPYN      *DEC                  /* No. of Copies             */
        DCL    &COMPN      *DEC                  /* WHSE COMPANY              */
        DCL    &WHSE       *CHAR (3)             /* WHSE                      */
        DCL    &OUTQ       *CHAR   (10)          /* Output Queue              */
        DCL    &HOLD       *CHAR   (4)           /* Hold Status               */
        DCL    &SAVE       *CHAR   (4)           /* Save Status               */
        DCL    &WEEKA      *CHAR   (8)           /* WEEK ENDING CHAR          */
        DCL    &WEEKN      *DEC    (8 0)         /* WEEK ENDING DEC           */
        DCL    &WEEKMD     *CHAR   (4)           /* WEEK ENDING MMDD          */
        DCL    &PRTZER     *CHAR   (1)           /* PRINT ZERO BALANCES       */
        DCL    &DWNLD      *CHAR   (1)           /* DOWNLOAD FILE TO FTP      */
        DCL    &POST       *CHAR   (1)           /* POST Y OR NO  */
        DCL    &DOCUMENT  *CHAR   (10)
        DCL    &PCFILE    *CHAR   (32)
        DCL    &DOCUM     *CHAR   (08)
        DCL    &EMAIL      *CHAR   (25)          /* EMAIL                     */
        DCL    &EMAIL2     *CHAR   (25)          /* EMAIL                     */
        DCL    &EMFILE     *CHAR   (50)          /* EMAIL                     */
        DCL    &IVG        *CHAR   (1)           /* Inv Valuation Group       */
        DCL    &IVGDSC     *CHAR   (30)          /* Inv Valuation Group Desc */

/* INTERNAL FIELDS                                                            */
        DCL    &COPY       *DEC    (2 0)         /* Number of copies          */
        DCL    &COMP       *DEC    (3 0)         /* COMPANY                   */
             DCL        VAR(&CRTWF) TYPE(*CHAR) LEN(1) VALUE('N')

/* ERROR MESSAGE FIELDS                                                       */
        DCL    &MSG        *CHAR   (512)         /* message                   */
        DCL    &MSGID      *CHAR   (7)           /* message id                */
        DCL    &MSGL       *DEC    (5 0)
        DCL    &COMMAND    *CHAR   (512)
        DCL     &DATA     *CHAR   10
        DCL     &DATAH    *CHAR   10

/********************************************************************/
/*   INITIALIZE VARIABLES                                           */
/********************************************************************/

             CHGVAR     VAR(&RETURN) VALUE('       ')
             CHGVAR     VAR(&COPY) VALUE(&COPYN)
             CHGVAR     VAR(&COMP) VALUE(&COMPN)
             CHGVAR     VAR(&WEEKN) VALUE(&WEEKA)
/********************************************************************/
/*   MONITOR FOR ALL MESSAGES AT PROGRAM LEVEL                      */
/********************************************************************/

             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(MSGTRAP))

/********************************************************************/
/*   CREATE QTEMP DOWNLOAD FILE                                    */
/********************************************************************/
BUILD:       CALL      PGM(CAQXXFR) PARM(' ' &DATA &DATAH)
             CRTDUPOBJ  OBJ(PPB3CPP) FROMLIB(&DATA) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)
             CRTDUPOBJ  OBJ(PPB3CPL0) FROMLIB(&DATA) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)
             CRTDUPOBJ  OBJ(PPB3CPL1) FROMLIB(&DATA) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)
/********************************************************************/
/*   CALL PRINT                                                   */
/********************************************************************/

             SBDPRTOVR  CMDVAR(&COMMAND) FILE(PPRAPFR$) +
                          OUTQ(&OUTQ) COPIES(&COPY) HOLD(&HOLD) +
                          SAVE(&SAVE)
             CALL       PGM(QCMDEXC) PARM(&COMMAND 512)
   /*------------------------------------------------*/
   /* CREATE WORKFILE IF DOWNLOAD OR EMAIL SELECTED                       */
   /*------------------------------------------------*/
             IF         COND(&DWNLD = 'Y') THEN(CHGVAR &CRTWF VALUE('Y'))
             IF         COND(&EMAIL > '   ') THEN(CHGVAR &CRTWF VALUE('Y'))
   /*------------------------------------------------*/

    /* R9083-Added Inv Valuation Grouping as parm. */
 /* PRINT: CALL      PGM(PPRAPFR) PARM(' ' &WEEKN &COMP &WHSE &PRTZER &CRTWF)*/
 PRINT:      CALL       PGM(PPRAPFR) PARM(' ' &IVG &WEEKN &COMP +
                          &WHSE &PRTZER &CRTWF &IVGDSC)

   /*------------------------------------------------*/
   /* EXECUTE SEQUEL TO CREATE THE OUTFILE TO FTP */
   /*-----------------------------------------------*/
 DOWNLD:     IF         COND(&DWNLD = 'Y') THEN(DO)
  CHGVAR VAR(&PCFILE)   VALUE('ftp:download.pork/' *cat &docum *cat '.xls')
             EXECUTE    VIEW(PRKPLIB/PPB3CPP) MBROPT(*REPLACE) +
                          NBRRCDS(*ALL) KEYFLDCNT(*ALL) +
                          ALWNULL(*NO) PCFMT(*XLS) REPLACE(*YES) +
                          RECIPIENT(&PCFILE) OPTIMIZE(*TOTAL) +
                          ALWCPY(*YES) MSG(*YES) UNIQUEKEY(*NONE) +
                          IGNDECERR(*NO) DTSTYLE(*FIELD '/' *FIELD +
                          ':') TEXT('Inventory Value Summary for +
                          TOM DYE')
      ENDDO
   /*------------------------------------------------*/
   /* EMAIL WORKFILE IF EMAIL ADDRESS PASSED IN PAR    */
   /*-----------------------------------------------*/
             CHGVAR     VAR(&EMFILE) VALUE(&EMAIL *CAT &EMAIL2)
             IF         COND(&EMFILE *NE '   ') THEN(DO)
             EXECUTE    VIEW(PRKPLIB/PPB3CPP) MBROPT(*REPLACE) +
                          NBRRCDS(*ALL) KEYFLDCNT(*ALL) +
                          ALWNULL(*NO) PCFMT(*XLS) REPLACE(*YES) +
                          RECIPIENT(&EMFILE) OPTIMIZE(*TOTAL) +
                          ALWCPY(*YES) MSG(*YES) UNIQUEKEY(*NONE) +
                          IGNDECERR(*NO) DTSTYLE(*FIELD '/' *FIELD +
                          ':') TEXT('Inventory Value Summary')

             ENDDO

/********************************************************************/
/* R12069 - Delete PPB3CPP File From QTEMP                          */
/********************************************************************/
             DLTF       FILE(QTEMP/PPB3CP*)

/*------------------------------------------------------------------*/
             GOTO       CMDLBL(END)
/*----------------------RECEIVE MESSAGES----------------------------*/

 MSGTRAP:    RCVMSG     MSGTYPE(*EXCP) MSGDTA(&MSG) MSGDTALEN(&MSGL) +
                          MSGID(&MSGID)
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(END))

             SNDPGMMSG  MSGID(&MSGID) MSGF(QCPFMSG) MSGDTA(%SST(&MSG +
                          1 &MSGL)) MSGTYPE(*ESCAPE)
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(END))

 END:        ENDPGM

