      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Triumph Foods
      * PROGRAM:     TF224 - Eopfees: Create Invoice Charge--Freight Variance
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     04/12/05
      *
      * Function:    This program writes a single Invoice Charge record for Freight
      *              Variance when:
      *                1) there are no errors from the edit
      *                2) it is a Final run
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 03/27/06  LeAnne Fedor
      *           Recompile only. New field "Product Code" added to Freight Variance file.
      *
      * 04/16/07  LeAnne Ramsey
      *           Recompile only. Field "Exclude from Mix Flag' was renamed to
      *           Mix Flag in the Freight Variance file.
      /eject
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Ftfp005    if   e           k disk
      *    Charge codes
      *
      *
     Ftfp017    if   e           k disk
      *    Freight variance
      *
      *
     Ftfp021    o    e           k disk
      *  Invoice charges
      *
      /eject
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Workfields
      *
     D wktfvam         s                   like(fvtfvam)
      *
      *
      * Parms
      *
     D xxicsn          s                   like(icicsn)
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *---------------------------------------------------------------
      * Standard database file information data structure
      *---------------------------------------------------------------
      *    externally defined as UTDBGR (record format: FDBCKD)
     D dbfeed        e ds                  extname(utdbfr)
      *
      *
      *---------------------------------------------------------------
      *  Local data area
      *---------------------------------------------------------------
      *
     d lda            uds                  dtaara(*lda)
     D  ldpfcd                 1      1
     D  ldcyr                  2      5  0
     D  ldcpe                  6      7  0
      *
     D  ldcbdt                 8     15  0
     D  ldcbmdy               16     21  0
     D  ldcbsyn               22     28  0
      *
     D  ldcedt                29     36  0
     D  ldcemdy               37     42  0
     D  ldcesyn               43     49  0
      *
     D  ldledt                50     57  0
     D  ldlemdy               58     63  0
     D  ldlesyn               64     70  0
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * Mainline
      ****************************************************************
      *
      * Read all the Freight Variance records for this period accumulating
      * the Triumph Freight Variance Amount.
      *
     C     ldcedt        setll     tfp017
      *
     C                   dou       *in91 = *on                                  Do Sales
     C     ldcedt        reade     tfp017                                 91
     C                   if        *in91 = *off                                 If not eof
     C                   add       fvtfvam       wktfvam
     C                   endif                                                  If not eof
     C                   enddo                                                  Do Sales
      *
      * If you had any Triumph Freight Variance dollars, write an
      * Invoice Charge record.
      *
     C                   if        wktfvam <> 0
     C                   exsr      $wrt021
     C                   endif
      *
      * EOF processing
      *
     C                   seton                                        lr
      /eject
      *---------------------------------------------------------------
      * Write Invoice Charge record
      *---------------------------------------------------------------
      *
     C     $wrt021       begsr
      *
      * Initialize Invoice Charge record.
      *
     C                   clear                   icrec
      *
      * Populate date field with the period end date from the LDA
      *
     C                   z-add     ldcedt        icicdt
      *
      * Set up the Invoice Charge Amount with your Triumph Freight Variance Amount.
      * Flip the sign on the amount.
      *
     C     -1            mult      wktfvam       icicam
      *
      * Set the System-generated Flag to Yes
      * Set the Invoice Status to Open
      * Set the Units field to 1
      *
     C                   move      yes           icsysfl
     C                   move      'O'           icinstcd
     C                   z-add     1             icunit
      *
      *
      * Hardcode the Charge Code value and then retrive Charge Code info.
      *
     C                   move      'FGTVAR    '  iccccd
     C     iccccd        chain     tfp005                             92
     C                   if        *in92 = *off                                 If valid code
     C                   move      ccinfl        icinfl
     C                   move      ccsmcd        icsmcd
     C                   endif                                                  If valid code
      *
      * Get the next system-assigned 'Invoice Charge Number'.
      *
     C                   call      'TF807'
     C     icicsn        parm      0             xxicsn
      *
      *
      * Write Invoice Charge record.
      *
     C                   write     icrec
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      *
     C                   endsr
      /eject
