     A*%%TS  SD  20040727  181638  ISLRAMS     REL-V5R2M0  5722-WDS
     A*****************************************************************
     A*
     A*  SYSTEM:        HOG PRODUCTION
     A*  DISPLAY FILE:  HPD5036 - SELECT ITEM TYPE
     A*  CREATED BY:    LEANNE FEDOR
     A*  DATE:          01/29/04
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      PRINT
     A                                      CF12(12 'PREVIOUS')
     A N62                                  CF04(04 'PROMPT')
     A N82                                  ROLLUP(25 'PAGE FORWARD')
     A*---------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP5036S1                  SFL
     A*%%TS  SD  20040129  172055  ISLRAMS     REL-V5R2M0  5722-WDS
     A            HDRRN1    R        H      REFFLD(@@RRN)
     A            D1ITTYCD  R        O  8  2REFFLD(@@ITTYCD)
     A            D1ITTYDS  R        O  8 10REFFLD(@@ITTYDS)
     A            D1AIST    R        O  8 48REFFLD(@@AIST)
     A*
     A*---------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP5036C1                  SFLCTL(HP5036S1)
     A*%%TS  SD  20040129  172055  ISLRAMS     REL-V5R2M0  5722-WDS
     A                                      SFLSIZ(0100)
     A                                      SFLPAG(0009)
     A                                      WINDOW(HP5036EC)
     A                                      SETOF(52)
     A                                      RTNCSRLOC(&RCD &FLD)
     A                                      OVERLAY
     A                                      SFLCSRRRN(&HDRRN)
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A            HDPAG1    R        H      REFFLD(@@PAG)
     A                                      SFLRCDNBR
     A            RCD           10A  H
     A            FLD           10A  H
     A            HDRRN     R        H      REFFLD(@@RRN)
     A            SDPGM     R        O  1  1REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 21'SELECT ITEM TYPE'
     A                                      DSPATR(HI)
     A            C1DESC    R        O  2 13REFFLD(@@DS30)
     A                                      DSPATR(HI)
     A                                  4  2'Position Cursor, Press Enter'
     A N62                              4 48'?'
     A                                      DSPATR(HI)
     A                                  5 41'Active/Inactive'
     A                                      DSPATR(HI)
     A                                  6  2'Code'
     A                                      DSPATR(HI)
     A                                  6 10'Description'
     A                                      DSPATR(HI)
     A                                  6 46'Status'
     A                                      DSPATR(HI)
     A            C1ITTYCD  R        B  7  2REFFLD(@@ITTYCD)
     A                                      DSPATR(HI)
     A            C1ITTYDS  R        B  7 10REFFLD(@@ITTYDS)
     A                                      CHGINPDFT(UL LC FE)
     A                                      DSPATR(HI)
     A N62        C1AIST    R        B  7 48REFFLD(@@AIST)
     A                                      DSPATR(HI)
     A  52                                  DSPATR(PC)
     A*---------------------------------------------------------------
     A* COMMAND LINE LINE
     A*---------------------------------------------------------------
     A          R HP5036K1
     A*%%TS  SD  20040311  082555  ISLRAMS     REL-V5R2M0  5722-WDS
     A                                      WINDOW(HP5036EC)
     A                                      OVERLAY
     A  43                             17  1'Message program failed. Call IS.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 18  1'F12=Previous'
     A N62                             18 15'F4=Prompt'
      *
     A*-------------------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
     A*-------------------------------------------------------------------------
     A          R HP5036ES                  SFL SFLMSGRCD(19)
      *
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
     A*
     A*-------------------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*-------------------------------------------------------------------------
     A*
     A          R HP5036EC                  SFLCTL(HP5036ES)
     A*%%TS  SD  19980327  105628  ISLRAMS     REL-V3R7M0  5716-PW1
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A                                      WINDOW(*DFT 20 55)
     A                                      WDWBORDER((*DSPATR RI) (*CHAR '    -
     A                                          '))
     A            MSGPGM                    SFLPGMQ(10)
     A*
     A*---------------------------------------------------------------
     A* ASSUME FORMAT -- REQUIRED FOR WINDOWS
     A*---------------------------------------------------------------
     A          R ASSUME
     A*%%TS  SD  20011025  160559  ISLRAMS     REL-V4R4M0  5769-PW1
     A                                      ASSUME
     A                                      OVERLAY
     A                                 24 79' '
