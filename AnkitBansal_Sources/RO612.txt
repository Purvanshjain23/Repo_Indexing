      *****************  RPG PROGRAM HEADING  ************************
     h option(*SRCSTMT:*NODEBUGIO)
      ****************************************************************
      *
      * ENVIRONMENT: Pork Division
      * SYSTEM:      Resource Optimization
      * PROGRAM:     RO612 - ByProduct Values Report
      * PROGRAMMER:  LeAnne Ramsey
      * CREATED:     01/19/07
      *
      * FUNCTION:    Print records from the ByProduct Detail & Values summary files.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 03/28/07  LeAnne Ramsey
      *           Replaced the Multiplier % column with Sales Yield %.
      *
      * 04/11/07  LeAnne Ramsey
      *           Replaced the "sales" values with "produced" values.
      *
      * 10/04/07  LeAnne Ramsey
      *           Recompile only. New field 'Produced Start Weight' added to
      *           Credit Item Group Meat Cost file.
      *
      * 12/21/07  Alice Brownfield
      *           Changes when we combined the Credit item Meat Cost files with
      *           the Byproduct MIX processing files.
      *
      * 06/10/08  LeAnne Ramsey
      *           Removed 2 columns (Packaging Cost Amount, Labor Cost Amount) to make
      *           room for 2 new columns (Ingredient Cost/LB and Other Cost/LB)..
      *
      * 11/21/08  LeAnne Ramsey
      *           As part of synchronizing the LDAs between the TFS Margin Adjustment Close
      *           and the Meat Costing, we changed the LDA positions.
      *
      * 04/02/09  LeAnne Ramsey
      *           Recompile only. Field 'Producing Company' added to Byproduct Detail.
      * 02/01/24  Jagdish Mistry (CM-S009004),Service request number-3009004
      *           Damon called to say that we need to take items which has include in mix
      *           flag as N out of Meat Cost calculations.Same will be excluded on report.
      /eject
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fombyrel1  if   e           k disk
      *    Item Default
      *
      *
     Frol101a   if   e           k disk
      *  ByProduct Values Summary
      *
      *
     Frol122e   if   e           k disk    rename(ydrec:ydrece) prefix(p2)
      *  ByProduct Detail
      *
      *
     Frol122d   if   e           k disk    rename(ydrec:ydrecd) prefix(p1)
      *  ByProduct Detail
      *
      *
     Fprint198  o    f  198        printer oflind(*inof)
      *
     ‚* Project-S009004-START
     FPPAOREL1  IF   E           K Disk
     ‚* Project-S009004-END
      /eject
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Standard fields
      *
     d rtime           s              6  0
     D dash            s            198    inz(*all'-')
      *
      *
      * Control fields
      *
     D prtfl           s              1    inz('N')
     D procfl          s              1    inz('Y')
     D sv1stpos        s              1
      *
      *
      * Print fields
      *
     d h1pfds          s             11
     d h1dsds          s              7
      *
     D l1totco         s                   like(p2ydpco)
      *
     D l2itds          s             25
     D l2totco         s                   like(p2ydpco)
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *---------------------------------------------------------------
      *  Local data area
      *---------------------------------------------------------------
      *
     d lda            uds                  dtaara(*lda)
     D  ldpfcd                 1      1
      *
     D  ldwedt                29     36  0
     D  ldwemdy               44     49  0
      *
     D  ldcono                82     84  0
      *
     D  lddsfl               135    135
      *
     D  lditcd               222    228  0
     D  lditds               229    258
      *
     D  ldcgcd               295    298
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * Mainline
      ****************************************************************
      *
      * Print headings
     C                   exsr      $h1hdr

      * Read all ByProduct Values summary records for the selected Company/Week.
      *
     C     key01         setll     rol101a
      *
     C                   dou       *in90 = *on                                  Main do
     C     key01         reade     rol101a                                90
     C                   if        *in90 = *off                                 If not EOF
      *
     C                   move      yes           procfl
      *
      * If you are printing the Detail version, determine if this Meat Cost
      * Group/Subgroup should be processed.
      *
     C                   if        lddsfl = 'D' and
     C                             (ldcgcd <> *blank or lditcd <> 0)
     C                   exsr      $check
     C                   endif
      *
     C                   if        procfl = yes                                 If process
      * Summary line
     C                   exsr      $l1dtl
      *
      * Print detail lines (items) for this Group/Subgroup
      *
     C                   if        lddsfl = 'D'
     C                   exsr      $detail
     C                   endif
      *
     C                   endif                                                  If process
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Main do
      *
      * EOF processing
      *
     C                   if        prtfl = no
     C                   except    nodata
     C                   endif
      *
     C                   seton                                        lr
      /eject
      *----------------------------------------------------------------------------------------
      * Determine if you should process this Meat Cost Group
      *----------------------------------------------------------------------------------------
      *
     C     $check        begsr
      *
     C                   select
     C                   when      ldcgcd <> *blank and ldcgcd <> gmcgcd
     C                   move      no            procfl
      *
     C                   when      lditcd <> 0
     C     key02         chain     rol122d                            92
     C                   if        *in92 = *on
     C                   move      no            procfl
     C                   endif
     C                   endsl
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Print Group/Subgroup Summary line
      *---------------------------------------------------------------
      *
     C     $l1dtl        begsr
      *
     C                   if        *inof = *on
     C                   exsr      $h1hdr
     C                   endif
      *
      * Total Cost/LB
     C                   eval      l1totco = gmpco + gmlco + gmico + gmoco
      *
      * Summary version
     C                   if        lddsfl = 'S'                                 If summary
      *
     C                   if        %subst(gmcgcd:1:1) <> sv1stpos
     C                   except    blank
     C                   eval      sv1stpos = %subst(gmcgcd:1:1)
     C                   endif
     C                   except    l1dtl
     C                   except    blank
     C                   endif                                                  If summary
      *
      * Detail version
     C                   if        lddsfl = 'D'                                 If detail
     C                   except    blank
     C                   except    blank
     C                   except    l1dtl
     C                   except    l1dtl
     C                   endif                                                  If detail
      *
     C                   move      yes           prtfl
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Retrieve all FG Detail records for this Group/Subgroup
      *---------------------------------------------------------------
      *
     C     $detail       begsr
      *
     C     key03         setll     rol122e
      *
     C                   dou       *in91 = *on                                  Do detail
     C     key03         reade     rol122e                                91
     C                   if        *in91 = *off and p2ydittycd = 'FG'             If not eof
     ‚* Project-S009004-START
     ‚* Skip Co-owned Item from Meat costs calculations
     C     Key04         Chain     PPAOREL1                           70
     C                   If        %Found(PPAOREL1) And AOSWSX ='N'
     C                   Iter
     C                   EndIf
     ‚* Project-S009004-END
      *
      * Retrieve Item Description
      *
     C     p2yditcd      chain     ombyrel1                           92
     C                   if        *in92 = *off
     C                   eval      l2itds  = byavna
     C                   else
     C                   eval      l2itds = 'Unknown'
     C                   endif
      *
     C                   eval      l2totco = p2ydpco + p2ydlco +
     C                                       p2ydico + p2ydoco
      *
     C                   if        *inof = *on
     C                   exsr      $l1dtl
     C                   endif
     C                   except    l2dtl
      *
     C                   endif                                                  If not eof
     C                   enddo                                                  Do detail
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Print report headings
      *---------------------------------------------------------------
      *
     C     $h1hdr        begsr
      *
     C                   except    h1hdr
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Key lists
      *
     C     key01         klist
     C                   kfld                    ldcono
     C                   kfld                    ldwedt
      *
     C     key02         klist
     C                   kfld                    ldwedt
     C                   kfld                    gmcgcd
     C                   kfld                    gmsgcd
     C                   kfld                    lditcd
      *
     C     key03         klist
     C                   kfld                    ldwedt
     C                   kfld                    gmcgcd
     C                   kfld                    gmsgcd
     ‚* Project-S009004-START
     C     Key04         KList
     C                   KFld                    P2YDTFCLCD
     C                   KFld                    P2YDISTYCD
     C                   KFld                    P2YDISGRCD
     C                   KFld                    P2YDISCLCD
     ‚* Project-S009004-END
      *
     C                   time                    rtime
      *
      * Set up report headings.
      *
      * This report is generated on-demand and also from the submitted
      * Weekly Meat Costing function.
      *
     C                   select
     C                   when      ldpfcd = 'D'
     C                   eval      h1pfds = ' On-Demand '
      *
     C                   when      ldpfcd = 'F'
     C                   eval      h1pfds = '   Final   '
      *
     C                   when      ldpfcd = 'P'
     C                   eval      h1pfds = 'Preliminary'
     C                   endsl
      *
      * Detail/Summary
      *
     C                   select
     C                   when      lddsfl = 'D'
     C                   eval      h1dsds = 'Detail'
     C                   seton                                        89
      *
     C                   when      lddsfl = 'S'
     C                   eval      h1dsds = 'Summary'
     C                   endsl
      *
      * Meat Cost Group Code
      *
     C                   if        ldcgcd = *blank
     C                   seton                                        87
     C                   endif
      *
      * Item
     C                   if        lditcd = 0
     C                   seton                                        88
     C                   endif
      *
     C                   endsr
      /eject
      *-------------------------------------------------------------
      * Report lines
      *-------------------------------------------------------------
      *
     Oprint198  e            h1hdr          1 01
     O                       sdpgm               10
     O                                          100 'Resource Optimization '
     O                                          106 'System'
     O                                          188 'DATE'
     O                       udate         y    198
      *
      *
     O          e            h1hdr          1
     O                       sdusr               10
     O                                          105 'ByProduct Values Report'
     O                                          188 'TIME'
     O                       rtime              198 '  :  :  '
      *
     O          e            h1hdr          1
     O                       h1pfds              97
     O                                          188 'PAGE'
     O                       page          z    198
      *
      *
     O          e            h1hdr          1
     O                                           23 'Company:'
     O                       ldcono        z     28
     O                       h1dsds              95
      *
     O          e            h1hdr          1
     O                                           23 'Week-ending date:'
     O                       ldwemdy             33 '  /  /  '
      *
     O          e            h1hdr          1
     O                                           23 'Meat Cost group code:'
     O              n87      ldcgcd              29
     O               87                          28 'All'
      *
     O          e            h1hdr          2
     O                                           23 'Item code:'
     O              n88      lditcd        z     32
     O              n88      lditds              65
     O               88                          28 'All'
      *
      *
      *-------------------------------------------------------------
      * Column headings
      *-------------------------------------------------------------
      *
     O          e            h1hdr          1
     O                                           12 'Meat Cost'
      *
     O          e            h1hdr          1
     O                                           12 'Sub-'
     O                                           60 'Produced'
     O                                           86 'Net'
     O               89                         195 'Product'
      *
     O          e            h1hdr          1
     O                                            6 'Group'
     O                                           13 'Group'
     O               89                          21 'Item'
     O                                           41 'Produced'
     O                                           60 'Value'
     O                                           73 'Produced'
     O                                           86 'Produced'
     O                                           99 'Sales Yld'
     O                                          111 'Packaging'
     O                                          123 'Labor'
     O                                          135 'Ingredient'
      *
     O                                          146 'Other'
     O                                          157 'Total'
     O               89                         198 '--Exception--'
      *
     O          e            h1hdr          1
     O                                            5 'Code'
     O                                           12 'Code'
     O               89                          21 'Code'
     O                                           41 'Pounds'
     O                                           60 'Amount'
     O                                           73 'Price/LB'
     O                                           86 'Price/LB'
     O                                           99 'Percent'
     O                                          111 'Cost/LB'
     O                                          123 'Cost/LB'
     O                                          135 'Cost/LB'
      *
     O                                          146 'Cost/LB'
     O                                          157 'Cost/LB'
     O               89                         175 'Item Description'
     O               89                         190 'Flag'
     O               89                         198 'Number'
      *
     O          e            h1hdr          0
     O                       dash               198
      *
      *-------------------------------------------------------------
      * Summary line for Group/Subgroup
      *-------------------------------------------------------------
      *
     O          e            l1dtl       0  0
     O                       gmcgcd               5
     O                       gmsgcd              12
     O                       gmpulb        k     42
     O                       gmpuam        k     61
     O                       gmpupr        k     74
     O                       gmnpupr       k     87
     O                       gmsypc        k    100
     O                       gmpco         k    112
     O                       gmlco         k    124
     O                       gmico         k    136
      *
     O                       gmoco         k    147
     O                       l1totco       k    158
      *
      *-------------------------------------------------------------
      * Detail line for Group/Subgroup
      *-------------------------------------------------------------
      *
     O          e            l2dtl       1  0
     O                       p2yditcd      m     22
     O                       p2ydapulb     k     42
     O                       p2ydapuam     k     61
     O                       p2ydpupr      k     74
     O                       p2ydnpupr     k     87
     O                       p2ydsypc      k    100
      *
     O                       p2ydpco       k    112
     O                       p2ydlco       k    124
     O                       p2ydico       k    136
      *
     O                       p2ydoco       k    147
     O                       l2totco       k    158
      *
     O                       l2itds             184
     O                       p2ydexfl           189
     O                       p2ydpesn      z    198
      /eject
      *-------------------------------------------------------------
      * Blank line
      *-------------------------------------------------------------
      *
     O          e            blank       1
      /eject
      *-------------------------------------------------------------
      * No data line
      *-------------------------------------------------------------
      *
     O          e            nodata      2
     O                                           19 'No data meets your '
     O                                           38 'selection criteria.'
      /eject
