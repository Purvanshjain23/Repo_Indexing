      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Hog Production System
      * PROGRAM:     HP3714-Listing of Farms with the Same Item on Multiple Active Budgets
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     03/01/04
      *
      * Function:    This function:
      *                1) Reads all Active Farm Budget Detail records and
      *                2) Prints any/all items that are on 2 budgets for the same Farm
      *
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 10/13/04  LeAnne Fedor
      *           Recompile only. Cost Per Unit Amount was changed from
      *           9,2 to 9,4.
      *
      /eject
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fhsj189j   if   e           k disk
      *    Farm budget detail + Farm budget header
      *
      *
     Fhsp3714   uf a e           k disk
      *    Workfile: Duplicate Items
      *
      *
     Fqprint    o    f   80        printer oflind(*inof)
      *
      /eject
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      *----------------------------------------------------------------
      * CONSTANTS
      *----------------------------------------------------------------
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      *----------------------------------------------------------------
      * STANDALONE FIELDS
      *----------------------------------------------------------------
      *
      * Standard fields
      *
     D dash            s             80    inz(*all'-')
     d rtime           s              6  0
      *
      *
      * Control fields
      *
     d first           s              1    inz('Y')
     D svfscd          s                   like(jhfscd)
     D svbgit          s                   like(jdbgit)
      *
      *
      * Print fields
      *
     D l1fmdy          s              6  0
     D l1tmdy          s              6  0
      *
      *
      * Workfields for date manipulation
      *
     D wkisodate       s               D   datfmt(*iso)
      *
      *
      * Parm fields
      *
     d xxprtfl         s              1
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * Mainline
      ****************************************************************
      *
      * Read each record from the join file of Farm Budget Detail/Farm Budget Header.
      * (The file selects only Active budgets.)
      *
     C     *loval        setll     hsj189j
      *
     C                   dou       *in90 = *on                                  Main do
     C                   read      hsj189j                                90
     C                   if        *in90 = *off                                 If not EOF
      *
      * If duplicate, write to workfile for later processing/printing.
      *
     C                   if        jdfscd = svfscd and
     C                             jdbgit = svbgit
     C                   exsr      $wrt3714
     C                   endif
      *
      * Always save fields
      *
     C                   z-add     jhfscd        svfscd
     C                   move      jdbgit        svbgit
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Main do
      *
      *----------------
      * EOF processing
      *----------------
      *
      * Process all records in the workfile---if you had any.
      *
     C                   if        xxprtfl = yes
     C                   exsr      $read3714
     C                   else
     C                   except    h1hdr
     C                   except    nodata
     C                   endif
      *
     C                   seton                                        lr
      /eject
      *---------------------------------------------------------------
      * Write a record to the workfile
      *---------------------------------------------------------------
      *
     C     $wrt3714      begsr
      *
     C     key01         chain     hsp3714                            92
     C                   if        *in92 = *on                                  If not there
     C                   z-add     svfscd        w1fscd
     C                   move      svbgit        w1bgit
     C                   write     w1rec
     C                   move      yes           xxprtfl
     C                   endif                                                  If not there
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Process workfile records
      *---------------------------------------------------------------
      *
     C     $read3714     begsr
      *
     C     *loval        setll     hsp3714
      *
     C                   dou       *in91 = *on                                  Do loop
     C                   read      hsp3714                                91
     C                   if        *in91 = *off                                 If not EOF
     C                   exsr      $print
     C                   endif                                                  If not EOF
     C                   enddo                                                  Do loop
      *
     C                   endsr
      /eject
      *----------------------------------------------------------------------------------
      * Print a line for each occurrence of farm/item from the Farm Budget Detail file
      *----------------------------------------------------------------------------------
      *
     C     $print        begsr
      *
     C     key02         setll     hsj189j
      *
     C                   dou       *in93 = *on                                  Do print loop
     C     key02         reade     hsj189j                                93
     C                   if        *in93 = *off                                 If not EOF
      *
      * Flip From Date from CCYYMMDD to MMDDYY for display
      *
     C     *iso          test(d)                 jhbfdt                 92
     C                   if        *in92 = *off                                 If OK date
     C                   move      jhbfdt        wkisodate
     C     *mdy          move      wkisodate     l1fmdy
     C                   endif                                                  If OK date
      *
      * Flip To Date from CCYYMMDD to MMDDYY for display
      *
     C     *iso          test(d)                 jhbtdt                 92
     C                   if        *in92 = *off                                 If OK date
     C                   move      jhbtdt        wkisodate
     c     *mdy          move      wkisodate     l1tmdy
     C                   endif                                                  If OK date
      *
      * Print the Headings
      *
     C                   select
     C                   when      *inof = *on
     C                   except    h1hdr
      *
     C                   when      first = yes
     C                   except    h1hdr
     C                   move      no            first
     C                   endsl
      *
      * Print detail line
      *
     C                   except    l1dtl
     C                   endif                                                  If not EOF
     C                   enddo                                                  Do print loop
      *
      * Print a blank line
     C                   except    blank
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Parm lists
      *
     C     *entry        plist
     C                   parm                    xxprtfl
      *
     C     key01         klist
     C                   kfld                    jdfscd
     C                   kfld                    jdbgit
      *
      *
     C     key02         klist
     C                   kfld                    w1fscd
     C                   kfld                    w1bgit
      *
      * Set up heading info.
      *
     C                   time                    rtime
      *
     C                   endsr
      /eject
      *-------------------------------------------------------------
      * Report heading lines
      *-------------------------------------------------------------
      *
     Oqprint    e            h1hdr          1 01
     O                       sdpgm               10
     O                                           50 'HOG PRODUCTION SYSTEM'
     O                                           70 'DATE'
     O                       udate         y     80
      *
     O          e            h1hdr          1
     O                       sdusr               10
     O                                           70 'TIME'
     O                       rtime               80 '  :  :  '
      *
     O          e            h1hdr          2
     O                                           35 'FARMS WITH THE SAME ITEM '
     O                                           61 'ON MULTIPLE ACTIVE BUDGETS'
     O                                           70 'PAGE'
     O                       page          z     80
      *
      *
      *-------------------------------------------------------------
      * Column headings
      *-------------------------------------------------------------
      *
     O          e            h1hdr       0  1
     O                                            5 'Farm'
     O                                           45 'Budget'
     O                                           53 'Budget'
      *
     O          e            h1hdr       0  1
     O                                            5 'Site'
     O                                           18 'Budget Item'
     O                                           45 'Number'
     O                                           55 'Template'
     O                                           67 'From Date'
     O                                           79 'To Date'
      *
     O          e            h1hdr       0  2
     O                       dash                80
      *
      *-------------------------------------------------------------
      * Detail line
      *-------------------------------------------------------------
      *
     O          e            l1dtl          1
     O                       jhfscd        z      5
     O                       jdbgit              32
     O                       jhfbsn        z     45
     O                       jhbtcd              53
     O                       l1fmdy         b    66 '  /  /  '
     O                       l1tmdy         b    80 '  /  /  '
      *
      *-------------------------------------------------------------
      * Print a blank line
      *-------------------------------------------------------------
      *
     O          e            blank       1
      *
      *-------------------------------------------------------------
      * No data message line
      *-------------------------------------------------------------
      *
     O          e            nodata         1
     O                                           16 'No farm budgets '
     O                                           37 'have duplicate items.'
      /eject
