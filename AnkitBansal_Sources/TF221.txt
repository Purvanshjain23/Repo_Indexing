      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Triumph Foods
      * PROGRAM:     Eopfees: Update Function Control File
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     01/13/05
      *
      *              As the last step of each EOP Fees Close, we will update the
      *              Function Control file.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 11/30/06  LeAnne Ramsey
      *           Changed to use the Datamart Calendar file.
      *
      * 01/21/08  LeAnne Ramsey
      *           Added a parm of P=Production on the call to generic program TF815.
      *           A "P" will continue to return the HPS Year/Period/Week to us.
      *           (The JDE programs that call TF815 will now send in A=Accounting to
      *           return Accounting Year/Period.)
      /eject
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Ftfp099    uf   e           k disk
      *    Function control
      *
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      *
      ****************************************************************
      * ARRAYS AND TABLES
      ****************************************************************
      *
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Work fields for date manipulation
      *
     D wkisodate       s               d   datfmt(*iso)
     D wkcldt          s              8  0
      *
      *
      * Parm fields
      *
     d xxpfcd          s              1
      *
     D xxyr            s              4  0
     D xxpe            s              2  0
     D xxwk            s              2  0
     D xxdt            s              8  0
     d xxapfl          s              1
      *
     D xxpbdt          s              8  0
     D xxpbsyndt       s              7  0
     D xxpbmdy         s              6  0
      *
     D xxpedt          s              8  0
     D xxpesyndt       s              7  0
     D xxpemdy         s              6  0
      *
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * MAINLINE
      ****************************************************************
      *
      * Retrieve Function Control record.
      *
     C     'EOPFEES   '  chain     tfp099                             92
     C                   if        *in92 = *off                                 If hit
      *
      * Perform the "Final" updates when:
      *  1) it is a Final run
      *
     C                   if        xxpfcd = 'F'
     C                   exsr      $final
     C                   endif
      *
      * Always initialize control fields.
      *
     C                   move      *blank        fnsbfl
     C                   move      *blank        fnpfcd
      *
     C                   update    fnrec
     C                   endif                                                  If done
      *
     C                   seton                                        lr
      /eject
      *---------------------------------------------------------------
      * Updating when it is a clean Final run.
      *---------------------------------------------------------------
      *
     C     $final        begsr
      *
      * Populate Close Date
     C                   z-add     wkcldt        fncldt
      *
      * Move 'current' data to 'last' data.
      *
     C                   z-add     fncyr         fnlyr
     C                   z-add     0             fnlwk
     C                   z-add     fncpe         fnlpe
      * Begin dates
     C                   z-add     fncbdt        fnlbdt
     C                   z-add     fncbmdy       fnlbmdy
     C                   z-add     fncbsyn       fnlbsyn
      * End dates
     C                   z-add     fncedt        fnledt
     C                   z-add     fncemdy       fnlemdy
     C                   z-add     fncesyn       fnlesyn
      *
      * Retrieve the data for the next period.
      *
     C                   exsr      $next
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Retrieve all the calendar data for the 'next' current period
      *---------------------------------------------------------------
      *
     C     $next         begsr
      *
      * Add 1 day to the Current Ending Date to get the next Beginning Date
      *
     C                   movel     fncedt        wkisodate
     C                   adddur    1:*days       wkisodate
     C                   move      wkisodate     xxdt
      *
      * Retrieve the Produciton Year/Period for this "new" date.
      *
     C                   call      'TF815'
     C                   parm                    xxdt
     C                   parm      'P'           xxapfl
     C     fncyr         parm      0             xxyr
     C     fncpe         parm      0             xxpe
     C                   parm      0             xxwk
      *
      * SSet "week" to zero.
     C                   z-add     0             fncwk
      *
      * Retrieve the Begin/End Dates for the new Period.
      *
     C                   call      'TF816'
     C                   parm      fncyr         xxyr
     C                   parm      fncpe         xxpe
     C     fncbdt        parm      0             xxpbdt
     C     fncbsyn       parm      0             xxpbsyndt
     C     fncbmdy       parm      0             xxpbmdy
     C     fncedt        parm      0             xxpedt
     C     fncesyn       parm      0             xxpesyndt
     C     fncemdy       parm      0             xxpemdy
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
     C     *entry        plist
     C                   parm                    xxpfcd
      *
      *
      * Save date for Closed Date
      *
     C     *mdy          move      udate         wkisodate
     C                   move      wkisodate     wkcldt
      *
     C                   endsr
      /eject
