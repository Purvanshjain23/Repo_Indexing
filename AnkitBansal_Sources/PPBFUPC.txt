/********************************************************************/
/*                                                                  */
/*  SYNOPSIS -  PRINT BROKER PERFORMANCE REPORT & EOP UPDATE        */
/*                                                                  */
/*  SYSTEM    - ORDER MANAGEMENT SYSTEM                             */
/*  DATE      - 11/06/03                                            */
/*                                                                  */
/*      PARAMETERS:                                                 */
/*                                                                  */
/*     O    &RETURN     - RETURN CODE                               */
/*     I    &COPYN      -  NO. OF COPIES (PASSED)                   */
/*     I    &OUTQ       -  OUTPUT QUEUE                             */
/*                         IF THIS IS BLANK AT START, PROMPT        */
/*                         PGM IS CALLED; OTHERWISE, THE PRINT CL   */
/*                         IS SUBMITTED WITHOUT PROMPTING           */
/*     I    &HOLD       -  HOLD STATUS                              */
/*     I    &SAVE       -  SAVE STATUS                              */
/*     I    &SHIPL    -    SHIP TO CUST PASS                        */
/*     I    &ACCR      -   ACCRUAL CODE                             */
/*     I    &BROKER    -   BROKERL CODE                             */
/*     I    &FDATEL    -   FROM ACT SHIP DATE - PASS                */
/*     I    &TDATEL    -   THRU ACT SHIP DATE - PASS                */
/*     I    &STATUS    -   EOP UPDATE IF 'Y'                        */
/*     I    &PRINTYN   -   PRINT OR HIDE AMOUNT FIELDS IN REPORT    */
/*     I    &PAYEE     -   PAYEE TYPE                               */
/*                                                                  */
/********************************************************************/
/* RMC EXPCOM 10/21/04 REMOVE CO FROM PARM AND CALLED PGMS          */
/* LJB OMS045D 10/19/2006 ADD FLAG TO PRINT OR HIDE AMOUNTS         */
/* RMC DLY029  05/23/07  BROKER CODE TO 6A                          */
/* RMC DLY029  08/14/07  DLY029 - ADDED BROKER STMT PRINT PER PAYEE TYPE */
/*                   ADD PAYEE TYPE PARM-- CALL DRIVER A LIEU PRTFILE   */
/* RMC E00068  04/10/08  ADDED PAYEE CODE PARM TO CALL PPBEPFR      */
/********************************************************************/
 PPBFUPC:    PGM        PARM(&RETURN &ACCR &BROKER &OUTQ &HOLD &SAVE +
                          &SHIPL &FDATEL &TDATEL &COPYN &STATUS +
                          &PRINTYN &PAYEE &CO)
/* PARMS                                                                      */
        DCL    &RETURN     *CHAR   (7)           /* Return Code               */
        DCL    &COPYN      *DEC                  /* No. of Copies             */
        DCL    &OUTQ       *CHAR   (10)          /* Output Queue    */
        DCL    &HOLD       *CHAR   (4)           /* Hold Status     */
        DCL    &SAVE       *CHAR   (4)           /* Save Status     */

        DCL    &ACCR       *CHAR   (3)           /* ACCRUAL CODE    */
        DCL    &BROKER     *CHAR   (6)           /* BROKER CODE     */
        DCL    &STATUS     *CHAR   (1)           /* EOP UPDATE IF Y */
        DCL    &PAYEE      *CHAR   (2)           /* PAYEE TYPE      */
        DCL    &CO        *DEC    (3 0)           /*  SHIP TO       */
        DCL    &SHIP      *DEC    (7 0)           /*  SHIP TO       */
        DCL    &SHIPL     *DEC                    /*  FOR PASSING   */
        DCL    &FDATE     *DEC    (7 0)           /*  DATE          */
        DCL    &FDATEL    *DEC                    /*  FOR PASSING   */
        DCL    &TDATE     *DEC    (7 0)           /*  DATE          */
        DCL    &TDATEL    *DEC                    /*  FOR PASSING   */
        DCL    &PRINTYN   *CHAR   (1)             /* PRINT OR HIDE  */

/* INTERNAL FIELDS                                                            */
        DCL    &COPY       *DEC    (2 0)         /* Number of copies          */

/* ERROR MESSAGE FIELDS                                                       */
        DCL    &MSG        *CHAR   (512)         /* message                   */
        DCL    &MSGID      *CHAR   (7)           /* message id                */
        DCL    &MSGL       *DEC    (5 0)
        DCL    &COMMAND    *CHAR   (512)

/********************************************************************/
/*   INITIALIZE VARIABLES                                           */
/********************************************************************/

             CHGVAR     VAR(&RETURN) VALUE('       ')
             CHGVAR     VAR(&COPY) VALUE(&COPYN)
             CHGVAR     VAR(&SHIP) VALUE(&SHIPL) /* Change to length */
             CHGVAR     VAR(&FDATE) VALUE(&FDATEL) /* Change to length */
             CHGVAR     VAR(&TDATE) VALUE(&TDATEL) /* Change to length */
/*********************************************************************/
/*   MONITOR FOR ALL MESSAGES AT PROGRAM LEVEL                       */
/*********************************************************************/

             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(MSGTRAP))


/********************************************************************/
/*   CALL EOP UPDATE IF 'Y'                                         */
/********************************************************************/
             IF         COND(&STATUS *EQ 'Y') THEN(DO)
             CALL       PGM(PPBOXFR) PARM(' '  &SHIP &ACCR +
                          &FDATE &TDATE)
             ENDDO
/********************************************************************/
/*   CALL OVRPRTF                                                  */
/********************************************************************/
             SBDPRTOVR  CMDVAR(&COMMAND) FILE(PPBEPFR$) +
                          OUTQ(&OUTQ) COPIES(&COPY) HOLD(&HOLD) +
                          SAVE(&SAVE)
             CALL       PGM(QCMDEXC) PARM(&COMMAND 512)
/********************************************************************/
/*   CALL ORIGINAL BROKER PERF REPORT                          */
/********************************************************************/
             IF         COND(&PAYEE *EQ '  ') THEN(DO)
             CALL       PGM(PPBEPFR) PARM(' ' &SHIP &BROKER &ACCR +
                          &FDATE &TDATE &PRINTYN &CO &PAYEE)
             GOTO       CMDLBL(END)
             ENDDO
/********************************************************************/
/*   CALL BROKER STATEMENTS PER PAYEE TYPE USING DRIVER OVER BROKER FILE  */
/********************************************************************/
             IF         COND(&PAYEE *NE '  ') THEN(DO)
             CALL       PGM(PMLYXFR) PARM(' '  &BROKER &PAYEE +
                           &FDATE &TDATE &SHIP &ACCR &PRINTYN &CO)
             GOTO       CMDLBL(END)
             ENDDO

/*----------------------RECEIVE MESSAGES-----------------------------*/

 MSGTRAP:    RCVMSG     MSGTYPE(*EXCP) MSGDTA(&MSG) MSGDTALEN(&MSGL) +
                          MSGID(&MSGID)
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(END))

             SNDPGMMSG  MSGID(&MSGID) MSGF(QCPFMSG) MSGDTA(%SST(&MSG +
                          1 &MSGL)) MSGTYPE(*ESCAPE)
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(END))

 END:        ENDPGM

