     A*%%TS  SD  20250729  051338  ISJMIST     REL-V7R5M0  5770-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD116
     A*  TITLE:         REVISE TRUCKER
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  CREATED:       01/06/03
     A*
     A*
     A*  INDICATOR 60 IS ON WHEN IN REVISE MODE
     A*  INDICATOR 61 IS ON WHEN IN CREATE MODE
     A*  INDICATOR 62 IS ON WHEN IN DELETE MODE OR WHEN THE RECORD
     A*               HAS BEEN DELETED OUT FROM UNDER THE USER.
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A  60N62                               CF05(05 'REFRESH')
     A                                      CA12(12 'PREVIOUS')
     A*
     A*---------------------------------------------------------------
     A* RECORD FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP10262R1
     A*%%TS  SD  20250729  051338  ISJMIST     REL-V7R5M0  5770-WDS
     A                                      SETOF(34)
     A                                      SETOF(35)
     A                                      SETOF(36)
     A                                      SETOF(37)
     A                                      SETOF(56)
     A                                      SETOF(57)
     A                                      RTNCSRLOC(&RCD &FLD)
     A                                      OVERLAY
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 27'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USR)
     A                                      DSPATR(HI)
     A            D1MODE    R        O  2  2REFFLD(@@MODE)
     A                                      DSPATR(HI)
     A                                  2 30'REVISE REGION'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@JOB)
     A                                      DSPATR(HI)
     A                                  4  2'Region Code .............'
     A            D1RGCD    R        B  4 30REFFLD(@@RGCD)
     A N35                                  DSPATR(HI)
     A  35                                  DSPATR(PC)
     A  35                                  DSPATR(RI)
     A  61                                  DSPATR(PR)
     A N61                                  DSPATR(UL)
     A                                  5  2'Region Description.......'
     A            D1RGDS    R        B  5 30REFFLD(HSRREF/RGRGDS *LIBL/HSPREF)
     A                                      CHECK(LC)
     A  34                                  DSPATR(RI)
     A  34                                  DSPATR(PC)
     A  60                                  DSPATR(PR)
     A N34                                  DSPATR(HI)
     A*
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R HP10262R2
     A*%%TS  SD  20250723  061054  ISJMIST     REL-V7R5M0  5770-WDS
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A N62N61                          23 11'F5=Refresh'
     A                                 23 23'F12=Previous'
     A*
     A*---------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE FORMAT
     A*---------------------------------------------------------------
     A*
     A          R HP10262ES                 SFL
     A*%%TS  SD  20250722  071335  ISJMIST     REL-V7R5M0  5770-WDS
     A                                      SFLMSGRCD(24)
     A*
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ(10)
     A*
     A*---------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*---------------------------------------------------------------
     A*
     A          R HP10262EC                 SFLCTL(HP10262ES)
     A*%%TS  SD  20250722  071335  ISJMIST     REL-V7R5M0  5770-WDS
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ(10)
