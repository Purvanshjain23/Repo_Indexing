      *****************  RPG PROGRAM HEADING  ************************
     h option(*SRCSTMT:*NODEBUGIO)
      *
      * ENVIRONMENT: Pork Division
      * SYSTEM:      Margin Management
      * PROGRAM:     SD219 - Signal Demand: Freezer Allocation
      * PROGRAMMER:  LeAnne Ramsey
      * CREATED:     05/08/08
      *
      * FUNCTION: This program builds the Freezer Allocation file.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * Date      Programmer
      *
      /eject
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fpmdxcpl2  if   e           k disk
      *   PS Freezer allocation
      *
      *
     Fsdp219    o    e           k disk
      *    Signal Demand: Freezer Allocation
      *
      /EJECT
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      *---------------------------------------------------------------
      *  Named Constants
      *---------------------------------------------------------------
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      *---------------------------------------------------------------
      * Standalone fields
      *---------------------------------------------------------------
      *
      * Fields for date manipulation
      *
     D wksysdt7        s              7  0
     D wkisodate       s               d   datfmt(*iso)
      *
      ****************************************************************
      * Data Structures
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ***************************************************************************************
      * Mainline
      ***************************************************************************************
      *
      * Process all Freezer Allocation records for system date forward.
      *
     C     wksysdt7      setll     pmdxcpl2
      *
     C                   dou       *in90 = *on                                  Main do
     C                   read      pmdxcpl2                               90
     C                   if        *in90 = *off                                 If not eof
      * Company
     C                   z-add     dxaic3        faaic3
      * Item
      * Consumed Item
     C                   z-add     dxhgcd        fahgcd
     C                   z-add     dxnudt        fanudt
      * Production Qty
     C                   z-add     dxnjqt        fanjqt
      *
      * Flip Production Date to CCYYMMDD
      *
     C     *cymd         test(d)                 dxeldt                 92
     C                   if        *in92 = *off
     C     *cymd         move      dxeldt        wkisodate
     C     *iso          move      wkisodate     faeldt
     C                   else
     C                   z-add     0             faeldt
     C                   endif
      *
      * Write Freezer Allocation record
      *
     C                   write     farec
      *
     C                   endif                                                  If not eof
     C                   enddo                                                  Main do
      *
     C                   seton                                        lr
      /EJECT
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Put system date into Synon format of CYYMMDD.
      *
     C     *mdy          movel     udate         wkisodate
     C     *cymd         movel     wkisodate     wksysdt7
      *
     C                   endsr
      /EJECT
