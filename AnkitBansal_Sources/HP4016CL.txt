/***************************************************************** */
/*                                                                 */
/*  PROGRAM NUMBER:  HP4016CL                                      */
/*  PROGRAM NAME:    LISTINGS FROM WORK WITH GROUP OVERRIDES       */
/*  PROGRAMMER:      LEANNE FEDOR                                  */
/*  CREATE DATE:     09/01/04                                      */
/*                                                                 */
/*  FUNCTION:        THIS CL IS SUBMITTED WITH QCMDEXC FROM        */
/*                   HP4016-WORK WITH GROUP OVERRIDES.             */
/*                   OPTIONS ARE PASSED THROUGH THE LDA.           */
/*                                                                 */
/*                                                                 */
/*******************************************************************/
/*  MODIFIED:                                                      */
/*******************************************************************/

             PGM

/* DEFINE THE VARIABLE REQUIRED IN THE COMMAND TO OVERRIDE THE     */
/* PRINT FILE TO A LASER PRINTER                                   */

             DCL        VAR(&CMD) TYPE(*CHAR) LEN(512)
             DCL        VAR(&QDATA) TYPE(*CHAR) LEN(150)

             DCL        VAR(&LDREPORT) TYPE(*CHAR) LEN(1)
             DCL        VAR(&LDGSCD) TYPE(*CHAR) LEN(2)
             DCL        VAR(&LDPPCD) TYPE(*CHAR) LEN(5)
             DCL        VAR(&LDSICD) TYPE(*CHAR) LEN(5)
             DCL        VAR(&LDFSCD) TYPE(*DEC) LEN(5)
             DCL        VAR(&LDOBFS) TYPE(*DEC) LEN(5)
             DCL        VAR(&LDONFS) TYPE(*DEC) LEN(5)

             DCL        VAR(&HOLD) TYPE(*CHAR) LEN(10)
             DCL        VAR(&COPYS) TYPE(*DEC) LEN(2)
             DCL        VAR(&OUTQ) TYPE(*CHAR) LEN(10)

             CHGVAR     VAR(&LDREPORT) VALUE(%SST(*LDA 1 1))
             CHGVAR     VAR(&LDPPCD) VALUE(%SST(*LDA 9 5))
             CHGVAR     VAR(&LDSICD) VALUE(%SST(*LDA 14 5))
             CHGVAR     VAR(&LDOBFS) VALUE(%SST(*LDA 19 5))
             CHGVAR     VAR(&LDONFS) VALUE(%SST(*LDA 24 5))
             CHGVAR     VAR(&LDFSCD) VALUE(%SST(*LDA 29 5))
             CHGVAR     VAR(&LDGSCD) VALUE(%SST(*LDA 34 2))


/*------------------------------------------------------------------*/
/*  SET UP PRINT FILE OVERRIDES                                      */
/*------------------------------------------------------------------*/

 HOLD:       IF         COND(%SST(*LDA 411 1) = 'Y') THEN(CHGVAR +
                          VAR(&HOLD) VALUE(*YES))
             ELSE       CMD(CHGVAR VAR(&HOLD) VALUE(*NO))

 COPYS:      CHGVAR     VAR(&COPYS) VALUE(%SST(*LDA 412 1))
             IF         COND(&COPYS = 0) THEN(CHGVAR VAR(&COPYS) +
                          VALUE(1))

 OUTQ:       CHGVAR     VAR(&OUTQ) VALUE(%SST(*LDA 413 10))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(%SST(*LDA 401 10)))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(*JOB))

/*------------------------------------------------------------------*/
/* LISTING FROM 6=PRINT FOR A SINGLE GROUP                          */
/*------------------------------------------------------------------*/

             IF         COND(&LDREPORT *EQ 'P') THEN(DO)

             SBDPRTOVR  CMDVAR(&CMD) FILE(QPRINT) OUTQ(&OUTQ) +
                          WIDTH(80) LANDSCAPE(*PRTF) DRAWER(*PRTF) +
                          COPIES(&COPYS) HOLD(&HOLD)
             CALL       PGM(QCMDEXC) PARM(&CMD 512)

             CALL       PGM(HP6016)
             ENDDO


/*------------------------------------------------------------------*/
/* LISTING FROM F7=LISTING                                          */
/*------------------------------------------------------------------*/

             IF         COND(&LDREPORT *EQ 'L') THEN(DO)

             CHGVAR     VAR(&QDATA) VALUE(' ')
             CHGVAR     VAR(%SST(&QDATA 1 12)) VALUE('HGHGSN *NE 0')

/*  IF PRODUCTION PHASE */

             IF         COND(&LDPPCD *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 14 17)) VALUE('*AND HGPPCD +
                          *EQ "')
             CHGVAR     VAR(%SST(&QDATA 31 5)) VALUE(&LDPPCD)
             CHGVAR     VAR(%SST(&QDATA 36 1)) VALUE('"')
             ENDDO

/*  IF SIRE LINE        */

             IF         COND(&LDSICD *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 38 17)) VALUE('*AND GOSICD +
                          *EQ "')
             CHGVAR     VAR(%SST(&QDATA 55 5)) VALUE(&LDSICD)
             CHGVAR     VAR(%SST(&QDATA 60 1)) VALUE('"')
             ENDDO

/* ORIGIN BGF FARM      */

             IF         COND(&LDOBFS *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 62 15)) VALUE('*AND GOOBFS +
                          *EQ')
             CHGVAR     VAR(%SST(&QDATA 78 5)) VALUE(&LDOBFS)
             ENDDO


/* ORIGIN NURSERY FARM  */

             IF         COND(&LDONFS *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 84 15)) VALUE('*AND GOONFS +
                          *EQ')
             CHGVAR     VAR(%SST(&QDATA 100 5)) VALUE(&LDONFS)
             ENDDO

/* FARM SITE */

             IF         COND(&LDFSCD *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 106 15)) VALUE('*AND HGFSCD +
                          *EQ')
             CHGVAR     VAR(%SST(&QDATA 122 5)) VALUE(&LDFSCD)
             ENDDO


/*  GROUP STATUS */

             IF         COND(&LDGSCD *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 128 17)) VALUE('*AND HGGSCD +
                          *EQ "')
             CHGVAR     VAR(%SST(&QDATA 145 2)) VALUE(&LDGSCD)
             CHGVAR     VAR(%SST(&QDATA 147 1)) VALUE('"')
             ENDDO


             OVRDBF     FILE(HSJ034S) SHARE(*YES)
             OPNQRYF    FILE((HSJ034S)) QRYSLT(&QDATA) KEYFLD(*FILE)

             SBDPRTOVR  CMDVAR(&CMD) FILE(QPRINT) OUTQ(&OUTQ) +
                          WIDTH(132) LANDSCAPE(*PRTF) DRAWER(*PRTF) +
                          COPIES(&COPYS) HOLD(&HOLD)
             CALL       PGM(QCMDEXC) PARM(&CMD 512)

             CALL       PGM(HP6017)

             CLOF       OPNID(HSJ034S)
             ENDDO


             DLTOVR     FILE(*ALL)

             ENDPGM

