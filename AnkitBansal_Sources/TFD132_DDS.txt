     A*%%TS  SD  20050316  140153  ISLRAMS     REL-V5R2M0  5722-WDS
     A*****************************************************************
     A*
     A*  SYSTEM:        TRIUMPH FOODS
     A*  DISPLAY FILE:  TFD132
     A*  TITLE:         REVISE INVOICE CHARGE
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  CREATED:       03/09/05
     A*
     A*  INDICATOR 60 IS ON WHEN IN REVISE MODE
     A*  INDICATOR 61 IS ON WHEN IN CREATE MODE
     A*  INDICATOR 62 IS ON WHEN IN DELETE MODE
     A*  INDICATOR 63 IS ON WHEN YOU ARE PROTECTING SOME FIELDS
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/TFPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A N62N63                               CF04(04 'PROMPT')
     A  60N62                               CF05(05 'REFRESH')
     A  60N62N63                            CF11(11 'DELETE')
     A                                      CA12(12 'PREVIOUS')
     A*
     A*-----------------------------------------------------------------
     A* RECORD FORMAT ONE
     A*-----------------------------------------------------------------
     A          R TF132R1
     A*%%TS  SD  20050316  140153  ISLRAMS     REL-V5R2M0  5722-WDS
     A                                      SETOF(30)
     A                                      SETOF(31)
     A                                      SETOF(33)
     A                                      SETOF(53)
     A                                      RTNCSRLOC(&RCD &FLD)
     A                                      OVERLAY
     A            RCD       R        H      REFFLD(@@RCD)
     A            FLD       R        H      REFFLD(@@FLD)
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 28'REVISE INVOICE CHARGE'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USR)
     A                                      DSPATR(HI)
     A            D1MODE         6A  O  2  2DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@JOB)
     A                                      DSPATR(HI)
     A                                  3  2'Invoice Charge:'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  3 46'System-generated flag:'
     A                                      COLOR(PNK)
     A            D1SYSFL   R        O  3 69REFFLD(TFREF/@@SYSFL *LIBL/TFPREF)
     A                                      COLOR(PNK)
     A                                  4  2'Date .................'
     A            D1ICDTMDY R        B  4 27REFFLD(TFREF/@@DT06 *LIBL/TFPREF)
     A                                      EDTWRD('  /  /  -')
     A  30                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A N62N63N30                            DSPATR(HI)
     A N62N63                               DSPATR(UL)
     A  62
     AO 63                                  DSPATR(PR)
     A                                  4 46'Invoice charge status:'
     A                                      COLOR(PNK)
     A            D1STDS    R        O  4 69REFFLD(TFREF/@@STDS *LIBL/TFPREF)
     A                                      COLOR(PNK)
     A                                  5  2'Amount ...............'
     A            D1ICAM    R        B  5 27REFFLD(TFREF/@@ICAM *LIBL/TFPREF)
     A                                      EDTCDE(K)
     A  31                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A N62N63N31                            DSPATR(HI)
     A N62N63                               DSPATR(UL)
     A  62
     AO 63                                  DSPATR(PR)
     A                                  5 46'Invoice charge number:'
     A                                      COLOR(PNK)
     A            D1ICSN    R        O  5 69REFFLD(TFREF/@@ICSN *LIBL/TFPREF)
     A                                      EDTCDE(Z)
     A                                      COLOR(PNK)
     A                                  6  2'Units ................'
     A            D1UNIT    R        B  6 27REFFLD(TFREF/@@UNIT *LIBL/TFPREF)
     A                                      EDTCDE(K)
     A N62N63                               DSPATR(HI)
     A N62N63                               DSPATR(UL)
     A  62
     AO 63                                  DSPATR(PR)
     A                                  7  2'Charge code ..........'
     A N62N63                           7 25'?'
     A            D1CCCD    R        B  7 27REFFLD(TFREF/@@CCCD *LIBL/TFPREF)
     A  33
     AO 53                                  DSPATR(PC)
     A  33                                  DSPATR(RI)
     A N62N63N33                            DSPATR(HI)
     A N62N63                               DSPATR(UL)
     A  62
     AO 63                                  DSPATR(PR)
     A                                  8  2'Comment ..............'
     A            D1ICCOM   R        B  8 27REFFLD(TFREF/@@ICCOM *LIBL/TFPREF)
     A                                      CHECK(LC)
     A N62                                  DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(PR)
     A                                 10  2'Invoice Info:'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 39'Invoice Date'
     A                                      DSPATR(UL)
     A                                 10 54'Due Date'
     A                                      DSPATR(UL)
     A                                 11  2'Invoice number .........:'
     A            D1INSN    R        O 11 29REFFLD(TFREF/@@INSN *LIBL/TFPREF)
     A                                      EDTCDE(M)
     A            D1INDTMDY R        O 11 41REFFLD(TFREF/@@DT06 *LIBL/TFPREF)
     A                                      EDTWRD('  /  / 0')
     A            D1DUDTMDY R        O 11 54REFFLD(TFREF/@@DT06 *LIBL/TFPREF)
     A                                      EDTWRD('  /  / 0')
     A                                 13  2'Charge Code Info:'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 13 46'User-controlled:'
     A                                      COLOR(PNK)
     A            D1UCFL    R        O 13 63REFFLD(TFREF/@@UCFL *LIBL/TFPREF)
     A                                      COLOR(PNK)
     A                                 14  2'Charge code ............:'
     A            D1CCCD2   R        O 14 29REFFLD(TFREF/@@CCCD *LIBL/TFPREF)
     A            D1CCDS    R        O 14 46REFFLD(TFREF/@@CCDS *LIBL/TFPREF)
     A                                 15  2'Company ................:'
     A            D1CONO    R        O 15 29REFFLD(TFREF/@@CONO *LIBL/TFPREF)
     A                                      EDTCDE(Z)
     A            D1CONM    R        O 15 46REFFLD(TFREF/@@CONM *LIBL/TFPREF)
     A                                 16  2'G/L code - debit  ......:'
     A            D1GLDBCD  R        O 16 29REFFLD(TFREF/@@GLDBCD *LIBL/TFPREF)
     A            D1GLDBCDDSR        O 16 46REFFLD(TFREF/@@GLDBCDDS *LIBL/TFPRE-
     A                                      F)
     A                                 17  2'G/L code - credit ......:'
     A            D1GLCRCD  R        O 17 29REFFLD(TFREF/@@GLCRCD *LIBL/TFPREF)
     A            D1GLCRCDDSR        O 17 46REFFLD(TFREF/@@GLCRCDDS *LIBL/TFPRE-
     A                                      F)
     A                                 18  2'Active/inactive status .:'
     A            D1AIST    R        O 18 29REFFLD(TFREF/@@AIST *LIBL/TFPREF)
     A                                 19  2'Charge code frequency ..:'
     A            D1CFQCD   R        O 19 29REFFLD(TFREF/@@CFQCD *LIBL/TFPREF)
     A            D1CFQDS   R        O 19 46REFFLD(TFREF/@@CFQDS *LIBL/TFPREF)
     A                                 20  2'Include in invoice total:'
     A            D1INFL    R        O 20 29REFFLD(TFREF/@@INFL *LIBL/TFPREF)
     A                                 21  2'System module ..........:'
     A            D1SMCD    R        O 21 29REFFLD(TFREF/@@SMCD *LIBL/TFPREF)
     A            D1SMDS    R        O 21 46REFFLD(TFREF/@@SMDS *LIBL/TFPREF)
     A*
     A*-----------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*-----------------------------------------------------------------
     A          R TF132K1
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A N62N63                          23 11'F4=Prompt'
     A N62N61                          23 22'F5=Refresh'
     A N62N61N63                       23 34'F11=Delete'
     A                                 23 46'F12=Previous'
      *
      *------------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *-----------------------------------------------------------------
     A          R TF132ES                   SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
      *------------------------------------------------------------------
      * ERROR MESSAGE SUBFILE CONTROL FORMAT
      *-----------------------------------------------------------------
     A          R TF132EC                   SFLCTL(TF132ES)
      *
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
