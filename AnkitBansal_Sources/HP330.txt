      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Hog Production System
      * PROGRAM:     Workfile: Process Grow Finish-to-Grow Finish Transfers
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     11/20/01
      *
      * FUNCTION:    This function writes records to the GF-to-GF transfer workfile.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 07/06/09  LeAnne Ramsey
      *           Recompile only. Added new field 'Continuous Flow Flag' to Hog Group file.
      *
      * 10/15/13  LeAnne Ramsey (E2831)
      *           Recompile only. Added field 'MTech Reference'.
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fhsl034d   if   e           k disk
      *    Hog group
      *
      *
     Fhsp018    if   e           k disk
      *    Farm site
      *
      *
     Fhsj075g   if   e           k disk
      *  Transfer detail + transfer header
      *  (Records selected in open query)
      *
      *
     Fhsp330    if   e           k disk    prefix(p1)
      *  Workfile: Grow finish-to-grow finish transfers
      *
      *
     Fhsl330a   uf a e           k disk    rename(w6rec:w6reca)
      *  Workfile: Grow finish-to-grow finish transfers
      *
      /EJECT
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      ****************************************************************
      * ARRAYS AND TABLES
      ****************************************************************
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ***************************************************************************************
      * MAINLINE
      ***************************************************************************************
      *
      * STEP 1: Process all GF-to-GF transfers.
      *
     C                   exsr      $step1
      *
      *
      * STEP 2: Read the workfile that you just populated and populate the Catch-22
      *         flag.
     C                   exsr      $step2
      *
      * EOF Processing
      *
     C                   seton                                        lr
      /eject
      *--------------------------------------------------------------
      * Step 1 - Write transfers workfile
      *--------------------------------------------------------------
      *
      * Process all GF-to-GF selected by the open query that had quality or
      * rejected head.
      * Write each record to the workfile.
      *
     C     $step1        begsr
      *
     C                   dou       *in90 = *on                                  Step 1 loop
     C                   read      hsj075g                                90
     C                   if        *in90 = *off and                             If not EOF
     C                             (mdqlhd <> 0 or mdrjhd <> 0)
      *
     C                   exsr      $wrt330
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Step 1 loop
      *
     C                   endsr
      /eject
      *--------------------------------------------------------------
      * Step 2 - Populate the Catch-22 flag.
      *--------------------------------------------------------------
      *
      * We can never determine the nursery source groups for grow finish groups that
      * are both origin and destination groups on GF-to-GF transfers.
      * So, to avoid this catch-22 situation, we want to flag the transfer records
      * where the origin group is a destination group on any other GF-to-GF transfer.
      *
      * So, read each record in the workfile.  Using the origin group, chain
      * to the same workfile using the logical file keyed by destination group. If you
      * get a hit, then flag the original record as a catch-22 record.
      *
     C     $step2        begsr
      *
     C     *loval        setll     hsl330a
      *
     C                   dou       *in91 = *on                                  Step 2 loop
     C                   read      hsl330a                                91
     C                   if        *in91 = *off                                 If not EOF
      *
     C     w6orhgsn      chain     hsp330                             92
     C                   if        *in92 = *off
     C                   move      yes           w622fl
     C                   else
     C                   move      no            w622fl
     C                   endif
      *
     C                   update    w6reca
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Step 2 loop
      *
     C                   endsr
      /eject
      *--------------------------------------------------------------
      * Write a record to the workfile
      *--------------------------------------------------------------
      *
     C     $wrt330       begsr
      *
      * Retrieve destination hog group code
      *
     C     mddnsn        chain     hsl034d                            92
     C                   if        *in92 = *off
     C                   move      hghgcd        w6dnhgcd
     C                   move      hgppcd        w6dnppcd
     C                   else
     C                   move      *blank        w6dnhgcd
     C                   move      *blank        w6dnppcd
     C                   endif
      *
      * Retrieve origin hog group code
      *
     C     mdorsn        chain     hsl034d                            92
     C                   if        *in92 = *off
     C                   move      hghgcd        w6orhgcd
     C                   move      hgppcd        w6orppcd
     C                   else
     C                   move      *blank        w6orhgcd
     C                   move      *blank        w6orppcd
     C                   endif
      *
      * Retrieve destination farm site cell
      *
     C     mhdnfs        chain     hsp018                             92
     C                   if        *in92 = *off
     C                   z-add     fscell        w6dncell
     C                   else
     C                   z-add     0             w6dncell
     C                   endif
      *
      * Retrieve origin farm site cell
      *
     C     mhorfs        chain     hsp018                             92
     C                   if        *in92 = *off
     C                   z-add     fscell        w6orcell
     C                   else
     C                   z-add     0             w6orcell
     C                   endif
      *
     C                   z-add     mhrcdt        w6rcdt
     C                   z-add     mhorfs        w6orfscd
     C                   z-add     mdorsn        w6orhgsn
     C                   z-add     mhdnfs        w6dnfscd
     C                   z-add     mddnsn        w6dnhgsn
     C     mdrjhd        add       mdqlhd        w6rchd
      *
     C                   write     w6reca
      *
     C                   endsr
      /EJECT
      *----------------------------------------------------------------
      * Initialization subroutine
      *----------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
     C                   endsr
      /EJECT
