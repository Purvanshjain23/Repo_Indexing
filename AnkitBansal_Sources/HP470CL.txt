/***************************************************************** */
/*  ENVIRONMENT:       HOG PRODUCTION SYSTEM                       */
/*  PROGRAM NUMBER:    HP470CL                                     */
/*  PROGRAM NAME:      PRINT SALES MOVEMENT BILL OF LADING         */
/*  PROGRAMMER:        LEANNE FEDOR                                */
/*  DATE:              01/31/96                                    */
/*                                                                 */
/*******************************************************************/
/* MODIFICATIONS                                                   */
/* 06/19/97  LEANNE FEDOR                                          */
/*           CHANGED OUT QUEUE LOGIC.                              */
/*                                                                 */
/* 13/16/02  LEANNE FEDOR                                          */
/*           PRODUCTION PHASE WAS REMOVED AS A SELECTOR.           */
/*                                                                 */
/***************************************************************** */

             PGM

             DCL        VAR(&QDATA) TYPE(*CHAR) LEN(250)
             DCL        VAR(&LDFSBO) TYPE(*CHAR) LEN(5)
             DCL        VAR(&LDFYMD) TYPE(*DEC) LEN(8)
             DCL        VAR(&LDTYMD) TYPE(*DEC) LEN(8)
             DCL        VAR(&LDFSCD) TYPE(*DEC) LEN(5)
             DCL        VAR(&LDCVNO) TYPE(*DEC) LEN(8)
             DCL        VAR(&LDSTCD) TYPE(*CHAR) LEN(5)
             DCL        VAR(&LDTICD) TYPE(*CHAR) LEN(5)
             DCL        VAR(&LDMVSN) TYPE(*DEC) LEN(7)

             DCL        VAR(&HOLD) TYPE(*CHAR) LEN(10)
             DCL        VAR(&OUTQ) TYPE(*CHAR) LEN(10)

             CHGVAR     VAR(&LDFSBO) VALUE(%SST(*LDA 1 5))
             CHGVAR     VAR(&LDFYMD) VALUE(%SST(*LDA 6 8))
             CHGVAR     VAR(&LDTYMD) VALUE(%SST(*LDA 14 8))
             CHGVAR     VAR(&LDFSCD) VALUE(%SST(*LDA 27 5))
             CHGVAR     VAR(&LDCVNO) VALUE(%SST(*LDA 34 8))
             CHGVAR     VAR(&LDSTCD) VALUE(%SST(*LDA 42 5))
             CHGVAR     VAR(&LDTICD) VALUE(%SST(*LDA 47 5))
             CHGVAR     VAR(&LDMVSN) VALUE(%SST(*LDA 52 7))

/*-------------------------------------------------------------------*/
/*  SETUP QUERY OVER THE SALES MOVEMENT HEADER FILE                  */
/*-------------------------------------------------------------------*/

             CHGVAR     VAR(&QDATA) VALUE(' ')

/*  IF THE USER ELECTED TO SEE ONE SPECIFIC MOVEMENT NUMBER,      */
/*  SET THE QUERY UP FOR THAT MOVEMENT ONLY                       */

             IF         COND(&LDMVSN *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 1 10)) VALUE('SHMVSN *EQ')
             CHGVAR     VAR(%SST(&QDATA 12 7)) VALUE(&LDMVSN)
             ENDDO
             ELSE       CMD(DO)

/*  IF THE USER DID NOT LIMIT HIS SELECTION TO ONE MOVEMENT       */

/*  ALWAYS SELECT ONLY SALES MOVEMENTS THAT MATCH THE USER'S     */
/*  SELECTED BUSINESS OFFICE                                     */

             CHGVAR     VAR(%SST(&QDATA 1 12)) VALUE('SHFSBO *EQ "')
             CHGVAR     VAR(%SST(&QDATA 13 5)) VALUE(&LDFSBO)
             CHGVAR     VAR(%SST(&QDATA 18 1)) VALUE('"')

/*  ALWAYS SELECT ONLY SCHEDULED SALES MOVEMENTS                 */

             CHGVAR     VAR(%SST(&QDATA 20 20)) VALUE('*AND SHMSCD +
                          *EQ "SC"')

/*  ALWAYS SELECT ONLY SALES MOVEMENTS WITH A SCHEDULED SHIP DATE */
/*  IN THE USERS REQUESTED DATE RANGE                             */

             CHGVAR     VAR(%SST(&QDATA 41 15)) VALUE('*AND SHSCDT +
                          *GE')
             CHGVAR     VAR(%SST(&QDATA 57 8)) VALUE(&LDFYMD)
             CHGVAR     VAR(%SST(&QDATA 66 15)) VALUE('*AND SHSCDT +
                          *LE')
             CHGVAR     VAR(%SST(&QDATA 82 8)) VALUE(&LDTYMD)


/*  ALWAYS SELECT ONLY SALES MOVEMENTS THAT HAVE NOT YET HAD A    */
/*  BILL OF LADING PRINTED                                        */

             CHGVAR     VAR(%SST(&QDATA 91 17)) VALUE('*AND SHPTCT +
                          *EQ 0')

/*  IF FARM SITE                                                 */

             IF         COND(&LDFSCD *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 133 15)) VALUE('*AND SHFSCD +
                          *EQ')
             CHGVAR     VAR(%SST(&QDATA 149 5)) VALUE(&LDFSCD)
             ENDDO


/* IF CUSTOMER                                                   */

             IF         COND(&LDCVNO *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 155 15)) VALUE('*AND SHCVNO +
                          *EQ')
             CHGVAR     VAR(%SST(&QDATA 171 8)) VALUE(&LDCVNO)
             ENDDO


/*  IF SALES TYPE                                                  */

             IF         COND(&LDSTCD *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 180 17)) VALUE('*AND SHSTCD +
                          *EQ "')
             CHGVAR     VAR(%SST(&QDATA 197 5)) VALUE(&LDSTCD)
             CHGVAR     VAR(%SST(&QDATA 202 1)) VALUE('"')
             ENDDO


/*  IF TRUCKER                                                     */

             IF         COND(&LDTICD *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 204 17)) VALUE('*AND SHTICD +
                          *EQ "')
             CHGVAR     VAR(%SST(&QDATA 221 5)) VALUE(&LDTICD)
             CHGVAR     VAR(%SST(&QDATA 226 1)) VALUE('"')
             ENDDO

             ENDDO                            /* MULTIPLE MOVEMENTS */


/*-------------------------------------------------------------------*/
/* OVERRIDE THE SALES MOVEMENT HEADER FILE                           */
/*-------------------------------------------------------------------*/

             OVRDBF     FILE(HSP084) SHARE(*YES)
             OPNQRYF    FILE((HSP084)) OPTION(*INP *UPD) +
                          QRYSLT(&QDATA) KEYFLD((SHSTCD) (SHFSCD) +
                          (SHSCDT) (SHMVSN)) SEQONLY(*NO)

/*-------------------------------------------------------------------*/
/* SET UP PRINT OVERRIDES                                            */
/*-------------------------------------------------------------------*/

 HOLD:       IF         COND(%SST(*LDA 411 1) = 'Y') THEN(CHGVAR +
                          VAR(&HOLD) VALUE(*YES))
             ELSE       CMD(CHGVAR VAR(&HOLD) VALUE(*NO))

 OUTQ:       CHGVAR     VAR(&OUTQ) VALUE(%SST(*LDA 413 10))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(%SST(*LDA 401 10)))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(*JOB))

 OVRPRTF:    OVRPRTF    FILE(QPRINT) OUTQ(&OUTQ) FORMTYPE(SALESBOL) +
                          HOLD(&HOLD)

/*-------------------------------------------------------------------*/
/* PRINT THE BILL OF LADINGS                                         */
/*-------------------------------------------------------------------*/

             CALL       PGM(HP670)

             DLTOVR     FILE(*ALL)

             ENDPGM

