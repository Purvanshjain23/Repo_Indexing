      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Triumph Foods
      * PROGRAM:     TF236--Base Marketing Close: Edit with Error Listing
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     03/24/05
      *
      * Function:    This function edits the Tattoo data and prints an error listing.
      *
      *              The Weekly Base Marketing Close is all-or-nothing. So, if there are
      *              errors, the process will terminate with this step.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 11/29/06  LeAnne Ramsey
      *           Replaced the calls to the generic date conversion programs
      *           with ILE date logic.
      *
      * 11/03/20  ISE (H16853)
      *           Recompile only. Increased the Buy Order Number field length 5 to 7.
      /eject
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Ftfp001    if   e           k disk
      *    Preliminary/final
      *
      *
     Fpka1cpla  if   e           k disk
      *    Tattoo header (records selected in open query)
      *
      *
     Fprint80   o    f   80        printer oflind(*inof)
      *
      /eject
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      *----------------------------------------------------------------
      * CONSTANTS
      *----------------------------------------------------------------
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      *
      *----------------------------------------------------------------
      * ARRAYS AND TABLES
      *----------------------------------------------------------------
      *
      *----------------------------------------------------------------
      * STANDALONE FIELDS
      *----------------------------------------------------------------
      *
      * Standard fields
      *
     D dash            s             80    inz(*all'-')
     d rtime           s              6  0
      *
      *
      * Control fields
      *
     d first           s              1    inz('Y')
      *
      *
      * Workfields for date manipulation
      *
     D wkisodate       s               D   datfmt(*iso)
      *
      *
      * Print fields
      *
     d h1pfds          s                   like(pfpfds)
     d l1kldtmdy       s              6  0
      *
      *
      * Parm fields
      *
     d xxerror         s              1
     d xxpfcd          s              1
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *---------------------------------------------------------------
      * Local data area
      *---------------------------------------------------------------
      *
     d lda            uds                  dtaara(*lda)
     D  ldfmdy                39     44  0
     D  ldtmdy                45     50  0
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * Mainline
      ****************************************************************
      *
      * Print headings
     C                   exsr      $h1hdr
      *
      * Since the logical file selects only "Active" records and the open
      * query selects records for the week, the only 'error' that we check
      * for here is:
      *     1) Payment Status
      *
     C     *loval        setll     pka1cpla
      *
     C                   dou       *in90 = *on                                  Main do
     C                   read      pka1cpla                               90
     C                   if        *in90 = *off                                 If not EOF
     C                   move      no            first
      *
     C                   if        a1cwst <> 'PL' and                           If wrong
     C                             a1cwst <> 'PC' and
     C                             a1cwst <> 'NL' and
     C                             a1cwst <> 'NC'
     C                   move      yes           xxerror
      *
      * Convert Synon Date to MMDDYY format for printing.
      *
     C     *cymd         test(d)                 a1b0dt                 92
     C                   if        *in92 = *off                                 If OK date
     C     *cymd         move      a1b0dt        wkisodate
     C     *mdy          move      wkisodate     l1kldtmdy
     C                   endif                                                  If OK date
      *
     C                   if        *inof = *on
     C                   exsr      $h1hdr
     C                   endif
      *
     C                   except    l1dtl
      *
     C                   endif                                                  If wrong
     C                   endif                                                  If not EOF
     C                   enddo                                                  Main do
      *
      * EOF processing
      *
     C                   select
     C                   when      first = yes
     C                   except    notattos
     C                   move      yes           xxerror
     C
     C                   when      xxerror = no
     C                   except    noerrmsg
     C                   endsl
      *
     C                   seton                                        lr
      /eject
      *-----------------------------------------------------------------------------------
      * Print headings
      *-----------------------------------------------------------------------------------
      *
     C     $h1hdr        begsr
      *
     C                   except    h1hdr
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Parm lists
      *
     C     *entry        plist
     C                   parm                    xxerror
     C                   parm                    xxpfcd
      *
      *
      * Set up heading info.
      *
     C                   time                    rtime
      *
      * Retrieve 'preliminary/final' description
      *
     C     xxpfcd        chain     tfp001                             92
     C                   if        *in92 = *off
     C                   move      pfpfds        h1pfds
     C                   endif
      *
     C                   endsr
      /eject
      *-------------------------------------------------------------
      * Report heading lines
      *-------------------------------------------------------------
      *
     Oprint80   e            h1hdr          1 01
     O                       sdpgm               10
     O                                           49 'Weekly Base Marketing'
     O                                           55 ' Close'
     O                                           70 'Date'
     O                       udate         y     80
      *
     O          e            h1hdr          1
     O                       sdusr               10
     O                                           48 'Error Listing'
     O                                           70 'Time'
     O                       rtime               80 '  :  :  '
      *
     O          e            h1hdr          1
     O                                           49 'Tattoos Not Paid'
     O                                           70 'Page'
     O                       page          z     80
      *
      *
     O          e            h1hdr          1
     O                                           10 'From date:'
     O                       ldfmdy              20 '  /  /  '
     O                       h1pfds              47
      *
      *
     O          e            h1hdr          2
     O                                           10 'To date .:'
     O                       ldtmdy              20 '  /  /  '
      *
      *-------------------------------------------------------------
      * Column headings
      *-------------------------------------------------------------
      *
     O          e            h1hdr       0  1
     O                                           37 'Payment'
      *
     O          e            h1hdr       0  1
     O                                            7 'Company'
     O                                           16 'Tattoo'
     O                                           28 'Kill Date'
     O                                           37 'Status'
      *
     O          e            h1hdr       0  1
     O                       dash                80
      *
      *-------------------------------------------------------------
      * Detail line
      *-------------------------------------------------------------
      *
     O          e            l1dtl          2
     O                       a1hmnb        z      5                             Company
     O                       a1cvnb        z     16                             Tattoo
     O                       l1kldtmdy      b    28 '  /  /  '                  Kill date
     O                       a1cwst              35                             Payment Status
      *
      *-------------------------------------------------------------
      * There were no errors.
      *-------------------------------------------------------------
      *
     O          e            noerrmsg       2
     O                                           20 'There are no errors.'
      *-------------------------------------------------------------
      * There were no tattoos.
      *-------------------------------------------------------------
      *
     O          e            notattos       2
     O                                           22 'There were no tattoos '
     O                                           45 'for Company 960 for the'
     O                                           51 ' week.'
      /eject
