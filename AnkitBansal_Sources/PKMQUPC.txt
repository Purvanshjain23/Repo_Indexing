/***************************************************************** */
/*                                                                 */
/*  PROGRAM NUMBER:  PKHTUPR                                       */
/*  PROGRAM NAME:    PRINT CARCASS SUMMARY                         */
/*  PROGRAMMER:      MARK ARENSBERG                                */
/*  CREATE DATE:     02/24/97                                      */
/*                                                                 */
/*  FUNCTION:        THIS CL IS CALLED BY THE PAYMENT POST PROGRAM */
/*                   (PKTFXFR) AND THE PRINT CARC DTL & SUM CLP    */
/*                   (PKXCUPR).  THE PURPOSE OF THE CL IS TO       */
/*                   CHANGE THE OVERRIDE ON THE PRINT FILE FOR     */
/*                   FAXING OR FOR REPORTING.                      */
/*                   USER OPTIONS ARE PASSED THROUGH THE PARMS.    */
/*                                                                 */
/*******************************************************************/
/*  Modification History                                           */
/*                                                                 */
/*  SLM     03/21/97 DO NOT ALLOW PROGRAMERS TO FAST FAX ON        */
/*          EITHER LITTBITE OR BIGBYTE                             */
/*                                                                 */
/*******************************************************************/
             PGM        PARM(&RTN &XCOMP &XBYORD &XLOAD &XTATTOO +
                             &ANAL &PDEV1 &HOLD &SAVE &XCOPIES +
                             &XPRDVN# &XPRDLCVN# &FXPROD &FXPRDLC +
                             &IGNFAX)
/* INPUT PARMS                                             */
             DCL        VAR(&RTN) TYPE(*CHAR) LEN(7)
             DCL        VAR(&PDEV1) TYPE(*CHAR) LEN(10)
             DCL        VAR(&HOLD) TYPE(*CHAR) LEN(4)
             DCL        VAR(&SAVE) TYPE(*CHAR) LEN(4)
             DCL        VAR(&XCOMP) TYPE(*CHAR) LEN(15)
             DCL        VAR(&XBYORD) TYPE(*CHAR) LEN(15)
             DCL        VAR(&XLOAD) TYPE(*CHAR) LEN(15)
             DCL        VAR(&XTATTOO) TYPE(*CHAR) LEN(15)
             DCL        VAR(&XPRDVN#) TYPE(*CHAR) LEN(15)
             DCL        VAR(&XPRDLCVN#) TYPE(*CHAR) LEN(15)
             DCL        VAR(&XCOPIES) TYPE(*CHAR) LEN(15)
             DCL        VAR(&ANAL) TYPE(*CHAR) LEN(1)
             DCL        VAR(&FXPROD) TYPE(*CHAR) LEN(1)
             DCL        VAR(&FXPRDLC) TYPE(*CHAR) LEN(1)
             DCL        VAR(&IGNFAX) TYPE(*CHAR) LEN(1)
             DCL        VAR(&GRPAUT) TYPE(*CHAR) LEN(10)

/* NUMERIC VARIABLES                                             */
             DCL        VAR(&COPIES) TYPE(*DEC) LEN(2 0)
             DCL        VAR(&COMP) TYPE(*DEC) LEN(3 0)
             DCL        VAR(&BYORD) TYPE(*DEC) LEN(5 0)
             DCL        VAR(&LOAD) TYPE(*DEC) LEN(5 0)
             DCL        VAR(&TATTOO) TYPE(*DEC) LEN(5 0)
             DCL        VAR(&PRDVN#) TYPE(*DEC) LEN(8 0)
             DCL        VAR(&PRDLCVN#) TYPE(*DEC) LEN(8 0)

/* CHANGE ALPHA PARMS TO NUMERIC VARIABLES                       */
             CHGVAR     VAR(&COPIES) VALUE(&XCOPIES)
             CHGVAR     VAR(&COMP) VALUE(&XCOMP)
             CHGVAR     VAR(&BYORD) VALUE(&XBYORD)
             CHGVAR     VAR(&LOAD) VALUE(&XLOAD)
             CHGVAR     VAR(&TATTOO) VALUE(&XTATTOO)
             CHGVAR     VAR(&PRDVN#) VALUE(&XPRDVN#)
             CHGVAR     VAR(&PRDLCVN#) VALUE(&XPRDLCVN#)

             RTVUSRPRF  GRPAUT(&GRPAUT)

/* WHEN TESTING ON LITTLBITE OR BIGBYTE PROGRAMERS WILL     */
/* NOT USE THE FASTFAX ROUTINE                              */

             IF         COND(&GRPAUT *EQ 'QGPMR') THEN(DO)
             CHGVAR     VAR(&FXPROD) VALUE('N')
             CHGVAR     VAR(&FXPRDLC) VALUE('N')
             ENDDO

/* IF THE REPORT IS GOING TO BE FAXED THEN SETUP THE        */
/* OVERRIDE TO THE FAST FAX QUEUE ELSE OVERRIDE FOR PRINTING  */
             IF         COND((&FXPROD *EQ 'Y' *OR &FXPRDLC *EQ 'Y') +
                          *AND (&IGNFAX *EQ 'N')) THEN(DO)

             OVRPRTF    FILE(PKTBPFR$) OUTQ(FASTFAX/FFXOTQIAP)
             ENDDO

             ELSE DO

             OVRPRTF    FILE(PKTBPFR$) OUTQ(&PDEV1) FORMTYPE(*STD) +
                          COPIES(&COPIES) HOLD(&HOLD) SAVE(&SAVE)
             ENDDO

/* PRINT CARCASS SUMMARY                                       */

             CALL       PGM(PKTBPFR) PARM(&RTN &COMP &BYORD &LOAD +
                                          &TATTOO &ANAL)

/*   SEND FAX IF PRODUCER OR PRODUCER LOCATION IS SETUP TO         */
             IF         COND((&FXPROD *EQ 'Y' *OR &FXPRDLC *EQ 'Y') +
                          *AND (&IGNFAX *EQ 'N')) THEN(DO)

             CALL PGM(PKHVXFR) PARM(&RTN &PRDVN# &PRDLCVN# &FXPROD +
                                    &FXPRDLC)

             ENDDO

             DLTOVR *ALL

             ENDPGM
