      *****************  RPG PROGRAM HEADING  ************************
      *
      * ENVIRONMENT: Pork Division
      * SYSTEM:      Datamart--Sales
      * PROGRAM:     SH202 - Datamart Sales: Build Sales Route
      * PROGRAMMER:  Alice
      * CREATED:     03/24/00
      *
      * FUNCTION: This program consolidates a number of files
      *           in the OMS-Order Management System into a Sales Route
      *           Master for Datamart.
      *
      *           This file in the Data Mart will be re-created with
      *           each load of the Data Mart.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * Date      Programmer
      *
      * 09/04/07  LeAnne Ramsey
      *           Recompiled in Production.
      *
      * 09/04/07  LeAnne Ramsey
      *           Added "Company Organization" as an incoming parm.
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
      *
     Fpdjerel1  if   e           k disk
      *  Sales route
      *
      *
     Fcaalrel1  if   e           k disk
      *  Region ... AKA:  Sales Manager Level (Channel)
      *
      *
     Fshp202    uf a e           k disk
      *  Datamart: Sales route
      *
      /EJECT
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      *---------------------------------------------------------------
      *  Named Constants
      *---------------------------------------------------------------
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      *---------------------------------------------------------------
      * Standalone fields
      *---------------------------------------------------------------
      *
      * Parms
      *
     D xxcorg          s                   like(srcorg)
      *
      ****************************************************************
      * Data Structures
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * Mainline
      ****************************************************************
      *
      * Process each record in the Sales Route file.
      *
     C                   dou       *in90 = *on                                  Main do loop
     C                   read      pdjerel1                               90
     C                   if        *in90 = *off                                 If not EOF
      *
      * We want 1 record per Sales Route Code in our Datamart Item Master...
      * not one record per company/route.  Move a B=Both into the Company
      * Organization field if the existing record is not the same
      * Company Organization as the new record. Always update the existing
      * record.
      *
     C     jer4cd        chain     shp202                             92
     C                   if        *in92 = *off                                 If already exists
      *
     C                   if        srcorg <> xxcorg
     C                   move      'B'           srcorg
     C                   endif
     C                   update    srrec
     C                   else
      *
      * We will be writing a new record.
      *
     C                   clear                   srrec
     C                   move      xxcorg        srcorg
      *
      * Populate sales route code and concatenate sales route code/description
      * into a single field.
      *
     C                   movel     jer4cd        srsrcd
     C                   eval      srsrds = %trim(srsrcd) + '-' +
     C                                      %trim(jemzna)
      *
      * Retrieve sales channel info associated with this sales route region.
      *
     C                   exsr      $channel
      *
     C                   write     srrec
     C                   endif                                                  If already exists
      *
     C                   endif                                                  If not eof
     C                   enddo                                                  Main do loop
      *
     C                   seton                                        lr
      /EJECT
      *--------------------------------------------------------------------
      * Using Region Code, retrieve Sales Channel info for the sales route
      *--------------------------------------------------------------------
      *
     C     $channel      begsr
      *
     C     jebfcd        chain     caalrel1                           92
     C                   if        *in92 = *off
      *
      * Populate the Sales Channel code. Then, concatenate the code/description
      * into a single description field.
      *
     C                   movel     albfcd        srsccd
     C                   eval      srscds = %trim(albfcd) + '-' +
     C                                        %trim(alauna)
     C                   endif
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Parms
     C     *entry        plist
     C                   parm                    xxcorg
      *
     C                   endsr
      /EJECT
