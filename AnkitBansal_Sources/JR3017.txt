     ******************************************************************
     **
     ** APPLICATION:    JRD ACCRUALS
     ** PROGRAM:        JR3017
     ** DATE:           10/00
     ** PROGRAMMER:     TAE
     **
     ** DESCRIPTION:    CUSTOMER ACCRUALS WORKSHEET
     **
     ** MODIFICATIONS:
     ** 12/21/2007 SZ  DLY029
     **            1. IF NO REC FOUND IN JRCUSTS, USE THE OMS CUSTOMER
     **               FILE TO GET THE CUSTOMER NAME.
     **            2. WILL NO LONGER REQUIRE THE VENDOR INFORMATION.
     ******************************************************************
     H            Y
     FACCRUALSIP  E           K        DISK
     FJRCUSTS IF  E           K        DISK
     FAPMAST  IF  E           K        DISK
     FCABBREL1IF  E           K        DISK
     FLIST132 O   F     132     OF     PRINTER                            HYD3
     F**************************************************
     IACCRUAL     01
     I                                              CUST  L1
     IJRCUST
     I              ID                              IDX
     I              FAX                             FAXX
     ISYSDA1      DS
     I                                        1   7 $NUMB
     I                                        8  55 $NAME
     I                                       56  70 $APPL
     I                                       71 100 $BLANK
     C*****************************************************************
     C*
     C*
     C           *NAMVAR   DEFN           SYSDA1
     C                     IN   SYSDA1
     C           *IN20     IFEQ *OFF
     C                     MOVE *DATE     NEWDTE  80
     C                     TIME           CLOCK  140
     C                     MOVELCLOCK     NEWTIM  40
     C                     EXCPTHEADER
     C                     SETON                     20
     C                     ENDIF
     C*
     C   01      ACCAMT    ADD  ACCTOT    ACCTOT  82
     C   01      ACCAMT    ADD  RPTTOT    RPTTOT  92
     C*
     CL1         CUST      CHAINJRCUSTS              99
     CL1         VENDOR    CHAINAPMAST               98
     C*
     C* DLY029 IF NO REC IN JRCUSTS, GET FROM CABBREP
     C*
     CL1         *IN99     IFEQ *ON
     CL1                   Z-ADDCUST      BBBKC7
     CL1         BBBKC7    CHAINCABBREL1             97
     CL1         *IN97     IFEQ *ON
     CL1                   MOVE *BLANKS   NAME
     CL1                   MOVEL'*INVALID'NAME
     CL1                   ELSE
     CL1                   MOVE *BLANKS   NAME
     CL1                   MOVELBBAYTX    NAME
     CL1                   ENDIF
     CL1                   ENDIF
     C*
     CL1                   EXCPTL1BRK
     CLR                   EXCPTFOOTER
     OLIST132 E  204           HEADER                                     HYD3
     O       OR        OF
     O                                    8 'JR3017R1'
     O                         $NAME     58                               WHL003
     O                         $APPL     75
     O        E  2             HEADER                                     HYD3
     O       OR        OF
     O                                   25 'CUSTOMER ACCRUALS WORK'      HYD3
     O                                   31 'SHEET'
     O                                   41 'AS OF'                       HYD3
     O                         NEWTIM    46                               HYD3
     O                         NEWDTEY   58                               HYD3
     O                                   80 'PAGE'
     O                         PAGE  Z   85
     O        E  1             HEADER                                     HYD3
     O       OR        OF
     O                                   67 'ACCRUAL'
     O        E  2             HEADER                                     HYD3
     O       OR        OF
     O                                    8 'CUSTOMER'
     O                                   21 'NAME'
     O                                   67 'BALANCE'
     O*****                              55 'VENDOR'
     O******                             60 'NAME'
     O                                   92 'AMOUNT TO PAY'
     O        EF 2             L1BRK                                      HYD3
     O                         CUST       8
     O***             N99      NAME      40
     O                         NAME      43
     O                         ACCTOTJB  68
     O******                   VENDOR    54
     O*******         N98      RNAME     86
     O******           98                80 '*INVALID VENDOR*'
     O                                   93 '*              *'
     O        E 2              FOOTER
     O                                   15 'END OF REPORT'
     O                         RPTTOTJB  48
     O                                   50 '**'
