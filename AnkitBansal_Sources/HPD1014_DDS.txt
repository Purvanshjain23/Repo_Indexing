     A*%%TS  SD  20090318  100426  ISLRAMS     REL-V5R4M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD1014
     A*  TITLE:         REVISE TRANSPORATION OFFICE
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  CREATED:       12/22/02
     A*
     A*
     A*  INDICATOR 60 IS ON WHEN IN REVISE MODE
     A*  INDICATOR 61 IS ON WHEN IN CREATE MODE
     A*  INDICATOR 62 IS ON WHEN IN DELETE MODE OR WHEN THE RECORD
     A*               HAS BEEN DELETED OUT FROM UNDER THE USER.
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A N62                                  CF04(04 'PROMPT')
     A  60N62                               CF05(05 'REFRESH')
     A  60N62                               CF11(11 'DELETE')
     A                                      CA12(12 'PREVIOUS')
     A*
     A*---------------------------------------------------------------
     A* RECORD FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP1014R1
     A*%%TS  SD  20090318  100426  ISLRAMS     REL-V5R4M0  5722-WDS
     A                                      SETOF(32)
     A                                      SETOF(33)
     A                                      SETOF(34)
     A                                      SETOF(35)
     A                                      SETOF(36)
     A                                      SETOF(37)
     A                                      SETOF(38)
     A                                      SETOF(39)
     A                                      SETOF(52)
     A                                      SETOF(53)
     A                                      SETOF(54)
     A                                      SETOF(56)
     A                                      SETOF(59)
     A                                      RTNCSRLOC(&RCD &FLD)
     A                                      OVERLAY
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 27'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USR)
     A                                      DSPATR(HI)
     A            D1MODE    R        O  2  2REFFLD(@@MODE)
     A                                      DSPATR(HI)
     A                                  2 24'REVISE TRANSPORTATION OFFICE'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@JOB)
     A                                      DSPATR(HI)
     A                                  4  2'Transportation office code ..'
     A            D1TOCD    R        B  4 34REFFLD(@@TOCD)
     A N60N62N35                            DSPATR(HI)
     A  35                                  DSPATR(PC)
     A  35                                  DSPATR(RI)
     A  60
     AO 62                                  DSPATR(PR)
     A N60N62                               DSPATR(UL)
     A                                  5  2'Transportation office name ..'
     A            D1TONM    R        B  5 34REFFLD(HSRREF/@@TONM *LIBL/HSPREF)
     A                                      CHECK(LC)
     A  37                                  DSPATR(RI)
     A  37                                  DSPATR(PC)
     A  62                                  DSPATR(PR)
     A N62                                  DSPATR(UL)
     A N62N37                               DSPATR(HI)
     A                                  6  2'Default output queue ........'
     A            D1OUTQ    R        B  6 34REFFLD(HSRREF/@@OUTQ *LIBL/HSPREF)
     A  38                                  DSPATR(RI)
     A  38                                  DSPATR(PC)
     A  62                                  DSPATR(PR)
     A N62                                  DSPATR(UL)
     A N62N38                               DSPATR(HI)
     A                                  7  2'Default trucker code ........'
     A N62                              7 32'?'
     A            D1TICD    R        B  7 34REFFLD(HSRREF/@@TICD *LIBL/HSPREF)
     A  33                                  DSPATR(RI)
     A  33
     AO 53                                  DSPATR(PC)
     A  62                                  DSPATR(PR)
     A N62                                  DSPATR(UL)
     A N62N33                               DSPATR(HI)
     A            D1TINM    R        O  7 46REFFLD(HSRREF/@@TINM *LIBL/HSPREF)
     A                                  8  2'Default driver code .........'
     A N62                              8 32'?'
     A            D1DICD    R        B  8 34REFFLD(@@DICD)
     A  39                                  DSPATR(RI)
     A  39
     AO 59                                  DSPATR(PC)
     A  62                                  DSPATR(PR)
     A N62                                  DSPATR(UL)
     A N62N39                               DSPATR(HI)
     A            D1DINM    R        O  8 46REFFLD(HSRREF/@@DINM *LIBL/HSPREF)
     A                                  9  2'Default load crew code ......'
     A N62                              9 32'?'
     A            D1LCCD    R        B  9 34REFFLD(@@LCCD)
     A  34                                  DSPATR(RI)
     A  34
     AO 54                                  DSPATR(PC)
     A  62                                  DSPATR(PR)
     A N62                                  DSPATR(UL)
     A N62N34                               DSPATR(HI)
     A            D1LCDS    R        O  9 46REFFLD(HSRREF/@@LCDS *LIBL/HSPREF)
     A                                 10  2'Default route start point ...'
     A N62                             10 32'?'
     A            D1RSPCD   R        B 10 34REFFLD(HSRREF/@@RSPCD *LIBL/HSPREF)
     A  32                                  DSPATR(RI)
     A  32
     AO 52                                  DSPATR(PC)
     A  62                                  DSPATR(PR)
     A N62                                  DSPATR(UL)
     A N62N32                               DSPATR(HI)
     A            D1RSPDS   R        O 10 46REFFLD(HSRREF/@@RSPDS *LIBL/HSPREF)
     A                                 11  2'Active/inactive status ......'
     A N62                             11 32'?'
     A            D1AIST    R        B 11 34REFFLD(HSRREF/@@AIST *LIBL/HSPREF)
     A  36                                  DSPATR(RI)
     A  36
     AO 56                                  DSPATR(PC)
     A  62                                  DSPATR(PR)
     A N62                                  DSPATR(UL)
     A N62N36                               DSPATR(HI)
     A*
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R HP1014K1
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A    N62                          23 11'F4=Prompt'
     A N62N61                          23 23'F5=Refresh'
     A N62N61                          23 35'F11=Delete'
     A                                 23 45'F12=Previous'
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *---------------------------------------------------------------
     A          R HP1014ES                  SFL SFLMSGRCD(24)
      *
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE CONTROL FORMAT
      *---------------------------------------------------------------
     A          R HP1014EC                  SFLCTL(HP1014ES)
      *
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
