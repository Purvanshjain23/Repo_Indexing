/***************************************************************** */
/*                                                                 */
/*  PROGRAM NUMBER:    HP179CL                                     */
/*  PROGRAM NAME:      DISPOSE HOG GROUPS--PRINT LISTINGS          */
/*  PROGRAMMER:        LEANNER FEDOR                               */
/*  DATE:              10/26/99                                    */
/*                                                                 */
/*  PROGRAM PURPOSE:   PRINT LISTINGS.                             */
/*                                                                 */
/*                     TWO LISTINGS CAN BE PRINTED:                */
/*                       M=MOVEMENTS FOR A SPECIFIC GROUP          */
/*                       R=HOG GROUP REVIEW FOR A SPECIFIC GROUP   */
/*                                                                 */
/*******************************************************************/
/* MODIFICATIONS                                                   */
/*                                                                 */
/* 04/10/08  LeAnne Ramsey                                         */
/*           We were getting level checks from HP686-Hog Group     */
/*           Review over workfile HSP381. Added a CRTDUPOBJ of     */
/*           the workfile into QTEMP.                              */
/*******************************************************************/

             PGM

/* DEFINE THE VARIABLE REQUIRED IN THE COMMAND TO OVERRIDE THE     */
/* PRINT FILE TO A LASER PRINTER                                   */

             DCL        VAR(&CMD) TYPE(*CHAR) LEN(512)

             DCL        VAR(&RPFL) TYPE(*CHAR) LEN(1)
             DCL        VAR(&HOLD) TYPE(*CHAR) LEN(10)
             DCL        VAR(&COPYS) TYPE(*DEC) LEN(2)
             DCL        VAR(&OUTQ) TYPE(*CHAR) LEN(10)

             CHGVAR     VAR(&RPFL) VALUE(%SST(*LDA 51 1))

/*------------------------------------------------------------------*/
/* SET OVERRIDES FOR PRINT FILES                                    */
/*------------------------------------------------------------------*/

 HOLD:       IF         COND(%SST(*LDA 411 1) = 'Y') THEN(CHGVAR +
                          VAR(&HOLD) VALUE(*YES))
             ELSE       CMD(CHGVAR VAR(&HOLD) VALUE(*NO))

 COPYS:      CHGVAR     VAR(&COPYS) VALUE(%SST(*LDA 412 1))
             IF         COND(&COPYS = 0) THEN(CHGVAR VAR(&COPYS) +
                          VALUE(1))

 OUTQ:       CHGVAR     VAR(&OUTQ) VALUE(%SST(*LDA 413 10))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(%SST(*LDA 401 10)))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(*JOB))


/*------------------------------------------------------------------*/
/*  LISTING OF ALL MOVEMENTS FOR A GROUP                            */
/*------------------------------------------------------------------*/

             IF         COND(&RPFL *EQ 'M') THEN(DO)

/* USE THE CUSTOMIZED SEABOARD COMMAND TO OVERRIDE THE PRINT FILE.  */
/* THIS REPORT CAN FIT IN 80 CHARACTERS AT 10 CPI.                  */

             SBDPRTOVR  CMDVAR(&CMD) FILE(QPRINT) OUTQ(&OUTQ) +
                          WIDTH(80) COPIES(&COPYS) HOLD(&HOLD)
             CALL       PGM(QCMDEXC) PARM(&CMD 512)

/*  RUN PROGRAM TO PRINT LISTING                                    */

             CALL       PGM(HP630)
             ENDDO

/*------------------------------------------------------------------*/
/*  HOG GROUP REVIEW                                                */
/*------------------------------------------------------------------*/

             IF         COND(&RPFL *EQ 'R') THEN(DO)

/* CREATE WORKFILES                                                 */

             CRTDUPOBJL OBJ(HSP381) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)
             CALL       PGM(HP381)             /* Movement Workfile */

             CRTDUPOBJL OBJ(HSP386) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)
             CALL       PGM(HP386)          /* Feed Ticket Workfile */

/* USE THE CUSTOMIZED SEABOARD COMMAND TO OVERRIDE THE PRINT FILE.  */
/* THIS REPORT IS CONDENSED PRINT AT 15 CPI; SO, ON A LASER PRINTER */
/* FORCE IT TO DRAWER '2' FOR LEGAL PAPER AND FORCE 'LANDSCAPE'     */

             SBDPRTOVR  CMDVAR(&CMD) FILE(QPRINT) OUTQ(&OUTQ) +
                          WIDTH(198) LANDSCAPE(*YES) DRAWER(2) +
                          COPIES(&COPYS) HOLD(&HOLD)

             CALL       PGM(QCMDEXC) PARM(&CMD 512)

/*  RUN PROGRAM TO PRINT HOG GROUP REVIEW                          */

             CALL       PGM(HP686)
             ENDDO

             DLTOVR     FILE(*ALL)

             ENDPGM

