      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      HPS Data Mart
      * PROGRAM:     HP210 - Delete Current Records
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     02/07/01
      *
      * Function:    The build for the Finishers was taking 3 to 4 hours.
      *              So, we have implemented logic that allows us to rebuild only the
      *              'current' data (as identified in the data area DAFIN.)
      *
      *              This program is conditionally called from HPBLDFIN.
      *
      *              If HPBLDFIN is submitted with an incoming parm of ' ' instead of
      *              H=history, this program will be called to delete the 'current'
      *              records out of Datamart files prior to building the data.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 05/30/02  LeAnne Fedor
      *           Expanded 'current' logic for additional files other than the
      *           original 2 weekly files.
      *
      * 08/01/02  LeAnne Fedor
      *           Increased DAFIN data area by 1. 'Days' is now 3 long instead of 2.
      *
      * 10/23/02  LeAnne Fedor
      *           Added 'current' logic for new Reported Daily Activity file.
      *
      * 10/22/03  Barb Gutierrez
      *           Removed processing of file HPPF033.  We're no longer using it.
      *
      * 10/11/04  LeAnne Fedor
      *           Added a 'submitted flag' position to the data area DAFIN.
      *
      * 10/27/04  LeAnne Fedor
      *           Recompile only. New field "3-week average dead" added to files:
      *               Datamart FIN: Weekly Group Detail
      *               Datamart FIN: Weekly Farm/Building/Room
      *
      * 11/29/04  LeAnne Fedor
      *           Recompile only. Two new fields added to Datamart FIN: Reported
      *           Daily Activity:
      *               Daily farm phase
      *               Origin BGF pod
      *
      * 02/01/05  LeAnne Fedor
      *           Recompile only. Two new fields (sire line company, prime line)
      *           added to Sire Line Master file and Datamart files.
      *
      * 03/21/05  Barb Gutierrez
      *           Recompile only. Two new fields (origin BGF Pod and Sire line code)
      *           added to HPPF088.  And two new fields (culled head and culled lbs)
      *           added to HPPF103.
      *
      * 08/25/05  LeAnne Fedor
      *           Recompile only.
      *           Added a new field "Man Hours" to Weekly Group Detail file.
      *
      * 10/03/05  LeAnne Fedor
      *           Recompile only.
      *           Added a new field "Avg Prior Week Dead Pounds" to the
      *           Datamart FIN: Reported Daily Activity file.
      *
      * 08/02/06  LeAnne Fedor
      *           Recompile only. Two new fields (ration 8 feed pounds, ration paylean flag)
      *           added to Datamart Group Header file.
      *
      * 08/10/07  LeAnne Ramsey
      *           Recompile only. New "target" fields renamed in the Datamart Group
      *           Header file.
      *
      * 09/19/07  LeAnne Ramsey
      *           Recompile only. New "target" fields added to the Datamart FIN files:
      *              1) Weekly Group Detail
      *              2) Group Header
      *
      * 04/07/09  LeAnne Ramsey
      *           We are deleting file HPPF088-Datamart FIN: Farm Deaths with Reasons; it
      *           is no longer being used.
      *
      * 05/19/09  LeAnne Ramsey
      *           Recompile only. Field "projected destroyed head" was removed from
      *           Reported Daily Activity.
      *
      * 05/24/10  LeAnne Ramsey
      *           Recompile only. Added 2 fields to HPPF075-Weekly Group Detail:
      *              WBWEDTMDCY-Week Ending Date in date format mm/dd/ccyy
      *              WBPAJTIHD -Placement Adjusted Transfer In Head
      *
      * 10/18/10  LeAnne Ramsey  (E1079)
      *           Recompile only. Sire Line Company is now 2-characters instead
      *           of one.
      *
      * 10/14/16  Barb Gutierrez
      *           Recompile only. Added fields Sold days in phase, Avg Sold days in phase,
      *           Wgt open date, and company number to HPFF034.         E6136
      *
      * 11/14/17  Danny Nguyen     (E11978 - DM Add Origin Sow Cell to NurFin Daily File)
      *           Recompile only. Added 1 field to HPPF103 file:
      *              RDOBCELL - Origin BGF Cell
      *
      /eject
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fhppf034   uf   e           k disk
      *    Datamart FIN: Group header
      *
      *
     Fhplf103b  uf   e           k disk
      *  Datamart FIN: Reported Daily Activity
      *
      *
     Fhplf175a  uf   e           k disk
      *  DataMart FIN: Weekly Farm/Building/Room
      *
      *
     Fhplf075a  uf   e           k disk
      *    DataMart FIN: Weekly Group Detail
      *
      /eject
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
     D wkwkbegdt       s               d   datfmt(*iso)
     D wkwkenddt       s               d   datfmt(*iso)
     D wknulldt        s                   like(hgcldt) inz(D'0001-01-01')
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *---------------------------------------------------------------
      * Definition for external data area DAFIN
      *---------------------------------------------------------------
     D
     Ddafin            ds                  dtaara(dafin)
     D  dacwkbegdt            29     36  0
     D  dacwkenddt            38     45  0
     D  dadays                47     49  0
     D  dasbmfl               62     62
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * Mainline
      ****************************************************************
      *
      * Reported Daily Activity
      * (Logic deletes records where the record week-ending date is equal to
      * or greater than the data area week-ending date.)
      *
     C     wkwkenddt     setll     hplf103b
      *
     C                   dou       *in90 = *on                                  Do no reasons
     C                   read      hplf103b                               90
     C                   if        *in90 = *off                                 If not EOF
     C                   delete    rdrec
     C                   endif                                                  If not EOF
     C                   enddo                                                  Do no reasons
      *
      *
      * Weekly Group Detail file
      * (Logic deletes records where the record week-ending date is equal to
      * or greater than the data area week-ending date.)
      *
     C     wkwkenddt     setll     hplf075a
      *
     C                   dou       *in90 = *on                                  Do weekly group
     C                   read      hplf075a                               90
     C                   if        *in90 = *off                                 If not EOF
     C                   delete    wbrec
     C                   endif                                                  If not EOF
     C                   enddo                                                  Do weekly group
      *
      *
      * Weekly Farm/building/room file
      * (Logic deletes records where the record week-ending date is equal to
      * or greater than the data area week-ending date.)
      *
     C     wkwkenddt     setll     hplf175a
      *
     C                   dou       *in90 = *on                                  Do farm
     C                   read      hplf175a                               90
     C                   if        *in90 = *off                                 If not EOF
     C                   delete    wfrec
     C                   endif                                                  If not EOF
     C                   enddo                                                  Do  farm
      *
      *
      * Hog groups
      * (Logic deletes all groups that a) are not closed or b) have a closed
      * date on/after the data area week-beginning date.)
      *
     C     *loval        setll     hppf034
      *
     C                   dou       *in90 = *on                                  Do groups
     C                   read      hppf034                                90
     C                   if        *in90 = *off and                             If not EOF
     C                             (hgcldt = wknulldt or hgcldt >= wkwkbegdt)
     C                   delete    hgrec
     C                   endif                                                  If not EOF
     C                   enddo                                                  Do  groups
      *
     C                   seton                                        lr
      /eject
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Bring in the data area that contains the week-beginning date and the week-ending
      * date for the first week of 'current' data.
      *
     C                   in        dafin
      *
     C                   movel     dacwkbegdt    wkwkbegdt
     C                   movel     dacwkenddt    wkwkenddt
      *
     C                   endsr
      /eject
