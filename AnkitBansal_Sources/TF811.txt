      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Triumph Foods
      * PROGRAM:     TF811
      * TITLE:       Retrieve Saturday Week-ending Date
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     06/08/05
      *
      * FUNCTION:  Given an incoming date, this program retrieves the Saturday week-ending
      *            date and the Year/Period/Week associated with the Saturday.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 12/06/06  LeAnne Ramsey
      *           We changed the file name/text on the Calendar file.
      *
      * 09/10/07  LeAnne Ramsey
      *           We changed the file name/text on the Calendar file.
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fcalcald   if   e           k disk
      *  Calendar file
      *
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      *
      ****************************************************************
      * ARRAYS AND TABLES
      ****************************************************************
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Work fields
      *
     D wkcocd          s              3  0 inz(350)
      *
      * Parms
      *
     D xxdt            s              8  0
     D xxsatdt         s              8  0
     D xxyr            s              4  0
     D xxpe            s              2  0
     D xxwk            s              2  0
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * Mainline
      ****************************************************************
      *
      * First, see if the incoming date exists in the Calendar file.
      * If it does, continue.
      *
     C     key01         chain     calcald                            92
     C                   if        *in92 = *off                                 If good incoming
      *
     C     key01         setll     calcald
      *
     C                   dou       *in90 = *on or catdtdow = 7                  Do loop
     C     wkcocd        reade     calcald                                90
     C                   if        *in90 = *off                                 If not eof
      *
     C                   if        catdtdow = 7
     C                   z-add     catdt08       xxsatdt
     C                   z-add     cahpsyr       xxyr
     C                   z-add     caaper        xxpe
     C                   z-add     cahpswk       xxwk
     C                   endif
      *
     C                   endif                                                  If not eof
     C                   enddo                                                  Do loop
     C                   endif                                                  If good incoming
      *
     C                   seton                                        lr
      /EJECT
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Parm lists
      *
     C     *entry        plist
     C                   parm                    xxdt
     C                   parm                    xxsatdt
     C                   parm                    xxyr
     C                   parm                    xxpe
     C                   parm                    xxwk
      *
      * Key lists
      *
     C     key01         klist
     C                   kfld                    wkcocd
     C                   kfld                    xxdt
      *
      *
     C                   endsr
      /EJECT
