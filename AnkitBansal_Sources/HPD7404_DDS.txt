     A*%%TS  SD  20090429  091835  ISLRAMS     REL-V5R4M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD7404
     A*  TITLE:         VIEW UPLOAD STATUS
     A*  PROGRAMMER:    LEANNE RAMSEY
     A*  DATE:          11/11/08
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A                                      CF05(05 'REFRESH')
     A*
     A*---------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP7404S1                  SFL
     A*%%TS  SD  20081111  161955  ISLRAMS     REL-V5R4M0  5722-WDS
     A  85                                  SFLNXTCHG
     A            HDRRN1         4Y 0H
     A            D1HUCD    R        O  9  3REFFLD(HSRREF/@@HUCD *LIBL/HSPREF)
     A            D1HUDS    R        O  9  7REFFLD(HSRREF/@@HUDS *LIBL/HSPREF)
     A            D1UPLSCD  R        O  9 38REFFLD(HSRREF/@@UPLSCD *LIBL/HSPREF)
     A            D1CRDTMDY R        O  9 41REFFLD(HSRREF/@@DT06 *LIBL/HSPREF)
     A                                      EDTWRD('  /  /  ')
     A            D1CRTM    R        O  9 50REFFLD(HSRREF/@@CRTM *LIBL/HSPREF)
     A                                      EDTWRD('  :  :  ')
     A            D1CRUSR   R        O  9 59REFFLD(HSRREF/@@CRUSR *LIBL/HSPREF)
     A            D1PGM     R        O  9 70REFFLD(HSRREF/@@PGM *LIBL/HSPREF)
     A*
     A*
     A*---------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP7404C1                  SFLCTL(HP7404S1)
     A*%%TS  SD  20090429  091835  ISLRAMS     REL-V5R4M0  5722-WDS
     A                                      SFLSIZ(0050)
     A                                      SFLPAG(0013)
     A N82                                  ROLLUP(25 'PAGE FORWARD')
     A                                      OVERLAY
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 28'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  2 29'VIEW UPLOAD STATUS'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  3  6'Co:'
     A            C1COCD    R        O  3 11REFFLD(HSRREF/@@COCD *LIBL/HSPREF)
     A                                  4  4'Year:'
     A            C1ACYR    R        O  4 10REFFLD(HSRREF/@@ACYR *LIBL/HSPREF)
     A                                      EDTCDE(M)
     A                                  5  2'Period:'
     A            C1ACPE    R        O  5 12REFFLD(HSRREF/@@ACPE *LIBL/HSPREF)
     A                                      EDTCDE(M)
     A            C1BPMDY   R     Y  O  5 16REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A                                  5 26'TO'
     A            C1EPMDY   R     Y  O  5 29REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A                                  7 38'Up'
     A                                      DSPATR(HI)
     A                                  7 41'--------- Last Status Change ------
     A                                      -----'
     A                                      DSPATR(HI)
     A                                  8  2'Code'
     A                                      DSPATR(HI)
     A                                  8  7'Description'
     A                                      DSPATR(HI)
     A                                  8 38'St'
     A                                      DSPATR(HI)
     A                                  8 43'Date'
     A                                      DSPATR(HI)
     A                                  8 52'Time'
     A                                      DSPATR(HI)
     A                                  8 59'User'
     A                                      DSPATR(HI)
     A                                  8 70'Program'
     A                                      DSPATR(HI)
     A*
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R HP7404K1
     A*%%TS  SD  20090428  110827  ISLRAMS     REL-V5R4M0  5722-WDS
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A                                 23 11'F5=Refresh'
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *---------------------------------------------------------------
     A          R HP7404ES                  SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE CONTROL FORMAT
      *---------------------------------------------------------------
     A          R HP7404EC                  SFLCTL(HP7404ES)
      *
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
