/* PROGRAM:      PWKIUPC                                                  */
/* PROGRAM NAME: Exc Save/Rmv CS UPL MbrCL Execute user program           */
/* PROGRAMMER:   Danny Nguyen                                             */
/* CREATE DATE:  03/18/2020                                               */
/* PROJECT:      S16293                                                   */
/*                                                                        */
/* PURPOSE:      Process will save All Members from PLARSVxxx (where xxx  */
/*               is GUP,STP,TFP) to PRKFSAVF library for Co 360/440/960 & */
/*               remove all the Members for each Company.                 */
/*               This is done to prevent from creating duplicate Member   */
/*               Name in the file.                                        */
/*                                                                        */
/* Called From:  For PROD - JS 'HPECLDSCL' ; Runs EOY Annually 12/31.     */
/*               For TEST - JS 'HPECLDSCLT'.                              */
/*                                                                        */
/* PARAMETERS:  &COMP - Passed in the Company to process.                 */
/*                                                                        */
/**************************************************************************/
/* MODIFICATION HISTORY:                                                  */
/* DATE     INT  PROJECT DESCRIPTION                                      */
/* -------- ---- ------- -------------------------------------------------*/
/* MM/DD/YY                                                               */
/**************************************************************************/

             PGM        PARM(&COMP)

             DCL        VAR(&COMP) TYPE(*DEC) LEN(3 0)

             DCL        VAR(&LIBNNN) TYPE(*CHAR) LEN(10)
             DCL        VAR(&FLIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&HLIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&SAVTOLIB) TYPE(*CHAR) LEN(10)


/*--------------------------------------------------------------------*/
/* Determine if in PROD or TEST Environment to Set Backup Save Lib.   */
/*--------------------------------------------------------------------*/
             CHGVAR     VAR(&SAVTOLIB) VALUE('PRKFSAV')    /* PROD LIB */

             CALL       PGM(CAQXXFR) PARM(' ' &FLIB &HLIB)

             IF         COND(&FLIB *NE 'PRKFLIB') THEN(DO)
             CHGVAR     VAR(&SAVTOLIB) VALUE('TPMTESTF') /* TEST LIB */
             ENDDO

/*--------------------------------------------------------------------*/
/* Get HPE Data Library Per Plant in PARM & Change Current Library.   */
/*--------------------------------------------------------------------*/
             CALL       PGM(POMTXFR) PARM(' ' &COMP 'HPELIB   ' +
                          &LIBNNN)

             CHGCURLIB  CURLIB(&LIBNNN)

    /* Select To Process By Company */
             SELECT
             WHEN       COND(&COMP *EQ 360) THEN(GOTO CMDLBL(GUY360))
             WHEN       COND(&COMP *EQ 440) THEN(GOTO CMDLBL(STF440))
             WHEN       COND(&COMP *EQ 960) THEN(GOTO CMDLBL(TF960))
             OTHERWISE  CMD(GOTO CMDLBL(END))
             ENDSELECT

/*--------------------------------------------------------------------*/
/*Company 360 - Guymon, OK - Seaboard Foods                          */
/*               Copy All Members to Save Library & Then Remove All   */
/*               Members Starting with 'G*'.                          */
/*--------------------------------------------------------------------*/
 GUY360:     CPYF       FROMFILE(&LIBNNN/PLARSVGUP) +
                          TOFILE(&SAVTOLIB/PLARSVGUP) FROMMBR(*ALL) +
                          MBROPT(*REPLACE) CRTFILE(*YES)
             MONMSG     MSGID(CPC2957) /* NO RECORDS IN FILE */
             MONMSG     MSGID(CPF2817) /* NO RECORDS IN FILE */

             RMVM       FILE(&LIBNNN/PLARSVGUP) MBR(G*)

             GOTO       CMDLBL(END)

/*--------------------------------------------------------------------*/
/*Company 440 - Sioux City, IA - Seaboard Triumph Foods              */
/*               Copy All Members to Save Library & Then Remove All   */
/*               Members Starting with 'S*'.                          */
/*--------------------------------------------------------------------*/
 STF440:     CPYF       FROMFILE(&LIBNNN/PLARSVSTP) +
                          TOFILE(&SAVTOLIB/PLARSVSTP) FROMMBR(*ALL) +
                          MBROPT(*REPLACE) CRTFILE(*YES)
             MONMSG     MSGID(CPC2957) /* NO RECORDS IN FILE */
             MONMSG     MSGID(CPF2817) /* NO RECORDS IN FILE */

             RMVM       FILE(&LIBNNN/PLARSVSTP) MBR(S*)

             GOTO       CMDLBL(END)

/*--------------------------------------------------------------------*/
/*Company 960 - Saint Joseph, MO - Triumph Foods                     */
/*               Copy All Members to Save Library & Then Remove All   */
/*               Members Starting with 'T*'.                          */
/*--------------------------------------------------------------------*/
 TF960:      CPYF       FROMFILE(&LIBNNN/PLARSVTFP) +
                          TOFILE(&SAVTOLIB/PLARSVTFP) FROMMBR(*ALL) +
                          MBROPT(*REPLACE) CRTFILE(*YES)
             MONMSG     MSGID(CPC2957) /* NO RECORDS IN FILE */
             MONMSG     MSGID(CPF2817) /* NO RECORDS IN FILE */

             RMVM       FILE(&LIBNNN/PLARSVTFP) MBR(T*)


 END:        ENDPGM
