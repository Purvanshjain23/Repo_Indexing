/***************************************************************** */
/*                                                                 */
/*  PROGRAM NUMBER:  PBAXUPC                                       */
/*  PROGRAM NAME:    DELETE WF EXPORT HEADER RECORDS               */
/*                   CALL PGM TO DELETE MATCHING DETAIL RECORDS.   */
/*                   THIS JOB IS SUBMITTED BY SSIS FROM THE        */
/*                   NETWORK SERVER                                */
/*                                                                 */
/*  PROGRAMMER:      LARA BUSER                                    */
/*  CREATE DATE:     11/22/2011                                    */
/*  FUNCTION:        READS JAPANESE ORDERS HEADER WORK FILE.       */
/*                   CALLS A PGM TO DELETE THE DETAIL RECORDS      */
/*                   FOR EACH HEADER RECORD. WHEN ALL DETAIL       */
/*                   RECORDS ARE DELETED, THEN ALL THE HEADER      */
/*                   RECORDS FOR THIS COMPANY ARE DELETED.         */
/*  PARAMETERS:                                                    */
/*    &COMPYA     3    COMPANY NUMBER *CHAR                        */
/*    &BROKER     6    BROKER CODE                                 */
/*    &RTNCODE    7    RETURN CODE                                 */
/*******************************************************************/
/* 10/07/2013 LJB E7358   CHANGED FOR TESTING IN PRKTEST           */
/*              ****  DON'T INSTALL THIS VERSION  *****            */
/* RMC 07/18/16   E004201 GET READY FOR PROMOTE TO PROD            */
/* 08/15/2014 LJB E003326 ADD BROKER CODE TO THE INPUT PARMS       */
/*            RESTRICT PROCESSING BY BROKER AND COMPANY            */
/* 10/10/2013 LJB E002823 NO CHANGES WERE MADE. I JUST UNCOMMENTED */
/*            PRKDEVQRY SO I COULD RUN TESTS                       */
/*            DEVELOPMENT LIB SO I COULD RUN TESTS.                */
/* 10/29/2020 DN  R16637 - PNHGCPL1 FILE CHANGED.                  */
/*                         REPLACED PRKEOA WITH PDINETCT DL.       */
/*                         PRKEOA DL DOES NOT EXIST IN ESEND NOR   */
/*                         HALCYON.                                */
/* 01/28/2021 DN  R17013 - DBFC ON WF EXPORT UPLOAD HEADER         */
/*                         (PNHGCPP) FILE. RECOMPIILE ONLY.        */
/*******************************************************************/
             PGM        PARM(&COMPYA &BROKER)

             DCL        VAR(&RTNCODE) TYPE(*CHAR) LEN(7)
             DCL        VAR(&RTNHDR)  TYPE(*CHAR) LEN(7)
             DCL        VAR(&COMPY) TYPE(*DEC) LEN(3 0)
             DCL        VAR(&COMPYA) TYPE(*CHAR) LEN(3)
             DCL        VAR(&CUSTOMERN) TYPE(*DEC)
             DCL        VAR(&BROKER) TYPE(*CHAR) LEN(6)

/* MAINLINE                                                        */
             DCLF       FILE(PNHGCPL1)

             MONMSG     MSGID(CPF0000)

/**** FOR TEST ONLY                              +
      ADDLIBLE PRKDEVQRY *LAST                    */

/* ONLY PROCESS RECORDS THAT MATCH THE COMPANY PARAMETER. WHEN ALL  */
/* DETAIL RECORDS HAVE BEEN DELETED DROP DOWN AND DELETE THE HEADER */
/* RECORDS FOR THIS COMPANY. ALSO, MATCH BY BROKER CODE   E003326   */

             CHGVAR     VAR(&COMPY) VALUE(000)
             CHGVAR     VAR(&COMPY) VALUE(&COMPYA)
             MONMSG     MSGID(CPF9999 CPF0818)

 NEXTREC:    RCVF
             MONMSG     MSGID(CPF0864) EXEC(GOTO CMDLBL(END)) /* EOF */

/* ONLY DELETE RECORDS FOR REQUESTED COMPANY                         */
             IF         COND(&HGAIC3 *NE &COMPY) THEN(GOTO +
                          CMDLBL(NEXTREC))

/* ONLY DELETE RECORDS FOR REQUESTED BROKER                          */
             IF         COND(&HGHHAA *NE &BROKER) THEN(GOTO +
                          CMDLBL(NEXTREC))

/* CALL PGM TO DELETE RECORDS FROM THE DETAIL WORK FILE            */
/* (EXECUTES VIEW: PNHHCPPV1)                                      */
/* WF EXPORT UPLOAD DETAIL   CLP SQL DELETE DTL REC CL             */
             CHGVAR     VAR(&CUSTOMERN) VALUE(0000000)
             CHGVAR     VAR(&CUSTOMERN) VALUE(&HGBKC7)
             CHGVAR     VAR(&RTNCODE) VALUE('       ')
             CALL       PGM(PBAWUPC) PARM(&CUSTOMERN &HGBCNA +
                          &RTNCODE)

/* IF ERROR DELETING THE DETAIL RECORDS THEN DON'T DELETE THE HEADER */
             IF (&RTNCODE *NE '      ') THEN(DO)
             CHGVAR     VAR(&RTNHDR) VALUE('SQLHERR')
    /* R16637 - REPLACED DL PRKEOA WITH PDINETCT DL. */
             SNDPGRMSG  TOPGR(PDINETCT) MSG('Automated Order Upload +
                          Detail Work File Record(s) were not +
                          deleted for Company/Customer PO:' *CAT +
                          &COMPYA *BCAT &HGBCNA *TCAT '. Record(s) +
                          may need to be deleted manually.') +
                          CONFMSG(*NO)
             CHGVAR     VAR(&RTNCODE) VALUE('       ')
             CHGJOB     LOG(4 00 *SECLVL)
             ENDDO

             GOTO       CMDLBL(NEXTREC)

/* Delete from HEADER WORK FILE                                    */

 END:        IF         COND(&RTNHDR *EQ '      ') THEN(DO)
             DELETE     VIEW(PNHGCPPV2) SETVAR((&COMPY &COMPYA) +
                          (&BROKER &BROKER)) OPTIMIZE(*MINWAIT)

             MONMSG     MSGID(CPF0000) EXEC(DO)
    /* R16637 - REPLACED DL PRKEOA WITH PDINETCT DL. */
             SNDPGRMSG  TOPGR(PDINETCT) MSG('Automated Order Upload +
                          Header Work File Record(s) were not +
                          deleted for Company: ' *CAT &COMPYA *BCAT +
                          '. Record(s) may need to be deleted +
                          manually.') CONFMSG(*NO)
             CHGJOB     LOG(4 00 *SECLVL)
             ENDDO
             ENDDO

             IF         COND(&RTNHDR *NE '      ') THEN(DO)
    /* R16637 - REPLACED DL PRKEOA WITH PDINETCT DL. */
             SNDPGRMSG  TOPGR(PDINETCT) MSG('Automated Order Upload +
                          Header Work File Record(s) were not +
                          deleted for Company: ' *CAT &COMPYA *BCAT +
                          'because there were errors deleting +
                          Detail Record(s).') CONFMSG(*NO)
             CHGJOB     LOG(4 00 *SECLVL)
             ENDDO

 ENDPGM:     ENDPGM
