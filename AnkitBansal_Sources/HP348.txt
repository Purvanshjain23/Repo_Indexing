      *****************  RPG PROGRAM HEADING  ************************
      * SYSTEM:      HOG PRODUCTION
      * PROGRAM:     HP348
      * TITLE:       BUILD WORKFILE FOR ANNUAL SALES FOR TAX REPORT
      * PROGRAMMER:  LEANNE FEDOR
      * CREATED:     03/15/96
      *
      * FUNCTION:    THIS PROGRAM BUILDS A WORKFILE TO BE READ IN:
      *              HP648  ANNUAL SALES FOR TAX REPORT
      *
      *              THIS PROGRAM IS SUBMITTED FROM HP448CL AND
      *              USER SELECTIONS ARE PASSED IN THE LDA.
      *
      *              THE CUSTOMER STATE IS RETRIEVED FROM J.D. EDWARDS.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 02/16/98  LeAnne Fedor
      *           Recompile only. Added the following fields to the
      *           database for sales movement detail and check detail:
      *                     condemned head and pounds
      *                     yard dead head and pounds
      *
      * 01/11/99  LeAnne Fedor
      *           Recompile only. New field 'scheduled kill date' added
      *           to sales movement header file.
      *
      * 10/17/00  LeAnne Fedor
      *           Recompile only. New field 'load type' added to the sales
      *           movement head file.
      *
      * 01/21/21  ISE             TSN593
      *           Replaced *loval with *start
      *
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     FHSL064B   IF   E           K DISK
      *    CHECK DETAIL              (KEY: CDMVSN CDHGSN)
      *
     FHSP084    IF   E           K DISK
      *  SALE HEADER                 (KEY: SHFSBO SHCVNO)
      *  (RECORDS SELECTED AND KEYED IN OPEN QUERY)
      *
     FHSP347    UF A E           K DISK
      *                              (KEY: WCCVNO)
      *  ANNUAL SALES FOR TAX REPORT WORKFILE
      *  (CUSTOMERS WITHOUT STATES)
      *
     FHSP348    UF A E           K DISK
      *                              (KEY: WFFSBO WFSTAT)
      *  ANNUAL SALES FOR TAX REPORT WORKFILE
      *  (GROSS SALES DOLLARS)
      *
      /EJECT
      ****************************************************************
      * INPUT SPECS
      ****************************************************************
      *
      *----------------------------------------------------------------
      * LDA
      *----------------------------------------------------------------
      *
     D                UDS
     D  LDFSBO                 1      5
     D  LDFYMD                 6     13  0
     D  LDTYMD                14     21  0
     D  LDCCYY                22     25  0
     D  LDBODS                72    101
      *
     D  DUMMYD                 1      1
      *
      *---------------------------------------------------------------
      *  NAMED CONSTANTS
      *---------------------------------------------------------------
      *
     D YES             C                   CONST('Y')
     D NO              C                   CONST('N')
      *  THE PRECEDING DUMMY DATA STRUCTURE IS ONLY TO KEEP THE I-SPEC
      *  INFO FROM RUNNING OVER INTO THE 'CALC SPECS' ON THE COMPILE
      *  LISTING.  IT SERVES NO OTHER PURPOSE AND CAN BE DELETED
      *  AT ANY TIME!!!
      /EJECT
      ****************************************************************
      * MAIN LINE
      ****************************************************************
      *
      * PROCESS EACH SALES MOVEMENT SELECTED BY THE OPEN QUERY
      *
593  C*    *LOVAL        SETLL     HSP084
593  C     *START        SETLL     HSP084
      *
     C     *IN90         DOUEQ     *ON                                          MAIN DO
     C                   READ      HSP084                                 90
     C     *IN90         IFEQ      *OFF                                         IF NOT EOF
      *
      * IF THE CUSTOMER NUMBER CHANGES, RETRIEVE THE STATE
      * FOR THE CUSTOMER FROM J.D. EDWARDS.
      *
     C     SHCVNO        IFNE      SVCVNO                                       IF DIFF CUST
     C                   EXSR      $CUST
     C                   ENDIF                                                  IF DIFF CUST
      *
      * READ EACH CHECK DETAIL RECORD ASSOCATED WITH THIS
      * MOVEMENT TO ACCUMULATE GROSS DOLLARS.
      *
     C     SHMVSN        SETLL     HSL064B
     C                   Z-ADD     0             WKGRAM
      *
     C     *IN93         DOUEQ     *ON                                          DO CHECKS
     C     SHMVSN        READE     HSL064B                                93
     C     *IN93         IFEQ      *OFF                                         IF CHECKS
     C                   ADD       CDGRAM        WKGRAM
     C                   ENDIF                                                  IF CHECKS
     C                   ENDDO                                                  DO CHECKS
      *
      * WRITE/UPDATE THE WORKFILE RECORD OF GROSS SALES
      * BY STATE FOR THE MOVEMENT YOU HAVE JUST PROCESSED.
      *
     C     K1P348        CHAIN     HSP348                             92
     C     *IN92         IFEQ      *OFF                                         IF FOUND
     C                   ADD       WKGRAM        WFGRAM
     C                   UPDATE    WFREC
     C                   ELSE
     C                   MOVEL     SHFSBO        WFFSBO
     C                   MOVEL     XXSTAT        WFSTAT
     C                   Z-ADD     WKGRAM        WFGRAM
     C                   WRITE     WFREC
     C                   ENDIF                                                  IF FOUND
      *
     C                   ENDIF                                                  IF NOT EOF
     C                   ENDDO                                                  MAIN DO
      *
     C                   SETON                                        LR
      /EJECT
      *----------------------------------------------------------------
      * $CUST - RETRIEVE CUSTOMER'S STATE FROM J.D. EDWARDS
      *----------------------------------------------------------------
      *
     C     $CUST         BEGSR
      *
     C                   CALL      'HPJDES'
     C     WCCVNO        PARM      SHCVNO        XXCVNO
     C                   PARM      *BLANK        XXSTAT
     C     WCNAME        PARM      *BLANK        XXNAME
      *
      * IF STATE IS MISSING, WRITE A RECORD TO THE CUSTOMER
      * WORKFILE.
      *
     C     XXSTAT        IFEQ      *BLANK                                       IF NO STATE
     C     WCCVNO        CHAIN     HSP347                             92
     C     *IN92         IFEQ      *ON                                          IF NOT THERE
     C                   WRITE     WCREC
     C                   ENDIF                                                  IF NOT THERE
     C                   ENDIF                                                  IF NO STATE
      *
     C                   Z-ADD     SHCVNO        SVCVNO
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * *INZSR - INITIALIZATION SUBROUTINE
      *----------------------------------------------------------------
      *
     C     *INZSR        BEGSR
      *
      * KEY LIST
     C     K1P348        KLIST
     C                   KFLD                    SHFSBO
     C                   KFLD                    XXSTAT
      *
      * PARMS FOR CALL TO J.D. EDWARDS TO RETRIEVE CUSTOMER STATE
      *
     C                   MOVE      *BLANK        XXNAME           40
     C     *LIKE         DEFINE    WFSTAT        XXSTAT
     C     *LIKE         DEFINE    SHCVNO        XXCVNO
      *
      * CONTROL FIELDS
     C     *LIKE         DEFINE    SHCVNO        SVCVNO
      *
      * WORKFILEDS
     C                   Z-ADD     0             WKGRAM           15 2
      *
     C                   ENDSR
      *
