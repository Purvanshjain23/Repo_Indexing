      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Hog Production System
      * PROGRAM:     HP247 - Download: Market Selector--Group Sire Lines
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     05/29/01
      *
      * FUNCTION:    This program builds a file of HPS group sire lines that
      *              will be FTP'd to the Pork Intranet Site on a nightly
      *              basis.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 12/16/02  LeAnne Fedor
      *           We have changed the sow/sire line derivation programs to include files
      *           with stats based on 'total head received'...instead of stats based on
      *           transfers we could process (ie: omitted catch-22 transfers).
      *           So, per AMB, we will now send stats based on these new files (ie: based
      *           on 'total head received'). So, we will use:
      *               HSP340 instead of HSP328 for Nursery
      *               HSP350 instead of HSP331 for Grow Finish
      *
      * 01/27/05  LeAnne Fedor
      *           Recompile only. Two new fields (sire line company, prime line)
      *           added to Sire Line file.
      *
      * 04/10/08  LeAnne Ramsey
      *           Per John Zimmerman, override Cell 5 to be Cell 4 in the download files.
      *           Sunray Feed Mill is causing a problem in the pc functions. This hardcoded
      *           override will be temporary--just until the new programmer can change/fix
      *           the pc functions. (FYI: They do not want to change the cell in HPS.)
      *
      * 07/02/09  LeAnne Ramsey
      *           Recompile only. Added new field 'Continuous Flow Flag' to Hog Group file.
      *
      * 10/14/10  LeAnne Ramsey  (E1079)
      *           Recompile only. Sire Line Company is now 2-characters instead
      *           of one in file HSP098-Sire Lines.
      *
      * 10/15/13  LeAnne Ramsey (E2831)
      *           Recompile only. Added field 'MTech Reference'.
      *
      * 09/22/16  Brad Baden    (E7324)
      *           Remove override of CELL5 to CELL4 per John Zimmerman.
      /eject
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
      *
     Fhsp018    if   e           k disk
      *    Farm site
      *
      *
     Fhsp034    if   e           k disk
      *    Hog group
      *    (records selected with open query)
      *
      *
     Fhsp098    if   e           k disk
      *  Sire lines
      *
      *
     Fhsp340    if   e           k disk
      *  Workfile: Nursery groups--sire line total
      *
      *
     Fhsp350    if   e           k disk
      *  Workfile: Grow finish groups--sire line total
      *
      *
     Fhsp247    uf a e           k disk
      *   Download: Market selector--group sire lines
      *
      /eject
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Workfields
      *
     D wksicd          s                   like(w0sicd)
     D wkpr            s                   like(w0hdpr)
     D wkgg0           s                   like(sigg0)
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * MAINLINE
      ****************************************************************
      *
      * Process each group in the Hog Group Master (records selected with open query)
      *
     C     *loval        setll     hsp034
      *
     C                   dou       *in90 = *on                                  Do groups
     C                   read      hsp034                                 90
     C                   if        *in90 = *off                                 If not EOF
     C                   exsr      $sireline
     C                   endif                                                  If not EOF
     C                   enddo                                                  Do groups
      *
      * Now, reread the sireline records that you have just written
      * and calculate the dose 'rate' from the dose 'percent'.
      *
     C                   exsr      $rate
      *
     C                   seton                                        lr
      /eject
      *---------------------------------------------------------------
      * Retrieve sire line data for a group
      *---------------------------------------------------------------
      *
     C     $sireline     begsr
      *
     C                   select
     C                   when      hgppcd = 'GF   '
      *
     C     hghgsn        setll     hsp350
     C                   dou       *in91 = *on                                  Do GF lines
     C     hghgsn        reade     hsp350                                 91
     C                   if        *in91 = *off                                 If not EOF
     C                   move      wasicd        wksicd
     C                   z-add     wahdpr        wkpr
     C                   exsr      $cognos
     C                   exsr      $doses
     C                   endif                                                  If not EOF
     C                   enddo                                                  Do GF lines
      *
      *
     C                   when      hgppcd = 'NUR  '
      *
     C     hghgsn        setll     hsp340
     C                   dou       *in91 = *on                                  Do nur lines
     C     hghgsn        reade     hsp340                                 91
     C                   if        *in91 = *off                                 If not EOF
     C                   move      w0sicd        wksicd
     C                   z-add     w0hdpr        wkpr
     C                   exsr      $cognos
     C                   exsr      $doses
     C                   endif                                                  If not EOF
     C                   enddo                                                  Do nur lines
     C                   endsl
      *
     C                   endsr
      /eject
      *--------------------------------------------------------------------------
      * Retrieve the 'Cognos report grouping code' associated with the sire line
      *--------------------------------------------------------------------------
      *
     C     $cognos       begsr
      *
     C     wksicd        chain     hsp098                             92
     C                   if        *in92 = *off                                 If hit
     C                   move      sigg0         wkgg0
     C                   else
     C                   move      wksicd        wkgg0
     C                   endif                                                  If hit
      *
     C                   endsr
      /eject
      *--------------------------------------------------------------------------
      * Write or update the 'doses' record for the Cognos Report Grouping
      *--------------------------------------------------------------------------
      *
     C     $doses        begsr
      *
     C     key01         chain     hsp247                             92
     C                   if        *in92 = *off                                 If hit
     C                   add       wkpr          d4dopr
     C                   update    d4rec
     C                   else
      *
     C                   move      wkgg0         d4gg0
     C                   z-add     wkpr          d4dopr
      *
     C                   z-add     hghgsn        d4hgsn
     C                   move      hghgcd        d4hgcd
     C                   z-add     hgfscd        d4fscd
     C                   move      hgppcd        d4ppcd
      *
     C     hgfscd        chain     hsp018                             92
     C                   if        *in92 = *off                                 If farm
     C                   z-add     fscell        d4cell
     C                   else
     C                   z-add     0             d4cell
     C                   endif                                                  If farm
      *
      * Temporary override of Cell 5 to be Cell 4.
      *
      *
      * 09/22/2016 Brad Baden  E7324 - Remove override of Cell 5 to Cell 4
     C*                  if        d4cell = 5
     C*                  z-add     4             d4cell
     C*                  endif
      *
     C                   write     d4rec
     C                   endif                                                  If hit
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Calculate the 'rate' that corresponds to the 'percent'
      *---------------------------------------------------------------
      *
      * Brad wanted a 'rate' (5,4) instead of a 'percent' (5,2).
      * So, just divide each 'percent' by 100 to get the rate for each record.
      *
     C     $rate         begsr
      *
     C     *loval        setll     hsp247
      *
     C                   dou       *in91 = *on                                  Do rates
     C                   read      hsp247                                 91
     C                   if        *in91 = *off                                 If not EOF
     C     d4dopr        div       100           d4dort
     C                   update    d4rec
     C                   endif                                                  If not EOF
     C                   enddo                                                  Do rates
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * *inzsr - initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Parm lists
      *    None
      *
      *
      * Key lists
      *
     C     key01         klist
     C                   kfld                    hghgsn
     C                   kfld                    wkgg0
      *
     C                   endsr
      /eject
