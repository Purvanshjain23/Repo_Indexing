/*                                                                  */
/*  SYNOPSIS -  THIS PROGRAM WILL BE CALLED FROM A TRIGGER ON THE   */
/*              OM CARRIER FILE IN THE STC POULTRY SYSTEM           */
/*              THIS PROGRAM WILL RETRIEVE THE CARRIER CODE, OLD    */
/*              CARRIER NAME AND NEW CARRIER NAME FROM THE TRIGGER  */
/*              BUFFER                                              */
/*              THIS PROGRAM WILL CALL AN RPG PROGRAM TO UPDATE     */
/*              THE CARRIER, ORDER HEADER, LOAD HEADER AND HISTORY  */
/*              FILES IN THE ELBERTON SYSTEM.                       */
/*                                                                  */
/*  SYSTEM    - IDEAS/400 ORDER PROCESSING                          */
/*  USER NAME - MARK ARENSBERG                                      */
/*  DATE      - 01/26/99                                            */
/*                                                                  */
/********************************************************************/
/*  MODIFIED:                                                       */
/*   5/20/99   MARY SHARP      ONLY RUN CL IF TRIGGER WAS FIRED     */
/*                             ON BIGBYTE (DATA AREA)               */
/*                                                                  */
/********************************************************************/
/*      PARAMETERS RECEIVED:                                        */
/*                                                                  */
/*               &TRGBUF  - TRIGGER BUFFER                          */
/*               &TRGLEN  - TRIGGER LENGTH                          */
/********************************************************************/
/*      PARAMETERS USED:                                            */
/*                                                                  */
/*               &CARR    - CARRIER CODE                            */
/*               &OLDNM   - OLD CARRIER NAME                        */
/*               &NEWNM   - NEW CARRIER NAME                        */
/*               &RUN     - FIRE TRIGGER                            */
/********************************************************************/

 PDVGPCLP:  PGM         PARM(&TRGBUF &TRGLEN)

         DCL     &TRGBUF     *CHAR    (1524) /*  TRIGGER BUFFER */
         DCL     &TRGLEN     *DEC     (4 0)  /*  BUFFER LENGTH  */
         DCL     &CARR       *CHAR    (3)    /*  CARRIER CDE*/
         DCL     &OLDNM      *CHAR    (30)   /*  OLD CARR NAME */
         DCL     &NEWNM      *CHAR    (30)   /*  NEW CARR NAME */
         DCL     &RUN        *CHAR    (1)    /*  FIRE TRIGGER */

/********************************************************************/
/*   CHECK BIGBYTE DATA AREA TO SEE IF TRIGGER SHOULD BE FIRED      */
/********************************************************************/

             RTVDTAARA  DTAARA(QGPL/BIGBYTE) RTNVAR(&RUN)

             IF         COND(&RUN *EQ 'Y') THEN(DO)

/********************************************************************/
/*   RTV CARRIER, OLD NAME AND NEW NAME FROM THE TRIGGER BUFFER     */
/********************************************************************/

             CHGVAR     VAR(&CARR) VALUE(%SST(&TRGBUF 811 3))
             CHGVAR     VAR(&OLDNM) VALUE(%SST(&TRGBUF 100 30))
             CHGVAR     VAR(&NEWNM) VALUE(%SST(&TRGBUF 814 30))

/********************************************************************/
/*   SETUP ELBERTON LIBRARY LIST AND ADD POULTRY FILE LIBRARY LAST  */
/********************************************************************/

             PSHLIBL
             CALL SETELB

/********************************************************************/
/*   CALL PROGRAM TO ADD OR CHANGE CARRIER IN ELBERTON FILES        */
/********************************************************************/

             CALL       PGM(LP208)  PARM(&CARR &OLDNM &NEWNM)

/********************************************************************/
/*   SETUP POULTRY LIBRARY LIST                                     */
/********************************************************************/

             POPLIBL

             ENDDO

 END:        ENDPGM

