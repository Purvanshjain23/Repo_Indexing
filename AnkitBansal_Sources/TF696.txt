      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Triumph Foods
      * PROGRAM:     TF696
      * TITLE:       Listing of Base Marketing Fees
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     12/16/04
      *
      * FUNCTION:  Batch program to print a record(s) in the Base Marketing Fees file.
      *            This listing is submitted from W/W Base Marketing Fess when the user
      *            takes:
      *                1) F7=Listing or
      *                2) 6=Print
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Ftfp003    if   e           k disk
      *    Base marketing fees
      *    (records selected by open query)
      *
      *
     Fprint132  o    f  132        printer oflind(*inof)
      *
      /EJECT
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Standard fields
      *
     D dash            s            132    inz(*all'-')
     d rtime           s              6  0
      *
      *
      * Control fields
      *
     D first           s              1    inz('Y')
      *
      *
      * Workfields for date manipulation
      *
     D wkisodate       s               D   datfmt(*iso)
      *
      *
      * Print fields
      *
     D l1ybmdy         s              6  0
     D l1yemdy         s              6  0
      *
      ****************************************************************
      * ARRAYS AND TABLES
      ****************************************************************
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *
      *---------------------------------------------------------------
      * Local data area.
      *---------------------------------------------------------------
      *
     Dlda             uds                  dtaara(*lda)
     D  ldyrno                 1      3  0
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ***************************************************************************************
      * MAINLINE
      ***************************************************************************************
      *
      * Print headings
     C                   exsr      $h1hdr
      *
      * Read/print each record in the Base Marketing Fees file selected by
      * the open query.
      *
     C                   dou       *in90 = *on                                  Main do
     C                   read      tfp003                                 90
     C                   if        *in90 = *off                                 If not EOF
     C                   move      no            first
      *
      * Print record
     C                   exsr      $l1dtl
     C                   endif                                                  If not EOF
     C                   enddo                                                  Main do
      *
      *---------------------------------------------------------------
      * EOF processing
      *---------------------------------------------------------------
      * If the file was empty, print the standard 'no data...' message
      *
     C                   if        first = yes
     C                   except    nodata
     C                   endif
      *
     C                   seton                                        lr
      /EJECT
      *---------------------------------------------------------------
      * Print detail line
      *---------------------------------------------------------------
      *
     C     $l1dtl        begsr
      *
      *
      * Year-beginning date
      *
     C     *iso          test(d)                 bmybdt                 92
     C                   if        *in92 = *off                                 If OK begin
     C                   move      bmybdt        wkisodate
     C     *mdy          move      wkisodate     l1ybmdy
     C                   endif                                                  If OK begin
      *
      *
      * Year-ending date
      *
     C     *iso          test(d)                 bmyedt                 92
     C                   if        *in92 = *off                                 If OK end
     C                   move      bmyedt        wkisodate
     C     *mdy          move      wkisodate     l1yemdy
     C                   endif                                                  If OK end
      *
     C                   if        *inof = *on
     C                   exsr      $h1hdr
     C                   endif
      *
     C                   except    l1dtl
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Print report heading
      *---------------------------------------------------------------
      *
     C     $h1hdr        begsr
      *
     C                   except    h1hdr
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Retrieve time for report heading.
      *
     C                   time                    rtime
      *
     C                   if        ldyrno = 0
     C                   seton                                        97
     C                   endif
      *
     C                   endsr
      /EJECT
      *-------------------------------------------------------------
      * Report heading lines
      *-------------------------------------------------------------
      *
     Oprint132  e            h1hdr          1 01
     O                       sdpgm               10
     O                                           71 'LISTING OF BASE '
     O                                           85 'MARKETING FEES'
     O                                          122 'DATE'
     O                       udate         y    132
      *
     O          e            h1hdr          1
     O                       sdusr               10
     O                                          122 'TIME'
     O                       rtime              132 '  :  :  '
      *
      *
     O          e            h1hdr          2
     O                                          122 'PAGE'
     O                       page          z    132
      *
      *
     O          e            h1hdr          2
     O                                           12 'Year number:'
     O              n97      ldyrno        z     16
     O               97                          16 'All'
      *
      *
      *-------------------------------------------------------------
      * Column headings
      *-------------------------------------------------------------
      *
     O          e            h1hdr       0  1
     O                                           54 'Year'
     O                                           69 'Year'
     O          e            h1hdr       0  1
     O                                            6 'Year'
     O                                           17 'Current'
     O                                           30 'Deferred'
     O                                           42 'Total'
     O                                           56 'Beginning'
     O                                           70 'Ending'
      *
     O          e            h1hdr       0  1
     O                                            6 'Number'
     O                                           17 'Fee'
     O                                           30 'Fee'
     O                                           42 'Fee'
     O                                           54 'Date'
     O                                           69 'Date'
      *
     O          e            h1hdr          1
     O                       dash               132
      *
      *
      *-------------------------------------------------------------
      * Detail line
      *-------------------------------------------------------------
      *
     O          e            l1dtl          1
     O                       bmyrno        z      5
     O                       bmbmcfee      k     18
     O                       bmbmdfee      k     31
     O                       bmbmtfee      k     43
     O                       l1ybmdy        b    55 '  /  /  '
     O                       l1yemdy        b    70 '  /  /  '
      *
      *-------------------------------------------------------------
      * No data message line
      *-------------------------------------------------------------
      *
     O          e            nodata      1
     O                                           19 'No data meets your'
     O                                           39 'selection criteria.'
