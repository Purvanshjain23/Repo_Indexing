     H DATFMT(*YMD) DATEDIT(*YMD) DEBUG(*YES)
     Z* CRTBNDRPG
     Z* DFTACTGRP(*NO) DBGVIEW(*SOURCE) CVTOPT(*DATETIME)
     Z* ACTGRP(*CALLER) OPTIMIZE(*BASIC)
     H****************** ILE RPG PROGRAM ***********************************************************
     H* SYSTEM:      Seaboard Foods
     H* PROGRAM:     TRG038W
     H* PROGRAM DSC: HPS Trigger Wrapper for HSP038 File
     H* PROGRAMMER:  Danny Nguyen
     H* CREATED:     01/20/2022
     H* PROJECT:     W88663
     H* PURPOSE:     This is a trigger wrapper program which accepts IBM defined parameters for
     H*              the trigger buffer.
     H*
     H* COMPILING NOTES:
     H*    ˆCRTBNDRPG
     H*    ˆDFTACTGRP(*NO) DBGVIEW(*SOURCE) CVTOPT(*DATETIME)
     H*    ˆACTGRP(*CALLER) OPTIMIZE(*BASIC)
     H*
     H* DB2 TRIGGER NOTES:
     H*     1. Files with trigger MUST be journal to save Before & After images.
     H*     2. Create CL program to add the physical file trigger. Reference the trigger wrapper
     H*        program in the ADDPFTRG command. See PUABUPC program for example.
     H*     3. If a file is regenerated and recompiled the RMVPFTRG & ADDPFTRG commands need to be
     H*        reapplied. Also, change/recompile the TRG Process and TRG Wrapper program.
     H*     4. If adding/removing trigger to an existing file, the file must NOT be in use to
     H*        perform the RMVPFTRG & ADDPFTRG command. Typically, if the file is heavily used
     H*        then this could be resolve after an IPL in a 'restricted state'.
     H*     5. Be caution when manually updating files with triggers via SQL/DBU/YWRKF, etc or
     H*        fix/conversion program since these events will call the triggering program.
     H*     6. IBM command:
     H*        PRTTRGPGM will create a report to list all files with triggers.
     H*        RMVPFTRG will remove physical file trigger.
     H*     7. You can clone PTPTUPR program and apply this to any DB2 file.
      * 05/10/22 Eric L SDN736 Recompiled ticket nbr increase (TKNO & RTNO 7.0 TO 9.0)
     H**********************************************************************************************
     H* MODIFICATIONS:
     H**********************************************************************************************
     H* DATE      INT  PROJECT  DESCRIPTION
     H* --------  ---  -------  -------------------------------------------------------------------
     H* 04/05/22  blg  WHD88663 Changed to execute program TRG038P instead of TRG001P to follow our
     H*                         HPS standards.  Also renamed pgm from TRG001W to TRG038W.
     H**********************************************************************************************
      *
      * Data Structure Passed in by the IBM Defined Database
     D TrgBuffer       DS
     D  TFileName                    10
     D  TLibraryName                 10
     D  TMemberName                  10
     D  TEvent                        1
     D  TTime                         1
     D  TCommitLock                   1
     D  TFill01                       3
     D  TCCSID                       10I 0
     D  TRRN                         10I 0
     D  TFill02                      10I 0
     D  TOldOffset                   10I 0
     D  TOldLength                   10I 0
     D  TOldNullOff                  10I 0
     D  TOldNullLen                  10I 0
     D  TNewOffset                   10I 0
     D  TNewLength                   10I 0
     D  TNewNullOff                  10I 0
     D  TNewNullLen                  10I 0
      *
     D TrgBufferLen    S             10I 0
      * Before and After Record Images: HPS Feed Ticket Dtl
     D OldRecord     E DS                  Extname(HSP038) Prefix(O_)
     D                                     Based(OldRecPtr)
      *
     D NewRecord     E DS                  Extname(HSP038)
     D                                     Based(NewRecPtr)
      *
      *
      **********************************************************************************************
      * Entry Parameter List
      **********************************************************************************************
     C     *entry        plist
     C                   parm                    TrgBuffer
     C                   parm                    TrgBufferLen
      *
      *
      * Map External DS to Trigger Buffer Using Pointers.
     C                   eval      OldRecPtr = %addr(TrgBuffer) + TOldOffSet
     C                   eval      NewRecPtr = %addr(TrgBuffer) + TNewOffSet
      *
      * Clear Old Buffer for Insert and New Buffer for Delete To Prevent Previous Record Data.
     C                   If        TEvent = '1'                                 Insert
     C                   Clear                   OldRecord
     C                   ElseIf    TEvent = '2'                                 Delete
     C                   Clear                   NewRecord
     C                   EndIf
      *
      * Call Program for Processing.
     C******             Call      'TRG001P'
     C                   Call      'TRG038P'
     C                   Parm      *Blank        W0RTN             7
     C                   Parm                    OldRecord
     C                   Parm                    NewRecord
     C                   Parm                    TEvent
      *
      *
     C                   return
      *
