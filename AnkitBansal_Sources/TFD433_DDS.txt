     A*%%TS  SD  20050314  172237  ISLRAMS     REL-V5R2M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        TRIUMPH FOODS
     A*  DISPLAY FILE:  TFD433
     A*  TITLE:         WORK WITH INVOICES
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  DATE:          03/09/05
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/TFPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A                                      CF04(04 'PROMPT')
     A                                      CF05(05 'REFRESH')
     A N87                                  CF06(06 'CREATE')
     A                                      CF07(07 'LISTING')
     A                                      CF17(17 'PRINT OPTIONS')
     A                                      CF20(20 'WRKSPLF')
     A*
     A*-------------------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*-------------------------------------------------------------------------
     A          R TF433S1                   SFL
     A*%%TS  SD  20050314  172237  ISLRAMS     REL-V5R2M0  5722-WDS
     A  85                                  SFLNXTCHG
     A            HDRRN1    R        H      REFFLD(@@RRN)
     A            D1OPT     R        B 11  2REFFLD(@@OPT)
     A                                      CHGINPDFT(UL)
     A  30                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A N30                                  DSPATR(HI)
     A            D1INDTMDY R        O 11  6REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  ')
     A            D1INSN    R        O 11 16REFFLD(TFREF/@@INSN *LIBL/TFPREF)
     A                                      EDTCDE(M)
     A            D1INSTCD  R        O 11 29REFFLD(@@INSTCD)
     A            D1CFQCD   R        O 11 37REFFLD(TFREF/@@CFQCD *LIBL/TFPREF)
     A            D1DUDTMDY R        O 11 43REFFLD(TFREF/@@DT06 *LIBL/TFPREF)
     A                                      EDTWRD('  /  / 0')
     A            D1PODTMDY R        O 11 54REFFLD(TFREF/@@DT06 *LIBL/TFPREF)
     A                                      EDTWRD('  /  / 0')
     A*
     A*-------------------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*-------------------------------------------------------------------------
     A          R TF433C1                   SFLCTL(TF433S1)
     A*%%TS  SD  20050314  172237  ISLRAMS     REL-V5R2M0  5722-WDS
     A                                      SFLSIZ(0050)
     A                                      SFLPAG(0010)
     A N82                                  ROLLUP(25 'PAGE FORWARD')
     A                                      SETOF(30)
     A                                      SETOF(31)
     A                                      SETOF(51)
     A                                      SETOF(52)
     A                                      SETOF(53)
     A                                      RTNCSRLOC(&RCD &FLD)
     A                                      OVERLAY
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 27'WORK WITH INVOICES'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USR)
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@JOB)
     A                                      DSPATR(HI)
     A                                  3  2'Include posted invoices ..'
     A                                  3 29'?'
     A            C1YNFL    R        B  3 31REFFLD(@@FLAG)
     A  31
     AO 51                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A N31                                  DSPATR(HI)
     A                                  5  2'2=Revise'
     A                                  5 12'4=Delete'
     A                                  5 22'5=View'
     A                                  5 30'6=Print Invoice Preview'
     A                                  5 55'7=Assign/Unassign Charges'
     A                                  6 29'15=Post/Print Invoice'
     A                                  6 54'18=Reprint Invoice'
     A                                  7 29'?'
     A                                      DSPATR(HI)
     A                                  7 37'?'
     A                                      DSPATR(HI)
     A                                  8  7'Invoice'
     A                                      DSPATR(HI)
     A                                  8 16'Invoice'
     A                                      DSPATR(HI)
     A                                  8 26'Invoice'
     A                                      DSPATR(HI)
     A                                  8 36'Freq'
     A                                      DSPATR(HI)
     A                                  8 45'Due'
     A                                      DSPATR(HI)
     A                                  8 55'Posted'
     A                                      DSPATR(HI)
     A                                  9  2'Opt'
     A                                      DSPATR(HI)
     A                                  9  8'Date'
     A                                      DSPATR(HI)
     A                                  9 17'Number'
     A                                      DSPATR(HI)
     A                                  9 26'Status'
     A                                      DSPATR(HI)
     A                                  9 36'Code'
     A                                      DSPATR(HI)
     A                                  9 45'Date'
     A                                      DSPATR(HI)
     A                                  9 56'Date'
     A                                      DSPATR(HI)
     A            C1INDTMDY R        B 10  6REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  ')
     A                                      DSPATR(HI)
     A N31N51N52
     A N53                                  DSPATR(PC)
     A            C1INSN    R        B 10 16REFFLD(TFREF/@@INSN *LIBL/TFPREF)
     A                                      DSPATR(HI)
     A                                      EDTCDE(M)
     A            C1INSTCD  R        B 10 29REFFLD(TFREF/@@INSTCD *LIBL/TFPREF)
     A  52                                  DSPATR(PC)
     A                                      DSPATR(HI)
     A            C1CFQCD   R        B 10 37REFFLD(TFREF/@@CFQCD *LIBL/TFPREF)
     A  53                                  DSPATR(PC)
     A                                      DSPATR(HI)
     A            C1DUDTMDY R        B 10 43REFFLD(TFREF/@@DT06 *LIBL/TFPREF)
     A                                      DSPATR(HI)
     A                                      EDTWRD('  /  / 0')
     A            C1PODTMDY R        B 10 54REFFLD(TFREF/@@DT06 *LIBL/TFPREF)
     A                                      DSPATR(HI)
     A                                      EDTWRD('  /  / 0')
     A*
     A*-------------------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*-------------------------------------------------------------------------
     A          R TF433K1
     A*%%TS  SD  20050309  134621  ISLRAMS     REL-V5R2M0  5722-WDS
     A  43                             21  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 22  2'F3=Exit'
     A                                 22 11'F4=Prompt'
     A                                 22 22'F5=Refresh'
     A                                 22 34'F6=Create'
     A                                 22 45'F7=Listing'
     A                                 22 57'F17-Print Options'
     A                                 23 57'F20=Spool Files'
      *
     A*-------------------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
     A*-------------------------------------------------------------------------
     A          R TF433ES                   SFL SFLMSGRCD(24)
      *
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
     A*-------------------------------------------------------------------------
      * ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*-------------------------------------------------------------------------
     A          R TF433EC                   SFLCTL(TF433ES)
      *
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
