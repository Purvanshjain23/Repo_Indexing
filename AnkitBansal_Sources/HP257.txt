      *****************  RPG PROGRAM HEADING  ************************
      *
      * ENVIRONMENT: Pork Division
      * SYSTEM:      Hog Production System
      * PROGRAM:     HP257 - Retrieve Sales Movement Shipped Date and Load Type
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     03/26/02
      *
      * FUNCTION:    This program was created to be called from Synon program(s).
      *              The Synon program(s) that call this program pass in a Movement
      *              Number. This program retrieves the load type and shipping dates/
      *              times and, after some manipulation, returns them to the Synon
      *              System(s).
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 01/15/03  LeAnne Fedor
      *           The HPS users are not entering the 'ship times'. In conjunction with
      *           the new 'delivery schedule' project in HPE, we are removing these
      *           'times' from the HPS Sales Movement Header file.
      *
      *           But, it was easier to leave them as parms than to try to coordinate
      *           this change with HPE. So, they are still parms....we just always put
      *           zero in them. I also commented out some 'next' day logic based on
      *           a comparison of shipped in/out times....you never know if we will have
      *           to put this back in!
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fhsp084    if   e           k disk
      *    Sales movement header
      *
      /EJECT
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      ****************************************************************
      * ARRAYS AND TABLES
      ****************************************************************
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Work fields for date manipulation
      *
     D wkisocymd       s               D   datfmt(*iso)
     D wkccyymmdd      s              8  0
      *
      *
      * Parms
      *
     D xxreturn        s              7
     D xxmvsn          s                   like(shmvsn)
     D xxltcd          s                   like(shltcd)
     D xxsitm          s              4  0
     D xxsotm          s              4  0
     D xxsidt          s              7  0
     D xxsodt          s              7  0
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *-----------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ***************************************************************************************
      * MAINLINE
      ***************************************************************************************
      *
      * Retrieve the Sales Movement header record for the incoming movement number
      *
      *
     C     xxmvsn        chain     hsp084                             92
     C                   if        *in92 = *off                                 If found
     C                   move      *blank        xxreturn
     C                   move      shltcd        xxltcd
     C                   z-add     0             xxsitm
     C                   z-add     0             xxsotm
     C                   exsr      $dates
     C                   else
      *
     C                   eval      xxreturn = 'HPSM1'
     C                   move      *blank        xxltcd
     C                   z-add     0             xxsitm
     C                   z-add     0             xxsotm
     C                   z-add     0             xxsidt
     C                   z-add     0             xxsodt
     C                   endif                                                  If found
      *
      * EOF Processing
      *
     C                   seton                                        lr
      /EJECT
      *----------------------------------------------------------------
      * Manipulate/format dates
      *----------------------------------------------------------------
      *
     C     $dates        begsr
      *
     C                   select
     C                   when      shshdt = 0
     C                   z-add     0             xxsidt
     C                   z-add     0             xxsodt
     C                   other
      *
      * Format shipped date into Synon format (HPS's 'shipped date' is passed
      * out as the value for the 'shipped in date'. As a default, put this date
      * into the 'shipped out date' as well.
      *
     C                   call      'HP8008'
     C                   parm                    shshdt
     C     xxsodt        parm                    xxsidt
      *
      * Using 'shipped times' and 'shipped date', calculate the value for
      * the 'shipped out date'. If 'out time' is earlier than 'in time', add 1
      * day to the 'shipped date'. Then, format into Synon format.
      *
     C***                if        shsotm < shsitm and
     C***                          shsotm <> 0 and
     C***                          shsitm <> 0
     C***  *iso          movel     shshdt        wkisocymd
     C***                adddur    1:*d          wkisocymd
     C***                movel     wkisocymd     wkccyymmdd
     C***                call      'HP8008'
     C***                parm                    wkccyymmdd
     C***                parm                    xxsodt
     C***                endif
      *
     C                   endsl
      *
     C                   endsr
      /EJECT
      *----------------------------------------------------------------
      * Initialization subroutine
      *----------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Parm lists
      *
     C     *entry        plist
     C                   parm                    xxreturn
     C                   parm                    xxmvsn
     C                   parm                    xxltcd
     C                   parm                    xxsitm
     C                   parm                    xxsotm
     C                   parm                    xxsidt
     C                   parm                    xxsodt
      *
     C                   endsr
      /EJECT
