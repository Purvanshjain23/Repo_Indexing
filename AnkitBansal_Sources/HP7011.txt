      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Hog Production
      * PROGRAM:     HP7011
      * TITLE:       View Sire Line Doses for All Farms
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     10/25/01
      *
      *
      * FUNCTION: This is a View that displays the records in the Weekly
      *           Sire Line Doses file.
      *
      *           Restrictors:
      *                Show most recent week only flag
      *
      *           Positioners:
      *               Farm site
      *
      *           Selectors:
      *                Calendar year
      *                Calendar week
      *                Sire line source
      *                Sire line
      *                Cell
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 10/16/13  LeAnne Ramsey (E2831)
      *           Recompile only. Added field 'MTech Reference'.
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fhpd7011   cf   e             workstn
     F                                     sfile(hp7011s1:hdrrn1)
     F                                     infds(iofeed)
      *
      *
     Fhsp018    if   e           k disk
      *    Farm site
      *
      *
     Fhsl081a   if   e           k disk
     F                                     infds(dbfeed)
      *    Weekly sire line doses
      *
      /EJECT
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
     D set1            c                   'SET1  '
     D scrn1           c                   'SCRN1 '
     D exit            c                   'EXIT  '
      *
      ****************************************************************
      * ARRAYS AND TABLES
      ****************************************************************
      *
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Standard fields
      *
     D mode            s              6
     D pgm             s             10
     D msgfl           s             10
     D maxmsg          s              2  0  inz(20)
     D routne          s              6
     D maxrrn          s              4  0
     D reset           s              1    inz('N')
     D norecs          s              1    inz('N')
     D count           s              2  0
     D wkflow          s                   like(idflow)
     D rtncd           s              2
     D svrrn1          s                   like(hdrrn1)
      *
      *
      * Workfields required for subfile paging--see initialization
      * subroutine.
      *
     D wkplin          s              2  0
     D wkrlin          s              2  0
      *
      *
      * Control fields and save fields for restrictors, positioners and selectors
      *
     D first           s              1
     D procfl          s              1
     D svynfl          s                   like(c1ynfl)
     D svfscd          s                   like(c1fscd)
     D svcdyr          s                   like(c1cdyr)
     D svcdwk          s                   like(c1cdwk)
     D svsscd          s                   like(c1sscd)
     D svsicd          s                   like(c1sicd)
     D svcell          s                   like(c1cell)
      *
      * Save fields for loading subfile when user has selected to see only the
      * most recent records.
      *
     D svfscd2         s                   like(d1fscd)
     D svcdyr2         s                   like(c1cdyr)
     D svcdwk2         s                   like(c1cdwk)
      *
      *
      * Work fields for date manipulation
      *
     D wkmdydate       s               D   datfmt(*mdy)
      *
      * Parms
      *
     D xxfsbo          s              5
     D xxfscd          s                   like(c1fscd)
     D xxfsnm          s             25
     D xxynfl          s                   like(c1ynfl)
     D xxsscd          s                   like(c1sscd)
     D xxssds          s             25
     D xxsicd          s                   like(c1sicd)
     D xxaist          s              1
      *
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * standard error message handling data structures
      *---------------------------------------------------------------
      *
      * For error message handling, a packed index (3) is required.
      * For program readability, define a corresponding error count
      * field called 'error'
      *
     D                 ds                  inz
     D  error                         2p 0
     D   e                            2p 0 overlay(error)
      *
      * This data structure supplies the name of the message file to
      * the message handling CL program.  The field name MSGFIL must be
      * constant. The value in quotes is the name of the specific
      * message file containing the user defined messages.
      *
     D                 ds                  inz
     D  msgfil                       10    inz('HSMSGF    ')
      *
      *---------------------------------------------------------------
      *  Standard message data structures
      *---------------------------------------------------------------
      *
      * The following 3 data structures are used to speed message
      * handling since it is faster to clear data structures than
      * arrays.  Each is associated with a standard message array.
      *
     D mgi             ds                  inz
     D  mgid                          7    dim(20)
      *
     D mgd             ds                  inz
     D  mgdt                         50    dim(20)
      *
     D mgwk            ds                  inz
     D  mgw                           1    dim(50)
      *
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *---------------------------------------------------------------
      * Standard workstation information data structure
      *---------------------------------------------------------------
      *    externally defined as UTWSFR (record format: UTIDFRR)
     D iofeed        e ds                  extname(utwsfr)
      *
      *
      *---------------------------------------------------------------
      * Standard database file information data structure
      *---------------------------------------------------------------
      *    externally defined as UTDBGR (record format: FDBCKD)
     D dbfeed        e ds                  extname(utdbfr)
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * MAINLINE
      ****************************************************************
      *
     C                   eval      routne = set1
      *
     C     *inlr         doweq     *off                                         Main do loop
     C     routne        caseq     set1          $set1
     C     routne        caseq     scrn1         $scrn1
     C     routne        caseq     exit          $exit
     C                   endcs
     C                   enddo                                                  Main do loop
      /EJECT
      *----------------------------------------------------------------
      * $set1 - set environment for screen 1
      *----------------------------------------------------------------
      *
     C     $set1         begsr
      *
      * Clear and fill the subfile
      *
     C                   exsr      $clr1
     C                   exsr      $frsh1
      *
      * Set routine to display screen
      *
     C                   eval      routne = scrn1
      *
     C                   endsr
      /eject
      *----------------------------------------------------------------
      * $scrn1 - perform operator I/O for screen 1
      *----------------------------------------------------------------
      *
     C     $scrn1        begsr
      *
      *
      * Write the error messages from the error arrays to the
      * error message subfile
      *
     C                   exsr      $wrmsg
      *
      * Write screen 1 to crt
      *
     C                   write     hp7011k1
     C                   write     hp7011ec
     C                   exfmt     hp7011c1
      *
      * Save the value of the lowest subfile rrn on the screen for
      * later repositioning of the subfile
      *
     C                   eval      wkflow = idflow
      *
      * Clear the messages from the message data structures and
      * program message queue
      *
     C                   exsr      $clmsg
      *
      * Get user's entry and set next routine
      *
     C                   select
     C                   when      *in03 = *on                                  F3-EXIT
     C                   eval      routne = exit
      *
     C                   when      *in04 = *on                                  F4-prompt
     C                   exsr      $f4s1
      *
     C                   if        fld = 'C1YNFL' and
     C                             c1ynfl <> svynfl
     C                   exsr      $ynfl
     C                   endif
      *
     C                   if        error = 0
     C                   exsr      $chksv
     C                   else
     C                   eval      routne = scrn1
     C                   endif
      *
     C                   when      *in05 = *on and                              F5-refresh
     C                             (c1ynfl = yes or c1ynfl = no)
     C                   movel     yes           reset
     C                   eval      routne = set1
      *
      * Reposition the subfile if the user has changed any of the positioners
      * or selectors in the control record.
      *
     C                   when      c1ynfl <> svynfl or                          Positioners
     C                             c1fscd <> svfscd or
     C                             c1cdyr <> svcdyr or
     C                             c1cdwk <> svcdwk or
     C                             c1sscd <> svsscd or
     C                             c1sicd <> svsicd or
     C                             c1cell <> svcell
      *
     C                   if        c1ynfl <> svynfl
     C                   exsr      $ynfl
     C                   endif
      *
     C                   if        error = 0
     C                   exsr      $chksv
     C                   else
     C                   eval      routne = scrn1
     C                   endif
      *
     C                   when      *in25 = *on                                  F25-ROLLUP
     C                   exsr      $load1
     C                   other
      *
      * User has pressed enter
      *
     C                   eval      routne = set1
     C                   move      yes           reset
     C                   endsl
      *
     C                   endsr
      /EJECT
      *----------------------------------------------------------------
      * $chksv - Save the control record positioners/selectors
      *----------------------------------------------------------------
      *
     C     $chksv        begsr
      *
     C                   movel     no            reset
     C                   eval      routne = set1
      *
     C                   move      c1ynfl        svynfl
     C                   z-add     c1fscd        svfscd
     C                   z-add     c1cdyr        svcdyr
     C                   z-add     c1cdwk        svcdwk
     C                   move      c1sscd        svsscd
     C                   move      c1sicd        svsicd
     C                   z-add     c1cell        svcell
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Validate 'show most recent week' flag
      *---------------------------------------------------------------
      *
     C     $ynfl         begsr
      *
     C                   if        c1ynfl <> yes and
     C                             c1ynfl <> no
     C                   seton                                        31
     C                   if        error < maxmsg
     C                   add       1             error
     C                   eval      mgid(e) = 'HS09106'
     C                   endif
     C                   endif
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * F4 prompt on screen 1
      *---------------------------------------------------------------
      *
     C     $f4s1         begsr
      *
     C                   select
      *
      * Show most recent week only yes/no flag
      *
     C                   when      fld = 'C1YNFL'
     C                   call      'HP522'
     C                   parm                    xxynfl
     C                   seton                                        51
     C                   if        xxynfl <> *blank
     C                   movel     xxynfl        c1ynfl
     C                   endif
      *
      * Farm site
      *
     C                   when      fld = 'C1FSCD'
     C                   call      'HP591'
     C                   parm      *blank        xxfsbo
     C                   parm      0             xxfscd
     C                   parm      *blank        xxfsnm
     C                   seton                                        52
     C                   if        xxfscd <> 0
     C                   z-add     xxfscd        c1fscd
     C                   endif
      *
      * Sire line source
     C                   when      fld = 'C1SSCD'
     C                   call      'HP5015'
     C                   parm      *blank        xxaist
     C                   parm      *blank        xxsscd
     C                   parm      *blank        xxssds
     C                   seton                                        53
     C                   if        xxsscd <> *blank
     C                   move      xxsscd        c1sscd
     C                   endif
      *
      * Sire line
      *
     C                   when      fld = 'C1SICD'
     C                   call      'HP5016'
     C                   parm      *blank        xxaist
     C                   parm      *blank        xxsicd
     C                   seton                                        54
     C                   if        xxsicd <> *blank
     C                   movel     xxsicd        c1sicd
     C                   endif
      *
     C                   other
      *
      * F4 not valid for this field...issue message
      *
     C                   if        error < maxmsg
     C                   add       1             error                                        013
     C                   movel     'HS09011'     mgid(e)
     C                   endif
      *
     C                   endsl
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * $clr1  - clear subfile for screen 1
      *---------------------------------------------------------------
      *
     C     $clr1         begsr
      *
     C                   z-add     0             svrrn1
      *
     C                   clear                   hp7011s1
     C                   seton                                        83        sflclr
     C                   write     hp7011c1
     C                   setoff                                       818283
      *                                                    81=sfldsp
      *                                                    82=sflend
      *                                                    83=sflclr
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * $frsh1 - refresh/fill the subfile for screen 1
      *---------------------------------------------------------------
      *
     C     $frsh1        begsr
      *
      * Set position in file
      *
     C                   move      yes           first
     C                   z-add     0             svfscd2
     C                   z-add     0             svcdyr2
     C                   z-add     0             svcdwk2
      *
     C     c1fscd        setll     hsl081a
      *
      *
      * The field 'reset' is a flag field to indicate whether you are
      * attempting to reposition the user close to where he was in the
      * subfile.
      * The lowest subfile RRN on the screen from the DSPF feedback
      * area (IDFLOW) was retrieved earlier and put in WKFLOW.
      * Add to WKFLOW the number of lines on your subfile page or the
      * number less 1 to determine a subfile RRN to reposition to.
      *
      *
     C                   if        reset = yes
     C                   eval      maxrrn = wkflow + wkrlin
     C                   else
      *
      * Don't try to reposition the user, just start the subfile over
      *
     C                   eval      maxrrn = wkplin
     C                   endif
      *
      * Load the subfile until you have the required number of records.
      * The number will depend on whether uou are repositioning the
      * subfile or not. Stop if you get to the end of the subfile.
      *
     C                   dou       svrrn1 >= maxrrn or *in82 = *on
     C                   exsr      $load1
     C                   enddo
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * $load1- load subfile for screen 1
      *---------------------------------------------------------------
      *
     C     $load1        begsr
      *
     C                   z-add     svrrn1        hdrrn1
      *
      * Set the count of records on the subfile page to zero.
      *
     C                   eval      count = 0
      *
      * Fill the subfile until the end of the database file or
      * you have filled all the lines on the subfile page.
      *
     C                   setoff                                       82
     C                   dou       *in82 = *on or count >= wkplin               Do fill
     C                   read      hsl081a                                82
     C                   if        *in82 = *off                                 If read
      *
     C                   if        first = yes
     C                   z-add     swfscd        svfscd2
     C                   z-add     swcdyr        svcdyr2
     C                   z-add     swcdwk        svcdwk2
     C                   move      no            first
     C                   endif
      *
     C                   select
     C                   when      c1ynfl = no
     C                   move      yes           procfl
     C                   when      c1ynfl = yes and swfscd <> svfscd2
     C                   z-add     swfscd        svfscd2
     C                   z-add     swcdyr        svcdyr2
     C                   z-add     swcdwk        svcdwk2
     C                   move      yes           procfl
     C                   when      c1ynfl = yes and swcdyr = svcdyr2 and
     C                             swcdwk = svcdwk2
     C                   move      yes           procfl
     C                   other
     C                   move      no            procfl
     C                   endsl
      *
      * If you need to process this record.
      *
     C                   if        procfl = yes                                 If process
      *
      * Retrieve cell associated with farm site.
      *
     C     swfscd        chain     hsp018                             92
     C                   if        *in92 = *off                                 If found
     C                   z-add     fscell        d1cell
     C                   else
     C                   z-add     0             d1cell
     C                   endif                                                  If found
      *
      *
      * Only include records that match the user's selectors
      *
     C                   if        c1cdyr <> 0 and                               If year
     C                             c1cdyr <> swcdyr
     C                   else
      *
     C                   if        c1cdwk <> 0 and                              If week
     C                             c1cdwk <> swcdwk
     C                   else
      *
     C                   if        c1sscd <> *blank and                         If source
     C                             c1sscd <> swsscd
     C                   else
      *
     C                   if        c1sicd <> *blank and                         If sire line
     C                             c1sicd <> swsicd
     C                   else
      *
     C                   if        c1cell <> 0 and                              If cell
     C                             c1cell <> fscell
     C                   else
      *
     C                   add       1             count
     C                   add       1             hdrrn1
     C                   z-add     swfscd        d1fscd
     C                   z-add     swcdyr        d1cdyr
     C                   z-add     swcdwk        d1cdwk
     C                   move      swsscd        d1sscd
     C                   move      swsicd        d1sicd
     C                   z-add     swdono        d1dono
      *
      *
      * Flip week-ending date from CCYYMMDD to MMDDYY using a date format field.
      *
     C                   if        swwedt <> 0                                  If a date
     C     *iso          move      swwedt        wkmdydate
     C                   move      wkmdydate     d1mdywedt
     C                   endif                                                  If a date
      *
     C                   write     hp7011s1
     C                   seton                                        81
      *
      * For positioning, place the subfile relative record number into
      * the sflrcdnbr field (hdpag1)
      *
     C                   eval      hdpag1 = hdrrn1
      *
     C                   endif                                                  If cell
     C                   endif                                                  If sire line
     C                   endif                                                  If source
     C                   endif                                                  If week
     C                   endif                                                  If year
     C                   endif                                                  If process
     C                   endif                                                  If read
     C                   enddo                                                  Do fill
      *
      *
      * If no records were written to the subfile, then issue
      * the standard message 'No data to Display'.
      *
     C                   if        hdrrn1 = 0                                   If no recs
     C                   movel     yes           norecs
      *
     C                   if        error < maxmsg
     C                   add       1             error
     C                   eval      mgid(e) = 'HS09002'
     C                   endif
      *
     C                   else
     C                   movel     no            norecs
     C                   endif                                                  If no recs
      *
     C                   z-add     hdrrn1        svrrn1
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * $exit - seton last record and end job
      *---------------------------------------------------------------
      *
     C     $exit         begsr
     C                   seton                                        LR
     C                   endsr
      *
      /EJECT
      *---------------------------------------------------------------
      * $wrmsg - write error messages
      *---------------------------------------------------------------
      *
     C     $wrmsg        begsr
      *
     C                   call      'UT80045J'                           43
     C                   parm                    mgid
     C                   parm                    mgdt
     C                   parm                    error
     C                   parm      sdpgm         pgm
     C                   parm      msgfil        msgfl
      *
      * If call to UT80045J failed, a message hardcoded in the DSPF
      * command line format will be set on. So, redisplay screen.
      *
     C                   if        *in43 = *on
     C                   eval      routne = set1
     C                   endif
      *
     C                   endsr
      *
      *----------------------------------------------------------------
      * $clmsg - clear messages
      *----------------------------------------------------------------
      *
     C     $clmsg        begsr
      *
     C                   call      'UT80045J'                           43
     C                   parm      *blank        mgi
     C                   parm      *blank        mgd
     C                   parm      *zero         error
     C                   parm      sdpgm         pgm
     C                   parm      msgfil        msgfl
      *
      * If call to UT80045J failed, a message hardcoded in the DSPF
      * command line format will be set on; so, redisplay screen.
      *
     C                   if        *in43 = *on
     C                   eval      routne = set1
     C                   endif
      *
     C                   endsr
      /EJECT
      *----------------------------------------------------------------
      * *inzsr - initialization subroutine
      *----------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Parm lists
      *     None
      *
      * To support F5-Refresh, logic requires the number of lines on
      * the subfile page and also the number of lines less 1.  By
      * setting these values up as constant work fields, the processing
      * logic can be standardized between programs.
      *
     C                   z-add     12            wkplin                         sflpag
     C     wkplin        sub       1             wkrlin                         sflpag - 1
      *
      *
      * Default to 'yes' the value for control field 'show recent week only'
      *
     C                   move      yes           c1ynfl
     C                   move      yes           svynfl
      *
      *
      * The following standard code must be included to make the
      * standard error message handling program function properly.
      * This code sets and clears the program message queue.
      *
     C                   movel     '*'           msgpgm
     C                   exsr      $clmsg
      *
     C                   endsr
      /eject
