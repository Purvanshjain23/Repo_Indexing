/*****************************************************************   */
/*  PROGRAM NUMBER --> HP490CL                                       */
/*  PROGRAM NAME ----> DISPOSED GROUP PERFORMANCE REPORT             */
/*  PROGRAMMER ------> LEANNE FEDOR                                  */
/*  DATE ------------> 10/12/94                                      */
/*                                                                   */
/*  PROGRAM PURPOSE -> THIS CL CALLS THE PROGRAM TO PRINT THE        */
/*                     DISPOSED GROUP PERFORMANCE REPORT.            */
/*                     USER SPECIFICATIONS ARE PASSED IN THE LDA.    */
/*                                                                   */
/*                     REQUIRED SELECTIONS:                          */
/*                       BUSINESS OFFICE                             */
/*                       FROM DISPOSE DATE                           */
/*                       TO DISPOSE DATE                             */
/*                                                                   */
/*                     OPTIONAL SELECTIONS:                          */
/*                       PRODUCTION TYPE                             */
/*                       PRODUCTION PHASE                            */
/*                       FARM SITE                                   */
/*                       HOG GROUP                                   */
/*                                                                   */
/*********************************************************************/
/* MODIFICATIONS                                                     */
/* DATE      PROGRAMMER                                              */
/*                                                                 */
/*  12/05/94  LEANNE FEDOR                                         */
/*            CHANGED WORKFILE LOGIC. WE WILL NOW CREATE WORKFILES */
/*            IN QTEMP INSTEAD OF CLEARING THEM IN PRODUCTION.     */
/*                                                                 */
/*  03/13/95  LEANNE FEDOR                                         */
/*            RECOMPILE ONLY.  ORIGIN BGF FARM SITE AND ORIGIN     */
/*            NURSERY FARM SITE ADDED TO HOG GROUP FILE            */
/*                                                                 */
/*  04/21/95  LEANNE FEDOR                                         */
/*            FARM SITE WAS CHANGED FROM ALPHA TO NUMERIC.         */
/*                                                                 */
/*  08/30/95  LEANNE FEDOR                                         */
/*            BUSINESS OFFICE IS NOW OPTIONAL AS A REPORT          */
/*            SELECTION.                                           */
/*                                                                 */
/*  11/01/95  LEANNE FEDOR                                         */
/*            RECOMPILE ONLY. NEW FIELD 'GROUP TYPE' ADDED TO      */
/*            HOG GROUP FILE.                                      */
/*                                                                 */
/*  11/15/95  LEANNE FEDOR                                         */
/*            ADDED SELECTION LOGIC FOR THE NEW FIELD 'GROUP       */
/*            TYPE'.                                               */
/*                                                                 */
/* 03/19/96  LEANNE FEDOR                                          */
/*           ADDED CUSTOMIZED PRINTER OVERRIDE COMMAND TO PRINT    */
/*           REPORTS ON LASER PRINTERS.                            */
/*                                                                 */
/* 05/21/96  LEANNE FEDOR                                          */
/*           IN PREPARATION FOR HAVING TWO BOXES (CISC AND RISC),  */
/*           REPLACED STANDARD CREATE DUPLICATE OBJECT COMMAND     */
/*           WITH A COMMAND CUSTOMIZED BY JOHN ERICSON. THIS       */
/*           COMMAND WILL RETRIEVE/USE THE FILE FROM THE APPRO-    */
/*           PRIATE LIBRARY INSTEAD OF USING HARDCODED 'PRKFLIB'.  */
/*           THE CUSTOMIZED COMMAND IS CRTDUPOBJL FOR 'CREATE      */
/*           DUPLICATE OBJECT FROM LIBRARY'.                       */
/*                                                                 */
/* 06/03/97 LEANNE FEDOR                                           */
/*          RECOMPILE ONLY.  NEW FIELDS 'PURGE FLAG' AND 'DATE OF  */
/*          FIRST PURGE' ADDED TO HOG GROUP FILE.                  */
/*                                                                 */
/* 06/19/97 LEANNE FEDOR                                           */
/*          CHANGED OUT QUEUE LOGIC.                               */
/*                                                                 */
/* 05/22/01 LEANNE FEDOR                                           */
/*          THIS CL WAS USED TO PRINT EITHER THE A) SUMMARY OR     */
/*          B) PERFORMANCE REPORT. NOW, IT IS USED ONLY FOR        */
/*          THE PERFORMANCE REPORT.                                */
/*                                                                 */
/* 06/27/01 LEANNE FEDOR                                           */
/*          REMOVED 'MULTISITE' AS A REPORT SELECTOR.              */
/*                                                                 */
/* 06/23/10 LEANNE RAMSEY                                          */
/*          REPLACED HSJ018G WITH HSJ034N TO GIVE US A JOIN FILE   */
/*          KEYED BY HOG GROUP PRODUCTION PHASE INSTEAD OF FARM    */
/*          SITE PRODUCTION PHASE.                                 */
/*                                                                 */
/*******************************************************************/

             PGM

/* DEFINE THE VARIABLE REQUIRED IN THE COMMAND TO OVERRIDE THE     */
/* PRINT FILE TO A LASER PRINTER                                   */

             DCL        VAR(&CMD) TYPE(*CHAR) LEN(512)

             DCL        VAR(&QDATA)  TYPE(*CHAR) LEN(225)
             DCL        VAR(&LDFSBO) TYPE(*CHAR) LEN(5)
             DCL        VAR(&LDFYMD) TYPE(*DEC)  LEN(8)
             DCL        VAR(&LDTYMD) TYPE(*DEC)  LEN(8)

             DCL        VAR(&LDPTCD) TYPE(*CHAR) LEN(5)
             DCL        VAR(&LDPPCD) TYPE(*CHAR) LEN(5)
             DCL        VAR(&LDFSCD) TYPE(*DEC) LEN(5 0)
             DCL        VAR(&LDCELL) TYPE(*DEC) LEN(2 0)
             DCL        VAR(&LDGTCD) TYPE(*CHAR) LEN(1)
             DCL        VAR(&LDHGCD) TYPE(*CHAR) LEN(7)

             DCL        VAR(&HOLD) TYPE(*CHAR) LEN(10)
             DCL        VAR(&COPYS) TYPE(*DEC) LEN(2)
             DCL        VAR(&OUTQ) TYPE(*CHAR) LEN(10)

             CHGVAR     VAR(&LDFSBO) VALUE(%SST(*LDA 1 5))
             CHGVAR     VAR(&LDFYMD) VALUE(%SST(*LDA 36 8))
             CHGVAR     VAR(&LDTYMD) VALUE(%SST(*LDA 50 8))
             CHGVAR     VAR(&LDPPCD) VALUE(%SST(*LDA 79 5))
             CHGVAR     VAR(&LDPTCD) VALUE(%SST(*LDA 64 5))
             CHGVAR     VAR(&LDFSCD) VALUE(%SST(*LDA 149 5))
             CHGVAR     VAR(&LDGTCD) VALUE(%SST(*LDA 114 1))
             CHGVAR     VAR(&LDHGCD) VALUE(%SST(*LDA 224 7))
             CHGVAR     VAR(&LDCELL) VALUE(%SST(*LDA 179 2))

/*  CREATE WORKFILE                                              */

             CRTDUPOBJL OBJ(HSP390) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)

/*-------------------------------------------------------------------*/
/*  SETUP QUERY SELECTION OF HOG GROUPS                              */
/*-------------------------------------------------------------------*/

/*  ALWAYS SELECT ONLY GROUPS IN THE DATE RANGE SELECTED             */

             CHGVAR     VAR(%SST(&QDATA 1 10)) VALUE('HGDSDT +
                          *GE')
             CHGVAR     VAR(%SST(&QDATA 12 8)) VALUE(&LDFYMD)
             CHGVAR     VAR(%SST(&QDATA 21 15)) VALUE('*AND HGDSDT +
                          *LE')
             CHGVAR     VAR(%SST(&QDATA 37 8)) VALUE(&LDTYMD)

/*  IF THE USER SELECTED A SPECIFIC BUSINESS OFFICE                  */

             IF         COND(&LDFSBO *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 46 17)) VALUE('*AND HGFSBO +
                          *EQ "')
             CHGVAR     VAR(%SST(&QDATA 63 5)) VALUE(&LDFSBO)
             CHGVAR     VAR(%SST(&QDATA 68 1)) VALUE('"')
             ENDDO

/* IF THE USER SELECTED A SPECIFIC GROUP                             */

             IF         COND(&LDHGCD *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 92 17)) VALUE('*AND HGHGCD +
                          *EQ "')
             CHGVAR     VAR(%SST(&QDATA 109 7)) VALUE(&LDHGCD)
             CHGVAR     VAR(%SST(&QDATA 116 1)) VALUE('"')
             ENDDO

/*  IF THE USER SELECTED A PRODUCTION TYPE                          */

             IF         COND(&LDPTCD *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 118 17)) VALUE('*AND HGPTCD +
                          *EQ "')
             CHGVAR     VAR(%SST(&QDATA 135 5)) VALUE(&LDPTCD)
             CHGVAR     VAR(%SST(&QDATA 140 1)) VALUE('"')
             ENDDO

/*  IF THE USER SELECTED A PRODUCTION PHASE                      */

             IF         COND(&LDPPCD *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 142 17)) VALUE('*AND HGPPCD +
                          *EQ "')
             CHGVAR     VAR(%SST(&QDATA 159 5)) VALUE(&LDPPCD)
             CHGVAR     VAR(%SST(&QDATA 164 1)) VALUE('"')
             ENDDO

/*  IF THE USER SELECTED A FARM SITE                             */

             IF         COND(&LDFSCD *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 166 4)) VALUE('*AND')
             CHGVAR     VAR(%SST(&QDATA 171 10)) VALUE('HGFSCD +
                          *EQ')
             CHGVAR     VAR(%SST(&QDATA 182 5)) VALUE(&LDFSCD)
             ENDDO


/*  IF THE USER SELECTED A GROUP TYPE                            */

             IF         COND(&LDGTCD *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 188 17)) VALUE('*AND HGGTCD +
                          *EQ "')
             CHGVAR     VAR(%SST(&QDATA 205 1)) VALUE(&LDGTCD)
             CHGVAR     VAR(%SST(&QDATA 206 1)) VALUE('"')
             ENDDO


/*  IF THE USER SELECTED A CELL                                  */

             IF         COND(&LDCELL *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 208 15)) VALUE('*AND FSCELL +
                          *EQ')
             CHGVAR     VAR(%SST(&QDATA 224 2)) VALUE(&LDCELL)
             ENDDO


/*  RUN QUERY FOR HOG GROUPS                                     */

             OVRDBF     FILE(HSJ034N) SHARE(*YES)
             OPNQRYF    FILE((HSJ034N)) QRYSLT(&QDATA) KEYFLD(*FILE) +
                          SEQONLY(*YES)

/*-------------------------------------------------------------------*/
/*  CALL PROGRAM TO BUILD WORKFILE                                   */
/*-------------------------------------------------------------------*/

             CALL       PGM(HP390)

/*-------------------------------------------------------------------*/
/* SET PARAMETERS FROM LDA FOR PRINTING                              */
/*-------------------------------------------------------------------*/

 HOLD:       IF         COND(%SST(*LDA 411 1) = 'Y') THEN(CHGVAR +
                          VAR(&HOLD) VALUE(*YES))
             ELSE       CMD(CHGVAR VAR(&HOLD) VALUE(*NO))

 COPYS:      CHGVAR     VAR(&COPYS) VALUE(%SST(*LDA 412 1))
             IF         COND(&COPYS = 0) THEN(CHGVAR VAR(&COPYS) +
                          VALUE(1))

 OUTQ:       CHGVAR     VAR(&OUTQ) VALUE(%SST(*LDA 413 10))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(%SST(*LDA 401 10)))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(*JOB))

/*-------------------------------------------------------------------*/
/* PRINT REPORT                                                      */
/*-------------------------------------------------------------------*/

/* THIS REPORT IS CONDENSED PRINT AT 15 CPI; SO, ON A LASER PRINTER */
/* FORCE IT TO DRAWER '2' FOR LEGAL PAPER AND FORCE 'LANDSCAPE'     */

             SBDPRTOVR  CMDVAR(&CMD) FILE(QPRINT) OUTQ(&OUTQ) +
                          WIDTH(198) LANDSCAPE(*YES) DRAWER(2) +
                          COPIES(&COPYS) HOLD(&HOLD)
             CALL       PGM(QCMDEXC) PARM(&CMD 512)

             CALL PGM(HP690)


/*-------------------------------------------------------------------*/
/* DELETE FILE OVERRIDES                                             */
/*-------------------------------------------------------------------*/

             CLOF       OPNID(HSJ034N)
             DLTOVR     FILE(*ALL)

             ENDPGM

