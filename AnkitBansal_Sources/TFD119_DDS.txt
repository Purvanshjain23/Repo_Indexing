     A*%%TS  SD  20080702  095557  ISLRAMS     REL-V5R4M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        TRIUMPH FOODS
     A*  DISPLAY FILE:  TFD119
     A*  TITLE:         REVISE PRODUCT EXCEPTION
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  DATE:          09/08/06
     A*
     A*  INDICATOR 60 IS ON WHEN IN REVISE MODE
     A*  INDICATOR 61 IS ON WHEN IN CREATE MODE
     A*  INDICATOR 62 IS ON WHEN IN DELETE MODE
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/TFPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A N62                                  CF04(04 'PROMPT')
     A  60N62                               CF05(05 'REFRESH')
     A N62                                  CF09(09 'ACCEPT')
     A  60N62                               CF11(11 'DELETE')
     A                                      CA12(12 'PREVIOUS')
     A*
     A*--------------------------------------------------------------
     A* RECORD FORMAT ONE
     A*--------------------------------------------------------------
     A          R TF119R1
     A*%%TS  SD  20080702  095557  ISLRAMS     REL-V5R4M0  5722-WDS
     A                                      SETOF(30)
     A                                      SETOF(31)
     A                                      SETOF(32)
     A                                      SETOF(37)
     A                                      SETOF(38)
     A                                      SETOF(39)
     A                                      SETOF(40)
     A                                      SETOF(41)
     A                                      SETOF(42)
     A                                      SETOF(44)
     A                                      SETOF(45)
     A                                      SETOF(50)
     A                                      SETOF(51)
     A                                      SETOF(52)
     A                                      RTNCSRLOC(&RCD &FLD)
     A                                      OVERLAY
     A            RCD       R        H      REFFLD(@@RCD)
     A            FLD       R        H      REFFLD(@@FLD)
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 27'REVISE PRODUCT EXCEPTION'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USR)
     A                                      DSPATR(HI)
     A            D1MODE         6A  O  2  2DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USR)
     A                                      DSPATR(HI)
     A                                  4  2'Week-ending date:'
     A            D1WEDTMDY R        O  4 20REFFLD(TFREF/@@DT06 *LIBL/TFPREF)
     A                                      EDTWRD('  /  /  -')
     A                                  6  2'Product code ....'
     A            D1PRCD    R        B  6 20REFFLD(TFREF/@@PRCD *LIBL/TFPREF)
     A                                      EDTCDE(M)
     A  30
     AO 50                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A  61N30                               DSPATR(HI)
     A  61                                  DSPATR(UL)
     A N61                                  DSPATR(PR)
     A  61                              6 25'?'
     A            D1PRDS    R        O  6 32REFFLD(TFREF/@@PRDS *LIBL/TFPREF)
     A                                  7  2'Comment .........'
     A            D1EXCOM   R        B  7 20REFFLD(TFREF/@@EXCOM *LIBL/TFPREF)
     A                                      CHECK(LC)
     A N62                                  DSPATR(HI)
     A                                  9  2'FLAGS:'
     A                                      DSPATR(UL)
     A                                      COLOR(BLU)
     A                                  9 23'Normal Flag Values'
     A                                      DSPATR(UL)
     A                                 10  2'Mix flag ......'
     A                                 10 18'?'
     A            D1MIXFL   R        B 10 20REFFLD(TFREF/@@FLAG *LIBL/TFPREF)
     A  31
     AO 51                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A N62N31                               DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(PR)
     A            D1STDMIXFLR        O 10 30REFFLD(TFREF/@@FLAG *LIBL/TFPREF)
     A                                 11  2'Volume flag ...'
     A                                 11 18'?'
     A            D1VOLFL   R        B 11 20REFFLD(TFREF/@@FLAG *LIBL/TFPREF)
     A  32
     AO 52                                  DSPATR(PC)
     A  32                                  DSPATR(RI)
     A N62N32                               DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(PR)
     A            D1STDVOLFLR        O 11 30REFFLD(TFREF/@@FLAG *LIBL/TFPREF)
     A                                 13  2'ADJUSTMENT VALUES:'
     A                                      DSPATR(UL)
     A                                      COLOR(BLU)
     A                                 13 33'SEABOARD           '
     A                                      DSPATR(UL)
     A                                      COLOR(BLU)
     A                                 13 55'TRIUMPH            '
     A                                      DSPATR(UL)
     A                                      COLOR(BLU)
     A                                 14  2'Sold pounds .................'
     A            D1SEXSLLB R        B 14 33REFFLD(TFREF/@@SEXSLLB *LIBL/TFPREF)
     A                                      EDTCDE(K)
     A  37                                  DSPATR(PC)
     A  37                                  DSPATR(RI)
     A N62N37                               DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(PR)
     A            D1TEXSLLB R        B 14 55REFFLD(TFREF/@@TEXSLLB *LIBL/TFPREF)
     A                                      EDTCDE(K)
     A  38                                  DSPATR(PC)
     A  38                                  DSPATR(RI)
     A N62N38                               DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(PR)
     A                                 15  2'Net product revenue amount ..'
     A            D1SEXNPRAMR        B 15 34REFFLD(@@SEXNPRAM)
     A                                      EDTCDE(K)
     A  39                                  DSPATR(PC)
     A  39                                  DSPATR(RI)
     A N62N39                               DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(PR)
     A            D1TEXNPRAMR        B 15 56REFFLD(@@TEXNPRAM)
     A                                      EDTCDE(K)
     A  40                                  DSPATR(PC)
     A  40                                  DSPATR(RI)
     A N62N40                               DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(PR)
     A                                 16  2'Produced pounds .............'
     A            D1SEXPULB R        B 16 33REFFLD(TFREF/@@SEXPULB *LIBL/TFPREF)
     A                                      EDTCDE(K)
     A  41                                  DSPATR(PC)
     A  41                                  DSPATR(RI)
     A N62N41                               DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(PR)
     A            D1TEXPULB R        B 16 55REFFLD(TFREF/@@TEXPULB *LIBL/TFPREF)
     A                                      EDTCDE(K)
     A  42                                  DSPATR(PC)
     A  42                                  DSPATR(RI)
     A N62N42                               DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(PR)
     A                                 17  2'Produced amount .............'
     A            D1SEXPUAM R        B 17 34REFFLD(TFREF/@@SEXPUAM *LIBL/TFPREF)
     A                                      EDTCDE(K)
     A  44                                  DSPATR(PC)
     A  44                                  DSPATR(RI)
     A N62N44                               DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(PR)
     A            D1TEXPUAM R        B 17 56REFFLD(TFREF/@@TEXPUAM *LIBL/TFPREF)
     A                                      EDTCDE(K)
     A  45                                  DSPATR(PC)
     A  45                                  DSPATR(RI)
     A N62N45                               DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(PR)
     A*--------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*--------------------------------------------------------------
     A          R TF119K1
     A*%%TS  SD  20061117  161942  ISLRAMS     REL-V5R3M0  5722-WDS
     A  43                             22  2'Message ''F4gram failed.  Call MIS-
     A                                       user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A N62                             23 11'F4=Prompt'
     A N62N61                          23 22'F5=Refresh'
     A N62                             23 34'F9=Accept'
     A N62N61                          23 45'F11=Delete'
     A                                 23 57'F12=Previous'
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *---------------------------------------------------------------
     A          R TF119ES                   SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE CONTROL FORMAT
      *---------------------------------------------------------------
     A          R TF119EC                   SFLCTL(TF119ES)
      *
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
