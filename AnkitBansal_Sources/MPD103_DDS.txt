     A*%%TS  SD  20140306  103528  ISLRAMS     REL-V6R1M0  5761-WDS
     A*****************************************************************
     A*
     A* ENVIRONMENT:  PORK DIVISION
     A* SYSTEM:       AS/400
     A* FILE:         MPD103
     A* DESCRIPTION:  MP2: REVISE UNPOSTED RECEIPTS
     A* PROGRAMMER:   LEANNE FEDOR
     A* DATE:         03/20/02
     A*
     A*----------------------------------------------------------------
     A* MODIFICATIONS LOG:
     A*----------------------------------------------------------------
     A* MODIFIED BY:  BARB GUTIERREZ 03/29/2011
     A*               QTY AND RATE INCREASED.  RECOMPILED ONLY.
     A*
     A* 06/08/21 S17068 Danny Nguyen
     A*          Unit Cost Per Item field length changed from 9.4 to 11.4.
     A*          Recompile only.
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/MP2REF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A                                      CF05(05 'REFRESH')
     A*
     A*---------------------------------------------------------------
     A* RECORD FORMAT ONE
     A*---------------------------------------------------------------
     A          R MP103R1
     A*%%TS  SD  20140306  103528  ISLRAMS     REL-V6R1M0  5761-WDS
     A                                      SETOF(31)
     A                                      OVERLAY
     A            SDPGM         10A  O  1  2DSPATR(HI)
     A                                  1 26'MP2 INTERFACE TO JDE'
     A                                      DSPATR(HI)
     A            SDUSR         10A  O  1 70DSPATR(HI)
     A            D1MODE         6A  O  2  2DSPATR(HI)
     A                                  2 24'REVISE UNPOSTED RECEIPTS'
     A                                      DSPATR(HI)
     A            SDJOB         10A  O  2 70DSPATR(HI)
     A                                  4  2'Purchase order ......:'
     A            D1PONO    R        O  4 25REFFLD(@@PONO)
     A                                  4 46'Purchasing center:'
     A            D1PCID    R        O  4 65REFFLD(@@PCID)
     A                                  5  2'JDE vendor number ...:'
     A            D1JDVNNO  R        O  5 25REFFLD(@@JDVNNO)
     A                                      EDTCDE(M)
     A                                  5 45'Accounting company:'
     A            D1ACONO   R        O  5 65REFFLD(@@ACONO)
     A                                      EDTCDE(Z)
     A                                  6  2'Transaction type code:'
     A            D1TTCD    R        O  6 25REFFLD(@@TTCD)
     A                                  7  2'Receipt number ......:'
     A            D1RCNO    R        O  7 25REFFLD(@@RCNO)
     A                                      EDTCDE(M)
     A                                  8  2'Serial number .......:'
     A            D1SERNO   R        O  8 25REFFLD(@@SERNO)
     A                                      EDTCDE(M)
     A                                  9  2'Transaction date ....:'
     A            D1TRDT    R        O  9 25REFFLD(@@TRDT)
     A                                      EDTWRD('    -  -  ')
     A                                 10  2'Transaction quantity :'
     A            D1TRQT    R        O 10 25REFFLD(@@TRQT)
     A                                      EDTCDE(K)
     A                                 11  2'Item number .........:'
     A            D1ITEMNO  R        O 11 25REFFLD(@@ITEMNO)
     A                                 12  2'Item description ....:'
     A            D1ITEMDS  R        O 12 25REFFLD(@@ITEMDS)
     A                                 13  2'Unit cost per item ...'
     A            D1ITEMRT  R        O 13 25REFFLD(@@ITEMRT)
     A                                      EDTCDE(K)
     A                                 14  2'Cost center ..........'
     A            D1MCU     R        B 14 25REFFLD(@@MCU)
     A                                      CHECK(RB)
     A N31                                  DSPATR(HI)
     A  31                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A                                      DSPATR(UL)
     A                                 15  2'Object account .......'
     A            D1OBJ     R        B 15 25REFFLD(@@OBJ)
     A N31                                  DSPATR(HI)
     A  31                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A                                      DSPATR(UL)
     A                                 16  2'Subsidiary ...........'
     A            D1SUB     R        B 16 25REFFLD(MPREC/@@SUB *LIBL/MP2REF)
     A N31                                  DSPATR(HI)
     A  31                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A                                      DSPATR(UL)
     A                                 17  2'Comment ..............'
     A            D1COM     R        B 17 25REFFLD(@@COM)
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R MP103K1
     A*%%TS  SD  20000925  143030  ISLRAMS     REL-V4R4M0  5769-PW1
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A                                 23 11'F5=Refresh'
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *---------------------------------------------------------------
     A          R MP103ES                   SFL SFLMSGRCD(24)
      *
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE CONTROL FORMAT
      *---------------------------------------------------------------
     A          R MP103EC                   SFLCTL(MP103ES)
      *
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
