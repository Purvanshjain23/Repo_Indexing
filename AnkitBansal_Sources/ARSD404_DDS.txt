     A*%%TS  SD  19940919  092929  ISSZURN     REL-V3R0M5  5763-PW1
     A*****************************************************************
     A*  PROGRAM NUMBER  -> ARSD404
     A*
     A*  PROGRAM NAME    -> A/R LOCKBOX FILE INQUIRY  TO PRINT PROOF
     A*                     LISTING
     A*
     A*  PROGRAM PURPOSE -> THIS PROGRAM DISPLAYS ALL THE UNPOSTED
     A*                     BATCHES IN BANK DEPOSIT BATCH CONTROL
     A*                     FILE TO CHOOSE FROM TO PRINT THE PROOF
     A*                     LISTING.
     A*
     A*  PROGRAMMER      -> SHARON ZURN             DATE -> 04/11/94
     A*  MODIFIED BY     ->                         DATE ->   /  /
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CF01(01 'EXIT')
     A*            11:03:54    SBDFARMS    REL-R07M00  5714-UT1
     A          R ARSS01S                   SFL
     A            DSLKBX         7Y 0O  9  5EDTCDE(Z)
     A            DSCONO         2S 0O  9 14
     A            DSBKNM        30A  O  9 23
     A            DSDPDT         6Y 0O  9 57EDTCDE(Y)
     A            DSTOT$        10Y 2O  9 68EDTCDE(2)
     A            CKOPT          1A  B  9  2
     A            DSBKNO         2S 0O  9 20
     A          R ARSS01C                   SFLCTL(ARSS01S)
     A                                      SFLSIZ(0065)
     A                                      SFLPAG(0010)
     A  74                                  SFLDSP
     A  70                                  SFLDSPCTL
     A  72                                  SFLCLR
     A                                      OVERLAY
     A                                  1  2'ARS404'
     A                                      DSPATR(HI)
     A                                  2  2'ARSS01C'
     A                                      DSPATR(HI)
     A            CONAME        30A  O  1 28DSPATR(HI)
     A                                  1 71DATE
     A                                      EDTCDE(Y)
     A                                  2 28'PROOF LISTING DATE SELECTION'
     A                                      DSPATR(HI)
     A                                  2 71TIME
     A                                  7 14'CO'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  7 58'  DATE  '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  7 69'  AMOUNT   '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  7  1'OPT'
     A                                      DSPATR(HI)
     A                                  6 70'LOCKBOX'
     A                                      DSPATR(HI)
     A                                  7  6'NUMBER'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  6  5'LOCKBOX'
     A                                      DSPATR(HI)
     A                                  6 58'DEPOSIT'
     A                                      DSPATR(HI)
     A                                  7 20'BANK NUMBER / NAME                '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  5  2'P=PRINT LISTING'
     A                                      DSPATR(HI)
     A          R ARSS09D
     A                                 23  4'CMD 1 - RETURN TO MENU'
     A                                      DSPATR(HI)
     A                                 23 34'ENTER TO CONTINUE'
     A                                      DSPATR(HI)
      *
     A  55                             24  5'*NO DATA EXISTS FOR ANY LOCKBOXES*'
     A                                      DSPATR(HI)
     A  29                             24  5'PROOF LISTING HAS BEEN SUBMITTED'
     A                                      DSPATR(HI)
