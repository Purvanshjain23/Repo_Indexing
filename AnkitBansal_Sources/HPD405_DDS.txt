     A*%%TS  SD  19970613  162059  ISLRAMS     REL-V3R6M0  5716-PW1
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION
     A*  DISPLAY FILE:  HPD405
     A*  TITLE:         WORK WITH FEED MANUFACTURING TYPES
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  CREATED:       04/05/94
     A*
     A*****************************************************************
     A* MODIFICATIONS:
     A* 06/13/97  LEANNE FEDOR
     A*           ADDED A FUNCTION KEY FOR PRINT OPTIONS.
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A N99                                  CF05(05 'REFRESH')
     A N99                                  CF06(06 'CREATE')
     A N99                                  CF07(07 'LISTING')
     A N99                                  CF17(17 'PRINT OPTIONS')
     A N99                                  CF20(20 'WRKSPLF')
     A*
     A*---------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP405S1                   SFL
     A*%%TS  SD  19941209  114239  ISLRAMS     REL-V3R0M5  5763-PW1
     A  85                                  SFLNXTCHG
     A            HDRRN1         4Y 0H
     A            DFOPT          2A  B  7  2CHGINPDFT(UL)
     A  30                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A N30                                  DSPATR(HI)
     A  99                                  DSPATR(PR)
     A            DFFTCD    R        O  7 12REFFLD(FTFTCD)
     A            DFFTDS    R        O  7 22REFFLD(FTFTDS)
     A            DFFTST    R        O  7 41REFFLD(FTFTST)
     A*
     A*---------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP405C1                   SFLCTL(HP405S1)
     A N82N99                               ROLLUP(25 'PAGE FORWARD')
     A                                      SFLSIZ(0050)
     A                                      SFLPAG(0015)
     A                                      OVERLAY
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A            HDPAG1         4S 0H      SFLRCDNBR
     A                                  1  2'HP405'
     A                                      DSPATR(HI)
     A                                  1 26'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  2 20'WORK WITH FEED MANUFACTURING TYPES'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  4  2'2=Revise'
     A                                  4 12'4=Delete'
     A                                  6  2'Opt'
     A                                      DSPATR(HI)
     A                                  6  8'Feed Type'
     A                                      DSPATR(HI)
     A                                  6 22'Description'
     A                                      DSPATR(HI)
     A                                  6 39'Status'
     A                                      DSPATR(HI)
     A*
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R HP405K1
     A*%%TS  SD  19970613  162059  ISLRAMS     REL-V3R6M0  5716-PW1
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A N99                             23 11'F5=Refresh'
     A N99                             23 23'F6=Create'
     A N99                             23 34'F7=Listing'
     A N99                             23 46'F17=Print Options'
     A N99                             23 65'F20=Spool Files'
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *---------------------------------------------------------------
     A          R HP405ES                   SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE CONTROL FORMAT
      *---------------------------------------------------------------
     A          R HP405EC                   SFLCTL(HP405ES)
      *
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
