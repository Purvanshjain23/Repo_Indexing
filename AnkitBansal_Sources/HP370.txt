      *****************  RPG PROGRAM HEADING  ************************
      * SYSTEM:      HOG PRODUCTION
      * PROGRAM:     HP370
      * TITLE:       BUILD WORKFILE FOR VARIOUS MOVEMENT REPORTS
      * PROGRAMMER:  SHARON ZURN
      * CREATED:     5/19/94
      *
      * *NOTE:       ALL SCHEDULED MOVEMENTS ARE OMITTED.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 04/20/95  LEANNE FEDOR
      *           CHANGED FARM SITE CODE FROM ALPHA TO NUMERIC
      *
      * 08/10/95  LEANNE FEDOR
      *           REWRITTEN FOR MOVEMENT REWRITE
      *
      * 05/13/96  LEANNE FEDOR
      *           MADE CHANGES TO FACILITATE NEW JDE RELEASE CONVERSION
      *           CHANGED SEARCH TYPE TO 3A; CALL NEW GENERIC
      *           PROGRAM HPJDEV TO VALIDATE INSTEAD OF HPJDEA SINCE
      *           NO VENDOR ADDRESS IS REQUIRED.
      *
      * 06/08/00  LeAnne Fedor
      *           Added origin cell and destination cell.
      *
      * 07/12/01  LeAnne Fedor
      *           Alice wants to see farm site number (and as much name as possible) for
      *           destination farms. So, I changed the logic to concatenate the farm site
      *           number and name together.
      *
      * 08/08/02  LeAnne Fedor
      *           Added new DOA head and KOA head fields to workfile. The original 'DOHD'
      *           field now holds the sum of DOA + KOA.
      *
      * 12/18/02  LeAnne Fedor
      *           Production Phase/Type were removed from Sales Movement Header
      *           file. So, we changed the logic here to retrieve these values
      *           from the Hog Group master file.
      *
      * 07/06/09  LeAnne Ramsey
      *           Recompile only. Added new field 'Continuous Flow Flag' to Hog Group file.
      *
      * 05/23/11  LeAnne Ramsey (E1556)
      *           Added Sex Code for the Destination Group in workfile HSP370.
      *
      * 10/15/13  LeAnne Ramsey (E2831)
      *           Recompile only. Added field 'MTech Reference'.
      *
      * 06/07/21  Danny Nguyen   (P2098)
      *           Recompile only due to HSP370 file change.
      * 06/11/24  Jagdish Mistry (WI628)
      *           Add paramter to get count of records added in work file.
      *
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     FHSP018    IF   E           K DISK
      *    FARM SITE
      *
      *
     FHSL034D   IF   E           K DISK
      *    HOG GROUP
      *
      *
     FHSP058    IF   E           K DISK
      *    MOVEMENT EVENT FILE   (records selected by OPNQRY)
      *
      *
     FHSP074    IF   E           K DISK
      *  TRANSFER MOVEMENT HEADER
      *
      *
     FHSP075    IF   E           K DISK
      *  TRANSFER MOVEMENT DETAIL
      *
      *
     FHSP084    IF   E           K DISK
      *  SALE MOVEMENT HEADER
      *
      *
     FHSP085    IF   E           K DISK
      *  SALE MOVEMENT DETAIL
      *
      *
     FHSP370    O    E             DISK
      *    HOG MOVEMENT WORKFILE
      *
      *‚JM-WI628 06/11/2024 - Get Count of new records added in work file
     D p1CntNewRecds   S              8  0
     D wkCntNewRecds   S              8  0 Inz(*Zeros)
      /EJECT
      ****************************************************************
      * Definition Specifications
      ****************************************************************
      *
      *---------------------------------------------------------------
      *  NAMED CONSTANTS
      *---------------------------------------------------------------
      *
     D YES             C                   CONST('Y')
     D NO              C                   CONST('N')
      *
      *---------------------------------------------------------------
      * NAMED CONSTANTS
      *---------------------------------------------------------------
      * FOR ERROR MESSAGE HANDLING, A PACKED INDEX (E) IS REQUIRED.
      * FOR PROGRAM READABILITY, DEFINE A CORRESPONDING ERROR COUNT
      * FIELD CALLED 'ERROR'. ALSO, DEFINE CURSOR LOCATION FIELDS.
      *
     D                 DS                  INZ
     D  ERROR                  1      2P 0
     D  E                      1      2P 0
      *
      * THE FOLLOWING 3 DATA STRUCTURES ARE USED TO SPEED MESSAGE
      * HANDLING SINCE IT IS FASTER TO CLEAR DATA STRUCTURES THAN
      * ARRAYS.  EACH IS ASSOCIATED WITH A STANDARD MESSAGE ARRAY.
      *
     D MGI             DS           140    INZ
     D  MGID                   1    140
     D                                     DIM(20)                              MSG ID ARRAY
      *
     D MGD             DS          1000    INZ
     D  MGDT                   1   1000
     D                                     DIM(20)                              MSG PARMS
      *
     D MGWK            DS            50    INZ
     D  MGW                    1     50
     D                                     DIM(50)                              MSG WORK PARMS
      *
      *---------------------------------------------------------------
      * STANDARD PROGRAM STATUS DATA STRUCTURE
      *---------------------------------------------------------------
      *    EXTERNALLY DEFINED AS UTPGFR (RECORD FORMAT: PGMDSR)
     D PGMDS         ESDS                  EXTNAME(UTPGFR)
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * MAIN LINE
      ****************************************************************
      *
      * READ EACH EVENT RECORD SELECTED BY THE OPEN QUERY AND PROCESS
      * EACH UNIQUE MOVEMENT.
      *
     C     *LOVAL        SETLL     HSP058
      *
     C     *IN90         DOUEQ     *ON                                          DO MAIN
     C                   READ      HSP058                                 90
     C     *IN90         IFEQ      *OFF                                         IF EVENT
     C     MEMVSN        ANDNE     SVMVSN
      *
     C                   Z-ADD     MEMVSN        SVMVSN
      *
     C                   Z-ADD     MEMVSN        WFMVSN
     C                   MOVEL     MEMTCD        WFMTCD
     C                   Z-ADD     MEMEDT        WFMEDT
     C                   Z-ADD     MECVNO        WFCVNO
      *
     C                   SELECT
     C     MEMTCD        WHENEQ    'S'
     C                   EXSR      $SALE
      *
     C     MEMTCD        WHENEQ    'T'
     C                   EXSR      $TRAN
     C                   ENDSL
      *
     C                   EXSR      $CLEAR
      *
     C                   ENDIF                                                   IF EVENT
     C                   ENDDO                                                   MAIN DO
      *‚JM-WI628 06/11/2024 - Get Count of new records added in work file
     C                   Eval      p1CntNewRecds = wkCntNewRecds
      *
     C                   SETON                                        LR
      /EJECT
      *----------------------------------------------------------------
      * PROCESS A SALES MOVEMENT
      *----------------------------------------------------------------
      *
     C     $SALE         BEGSR
      *
      * RETRIEVE SALES MOVEMENT HEADER INFO AND CONTINUE PROCESSING
      * IF THE STATUS OF THE MOVEMENT IS NOT 'SCHEDULED'
      *
     C     MEMVSN        CHAIN     HSP084                             92
     C     *IN92         IFEQ      *OFF                                         IF NOT SCHED
     C     SHMSCD        ANDNE     'SC'
      *
     C                   MOVEL     SHMSCD        WFMSCD
     C                   MOVEL     SHSTCD        WFSTCD
     C                   MOVEL     SHSTCD        WFCODE
     C                   Z-ADD     SHSCDT        WFSCDT
     C                   Z-ADD     SHSHDT        WFSHDT
     C                   Z-ADD     SHRCDT        WFRCDT
     C                   MOVEL     SHTICD        WFTICD
      * ORIGIN
     C                   MOVEL     SHFSBO        WFORBO
     C                   Z-ADD     SHFSCD        WFORFS
      *
      *
      * RETRIEVE THE CELL OF THE ORIGIN FARM
      *
     C     WFORFS        CHAIN     HSP018                             92
     C     *IN92         IFEQ      *OFF
     C                   z-add     fscell        wforcl
     C                   ELSE
     C                   z-add     0             wforcl
     C                   ENDIF
      *
      * RETRIEVE THE CUSTOMER NAME
      *
     C                   Z-ADD     WFCVNO        INAJD
     C                   MOVEL(P)  'C'           INAT1
     C                   EXSR      $JDEV
     C     JDEFL         IFEQ      YES                                          IF NOT FOUND
     C                   MOVEL(P)  'UNKNOWN'     WFDNNM
     C                   ELSE
     C                   MOVEL     OUALPH        WFDNNM
     C                   ENDIF                                                  IF NOT FOUND
      *
      * RETREIVE ALL DETAIL RECORDS FOR THIS SALES MOVEMENT AND
      * WRITE A WORKFILE RECORD FOR EACH DETAIL RECORD.
      *
     C     MEMVSN        SETLL     HSP085
      *
     C     *IN91         DOUEQ     *ON                                          DO SALES DTL
     C     MEMVSN        READE     HSP085                                 91
     C     *IN91         IFEQ      *OFF                                         IF FOUND
      *
     C                   Z-ADD     SGSCHD        WFSCHD
     C                   Z-ADD     SGSHHD        WFSHHD
     C                   Z-ADD     SGSHLB        WFSHLB
      *
     C                   Z-ADD     SGLVHD        WFQLHD
      *
      * Sales movement detail records do not have 'KOA head'.
      * On sales movements, everything in the 'DOHD' field truly DOA--no KOA is
      * included like in transfers. SO, move this value to both of the following workfile
      * fields:
      *        DOA/KOA head
      *        DOA head
     C                   Z-ADD     SGDOHD        WFDOHD
     C                   Z-ADD     SGDOHD        WFDOAHD
     C                   Z-ADD     0             WFKOAHD
      *
     C                   Z-ADD     SGLVLB        WFQLLB
     C                   Z-ADD     SGDOLB        WFDOLB
      *
      * RETRIEVE HOG GROUP INFO
      *
     C                   Z-ADD     SGHGSN        WFORSN
     C     SGHGSN        CHAIN     HSL034D                            92
     C     *IN92         IFEQ      *OFF
     C                   MOVEL     HGHGCD        WFORHG
     C                   MOVEL     HGBLCD        WFORBL
     C                   MOVEL     HGRMCD        WFORRM
     C                   MOVEL     HGPTCD        WFORPT
     C                   MOVEL     HGPPCD        WFORPP
     C                   ELSE
     C                   MOVE      *BLANK        WFORHG
     C                   MOVE      *BLANK        WFORBL
     C                   MOVE      *BLANK        WFORRM
     C                   MOVEL     *BLANK        WFORPT
     C                   MOVEL     *BLANK        WFORPP
     C                   ENDIF
      *
      *‚JM-WI628 06/11/2024-Get Count of new records added in work file
     C                   Eval      wkCntNewRecds += 1
     C                   WRITE     WFREC
     C                   ENDIF                                                  IF FOUND
     C                   ENDDO                                                  DO SALES DTL
     C                   ENDIF                                                  IF NOT SCHED
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * PROCESS A TRANSFER MOVEMENT
      *----------------------------------------------------------------
      *
     C     $TRAN         BEGSR
      *
     C                   MOVEL     'TRAN '       WFCODE
      *
      * RETRIEVE TRANSFER MOVEMENT HEADER INFO AND CONTINUE
      * PROCESSING IF THE STATUS IS NOT 'SCHEDULED'
      *
     C     MEMVSN        CHAIN     HSP074                             92
     C     *IN92         IFEQ      *OFF                                         IF NOT SCHED
     C     MHMSCD        ANDNE     'SC'
     C                   MOVEL     MHMSCD        WFMSCD
     C                   Z-ADD     MHSCDT        WFSCDT
     C                   Z-ADD     MHSHDT        WFSHDT
     C                   Z-ADD     MHRCDT        WFRCDT
     C                   MOVEL     MHTICD        WFTICD
      * ORIGIN
     C                   MOVEL     MHORBO        WFORBO
     C                   MOVEL     MHORPT        WFORPT
     C                   MOVEL     MHORPP        WFORPP
     C                   Z-ADD     MHORFS        WFORFS
      *
      * RETRIEVE THE CELL OF THE ORIGIN FARM
      *
     C     WFORFS        CHAIN     HSP018                             92
     C     *IN92         IFEQ      *OFF
     C                   z-add     fscell        wforcl
     C                   ELSE
     C                   z-add     0             wforcl
     C                   ENDIF
      *
      * DESTINATION
     C                   MOVEL     MHDNBO        WFDNBO
     C                   MOVEL     MHDNPT        WFDNPT
     C                   MOVEL     MHDNPP        WFDNPP
     C                   Z-ADD     MHDNFS        WFDNFS
      *
      * RETRIEVE THE NAME AND CELL OF THE DESTINATION FARM
      *
     C     WFDNFS        CHAIN     HSP018                             92
     C     *IN92         IFEQ      *OFF
     C                   z-add     fscell        wfdncl
     C                   movel     fsfscd        wkalphfscd
     C                   move      yes           process
     C                   z-add     1             start1
      *
     C                   dow       process = yes and start1 <= 5                Do farm loop
     C                   if        %subst(wkalphfscd: start1: 1) = '0'          If replace
     C                   eval      %subst(wkalphfscd: start1: 1) = *blank
     C                   else
     C                   move      no            process
     C                   endif                                                  If replace
     C                   add       1             start1
     C                   enddo                                                  Do farm loop
      *
     C                   eval      wfdnnm = %trim(wkalphfscd) + ' ' + fsfsnm
     C                   ELSE
     C                   MOVEL(P)  'UNKNOWN'     WFDNNM
     C                   z-add     0             wfdncl
     C                   ENDIF
      *
      * RETREIVE ALL TRANSFER MOVEMENT DETAIL RECORDS FOR THIS
      * MOVEMENT AND WRITE A WORKFILE RECORD FOR EACH DETAIL RECORD.
      *
     C     MEMVSN        SETLL     HSP075
      *
     C     *IN91         DOUEQ     *ON                                          DO TRAN DTL
     C     MEMVSN        READE     HSP075                                 91
     C     *IN91         IFEQ      *OFF                                         IF TRAN DTL
      *
     C                   Z-ADD     MDSCHD        WFSCHD
     C                   Z-ADD     MDSHHD        WFSHHD
     C                   Z-ADD     MDSHLB        WFSHLB
      *
     C                   Z-ADD     MDQLHD        WFQLHD
     C                   Z-ADD     MDRJHD        WFRJHD
      * DOA and KOA
     C                   Z-ADD     MDDOHD        WFDOHD
      * DOA only
     C                   Z-ADD     MDDOAHD       WFDOAHD
      * KOA only
     C                   Z-ADD     MDKOAHD       WFKOAHD
      *
     C                   Z-ADD     MDQLLB        WFQLLB
     C                   Z-ADD     MDRJLB        WFRJLB
     C                   Z-ADD     MDDOLB        WFDOLB
      *
      * RETRIEVE ORIGIN HOG GROUP INFO
      *
     C                   Z-ADD     MDORSN        WFORSN
     C     MDORSN        CHAIN     HSL034D                            92
     C     *IN92         IFEQ      *OFF
     C                   MOVEL     HGHGCD        WFORHG
     C                   MOVEL     HGBLCD        WFORBL
     C                   MOVEL     HGRMCD        WFORRM
     C                   ELSE
     C                   MOVE      *BLANK        WFORHG
     C                   MOVE      *BLANK        WFORBL
     C                   MOVE      *BLANK        WFORRM
     C                   ENDIF
      *
      * RETRIEVE DESTINATION HOG GROUP INFO
      *
     C                   Z-ADD     MDDNSN        WFDNSN
     C     MDDNSN        CHAIN     HSL034D                            92
     C     *IN92         IFEQ      *OFF
     C                   MOVEL     HGHGCD        WFDNHG
     C                   MOVEL     HGBLCD        WFDNBL
     C                   MOVEL     HGRMCD        WFDNRM
     C                   MOVEL     HGSXCD        WFDNSXCD
     C                   ELSE
     C                   MOVE      *BLANK        WFDNHG
     C                   MOVE      *BLANK        WFDNBL
     C                   MOVE      *BLANK        WFDNRM
     C                   MOVE      *BLANK        WFDNSXCD
     C                   ENDIF
      *
      *‚JM-WI628 06/11/2024-Get Count of new records added in work file
     C                   Eval      wkCntNewRecds += 1
     C                   WRITE     WFREC
      *
     C                   ENDIF                                                  IF TRAN DTL
     C                   ENDDO                                                  DO TRAN DTL
     C                   ENDIF                                                  IF NOT SCHED
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * CLEAR THE WORKFILE FIELDS
      *----------------------------------------------------------------
      *
     C     $CLEAR        BEGSR
      *
     C                   Z-ADD     0             WFMVSN
     C                   MOVE      *BLANK        WFMTCD
     C                   MOVE      *BLANK        WFCODE
     C                   MOVE      *BLANK        WFMSCD
     C                   Z-ADD     0             WFMEDT
     C                   Z-ADD     0             WFSCDT
     C                   Z-ADD     0             WFSHDT
     C                   Z-ADD     0             WFRCDT
     C                   MOVE      *BLANK        WFTICD
      * ORIGIN
     C                   MOVE      *BLANK        WFORBO
     C                   MOVE      *BLANK        WFORPT
     C                   MOVE      *BLANK        WFORPP
     C                   Z-ADD     0             WFORFS
     C                   Z-ADD     0             WFORCL
     C                   Z-ADD     0             WFORSN
     C                   MOVE      *BLANK        WFORHG
     C                   MOVE      *BLANK        WFORBL
     C                   MOVE      *BLANK        WFORRM
     C                   Z-ADD     0             WFSCHD
     C                   Z-ADD     0             WFSHHD
     C                   Z-ADD     0             WFSHLB
      *
      * DESTINATION
     C                   MOVE      *BLANK        WFDNBO
     C                   MOVE      *BLANK        WFDNPT
     C                   MOVE      *BLANK        WFDNPP
     C                   Z-ADD     0             WFDNFS
     C                   Z-ADD     0             WFDNCL
     C                   Z-ADD     0             WFDNSN
     C                   MOVE      *BLANK        WFDNHG
     C                   MOVE      *BLANK        WFDNBL
     C                   MOVE      *BLANK        WFDNRM
     C                   MOVE      *BLANK        WFDNSXCD
      *
     C                   Z-ADD     0             WFQLHD
     C                   Z-ADD     0             WFQLLB
     C                   Z-ADD     0             WFRJHD
     C                   Z-ADD     0             WFRJLB
     C                   Z-ADD     0             WFDOHD
     C                   Z-ADD     0             WFDOAHD
     C                   Z-ADD     0             WFKOAHD
     C                   Z-ADD     0             WFDOLB
     C                   MOVE      *BLANK        WFDNNM
      * SALES INFO
     C                   MOVE      *BLANK        WFSTCD
     C                   Z-ADD     0             WFCVNO
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * CALL THE EXTERNAL PROGRAM TO VALIDATE JDE ADDRESS NUMBERS
      *----------------------------------------------------------------
      *
     C     $JDEV         BEGSR
      *
     C                   MOVEL     NO            JDEFL                          JDE ERRORS
      *
     C                   CALL      'HPJDEV'
     C                   PARM                    MGI                            MSG ID'S
     C                   PARM                    MGD                            MSG DATA FLD
     C                   PARM                    ERROR                          # ERR MSGS
     C                   PARM                    INAJD                          JDE ADDR #
     C                   PARM                    INAT1                          JDE ADDR TYP
     C                   PARM                    JDEFL                          ERROR FLAG
     C                   PARM      *BLANK        OUALPH                         JDE NAME
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * INITIALIZATION SUBROUTINE
      *----------------------------------------------------------------
      *
     C     *INZSR        BEGSR
      *
     C     *LIKE         DEFINE    MEMVSN        SVMVSN
      *
     C                   move      *blank        wkalphfscd        5
     C                   move      *blank        process           1
     C                   z-add     0             start1            1 0
      *
      * J. D. EDWARDS INFO
     C                   MOVE      *BLANK        JDEFL             1            JDE ERR FLAG
     C                   MOVE      *BLANK        INAT1             3            JDE ADDR TYP
     C     *LIKE         DEFINE    FSFAJD        INAJD                          JDE ADDR IN
     C                   MOVE      *BLANK        OUALPH           40            JDE NAME
      *‚JM-WI628 06/11/2024 -Get Count of new records added in work file
     C     *ENTRY        PLIST
     C                   PARM                    p1CntNewRecds
      *
     C                   ENDSR
      /EJECT
