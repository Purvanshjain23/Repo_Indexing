     A*%%TS  SD  20060227  105813  ISLRAMS     REL-V5R3M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION
     A*  DISPLAY FILE:  HPD146
     A*  TITLE:         MAINTAIN SALE
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  DATE:          06/08/95
     A*
     A*----------------------------------------------------------------
     A*  INDICATORS:
     A*
     A*  60  REVISE MODE
     A*  61  CREATE MODE
     A*
     A*  63  PROTECT CONTROL RECORD FIELDS--SOME GROUP ON THIS MOVEMENT
     A*             IS DISPOSED OR CLOSED
     A*  68  PROTECT CUSTOMER NUMBER AND SALES TYPE CODE IN THE
     A*             CONTROL RECORD--THERE S A CHECK AGAINST SOME GROUP
     A*             IN THE SUBFILE.
     A*
     A*  64  PROTECT GROUP CODE/SCHEDULED HEAD/SHIPPED HEAD IN SUBFILE;
     A*             THE GROUP ON SUBFILE LINE IS DISPOSED OR CLOSED.
     A*  67  PROTECT GROUP CODE ON THIS SUBFILE RECORD--THERE IS A
     A*             CHECK AGAINST THIS GROUP.
     A*
     A*  90  THE SALE THE USER HAS SELECTED FROM THE WORK WITH HAS
     A*      BEEN DELETED BY ANOTHER USER SINCE SELECTED
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A N90                                  CF04(04 'PROMPT')
     A N90                                  CF05(05 'REFRESH')
     A N90                                  CF09(09 'ACCEPT')
     A N63N68N90
     A  60                                  CF11(11 'DELETE')
     A                                      CA12(12 'PREVIOUS')
     A N90                                  CF13(13 'RECEIVING')
     A*---------------------------------------------------------------
     A* SUBFILE FORMAT 1
     A*---------------------------------------------------------------
     A*
     A          R HP146S1                   SFL
     A*%%TS  SD  20020717  131017  ISLRAMS     REL-V5R1M0  5722-WDS
     A  85                                  SFLNXTCHG
     A            HDRRN1         4Y 0H
     A            HDHGSN    R        H      REFFLD(HGHGSN)
     A            HDGSCD    R        H      REFFLD(HGGSCD)
     A            HDSHLB    R        H      REFFLD(SGSHLB)
     A            DFBLCD    R        B 18  2REFFLD(HSRREF/@@BLCD *LIBL/HSPREF)
     A  49
     AO 59                                  DSPATR(PC)
     A  49                                  DSPATR(RI)
     A  64
     AO 67
     AO 90                                  DSPATR(PR)
     A N49N64N90
     A  42N67                               DSPATR(HI)
     A N49N64N90
     A  42N67                               DSPATR(UL)
     A            DFRMCD    R        B 18  9REFFLD(HSRREF/@@RMCD *LIBL/HSPREF)
     A  46
     AO 56                                  DSPATR(PC)
     A  46                                  DSPATR(RI)
     A  64
     AO 67
     AO 90                                  DSPATR(PR)
     A N46N64N90
     A  42N67                               DSPATR(HI)
     A N46N64N90
     A  42N67                               DSPATR(UL)
     A            DFHGCD    R        B 18 16REFFLD(HGHGCD)
     A  44
     AO 54                                  DSPATR(PC)
     A  44                                  DSPATR(RI)
     A  64
     AO 67
     AO 90                                  DSPATR(PR)
     A N44N64N90
     A  42N67                               DSPATR(HI)
     A N44N64N90
     A  42N67                               DSPATR(UL)
     A            DFGSCD    R        O 18 25REFFLD(HGGSCD)
     A            DFPTCD    R        O 18 29REFFLD(PTREC/PTPTCD *LIBL/HSP010)
     A            DFPPCD    R        O 18 36REFFLD(HSRREF/@@PPCD *LIBL/HSPREF)
     A            DFSCHD    R        B 18 43REFFLD(SGSCHD)
     A                                      EDTCDE(K)
     A  74                                  DSPATR(PC)
     A  74                                  DSPATR(RI)
     A  90
     AO 64                                  DSPATR(PR)
     A N74N64N90
     A  42                                  DSPATR(HI)
     A N74N64N90
     A  42                                  DSPATR(UL)
     A            DFSHHD    R     Y  B 18 51REFFLD(SGSHHD)
     A                                      EDTCDE(K)
     A  71                                  DSPATR(PC)
     A  71                                  DSPATR(RI)
     A  90
     AO 64                                  DSPATR(PR)
     A N71N64N90
     A  42                                  DSPATR(HI)
     A N71N64N90
     A  42                                  DSPATR(UL)
     A            DFLVHD    R     Y  O 18 63REFFLD(SGLVHD)
     A                                      EDTCDE(K)
     A            DFDOHD    R     Y  O 18 72REFFLD(SGDOHD)
     A                                      EDTCDE(K)
     A*
     A*----------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT  1
     A*----------------------------------------------------------------
     A*
     A          R HP146C1                   SFLCTL(HP146S1)
     A*%%TS  SD  20060227  105813  ISLRAMS     REL-V5R3M0  5722-WDS
     A                                      SFLSIZ(0100)
     A                                      SFLPAG(0004)
     A N90                                  ROLLUP(25 'PAGE FORWARD')
     A                                      SFLCSRRRN(&HDRR1)
     A                                      OVERLAY
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A                                      SETOF(30)
     A                                      SETOF(31)
     A                                      SETOF(32)
     A                                      SETOF(33)
     A                                      SETOF(35)
     A                                      SETOF(36)
     A                                      SETOF(37)
     A                                      SETOF(38)
     A                                      SETOF(39)
     A                                      SETOF(44)
     A                                      SETOF(46)
     A                                      SETOF(47)
     A                                      SETOF(49)
     A                                      SETOF(50)
     A                                      SETOF(51)
     A                                      SETOF(52)
     A                                      SETOF(53)
     A                                      SETOF(54)
     A                                      SETOF(56)
     A                                      SETOF(57)
     A                                      SETOF(58)
     A                                      SETOF(59)
     A                                      SETOF(71)
     A                                      SETOF(74)
     A                                      SETOF(75)
     A                                      SETOF(76)
     A                                      SETOF(78)
     A                                      SETOF(79)
     A                                      RTNCSRLOC(&RCD &FLD)
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            HDRR1          5S 0H
     A            HDTPCD    R        H      REFFLD(SHTPCD)
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A            DFMODE    R        O  1 14REFFLD(@@MODE)
     A                                      DSPATR(HI)
     A                                  1 28'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A            C1VERSION     18A  O  2  2DSPATR(HI)
     A                                  2 32'MAINTAIN SALE'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  4  2'Movement number .....:'
     A            C1MVSN    R        O  4 27REFFLD(@@MVSN)
     A                                      EDTCDE(Z)
     A                                  4 41'Movement status .......'
     A N90                              4 65'?'
     A            C1MSCD    R     A  B  4 67REFFLD(SHMSCD)
     A  78
     AO 58                                  DSPATR(PC)
     A  78                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A N78N90 42                            DSPATR(HI)
     A N78N90 42                            DSPATR(UL)
     A                                  5  2'Scheduled ship date ..'
     A            C1MDY     R     S  B  5 27REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  ')
     A  76                                  DSPATR(PC)
     A  76                                  DSPATR(RI)
     A  63
     AO 90                                  DSPATR(PR)
     A N63N76N90
     A  42                                  DSPATR(HI)
     A N63N76N90
     A  42                                  DSPATR(UL)
     A                                  5 41'Sale type .............'
     A N63N68N90                        5 65'?'
     A            C1STCD    R        B  5 67REFFLD(STSTCD)
     A  47
     AO 57                                  DSPATR(PC)
     A  47                                  DSPATR(RI)
     A  63
     AO 68
     AO 90                                  DSPATR(PR)
     A N47N63N68
     A  42N90                               DSPATR(HI)
     A N47N63N68
     A  42N90                               DSPATR(UL)
     A                                  6  2'Scheduled kill date ..'
     A            C1SCKLMDY R     S  B  6 27REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  ')
     A  75                                  DSPATR(PC)
     A  75                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A N75N90 42                            DSPATR(HI)
     A N75N90 42                            DSPATR(UL)
     A                                  6 41'Load type .............'
     A N90                              6 65'?'
     A            C1LTCD    R        B  6 67REFFLD(HSRREF/@@LTCD *LIBL/HSPREF)
     A  30
     AO 50                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A N30N90 42                            DSPATR(HI)
     A N30N90 42                            DSPATR(UL)
     A                                  7  2'Shipped date .........'
     A            C1SMDY    R     S  B  7 27REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  ')
     A  38                                  DSPATR(PC)
     A  38                                  DSPATR(RI)
     A  63
     AO 90                                  DSPATR(PR)
     A N38N63N90
     A  42                                  DSPATR(HI)
     A N38N63N90
     A  42                                  DSPATR(UL)
     A                                      TEXT('received date')
     A                                  7 41'Trucker ...............'
     A N90N63                           7 65'?'
     A            C1TICD    R        B  7 67REFFLD(SHTICD)
     A  33
     AO 53                                  DSPATR(PC)
     A  33                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A  42N33N90                            DSPATR(HI)
     A  42N33N90                            DSPATR(UL)
     A                                  8  2'Received date ........'
     A            C1RMDY    R     S  B  8 27REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  ')
     A  39                                  DSPATR(PC)
     A  39                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A N39N90 42                            DSPATR(HI)
     A N39N90 42                            DSPATR(UL)
     A                                  8 41'Customer number .......'
     A N90N68                           8 65'?'
     A            C1CVNO    R        B  8 67REFFLD(SHCVNO)
     A                                      EDTCDE(M)
     A  32
     AO 52                                  DSPATR(PC)
     A  32                                  DSPATR(RI)
     A  68
     AO 90                                  DSPATR(PR)
     A N32N68N90
     A  42                                  DSPATR(HI)
     A N32N68N90
     A  42                                  DSPATR(UL)
     A                                  9  2'Farm .................'
     A N90N63                           9 25'?'
     A            C1FSCD    R        B  9 27REFFLD(FSFSCD)
     A  31
     AO 51                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A  63
     AO 90                                  DSPATR(PR)
     A N63N31N90
     A  42                                  DSPATR(HI)
     A N63N31N90
     A  42                                  DSPATR(UL)
     A            C1CNAM    R        O  9 49REFFLD(@@DS30)
     A            C1FSNM    R        O 10  3REFFLD(FSFSNM)
     A                                 11 21'Scheduled'
     A                                      DSPATR(HI)
     A                                 11 33'******* Shipped *******'
     A                                      DSPATR(HI)
     A                                 11 59'** Arrived Head ***'
     A                                      DSPATR(HI)
     A                                 12 24'Head'
     A                                 12 35'Head'
     A                                 12 42'Pounds'
     A                                 12 53'Avg'
     A                                      DSPATR(HI)
     A                                 12 65'Live'
     A                                 12 75'DOA'
     A            C1SCHD    R        B 13 22REFFLD(SGSCHD)
     A                                      EDTCDE(K)
     A  79                                  DSPATR(PC)
     A  79                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A N79N90 42                            DSPATR(HI)
     A N79N90 42                            DSPATR(UL)
     A            C1SHHD    R        B 13 33REFFLD(SGSHHD)
     A                                      EDTCDE(K)
     A  36                                  DSPATR(PC)
     A  36                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A N36N90 42                            DSPATR(HI)
     A N36N90 42                            DSPATR(UL)
     A            C1SHLB    R        B 13 42REFFLD(SGSHLB)
     A                                      EDTCDE(K)
     A  37                                  DSPATR(PC)
     A  37                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A N37N90 42                            DSPATR(HI)
     A N37N90 42                            DSPATR(UL)
     A            C1AVSHLB  R        O 13 50REFFLD(HSRREF/@@NU50 *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A  35                                  DSPATR(RI)
     A            C1LVHD    R     Y  O 13 63REFFLD(SGLVHD)
     A                                      EDTCDE(K)
     A            C1DOHD    R     Y  O 13 72REFFLD(SGDOHD)
     A                                      EDTCDE(K)
     A                                 14  2'------ SCHEDULING AND SHIPPING INF-
     A                                      ORMATION ------------'
     A                                      DSPATR(HI)
     A                                 14 59'RECEIVING INFORMATION'
     A                                      DSPATR(HI)
     A N90                             15  4'?'
     A                                 15 11'?'
     A                                 15 18'?'
     A                                 15 29'-Production-'
     A                                 15 44'Sched'
     A                                 15 50'Shipped'
     A                                 15 65'Live'
     A                                 15 75'DOA'
     A                                 16  2'Bldg'
     A                                 16  9'Room'
     A                                 16 16'Group'
     A                                 16 25'St'
     A                                 16 29'Type'
     A                                 16 36'Phase'
     A                                 16 45'Head'
     A                                 16 53'Head'
     A                                 16 65'Head'
     A                                 16 74'Head'
     A*----------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*----------------------------------------------------------------
     A          R HP146K1
     A*%%TS  SD  19991103  133734  ISLRAMS     REL-V4R2M0  5769-PW1
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A N90                             23 10'F4=Prompt'
     A N90                             23 20'F5=Refresh'
     A N90                             23 31'F9=Accept'
     A N61N63N68
     A N90 60                          23 41'F11=Delete'
     A                                 23 52'F12=Previous'
     A N90                             23 65'F13=View Rcv Dtl'
      *----------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *----------------------------------------------------------------
     A          R HP146ES                   SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
     A*----------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*----------------------------------------------------------------
     A*
     A          R HP146EC                   SFLCTL(HP146ES)
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
