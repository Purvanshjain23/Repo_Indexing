      *****************  RPG PROGRAM HEADING  ************************
      *
      * ENVIRONMENT: Pork Division
      * SYSTEM:      HPS---Datamart
      * PROGRAM:     HP222 Update Groups-Temporary Work-Around COGNOS
*     *                    (Nursery/Grow Finish Only)
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     03/08/01
      *
      *  FUNCTION:   This program is intended to be temporary as a work-around for something
      *              we cannot figure out how to do in COGNOS.  We should get rid of this
      *              program once we figure out how to make COGNOS work.
      *
      *              We intentionally distort the group data here to make COGNOS work.
      *
      *              We update the first group record we find with:
      *                 1) 7-week average dead for a farm/building/room
      *                 2) 3-week average dead for a farm/building/room
      *
      ****************************************************************************************
      * Modifications:
      ****************************************************************************************
      * Date      Programmer
      *
      * 06/11/01  LeAnne Fedor
      *           Recompile only. New field 'accounting quarter' added to Datamart
      *           Weekly file.
      *
      * 10/11/01  LeAnne Fedor
      *           Recompile only. New field 'cull sale amount' added to Datamart
      *           Weekly file.
      *
      * 02/28/02  LeAnne Fedor
      *           Increased length of data area DAFIN.
      *
      * 08/01/02  LeAnne Fedor
      *           Increased DAFIN data area by 1. 'Days' is now 3 long instead of 2.
      *
      * 10/11/04  LeAnne Fedor
      *           Added a 'submitted flag' position to the data area DAFIN.
      *
      * 10/27/04  LeAnne Fedor
      *           New field "3-week average dead" added to file:
      *               Datamart FIN: Weekly Group Detail.
      *
      * 08/25/05  LeAnne Fedor
      *           New field "Man Hours" added to file:
      *               Datamart FIN: Weekly Group Detail.
      *
      * 09/19/07  LeAnne Ramsey
      *           Recompile only. New "target" fields added to the Datamart FIN: Weekly
      *           Group Detail file.
      *
      * 05/24/10  LeAnne Ramsey
      *           Recompile only. Added 2 fields to HPPF075-Weekly Group Detail:
      *              WBWEDTMDCY-Week Ending Date in date format mm/dd/ccyy
      *              WBPAJTIHD -Placement Adjusted Transfer In Head
      /EJECT
      ****************************************************************************************
      * File Specifications
      ****************************************************************************************
      *
      *
     Fhplf175a  if   e           k disk
      *    DataMart FIN: Weekly Farm/Building/Room
      *
      *
     Fhplf075a  uf   e           k disk
      *    DataMart FIN: Weekly Group Detail
      *
      *
      /EJECT
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      *---------------------------------------------------------------
      *  Named Constants
      *---------------------------------------------------------------
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      *
      ****************************************************************
      * Arrays and Tables
      ****************************************************************
      *
      ****************************************************************
      * Standalone fields
      ****************************************************************
      *
      * Parm fields
      *
     D xxprocfl        s              1a
      *
      *
      * Workfields for dates
      *
     D wkstartdt       s               D   datfmt(*iso)
      *
      *
      ****************************************************************
      * Data Structures
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *---------------------------------------------------------------
      * Standard database file information data structure
      *---------------------------------------------------------------
      *    externally defined as UTDBGR (record format: FDBCKD)
     D dbfeed        e ds                  extname(utdbfr)
      *
      *---------------------------------------------------------------
      * Definition for external data area
      *---------------------------------------------------------------
     D
     Ddafin            ds                  dtaara(dafin)
     D  dahwkenddt            15     22  0
     D  dacwkenddt            38     45  0
     D  dadays                47     49  0
     D  dasbmfl               62     62
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * MAINLINE
      ****************************************************************
      *
      * Read the Datamart Weekly Farm/Building/Room file. Update
      * one group record for each Room record.
      *
     C     wkstartdt     setll     hplf175a
      *
     C                   dou       *in90 = *on                                  Main do loop
     C                   read      hplf175a                               90
     C                   if        *in90 = *off                                 If not EOF
      *
     C     key01         chain     hplf075a                           92
     C                   if        *in92 = *off
     C                   z-add     wfavddhd      wbavddhd
     C                   z-add     wfa3ddhd      wba3ddhd
     C                   update    wbrec
     C                   endif
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Main do loop
      *
      *----------------------------------------------------------------
      * EOF processing
      *----------------------------------------------------------------
      *
     C                   seton                                        lr
      /EJECT
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Parm lists
      *
     C     *entry        plist
     C                   parm                    xxprocfl
      *
      *
      * Key lists
      *
     C     key01         klist
     C                   kfld                    wfwedt
     C                   kfld                    wffscd
     C                   kfld                    wfblcd
     C                   kfld                    wfrmcd
      *
      *
      * Bring in the data area that contains the week-ending date for the first week.
      * Move the retrieved date to a date format field.
      *
     C                   in        dafin
      *
     C                   select
     C                   when      xxprocfl = 'H'
     C                   movel     dahwkenddt    wkstartdt
      *
     C                   when      xxprocfl = ' '
     C                   movel     dacwkenddt    wkstartdt
     C                   endsl
      *
     C                   endsr
      /EJECT
