      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Hog Production System
      * PROGRAM:     HP5050
      * TITLE:       Select Scheduled Sales Movement
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     10/12/04
      *
      *
      * FUNCTION: This is a select function window display of the records in the
      *           Sales Movement header file. Only Scheduled sales are included.
      *
      *           A record is selected by positioning the cursor on the line
      *           and pressing enter.
      *
      *           Restrictors:
      *              The value for incoming parm 'Reprint', controls which movements are
      *              loaded into the subfile.
      *
      *           Positioners:
      *              scheduled ship date
      *
      *           Selectors:
      *              sales type
      *              transportation office
      *              trucker
      *              customer number
      *              cell
      *              farm site
      *
      *
      * NOTE:    This is a pop-up window program.  Please note that in window
      *          programs the record format containing the window definition
      *          must be written first.  So, we write the error message subfile
      *          control record first followed by the command line record format.
      *          This is opposite of the non-window programs.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 10/16/13  LeAnne Ramsey (E2831)
      *           Recompile only. Added field 'MTech Reference'.
      *
      * 10/21/13  LeAnne Ramsey (E2831)
      *           Added MTech Reference as a parm on the calls to:
      *               HP538-Select Trucker
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fhpd5050   cf   e             workstn
     F                                     sfile(hp5050s1:hdrrn1)
     F                                     infds(iofeed)
      *
      *
     Fhsp009    if   e           k disk
      *    Business office
      *
      *
     Fhsp018    if   e           k disk
      * Farm site
      *
      *
     Fhsl084e   if   e           k disk    infds(dbfeed)
      * Sales movement header
      *
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
     D set1            c                   'SET1  '
     D edit1           c                   'EDIT1 '
     D scrn1           c                   'SCRN1 '
     D exit            c                   'EXIT  '
      *
      *
      ****************************************************************
      * ARRAYS AND TABLES
      ****************************************************************
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Standard fields
      *
     D pgm             s             10
     D msgfl           s             10
     D maxmsg          s              2  0  inz(20)
     D routne          s              6
     D maxrrn          s              4  0
     D norecs          s              1    inz('N')
     D reset           s              1    inz('N')
     D count           s              2  0
      *
      *
      * Workfields required for subfile paging--see initialization
      * subroutine.
      *
     D wkplin          s              2  0
     D wkrlin          s              2  0
     D wkflow          s                   like(idflow)
      *
      *
      * Control fields
      *
     D procfl          s              1
      *
     D svscmdy         s                   like(c1scmdy)
     D svstcd          s                   like(c1stcd)
     D svticd          s                   like(c1ticd)
     D svcvno          s                   like(c1cvno)
     D svtocd          s                   like(c1tocd)
     D svcell          s                   like(c1cell)
     D svfscd          s                   like(c1fscd)
      *
      *
      *
      * Workfields
      *
     D wkscdt          s                   like(shscdt)
     D wkisodate       s               d   datfmt(*iso)
      *
      *
      * Parm fields
      *
     D xxmvsn          s                   like(d1mvsn)
     D xxreprint       s              1
      *
     D xxtocd          s                   like(fstocd)
     D xxtonm          s             25
     D xxaist          s              1
      *
     D xxfscd          s                   like(d1fscd)
     D xxfsnm          s             25
      *
     D xxstcd          s                   like(c1stcd)
     D xxstds          s             20
      *
     D xxticd          s                   like(shstcd)
     D xxtinm          s             25
     D xxmtechref      s                   like(fsmtechref)
      *
     D oualph          s             40
     D ouajd           s                   like(shcvno)
     D inat1           s              3
     D inajd           s                   like(shcvno)
     D jdefl           s              1
     D incocd          s              3
      *
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * standard error message handling data structures
      *---------------------------------------------------------------
      *
      * For error message handling, a packed index (3) is required.
      * For program readability, define a corresponding error count
      * field called 'error'
      *
     D                 ds                  inz
     D  error                         2p 0
     D   e                            2p 0 overlay(error)
      *
      * This data structure supplies the name of the message file to
      * the message handling CL program.  The field name MSGFIL must be
      * constant. The value in quotes is the name of the specific
      * message file containing the user defined messages.
      *
     D                 ds                  inz
     D  msgfil                       10    inz('HSMSGF    ')
      *
      *---------------------------------------------------------------
      *  Standard message data structures
      *---------------------------------------------------------------
      *
      * The following 3 data structures are used to speed message
      * handling since it is faster to clear data structures than
      * arrays.  Each is associated with a standard message array.
      *
     D mgi             ds                  inz
     D  mgid                          7    dim(20)
      *
     D mgd             ds                  inz
     D  mgdt                         50    dim(20)
      *
     D mgwk            ds                  inz
     D  mgw                           1    dim(50)
      *
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *---------------------------------------------------------------
      * Standard workstation information data structure
      *---------------------------------------------------------------
      *    externally defined as UTWSFR (record format: UTIDFRR)
     D iofeed        e ds                  extname(utwsfr)
      *
      *
      *---------------------------------------------------------------
      * Standard database file information data structure
      *---------------------------------------------------------------
      *    externally defined as UTDBGR (record format: FDBCKD)
     D dbfeed        e ds                  extname(utdbfr)
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * MAINLINE
      ****************************************************************
      *
     C                   eval      routne = set1
      *
     C     *inlr         doweq     *off                                         Main do loop
     C     routne        caseq     set1          $set1
     C     routne        caseq     scrn1         $scrn1
     C     routne        caseq     exit          $exit
     C                   endcs
     C                   enddo                                                  Main do loop
      /EJECT
      *---------------------------------------------------------------
      * Set environment for screen 1
      *---------------------------------------------------------------
      *
     C     $set1         begsr
      *
      * Clear/fill the subfile
      *
     C                   exsr      $clr1
     C                   exsr      $frsh1
      *
      * Set routine to display screen
      *
     C                   eval      routne = scrn1
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Perform operator I/O for screen 1
      *---------------------------------------------------------------
      *
     C     $scrn1        begsr
      *
      * Write the error messages from the error arrays to the
      * error message subfile
      *
     C                   exsr      $wrmsg
      *
      * Write screen 1 to crt
      *
     C                   write     hp5050ec
     C                   write     hp5050k1
     C                   exfmt     hp5050c1
      *
      * Clear the messages from the message data structures and
      * program message queue
      *
     C                   exsr      $clmsg
      *
      * Get user's entry and set next routine
      *
     C                   select
     C                   when      *in12 = *on                                  F12=previous
     C                   eval      routne = exit
      *
     C                   when      *in04 = *on                                  F4-prompt
     C                   exsr      $f4s1
     C                   exsr      $chksv
      *
      * Reposition the subfile if the user has changed any of the
      * positioners or selectors in the control record.
      *
     C                   when      c1scmdy  <> svscmdy  or                      Positioners
     C                             c1stcd <> svstcd or
     C                             c1tocd <> svtocd or
     C                             c1ticd <> svticd or
     C                             c1cvno <> svcvno or
     C                             c1cell <> svcell or
     C                             c1fscd <> svfscd
     C                   exsr      $chksv
      *
     C                   when      *in25 = *on                                  F25-rollup
     C                   exsr      $load1
     C                   eval      routne = scrn1
     C                   other
      *
      * User has pressed enter
      *
     C                   if        norecs = yes                                 If empty sfl
     C                   eval      routne = set1
     C                   else
     C                   exsr      $proc1
     C                   endif                                                  If empty sfl
     C                   endsl
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * F4 prompt on screen 1
      *---------------------------------------------------------------
      *
     C     $f4s1         begsr
      *
      * Sales type
     C                   select
     C                   when      fld = 'C1STCD'
     C                   call      'HP544'
     C                   parm      *blank        xxstcd
     C                   parm                    xxstds
     C                   seton                                        50
     C                   if        xxstcd <> *blank
     C                   move      xxstcd        c1stcd
     C                   endif
      *
      *
      * Transportation Office
      *
     C                   when      fld = 'C1TOCD'
     C                   call      'HP5026'
     C                   parm      *blank        xxaist
     C                   parm      *blank        xxtocd
     C                   parm      *blank        xxtonm
     C                   seton                                        51
     C                   if        xxtocd <> *blank
     C                   movel     xxtocd        c1tocd
     C                   endif
      *
      * Trucker
     C                   when      fld = 'C1TICD'
     C                   call      'HP538'
     C                   parm      *blank        xxaist
     C                   parm      *blank        xxticd
     C                   parm      *blank        xxtinm
     C                   parm      *blank        xxmtechref
     C                   seton                                        53
     C                   if        xxticd <> *blank
     C                   move      xxticd        c1ticd
     C                   endif
      *
      *
      * JDE customer number
      * (requires a Company--get one from the Business Office file)
      *
     C                   when      fld = 'C1CVNO'
      *
     C     *loval        setll     hsp009
     C                   read      hsp009                                 92
     C                   if        *in92 = *off
     C                   eval      inat1 = 'C'
     C                   call      'HP513'
     C                   parm                    incocd                         COMPANY
     C                   parm                    inat1                          SEARCH TYPE
     C                   parm      *zero         ouajd                          NUMBER
     C                   parm      *blank        oualph                         NAME
     C                   endif
      *
     C                   seton                                        52
     C                   if        ouajd <> 0
     C                   z-add     ouajd         c1cvno
     C                   endif
      *
      * Farm Site
     C                   when      fld = 'C1FSCD'
     C                   call      'HP5040'
     C                   parm      *blank        xxaist
     C                   parm      0             xxfscd
     C                   parm      *blank        xxfsnm
     C                   seton                                        54
     C                   if        xxfscd <> 0
     C                   z-add     xxfscd        c1fscd
     C                   endif
     C*                  other
      *
      * F4 not valid for this field...issue message
      *
     C                   if        error < maxmsg
     C                   add       1             error                                        013
     C                   movel     'HS09011'     mgid(e)
     C                   endif
     C                   endsl
      *
     C                   endsr
      /EJECT
      *----------------------------------------------------------------
      * Process user's selection
      *----------------------------------------------------------------
      *
     C     $proc1        begsr
      *
      * If cursor location returned is the rrn of a record in the
      * subfile, chain to the subfile record and load the data into
      * the parms to return to the calling program.
      *
     C                   if        hdrrn <> 0
     C                   eval      hdrrn1 = hdrrn
     C     hdrrn1        chain     hp5050s1                           92
     C                   z-add     d1mvsn        xxmvsn
     C                   endif
      *
      * If the user selected a record, set the routine to exit.  If no record
      * was selected, redisplay the screen.
      *
     C                   if        xxmvsn = 0
     c                   eval      routne = set1
     C                   else
     c                   eval      routne = exit
     C                   endif
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Clear subfile for screen 1
      *---------------------------------------------------------------
      *
     C     $clr1         begsr
      *
     C                   clear                   hp5050s1
     C                   seton                                        83        sflclr
     C                   write     hp5050c1
     C                   setoff                                       818283
      *                                                    81=sfldsp
      *                                                    82=sflend
      *                                                    83=sflclr
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Refresh/fill the subfile for screen 1
      *---------------------------------------------------------------
      *
     C     $frsh1        begsr
      *
      * Set position in file
      *
     C     wkscdt        setll     hsl084e
      *
      * The lowest subfile RRN on the screen from the DSPF feedback area (IDFLOW)
      * was retrieved earlier and put in WKFLOW. Add to WKFLOW the number of lines
      * on your subfile page or the number less 1 to determine a subfile RRN to
      * reposition to. RESET is a flag field to indicate whether you are attempting
      * to reposition the user close to where he was in the subfile. If RESET is
      * 'NO' don't reposition--just start over.
      *
     C                   if        reset = yes
     C     wkflow        add       wkrlin        maxrrn
     C                   else
     C                   z-add     wkplin        maxrrn
     C                   endif
      *
      * Load the subfile until you have the required number of records.
      * The number will depend on whether uou are repositioning the
      * subfile or not. Stop if you get to the end of the subfile.
      *
     C                   dou       hdrrn1 >= maxrrn or *in82 = *on
     C                   exsr      $load1
     C                   enddo
      *
     C                   endsr
      /EJECT
      *----------------------------------------------------------------
      * Save the control record positioners/selectors
      *----------------------------------------------------------------
      *                                                                        z
     C     $chksv        begsr
      *
     C                   eval      routne = set1
     C                   eval      reset = no
      *
     C                   move      c1stcd        svstcd
     C                   move      c1tocd        svtocd
     C                   move      c1ticd        svticd
     C                   z-add     c1cvno        svcvno
     C                   z-add     c1fscd        svfscd
     C                   z-add     c1cell        svcell
     C                   z-add     c1scmdy       svscmdy
      *
      *
      * Flip date selector from mmddyy to ccyymmdd format
      *
     C     *mdy          test(d)                 c1scmdy                92
     C                   if        *in92 = *on                                  If bad date
     C                   z-add     0             wkscdt
     C                   else
     C     *mdy          move      c1scmdy       wkisodate
     C                   move      wkisodate     wkscdt
     C                   endif                                                  If bad date
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Load subfile for screen 1
      *---------------------------------------------------------------
      *
     C     $load1        begsr
      *
      * Set the count of records on the subfile page to zero.
      *
     C                   z-add     0             count
      *
      * Fill the subfile until the end of the database file or
      * you have filled all the lines on the subfile page.
      *
     C                   setoff                                       82
     C                   dou       *in82 = *on or count >= wkplin               Do fill
      *
     C                   read      hsl084e                                82
     C                   if        *in82 = *off                                 If read
      *
     C                   move      yes           procfl
      *
      * Are you supposed to show movements that have already been printed or
      * not?
     C                   select
     C                   when      xxreprint = yes and shptct = 0
     C                   move      no            procfl
     C                   when      xxreprint = no and shptct <> 0
     C                   move      no            procfl
     C                   endsl
      *
      * Retrieve farm data
      *
     C     shfscd        chain     hsp018                             92
     C                   if        *in92 = *off
     C                   z-add     fscell        d1cell
     C                   move      fstocd        d1tocd
     C                   else
     C                   z-add     0             d1cell
     C                   move      *blank        d1tocd
     C                   endif
      *
      * Only include records that match the user's selectors
      *
     C                   if        c1stcd <> *blank and
     C                             c1stcd <> shstcd
     C                   move      no            procfl
     C                   endif
      *
     C                   if        c1tocd <> *blank and
     C                             c1tocd <> d1tocd
     C                   move      no            procfl
     C                   endif
      *
     C                   if        c1ticd <> *blank and
     C                             c1ticd <> shticd
     C                   move      no            procfl
     C                   endif
      *
     C                   if        c1cvno <> 0 and
     C                             c1cvno <> shcvno
     C                   move      no            procfl
     C                   endif
      *
     C                   if        c1cell <> 0 and
     C                             c1cell <> d1cell
     C                   move      no            procfl
     C                   endif
      *
     C                   if        c1fscd <> 0 and
     C                             c1fscd <> shfscd
     C                   move      no            procfl
     C                   endif
      *
      *
     C                   if        procfl = yes                                 If process
      *
     C                   add       1             count
     C                   add       1             hdrrn1
     C                   z-add     shmvsn        d1mvsn
     C                   z-add     shfscd        d1fscd
     C                   move      shstcd        d1stcd
     C                   move      shticd        d1ticd
     C                   z-add     shcvno        d1cvno
     C                   z-add     shptct        d1ptct
      *
      * Flip Scheduled Date from CCYYMMDD to MMDDYY
      *
     C     *iso          test(d)                 shscdt                 92
     C                   if        *in92 = *off                                 If OK date
     C                   move      shscdt        wkisodate
     C     *mdy          move      wkisodate     d1scmdy
     C                   else
     C                   z-add     0             d1scmdy
     C                   endif                                                  If OK date
      *
     C                   write     hp5050s1
     C                   seton                                        81
      *
      * For positioning, place the subfile relative record number into the
      * sflrcdnbr field (HDPAG1)
      *
     C                   z-add     hdrrn1        hdpag1
     C                   endif                                                  If process
     C                   endif                                                  If read
     C                   enddo                                                  Do fill
      *
      * If no records were written to the subfile, then issue
      * the standard message 'No data to Display'.
      *
     C                   if        hdrrn1 = 0                                   If no recs
     C                   eval      norecs = yes
      *
     C                   if        error < maxmsg
     C                   add       1             error
     C                   eval      mgid(e) = 'HS09002'
     C                   endif
     C                   else
      *
     C                   eval      norecs = no
     C                   endif                                                  If no recs
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * End job
      *---------------------------------------------------------------
      *
     C     $exit         begsr
     C                   seton                                        LR
     C                   endsr
      *
      *---------------------------------------------------------------
      * Write error messages
      *---------------------------------------------------------------
      *
     C     $wrmsg        begsr
      *
     C                   call      'UT80045J'                           43
     C                   parm                    mgid
     C                   parm                    mgdt
     C                   parm                    error
     C                   parm      sdpgm         pgm
     C                   parm      msgfil        msgfl
      *
      * If call to UT80045J failed, a message hardcoded in the DSPF
      * command line format will be set on. So, redisplay screen.
      *
     C                   if        *in43 = *on
     C                   eval      routne = set1
     C                   endif
      *
     C                   endsr
      *
      *----------------------------------------------------------------
      * Clear messages
      *----------------------------------------------------------------
      *
     C     $clmsg        begsr
      *
     C                   call      'UT80045J'                           43
     C                   parm      *blank        mgi
     C                   parm      *blank        mgd
     C                   parm      *zero         error
     C                   parm      sdpgm         pgm
     C                   parm      msgfil        msgfl
      *
      * If call to UT80045J failed, a message hardcoded in the DSPF
      * command line format will be set on; so, redisplay screen.
      *
     C                   if        *in43 = *on
     C                   eval      routne = set1
     C                   endif
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Key lists
      *     None
      *
      * Parm lists
      *
     C     *entry        plist
     C                   parm                    xxreprint
     C                   parm                    xxmvsn
      *
      *
      * To support subfile paging, logic requires the number of lines on
      * the subfile page.
      *
     C                   z-add     9             wkplin                         sflpag
     C     wkplin        sub       1             wkrlin
      *
      *
      * The following standard code must be included to make the
      * standard error message handling program function properly.
      * This code sets and clears the program message queue.
      *
     C                   movel     '*'           msgpgm
     C                   exsr      $clmsg
      *
      * Execute required window format
      *
     C                   write     assume
      *
     C                   endsr
      /EJECT
