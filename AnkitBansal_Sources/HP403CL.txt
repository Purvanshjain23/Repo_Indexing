/***************************************************************** */
/*  SYSTEM:            HOG PRODUCTION SYSTEM                       */
/*  PROGRAM NUMBER:    HP403CL                                     */
/*  PROGRAM NAME:      DEATHS AND CULL SALES BY WEEK REPORT        */
/*                     (CLOSED GROUPS ONLY)                        */
/*  PROGRAMMER:        LEANNE FEDOR                                */
/*  DATE:              03/04/96                                    */
/*                                                                 */
/*  PROGRAM PURPOSE -> THIS CL CALLS THE PROGRAM TO PRINT THE      */
/*                     DEATHS AND CULL SALES BY WEEK REPORT.       */
/*                     VALUES ARE PASSED THRU THE LDA FROM         */
/*                     SPECIFY HP403.                              */
/*                                                                 */
/***************************************************************** */
/* MODIFICATIONS                                                   */
/* 03/07/96  LEANNE FEDOR                                          */
/*           AS REQUESTED BY JUDY SCHAUB, ADDED CUSTOMIZED PRINTER */
/*           OVERRIDE COMMAND TO FORCE REPORT TO PRINT ON LASER    */
/*           PRINTERS ON LEGAL PAPER.                              */
/*                                                                 */
/* 05/21/96  LEANNE FEDOR                                          */
/*           IN PREPARATION FOR HAVING TWO BOXES (CISC AND RISC),  */
/*           REPLACED STANDARD CREATE DUPLICATE OBJECT COMMAND     */
/*           WITH A COMMAND CUSTOMIZED BY JOHN ERICSON. THIS       */
/*           COMMAND WILL RETRIEVE/USE THE FILE FROM THE APPRO-    */
/*           PRIATE LIBRARY INSTEAD OF USING HARDCODED 'PRKFLIB'.  */
/*           THE CUSTOMIZED COMMAND IS CRTDUPOBJL FOR 'CREATE      */
/*           DUPLICATE OBJECT FROM LIBRARY'.                       */
/*                                                                 */
/* 06/03/97 LEANNE FEDOR                                           */
/*          RECOMPILE ONLY.  NEW FIELDS 'PURGE FLAG' AND 'DATE OF  */
/*          FIRST PURGE' ADDED TO HOG GROUP FILE.                  */
/***************************************************************** */

             PGM

/* DEFINE THE VARIABLE REQUIRED IN THE COMMAND TO OVERRIDE THE     */
/* PRINT FILE TO A LASER PRINTER                                   */

             DCL        VAR(&CMD) TYPE(*CHAR) LEN(512)


             DCL        VAR(&QDATA)  TYPE(*CHAR) LEN(240)
             DCL        VAR(&LDFSBO) TYPE(*CHAR) LEN(5)
             DCL        VAR(&LDFYMD) TYPE(*DEC)  LEN(8)
             DCL        VAR(&LDTYMD)   TYPE(*DEC)  LEN(8)
             DCL        VAR(&LDPPCD) TYPE(*CHAR) LEN(5)
             DCL        VAR(&LDPTCD) TYPE(*CHAR) LEN(5)
             DCL        VAR(&LDFSCD) TYPE(*DEC) LEN(5)
             DCL        VAR(&LDHGSN) TYPE(*DEC) LEN(7)
             DCL        VAR(&LDGTCD) TYPE(*CHAR) LEN(1)

             DCL        VAR(&HOLD) TYPE(*CHAR) LEN(10)
             DCL        VAR(&COPYS) TYPE(*DEC) LEN(2)
             DCL        VAR(&OUTQ) TYPE(*CHAR) LEN(10)

             CHGVAR     VAR(&LDFSBO) VALUE(%SST(*LDA 1 5))
             CHGVAR     VAR(&LDFYMD) VALUE(%SST(*LDA 6 8))
             CHGVAR     VAR(&LDTYMD) VALUE(%SST(*LDA 14 8))
             CHGVAR     VAR(&LDPPCD) VALUE(%SST(*LDA 22 5))
             CHGVAR     VAR(&LDPTCD) VALUE(%SST(*LDA 48 5))
             CHGVAR     VAR(&LDFSCD) VALUE(%SST(*LDA 27 5))
             CHGVAR     VAR(&LDHGSN) VALUE(%SST(*LDA 53 7))
             CHGVAR     VAR(&LDGTCD) VALUE(%SST(*LDA 47 1))

/*********************************************************************/
/* SETUP QUERY FOR HOG GROUPS                                        */
/*********************************************************************/

/* ALWAYS SELECT ONLY CLOSED HOG GROUPS                              */

             CHGVAR     VAR(%SST(&QDATA 1 12)) VALUE('HGGSCD *EQ "')
             CHGVAR     VAR(%SST(&QDATA 13 2)) VALUE('CL')
             CHGVAR     VAR(%SST(&QDATA 15 1)) VALUE('"')

/* IF THE USER DID NOT SELECT ONE SPECIFIC GROUP,                    */
/* THEN SELECT ONLY GROUPS WITH A CLOSE DATE IN THE SELECTED RANGE   */

             IF         COND(&LDHGSN *EQ 0000000) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 17 15)) VALUE('*AND HGCLDT +
                          *GE')
             CHGVAR     VAR(%SST(&QDATA 33 8)) VALUE(&LDFYMD)
             CHGVAR     VAR(%SST(&QDATA 42 15)) VALUE('*AND HGCLDT +
                          *LE')
             CHGVAR     VAR(%SST(&QDATA 58 8)) VALUE(&LDTYMD)
             ENDDO

/*  IF BUSINESS OFFICE                                               */

             IF         COND(&LDFSBO *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 67 17)) VALUE('*AND HGFSBO +
                          *EQ "')
             CHGVAR     VAR(%SST(&QDATA 84 5)) VALUE(&LDFSBO)
             CHGVAR     VAR(%SST(&QDATA 89 1)) VALUE('"')
             ENDDO

/*  IF PRODUCTION TYPE                                               */

             IF         COND(&LDPTCD *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 91 17)) VALUE('*AND HGPTCD +
                          *EQ "')
             CHGVAR     VAR(%SST(&QDATA 108 5)) VALUE(&LDPTCD)
             CHGVAR     VAR(%SST(&QDATA 113 1)) VALUE('"')
             ENDDO

/*  IF PRODUCTION PHASE                                              */

             IF         COND(&LDPPCD *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 115 17)) VALUE('*AND HGPPCD +
                          *EQ "')
             CHGVAR     VAR(%SST(&QDATA 132 5)) VALUE(&LDPPCD)
             CHGVAR     VAR(%SST(&QDATA 137 1)) VALUE('"')
             ENDDO

/*  IF FARM SITE                                                     */

             IF         COND(&LDFSCD *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 139 4)) VALUE('*AND')
             CHGVAR     VAR(%SST(&QDATA 144 10)) VALUE('HGFSCD *EQ')
             CHGVAR     VAR(%SST(&QDATA 155 5)) VALUE(&LDFSCD)
             ENDDO

/*  IF HOG GROUP                                                    */

             IF         COND(&LDHGSN *NE 0000000) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 161 15)) VALUE('*AND HGHGSN +
                          *EQ')
             CHGVAR     VAR(%SST(&QDATA 177 7)) VALUE(&LDHGSN)
             ENDDO

/*  IF GROUP TYPE                                                */

             IF         COND(&LDGTCD *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 185 17)) VALUE('*AND HGGTCD +
                          *EQ "')
             CHGVAR     VAR(%SST(&QDATA 202 1)) VALUE(&LDGTCD)
             CHGVAR     VAR(%SST(&QDATA 203 1)) VALUE('"')
             ENDDO


/* RUN QUERY OVER THE HOG GROUP FILE                                 */

             OVRDBF     FILE(HSP034) SHARE(*YES)
             OPNQRYF    FILE((HSP034)) QRYSLT(&QDATA) KEYFLD(*FILE) +
                          SEQONLY(*YES)

/*------------------------------------------------------------------*/
/*  ALWAYS CREATE FOUR PHYSICAL WORKFILES AND REQUIRED LOGICALS     */
/*------------------------------------------------------------------*/

             CRTDUPOBJL OBJ(HSP303) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)

             CRTDUPOBJL OBJ(HSP304) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)
             CRTDUPOBJL OBJ(HSL304A) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)
             CRTDUPOBJL OBJ(HSL304B) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)

             CRTDUPOBJL OBJ(HSP305) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)
             CRTDUPOBJL OBJ(HSL305A) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)

             CRTDUPOBJL OBJ(HSP306) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)

/*------------------------------------------------------------------*/
/*  CALL THE PROGRAM TO CREATE THE WORKFILE DATA                    */
/*------------------------------------------------------------------*/

             CALL       PGM(HP303)

/*------------------------------------------------------------------*/
/* SETUP THE REPORT PRINT PARAMETERS                                */
/*------------------------------------------------------------------*/

 HOLD:       IF         COND(%SST(*LDA 411 1) = 'Y') THEN(CHGVAR +
                          VAR(&HOLD) VALUE(*YES))
             ELSE       CMD(CHGVAR VAR(&HOLD) VALUE(*NO))

 COPYS:      CHGVAR     VAR(&COPYS) VALUE(%SST(*LDA 412 1))
             IF         COND(&COPYS = 0) THEN(CHGVAR VAR(&COPYS) +
                          VALUE(1))

 OUTQ:       CHGVAR     VAR(&OUTQ) VALUE(%SST(*LDA 413 10))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(*JOB))


/*------------------------------------------------------------------*/
/* OVERRIDE PRINT FILE                                              */
/*------------------------------------------------------------------*/

/* USE THE CUSTOMIZED SEABOARD COMMAND TO OVERRIDE THE PRINT FILE.  */
/* THIS COMMAND ALLOWS THE REPORT TO BE GENERATED ON A STANDARD LINE*/
/* PRINTER OR A LASER PRINTER. TO USE THIS OVERRIDE YOU MUST DECLARE*/
/* A COMMAND VARIABLE AND FOLLOW THE OVERRIDE WITH A CALL TO        */
/* QCMDEXC.                                                         */

/* THIS REPORT IS CONDENSED PRINT AT 15 CPI; SO, ON A LASER PRINTER */
/* FORCE IT TO DRAWER '2' FOR LEGAL PAPER AND FORCE 'LANDSCAPE'     */

             SBDPRTOVR  CMDVAR(&CMD) FILE(QPRINT) OUTQ(&OUTQ) +
                          WIDTH(198) LANDSCAPE(*YES) DRAWER(2) +
                          COPIES(&COPYS) HOLD(&HOLD)

             CALL       PGM(QCMDEXC) PARM(&CMD 512)

/*------------------------------------------------------------------*/
/* CALL THE PROGRAM TO PRINT THE REPORT                             */
/*------------------------------------------------------------------*/

             CALL PGM(HP603)

EXIT:        CLOF       OPNID(HSP034)

             DLTOVR     FILE(*ALL)

             ENDPGM

