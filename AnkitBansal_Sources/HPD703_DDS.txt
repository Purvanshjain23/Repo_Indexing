     A*%%TS  SD  20010709  151044  ISLRAMS     REL-V4R4M0  5769-PW1
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD703
     A*  TITLE:         VIEW FARMS FOR A MANAGER
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  DATE:          07/09/01
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CF03(03 'EXIT')
     A                                      CF04(04 'PROMPT')
     A                                      CF05(05 'REFRESH')
     A                                      CF12(12 'PREVIOUS')
     A*
     A*
     A*---------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP703S1                   SFL
     A*%%TS  SD  20010705  152602  ISLRAMS     REL-V4R4M0  5769-PW1
     A  85                                  SFLNXTCHG
     A            HDRRN1         4Y 0H
     A            D1FSCD    R        O  9  3REFFLD(@@FSCD)
     A                                      EDTCDE(M)
     A            D1FSNM    R        O  9 11REFFLD(FSREC/FSFSNM *LIBL/HSP018)
     A            D1CELL    R        O  9 39REFFLD(FSREC/FSCELL *LIBL/HSP018)
     A                                      EDTCDE(M)
     A            D1PTCD    R        O  9 45REFFLD(FSPTCD)
     A            D1PPCD    R        O  9 52REFFLD(FSPPCD)
     A            D1CMGCD   R        O  9 60REFFLD(FSREC/FSCMGCD *LIBL/HSP018)
     A            D1PMGCD   R        O  9 67REFFLD(FSREC/FSPMGCD *LIBL/HSP018)
     A            D1MMGCD   R        O  9 74REFFLD(FSREC/FSMMGCD *LIBL/HSP018)
     A*
     A*
     A*
     A*---------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP703C1                   SFLCTL(HP703S1)
     A                                      SFLSIZ(0050)
     A                                      SFLPAG(0013)
     A N82                                  ROLLUP(25 'PAGE FORWARD')
     A                                      SETOF(53)
     A                                      SETOF(54)
     A                                      RTNCSRLOC(&RCD &FLD)
     A                                      OVERLAY
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 26'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  2 25'VIEW FARMS FOR A MANAGER'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  4  2'Manager ..:'
     A            C1MGCD    R        O  4 14REFFLD(HSRREF/@@MGCD *LIBL/HSPREF)
     A            C1MGNM    R        O  4 21REFFLD(HSRREF/@@MGNM *LIBL/HSPREF)
     A                                  5 47'?'
     A                                      DSPATR(HI)
     A                                  5 54'?'
     A                                      DSPATR(HI)
     A                                  6 45'-Production-'
     A                                      DSPATR(HI)
     A                                  6 60'----- Managers ----'
     A                                      DSPATR(HI)
     A                                  7  4'Farm'
     A                                      DSPATR(HI)
     A                                  7 11'Name'
     A                                      DSPATR(HI)
     A                                  7 38'Cell'
     A                                      DSPATR(HI)
     A                                  7 45'Type'
     A                                      DSPATR(HI)
     A                                  7 52'Phase'
     A                                      DSPATR(HI)
     A                                  7 60'Cell'
     A                                      DSPATR(HI)
     A                                  7 67'Prod'
     A                                      DSPATR(HI)
     A                                  7 74'Multi'
     A                                      DSPATR(HI)
     A            C1FSCD    R        B  8  3REFFLD(@@FSCD)
     A                                      DSPATR(HI)
     A                                      EDTCDE(M)
     A            C1CELL    R        B  8 39REFFLD(HSRREF/@@CELL *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A            C1PTCD    R        B  8 45REFFLD(FSPTCD)
     A                                      DSPATR(HI)
     A  53                                  DSPATR(PC)
     A  99                                  DSPATR(PR)
     A            C1PPCD    R        B  8 52REFFLD(FSPPCD)
     A                                      DSPATR(HI)
     A  54                                  DSPATR(PC)
     A  99                                  DSPATR(PR)
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R HP703K1
     A*%%TS  SD  20010709  094853  ISLRAMS     REL-V4R4M0  5769-PW1
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A                                 23 11'F4=Prompt'
     A                                 23 22'F5=Refresh'
     A                                 23 34'F12=Previous'
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *---------------------------------------------------------------
     A          R HP703ES                   SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE CONTROL FORMAT
      *---------------------------------------------------------------
     A          R HP703EC                   SFLCTL(HP703ES)
      *
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
