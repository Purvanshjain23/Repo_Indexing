/********************************************************************/
/*                                                                  */
/*  SYNOPSIS - PRINT CUSTOMER FILE JOURNAL ENTRIES                  */
/*                                                                  */
/*  SYSTEM    - ORDER MANAGEMENT SYSTEM                             */
/*  DATE      - 04/11/00                                            */
/*  DEVELOPER - PURVA DROGE                                         */
/*                                                                  */
/********************************************************************/
/* MODIFICATIONS:                                                   */
/* 04/27/00 PKP ADDED PROCESSING FOR DETERMINING THE STARTING       */
/*              JOURNAL RECEIVER.  THERE MAY BE MORE THEN ONE       */
/*              RECEIVER IF THE RECEIVER REACHES ITS MAXIMUM       */
/*              SIZE AND THE RECEIVER IS ROLLED IN THE MIDDLE      */
/*              OF THE DAY.                                        */
/* 08/10/04 SLM Remove PRKFLIB from being hard coded.               */
/* 12/09/08 RMC CHANGE QRY TO SEQUEL IN PRKPLIB                     */
/* 01/13/09 LJB E00327 CHANGE PRINTER FROM PRKPRT05 TO PRKPRT08     */
/********************************************************************/
             PGM

             DCL        VAR(&STRRCV) TYPE(*CHAR) LEN(10)
             DCL        VAR(&JOBDATE) TYPE(*CHAR) LEN(6)
             DCLF       FILE(SBDSYS/DSPOBJD)

/*------------------------------------------------------------------*/
/* DETERMINE STARTING RECEIVER                                      */
/*------------------------------------------------------------------*/
             RTVJOBA    DATE(&JOBDATE)

             DSPOBJD    OBJ(MIMIXPRK/MXRCVR*) OBJTYPE(*JRNRCV) +
                          OUTPUT(*OUTFILE) OUTFILE(QTEMP/JRNRCV)

             OVRDBF     FILE(DSPOBJD) TOFILE(QTEMP/JRNRCV)

 LOOP:       RCVF
             MONMSG     MSGID(CPF0864) EXEC(GOTO CMDLBL(DONE))

             IF COND(&ODCDAT *EQ &JOBDATE) THEN(DO)
             CHGVAR     VAR(&STRRCV) VALUE(&ODOBNM)
             GOTO       CMDLBL(DONE)
             ENDDO
             GOTO       CMDLBL(LOOP)

/*------------------------------------------------------------------*/
/* DISPLAY THE JOURNAL INTO AN OUTFILE                              */
/*------------------------------------------------------------------*/
 DONE:
             DSPJRN     JRN(MIMIXPRK/PRKJRN) FILE((CAEOREP *FIRST)) +
                          RCVRNG(MIMIXPRK/&STRRCV *CURRENT) +
                          ENTTYP(*RCD) OUTPUT(*OUTFILE) +
                          OUTFILE(PRKFLIB/CAEOJOP)
             MONMSG     MSGID(CPF0000)

/*------------------------------------------------------------------*/
/* PRINT QUERY                                                      */
/*------------------------------------------------------------------*/
          /* RUNQRY     QRY(PRKPLIB/CAEOJOP) QRYFILE((PRKFLIB/CAEOJOP)) */
/*------------------------------------------------------------------*/
             REPORT     REPORT(PRKPLIB/CAEOJOP) +
                          VIEW(PRKPLIB/CAEOJOPV) HOLD(*NO) +
                          OUTQ(PRKPRT08)
/***         REPORT     REPORT(PRKPLIB/CAEOJOP) +
                          VIEW(PRKPLIB/CAEOJOPV) HOLD(*NO) +
                          OUTQ(PRKPRT05)                  ***********/
/*------------------------------------------------------------------*/
/* END PROGRAM                                                      */
/*------------------------------------------------------------------*/
             ENDPGM
