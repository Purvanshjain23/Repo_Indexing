       //----------------------------------------------------------------------
       // Program To Consume the GetPeriodNo Web Service
       // Using the RPGLE Stub Service Program "FiscalPeriod"
       // ˆMARINE:
       // Create using: CRTRPGMOD MODULE(E1INT1/GETFYPNO)
       //                         SRCFILE(E1INT1QRPGLESRC)
       //                         SRCMBR(GETFYPNO)
       //                         REPLACE(*NO)
       //                           INCDIR('/e1int/fp')
       //
       //               CRTPGM PGM(E1INT1/GETFYPNO)
       //                   BNDSRVPGM((E1INT1/FPERIOD))
       //
       // ˆCHANGE FOR FOODS:
       // 1.a.  addlible EIItestf -- need file f55config
       // 1.b.  Create using: CRTRPGMOD MODULE(E1iDEVGEN/GET09031)
       //                         SRCFILE(E1iDEVGEN/QRPGLESRC)
       //                         SRCMBR(GET09031)
       //                           INCDIR('/e1int/fp')
       //   2  have lee copy the service pgm from e1int1 to e1iplib- first time only
       //   3.          CRTPGM PGM(E1IDEVGEN/GET09031)
       //                      BNDSRVPGM((E1IPLIB/FPERIOD)  (QSYSDIR/QAXIS10CC))
       //
     FF55CONFIG IF   E           K DISK
       //----------------------------------------------------------------------
     d*MainRoutine     PR                  Extpgm('GETFYPNO')
     d MainRoutine     PR                  Extpgm('GET09031')
     d                                5A
     d                                6  0
     d                                2  0
     d                                2  0
     d                                2  0
     d                                1n
     d MainRoutine     PI
     d $company                       5A
     d $datejul                       6  0
     d $fperiod                       2  0
     d $fyear                         2  0
     d $fcentury                      2  0
     d $bssv                          1n
       // The include members below are created by WSDL2RPG
      /copy /e1int/fp/GetPeriodNoManager.rpgleinc
      /copy /e1int/fp/GetPeriodNoManager_util.rpgleinc
      /copy /QIBM/ProdData/OS/WebServices/V1/client/include/Axis.rpgleinc
     d ws_ds           ds                  likeds(This_t) inz
     d m1              ds                  likeds(InputGetPN_t)
     d m2              ds                  likeds(OutputGetPN_t)
     d m3              ds                  likeds(xsd_int)
     d m4              ds                  likeds(xsd_string)
     d X_A1            S             10I 0 inz
     d X_A2            S             10A   inz
     d X_A3            S              6A   inz
     d X_A4            S              1A   inz
     d X_A7            S             80A   inz
     d X_ENDPOINT      S            100A   inz
     d X_USER          S             10A   inz
 CH01d X_ENV           S             10A   inz
 CH01d X_CSTR          S             40A   inz
     d X_PASS          S             30A   inz
     d NULLSTR         s              1

     d rtnInd          s              1n
     D phb             S               *
     D BNode1          S               *
     D BNode2          S               *
     D BNode3          S               *
     D uriWSSE         C                   'http://docs.oasis-open.org/wss-
     D                                     /2004/01/oasis-200401-wss-wssec-
     D                                     urity-secext-1.0.xsd'
     D uriWSU          C                   'http://docs.oasis-open.org/wss-
     D                                     /2004/01/oasis-200401-wss-wssec-
     D                                     urity-utility-1.0.xsd'
     D uriToken        C                   'http://docs.oasis-open.org/wss-
     D                                     /2004/01/oasis-200401-wss-usern-
     D                                     ame-token-profile-1.0#PasswordT-
     D                                     ext'
     D uriEncry        C                   'http://docs.oasis-open.org/wss-
     D                                     /2004/01/oasis-200401-wss-soap-
     D                                     message-security-1.0#Base64Bina-
     D                                     ry'
      /free
           setll *start F55CONFIG;
           READ F55CONFIG;
           Dow Not %EOF(F55CONFIG);
           X_A7=S1DS01;
           X_USER=S1USER;
           X_PASS=S1DL01;
           X_ENV=S1ENVD;
           READ F55CONFIG;
           EndDo;
       X_ENDPOINT=%TRIML(%TRIMR(X_A7)) +'/GetPeriodNoManager';
       X_CSTR='DN='+ %TRIML(%TRIMR(X_USER)) + ',ENV=' +
               %TRIML(%TRIMR(X_ENV)) + ',ROLE=*ALL';
       ws_ds.endpoint = %TRIML(%TRIMR(X_ENDPOINT));
       // Create a connection to the web service
       if stub_create_GetPeriodNoManager(ws_ds);
       m1.codeLength.value=$datejul;
       m1.company.value=$company;
       m1.isNil_InputGetPN_t='0';
       $fperiod=0;
       X_A3=%char($datejul);
       $fyear=%int(%subst(X_A3:2:2));
       X_A4=%subst(X_A3:1:1);
       if X_A4='1';
       $fcentury=20;
       endif ;
       axiscStubSetSecure(ws_ds.handle:
       '/QIBM/UserData/ICSS/Cert/Server/DEFAULT.KDB':
       '':'':'NONE':'NONE':'':'':'true');
         phb = axiscStubCreateSOAPHeaderBlock(
         ws_ds.handle:'Security':uriWSSE:'wsse');
         axiscHeaderBlockCreateNamespaceDeclINamespace(
         phb:'wsu':uriWSU);
         axiscHeaderBlockCreateStdAttribute(phb:
         AXISC_ATTR_MUST_UNDERSTAND_TRUE:AXISC_SOAP_VER_1_1);
         Bnode1=axiscHeaderBlockCreateChildBasicNode(phb:
         AXISC_ELEMENT_NODE:'UsernameToken':'wsse':*NULL:*NULL);
         axiscHeaderBlockAddChild(phb:Bnode1);
         Bnode2=axiscHeaderBlockCreateChildBasicNode(phb:
         AXISC_ELEMENT_NODE:'Username':'wsse':*NULL:*NULL);
         axiscBasicNodeAddChild(Bnode1:Bnode2);
         Bnode3=axiscHeaderBlockCreateChildBasicNode(phb:
         AXISC_CHARACTER_NODE:*NULL:*NULL:*NULL:%TRIML(%TRIMR(X_CSTR)));
         axiscBasicNodeAddChild(Bnode2:Bnode3);
         Bnode2=axiscHeaderBlockCreateChildBasicNode(phb:
         AXISC_ELEMENT_NODE:'Password':'wsse':*NULL:*NULL);
         axiscBasicNodeCreateAttribute(Bnode2:
         'Type':*NULL:*NULL:uriToken);
         axiscBasicNodeAddChild(Bnode1:Bnode2);
         Bnode3=axiscHeaderBlockCreateChildBasicNode(phb:
         AXISC_CHARACTER_NODE:*NULL:*NULL:*NULL:%TRIML(%TRIMR(X_PASS)));
         axiscBasicNodeAddChild(Bnode2:Bnode3);
          // Call the web service TO Get Period Number
          rtnInd = stub_op_GetPNManager(ws_ds
                                   : m1
                                   : m2);

          $bssv=rtnInd;
          // Display The Returned Value of Period
             m3.value=m2.nextNumberingIndexNumber.value;
             X_A2=%char(m3.value);
             $fperiod=m3.value;

          // Close the web service connection
          stub_destroy_GetPeriodNoManager(ws_ds);
       else;
          dsply %subst(ws_ds.excString:1:50);
       endif;

       return;
      /end-free

