      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Triumph Foods
      * PROGRAM:     TF813 - Retrieve Product Classification and Structure
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     08/08/05
      *
      * Function:    This function retrieves Item Structure and Class Group stuff
      *              for an incoming Item.
      *
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 09/19/06  LeAnne Ramsey
      *           Recompile only. Synon file PPAMREP was changed.
      *
      * 08/13/07  LeAnne Ramsey
      *           Added a new parm: Item Type Code
      *
      * 09/11/08  LeAnne Ramsey
      *           Added a new parm: Export/Domestic Flag
      /eject
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fombyrel1  if   e           k disk
      *    Item default
      *
      *
     Fppamrel1  if   e           k disk
      *    TF Classification
      *
      /eject
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      *---------------------------------------------------------------
      * CONSTANTS
      *---------------------------------------------------------------
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      *---------------------------------------------------------------
      * STANDALONE FIELDS
      *---------------------------------------------------------------
      *
      *
      * Parm fields
      *
     D xxprcd          s                   like(byhgcd)
     D xxistycd        s                   like(byrgnb)
     D xxisgrcd        s                   like(byrhnb)
     D xxisclcd        s                   like(byrinb)
     D xxtfclcd        s                   like(bytcst)
     D xxtfcgcd        s                   like(amsvsx)
     D xxglsub         s                   like(bysycd)
     D xxitycd         s                   like(byn0cd)
     D xxedfl          s                   like(bysust)
     D xxcoownfl       s              1
     D xxprdcmp        s              3
      *
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * Mainline
      ****************************************************************
      *
      *
      * Retrieve Item Structure stuff from Item Default file:
      *  1) Item Structure Type Code
      *  2) Item Structure Group Code
      *  3) Item Structure Class Code
      *  4) GL Sub Code
      *  5) Item Type Code
      *  6) Co-owned
      *
     C     xxprcd        chain     ombyrel1                           92
     C                   if        *in92 = *off                                 If hit
      *
     C                   move      bytcst        xxtfclcd
     C                   z-add     byrgnb        xxistycd
     C                   z-add     byrhnb        xxisgrcd
     C                   z-add     byrinb        xxisclcd
     C                   move      bysycd        xxglsub
     C                   move      byn0cd        xxitycd
     C                   move      bysust        xxedfl
      *
      * Set the Co-Owned Flag and Producing Company values.
      *
     C                   select
     C                   when      byp3st = 'N'
     C                   move      no            xxcoownfl
     C                   move      *blank        xxprdcmp
      *
     C                   when      byp3st = 'S'
     C                   move      yes           xxcoownfl
     C                   eval      xxprdcmp = 'SBF'
      *
     C                   when      byp3st = 'T'
     C                   move      yes           xxcoownfl
     C                   eval      xxprdcmp = 'TF'
     C                   endsl
      *
      * Retrieve TF Class Group from TF Classification file using the
      * TF Classification Code.
      *
     C     xxtfclcd      chain     ppamrel1                           92
     C                   if        *in92 = *off
     C                   move      amsvsx        xxtfcgcd
     C                   endif
      *
     C                   endif                                                  If hit
      *
      *
     C                   seton                                        lr
      /eject
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
     C     *entry        plist
     C                   parm                    xxprcd
     C                   parm                    xxistycd
     C                   parm                    xxisgrcd
     C                   parm                    xxisclcd
     C                   parm                    xxtfclcd
     C                   parm                    xxtfcgcd
     C                   parm                    xxglsub
     C                   parm                    xxitycd
     C                   parm                    xxcoownfl
     C                   parm                    xxprdcmp
     C                   parm                    xxedfl
      *
     C                   endsr
      /eject
