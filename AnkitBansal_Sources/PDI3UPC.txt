/*********************************************************************/
/*     Program: PDI3UPC                                              */
/*       Title: Prompt for printer overrides, then submit report.    */
/*        Date: 02/09/98                                             */
/* Description: - Calls print options prompt program                 */
/*              - Submittes Lockbox Proof Report                     */
/*                                                                   */
/*  Parameters:                                                      */
/*  Usage   Field       Field Description                            */
/*  -----   ---------   ------------------------------------------   */
/*    O     &RETURN     Return code                                  */
/*    I     &COMPANY    Company Number                               */
/*    I     &LCKBOX     Lockbox Number                               */
/*    I     &DDATE      Deposit Date                                 */
/*    I     &DESC       Description                                  */
/*    I     &APPLIC     Application Code                             */
/*                                                                   */
/*********************************************************************/
/* Modifications:                                                    */
/*                                                                   */
/*   Date     Pgmr     Description                                   */
/* -------- ---------- --------------------------------------------  */
/* 04/6/05   RMC       REMOVED CO AND ADDED DEPOSIT TIME TO KEY    */
/*********************************************************************/

             PGM  PARM(&RETURN &LCKBOX &DDATE &DTIME &DESC &APPLIC)

   /*-------------------*/
   /* Declare Variables */
   /*-------------------*/

             /* Parameter Fields */
             DCL  VAR(&RETURN)  TYPE(*CHAR) LEN(7)
 /*          DCL  VAR(&COMPANY) TYPE(*CHAR) LEN(3)    */
             DCL  VAR(&LCKBOX)  TYPE(*CHAR) LEN(7)
             DCL  VAR(&DDATE)   TYPE(*CHAR) LEN(7)
             DCL  VAR(&DTIME)   TYPE(*CHAR) LEN(4)
             DCL  VAR(&DESC)    TYPE(*CHAR) LEN(25)
             DCL  VAR(&APPLIC)  TYPE(*CHAR) LEN(6)

             /* Internal Work Fields */
             DCL  VAR(&OUTQ)    TYPE(*CHAR) LEN(10)
             DCL  VAR(&HOLD)    TYPE(*CHAR) LEN(4)
             DCL  VAR(&SAVE)    TYPE(*CHAR) LEN(4)
             DCL  VAR(&COPYN)   TYPE(*DEC)  LEN(2 0)
             DCL  VAR(&COPY)    TYPE(*CHAR) LEN(2)
             DCL  VAR(&NQSTAT)  TYPE(*CHAR) LEN(01)
             DCL  VAR(&NIGHTQ)  TYPE(*CHAR) LEN(10)
             DCL  VAR(&MSG)     TYPE(*CHAR) LEN(512)
             DCL  VAR(&MSGID)   TYPE(*CHAR) LEN(7)
             DCL  VAR(&MSGL)    TYPE(*DEC)  LEN(5 0)

   /*----------------------*/
   /* Initialize Variables */
   /*----------------------*/
             CHGVAR VAR(&RETURN) VALUE(' ')
             CHGVAR VAR(&OUTQ)   VALUE(' ')
             CHGVAR VAR(&HOLD)   VALUE(' ')
             CHGVAR VAR(&SAVE)   VALUE(' ')
             CHGVAR VAR(&COPYN)  VALUE(0)
             CHGVAR VAR(&NQSTAT) VALUE('N')
             CHGVAR VAR(&NIGHTQ) VALUE(' ')

   /*----------------------------------------------*/
   /* Monitor for all message at the program level */
   /*----------------------------------------------*/
             MONMSG MSGID(CPF0000) EXEC(GOTO CMDLBL(MSGTRAP))

   /*-----------------------------------*/
   /* Call Print Options Prompt Program */
   /*-----------------------------------*/
             CALL PGM(PDG0PVR) +
                  PARM(&RETURN &DESC &OUTQ &HOLD +
                       &SAVE &COPYN &NQSTAT &NIGHTQ &APPLIC)
             CHGVAR VAR(&COPY) VALUE(&COPYN)
             IF COND(&RETURN *NE ' ') THEN(DO)
                GOTO CMDLBL(USERQUIT)
             ENDDO

   /*---------------------------*/
   /* Submit CL to Print Report */
   /*---------------------------*/
             SBMJOB CMD(CALL PGM(PDI4UPC) +
                             PARM( &LCKBOX &DDATE +
                          &DTIME  &OUTQ &HOLD &SAVE &COPY)) +
                    JOB(PDI1PFR) JOBQ(&NIGHTQ)
             GOTO CMDLBL(END)

   /*-----------*/
   /* User Quit */
   /*-----------*/
 USERQUIT:
             CHGVAR VAR(&RETURN) VALUE('Y2U0005')
             GOTO CMDLBL(END)

   /*------------------------------*/
   /* Trap errors and send message */
   /*------------------------------*/
 MSGTRAP:
             RCVMSG MSGTYPE(*EXCP) MSGDTA(&MSG) MSGDTALEN(&MSGL) +
                    MSGID(&MSGID)
             MONMSG MSGID(CPF0000) EXEC(GOTO CMDLBL(END))
             SNDPGMMSG MSGID(&MSGID) MSGF(QCPFMSG) +
                       MSGDTA(%SST(&MSG 1 &MSGL)) +
                       MSGTYPE(*ESCAPE)
             MONMSG MSGID(CPF0000) EXEC(GOTO CMDLBL(END))

   /*----------------*/
   /* End of Program */
   /*----------------*/
 END:
             ENDPGM
