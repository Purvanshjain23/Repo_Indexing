     A*%%TS  SD  20010904  110149  ISLRAMS     REL-V4R4M0  5769-PW1
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD1008
     A*  TITLE:         MAINTAIN TARGET TABLES FOR BGF FARM
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  DATE:          09/21/00
     A*
     A*  INDICATOR 60 IS ON WHEN IN REVISE MODE
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A N90                                  CF04(04 'PROMPT')
     A N90                                  CF05(05 'REFRESH')
     A N90                                  CF09(09 'ACCEPT')
     A N90 60                               CF11(11 'DELETE')
     A*
     A*-------------------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*-------------------------------------------------------------------------
     A          R HP1008S1                  SFL
     A*%%TS  SD  20000921  155339  ISLRAMS     REL-V4R4M0  5769-PW1
     A  85                                  SFLNXTCHG
     A            HDRRN1    R        H      REFFLD(@@RRN)
     A            D1TTBCD   R        B 12  3REFFLD(@@TTBCD)
     A  31
     AO 51                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A N31N90                               DSPATR(HI)
     A            D1MDYEFDT R        B 12 15REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A  32                                  DSPATR(PC)
     A  32                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A N32N90                               DSPATR(HI)
     A*
     A*-------------------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*-------------------------------------------------------------------------
     A          R HP1008C1                  SFLCTL(HP1008S1)
     A*%%TS  SD  20010904  110149  ISLRAMS     REL-V4R4M0  5769-PW1
     A                                      SFLSIZ(0050)
     A                                      SFLPAG(0010)
     A N90                                  ROLLUP(25 'PAGE FORWARD')
     A                                      SFLCSRRRN(&HDRR1)
     A                                      SETOF(31)
     A                                      SETOF(32)
     A                                      SETOF(51)
     A                                      RTNCSRLOC(&RCD &FLD)
     A                                      OVERLAY
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A            HDPAG1    R        H      REFFLD(@@PAG)
     A                                      SFLRCDNBR
     A            HDRR1     R        H      REFFLD(@@RRN)
     A            RCD       R        H      REFFLD(@@RCD)
     A            FLD       R        H      REFFLD(@@FLD)
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 28'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USR)
     A                                      DSPATR(HI)
     A            C1MODE    R        O  2  2REFFLD(@@MODE)
     A                                      DSPATR(HI)
     A                                  2 19'MAINTAIN TARGET TABLES FOR BGF FAR-
     A                                      M SITE'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@JOB)
     A                                      DSPATR(HI)
     A                                  4  2'Farm site business office ..:'
     A            C1FSBO    R        O  4 33REFFLD(HSRREF/FSFSBO *LIBL/HSPREF)
     A            C1BODS    R        O  4 43REFFLD(HSRREF/BOBODS *LIBL/HSPREF)
     A                                  5  2'Farm site ..................:'
     A            C1FSCD    R        O  5 33REFFLD(HSRREF/FSFSCD *LIBL/HSPREF)
     A                                      EDTCDE(M)
     A            C1FSNM    R        O  5 43REFFLD(HSRREF/FSFSNM *LIBL/HSPREF)
     A                                  6  2'Production type ............:'
     A            C1PTCD    R        O  6 33REFFLD(HSRREF/FSPTCD *LIBL/HSPREF)
     A            C1PTDS    R        O  6 43REFFLD(HSRREF/PTPTDS *LIBL/HSPREF)
     A                                  9  5'?'
     A                                 10  3'Target'
     A                                 10 15'Effective'
     A                                 11  3'Table'
     A                                 11 17'Date'
     A*
     A*-------------------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*-------------------------------------------------------------------------
     A          R HP1008K1
     A*%%TS  SD  20000927  132850  ISLRAMS     REL-V4R4M0  5769-PW1
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A N90                             23 11'F4=Prompt'
     A N90                             23 22'F5=Refresh'
     A N90                             23 34'F9=Accept'
     A N90 60                          23 45'F11=Delete All Entries for Farm'
      *
     A*-------------------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
     A*-------------------------------------------------------------------------
     A          R HP1008ES                  SFL SFLMSGRCD(24)
      *
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
     A*-------------------------------------------------------------------------
      * ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*-------------------------------------------------------------------------
     A          R HP1008EC                  SFLCTL(HP1008ES)
      *
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
