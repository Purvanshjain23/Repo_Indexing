/********************************************************************/
/*                                                                  */
/*  SYNOPSIS -  THIS CL IS CALLED FROM THE REPRINT INVOICES SCREEN. */
/*              THE CL WILL SUBMIT A CL THAT WILL FAX THE INVOICE.  */
/*                                                                  */
/*  SYSTEM    - ORDER MANAGEMENT SYSTEM                             */
/*  PROGRAM   - FAX INVOICE                                         */
/*  DATE      - 01/13/97                                            */
/*                                                                  */
/*      PARAMETERS:                                                 */
/*                                                                  */
/*     O    &RETURN     - RETURN CODE                               */
/*     I    &APPLIC     - APPLICATION CODE                          */
/*     I    &PGM        - CL PROGRAM TO SUBMIT                      */
/*     I    &DESC       - PRINT PROGRAM DESCRIPTION (FOR PROMPT     */
/*                         SCREEN)                                  */
/*     I    &COMPNYN    -  COMPANY NUMBER (PASSED)                  */
/*     I    &ORDRN      -  ORDER NO. (PASSED)                       */
/*     I    &CUSTNO     -  CUSTOMER NUMBER (PASSED)                 */
/*     I    &RFLAG      -  REPRNT FLAG (PASSED)                     */
/*     I    &NIGHTQ     -  NIGHT QUEUE                              */
/*     I    &JOBCDE     -  JOB NAME CODE                            */
/*     I    &CVRNME     -  COVER SHEET NAME                         */
/*     I    &CRDFLAG    -  USE CREDIT CONTACT INFO (PASSED)         */
/********************************************************************/
/*  MARK ARENSBERG 02/24/97 - ADDED THE OPTION TO SEND THE          */
/*       JOBQ TO THE NIGHTQ                                         */
/********************************************************************/
 PDVMUPC:    PGM        PARM(&RETURN &PGM &APPLIC &DESC &COMPNYN +
                          &CUSTNO &ORDRN &RFLAG &NIGHTQ &JOBCDE +
                          &CVRNME &CRDFLAG)
/* PARMS                                                                      */
        DCL    &RETURN     *CHAR   (7)           /* Return Code               */
        DCL    &APPLIC     *CHAR   (06)          /* Application Code          */
        DCL    &RFLAG      *CHAR   (01)          /* REPRINT FLAG              */
        DCL    &PGM        *CHAR   (10)          /* CL to Submit              */
        DCL    &DESC       *CHAR   (25)          /* PRT Pgm Description       */
        DCL    &COMPNYN    *DEC                  /* Company Number (Passed)   */
        DCL    &ORDRN      *DEC                  /* Order Number   (Passed)   */
        DCL    &CUSTNO     *DEC                  /* CUSTOMER NO    (Passed)   */
        DCL    &NIGHTQ     *CHAR   (10)          /* Night Job Queue           */
        DCL    &JOBCDE     *CHAR   (10)          /* JOB NAME CODE             */
        DCL    &CVRNME     *CHAR   (10)          /* COVER SHEET NAME          */
        DCL    &CRDFLAG    *CHAR   (01)          /* CREDIT CONTACT FLAG       */

/* ERROR MESSAGE FIELDS                                                       */
        DCL    &MSG        *CHAR   (512)         /* message                   */
        DCL    &MSGID      *CHAR   (7)           /* message id                */
        DCL    &MSGL       *DEC    (5 0)

/********************************************************************/
/*   INITIALIZE VARIABLES                                           */
/********************************************************************/

             CHGVAR     VAR(&RETURN) VALUE('       ')
/********************************************************************/
/*   MONITOR FOR ALL MESSAGES AT PROGRAM LEVEL                      */
/********************************************************************/

             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(MSGTRAP))

/********************************************************************/
/*   CALL PROMPT PROGRAM                                            */
/********************************************************************/

             IF (&NIGHTQ *NE ' ') THEN(GOTO CMDLBL(SUBMIT))

             CALL       PGM(PDXOPVR) PARM(&RETURN &DESC &NIGHTQ +
                                          &APPLIC)
             IF         COND(&RETURN *NE ' ') THEN(GOTO +
                          CMDLBL(USERQUIT))

/*   SUBMIT PRINT CL PROGRAM                                        */
/********************************************************************/
 SUBMIT:     SBMJOB     CMD(CALL PGM(&PGM) PARM(&RETURN &COMPNYN +
                          &ORDRN &CUSTNO &RFLAG &JOBCDE &CVRNME +
                           &CRDFLAG)) JOB(INVOICES) JOBQ(&NIGHTQ)

   /*        CALL       PGM(&PGM) PARM(&RETURN &COMPNYN &ORDRN +
                          &CUSTNO &RFLAG &JOBCDE &CVRNME &CRDFLAG) */
             GOTO       CMDLBL(END)

/*-------------------------USER QUIT--------------------------------*/
 USERQUIT:   CHGVAR     VAR(&RETURN) VALUE('Y2U0005')
             GOTO       CMDLBL(END)

/*----------------------RECEIVE MESSAGES----------------------------*/

 MSGTRAP:    RCVMSG     MSGTYPE(*EXCP) MSGDTA(&MSG) MSGDTALEN(&MSGL) +
                          MSGID(&MSGID)
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(END))

             SNDPGMMSG  MSGID(&MSGID) MSGF(QCPFMSG) MSGDTA(%SST(&MSG +
                          1 &MSGL)) MSGTYPE(*ESCAPE)
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(END))

 END:

             ENDPGM

