/***************************************************************** */
/*                                                                 */
/*  PROGRAM NUMBER:  SHBLDSALES                                    */
/*  PROGRAM NAME:    BUILD DATAMART FILES FOR OMS SALES HISTORY    */
/*  PROGRAMMER:      ALICE                                         */
/*  CREATE DATE:     07/08/00                                      */
/*                                                                 */
/*  FUNCTION:        THIS CL IS SUBMITTED NIGHTLY FROM THE JOB     */
/*                   SCHEDULER ON BIGBYTE.                         */
/*                                                                 */
/*                   IT PERFORMS THE FOLLOWING FUNCTIONS:          */
/*                   A) CLEARS THE DATAMART FILES                  */
/*                   B) EXTRACTS SALES DATA                        */
/*                                                                 */
/*******************************************************************/
/* MODIFIED:                                                       */
/*                                                                 */
/* 09/11/07  LEANNE RAMSEY                                         */
/*           TO HANDLE DAILYS, WE WILL NOW WRITE DIRECTLY TO FILES */
/*           IN PRKDMFLIB.                                         */
/*                                                                 */
/* 12/31/07  LEANNE RAMSEY                                         */
/*           ADDED NEW "MARKETS" FILE AND LOGIC.                   */
/*                                                                 */
/* 01/17/08  LEANNE RAMSEY                                         */
/*           ADDED MORE "MARKET" FILES AND LOGIC.                  */
/*                                                                 */
/* 03/19/08  LEANNE RAMSEY                                         */
/*           REMOVED LOGIC FROM SH204 THAT POPULATED SEABOARD COST */
/*           FIELDS AND PUT IT IN A NEW PROGRAM. THIS WILL ADDRESS */
/*           THE ISSUES WITH THE 'TFS MARGIN ADJUSTMENT CLOSE'     */
/*           BEING REOPENED/RECLOSED. (LOGIC IS NOT FOR DAILYS')   */
/*                                                                 */
/* 10/20/08  LEANNE RAMSEY                                         */
/*           ADDED NEW "MARKET/ITEM" FILE/LOGIC.                   */
/*                                                                 */
/* 10/22/08  LEANNE RAMSEY                                         */
/*           ADDED NEW "ACCRUAL EXPENSE" FILE/LOGIC.               */
/*                                                                 */
/* 11/05/08  LEANNE RAMSEY                                         */
/*           WE ADDED 3 NEW FIELDS TO SHP204 WHICH ARE POPULATED   */
/*           WITH DATA FROM SHP201, SHP203, SHP210. SO, WE         */
/*           REARRANGED THE "CALLS" APPROPRIATELY.                 */
/*                                                                 */
/* 01/28/09  LEANNE RAMSEY                                         */
/*           WE WILL NO LONGER BUILD FILE SHP207-PRODUCT GROUPS.   */
/*           SO, REMOVED THE CALL TO SH207-BUILD PRODUCT GROUPS.   */
/*           DELETED PROGRAM SH207.                                */
/*           ONCE STEPH ALLEN HAS REMOVED FILE SHP207 FROM         */
/*           COGNOS, WE WILL DELETE THE FILE.                      */
/*           ALSO, ADDED 2 FIELDS (VIEW SEQUENCE NUMBER AND        */
/*           FOCUS FLAG) TO SHP201-ITEM.                           */
/*                                                                 */
/* 01/28/09  LEANNE RAMSEY                                         */
/*           WE WILL NO LONGER BUILD FILE SHP207-PRODUCT GROUPS.   */
/*                                                                 */
/* 08/06/09  LEANNE RAMSEY                                         */
/*           ADDED LOGIC FOR DAILYS ACCRUALS. ACCRUALS ARE NOW     */
/*           BROKEN OUT INTO VARIOUS BUCKETS...BEFORE WE HAD A     */
/*           SINGLE ACCRUAL AMOUNT.                                */
/*                                                                 */
/* 04/26/10  LEANNE RAMSEY                                         */
/*           ADDED 4 FIELDS TO SHP211-EXPENSE ACCRUALS:            */
/*                  Total Market Accrual Amt                       */
/*                  Non-Ledgered Market Accrual Amt                */
/*                  Ledgered Market Accrual Amt                    */
/*                  Weight Billed                                  */
/*           WE WILL POPULATE THE NEW FIELDS WITH A NEW PROGRAM:   */
/*             SH213-ADD ACCRUAL AMOUNTS TO ACCRUAL EXPENSE        */
/*                                                                 */
/* 01/16/13  LEANNE RAMSEY  (E2401)                                */
/*           We now have a new Datamart file/program:              */
/*            SHP214-Claims with Reasons                           */
/*            SH214-Claims with Reasons                            */
/*           We added this for Bret Getzel who is wanting a KPI    */
/*           'count' of claims to ding his salesmen with. We       */
/*           tried to explain the nuances of 'claims' to him...    */
/*           but, he just insisted on getting a 'count'. So,       */
/*           to get started, we are pulling very little data into  */
/*           this file.....maybe when Getzel sees some data he     */
/*           will get a better idea.                               */
/*                                                                 */
/*                                                                 */
/* 01/29/13  LEANNE RAMSEY  (E2401)                                */
/*           We went to Header/Detail files for the Claims stuff.  */
/*           So, we now have SHP214 and SHP215. I added reorg      */
/*           logic since we delete/rewrite the claims.             */
/*                                                                 */
/* 06/20/13  LEANNE RAMSEY  (E2649)                                */
/*           We added a file for Duke Sand/Adam Gross. It is,      */
/*           basically, the Synon PBAOCPP-MPR Wholesale Pork Daily */
/*           file where we have to send pricing info to the USDA   */
/*           at a specific time in the morning and the afternoon   */
/*           (Monday thru Friday). This is NOT for Dailys.         */
/*           I added reorg logic since I delete/rewrite 5 days of  */
/*           records with each run. (Note: I picked 5 days back    */
/*           from the System Date just to try to cover in freaky   */
/*           things.                                               */
/*                                                                 */
/* 07/30/13  LEANNE RAMSEY  (E2649)                                */
/*           I am removing the logic that I added on 6/20/13.      */
/*           Brad Hamilton is requiring us to create data in this  */
/*           file every 30 minutes. So, it will now have a         */
/*           separate CL that will be called from Rose Centonze's  */
/*           program...which will be on the Job Scheduler.         */
/*                                                                 */
/* 01/03/14  LEANNE RAMSEY  (E2967)                                */
/*           We added a new file for Adam Gross (SHP218).          */
/*           It is selected fields from CAEOREP-Company/Customer.  */
/*                                                                 */
/* 01/16/14  LeAnne Ramsey  (E2977)                                */
/*           We added a new file for Adam Gross/Kevin Smith.       */
/*           It is SHP219-Accrual Rates for Pork only..not Dailys. */
/*           They initially wanted it built for all Items...but    */
/*           that ran for 1.5 hours and created almost 1 million   */
/*           records. So, to start with, we will only build it     */
/*           for 'TRIM' Items...items in Item Structure Type 720.  */
/*                                                                 */
/* 03/13/14  LeAnne Ramsey  (E2986)                                */
/*           We added a new file for Adam Gross/Kevin Smith.       */
/*           It is SHP220-Commodity Market Price. Is is only for   */
/*           Seaboard...not Dailys. It is cleared/rebuilt on each  */
/*           run.                                                  */
/*                                                                 */
/* 05/15/14  LeAnne Ramsey  (E3115)                                */
/*           We added a new filE: Warehouse Master.                */
/*           It is only built for Seaboard.....not Dailys.         */
/*           It is cleared/rebuilt on each run.                    */
/*           It only has active warehouses.                        */
/*                                                                 */
/* 12/16/15  Julius High    (E3552SS)                              */
/*           We added a new files for Sales Structure              */
/*           They are only built for Seaboard.....not Dailys.      */
/*           They are cleared/rebuilt on each run.                 */
/*           They only have active records.                        */
/*           SHP254-Datamart Sales: Channel                        */
/*           SHP255-Datamart Sales: Territory                      */
/*           SHP256-Datamart Sales: Market                         */
/*           SHP257-Datamart Sales: Sold To                        */
/*           SHP258-Datamart Sales: Cust SoTo                      */
/*           SHP259-Datamart Sales: Salesperson                    */
/*           SHP260-Datamart Sales: Broker                         */
/*******************************************************************/

             PGM

             DCL        VAR(&QDATA) TYPE(*CHAR) LEN(110)
             DCL        VAR(&ERR)    TYPE(*CHAR) LEN(1) VALUE('N')

             DCL        VAR(&CORG)   TYPE(*CHAR) LEN(1)
             DCL        VAR(&CONO)   TYPE(*DEC)  LEN(3)
             DCL        VAR(&FROMDT) TYPE(*DEC)  LEN(8)
             DCL        VAR(&TODT)   TYPE(*DEC)  LEN(8)

             DCL        VAR(&EMAIL)    TYPE(*CHAR) LEN(38)
             DCL        VAR(&TSTPRDFL) TYPE(*CHAR) LEN(1)
             DCL        VAR(&SUBJECT)  TYPE(*CHAR) LEN(60) +
                          VALUE('SHBLDSALES-DATAMART SALES: Bld +
                          Files...DIST LIST: prk cognos')

/*------------------------------------------------------------------*/
/* Set up the Email Recipient (either Test or Production)           */
/*------------------------------------------------------------------*/

/* Retrieve the Data Area that lets us know whether we are in TEST  */
/* or PRODUCTION. (Always default to the 'TEST' Distribution List.) */

             CHGVAR     VAR(&EMAIL) VALUE('prk cognos test')
             RTVDTAARA  DTAARA(DATSTPRD (1 1)) RTNVAR(&TSTPRDFL)
             IF         COND(&TSTPRDFL *EQ 'P') THEN(CHGVAR +
                          VAR(&EMAIL) VALUE('prk cognos'))

/*-----------------------------------------------------------------*/
/* Add PRKDMFLIB to the top of the library list.                   */
/* We do not expect PRKDMFLIB to already be in the library list;   */
/* but, since we want it at the top of the list, we will remove/add*/
/* it.                                                             */
/*-----------------------------------------------------------------*/
             IF         COND(&TSTPRDFL *EQ 'P') THEN(DO)
             RMVLIBLE   LIB(PRKDMFLIB)
             MONMSG     MSGID(CPF0000)
             ADDLIBLE   LIB(PRKDMFLIB)
             ENDDO

/*-----------------------------------------------------------------*/
/* CLEAR SOME DATAMART FILES in PRKDMFLIB                          */
/*-----------------------------------------------------------------*/
CLRPF:
             CLRPFM     FILE(SHP201) /* Item                       */
             CLRPFM     FILE(SHP202) /* Salesperson                */
             CLRPFM     FILE(SHP203) /* Customer                   */
             CLRPFM     FILE(SHP205) /* Broker                     */
             CLRPFM     FILE(SHP206) /* Market                     */
             CLRPFM     FILE(SHP210) /* Market/Item                */
             CLRPFM     FILE(SHP218) /* Company/Customer           */
             CLRPFM     FILE(SHP219) /* Accrual Rates              */
             CLRPFM     FILE(SHP220) /* Commodity Market Price     */
             CLRPFM     FILE(SHP223) /* Warehouses                 */
             CLRPFM     FILE(SHP254) /* Datamart Sales: Channel    */
             CLRPFM     FILE(SHP255) /* Datamart Sales: Territory  */
             CLRPFM     FILE(SHP256) /* Datamart Sales: Market     */
             CLRPFM     FILE(SHP257) /* Datamart Sales: Sold To    */
             CLRPFM     FILE(SHP258) /* Datamart Sales: Cust SoTo  */
             CLRPFM     FILE(SHP259) /* Datamart Sales: Salesperson*/
             CLRPFM     FILE(SHP260) /* Datamart Sales: Broker     */

/*-----------------------------------------------------------------*/
/* DELETE RECORDS FROM SPECIFIC DATAMART FILES in PRKDMFLIB        */
/*-----------------------------------------------------------------*/

             CALL       PGM(SH2211) PARM(&FROMDT &TODT) /*  Accrual Expense */

/*-----------------------------------------------------------------*/
/* POPULATE DATAMART FILES IN PRKDMFLIB FOR SEABOARD               */
/*-----------------------------------------------------------------*/

             CHGVAR     VAR(&CORG) VALUE('S')

             CLRPFM     FILE(PMECCPP)          /* Clear Workfile */
             CALL       PGM(PMAAXFR) PARM(' ') /* Build Workfile */
             CALL       PGM(SH210) PARM(&CORG) /* Market/Item    */

             CALL       PGM(SH201) PARM(&CORG) /* Item                    */
             CALL       PGM(SH202) PARM(&CORG) /* Salesperson             */
             CALL       PGM(SH203) PARM(&CORG) /* Customer                */
             CALL       PGM(SH205) PARM(&CORG) /* Broker                  */
             CALL       PGM(SH206) PARM(&CORG) /* Market                  */
             CALL       PGM(SH204) PARM(&CORG) /* Sales History           */
             CALL       PGM(SH209)             /* Sales History: Cost     */
             CALL       PGM(SH208) PARM(&CORG) /* Weekly Quotas           */
             CALL       PGM(SH214) PARM(&CORG) /* Claims                  */
             CALL       PGM(SH218) PARM(&CORG) /* Company/Customer        */
             CALL       PGM(SH219CL)           /* Accrual Rates           */
             CALL       PGM(SH220) PARM(&CORG) /* Commodity Market Price  */
             CALL       PGM(SH223)             /* Warehouses              */
             CALL       PGM(SH254) PARM(' ' &CORG) /* Channel             */
             CALL       PGM(SH255) PARM(' ' &CORG) /* Territory           */
             CALL       PGM(SH256) PARM(' ' &CORG) /* Market              */
             CALL       PGM(SH257) PARM(' ' &CORG) /* Sold To             */
             CALL       PGM(SH258) PARM(' ' &CORG) /* Cust SoTo           */
             CALL       PGM(SH259) PARM(' ' &CORG) /* Salesperson         */
             CALL       PGM(SH260) PARM(' ' &CORG) /* Broker              */

/*--------------------------------------------------------------------*/
/* POPULATE DATAMART FILES IN PRKDMFLIB FOR DAILYS                    */
/*--------------------------------------------------------------------*/

             IF         COND(&TSTPRDFL *EQ 'P') THEN(DO)
             ADDLIBLE   LIB(DPMFLIB) POSITION(*AFTER PRKDMFLIB)
             MONMSG     MSGID(CPF0000)
             ENDDO

             CHGVAR     VAR(&CORG) VALUE('D')

             CLRPFM     FILE(PMECCPP)          /* Clear Workfile */
             CALL       PGM(PMAAXFR) PARM(' ') /* Build Workfile */
             CALL       PGM(SH210) PARM(&CORG) /* Market/Item    */

             CALL       PGM(SH201) PARM(&CORG) /* Item           */
             CALL       PGM(SH202) PARM(&CORG) /* Salesperson    */
             CALL       PGM(SH203) PARM(&CORG) /* Customer       */
             CALL       PGM(SH205) PARM(&CORG) /* Broker         */
             CALL       PGM(SH206) PARM(&CORG) /* Market         */
             CALL       PGM(SH204) PARM(&CORG) /* Sales History  */
             CALL       PGM(SH208) PARM(&CORG) /* Weekly Quotas  */
             CALL       PGM(SH214) PARM(&CORG) /* Claims         */
             CALL       PGM(SH218) PARM(&CORG) /* Company/Customer */
             CALL       PGM(SH254) PARM(' ' &CORG) /* Channel    */
             CALL       PGM(SH255) PARM(' ' &CORG) /* Territory  */
             CALL       PGM(SH256) PARM(' ' &CORG) /* Market     */
             CALL       PGM(SH257) PARM(' ' &CORG) /* Sold To    */
             CALL       PGM(SH258) PARM(' ' &CORG) /* Cust SoTo  */
             CALL       PGM(SH259) PARM(' ' &CORG) /* Salesperso */
             CALL       PGM(SH260) PARM(' ' &CORG) /* Broker     */

/* Accrual Expense for Missoula                                  */

             IF         COND(&TSTPRDFL *EQ 'P') THEN(DO)
             ADDLIBLE   LIB(DPMFLIB) POSITION(*AFTER PRKDMFLIB)
             MONMSG     MSGID(CPF0000)

             ADDLIBLE   LIB(JRDATA) POSITION(*AFTER PRKDMFLIB)
             MONMSG     MSGID(CPF0000)
             ENDDO

             CHGVAR     VAR(&QDATA) VALUE(' ')
             CHGVAR     VAR(%SST(&QDATA 1 8)) VALUE('DATE *GE')
             CHGVAR     VAR(%SST(&QDATA 10 8)) VALUE(&FROMDT)
             CHGVAR     VAR(%SST(&QDATA 19 13)) VALUE('*AND DATE +
                          *LE')
             CHGVAR     VAR(%SST(&QDATA 33 8)) VALUE(&TODT)
             CHGVAR     VAR(%SST(&QDATA 42 66)) VALUE('*AND (REC +
                          *EQ "5" *OR REC *EQ "6" *OR REC *EQ "7" +
                          *OR REC *EQ "8")')

             OVRDBF     FILE(ACCRHIST) SHARE(*YES)
             OPNQRYF    FILE((ACCRHIST)) OPTION(*INP) QRYSLT(&QDATA) +
                          KEYFLD((CUST)) SEQONLY(*YES)

             CHGVAR     VAR(&CONO) VALUE(363)
             CALL       PGM(SH211) PARM(&CONO)

             CLOF       OPNID(ACCRHIST)
             DLTOVR     FILE(ACCRHIST)

             IF         COND(&TSTPRDFL *EQ 'P') THEN(DO)
             RMVLIBLE    LIB(JRDATA)
             MONMSG     MSGID(CPF0000)
             ENDDO

/* Accrual Expense for Salt Lake                                 */

             IF         COND(&TSTPRDFL *EQ 'P') THEN(DO)
             ADDLIBLE   LIB(DFDATA) POSITION(*AFTER PRKDMFLIB)
             MONMSG     MSGID(CPF0000)
             ENDDO

             CHGVAR     VAR(&QDATA) VALUE(' ')
             CHGVAR     VAR(&QDATA) VALUE(' ')
             CHGVAR     VAR(%SST(&QDATA 1 8)) VALUE('DATE *GE')
             CHGVAR     VAR(%SST(&QDATA 10 8)) VALUE(&FROMDT)
             CHGVAR     VAR(%SST(&QDATA 19 13)) VALUE('*AND DATE +
                          *LE')
             CHGVAR     VAR(%SST(&QDATA 33 8)) VALUE(&TODT)
             CHGVAR     VAR(%SST(&QDATA 42 66)) VALUE('*AND (REC +
                          *EQ "5" *OR REC *EQ "6" *OR REC *EQ "7" +
                          *OR REC *EQ "8")')

             OVRDBF     FILE(ACCRHIST) SHARE(*YES)
             OPNQRYF    FILE((ACCRHIST)) OPTION(*INP) QRYSLT(&QDATA) +
                          KEYFLD((CUST)) SEQONLY(*YES)

             CHGVAR     VAR(&CONO) VALUE(364)
             CALL       PGM(SH211) PARM(&CONO)

             CLOF       OPNID(ACCRHIST)
             DLTOVR     FILE(ACCRHIST)

             IF         COND(&TSTPRDFL *EQ 'P') THEN(DO)
             RMVLIBLE   LIB(DFDATA)
             MONMSG     MSGID(CPF0000)
             ENDDO

/* Update the Accrual Expense file just built with some SH204 values */

SKIPDAILY:                                 /*** FOR TESTING *******/
             CALL       PGM(SH213) PARM(&CORG)

/*-----------------------------------------------------------------*/
/* REORG                                                           */
/*-----------------------------------------------------------------*/

/* We delete/rebuild current data on some files. So, reorg them.   */

             RGZPFM     FILE(*LIBL/SHP208) /* Weekly Quotas    */
             MONMSG     MSGID(CPF0000)

             RGZPFM     FILE(*LIBL/SHP211) /* Accrual Expenses */
             MONMSG     MSGID(CPF0000)

             RGZPFM     FILE(*LIBL/SHP214) /* Claim Header     */
             MONMSG     MSGID(CPF0000)

             RGZPFM     FILE(*LIBL/SHP215) /* Claim Detail     */
             MONMSG     MSGID(CPF0000)

             RGZPFM     FILE(*LIBL/SHP223) /* Warehouses       */
             MONMSG     MSGID(CPF0000)

/*---------------------------------------------------------------------*/
/* SEND COMPLETION MESSAGE TO I.S. FOLKS                               */
/*---------------------------------------------------------------------*/

             IF         COND(&ERR = 'N') THEN(ESNDMAIL +
                          RECIPIENT(&EMAIL) SUBJECT(&SUBJECT) +
                          MSG('The Sales data was successfully +
                          written to PRKDMFLIB; the build completed +
                          normally.') IMPORTANCE(*HIGH))

             ENDPGM

