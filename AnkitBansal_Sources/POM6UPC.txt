/********************************************************************/
/*                                                                  */
/*  SYNOPSIS -  THIS PROGRAM WILL BE CALLED FROM POST ORDER PGM     */
/*          THIS PROGRAM WILL SEND AN EMAIL TO A EXPORT SALES       */
/*          REP WHEN AN ORDER TO HIS/HER COUNTRY IS ENTERED IN OUR  */
/*          SYSTEM.  THE EMAIL ADDRESS IS IN THE COMPANY VALUES.    */
/*                                                                  */
/*  SYSTEM    - ORDER MANAGEMENT SYSTEM                             */
/*  DATE      - 08/29/01                                            */
/*                                                                  */
/********************************************************************/
/*      PARAMETERS RECEIVED:                                        */
/*                                                                  */
/*               &ORDER   - ORDER NUMBER                            */
/*               &ORDDAT  - ORDER DATE                              */
/*               &ORDTIM  - ORDER TIME                              */
/*               &REQDAT  - REQUESTED DELV DATE                     */
/*               &CUSTPO  - CUSTOMER PO                             */
/*               &ITEMD   - 1ST ITEM DESC                           */
/*               &INTNET  - EMAIL ADDRESS                           */
/********************************************************************/
/* MODIFIED BY:                                                     */
/*   DATE   NAME       DESCRIPTION                                  */
/* -------- ---------- ---------------------------------------------*/
/* 02/23/06 LARA BUSER MONITOR THE ADDLIBLE COMMAND                 */
/********************************************************************/
 POM6UPC:    PGM        PARM(&RETURN &ORDER &CUSTPO &ORDDAT &REQDAT +
                          &ORDTIM &ITEMN &ITEMD &INTNET)
/* PARMS */
         DCL     &RETURN     *CHAR    (7)
         DCL     &ORDER      *DEC     (7 0)
         DCL     &ORDDAT     *DEC    (7 0)
         DCL     &ORDTIM     *DEC    (6 0)
         DCL     &REQDAT     *DEC    (7 0)
         DCL     &ITEMN      *DEC    (7 0)
         DCL     &ITEMD      *CHAR   (30)
         DCL     &CUSTPO     *CHAR    15

         DCL     &CUSTNAME   *CHAR    (25)       /* CUSTMOMER NAME            */
         DCL     &INTNET     *CHAR    (40)       /* EMAIL ADDR                */

         DCL     &ORDERA     *CHAR    7          /* ORDER NUMBER ALPHA        */
         DCL     &ORDDATA    *CHAR    7          /* ORDER DATE   ALPHA        */
         DCL     &ORDTIMA    *CHAR    6          /* ORDER TIME   ALPHA        */
         DCL     &REQDATA    *CHAR    7          /* REQ DLV DT   ALPHA        */
         DCL     &ITEMA      *CHAR    7
         DCL     &ORDDAT2    *CHAR    8          /* ORDER DATE   ALPHA        */
         DCL     &ORDTIM2    *CHAR    8          /* ORDER TIME   ALPHA        */
         DCL     &REQDAT2    *CHAR    8          /* REQ DLV DT   ALPHA        */
         DCL     &MSG        *CHAR    256        /* MESSAGE FOR ESNDMAIL CMD  */
         DCL     &WRK1       *CHAR    3
         DCL     &WRK2       *CHAR    3
         DCL     &WRK3       *CHAR    2

             DCL        VAR(&A) TYPE(*CHAR) LEN(21)
             DCL        VAR(&B) TYPE(*CHAR) LEN(31)
             DCL        VAR(&C) TYPE(*CHAR) LEN(29)
             DCL        VAR(&D) TYPE(*CHAR) LEN(31)
             DCL        VAR(&E) TYPE(*CHAR) LEN(50)

             CHGVAR     VAR(&ORDERA) VALUE(&ORDER)
             CHGVAR     VAR(&ORDDATA) VALUE(&ORDDAT)
             CHGVAR     VAR(&ORDTIMA) VALUE(&ORDTIM)
             CHGVAR     VAR(&REQDATA) VALUE(&REQDAT)
             CHGVAR     VAR(&ITEMA) VALUE(&ITEMN)

FORMAT:      CVTDAT     DATE(&ORDDATA) TOVAR(&ORDDAT2) +
                          FROMFMT(*CYMD) TOFMT(*MDY) TOSEP(/)
             CVTDAT     DATE(&REQDATA) TOVAR(&REQDAT2) +
                          FROMFMT(*CYMD) TOFMT(*MDY) TOSEP(/)
             CHGVAR     VAR(&WRK1) VALUE(%SST(&ORDTIMA 1 2) *CAT ':')
             CHGVAR     VAR(&WRK2) VALUE(%SST(&ORDTIMA 3 2) *CAT ':')
             CHGVAR     VAR(&WRK3) VALUE(%SST(&ORDTIMA 5 2))
             CHGVAR     VAR(&ORDTIM2) VALUE(&WRK1 *CAT &WRK2 *CAT +
                          &WRK3)

MSG:         CHGVAR     VAR(&A) VALUE('ORDER NUMBER:' *BCAT &ORDERA)
             CHGVAR     VAR(&B) VALUE(' ORDER DATE:' *BCAT &ORDDAT2 +
                          *BCAT &ORDTIM2)
             CHGVAR     VAR(&C) VALUE(' REQUESTED DELIVERY:' *BCAT +
                          &REQDAT2)
             IF         COND(&CUSTPO *NE '               ') THEN(DO)
             CHGVAR     VAR(&D) VALUE('   P.O. NUMBER:' *BCAT &CUSTPO)
             ENDDO
             CHGVAR     VAR(&E) VALUE(' PRODUCT:' *BCAT &ITEMA *BCAT +
                          &ITEMD)

/*----------------------------------------------------------------------*/
/* ESNDMAIL                                                             */
/*----------------------------------------------------------------------*/
             ADDLIBLE   LIB(ESEND) POSITION(*LAST)
             MONMSG     MSGID(CPF0000)
             CHGVAR     VAR(&MSG) VALUE(&A +
                          *BCAT &B *BCAT &C *BCAT &E *BCAT &D)
             ESNDMAIL   RECIPIENT(&INTNET) SUBJECT('New Seaboard +
                          Order') MSG(&MSG)
/*----------------------------------------------------------------------*/
 END:        ENDPGM
