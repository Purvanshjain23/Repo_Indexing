      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:   HOG PRODUCTION
      * PROGRAM:  HP576
      * TITLE:    SELECT DISPOSED HOG GROUP
      * AUTHOR:   LEANNE FEDOR
      * CREATED:  10/12/94
      *
      * FUNCTION: THIS IS A SELECT FUNCTION WINDOW DISPLAY OF
      *           DISPOSED HOG GROUPS.
      *
      *         SELECTORS:    PRODUCTION PHASE
      *         POSITIONERS:  NONE
      *         RESTRICTORS:  BUSINESS OFFICE
      *                       PRODUCTION TYPE
      *                       FARM SITE
      *                       DISPOSED DATE
      *
      *         IT LOADS THE WINDOW SUBFILE A PAGE AT A TIME.
      *         IT DOES NOT USE THE STANDARD MESSAGE HANDLING PROGRAM.
      *         A RECORD IS SELECTED BY POSITIONING THE CURSOR
      *         ON THE LINE AND PRESSING ENTER.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 01/13/95  LEANNE FEDOR
      *           RECOMPILE ONLY. SEX ADDED TO HOG GROUP FILE.
      *
      * 03/13/95  LEANNE FEDOR
      *           RECOMPILE ONLY. ORIGIN BGF FARM SITE AND ORIGIN
      *           NURSERY FARM SITE ADDED TO HOG GROUP FILE.
      *
      * 04/21/95  LEANNE FEDOR
      *           CHANGED FARM SITE CODE FROM ALPHA TO NUMERIC
      *           ADDED PRODUCTION PHASE SELECTOR.
      *
      * 11/01/95  LEANNE FEDOR
      *           RECOMPILE ONLY. NEW FIELD 'GROUP TYPE' ADDED TO
      *           HOG GROUP FILE.
      *
      * 06/03/97  LEANNE FEDOR
      *           RECOMPILE ONLY. NEW FIELDS 'PURGE FLAG' AND 'DATE OF
      *           FIRST PURGE' ADDED TO HOG GROUP FILE.
      *
      * 06/26/01  LeAnne Fedor
      *           Recompile only.
      *           Manager and supervisor fields removed from Hog Group file.
      *
      * 07/06/09  LeAnne Ramsey
      *           Recompile only. Added new field 'Continuous Flow Flag' to Hog Group file.
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     FHPD576    CF   E             WORKSTN SFILE(HP576S1:HDRRN1)
     F                                     INFDS(IOFEED)
      *
      *
     FHSL034G   IF   E           K DISK    INFDS(DBFEED)
      *    HOG GROUP         (LF selects HGGSCd = DS)
      *
      ****************************************************************
      * INPUT SPECIFICATIONS
      ****************************************************************
      *
      *---------------------------------------------------------------
      * STANDARD PROGRAM STATUS DATA STRUCTURE
      *---------------------------------------------------------------
      *    EXTERNALLY DEFINED AS UTPGFR (RECORD FORMAT: PGMDSR)
     D PGMDS         ESDS                  EXTNAME(UTPGFR)
      *
      *---------------------------------------------------------------
      * STANDARD WORKSTATION INFORMATION DATA STRUCTURE
      *---------------------------------------------------------------
      *    EXTERNALLY DEFINED AS UTWSFR (RECORD FORMAT: UTIDFRR)
     D IOFEED        E DS                  EXTNAME(UTWSFR)
      *
      *---------------------------------------------------------------
      * STANDARD DATABASE FILE INFORMATION DATA STRUCTURE
      *---------------------------------------------------------------
      *    EXTERNALLY DEFINED AS UTDBFR (RECORD FORMAT: FDBCKD)
     D DBFEED        E DS                  EXTNAME(UTDBFR)
      *
      *---------------------------------------------------------------
      *  NAMED CONSTANTS
      *---------------------------------------------------------------
      *
     D CREATE          C                   CONST('CREATE')
     D REVISE          C                   CONST('REVISE')
     D DELETE          C                   CONST('DELETE')
     D VIEW            C                   CONST('VIEW  ')
     D SELECT          C                   CONST('SELECT')
     D YES             C                   CONST('Y')
     D NO              C                   CONST('N')
     D SET1            C                   CONST('SET1  ')
     D EDIT1           C                   CONST('EDIT1 ')
     D SCRN1           C                   CONST('SCRN1 ')
     D EXIT            C                   CONST('EXIT  ')
     D UPDATE          C                   CONST('UPDATE')
      *
      *---------------------------------------------------------------
      *  DATE FORMATS
      *---------------------------------------------------------------
      *
      * DATE IN MMDDYY FORMAT
      *
     D                 DS
     D  WKMDY                  1      6  0
     D  WKMM                   1      2  0
     D  WKDD                   3      4  0
     D  WKYY                   5      6  0
      *
      * DATE IN CCYYMMDD FORMAT
      *
     D                 DS
     D  WKCYMD                 1      8  0
     D  WKCEN                  1      2  0
     D  WKYER                  3      4  0
     D  WKMON                  5      6  0
     D  WKDAY                  7      8  0
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * MAIN LINE
      ****************************************************************
      *
     C                   MOVEL     SET1          ROUTNE
      *
     C     *INLR         DOWEQ     *OFF                                         MAIN DO LOOP
      *
     C     ROUTNE        CASEQ     SET1          $SET1
     C     ROUTNE        CASEQ     SCRN1         $SCRN1
     C     ROUTNE        CASEQ     EDIT1         $EDIT1
      *
     C     ROUTNE        CASEQ     EXIT          $EXIT
     C                   ENDCS
     C                   ENDDO                                                  MAIN DO LOOP
      *
      /EJECT
      ****************************************************************
      * SUBROUTINES
      ****************************************************************
      *
      *----------------------------------------------------------------
      * $SET1 - SUBROUTINE TO SET ENVIRONMENT FOR SCREEN 1
      *----------------------------------------------------------------
      *
     C     $SET1         BEGSR
      *
      *  CLEAR/FILL THE SUBFILE
      *
     C                   EXSR      $CLR1
     C                   EXSR      $FRSH1
      *
      *  SET ROUTINE TO DISPLAY SCREEN
      *
     C                   MOVEL     SCRN1         ROUTNE
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $SCRN1 - SUBROUTINE TO PERFORM OPERATOR I/O FOR SCREEN 1
      *----------------------------------------------------------------
      *
     C     $SCRN1        BEGSR
      *
     C                   WRITE     HP576K1
     C                   EXFMT     HP576C1
      *
      * SAVE THE VALUE FOR THE LOWEST SLF RRN ON THE SCREEN FOR
      * REPOSITIONING THE SUBFILE
      *
     C                   Z-ADD     IDFLOW        WKFLOW
      * RESET ERROR FLAG
     C                   Z-ADD     0             ERROR
      *
      * GET USER'S ENTRY AND RESET STANDARD RESPONSE
      *
     C                   SELECT
      *
     C     *IN12         WHENEQ    *ON                                          F12=PREVIOUS
     C                   MOVEL     EXIT          ROUTNE
      *
      * REPOSITION THE SUBFILE IF THE USER HAS CHANGED ANY OF THE
      * POSITIONERS OR SELECTORS IN THE CONTROL RECORD.
      *
     C     SVFSBO        WHENNE    C1FSBO                                       IF POSITION
     C     SVFSCD        ORNE      C1FSCD
     C     SVPPCD        ORNE      C1PPCD
     C     SVPTCD        ORNE      C1PTCD
     C     SVFMDY        ORNE      C1FMDY
     C     SVTMDY        ORNE      C1TMDY
     C                   MOVEL     NO            RESET
     C                   MOVEL     SET1          ROUTNE
     C                   MOVEL     C1FSBO        SVFSBO
     C                   Z-ADD     C1FSCD        SVFSCD
     C                   MOVEL     C1PPCD        SVPPCD
     C                   MOVEL     C1PTCD        SVPTCD
     C                   Z-ADD     C1FMDY        SVFMDY
     C                   Z-ADD     C1TMDY        SVTMDY
      *
     C     C1FMDY        IFEQ      0
     C                   Z-ADD     0             WKFYMD
     C                   ELSE
     C                   EXSR      $FYMD
     C                   ENDIF
      *
     C     C1TMDY        IFEQ      0
     C                   Z-ADD     0             WKTYMD
     C                   ELSE
     C                   EXSR      $TYMD
     C                   ENDIF
      *
     C     *IN25         WHENEQ    *ON                                          F25-ROLLUP
     C                   EXSR      $LOAD1
     C                   OTHER
      *
      * USER HAS PRESSED ENTER
      *
     C     NORECS        IFEQ      YES                                          IF EMPTY SFL
     C                   MOVEL     SET1          ROUTNE
     C                   ELSE
     C                   MOVEL     EDIT1         ROUTNE
     C                   ENDIF                                                  IF EMPTY SFL
      *
     C                   ENDSL
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $EDIT1 - SUBROUTINE TO EDIT INPUT FIELDS FOR SCREEN 1
      *----------------------------------------------------------------
      *
     C     $EDIT1        BEGSR
      *
      * IF THERE ARE NO ERRORS, PROCESS THE USER'S SELECTION
      *
     C     ERROR         IFEQ      0                                            IF NO ERROR
     C                   EXSR      $PROC1
      *
      * IF THE USER SELECTED A GROUP, THEN SET THE ROUTINE TO EXIT
      * THIS SELECTION PROGRAM.  IF NO GROUP WAS SELECTED, THEN
      * SET TO REDISPLAY THE SCREEN OF GROUPS.
      *
     C     XXHGCD        IFNE      *BLANKS
     C                   MOVEL     EXIT          ROUTNE
     C                   ELSE
     C                   MOVEL     SET1          ROUTNE
     C                   ENDIF
      *
      * ELSE THERE WERE ERRORS IN THE SUBFILE OPTIONS ENTERED, SO SET
      * TO REDISPLAY SCREEN AND ERRORS TO USER
      *
     C                   ELSE
     C                   MOVEL     SCRN1         ROUTNE
     C                   ENDIF                                                  IF NO ERROR
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * $CLR1  - SUBROUTINE TO CLEAR THE SUBFILE FOR SCREEN 1
      *---------------------------------------------------------------
      *
     C     $CLR1         BEGSR
      *
     C                   CLEAR                   HP576R1                        REF WINDOW
     C                   WRITE     HP576R1                                      REF WINDOW
      *
     C                   CLEAR                   HP576S1
     C                   SETON                                        83        SFLCLR
     C                   WRITE     HP576C1
     C                   SETOFF                                       818283
      *                                                    81=SFLDSP
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * $FRSH1 - SUBROUTINE TO REFRESH/FILL THE SUBFILE FOR SCREEN 1
      *---------------------------------------------------------------
      *
     C     $FRSH1        BEGSR
      *
      * SET POSITION IN FILE
      *
     C     K1L034        SETLL     HSL034G
      *
      * THE LOWEST SUBFILE RRN ON THE SCREEN FROM THE DSPF
      * FEEDBACK AREA (IDFLOW) WAS RETRIEVED EARLIER AND PUT IN WKFLOW.
      * ADD TO WKFLOW THE NUMBER OF LINES ON YOUR SUBFILE PAGE OR THE
      * NUMBER LESS 1 TO DETERMINE A SUBFILE RRN TO REPOSITION TO.
      * RESET IS A FLAG FIELD TO INDICATE WHETHER YOU ARE ATTEMPTING
      * TO REPOSITION THE USER CLOSE TO WHERE HE WAS IN THE SUBFILE.
      * IF RESET IS 'NO' DON'T REPOSITION THE USER--JUST START OVER.
      *
     C     RESET         IFEQ      YES
     C     WKFLOW        ADD       WKRLIN        MAXRRN
     C                   ELSE
     C                   Z-ADD     WKPLIN        MAXRRN
     C                   ENDIF
      *
      * LOAD THE SUBFILE UNTIL YOU HAVE THE REQUIRED NUMBER OF RECORDS.
      * THE NUMBER WILL DEPEND ON WHETHER YOU ARE REPOSITIONING THE
      * SUBFILE OR NOT. STOP IF YOU GET TO THE END OF THE SUBFILE.
      *
     C     HDRRN1        DOUGE     MAXRRN
     C     *IN82         OREQ      *ON
     C                   EXSR      $LOAD1
     C                   ENDDO
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * $LOAD1 - SUBROUTINE TO LOAD THE SUBFILE FOR SCREEN 1
      *---------------------------------------------------------------
      *
     C     $LOAD1        BEGSR
      *
      * SET THE COUNT OF RECORDS ON THE SUBFILE PAGE TO ZERO.
      *
     C                   Z-ADD     0             COUNT             2 0
      *
      * FILL THE SUBFILE UNTIL THE END OF THE DATABASE FILE OR YOU
      * HAVE FILLED ALL THE LINES ON THE SUBFILE PAGE
      *
     C                   SETOFF                                       82
     C     *IN82         DOUEQ     *ON                                          DO FILL LOOP
     C     COUNT         ORGE      WKPLIN
      *
      * READ GROUP MASTER FILE DEPENDING ON INPUT PARM VALUES
      *
     C                   SELECT
     C     C1FSBO        WHENNE    *BLANK
     C     C1FSCD        ANDNE     0
     C     K1L034        READE     HSL034G                                82
      *
     C     C1FSBO        WHENNE    *BLANK
     C     C1FSBO        READE     HSL034G                                82
     C                   OTHER
      *
     C                   READ      HSL034G                                82
     C                   ENDSL
      *
     C     *IN82         IFEQ      *OFF                                          IF READ
      *
      *   ONLY INCLUDE RECORDS THAT MATCH THE USER'S SELECTORS
      *
     C     C1FSBO        IFNE      *BLANK                                        IF B/O
     C     C1FSBO        ANDNE     HGFSBO
     C                   ELSE
      *
     C     C1PPCD        IFNE      *BLANK                                        IF PHASE
     C     C1PPCD        ANDNE     HGPPCD
     C                   ELSE
      *
     C     C1PTCD        IFNE      *BLANK                                        IF TYPE
     C     C1PTCD        ANDNE     HGPTCD
     C                   ELSE
      *
     C     C1FSCD        IFNE      0                                             IF FARM
     C     C1FSCD        ANDNE     HGFSCD
     C                   ELSE
      *
     C     WKFYMD        IFNE      0                                             IF FROM DT
     C     HGDSDT        ANDLT     WKFYMD
     C                   ELSE
      *
     C     WKTYMD        IFNE      0                                             IF TO DATE
     C     HGDSDT        ANDGT     WKTYMD
     C                   ELSE
      *
     C                   ADD       1             COUNT
     C                   ADD       1             HDRRN1
     C                   MOVEL     HGFSBO        DFFSBO
     C                   Z-ADD     HGFSCD        DFFSCD
     C                   MOVEL     HGPPCD        DFPPCD
     C                   MOVEL     HGPTCD        DFPTCD
     C                   MOVEL     HGHGCD        DFHGCD
      *
      * FLIP DISPOSE DATE FROM CCYYMMDD TO MMDDYY
      *
     C                   Z-ADD     HGDSDT        WKCYMD
     C                   EXSR      $TOMDY
     C                   Z-ADD     WKMDY         DFMDY
      *
     C                   WRITE     HP576S1
     C                   SETON                                        81
      *
      * FOR POSITIONING, PLACE THE SUBFILE RELATIVE RECORD NUMBER INTO
      * THE SFLRCDNBR FIELD (HDPAG1)
      *
     C                   Z-ADD     HDRRN1        HDPAG1
      *
     C                   ENDIF                                                  IF TO DATE
     C                   ENDIF                                                  IF FROM DT
     C                   ENDIF                                                  IF FARM
     C                   ENDIF                                                  IF TYPE
     C                   ENDIF                                                  IF PHASE
     C                   ENDIF                                                  IF B/O
      *
     C                   ENDIF                                                  IF READ
     C                   ENDDO                                                  DO FILL LOOP
      *
      * IF NO RECORDS WERE WRITTEN TO THE SUBFILE, THEN ISSUE
      * THE STANDARD MESSAGE 'NO DATA TO DISPLAY'.
      *
     C     HDRRN1        IFEQ      0
     C                   Z-ADD     1             ERROR
     C                   MOVEL     YES           NORECS
     C                   ELSE
     C                   MOVEL     NO            NORECS
     C                   ENDIF
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $TOMDY   FLIP DATE FROM YYMMDD TO MMDDYY
      *----------------------------------------------------------------
      *
     C     $TOMDY        BEGSR
      *
     C                   Z-ADD     WKMON         WKMM
     C                   Z-ADD     WKDAY         WKDD
     C                   Z-ADD     WKYER         WKYY
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * $PROC1 - SUBROUTINE TO READ SUBFILE RECORDS AND PROCESS
      *          USER SELECTIONS FOR SCREEN 1
      *---------------------------------------------------------------
      *
     C     $PROC1        BEGSR
      *
     C                   Z-ADD     0             ERROR
      *
      * IF CURSOR LOCATION RETURNED IS THE RRN OF A RCD IN THE SUBFILE,
      * CHAIN THE SUBFILE FOR THAT RECORD AND LOAD THE DATA INTO THE
      * PARM TO RETURN TO THE CALLING PROGRAM.
      *
     C                   SETOFF                                       82
     C     HDRRN         IFNE      0
     C                   Z-ADD     HDRRN         HDRRN1
     C     HDRRN1        CHAIN     HP576S1                            82
     C                   MOVEL     DFHGCD        XXHGCD
     C                   ENDIF
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * $FYMD - FLIP FROM DATE FROM MMDDYY TO CCYYMMDD
      *---------------------------------------------------------------
      *
     C     $FYMD         BEGSR
      *
     C                   Z-ADD     C1FMDY        PFRM8
     C                   EXSR      $DATE
     C     PRTRN         IFNE      *BLANK                                       IF BAD FROM
     C                   Z-ADD     0             WKFYMD
     C                   ELSE
     C                   Z-ADD     PTO8          WKFYMD
     C                   ENDIF                                                  IF BAD FROM
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * $TYMD - FLIP TO DATE FROM MMDDYY TO CCYYMMDD
      *---------------------------------------------------------------
      *
     C     $TYMD         BEGSR
      *
     C                   Z-ADD     C1TMDY        PFRM8
     C                   EXSR      $DATE
     C     PRTRN         IFNE      *BLANK                                       IF BAD TO
     C                   Z-ADD     0             WKTYMD
     C                   ELSE
     C                   Z-ADD     PTO8          WKTYMD
     C                   ENDIF                                                  IF BAD TO
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * $DATE -  MANIPULATE DATES WITH DATE UTILITY
      *---------------------------------------------------------------
      *
     C     $DATE         BEGSR
      *
     C                   Z-ADD     0             PTO8
     C                   MOVEL     'MDY '        PFRFMT
     C                   MOVEL     'CYMD'        PTOFMT
     C                   Z-ADD     0             PDAYS
     C                   MOVE      *BLANK        PCODE
     C                   MOVE      *BLANK        PRTRN
      *
     C                   CALL      'UT80060R'
     C                   PARM                    PFRM8             8 0
     C                   PARM                    PTO8              8 0
     C                   PARM                    PFRFMT            4
     C                   PARM                    PTOFMT            4
     C                   PARM                    PDAYS             4 0
     C                   PARM                    PCODE             1
     C                   PARM                    PRTRN             1
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * $EXIT - SUBROUTINE TO SET ON LR AND END JOB
      *---------------------------------------------------------------
      *
     C     $EXIT         BEGSR
     C                   SETON                                        LR
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * *INZSR - INITIALIZATION SUBROUTINE
      *---------------------------------------------------------------
      *
     C     *INZSR        BEGSR
      *
      * KEY FOR HSL034G-HOG GROUP
      *
     C     K1L034        KLIST
     C                   KFLD                    C1FSBO
     C                   KFLD                    C1FSCD
      *
      * PARM LISTS
      *
     C     *ENTRY        PLIST
     C     C1FSBO        PARM                    XXFSBO
     C     C1PTCD        PARM                    XXPTCD
     C     C1PPCD        PARM                    XXPPCD
     C     C1FSCD        PARM                    XXFSCD
     C     C1FMDY        PARM                    XXFMDY            6 0
     C     C1TMDY        PARM                    XXTMDY            6 0
     C                   PARM                    XXHGCD
      *
     C     *LIKE         DEFINE    HGFSBO        XXFSBO
     C     *LIKE         DEFINE    HGPTCD        XXPTCD
     C     *LIKE         DEFINE    HGPPCD        XXPPCD
     C     *LIKE         DEFINE    HGFSCD        XXFSCD
     C     *LIKE         DEFINE    HGHGCD        XXHGCD
     C     *LIKE         DEFINE    HGDSDT        WKFYMD
     C     *LIKE         DEFINE    HGDSDT        WKTYMD
      *
     C     *LIKE         DEFINE    C1FSBO        SVFSBO
     C                   MOVEL     C1FSBO        SVFSBO
     C     *LIKE         DEFINE    C1PTCD        SVPTCD
     C                   MOVEL     C1PTCD        SVPTCD
     C     *LIKE         DEFINE    C1PPCD        SVPPCD
     C                   MOVEL     C1PPCD        SVPPCD
     C     *LIKE         DEFINE    C1FSCD        SVFSCD
     C                   Z-ADD     C1FSCD        SVFSCD
     C     *LIKE         DEFINE    C1FMDY        SVFMDY
     C                   Z-ADD     C1FMDY        SVFMDY
     C     *LIKE         DEFINE    C1TMDY        SVTMDY
     C                   Z-ADD     C1TMDY        SVTMDY
      *
      * GLOBAL DEFINES
      *
     C                   MOVE      *BLANK        MODE              6            PARM
     C                   MOVE      *BLANK        PGM              10            USED IN MSG
     C                   MOVE      *BLANK        MSGFL            10            USED IN MSG
     C                   MOVE      *BLANK        ROUTNE            6            ROUTINE
     C                   Z-ADD     0             MAXRRN            4 0          REPOSITION
     C                   MOVEL     NO            RESET             1            REPOSITION
     C                   MOVEL     NO            NORECS            1            NO RECORDS
     C     *LIKE         DEFINE    IDFLOW        WKFLOW
     C                   Z-ADD     0             ERROR             3 0          ERRORS
      *
      * RETRIEVE THE CCYYMMDD FORMAT OF THE INCOMING FROM AND TO
      * DATE RANGE. IF EITHER DATE IS INVALID, SET IT TO ZERO.
      *
     C                   EXSR      $FYMD
     C                   EXSR      $TYMD
      *
      * TO SUPPORT F5-REFRESH, LOGIC REQUIRES THE NUMBER OF LINES ON
      * THE SUBFILE PAGE AND ALSO THE NUMBER OF LINES LESS 1.  BY
      * SETTING THESE VALUES UP AS CONSTANT WORK FIELDS, THE PROCESSING
      * LOGIC CAN BE STANDARDIZED BETWEEN PROGRAMS.
      *
     C                   Z-ADD     8             WKPLIN            2 0          SFLPAG(0008)
     C     WKPLIN        SUB       1             WKRLIN            2 0          SFLPAG - 1
      *
      *  DO ONE TIME ONLY:  MAKE SURE CALLING PROGRAM SCREEN IS NOT
      *  CLEARED AND WRITE THE WINDOW DEFINITION RCDFMT
      *
     C                   WRITE     HP576R2                                      DUMMY FMT
     C                   WRITE     HP576R1                                      REF WINDOW
      *
     C                   ENDSR
      /EJECT
