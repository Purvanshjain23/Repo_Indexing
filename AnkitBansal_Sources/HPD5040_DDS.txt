     A*%%TS  SD  20040804  144048  ISLRAMS     REL-V5R2M0  5722-WDS
     A*****************************************************************
     A*
     A*  SYSTEM:        HOG PRODUCTION
     A*  DISPLAY FILE:  HPD5040 - SELECT FARM SITE--ACTIVE/INACTIVE OPTIONS
     A*  CREATED BY:    LEANNE FEDOR
     A*  DATE:          02/05/04
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      PRINT
     A                                      CF04(04 'PROMPT')
     A                                      CA12(12 'PREVIOUS')
     A N82                                  ROLLUP(25 'PAGE FORWARD')
     A*---------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP5040S1                  SFL
     A*%%TS  SD  20040205  111311  ISLRAMS     REL-V5R2M0  5722-WDS
     A            HDRRN1    R        H      REFFLD(@@RRN)
     A            D1FSCD    R        O  8  1REFFLD(@@FSCD)
     A                                      EDTCDE(M)
     A            D1FSNM    R        O  8  9REFFLD(FSFSNM)
     A            D1PPCD    R        O  8 37REFFLD(HSRREF/@@PPCD *LIBL/HSPREF)
     A            D1PTCD    R        O  8 44REFFLD(HSRREF/@@PTCD *LIBL/HSPREF)
     A            D1TFCD    R        O  8 53REFFLD(HSRREF/@@TFCD *LIBL/HSPREF)
     A            D1CELL    R        O  8 63REFFLD(HSRREF/@@CELL *LIBL/HSPREF)
     A                                      EDTCDE(M)
     A            D1AIST    R        O  8 72REFFLD(@@AIST)
     A*
     A*---------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP5040C1                  SFLCTL(HP5040S1)
     A*%%TS  SD  20040205  111311  ISLRAMS     REL-V5R2M0  5722-WDS
     A                                      SFLSIZ(0100)
     A                                      SFLPAG(0009)
     A                                      WINDOW(HP5040EC)
     A                                      SETOF(51)
     A                                      SETOF(52)
     A                                      SETOF(53)
     A                                      SETOF(54)
     A                                      RTNCSRLOC(&RCD &FLD)
     A                                      OVERLAY
     A                                      SFLCSRRRN(&HDRRN)
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A            HDPAG1    R        H      REFFLD(@@PAG)
     A                                      SFLRCDNBR
     A            RCD           10A  H
     A            FLD           10A  H
     A            HDRRN     R        H      REFFLD(@@RRN)
     A            SDPGM     R        O  1  1REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 31'SELECT FARM SITE'
     A                                      DSPATR(HI)
     A            C1DESC    R        O  2 24REFFLD(@@DS30)
     A                                      DSPATR(HI)
     A                                  3  1'Position Cursor, Press Enter'
     A                                  4 39'?'
     A                                      DSPATR(HI)
     A                                  4 46'?'
     A                                      DSPATR(HI)
     A                                  4 57'?'
     A                                      DSPATR(HI)
     A N62                              4 72'?'
     A                                      DSPATR(HI)
     A                                  5  1'Farm'
     A                                      DSPATR(HI)
     A                                  5 37'-Production-'
     A                                      DSPATR(HI)
     A                                  5 52'Type of'
     A                                      DSPATR(HI)
     A                                  5 69'Active/'
     A                                      DSPATR(HI)
     A                                  6  1'Site'
     A                                      DSPATR(HI)
     A                                  6  9'Name'
     A                                      DSPATR(HI)
     A                                  6 37'Phase'
     A                                      DSPATR(HI)
     A                                  6 44'Type'
     A                                      DSPATR(HI)
     A                                  6 54'Farm'
     A                                      DSPATR(HI)
     A                                  6 62'Cell'
     A                                      DSPATR(HI)
     A                                  6 68'Inactive'
     A                                      DSPATR(HI)
     A            C1FSCD    R        B  7  1REFFLD(@@FSCD)
     A                                      DSPATR(HI)
     A            C1FSNM    R        B  7  9REFFLD(FSFSNM)
     A                                      CHGINPDFT(UL LC FE)
     A                                      DSPATR(HI)
     A            C1PPCD    R        B  7 37REFFLD(HSRREF/@@PPCD *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A  51                                  DSPATR(PC)
     A            C1PTCD    R        B  7 44REFFLD(HSRREF/@@PTCD *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A  52                                  DSPATR(PC)
     A            C1TFCD    R        B  7 53REFFLD(HSRREF/@@TFCD *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A  53                                  DSPATR(PC)
     A            C1CELL    R        B  7 63REFFLD(HSRREF/@@CELL *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A                                      EDTCDE(M)
     A N62        C1AIST    R        B  7 72REFFLD(HSRREF/@@AIST *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A  54                                  DSPATR(PC)
     A*
     A*---------------------------------------------------------------
     A* COMMAND LINE LINE
     A*---------------------------------------------------------------
     A          R HP5040K1
     A                                      WINDOW(HP5040EC)
     A                                      OVERLAY
     A  43                             17  1'Message program failed. Call IS.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 18  1'F4=Prompt'
     A                                 18 12'F12=Previous'
      *
     A*-------------------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
     A*-------------------------------------------------------------------------
     A          R HP5040ES                  SFL SFLMSGRCD(19)
      *
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
     A*
     A*-------------------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*-------------------------------------------------------------------------
     A*
     A          R HP5040EC                  SFLCTL(HP5040ES)
     A*%%TS  SD  19980327  105628  ISLRAMS     REL-V3R7M0  5716-PW1
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A                                      WINDOW(*DFT 20 75)
     A                                      WDWBORDER((*DSPATR RI) (*CHAR '    -
     A                                          '))
     A            MSGPGM                    SFLPGMQ(10)
     A*
     A*---------------------------------------------------------------
     A* ASSUME FORMAT -- REQUIRED FOR WINDOWS
     A*---------------------------------------------------------------
     A          R ASSUME
     A*%%TS  SD  20011025  160559  ISLRAMS     REL-V4R4M0  5769-PW1
     A                                      ASSUME
     A                                      OVERLAY
     A                                 24 79' '
