/***************************************************************** */
/*  PROGRAM NUMBER --> HP459CL                                     */
/*  PROGRAM NAME ----> INVENTORY ADJUSTMENT REPORTS                */
/*  PROGRAMMER ------> LEANNE FEDOR                                */
/*  DATE ------------> 04/07/95                                    */
/*                                                                 */
/*  PROGRAM PURPOSE -> THIS CL CALLS THE PROGRAM TO PRINT ONE      */
/*                     OF THE INVENTORY ADJUSTMENT REPORTS         */
/*                     VALUES ARE PASSED THRU THE LDA FROM         */
/*                                                                 */
/***************************************************************** */
/*  MODIFIED:                                                      */
/*                                                                 */
/*  11/01/95  LEANNE FEDOR                                         */
/*            RECOMPILE ONLY. NEW FIELD 'GROUP TYPE' ADDED TO      */
/*            HOG GROUP FILE.                                      */
/*                                                                 */
/* 04/23/96  LEANNE FEDOR                                          */
/*           ADDED CUSTOMIZED PRINTER OVERRIDE COMMAND TO FORCE    */
/*           REPORT TO PRINT ON LASER PRINTER.                     */
/*                                                                 */
/* 06/03/97 LEANNE FEDOR                                           */
/*          RECOMPILE ONLY.  NEW FIELDS 'PURGE FLAG' AND 'DATE OF  */
/*          FIRST PURGE' ADDED TO HOG GROUP FILE.                  */
/***************************************************************** */

             PGM

/* DEFINE THE VARIABLE REQUIRED IN THE COMMAND TO OVERRIDE THE     */
/* PRINT FILE TO A LASER PRINTER                                   */

             DCL        VAR(&CMD) TYPE(*CHAR) LEN(512)

             DCL        VAR(&QDATA) TYPE(*CHAR) LEN(269)

             DCL        VAR(&LDFSBO) TYPE(*CHAR) LEN(5)
             DCL        VAR(&LDFYMD) TYPE(*DEC) LEN(8)
             DCL        VAR(&LDTYMD) TYPE(*DEC) LEN(8)
             DCL        VAR(&LDPPCD) TYPE(*CHAR) LEN(5)
             DCL        VAR(&LDFSCD) TYPE(*DEC) LEN(5)
             DCL        VAR(&LDPTCD) TYPE(*CHAR) LEN(5)
             DCL        VAR(&LDHGSN) TYPE(*DEC) LEN(7)
             DCL        VAR(&LDRPFL) TYPE(*CHAR) LEN(1)

             DCL        VAR(&HOLD) TYPE(*CHAR) LEN(10)
             DCL        VAR(&COPYS) TYPE(*DEC) LEN(2)
             DCL        VAR(&OUTQ) TYPE(*CHAR) LEN(10)

             CHGVAR     VAR(&LDFSBO) VALUE(%SST(*LDA 1 5))
             CHGVAR     VAR(&LDFYMD) VALUE(%SST(*LDA 6 8))
             CHGVAR     VAR(&LDTYMD) VALUE(%SST(*LDA 14 8))
             CHGVAR     VAR(&LDPPCD) VALUE(%SST(*LDA 22 5))
             CHGVAR     VAR(&LDFSCD) VALUE(%SST(*LDA 27 5))
             CHGVAR     VAR(&LDPTCD) VALUE(%SST(*LDA 48 5))
             CHGVAR     VAR(&LDHGSN) VALUE(%SST(*LDA 53 7))
             CHGVAR     VAR(&LDRPFL) VALUE(%SST(*LDA 174 1))

/*  SELECT ALL RECORDS WITHIN THE DATE RANGE                         */

             CHGVAR     VAR(%SST(&QDATA 1 10)) VALUE('IAIADT *GE')
             CHGVAR     VAR(%SST(&QDATA 12 8)) VALUE(&LDFYMD)
             CHGVAR     VAR(%SST(&QDATA 21 15)) VALUE('*AND IAIADT +
                          *LE')
             CHGVAR     VAR(%SST(&QDATA 37 8)) VALUE(&LDTYMD)

/*  IF BUSINESS OFFICE                                       */

             IF         COND(&LDFSBO *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 46 17)) VALUE('*AND IAFSBO +
                          *EQ "')
             CHGVAR     VAR(%SST(&QDATA 63 5)) VALUE(&LDFSBO)
             CHGVAR     VAR(%SST(&QDATA 68 1)) VALUE('"')
             ENDDO

/*  IF PRODUCTION TYPE                                          */

             IF         COND(&LDPTCD *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 164 17)) VALUE('*AND HGPTCD +
                          *EQ "')
             CHGVAR     VAR(%SST(&QDATA 181 5)) VALUE(&LDPTCD)
             CHGVAR     VAR(%SST(&QDATA 186 1)) VALUE('"')
             ENDDO

/*  IF PRODUCTION PHASE                                          */

             IF         COND(&LDPPCD *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 188 17)) VALUE('*AND HGPPCD +
                          *EQ "')
             CHGVAR     VAR(%SST(&QDATA 205 5)) VALUE(&LDPPCD)
             CHGVAR     VAR(%SST(&QDATA 210 1)) VALUE('"')
             ENDDO

/*  IF FARM SITE                                                 */

             IF         COND(&LDFSCD *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 212 15)) VALUE('*AND IAFSCD +
                          *EQ')
             CHGVAR     VAR(%SST(&QDATA 228 5)) VALUE(&LDFSCD)
             ENDDO

/*  IF HOG GROUP                                                */

             IF         COND(&LDHGSN *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 237 17)) VALUE('*AND IAHGSN +
                          *EQ ')
             CHGVAR     VAR(%SST(&QDATA 254 7)) VALUE(&LDHGSN)
             CHGVAR     VAR(%SST(&QDATA 261 1)) VALUE(' ')
             ENDDO

/*------------------------------------------------------------------*/
/* SET UP PRINT PARMS                                               */
/*------------------------------------------------------------------*/

 HOLD:       IF         COND(%SST(*LDA 411 1) = 'Y') THEN(CHGVAR +
                          VAR(&HOLD) VALUE(*YES))
             ELSE       CMD(CHGVAR VAR(&HOLD) VALUE(*NO))

 COPYS:      CHGVAR     VAR(&COPYS) VALUE(%SST(*LDA 412 1))
             IF         COND(&COPYS = 0) THEN(CHGVAR VAR(&COPYS) +
                          VALUE(1))

 OUTQ:       CHGVAR     VAR(&OUTQ) VALUE(%SST(*LDA 413 10))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(%SST(*LDA 401 10)))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(*JOB))

/*------------------------------------------------------------------*/
/* OVERRIDE PRINT FILE                                              */
/*------------------------------------------------------------------*/

/* USE THE CUSTOMIZED SEABOARD COMMAND TO OVERRIDE THE PRINT FILE.  */
/* THIS COMMAND ALLOWS THE REPORT TO BE GENERATED ON A STANDARD LINE*/
/* PRINTER OR A LASER PRINTER. TO USE THIS OVERRIDE YOU MUST DECLARE*/
/* A COMMAND VARIABLE AND FOLLOW THE OVERRIDE WITH A CALL TO        */
/* QCMDEXC.                                                         */

/* THIS REPORT IS STANDARD 10 CPI WITH 80 COLUMNS OF PRINT          */

             SBDPRTOVR  CMDVAR(&CMD) FILE(QPRINT) OUTQ(&OUTQ) +
                          WIDTH(80) COPIES(&COPYS) HOLD(&HOLD)

             CALL       PGM(QCMDEXC) PARM(&CMD 512)


/*------------------------------------------------------------------*/
/* RUN REPORTS                                                      */
/*------------------------------------------------------------------*/

/* INVENTORY ADJUSTMENTS BY DATE REPORT */

             IF         COND(&LDRPFL *EQ 'D') THEN(DO)
             OVRDBF     FILE(HSJ069B) SHARE(*YES)
             OPNQRYF    FILE((HSJ069B)) QRYSLT(&QDATA) +
                          KEYFLD((HGPTCD) (HGPPCD) (IAIADT) +
                          (IAFSCD) (HGHGCD)) SEQONLY(*YES)
             CALL PGM(HP659)
             ENDDO

/* INVENTORY ADJUSTMENTS BY FARM REPORT */

             IF         COND(&LDRPFL *EQ 'F') THEN(DO)
             OVRDBF     FILE(HSJ069B) SHARE(*YES)
             OPNQRYF    FILE((HSJ069B)) QRYSLT(&QDATA) +
                          KEYFLD((HGPTCD) (HGPPCD) (IAFSCD) +
                          (HGHGCD) (IAIADT *DESCEND)) SEQONLY(*YES)
             CALL PGM(HP658)
             ENDDO

             CLOF       OPNID(HSJ069B)
             DLTOVR     FILE(*ALL)

             ENDPGM

