      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      HOG PRODUCTION
      * PROGRAM:     HP633
      * TITLE:       LISTING OF IOWA TOP MARKET PRICES
      * PROGRAMMER:  LEANNE FEDOR
      * CREATED:     08/24/94
      *
      * FUNCTION: BATCH PROGRAM SUBMITTED FROM SPECIFY IOWA TOP MARKET
      *           PRICE LISTING OPTIONS (HP489) WHICH IS CALLED FROM
      *           HP433-WORK WITH IOWA TOP MARKET PRICES.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      * 99/99/99  NAME
      *
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     FHSL027A   IF   E           K DISK
      *    IOWA TOP MARKET PRICES    (KEY: ITEFDT--DESCEND)
      *
     FQPRINT    O    F  132        PRINTER OFLIND(*INOF)
      *
      /EJECT
      ****************************************************************
      * INPUT SPECS
      ****************************************************************
      *
      *---------------------------------------------------------------
      *  LDA -  WITH THE REQUESTED PRINT SELECTIONS
      *---------------------------------------------------------------
      *
     D                UDS
     D  LDFMDY                 1     06  0
     D  LDTMDY                 7     12  0
     D  LDFCYM                13     20  0
     D  LDTCYM                21     28  0
      *
      * DATE IN MMDDYY FORMAT
      *
     D                 DS
     D  WKMDY                  1      6  0
     D  WKMM                   1      2  0
     D  WKDD                   3      4  0
     D  WKYY                   5      6  0
      *
      * DATE IN CCYYMMDD FORMAT
      *
     D                 DS
     D  WKCYMD                 1      8  0
     D  WKCEN                  1      2  0
     D  WKYER                  3      4  0
     D  WKMON                  5      6  0
     D  WKDAY                  7      8  0
      *
      *---------------------------------------------------------------
      * STANDARD PROGRAM STATUS DATA STRUCTURE
      *---------------------------------------------------------------
      *    EXTERNALLY DEFINED AS UTPGFR (RECORD FORMAT: PGMDSR)
     D PGMDS         ESDS                  EXTNAME(UTPGFR)
      *
      *---------------------------------------------------------------
      *  NAMED CONSTANTS
      *---------------------------------------------------------------
      *
     D YES             C                   CONST('Y')
     D NO              C                   CONST('N')
      *
      /EJECT
      ****************************************************************
      * MAIN LINE
      ****************************************************************
      *
      * PRINT HEADINGS FOR FIRST PAGE AND THEN
      * READ/PROCESS ALL RECORDS IN DATE RANGE
      *
     C     LDFCYM        SETLL     HSL027A
     C                   EXCEPT    HRHDR
      *
     C     *IN92         DOUEQ     *ON                                          MAIN DO
     C                   READ      HSL027A                                92
     C     *IN92         IFEQ      *OFF                                         IF NOT EOF
     C     ITEFDT        ANDLE     LDTCYM
      *
      * TEST FOR OVERFLOW AND PRINT HEADINGS IF ON
      *
     C     *INOF         IFEQ      *ON
     C                   EXCEPT    HRHDR
     C                   ENDIF
      *
      *  FLIP EFFECTIVE DATE TO MMDDYY FROM CCYYMMDD
      *
     C                   Z-ADD     ITEFDT        WKCYMD
     C                   Z-ADD     WKYER         WKYY
     C                   Z-ADD     WKMON         WKMM
     C                   Z-ADD     WKDAY         WKDD
      * PRINT DETAIL LINE
      *
     C                   EXCEPT    DTLINE
     C                   MOVE      YES           PRTFLG
      *
     C                   ENDIF                                                   IF NOT EOF
     C                   ENDDO                                                  MAIN DO
      *
      * EOF PROCESSING
      * PRINT STANDARD 'NO DATA...' ERROR MESSAGE IF NO DATA MET THE
      * USER'S SELECTION CRITERIA
      *
     C     PRTFLG        IFEQ      NO
     C                   EXCEPT    NODATA
     C                   ENDIF
      *
     C                   SETON                                        LR
      /EJECT
      ****************************************************************
      * SUBROUTINES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * *INZSR - INITIALIZATION SUBROUTINE
      *---------------------------------------------------------------
      *
     C     *INZSR        BEGSR
      *
      * SET PRINT FLAG TO DETERMINE IF ANY RECORDS WERE PRINTED
      * SET UP DASHED LINE THAT APPEARS UNDER HEADINGS
      * RETRIEVE TIME FOR REPORT HEADING
      *
     C                   MOVE      NO            PRTFLG            1
     C                   MOVE      *ALL'-'       DASH             80
     C                   TIME                    RTIME             6 0
      *
     C                   ENDSR
      /EJECT
      *-------------------------------------------------------------
      * REPORT HEADING LINES
      *-------------------------------------------------------------
      *
     OQPRINT    E            HRHDR          1 01
     O                       SDPGM               10
     O                                           49 'HOG PRODUCTION SYSTEM'
     O                                           70 'DATE'
     O                       UDATE         Y     80
      *
     O          E            HRHDR          1
     O                       SDUSR               10
     O                                           45 'LISTING OF IOWA TOP MARK'
     O                                           56 'ET PRICES  '
     O                                           70 'TIME'
     O                       RTIME               80 '  :  :  '
      *
     O          E            HRHDR          1
     O                                           70 'PAGE'
     O                       PAGE          Z     75
      *
     O          E            HRHDR          1
     O                                           20 'FROM EFFECTIVE DATE:'
     O                       LDFMDY        Y     30
      *
     O          E            HRHDR          2
     O                                           20 'TO EFFECTIVE DATE:'
     O                       LDTMDY        Y     30
      *
     O          E            HRHDR          1
     O                                           14 'EFFECTIVE DATE'
     O                                           38 'MARKET PRICE PER CWT'
      *
     O          E            HRHDR          1
     O                       DASH                80
      *
      *-------------------------------------------------------------
      * DETAIL LINE
      *-------------------------------------------------------------
      *
     O          E            DTLINE      1
     O                       WKMDY         Y     11
     O                       ITITRT        K     31
      *
      *-------------------------------------------------------------
      * NO DATA MESSAGE LINE
      *-------------------------------------------------------------
      *
     O          E            NODATA      1
     O                                           19 'NO DATA MEETS YOUR'
     O                                           38 'SELECTION CRITERIA'
