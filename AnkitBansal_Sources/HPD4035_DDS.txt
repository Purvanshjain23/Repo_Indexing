     A*%%TS  SD  20081222  162427  ISLRAMS     REL-V5R4M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD4035
     A*  TITLE:         SPECIFY OPTIONS FOR HOG DEFECT DOWNLOAD
     A*  PROGRAMMER:    LEANNE RAMSEY
     A*  CREATED:       12/22/08
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A                                      CF04(04 'PROMPT')
     A                                      CF08(08 'SUBMIT')
     A                                      CF20(20 'WRKSPLF')
     A*
     A*---------------------------------------------------------------
     A* RECORD FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP4035R1
     A*%%TS  SD  20081222  162427  ISLRAMS     REL-V5R4M0  5722-WDS
     A                                      SETOF(33)
     A                                      SETOF(34)
     A                                      SETOF(35)
     A                                      SETOF(36)
     A                                      SETOF(37)
     A                                      SETOF(38)
     A                                      SETOF(54)
     A                                      SETOF(55)
     A                                      OVERLAY
     A                                      RTNCSRLOC(&RCD &FLD)
     A            RCD       R        H      REFFLD(@@RCD)
     A            FLD       R        H      REFFLD(@@FLD)
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 25'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USR)
     A                                      DSPATR(HI)
     A                                  2 16'SPECIFY OPTIONS FOR HOG DEFECT DOW-
     A                                      NLOAD'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@JOB)
     A                                      DSPATR(HI)
     A                                  5  2'From kill date ..'
     A            D1FMDY    R     Y  B  5 22REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A N37                                  DSPATR(HI)
     A  37                                  DSPATR(PC)
     A  37                                  DSPATR(RI)
     A                                  6  2'To kill date ....'
     A            D1TMDY    R     Y  B  6 22REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A N36                                  DSPATR(HI)
     A  36                                  DSPATR(PC)
     A  36                                  DSPATR(RI)
     A                                  8  2'E-mail address ..'
     A            D1EMAIL   R        B  8 22REFFLD(TFREF/@@EMAIL *LIBL/TFPREF)
     A                                      CHGINPDFT(UL LC FE)
     A N38                                  DSPATR(HI)
     A  38                                  DSPATR(PC)
     A  38                                  DSPATR(RI)
     A                                 10  2'Farm site .......'
     A                                 10 20'?'
     A            D1FSCD    R        B 10 22REFFLD(HSRREF/@@FSCD *LIBL/HSPREF)
     A                                      EDTCDE(M)
     A N34                                  DSPATR(HI)
     A  34
     AO 54                                  DSPATR(PC)
     A  34                                  DSPATR(RI)
     A            D1FSNM    R        O 10 30REFFLD(HSRREF/FSFSNM *LIBL/HSPREF)
     A                                 10 57'(Blank for All)'
     A                                 11  2'Cell ............'
     A            D1CELL    R        B 11 22REFFLD(HSRREF/@@CELL *LIBL/HSPREF)
     A                                      EDTCDE(M)
     A N33                                  DSPATR(HI)
     A  33                                  DSPATR(PC)
     A  33                                  DSPATR(RI)
     A                                 11 57'(Blank for All)'
     A                                 12  2'Type of farm ....'
     A                                 12 20'?'
     A            D1TFCD    R        B 12 22REFFLD(HSRREF/@@TFCD *LIBL/HSPREF)
     A N35                                  DSPATR(HI)
     A  35
     AO 55                                  DSPATR(PC)
     A  35                                  DSPATR(RI)
     A            D1TFDS    R        O 12 30REFFLD(HSRREF/@@TFDS *LIBL/HSPREF)
     A                                 12 57'(Blank for All)'
     A*
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R HP4035K1
     A*%%TS  SD  20081219  154707  ISLRAMS     REL-V5R4M0  5722-WDS
     A  43                             21  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A                                 23 11'F4=Prompt'
     A                                 23 22'F8=Submit'
     A                                 23 60'F20=Spool Files'
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *---------------------------------------------------------------
     A          R HP4035ES                  SFL SFLMSGRCD(24)
      *
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
     A*---------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*---------------------------------------------------------------
     A*
     A          R HP4035EC                  SFLCTL(HP4035ES)
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
