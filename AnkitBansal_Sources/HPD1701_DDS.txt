     A*%%TS  SD  20050216  094602  ISLRAMS     REL-V5R2M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD1701
     A*  TITLE:         MAINTAIN BUDGET ITEMS FOR TEMPLATE
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  CREATED:       02/03/04
     A*
     A*  INDICATOR 60 IS ON WHEN IN REVISE MODE
     A*  INDICATOR 61 IS ON WHEN IN CREATE MODE
     A*
     A*  INDICATOR 63 IS ON WHEN THE SUBFILE FIELDS ARE NON-DISPLAY
     A*               BECAUSE THE RECORD WAS SUCCESSFULLY DELETED
     A*               (THE HIDDEN SUBFILE FIELD HDNDFL = YES)
     A*
     A*  INDICATOR 64 IS ON WHEN A SUBFILE RECORD FIELDS MUST BE PROTECTED.
     A*               (THE HIDDEN SUBFILE FIELD HDPFL = YES). FIELDS ARE
     A*               PROTECTED IF:
     A*               1) THE SUBFILE RECORD WAS SUCCESSFULLY CREATED
     A*
     A*  INDICATOR 90 IS ON WHEN THE TEMPLATE THE USER SELECTED
     A*               FROM THE WORK WITH HAS BEEN DELETED BY ANOTHER USER
     A**************************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A N90                                  CF04(04 'PROMPT')
     A N90                                  CF05(05 'REFRESH')
     A N90                                  CF10(10 'CHANGE MODES')
     A*
     A*---------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP1701S1                  SFL
     A*%%TS  SD  20040726  163133  ISLRAMS     REL-V5R2M0  5722-WDS
     A  85                                  SFLNXTCHG
     A            HDRRN1         4Y 0H
     A            HDPFL     R        H      REFFLD(@@FLAG)
     A            HDNDFL    R        H      REFFLD(@@FLAG)
     A            D1OPT          2A  B 11  2CHGINPDFT(UL)
     A  61
     AO 64
     AO 90                                  DSPATR(PR)
     A  61
     AO 64
     AO 90                                  DSPATR(ND)
     A  30                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A N30N61N64
     A N90                                  DSPATR(HI)
     A  60N30N64
     A N90                                  DSPATR(UL)
     A            D1BGIT    R        B 11  5REFFLD(HSRREF/@@BGIT *LIBL/HSPREF)
     A  60
     AO 64
     AO 90                                  DSPATR(PR)
     A  63                                  DSPATR(ND)
     A  33
     AO 53                                  DSPATR(PC)
     A  33                                  DSPATR(RI)
     A N33N60N64
     A N90                                  DSPATR(HI)
     A N33N60N64
     A N90                                  DSPATR(UL)
     A            D1DESC        37A  O 11 31
     A            D1ITTYCD  R        O 11 74REFFLD(HSRREF/@@ITTYCD *LIBL/HSPREF)
     A  63                                  DSPATR(ND)
     A                                 12  5'quantity:'
     A                                      COLOR(PNK)
     A            D1BTQT    R        B 12 15REFFLD(HSRREF/@@BTQT *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A  64
     AO 90                                  DSPATR(PR)
     A  63                                  DSPATR(ND)
     A  34                                  DSPATR(PC)
     A  34                                  DSPATR(RI)
     A N34N64N90                            DSPATR(HI)
     A N34N64N90                            DSPATR(UL)
     A                                 12 49'uom:'
     A                                      DSPATR(HI)
     A                                      COLOR(PNK)
     A            D1UOM     R        O 12 54REFFLD(HSRREF/@@UOM *LIBL/HSPREF)
     A  63                                  DSPATR(ND)
     A                                 12 64'uow:'
     A                                      DSPATR(HI)
     A                                      COLOR(PNK)
     A            D1UOW     R        O 12 69REFFLD(HSRREF/@@UOW *LIBL/HSPREF)
     A  63                                  DSPATR(ND)
     A*
     A*---------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*---------------------------------------------------------------
     A*
     A          R HP1701C1                  SFLCTL(HP1701S1)
     A*%%TS  SD  20050216  094602  ISLRAMS     REL-V5R2M0  5722-WDS
     A                                      SFLSIZ(0050)
     A                                      SFLPAG(0005)
     A                                      SETOF(30)
     A                                      SETOF(33)
     A                                      SETOF(34)
     A                                      SETOF(50)
     A                                      SETOF(53)
     A                                      SETOF(54)
     A                                      SETOF(63)
     A N82N90                               ROLLUP(25 'PAGE FORWARD')
     A                                      SFLCSRRRN(&HDRR1)
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND(*MORE)
     A                                      OVERLAY
     A                                      RTNCSRLOC(&RCD &FLD)
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            HDRR1          5S 0H
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 25'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A            C1MODE         6A  O  2  2DSPATR(HI)
     A                                  2 19'MAINTAIN BUDGET ITEMS FOR TEMPLATE'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  4  2'Template code:'
     A            C1BTCD    R        O  4 18REFFLD(HSRREF/@@BTCD *LIBL/HSPREF)
     A            C1BTDS    R        O  4 26REFFLD(HSRREF/@@BTDS *LIBL/HSPREF)
     A N61N90                           6  2'4=Delete'
     A                                  6 12'8=Update/add the Item on all Activ-
     A                                      e Farm Budgets linked to Template'
     A N90                              7 10'?'
     A                                      DSPATR(HI)
     A N90N61                           7 76'?'
     A                                      DSPATR(HI)
     A                                  8 74'Item'
     A                                      DSPATR(HI)
     A                                  9  2'Op'
     A                                      DSPATR(HI)
     A                                  9  5'Budget Item'
     A                                      DSPATR(HI)
     A                                  9 31'Description'
     A                                      DSPATR(HI)
     A                                  9 74'Type'
     A                                      DSPATR(HI)
     A N90        C1BGIT    R        B 10  5REFFLD(HSRREF/@@BGIT *LIBL/HSPREF)
     A  50                                  DSPATR(PC)
     A                                      DSPATR(HI)
     A  61                                  DSPATR(ND)
     A  61                                  DSPATR(PR)
     A N90        C1DESC        37A  B 10 31DSPATR(HI)
     A  61                                  DSPATR(ND)
     A  61                                  DSPATR(PR)
     A N90        C1ITTYCD  R        B 10 74REFFLD(HSRREF/@@ITTYCD *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A  61                                  DSPATR(ND)
     A  61                                  DSPATR(PR)
     A  54                                  DSPATR(PC)
     A*
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R HP1701K1
     A*%%TS  SD  20040331  145519  ISLRAMS     REL-V5R2M0  5722-WDS
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A                                      DSPATR(HI)
     A                                 23  2'F3=Exit'
     A N90                             23 11'F4=Prompt'
     A N90                             23 22'F5=Refresh'
     A  61N90                          23 34'F10=Go to Revise'
     A  60N90                          23 34'F10=Go to Create'
     A*
     A*---------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE FORMAT
     A*---------------------------------------------------------------
     A          R HP1701ES                  SFL SFLMSGRCD(24)
     A*
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
     A*
     A*---------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*---------------------------------------------------------------
     A          R HP1701EC                  SFLCTL(HP1701ES)
      *
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
