     A*%%TS  SD  19950228  081159  ISLRAMS     REL-V3R0M5  5763-PW1
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION
     A*  DISPLAY FILE:  HPD125
     A*  TITLE:         REVISE FUNCTION
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  DATE:          02/10/95
     A*
     A*  INDICATOR 60 IS ON WHEN IN REVISE MODE
     A*  INDICATOR 61 IS ON WHEN IN CREATE MODE
     A*  INDICATOR 63 IS ON WHEN IN DELETE MODE
     A*  INDICATOR 90 IS ON IF THE RECORD SELECTED BY THE USER HAS BEEN
     A*               DELETED BY ANOTHER USER SINCE SELECTED.
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A N90N63                               CF04(04 'PROMPT')
     A N90N63                               CF05(05 'REFRESH')
     A N90N61                               CF11(11 'DELETE')
     A                                      CA12(12 'PREVIOUS')
     A*
     A*--------------------------------------------------------------*
     A* RECORD FORMAT ONE
     A*--------------------------------------------------------------*
     A          R HP125R1
     A*%%TS  SD  19950228  081159  ISLRAMS     REL-V3R0M5  5763-PW1
     A                                      SETOF(31)
     A                                      SETOF(32)
     A                                      SETOF(33)
     A                                      SETOF(34)
     A                                      SETOF(35)
     A                                      SETOF(36)
     A                                      SETOF(37)
     A                                      SETOF(38)
     A                                      SETOF(53)
     A                                      SETOF(54)
     A                                      SETOF(55)
     A                                      SETOF(57)
     A                                      SETOF(58)
     A                                      OVERLAY
     A                                      RTNCSRLOC(&RCD &FLD)
     A            RCD           10A  H
     A            FLD           10A  H
     A                                  1  2'HP125'
     A                                      DSPATR(HI)
     A                                  1 29'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A            DFMODE    R        O  2  2REFFLD(@@MODE)
     A                                      DSPATR(HI)
     A                                  2 32'REVISE FUNCTION'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  4  2'Function code .............'
     A            DFFNCD    R        B  4 32REFFLD(FNFNCD)
     A  60
     AO 63
     AO 90                                  DSPATR(PR)
     A  31                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A N31N60N63
     A N90 42                               DSPATR(HI)
     A N31N60N63
     A N90 42                               DSPATR(UL)
     A                                  5  2'Description ...............'
     A            DFFNDS    R        B  5 32REFFLD(FNFNDS)
     A  63
     AO 90                                  DSPATR(PR)
     A  32                                  DSPATR(PC)
     A  32                                  DSPATR(RI)
     A N32N63N90
     A  42                                  DSPATR(HI)
     A N32N63N90
     A  42                                  DSPATR(UL)
     A                                  6  2'Secured function flag .....'
     A N63N90 42                        6 30'?'
     A            DFSFFL    R        B  6 32REFFLD(FNSFFL)
     A  63
     AO 90                                  DSPATR(PR)
     A  38
     AO 58                                  DSPATR(PC)
     A  38                                  DSPATR(RI)
     A N38N63N90
     A  42                                  DSPATR(HI)
     A N38N63N90
     A  42                                  DSPATR(UL)
     A                                  8  2'The following three fields must be-
     A                                       blank when the function is not sec-
     A                                      ured:'
     A                                      DSPATR(HI)
     A                                 10  2'Function type .............'
     A                                 10 30'?'
     A            DFFNTY    R        B 10 32REFFLD(FNFNTY)
     A  63
     AO 90                                  DSPATR(PR)
     A  33
     AO 53                                  DSPATR(PC)
     A  33                                  DSPATR(RI)
     A N33N63N90
     A  42                                  DSPATR(HI)
     A N33N63N90
     A  42                                  DSPATR(UL)
     A                                 11  2'Business office required ..'
     A N63N90 42                       11 30'?'
     A            DFBOFL    R        B 11 32REFFLD(FNBOFL)
     A  63
     AO 90                                  DSPATR(PR)
     A  34
     AO 54                                  DSPATR(PC)
     A  34                                  DSPATR(RI)
     A N34N63N90
     A  42                                  DSPATR(HI)
     A N34N63N90
     A  42                                  DSPATR(UL)
     A                                 12  2'Function division code ....'
     A N63N90 42                       12 30'?'
     A            DFDVCD    R        B 12 32REFFLD(FNDVCD)
     A  63
     AO 90                                  DSPATR(PR)
     A  35
     AO 55                                  DSPATR(PC)
     A  35                                  DSPATR(RI)
     A N35N63N90
     A  42                                  DSPATR(HI)
     A N35N63N90
     A  42                                  DSPATR(UL)
     A                                 14  2'The following fields are used to r-
     A                                      etrieve defaults for printed output-
     A                                      :'
     A                                      DSPATR(HI)
     A                                 16  2'Number of copies ..........'
     A            DFCOPY    R        B 16 32REFFLD(FNCOPY)
     A  63
     AO 90                                  DSPATR(PR)
     A  36                                  DSPATR(PC)
     A  36                                  DSPATR(RI)
     A N36N63N90
     A  42                                  DSPATR(HI)
     A N36N63N90
     A  42                                  DSPATR(UL)
     A                                 17  2'Hold output ...............'
     A                                 17 30'?'
     A            DFHOLD    R        B 17 32REFFLD(FNHOLD)
     A  63
     AO 90                                  DSPATR(PR)
     A  37
     AO 57                                  DSPATR(PC)
     A  37                                  DSPATR(RI)
     A N37N63N90
     A  42                                  DSPATR(HI)
     A N37N63N90
     A  42                                  DSPATR(UL)
     A*
     A*--------------------------------------------------------------*
     A* COMMAND KEY RECORD 1
     A*--------------------------------------------------------------*
     A          R HP125K1
     A*%%TS  SD  19940330  133000  ISMPALM     REL-V2R2M0  5738-PW1
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A N90N63                          23 11'F4=Prompt'
     A N90N63                          23 22'F5=Refresh'
     A N90N61                          23 34'F11=Delete'
     A                                 23 46'F12=Previous'
      *
      *--------------------------------------------------------------*
      * ERROR MESSAGE SUBFILE FORMAT
      *--------------------------------------------------------------*
     A          R HP125ES                   SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *                                                                *********
      *--------------------------------------------------------------*
      * ERROR MESSAGE SUBFILE CONTROL FORMAT
      *--------------------------------------------------------------*
     A          R HP125EC                   SFLCTL(HP125ES)
      *
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
