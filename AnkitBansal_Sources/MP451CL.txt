/***************************************************************** */
/*                                                                 */
/*  ENVIRONMENT:     PORK DIVISION                                 */
/*  SYSTEM:          INTERFACE MP2 TO JDE                          */
/*  PROGRAM NUMBER:  MP451CL                                       */
/*  PROGRAM NAME:    MP2: LIST OF OVERRIDDEN ITEMS                 */
/*  PROGRAMMER:      LEANNE FEDOR                                  */
/*  CREATE DATE:     04/30/02                                      */
/*                                                                 */
/*  FUNCTION:        THIS CL IS SUBMITTED WITH QCMDEXC FROM        */
/*                   MP451-SPECIFY LIST OF OVERRIDDEN ITEMS.       */
/*                   OPTIONS ARE PASSED THROUGH THE LDA.           */
/*                                                                 */
/*******************************************************************/
/* MODIFICATIONS:                                                  */
/*                                                                 */
/* 03/13/08  LEANNE RAMSEY                                         */
/*           REPLACE QPRINT WITH PRINT198.                         */
/*                                                                 */
/*******************************************************************/

             PGM

             DCL        VAR(&QDATA) TYPE(*CHAR) LEN(66)

             DCL        VAR(&LDFLAG) TYPE(*CHAR) LEN(1)
             DCL        VAR(&DATA)   TYPE(*CHAR) LEN(1)
             DCL        VAR(&LDBATNO) TYPE(*DEC) LEN(8)

             DCL        VAR(&LDFYMD) TYPE(*DEC) LEN(8)
             DCL        VAR(&LDFMDY) TYPE(*DEC) LEN(6)

             DCL        VAR(&LDTYMD) TYPE(*DEC) LEN(8)
             DCL        VAR(&LDTMDY) TYPE(*DEC) LEN(6)

             DCL        VAR(&LDJDVNNO) TYPE(*DEC) LEN(8)
             DCL        VAR(&LDJDVNNM) TYPE(*CHAR) LEN(40)

             DCL        VAR(&HOLD) TYPE(*CHAR) LEN(10)
             DCL        VAR(&COPYS) TYPE(*DEC) LEN(2)
             DCL        VAR(&OUTQ) TYPE(*CHAR) LEN(10)

             CHGVAR     VAR(&LDFLAG) VALUE(%SST(*LDA 85 1))
             CHGVAR     VAR(&LDBATNO)  VALUE(%SST(*LDA 77 8))

             CHGVAR     VAR(&LDFYMD) VALUE(%SST(*LDA 13 8))
             CHGVAR     VAR(&LDFMDY) VALUE(%SST(*LDA 1 6))

             CHGVAR     VAR(&LDTYMD) VALUE(%SST(*LDA 21 8))
             CHGVAR     VAR(&LDTMDY) VALUE(%SST(*LDA 7 6))

             CHGVAR     VAR(&LDJDVNNO) VALUE(%SST(*LDA 29 8))
             CHGVAR     VAR(&LDJDVNNM) VALUE(%SST(*LDA 37 40))


/*-------------------------------------------------------------------*/
/*  SET UP AND RUN OPEN QUERY OVER INVOICE HEADER FILE               */
/*-------------------------------------------------------------------*/

/* BY BATCH */
             IF         COND(&LDBATNO *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 1 14)) VALUE('IHPOFL +
                          *EQ "Y"')
             CHGVAR     VAR(%SST(&QDATA 16 16)) VALUE('*AND IHBATNO +
                          *EQ')
             CHGVAR     VAR(%SST(&QDATA 33 8)) VALUE(&LDBATNO)
             ENDDO

             ELSE       CMD(DO)

/* BY DATE RANGE */

             CHGVAR     VAR(%SST(&QDATA 1 14)) VALUE('IHPOFL +
                          *EQ "Y"')

             CHGVAR     VAR(%SST(&QDATA 16 16)) VALUE('*AND IHBATDT +
                          *GE')
             CHGVAR     VAR(%SST(&QDATA 33 8)) VALUE(&LDFYMD)
             CHGVAR     VAR(%SST(&QDATA 42 16)) VALUE('*AND IHBATDT +
                          *LE')
             CHGVAR     VAR(%SST(&QDATA 59 8)) VALUE(&LDTYMD)
             ENDDO


             OVRDBF     FILE(MPP104)  SHARE(*YES)
             OPNQRYF    FILE((MPP104)) QRYSLT(&QDATA) KEYFLD(*FILE) +
                          SEQONLY(*YES)


/*-------------------------------------------------------------------*/
/*  SET UP AND RUN OPEN QUERY OVER INVOICE DETAIL FILE               */
/*-------------------------------------------------------------------*/

             CHGVAR     VAR(&QDATA) VALUE(' ')

/*  ALWAYS SELECT ONLY RECORDS WHERE THE USER 'OVERRODE' THE AMOUNT   */

             CHGVAR     VAR(&QDATA) VALUE(' ')
             CHGVAR     VAR(%SST(&QDATA 1 14)) VALUE('IDOVFL +
                          *EQ "Y"')

             OVRDBF     FILE(MPP105)  SHARE(*YES)
             OPNQRYF    FILE((MPP105)) QRYSLT(&QDATA) KEYFLD(*FILE) +
                          SEQONLY(*YES)

/*------------------------------------ -----------------------------*/
/*  CREATE WORKFILE                                                 */
/*------------------------------------------------------------------*/

             CRTDUPOBJL OBJ(MPP351) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)

/*------------------------------------------------------------------*/
/*  CALL PROGRAM TO BUILD WORKFILE                                  */
/*------------------------------------------------------------------*/

             CALL       PGM(MP351)

/*------------------------------------------------------------------*/
/*  DELETE FILE OVERRIDES                                           */
/*------------------------------------------------------------------*/

             CLOF       OPNID(MPP104)
             DLTOVR     FILE(MPP104)

             CLOF       OPNID(MPP105)
             DLTOVR     FILE(MPP105)

/*------------------------------------------------------------------*/
/* SET OVERRIDES FOR PRINT FILES                                    */
/*------------------------------------------------------------------*/

 HOLD:       IF         COND(%SST(*LDA 411 1) = 'Y') THEN(CHGVAR +
                          VAR(&HOLD) VALUE(*YES))
             ELSE       CMD(CHGVAR VAR(&HOLD) VALUE(*NO))

 COPYS:      CHGVAR     VAR(&COPYS) VALUE(%SST(*LDA 412 1))
             IF         COND(&COPYS = 0) THEN(CHGVAR VAR(&COPYS) +
                          VALUE(1))

 OUTQ:       CHGVAR     VAR(&OUTQ) VALUE(%SST(*LDA 413 10))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(%SST(*LDA 401 10)))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(*JOB))

/*------------------------------------------------------------------*/
/* OVERRIDE PRINT FILE                                              */
/*------------------------------------------------------------------*/

             OVRPRTF    FILE(PRINT198) OUTQ(&OUTQ) COPIES(&COPYS) +
                          HOLD(&HOLD)

/*------------------------------------------------------------------*/
/* CALL PRINT PROGRAM                                               */
/*------------------------------------------------------------------*/

             CALL       PGM(MP651) PARM(&LDFLAG &DATA &LDBATNO &LDFMDY +
                          &LDTMDY &LDJDVNNO &LDJDVNNM)

             DLTOVR     FILE(*ALL)

             ENDPGM

