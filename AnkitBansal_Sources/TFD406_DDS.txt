     A*%%TS  SD  20090413  152859  ISLRAMS     REL-V5R4M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        TRIUMPH FOODS
     A*  DISPLAY FILE:  TFD406
     A*  TITLE:         SPECIFY OPTIONS FOR CASH DISTRIBUTION BALANCE REPORT
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  CREATED:       10/04/05
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/TFPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A                                      CF04(04 'PROMPT')
     A                                      CF08(08 'SUBMIT')
     A                                      CF17(17 'PRINT OPTIONS')
     A                                      CF20(20 'WRKSPLF')
     A*
     A*---------------------------------------------------------------
     A* RECORD FORMAT ONE
     A*---------------------------------------------------------------
     A          R TF406R1
     A*%%TS  SD  20090413  152859  ISLRAMS     REL-V5R4M0  5722-WDS
     A                                      SETOF(30)
     A                                      SETOF(31)
     A                                      SETOF(32)
     A                                      SETOF(33)
     A                                      SETOF(50)
     A                                      SETOF(51)
     A                                      OVERLAY
     A                                      RTNCSRLOC(&RCD &FLD)
     A            RCD       R        H      REFFLD(@@RCD)
     A            FLD       R        H      REFFLD(@@FLD)
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 15'SPECIFY OPTIONS FOR CASH DISTRIBUT-
     A                                      ION BALANCE REPORT'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USR)
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@JOB)
     A                                      DSPATR(HI)
     A                                  4  2'Exempt/Non-Exempt/Both ..'
     A                                  4 28'?'
     A            D1IECD    R        B  4 30REFFLD(TFREF/@@IECD *LIBL/TFPREF)
     A N30                                  DSPATR(HI)
     A  30
     AO 50                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A            D1IEDS    R        O  4 33REFFLD(TFREF/@@IEDS *LIBL/TFPREF)
     A                                  5  2'Report type .............'
     A                                  5 28'?'
     A            D1RTCD    R        B  5 30REFFLD(@@RTCD)
     A N31                                  DSPATR(HI)
     A  31
     AO 51                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A            D1RTDS    R        O  5 33REFFLD(TFREF/@@RTDS *LIBL/TFPREF)
     A                                  7  2'If Start Date is left blank, the r-
     A                                      eport will start with the first'
     A                                      COLOR(PNK)
     A                                  8  5'Week-ending date for which there i-
     A                                      s a remaining balance based on the'
     A                                      COLOR(PNK)
     A                                  9  5'Exempt/Non-Exempt/Both selection m-
     A                                      ade above.'
     A                                      COLOR(PNK)
     A                                 11  5'Start date ..'
     A            D1FMDY    R     Y  B 11 19REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A N32                                  DSPATR(HI)
     A  32                                  DSPATR(PC)
     A  32                                  DSPATR(RI)
     A                                 11 32'Week-ending:'
     A            D1FWEMDY  R     Y  O 11 45REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A                                 13  2'If a Stop Date is entered, the rep-
     A                                      ort will stop with that date.'
     A                                      COLOR(PNK)
     A                                 13 67' '
     A                                 15  5'Stop date ...'
     A            D1TMDY    R     Y  B 15 19REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A N33                                  DSPATR(HI)
     A  33                                  DSPATR(PC)
     A  33                                  DSPATR(RI)
     A*
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R TF406K1
     A*%%TS  SD  20040928  155440  ISLRAMS     REL-V5R2M0  5722-WDS
     A  43                             21  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A                                 23 11'F4=Prompt'
     A                                 23 22'F8=Submit'
     A                                 23 33'F17=Print Options'
     A                                 23 52'F20=Spool Files'
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *---------------------------------------------------------------
     A          R TF406ES                   SFL SFLMSGRCD(24)
      *
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
     A*---------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*---------------------------------------------------------------
     A*
     A          R TF406EC                   SFLCTL(TF406ES)
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
