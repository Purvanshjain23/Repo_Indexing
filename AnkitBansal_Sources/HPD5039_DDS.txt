     A*%%TS  SD  20040202  110823  ISLRAMS     REL-V5R2M0  5722-WDS
     A*****************************************************************
     A*
     A*  SYSTEM:        HOG PRODUCTION
     A*  DISPLAY FILE:  HPD5039 - SELECT BUDGET TEMPLATE
     A*  CREATED BY:    LEANNE FEDOR
     A*  DATE:          02/02/04
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      PRINT
     A                                      CA12(12 'PREVIOUS')
     A                                      CF04(04 'PROMPT')
     A N82                                  ROLLUP(25 'PAGE FORWARD')
     A*---------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP5039S1                  SFL
     A*%%TS  SD  20040202  110823  ISLRAMS     REL-V5R2M0  5722-WDS
     A            HDRRN1    R        H      REFFLD(@@RRN)
     A            D1BTCD    R        O  8  1REFFLD(@@BTCD)
     A            D1BTDS    R        O  8  9REFFLD(@@BTDS)
     A            D1BACD    R        O  8 36REFFLD(HSRREF/@@BACD *LIBL/HSPREF)
     A            D1CSCD    R        O  8 39REFFLD(HSRREF/@@CSCD *LIBL/HSPREF)
     A            D1BTWK    R        O  8 41REFFLD(HSRREF/@@BTWK *LIBL/HSPREF)
     A                                      EDTCDE(M)
     A            D1PPCD    R        O  8 48REFFLD(HSRREF/@@PPCD *LIBL/HSPREF)
     A            D1TFCD    R        O  8 55REFFLD(HSRREF/@@TFCD *LIBL/HSPREF)
     A            D1CELL    R        O  8 62REFFLD(HSRREF/@@CELL *LIBL/HSPREF)
     A                                      EDTCDE(M)
     A            D1SIZE    R        O  8 66REFFLD(HSRREF/@@SIZE *LIBL/HSPREF)
     A            D1AIST    R        O  8 74REFFLD(@@AIST)
     A*
     A*---------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP5039C1                  SFLCTL(HP5039S1)
     A*%%TS  SD  20040202  110823  ISLRAMS     REL-V5R2M0  5722-WDS
     A                                      SFLSIZ(0100)
     A                                      SFLPAG(0009)
     A                                      WINDOW(HP5039EC)
     A                                      SETOF(51)
     A                                      SETOF(52)
     A                                      SETOF(53)
     A                                      SETOF(54)
     A                                      SETOF(55)
     A                                      RTNCSRLOC(&RCD &FLD)
     A                                      OVERLAY
     A                                      SFLCSRRRN(&HDRRN)
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A            HDPAG1    R        H      REFFLD(@@PAG)
     A                                      SFLRCDNBR
     A            RCD           10A  H
     A            FLD           10A  H
     A            HDRRN     R        H      REFFLD(@@RRN)
     A            SDPGM     R        O  1  1REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 28'SELECT BUDGET TEMPLATE'
     A                                      DSPATR(HI)
     A            C1DESC    R        O  2 24REFFLD(@@DS30)
     A                                      DSPATR(HI)
     A                                  3  1'Position Cursor, Press Enter'
     A                                  4 36'?'
     A                                      DSPATR(HI)
     A                                  4 39'?'
     A                                      DSPATR(HI)
     A                                  4 50'?'
     A                                      DSPATR(HI)
     A                                  4 57'?'
     A                                      DSPATR(HI)
     A N62                              4 74'?'
     A                                      DSPATR(HI)
     A                                  5 36'A'
     A                                      DSPATR(HI)
     A                                  5 39'C'
     A                                      DSPATR(HI)
     A                                  5 48'Prod'
     A                                      DSPATR(HI)
     A                                  5 55'Type'
     A                                      DSPATR(HI)
     A                                  5 73'Act'
     A                                      DSPATR(HI)
     A                                  6  1'Code'
     A                                      DSPATR(HI)
     A                                  6  9'Description'
     A                                      DSPATR(HI)
     A                                  6 36'V'
     A                                      DSPATR(HI)
     A                                  6 39'S'
     A                                      DSPATR(HI)
     A                                  6 41'Weeks'
     A                                      DSPATR(HI)
     A                                  6 48'Phase'
     A                                      DSPATR(HI)
     A                                  6 55'Farm'
     A                                      DSPATR(HI)
     A                                  6 61'Cell'
     A                                      DSPATR(HI)
     A                                  6 66'Size'
     A                                      DSPATR(HI)
     A                                  6 73'/In'
     A                                      DSPATR(HI)
     A            C1BTCD    R        B  7  1REFFLD(@@BTCD)
     A                                      DSPATR(HI)
     A            C1BTDS    R        B  7  9REFFLD(@@BTDS)
     A                                      CHGINPDFT(UL LC FE)
     A                                      DSPATR(HI)
     A            C1BACD    R        B  7 36REFFLD(HSRREF/@@BACD *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A  51                                  DSPATR(PC)
     A            C1CSCD    R        B  7 39REFFLD(HSRREF/@@CSCD *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A  52                                  DSPATR(PC)
     A            C1BTWK    R        B  7 41REFFLD(HSRREF/@@BTWK *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A                                      EDTCDE(M)
     A            C1PPCD    R        B  7 48REFFLD(HSRREF/@@PPCD *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A  53                                  DSPATR(PC)
     A            C1TFCD    R        B  7 55REFFLD(HSRREF/@@TFCD *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A  54                                  DSPATR(PC)
     A            C1CELL    R        B  7 62REFFLD(HSRREF/@@CELL *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A                                      EDTCDE(M)
     A            C1SIZE    R        B  7 66REFFLD(HSRREF/@@SIZE *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A N62        C1AIST    R        B  7 74REFFLD(HSRREF/@@AIST *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A  55                                  DSPATR(PC)
     A*
     A*---------------------------------------------------------------
     A* COMMAND LINE LINE
     A*---------------------------------------------------------------
     A          R HP5039K1
     A                                      WINDOW(HP5039EC)
     A                                      OVERLAY
     A  43                             17  1'Message program failed. Call IS.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 18  1'F12=Previous'
     A                                 18 15'F4=Prompt'
      *
     A*-------------------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
     A*-------------------------------------------------------------------------
     A          R HP5039ES                  SFL SFLMSGRCD(19)
      *
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
     A*
     A*-------------------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*-------------------------------------------------------------------------
     A*
     A          R HP5039EC                  SFLCTL(HP5039ES)
     A*%%TS  SD  19980327  105628  ISLRAMS     REL-V3R7M0  5716-PW1
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A                                      WINDOW(*DFT 20 75)
     A                                      WDWBORDER((*DSPATR RI) (*CHAR '    -
     A                                          '))
     A            MSGPGM                    SFLPGMQ(10)
     A*
     A*---------------------------------------------------------------
     A* ASSUME FORMAT -- REQUIRED FOR WINDOWS
     A*---------------------------------------------------------------
     A          R ASSUME
     A*%%TS  SD  20011025  160559  ISLRAMS     REL-V4R4M0  5769-PW1
     A                                      ASSUME
     A                                      OVERLAY
     A                                 24 79' '
