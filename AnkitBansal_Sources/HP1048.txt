      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Hog Production System
      * PROGRAM:     HP1048
      * TITLE:       Revise Reported Weekly Activity
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     09/25/02
      *
      *
      * FUNCTION: Uses an edit record format to add/change/delete weekly group records in the
      *           Reported Weekly Activity file.
      *
      *           This program is called from HP4048 - Work with Reported Weekly Activity.
      *
      *           The field 'mode' contains the value passed into
      *           this program from the calling program.  The field
      *           'd1mode' is the screen field that displays the
      *           value of 'mode'.  Mode will be CREATE, REVISE or
      *           DELETE.           Once in the program, mode and
      *           d1mode will never change.  However, the action
      *           being performed can change depending on the function
      *           keys the user takes.
      *
      *           The user may delete an existing record by taking
      *           F11=DELETE from this program or option 4=DELETE
      *           from the WORK WITH. If the user is deleting with
      *           4=DELETE, no panel will be displayed from this program
      *           unless there is an error on the attempted deletion.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 01/16/06  LeAnne Fedor
      *           Recompile only.
      *           We have removed "Bin Set" from HSP020-Building/Rooms and put it in our new
      *           HSP113-Rooms/Bin Sets file.
      *
      * 07/02/09  LeAnne Ramsey
      *           Recompile only. Added new field 'Continuous Flow Flag' to Hog Group file.
      *
      * 10/15/13  LeAnne Ramsey (E2831)
      *           Recompile only. Added field 'MTech Reference'.
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fhpd1048   cf   e             workstn
     F                                     infds(iofeed)
      *
      *
     Fhsp009    if   e           k disk
      *    Business office
      *
      *
     Fhsp018    if   e           k disk
      *    Farm site
      *
      *
     Fhsp019    if   e           k disk
      *    Farm buildings
      *
      *
     Fhsp020    if   e           k disk
      *    building rooms
      *
      *
     Fhsp034    if   e           k disk
      *    Hog group
      *
      *
     Fhsl034o   if   e           k disk    rename(hgrec:hgreco)
      *    Hog group
      *
      *
     Fhsl068d   if   e           k disk
      *    Killed/dead hogs
      *
      *
     Fhsp102    uf a e           k disk
      *    Reported weekly activity
      *
      /eject
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D create          c                   'CREATE'
     D revise          c                   'REVISE'
     D delete          c                   'DELETE'
     D view            c                   'VIEW  '
     D select          c                   'SELECT'
     D yes             c                   'Y'
     D no              c                   'N'
     D set1            c                   'SET1  '
     D edit1           c                   'EDIT1 '
     D scrn1           c                   'SCRN1 '
     D exit            c                   'EXIT  '
     D update          c                   'UPDATE'
      *
      *
      ****************************************************************
      * ARRAYS AND TABLES
      ****************************************************************
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Standard fields
      *
     D mode            s              6
     D pgm             s             10
     D msgfl           s             10
     D routne          s              6
     D rtncd           s              2
     D action          s                   like(mode)
     D maxmsg          s              2  0  inz(20)
      *
      *
      * Workfields
      *
     D wkhgsn          s                   like(hghgsn)
      *
      *
      * Work fields for date manipulation
      *
     D wkisodate       s               D   datfmt(*iso)
     D wkccyymmdd      s              8  0
     D wkwedt          s              8  0
     D wkyymmdd        s              6  0
      *
      *
      * Parm fields
      *
     D xxalphfscd      s              5a
      *
     D xxfscd          s                   like(fsfscd)
     D xxfsnm          s                   like(fsfsnm)
     D xxppcd          s                   like(hgppcd)
     D xxptcd          s                   like(hgptcd)
     D xxgscd          s                   like(hggscd)
      *
     D xxblcd          s                   like(hgblcd)
     D xxblds          s             20a
      *
     D xxrmcd          s                   like(hgrmcd)
      *
     D xxhgsn          s                   like(hghgsn)
     D xxhgcd          s                   like(hghgcd)
      *
     D xxwedt          s                   like(rwwedt)
     D xxwemdy         s                   like(d1wemdy)
      *
     D xxbegdt         s                   like(rwwedt)
     D xxenddt         s                   like(rwwedt)
      *
     D xxfsbo          s                   like(bobobo)
     D xxcocd          s              3  0
     D xxreturn        s              7a
      *
     D xxjulian        s              5  0
     D xxcyymmdd       s              7  0
      *
     D xxhpscdyr       s                   like(rwcdyr)
     D xxhpscdwk       s                   like(rwcdwk)
     D xxhpspicdt      s              5  0
      *
     D xxcdyr          s                   like(rwcdyr)
     D xxcdwk          s                   like(rwcdwk)
     D xxday           s              1a
      *
     D xxacyr          s                   like(rwcdyr)
     D xxacpe          s              2  0
     D xxacwk          s                   like(rwcdwk)
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * standard error message handling data structures
      *---------------------------------------------------------------
      *
      * For error message handling, a packed index (3) is required.
      * For program readability, define a corresponding error count
      * field called 'error'
      *
     D                 ds                  inz
     D  error                         2p 0
     D   e                            2p 0 overlay(error)
      *
      * This data structure supplies the name of the message file to
      * the message handling CL program.  The field name MSGFIL must be
      * constant. The value in quotes is the name of the specific
      * message file containing the user defined messages.
      *
     D                 ds                  inz
     D  msgfil                       10    inz('HSMSGF    ')
      *
      *---------------------------------------------------------------
      *  Standard message data structures
      *---------------------------------------------------------------
      *
      * The following 3 data structures are used to speed message
      * handling since it is faster to clear data structures than
      * arrays.  Each is associated with a standard message array.
      *
     D mgi             ds                  inz
     D  mgid                          7    dim(20)
      *
     D mgd             ds                  inz
     D  mgdt                         50    dim(20)
      *
     D mgwk            ds                  inz
     D  mgw                           1    dim(50)
      *
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *---------------------------------------------------------------
      * Standard workstation information data structure
      *---------------------------------------------------------------
      *    externally defined as UTWSFR (record format: UTIDFRR)
     D iofeed        e ds                  extname(utwsfr)
      *
      *
      *---------------------------------------------------------------
      * Standard database file information data structure
      *---------------------------------------------------------------
      *    externally defined as UTDBGR (record format: FDBCKD)
     D dbfeed        e ds                  extname(utdbfr)
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * MAINLINE
      ****************************************************************
      *
     C                   eval      routne = set1
      *
     C     *inlr         doweq     *off                                         Main do loop
     C     routne        caseq     set1          $set1
     C     routne        caseq     scrn1         $scrn1
     C     routne        caseq     edit1         $edit1
     C     routne        caseq     update        $upd1
     C     routne        caseq     exit          $exit
     C                   endcs
     C                   enddo                                                  Main do loop
      /EJECT
      *----------------------------------------------------------------
      * Set environment for screen 1
      *----------------------------------------------------------------
      *
     C     $set1         begsr
      *
      * Clear fields on display panel if creating
      *
     C                   select
     C                   when      mode = create                                WH create
     C                   move      *blank        d1hgcd
     C                   z-add     0             d1fscd
     C                   move      *blank        d1blcd
     C                   move      *blank        d1rmcd
     C                   z-add     0             d1bginhd
     C                   z-add     0             d1tihd
     C                   z-add     0             d1tohd
     C                   z-add     0             d1cuhd
     C                   z-add     0             d1mkhd
     C                   z-add     0             wkhgsn
      *
      * If incoming mode is REVISE or DELETE, then retrieve the database
      * record.  Do not lock the record.  Typically, in DELETE mode, these
      * values will not be displayed. However, if there is an error on the
      * delete action, the panel will be displayed with messages.
      *
     C                   when      mode = revise or mode = delete               WH revise/delete
     C     key01         chain(n)  hsp102                             92
     C                   if        *in92 = *off                                 If found
     C                   z-add     rwbginhd      d1bginhd
     C                   z-add     rwtihd        d1tihd
     C                   z-add     rwtohd        d1tohd
     C                   z-add     rwcuhd        d1cuhd
     C                   z-add     rwmkhd        d1mkhd
      *
      * Retrieve Farm Site/building/room for group
      *
     C     d1hgcd        chain     hsp034                             92
     C                   if        *in92 = *off
     C                   z-add     hgfscd        d1fscd
     C                   move      hgblcd        d1blcd
     C                   move      hgrmcd        d1rmcd
     C                   endif
      *
     C                   else
     C                   seton                                        62
     C                   if        error < maxmsg
     C                   add       1             error
     C                   eval      mgid(e) = 'HS09008'
     C                   endif
      *
     C                   endif                                                  If found
     C                   endsl
      *
      *
      * Always retrieve killed/dead hogs and calculate ending inventory
      *
     C                   z-add     0             d1kdhd
     C                   z-add     0             d1eninhd
     C                   exsr      $kdhd
     C                   exsr      $eninhd
      *
      * If the user is deleting and there were no errors when retrieving the
      * record, go on to the update routine without displaying the screen.
      * Otherwise, display the screen.
      *
     C                   if        error = 0 and action = delete                If no error
     C                   eval      routne = update
     C                   else
     C                   eval      routne = scrn1
     C                   endif                                                  If no error
      *
     C                   endsr
      /EJECT
      *----------------------------------------------------------------
      * Perform operator I/O for screen 1
      *----------------------------------------------------------------
      *
     C     $scrn1        begsr
      *
      * Write the error messages from the error arrays to the
      * error message subfile
      *
     C                   exsr      $wrmsg
      *
      *
      * Set indicators to control input/output fields and available
      * function keys.
      *
     c                   select
     C                   when      mode = revise
     C                   seton                                        60
      *
     C                   when      mode = create
     C                   seton                                        61
      *
     C                   when      mode = delete
     C                   seton                                        62
     c                   endsl
      *
      * Write screen 1 to CRT
      *
     C                   write     hp1048k1
     C                   write     hp1048ec
     C                   exfmt     hp1048r1
      *
      * Clear messages
      *
     C                   exsr      $clmsg
      *
      * Reset action to be equal to the incoming mode.  This is required
      * in case the user took F11-Delete and got an error on the
      * deletion and now needs to be able to change the record and take
      * an action other than delete
      *
     C                   eval      action = mode
      *
      * Get user's input and set routine
      *
     C                   select
      *
     C                   when      *in03 = *on                                  F3-exit
     C                   eval      rtncd = '03'
     C                   eval      routne = exit
      *
     C                   when      *in04 = *on                                  F4=prompt
     C                   exsr      $f4s1
      *
     C                   when      *in05 = *on                                  F5-refresh
     C                   eval      routne = set1
      *
     C                   when      *in07 = *on                                  F7-deaths
     C                   call      'HP457'
     C                   exsr      $edit1
     C                   eval      routne = scrn1
      *
     C                   when      *in09 = *on                                  F9=accept
     C                   exsr      $edit1
     C                   if        error = 0                                    If no error
     C                   exsr      $upd1
     C                   else
     C                   eval      routne = scrn1
     C                   endif                                                  If no error
      *
     C                   when      *in11 = *on or mode = delete                 F11-delete
     C                   eval      action = delete
     C                   eval      routne = update
     C                   other
      *
      * User has pressed enter
      *
     C                   eval      routne = edit1
     C                   endsl
      *
     C                   endsr
      /EJECT
      *----------------------------------------------------------------
      * Edit input fields for screen 1
      *----------------------------------------------------------------
      *
     C     $edit1        begsr
      *
      * *IN62 will be on if the record the user is trying to access has
      * been deleted by another user.  So, only perform the edits for the
      * fields on the screen if the indicator is off
      *
     C                   if        *in62 = *off                                 If here
      *
      * Edits when Creating a new record
      *
     C                   if        action = create                              If create
     C                   exsr      $editcr
     C                   endif                                                  If create
      *
      * Always validate:
      *   1) beginning inventory head
      *   2) transferred in head
      *   3) transferred out head
      *   4) cull sale head
      *   5) market/feeder pig sale head
      *
     C                   exsr      $bginhd
     C                   exsr      $tihd
     C                   exsr      $tohd
     C                   exsr      $cuhd
     C                   exsr      $mkhd
      *
      * If you have a valid group and week-ending date, retrieve killed/dead head
      *
     C                   z-add     0             d1kdhd
     C                   if        (d1wemdy <> 0 and *in31 = *off) and
     C                             (d1hgcd <> *blank and *in32 = *off)
     C                   exsr      $kdhd
     C                   endif
      *
      *  Calculate Ending Inventory Head
      *
     C                   exsr      $eninhd
      *
     C                   endif                                                  If here
      *
     C                   movel     scrn1         routne
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Edits for Create mode only
      *---------------------------------------------------------------
      *
     C     $editcr       begsr
      *
      * Week-ending date
     C                   exsr      $wedt
      *
      * User must enter some of the key fields
      *
     C                   if        d1fscd = 0 and
     C                             d1blcd = *blank and
     C                             d1rmcd = *blank and
     C                             d1hgcd = *blank
     C                   seton                                        303839
     C                   seton                                        32
     C                   if        error < maxmsg
     C                   add       1             error
     C                   eval      mgid(e) = 'HS01184'
     C                   endif
     C                   endif
      *
      * Now:
      *  1) edit farm/bld/room and default group (if group not already entered)
      *  2) edit group and default farm/bldg/room (if farm/bldg/room are blank)
      *
     C                   if        error = 0                                    If OK so far
      *
     C                   if        d1fscd <> 0 or d1blcd <> *blank or
     C                             d1rmcd <> *blank
     C                   exsr      $fscd
     C                   exsr      $blcd
     C                   exsr      $rmcd
     C                   if        d1hgcd = *blank and
     C                             *in30 = *off and *in38 = *off and
     C                             *in39 = *off
     C                   exsr      $group
     C                   endif
     C                   endif
      *
     C                   if        d1hgcd <> *blank
     C                   exsr      $hgcd
     C                   endif
      *
     C                   endif                                                  If OK so far
      *
      * If both hog group and week-ending date are valid, check that no
      * weekly record already exists for this group/date.
      *
     C                   if        *in31 = *off and *in32 = *off                If both ok
     C     key01         chain(n)  hsp102                             92
     C                   if        *in92 = *off                                 If already exists
     C                   seton                                        3132
     C                   if        error < maxmsg
     C                   add       1             error
     C                   eval      mgid(e) = 'HS09097'
     C                   endif
     C                   endif                                                  If already exists
     C                   endif                                                  If both ok
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Validate farm site (in create mode only)
      *---------------------------------------------------------------
      *
     C     $fscd         begsr
      *
     C                   select
     C                   when      d1fscd = 0 and
     C                             (d1blcd <> *blank or d1rmcd <> *blank)
     C                   seton                                        30
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS01190'     mgid(e)
     C                   endif
      *
     C                   when      d1fscd < 0
     C                   seton                                        30
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS09000'     mgid(e)
     C                   endif
     C                   other
      *
     C     d1fscd        chain     hsp018                             92
     C                   if        *in92 = *on                                  If bad farm
     C                   seton                                        30
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS00131'     mgid(e)
     C                   clear                   mgw
     C                   move      d1fscd        xxalphfscd
     C                   movea     xxalphfscd    mgw(1)
     C                   movea     mgw           mgdt(e)
     C                   endif
     C                   else
      *
      * Cannot be a BGF farm
      *
     C                   if        fsppcd = 'BGF'                               If BGF
     C                   seton                                        30
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS09092'     mgid(e)
     C                   endif
     C                   endif                                                  If BGF
     C                   endif                                                  If bad farm
     C                   endsl
      *
     C                   endsr
      /EJECT
      *----------------------------------------------------------------
      * Validate building
      *----------------------------------------------------------------
      *
     C     $blcd         begsr
      *
     C                   select
     C                   when      d1blcd = *blank and
     C                             (d1fscd <> 0 or d1rmcd <> *blank)
     C                   seton                                        38
     C                   if        error < maxmsg
     C                   add       1             error
     C                   eval      mgid(e) = 'HS01191'
     C                   endif
     C                   other
      *
     C     key04         chain     hsp019                             92
     C                   if        *in92 = *on                                  If not found
     C                   seton                                        38
     C                   if        error < maxmsg
     C                   add       1             error
     C                   eval      mgid(e) = 'HS00116'
     C                   clear                   mgw
     C                   movea     d1blcd        mgw(1)
     C                   move      d1fscd        xxalphfscd
     C                   movea     xxalphfscd    mgw(6)
     C                   movea     mgw           mgdt(e)
     C                   endif
     C                   endif                                                  If not found
     C                   endsl
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Validate room (in create mode only)
      *---------------------------------------------------------------
      *
     C     $rmcd         begsr
      *
     C                   select
     C                   when      d1rmcd = *blank and
     C                             (d1fscd <> 0 or d1blcd <> *blank)
     C                   seton                                        39
     C                   if        error < maxmsg
     C                   add       1             error
     C                   eval      mgid(e) = 'HS01192'
     C                   endif
     C                   other
      *
      * Check that the room exists in the building on the farm site
     ‚*
     C     key03         chain     hsp020                             92
     C                   if        *in92 = *on                                  If no hit
     C                   seton                                        39
     C                   if        error < maxmsg
     C                   add       1             error
     C                   eval      mgid(e) = 'HS00117'
     C                   clear                   mgw
     C                   movea     d1rmcd        mgw(1)
     C                   movea     d1blcd        mgw(6)
     C                   move      d1fscd        xxalphfscd
     C                   movea     xxalphfscd    mgw(11)
     C                   movea     mgw           mgdt(e)
     C                   endif
     C                   else
     ‚*
      * Room must have an 'active' status
     ‚*
     C                   if        brrmst <> 'A'                                If not active
     C                   seton                                        39
     C                   if        error < maxmsg
     C                   add       1             error
     C                   eval      mgid(e) = 'HS00726'
     C                   clear                   mgw
     C                   movea     d1rmcd        mgw(1)
     C                   movea     mgw           mgdt(e)
     C                   endif
     C                   endif                                                  If not active
     C                   endif                                                  If no hit
      *
     C                   endsl
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------------------
      * Attempt to find a default group in the farm/building/room
      *---------------------------------------------------------------------------
      *
      * If only one group is created/open/disposed in that building/room, default
      * it in. But, if multiple groups exist, issue a message to the  user to select
      * a group.
      *
     C     $group        begsr
      *
     C     key03         chain     hsl034o                            92
     C                   if        *in92 = *off                                 If open group
      *
      * Check to see if there is another group that might fit.
      *
     C     key03         reade     hsl034o                                92
     C                   if        *in92 = *off                                 If mult group
     C                   seton                                        32
     C                   if        error < maxmsg
     C                   add       1             error
     C                   clear                   mgw
     C                   movel     'HS01188'     mgid(e)
     C                   endif
     C                   else
      *
     C                   movel     hghgcd        d1hgcd
     C                   endif                                                  If mult group
     C                   else
      *
      * No group was found
      *
     C                   seton                                        32
     C                   if        error < maxmsg
     C                   add       1             error
     C                   clear                   mgw
     C                   movel     'HS01189'     mgid(e)
     C                   endif
      *
     C                   endif                                                  If open group
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Validate hog group (in create mode only)
      *---------------------------------------------------------------
      *
      * Validations:
      *   1) must be valid in hog group master
      *   2) cannot be a BGF group
      *   3) cannot have a status of Voided
      *
     C     $hgcd         begsr
      *
     C                   z-add     0             wkhgsn
      *
     C     d1hgcd        chain     hsp034                             92
     C                   if        *in92 = *on                                  If not found
     C                   seton                                        32
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS00261'     mgid(e)
     C                   clear                   mgw
     C                   movea     d1hgcd        mgw(1)
     C                   movea     mgw           mgdt(e)
     C                   endif
     C                   else
      *
     C                   z-add     hghgsn        wkhgsn
      *
      * If there are no values in farm/bldg/room, default this group's values
      * into the fields. Otherwise, compare this group's farm/bldg/room to the
      * screen values.
     C                   select
     C                   when      d1fscd = 0 and d1blcd = *blank and
     C                             d1rmcd = *blank
     C                   z-add     hgfscd        d1fscd
     C                   move      hgblcd        d1blcd
     C                   move      hgrmcd        d1rmcd
     C                   other
      *
     C                   if        hgfscd <> d1fscd
     C                   seton                                        32
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS00468'     mgid(e)
     C                   clear                   mgw
     C                   movea     d1hgcd        mgw(1)
     C                   move      hgfscd        xxalphfscd
     C                   movea     xxalphfscd    mgw(8)
     C                   move      d1fscd        xxalphfscd
     C                   movea     xxalphfscd    mgw(13)
     C                   movea     mgw           mgdt(e)
     C                   endif
     C                   endif
      *
     C                   if        hgblcd <> d1blcd
     C                   seton                                        32
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS00481'     mgid(e)
     C                   clear                   mgw
     C                   movea     d1hgcd        mgw(1)
     C                   movea     hgblcd        mgw(8)
     C                   movea     d1blcd        mgw(13)
     C                   movea     mgw           mgdt(e)
     C                   endif
     C                   endif
      *
     C                   if        hgrmcd <> d1rmcd
     C                   seton                                        32
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS00482'     mgid(e)
     C                   clear                   mgw
     C                   movea     d1hgcd        mgw(1)
     C                   movea     hgrmcd        mgw(8)
     C                   movea     d1rmcd        mgw(13)
     C                   movea     mgw           mgdt(e)
     C                   endif
     C                   endif
      *
     C                   endsl
      *
     C                   if        hgppcd = 'BGF  '                             If BGF
     C                   seton                                        32
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS09093'     mgid(e)
     C                   endif
     C                   endif                                                  If BGF
      *
     C                   if        hggscd = 'VD'                                If voided
     C                   seton                                        32
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS09096'     mgid(e)
     C                   endif
     C                   endif                                                  If voided
      *
     C                   endif                                                  If not found
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Edit week-ending date (in create mode only)
      *---------------------------------------------------------------
      *
     C     $wedt         begsr
      *
     C                   z-add     0             d1cdyr
     C                   z-add     0             d1cdwk
     C                   z-add     0             wkwedt
      *
     C                   select
     C                   when      d1wemdy = 0
     C                   seton                                        31
     C                   if        error < maxmsg
     C                   add       1             error
     C                   eval      mgid(e) = 'HS09001'
     C                   endif
     C                   other
      *
     C     *mdy          test(d)                 d1wemdy                92
     C                   if        *in92 = *on                                  If bad date
     C                   seton                                        31
     C                   if        error < maxmsg
     C                   add       1             error
     C                   eval      mgid(e) = 'HS09004'
     C                   endif
     C                   else
      *
      * Flip the date from MMDDYY to CCYYMMDD.
      *
     C     *mdy          move      d1wemdy       wkisodate
     C                   move      wkisodate     wkccyymmdd
     C                   z-add     wkccyymmdd    wkwedt
      *
      * If date is valid, retrieve the following info from the OMS
      * Calendar file and verify that the date is a Saturday.
      *   1) HPS calendar year
      *   2) HPS calendar week
      *
      * You will call a SYNON program for this retrieval using as
      * input parms 1) company number and 2) date in the SYNON format of
      * CYYMMDD.
      *
     C                   exsr      $synon
      *
      * Check the returned SYNON values.
      *
     C                   select
     C                   when      xxreturn <> *blank
     C                   seton                                        31
     C                   if        error < maxmsg
     C                   add       1             error
     C                   eval      mgid(e) = 'HS09138'
     C                   clear                   mgw
     C                   movea     bococd        mgw(1)
     C                   movea     mgw           mgdt(e)
     C                   endif
      *
      * Must be Saturday.
     C                   when      xxday <> '7'
     C                   seton                                        31
     C                   if        error < maxmsg
     C                   add       1             error
     C                   eval      mgid(e) = 'HS09030'
     C                   endif
      *
     C                   other
     C                   z-add     xxhpscdyr     d1cdyr
     C                   z-add     xxhpscdwk     d1cdwk
     C                   endsl
      *
     C                   endif                                                  If bad date
     C                   endsl
      *
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Validate Beginning Inventory head
      *---------------------------------------------------------------
      *
      * Validations:
      *  1) cannot be negative
      *
     C     $bginhd       begsr
      *
     C                   if        d1bginhd < 0
     C                   seton                                        33
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS09000'     mgid(e)
     C                   endif
     C                   endif
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Validate Transferred In head
      *---------------------------------------------------------------
      *
      * Validations:
      *  1) cannot be negative
      *
     C     $tihd         begsr
      *
     C                   if        d1tihd < 0
     C                   seton                                        34
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS09000'     mgid(e)
     C                   endif
     C                   endif
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Validate Transferred Out head
      *---------------------------------------------------------------
      *
      * Validations:
      *  1) cannot be negative
      *
     C     $tohd         begsr
      *
     C                   if        d1tohd < 0
     C                   seton                                        35
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS09000'     mgid(e)
     C                   endif
     C                   endif
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Validate Cull Sale Head
      *---------------------------------------------------------------
      *
      * Validations:
      *  1) cannot be negative
      *
     C     $cuhd         begsr
      *
     C                   if        d1cuhd < 0
     C                   seton                                        36
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS09000'     mgid(e)
     C                   endif
     C                   endif
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Validate Head from Market and Feeder Pig Sales
      *---------------------------------------------------------------
      *
      * Validations:
      *  1) cannot be negative
      *
     C     $mkhd         begsr
      *
     C                   if        d1mkhd < 0
     C                   seton                                        37
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS09000'     mgid(e)
     C                   endif
     C                   endif
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Calculate Ending Inventory Head
      *---------------------------------------------------------------
      *
     C     $eninhd       begsr
      *
     C                   z-add     d1bginhd      d1eninhd
     C                   add       d1tihd        d1eninhd
     C                   sub       d1tohd        d1eninhd
     C                   sub       d1cuhd        d1eninhd
     C                   sub       d1mkhd        d1eninhd
     C                   sub       d1kdhd        d1eninhd
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      *  Use SYNON program to validate date
      *---------------------------------------------------------------
      *
     C     $synon        begsr
      *
      * Get the date into the SYNON format.
      *
     C                   z-add     wkccyymmdd    wkyymmdd
     C                   if        wkccyymmdd >= 20000000
     C     wkyymmdd      add       1000000       xxcyymmdd
     C                   else
     C                   z-add     wkyymmdd      xxcyymmdd
     C                   endif
      *
      *
     C                   call      'POF3XFR'
     C                   parm      *blank        xxreturn
     C                   parm      350           xxcocd
     C                   parm                    xxcyymmdd
     C                   parm      0             xxjulian
     C                   parm      0             xxacyr
     C                   parm      0             xxacpe
     C                   parm      0             xxacwk
     C                   parm      *blank        xxday
     C                   parm      0             xxcdwk
     C                   parm      0             xxhpspicdt
     C                   parm      0             xxhpscdyr
     C                   parm      0             xxhpscdwk
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Retrieve killed/dead head for this group/week
      *---------------------------------------------------------------
      *
     C     $kdhd         begsr
      *
      * You cannot retrieve the dead head if you don't have:
      *    1) a value week-ending date and
      *    2) a valid hog group
      *
     C     *iso          test(d)                 wkwedt                 92
     C                   if        *in92 = *off and wkhgsn <> 0                 If OK date
      *
     C                   z-add     wkwedt        xxenddt
      *
     C     *iso          move      xxenddt       wkisodate
     C                   subdur    6:*days       wkisodate
     C                   move      wkisodate     xxbegdt
      *
      *
     C     key02         setll     hsl068d
      *
     C                   dou       *in91 = *on                                  Do dead
     C     wkhgsn        reade     hsl068d                                91
     C                   if        *in91 = *off                                 If not EOF
     C                   add       kdkdhd        d1kdhd
     C                   endif                                                  If not EOF
     C                   enddo                                                  Do dead
     C                   endif                                                  If OK date
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      *  F4 prompt on screen 1  (only valid when in CREATE mode)
      *---------------------------------------------------------------
      *
     C     $f4s1         begsr
      *
     C                   select
      * Hog group
     C                   when      fld = 'D1HGCD'
     C                   call      'HP598'
     C                   parm      d1fscd        xxfscd
     C                   parm      *blank        xxfsnm
     C                   parm      d1blcd        xxblcd
     C                   parm      d1rmcd        xxrmcd
     C                   parm      *blank        xxhgcd
     C                   parm      *blank        xxgscd
     C                   parm      *blank        xxptcd
     C                   parm      *blank        xxppcd
     C                   seton                                        52
     C                   if        xxhgcd <> *blank
     C                   move      xxhgcd        d1hgcd
     C                   z-add     xxfscd        d1fscd
     C                   move      xxblcd        d1blcd
     C                   move      xxrmcd        d1rmcd
     C                   endif
      *
      * Farm site
     C                   when      fld = 'D1FSCD'
     C                   call      'HP551'
     C                   parm                    xxfsbo
     C                   parm      0             xxfscd
     C                   parm      *blank        xxfsnm
     C                   parm      *blank        xxppcd
     C                   parm      *blank        xxptcd
     C                   seton                                        50
     C                   if        xxfscd <> 0
     C                   z-add     xxfscd        d1fscd
     C                   endif
      *
      * Building
     C                   when      fld = 'D1BLCD'
     C                   call      'HP523'
     C                   parm                    d1fscd
     C                   parm      *blank        xxblcd
     C                   parm      *blank        xxblds
     C                   seton                                        58
     C                   if        xxblcd <> *blank
     C                   move      xxblcd        d1blcd
     C                   endif
      *
      * Room
     C                   when      fld = 'D1RMCD'
     C                   call      'HP524'
     C                   parm                    d1fscd
     C                   parm                    d1blcd
     C                   parm      *blank        xxrmcd
     C                   seton                                        59
     C                   if        xxrmcd <> *blank
     C                   move      xxrmcd        d1rmcd
     C                   endif
     C                   other
      *
      * F4 not valid for this field...issue message
      *
     C                   if        error < maxmsg
     C                   add       1             error                                        013
     C                   movel     'HS09011'     mgid(e)
     C                   endif
      *
     C                   endsl
      *
     C                   endsr
      /EJECT
      *----------------------------------------------------------------
      * Write/update/delete record in database file
      *----------------------------------------------------------------
      *
     C     $upd1         begsr
      *
      * If the user is deleting,
      *   perform deletion integrity checks here if there are any
      *
      *
      * If user action is revising or deleting, determine that the
      * record he is accessing still exists prior to attempting to
      * update.  Lock the record for update.
      *
     C                   if        error = 0 and                                If OK
     C                             (action = delete or action = revise)
      *
     C     key01         chain     hsp102                             92
     C                   if        *in92 = *on                                  If not found
     C                   seton                                        62
     C                   if        error < maxmsg
     C                   add       1             error
     C                   eval      mgid(e) = 'HS09008'
     C                   endif
      *
     C                   endif                                                  If not found
     C                   endif                                                  If OK
      *
      * If there are no errors, delete, revise or create the record.
      *
     C                   if        error = 0                                    If no error
      *
     c                   select
     C                   when      action = delete
     C                   delete    rwrec
     C                   other
      *
      * Move DSPF fields to database file fields
      *
     C                   z-add     wkwedt        rwwedt
     C                   z-add     wkhgsn        rwhgsn
     C                   z-add     d1cdyr        rwcdyr
     C                   z-add     d1cdwk        rwcdwk
     C                   z-add     d1bginhd      rwbginhd
     C                   z-add     d1tihd        rwtihd
     C                   z-add     d1tohd        rwtohd
     C                   z-add     d1cuhd        rwcuhd
     C                   z-add     d1mkhd        rwmkhd
      *
     C                   if        action = create                              If create
     C                   write     rwrec
     C                   else
     C                   update    rwrec
     C                   endif                                                  If create
      *
     C                   endsl
     C                   endif                                                  If no error
      *
      * Set next routine
      *
     C                   select
     C                   when      mode = create                                Wh creating
     C                   if        error = 0
     C                   add       1             error
     C                   eval      mgid(e) = 'HS09040'
     C                   eval      routne = set1
     c                   else
     C                   eval      routne = scrn1
     C                   endif
      *
     C                   when      error <> 0                                   Wh errors
     C                   eval      routne = scrn1
     C                   other
      *
     C                   eval      routne = exit
     C                   endsl
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Write error messages
      *---------------------------------------------------------------
      *
     C     $wrmsg        begsr
      *
     C                   call      'UT80045J'                           43
     C                   parm                    mgid
     C                   parm                    mgdt
     C                   parm                    error
     C                   parm      sdpgm         pgm
     C                   parm      msgfil        msgfl
      *
      * If call to UT80045J failed, a message hardcoded in the DSPF
      * command line format will be set on. So, redisplay screen.
      *
     C                   if        *in43 = *on
     C                   eval      routne = set1
     C                   endif
      *
     C                   endsr
      *
      *----------------------------------------------------------------
      * Clear messages
      *----------------------------------------------------------------
      *
     C     $clmsg        begsr
      *
     C                   call      'UT80045J'                           43
     C                   parm      *blank        mgi
     C                   parm      *blank        mgd
     C                   parm      *zero         error
     C                   parm      sdpgm         pgm
     C                   parm      msgfil        msgfl
      *
      * If call to UT80045J failed, a message hardcoded in the DSPF
      * command line format will be set on; so, redisplay screen.
      *
     C                   if        *in43 = *on
     C                   eval      routne = set1
     C                   endif
      *
     C                   endsr
      /EJECT
      *----------------------------------------------------------------
      * Set on last record indicator and end job
      *----------------------------------------------------------------
      *
     C     $exit         begsr
     C                   seton                                        lr
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Parm Lists
      *
     C     *entry        plist
     C     d1mode        parm                    mode
     C                   parm                    xxfsbo
     C     d1hgcd        parm                    xxhgcd
     C     wkhgsn        parm                    xxhgsn
     C     d1cdyr        parm                    xxcdyr
     C     d1cdwk        parm                    xxcdwk
     C     d1wemdy       parm                    xxwemdy
     C     wkwedt        parm                    xxwedt
     C                   parm                    rtncd
      *
     C                   eval      action = mode
      *
      * Key lists
      *
     C     key01         klist
     C                   kfld                    wkhgsn
     C                   kfld                    d1cdyr
     C                   kfld                    d1cdwk
      *
      *
     C     key02         klist
     C                   kfld                    wkhgsn
     C                   kfld                    xxbegdt
      *
      *
     C     key03         klist
     C                   kfld                    d1fscd
     C                   kfld                    d1blcd
     C                   kfld                    d1rmcd
      *
     C     key04         klist
     C                   kfld                    d1fscd
     C                   kfld                    d1blcd
      *
      *
      * For the retrieval of date information from the OMS Calendar file,
      * you will need the 'company' associated with the incoming business office.
      * And, you must move it to a numeric field.
      *
     C     xxfsbo        chain     hsp009                             92
     C                   if        *in92 = *off                                 If found
     C                   movel     bococd        xxcocd
     C                   endif                                                  If found
      *
      *
      * The following standard code must be included to make the
      * standard error message handling program function properly.
      * This code sets and clears the program message queue.
      *
     C                   movel     '*'           msgpgm
     C                   exsr      $clmsg
      *
     C                   endsr
      /EJECT
