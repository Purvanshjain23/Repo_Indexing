/*********************************************************************/
/*     Program: AR60UPC                                              */
/*       Title: Prompt for printer overrides, then submit report.    */
/*        Date: 01/03/97                                             */
/* Description: - Calls print options prompt program                 */
/*              - Submits A/R Aging by CUSTOMER OR BY SALES ROUTE    */
/*                                                                   */
/*  Parameters:                                                      */
/*  Usage   Field       Field Description                            */
/*  -----   ---------   ------------------------------------------   */
/*    O     &RETURN     Return code                                  */
/*    I     &APPLIC     Application Code                             */
/*    I     &CLP        CLP Program                                  */
/*    I     &DESC       Print program description                    */
/*    B     &OUTQ       Output queue                                 */
/*    B     &HOLD       Hold status                                  */
/*    B     &SAVE       Save status                                  */
/*    B     &NQSTAT     Night queue flag                             */
/*    B     &NIGHTQ     Night job queue                              */
/*    B     &COMPNYN    Company number                               */
/*    B     &ARCSTNO    A/R customer number                          */
/*    B     &ASOFDAT    As of date                                   */
/*    B     &COPYN      Number of copies                             */
/*    B     &PRTZR      Print zero balances                          */
/*    B     &RPTTYP     Report type (1.Customer 2.Sales Route)       */
/*    B     &SLSRT      Sales route                                  */
/*    B     &ARCSTN     A/R customer name                            */
/*    B     &SLSRTN     Sales route name                             */
/*    B     &DTLSUM     Detail /Summary                              */
/*    B     &INCLCB     Include Chargebacks                          */
/*    B     &INCLRB     Include REBILLS        E5262 JBB             */
/*    B     &PARENT     PARENT CUSTOMER                              */
/*    B     &PARNAM     PARENT CUSTOMER NAME                         */
/*    B     &INVS1,2,3,4,5    INVOICE SUFFIX                         */
/*                                                                   */
/*********************************************************************/
/* Modifications:                                                    */
/*                                                                   */
/*   Date     Pgmr     Description                                   */
/* -------- ---------- --------------------------------------------  */
/* 03/11/98 ISBLAWS    Removed customer type parameter and added     */
/*                     include credits parameter.                    */
/*                                                                   */
/* 02/21/02 ISRCENT    ADDED DETAIL/SUMMARY PARM AND INCLUDE         */
/*                     CHARGEBACKS PARM.                             */
/*                                                                   */
/* 10/24/02 ISRCENT    ADDED PARENT CUSTOMER PARM AFTER SALES ROUTE  */
/* 01/13/04 ISRCENT    ADDED 5 INVOICE SUFFIX PARMS                  */
/* 03/06/06 ISLRAMS    ADDED 'PRINT LAST PAGE ONLY' PARM ON THE      */
/*                     CALL TO AR600CLP.                             */
/* 11/22/06 ISLBUSE    ADDED A/R AGING CODE PARM AND PASS TO         */
/*                     SUBMITTED PROGRAM-AR600CLP                    */
/* 06/19/09 ISLBUSE    ADDED WATCH FLAG PARM TO PASS TO AR600CLP     */
/*                     FOR 2 NEW REPORT PROGRAMS: PNJWPFR AND        */
/*                     PNJ7PFR                                       */
/* 06/08/16 ISJBADE    ADDED INCLUDE REBILL PARAMETER AND PASSED IT  */
/*                     INTO  SUBMITTED JOBS.                         */
/*********************************************************************/
             PGM        PARM(&RETURN &APPLIC &CLP &DESC &OUTQ &HOLD +
                          &SAVE &NQSTAT &NIGHTQ &COMPNYN &ARCSTNO +
                          &ASOFDAT &COPYN &PRTZR &WATCH &DTLSUM +
                          &SLSRT &PARENT &ARCSTN &SLSRTN &RPTTYP +
                          &INCLCB &INCLRB &PARNAM &INVS1 &INVS2 +
                          &INVS3 &INVS4 &INVS5 &AGECODE)

   /*-------------------*/
   /* Declare Variables */
   /*-------------------*/

             /* Parameter Fields */
             DCL  VAR(&LAST)    TYPE(*CHAR) LEN(1)
             DCL  VAR(&RETURN)  TYPE(*CHAR) LEN(7)
             DCL  VAR(&APPLIC)  TYPE(*CHAR) LEN(06)
             DCL  VAR(&CLP)     TYPE(*CHAR) LEN(10)
             DCL  VAR(&DESC)    TYPE(*CHAR) LEN(25)
             DCL  VAR(&OUTQ)    TYPE(*CHAR) LEN(10)
             DCL  VAR(&HOLD)    TYPE(*CHAR) LEN(4)
             DCL  VAR(&SAVE)    TYPE(*CHAR) LEN(4)
             DCL  VAR(&NQSTAT)  TYPE(*CHAR) LEN(01)
             DCL  VAR(&NIGHTQ)  TYPE(*CHAR) LEN(10)
             DCL  VAR(&COMPNYN) TYPE(*DEC)
             DCL  VAR(&ARCSTNO) TYPE(*DEC)
             DCL  VAR(&ASOFDAT) TYPE(*DEC)
             DCL  VAR(&COPYN)   TYPE(*DEC)
             DCL  VAR(&PARENT) TYPE(*DEC)
             DCL  VAR(&PRTZR)   TYPE(*CHAR) LEN(1)
             DCL  VAR(&RPTTYP)  TYPE(*CHAR) LEN(1)
             DCL  VAR(&SLSRT)   TYPE(*CHAR) LEN(3)
             DCL  VAR(&ARCSTN)  TYPE(*CHAR) LEN(30)
             DCL  VAR(&SLSRTN)  TYPE(*CHAR) LEN(30)
             DCL  VAR(&PARNAM)  TYPE(*CHAR) LEN(30)
             DCL  VAR(&DTLSUM)   TYPE(*CHAR) LEN(1)
             DCL  VAR(&INCLCB)  TYPE(*CHAR) LEN(1)
             DCL  VAR(&INCLRB)  TYPE(*CHAR) LEN(1)     /* E5262 */
             DCL  VAR(&INVS1)  TYPE(*CHAR) LEN(02)
             DCL  VAR(&INVS2)  TYPE(*CHAR) LEN(02)
             DCL  VAR(&INVS3)  TYPE(*CHAR) LEN(02)
             DCL  VAR(&INVS4)  TYPE(*CHAR) LEN(02)
             DCL  VAR(&INVS5)  TYPE(*CHAR) LEN(02)
             DCL  VAR(&AGECODE) TYPE(*CHAR) LEN(6)
             DCL  VAR(&WATCH) TYPE(*CHAR) LEN(2)

             /* Internal Work Fields */
             DCL  VAR(&COPY)    TYPE(*DEC)  LEN(2 0)
             DCL  VAR(&APPL)    TYPE(*CHAR) LEN(06)
             DCL  VAR(&MSG)     TYPE(*CHAR) LEN(512)
             DCL  VAR(&MSGID)   TYPE(*CHAR) LEN(7)
             DCL  VAR(&MSGL)    TYPE(*DEC)  LEN(5 0)
             DCL  VAR(&COPY)    TYPE(*DEC)  LEN(2 0)

   /*----------------------*/
   /* Initialize Variables */
   /*----------------------*/
             CHGVAR VAR(&RETURN) VALUE(' ')
             CHGVAR VAR(&LAST)   VALUE(' ')
             CHGVAR VAR(&COPY)   VALUE(&COPYN)
             CHGVAR VAR(&APPL)   VALUE(&APPLIC)

   /*----------------------------------------------*/
   /* Monitor for all message at the program level */
   /*----------------------------------------------*/
             MONMSG MSGID(CPF0000) EXEC(GOTO CMDLBL(MSGTRAP))

   /*-----------------------------------*/
   /* Call Print Options Prompt Program */
   /*-----------------------------------*/
             CALL PGM(PDG0PVR) +
                  PARM(&RETURN &DESC &OUTQ &HOLD +
                       &SAVE &COPY &NQSTAT &NIGHTQ &APPL)
             CHGVAR VAR(&COPYN) VALUE(&COPY)
             IF COND(&RETURN *NE ' ') THEN(DO)
                GOTO CMDLBL(USERQUIT)
             ENDDO

   /*------------------------------------*/
   /* Submit AR600CLP CL to Print Report */
   /*------------------------------------*/
             SBMJOB     CMD(CALL PGM(AR600CLP) PARM(&COMPNYN +
                          &ASOFDAT &ARCSTNO &ARCSTN &PARENT &PARNAM +
                          &PRTZR &RPTTYP &SLSRT &SLSRTN &DTLSUM +
                          &INCLCB &INCLRB &INVS1 &INVS2 &INVS3 +
                          &INVS4 &INVS5 &OUTQ &HOLD &SAVE &COPYN +
                          &NIGHTQ &LAST &AGECODE &WATCH)) +
                          JOB(&CLP) JOBQ(&NIGHTQ)
   /*----FOR TESTING ONLY----------------*/
     /*      CALL       PGM(AR600CLP) PARM(&COMPNYN &ASOFDAT +
                          &ARCSTNO &ARCSTN &PARENT &PARNAM &PRTZR +
                          &RPTTYP &SLSRT &SLSRTN &DTLSUM &INCLCB +
                          &INCLRB &INVS1 &INVS2 &INVS3 &INVS4 +
                          &INVS5 &OUTQ &HOLD &SAVE &COPYN &NIGHTQ +
                          &LAST &AGECODE &WATCH)    */
             GOTO CMDLBL(END)

   /*-----------*/
   /* User Quit */
   /*-----------*/
 USERQUIT:
             CHGVAR VAR(&RETURN) VALUE('Y2U0005')
             GOTO CMDLBL(END)

   /*------------------------------*/
   /* Trap errors and send message */
   /*------------------------------*/
 MSGTRAP:
             RCVMSG MSGTYPE(*EXCP) MSGDTA(&MSG) MSGDTALEN(&MSGL) +
                    MSGID(&MSGID)
             MONMSG MSGID(CPF0000) EXEC(GOTO CMDLBL(END))
             SNDPGMMSG MSGID(&MSGID) MSGF(QCPFMSG) +
                       MSGDTA(%SST(&MSG 1 &MSGL)) +
                       MSGTYPE(*ESCAPE)
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(END))

   /*----------------*/
   /* End of Program */
   /*----------------*/
 END:
             ENDPGM
