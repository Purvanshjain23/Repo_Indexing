      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Triumph Foods
      * PROGRAM:     TF618
      * TITLE:       Margin: ByProduct and NVA Volume Adjustment Report
      * PROGRAMMER:  LeAnne Ramsey
      * CREATED:     08/29/08
      *
      * FUNCTION:  Batch program to print the report.
      *            Initially, this report was only for BP6 Byproducts.
      *
      *            In November 2008, we added Neckbones...which are not
      *            Byproducts; they are NVA.
      *
      *            In September 2009, we added Tongue Base...which are
      *            Byproducts...but, they are OFF instead of BP6.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 09/10/08  LeAnne Ramsey
      *           Per Tom Dye: If Cold Carcass Pounds are zero, print a
      *           message only..no data. Since no Carcass Pounds means no
      *           good data, he does not want folks to think that everything
      *           is OK.
      *
      * 11/26/08  LeAnne Ramsey
      *           Added logic for Neckbones...which are NVAs..not ByProducts.
      *           So, we changed the title and also changed the listing to
      *           sort/print "BP6" and "NVA" on the report.
      *
      * 11/28/08  LeAnne Ramsey
      *           As part of synchronizing the LDAs between the TFS Margin Adjustment Close
      *           and the Meat Costing, we changed the LDA positions
      *
      * 12/02/08  LeAnne Ramsey
      *           Recompile only.
      *
      * 08/31/09  LeAnne Ramsey
      *           Damon Ginther requested that we include OFF as well as BP6. He wants
      *           Tongue Base (BP/OFF/740/741/765) to show up on TF618-ByProduct and
      *           NVA Volume Adjustment Report. So, we will now need to include OFF in
      *           our logic. (As of now, this Tongue Base is the only OFF that he has
      *           set up as Volume = Yes.)
      *
      * 09/02/09  LeAnne Ramsey
      *           Damon Ginther wants the Equivalent Net Produced Price/Lb for OFF
      *           to equal TRGA. So, we have conditioned printing "TRGA Price/Lb" for
      *           this. (See Indicator 85).
      *
      * 12/21/09  LeAnne Ramsey
      *           Damon Ginther wants the Scruff Jowls (700/701/727) to be based on
      *           TRGA instead of BNIN. So, we changed the indicator that controls the
      *           print of this.
      *
      * 11/30/11  LeAnne Ramsey (E1827)
      *           Damon Ginther wants the Equivalent Net Produced Price/Lb for
      *           Kidneys (740/741/771) to equal BNIN....not TRGA.
      *
      * 05/02/12  LeAnne Ramsey (E2077)
      *           Damon Ginther is having us add BP1 Riblets to the logic. The
      *           calc is a combination of BNIN and T72A. So, I added
      *           conditioning to print this on l1dtl.
      *
      * 07/24/13  LeAnne Ramsey (E2693)
      *           Per Damon Ginther, we changed (in RO233) the Equivalent Net Produced Price/Lb
      *           calc for Offal Pork Ears (740/741/777) to be like Offal Kidneys (740/741/771).
      *           So, in this program, I have changed the indicator for Pork Ears to be 87.
      *‚12/11/24  Jagdish Mistry JM-S003543
      *‚          Damon Ginther wants the Equivalent Net Produced Price/Lb for OFF-Snouts
      *‚          to equal BNIN....not TRGA.
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Frop125    if   e           k disk
      *  TF ByBroduct volume
      *
      *
     Fprint198  o    f  198        printer oflind(*inof)
      *
      /EJECT
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
     D cclbmsg1        c                   Const('The data for this report is -
     D                                     built in the Meat Costing function.')
     D cclbmsg2        c                   Const('At the time it was built, no -
     D                                     Cold Carcass Pounds had been -
     D                                     entered for this week.')
     D cclbmsg3        c                   Const('Verify that -
     D                                     Cold Carcass Pounds are entered; -
     D                                     rerun the Meat Costing function; -
     D                                     try the report again.')
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Standard fields
      *
     d rtime           s              6  0
      *
      *
      * Control fields
      *
     D first           s              1    inz('Y')
     D cclbfl          s              1
     D count           s              5  0
      *
      *
      * Print fields
      *
     d h1demand        s             35
     d t1sduam         s                   like(yvsduam)
      *
      ****************************************************************
      * ARRAYS AND TABLES
      ****************************************************************
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *
      *---------------------------------------------------------------
      * Local data area.
      *---------------------------------------------------------------
      *
     Dlda             uds                  dtaara(*lda)
     D  ldpfcd                 1      1
     D  ldyr                   2      5  0
     D  ldwk                   6      7  0
      *
     D  ldwedt                29     36  0
     D  ldwemdy               44     49  0
      *
     D  ldtfclcd             156    158
     D  ldtfclds             159    188
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ***************************************************************************************
      * MAINLINE
      ***************************************************************************************
      *
      * Read the ByProduct Volume records created in the Meat Costing function.
      * Print the record (if it match's the user's selection.)
      *
     C     ldwedt        setll     rop125
     C                   dou       *in90 = *on                                  Main do
     C     ldwedt        reade     rop125                                 90
     C                   if        *in90 = *off and                             If not EOF
     C                             (ldtfclcd = *blank or
     C                             ldtfclcd = yvtfclcd)
      *
     C                   if        first = yes                                  If first
     C                   move      no            first
      *
     C                   if        yvacclb = 0
     C                   move      no            cclbfl
     C                   else
     C                   move      yes           cclbfl
     C                   exsr      $h1hdr
     C                   endif
     C                   endif                                                  If first
      *
      * If you have Cold Carcass pounds, print data.
      *
     C                   if        cclbfl = yes
     C                   exsr      $l1dtl
     C                   endif
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Main do
      *---------------
      * EOF processing
      *---------------
     C                   select
     C                   when      first = yes
     C                   seton                                        89
     C                   exsr      $h1hdr
     C                   except    nodata
      *
     C                   when      cclbfl = no
     C                   seton                                        89
     C                   exsr      $h1hdr
     C                   except    nocclb
      *
     C                   other
     C                   except    t1tot
     C                   endsl
      *
     C                   seton                                        lr
      /EJECT
      *---------------------------------------------------------------------------------------
      * Print the record
      *---------------------------------------------------------------------------------------
      *
     C     $l1dtl        begsr
      *
      * Accumulate "due" amount for Report Total
     C                   add       yvsduam       t1sduam
      *
      * This function prints up to 5 records on a page (kinda an EXCEL type layout).
      * So, see if you need to force a page break.
      *
     C                   add       1             count
     C                   if        count > 5
     C                   z-add     1             count
     C                   seton                                        of
     C                   endif
      *
      * Print Pounds Conditionally:
      *   NVA: Produced Start Pounds
      *   BP6: Produced Pounds
      *
     C                   if        yvtfclcd = 'NVA'
     C                   seton                                        86
     C                   else
     C                   setoff                                       86
     C                   endif
      *
      * Print specific "text" conditionally:
      *
     C                   setoff                                       84
     C                   setoff                                       858788
     C                   select
      *      Kidneys
     C                   when      yvistycd = 740 and
     C                             yvisgrcd = 741 and
     C                             yvisclcd = 771
     C                   seton                                        87
      *      Pork Ears
     C                   when      yvistycd = 740 and
     C                             yvisgrcd = 741 and
     C                             yvisclcd = 777
     C                   seton                                        87
     ‚* JM-S003543-START
     ‚* Use BNIN for Snouts
     C                   when      yvistycd = 740 and
     C                             yvisgrcd = 741 and
     C                             yvisclcd = 768
     C                   seton                                        87
     ‚* JM-S003543-End
      *       Offal
     C                   when      yvtfclcd = 'OFF'
     C                   seton                                        85
      * Scruff Jowls
     C                   when      yvistycd = 700 and
     C                             yvisgrcd = 701 and
     C                             yvisclcd = 727
     C                   seton                                        85
      *       Jowls
     C                   when      yvistycd = 700 and
     C                             yvisgrcd = 701 and
     C                             yvisclcd = 701
     C                   seton                                        88
      *       Riblets
     C                   when      yvistycd = 200 and
     C                             yvisgrcd = 850 and
     C                             yvisclcd = 854
     C                   seton                                        84
     C                   other
      *       Other
     C                   seton                                        87
     C                   endsl
      *
      *
     C                   if        *inof = *on
     C                   exsr      $h1hdr
     C                   endif
      *
     C                   except    l1dtl
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Print headings
      *---------------------------------------------------------------
      *
     C     $h1hdr        begsr
      *
     C                   except    h1hdr
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Retrieve time for report heading.
      *
     C                   time                    rtime
      *
      * This report is generated on-demand and also from the
      * Margin Adjustment Close function.
      *
     C                   select
     C                   when      ldpfcd = 'D'
     C                   eval      %subst(h1demand:13:9) = 'On-Demand'
      *
     C                   when      ldpfcd = 'F'
     C                   eval      %subst(h1demand:4:29) =
     C                             'Final Margin Adjustment Close'
      *
     C                   when      ldpfcd = 'P'
     C                   eval      %subst(h1demand:1:35) =
     C                             'Preliminary Margin Adjustment Close'
     C                   endsl
      *
      *
      * When the report is generated on-demand, the user can enter selections.
      *
      * TF classification code
      *
     C                   if        ldtfclcd = *blank
     C                   seton                                        96
     C                   endif
      *
     C                   endsr
      /EJECT
      *-------------------------------------------------------------
      * Report heading lines
      *-------------------------------------------------------------
      *
     Oprint198  e            h1hdr          1 01
     O                       sdpgm               10
     O                                           82 'Byproduct and NVA Volume'
     O                                          100 ' Adjustment Report'
     O                                          155 'DATE'
     O                       udate         y    165
      *
     O          e            h1hdr          1
     O                       sdusr               10
     O                       h1demand            98
     O                                          155 'TIME'
     O                       rtime              165 '  :  :  '
      *
     O          e            h1hdr          1
     O                                          155 'PAGE'
     O                       page          z    165
      *
     O          e            h1hdr          1
     O                                           23 'Week-ending date:'
     O                       ldwemdy             33 '  /  /  '
      *
     O          e            h1hdr          1
     O                                           23 'Year/week:'
     O                       ldyr          z     29
     O                       ldwk          z     33
      *
     O          e            h1hdr          1
     O                                           23 'TF classification code:'
     O              n96      ldtfclcd            28
     O               96                          28 'All'
     O              n96      ldtfclds            64
      *
      *
      *
     O          e   n89      h1hdr          1
     O                                           84 'TOTAL'
     O                                          106 'SEABOARD'
     O                                          128 'TRIUMPH'
      *
     O          e   n89      h1hdr          1
     O                                           84 '--------------'
     O                                          106 '--------------'
     O                                          128 '--------------'
     O          e   n89      h1hdr          1
     O                                           61 'Cold Carcass Pounds'
     O                       yvacclb       k     85
     O                       yvscclb       k    107
     O                       yvtcclb       k    129
      *
     O          e   n89      h1hdr          1
     O                                           46 'AGG Net Produced Price/LB '
     O                                           61 'Inedible (BNIN)'
     O                       yvbninpr      k     85
      *
     O          e   n89      h1hdr          1
     O                                           46 'AGG Net Produced Price/LB '
     O                                           61 'Special  (T72A)'
     O                       yvt72apr      k     85
      *
     O          e   n89      h1hdr          3
     O                                           46 'AGG Net Produced Price/LB '
     O                                           61 'Regular  (TRGA)'
     O                       yvtrgapr      k     85
      *
      *-------------------------------------------------------------
      * Detail lines
      *-------------------------------------------------------------
      *
     O          e            l1dtl       0  0
     O                       yvtfclcd             3
     O                       yvisclds            35
     O                                           84 'TOTAL'
     O                                          106 'SEABOARD'
     O                                          128 'TRIUMPH'
     O                                          150 'IS Type/Group/Class'
      *
     O          e            l1dtl          1
     O                       yvtfclcd             3
     O                       yvisclds            35
     O                                           84 'TOTAL'
     O                                          106 'SEABOARD'
     O                                          128 'TRIUMPH'
     O                                          150 'IS Type/Group/Class'
      *
     O          e            l1dtl          0
     O                                           84 '--------------'
     O                                          106 '--------------'
     O                                          128 '--------------'
      *
     O          e            l1dtl          1
     O                                           84 '--------------'
     O                                          106 '--------------'
     O                                          128 '--------------'
      *
     O          e            l1dtl          1
     O              n86                          41 'Produced Pounds'
     O               86                          41 'Produced Start Weight'
      *    For BP6
     O              n86      yvapulb       k     85
     O              n86      yvspulb       k    107
     O              n86      yvtpulb       k    129
      *    For NVA
     O               86      yvapuslb      k     85
     O               86      yvspuslb      k    107
     O               86      yvtpuslb      k    129
      *
     O                       yvistycd      z    138
     O                       yvisgrcd      z    143
     O                       yvisclcd      z    148
      *
     O          e            l1dtl          1
     O                                           41 'AGG Net Produced Price/Lb'
     O                       yvanpupr      k     85
      *
     O          e            l1dtl          1
     O                                           33 'Equivalent Net Produced '
     O                                           41 'Price/Lb'
     O                       yveqnpupr     k     85
     O               85                         145 'TRGA Price/lb'
     O               87                         145 'BNIN Price/lb'
     O               88                         154 'BNIN & TRGA calculation'
     O               84                         154 'T72A & BNIN calculation'
      *
     O          e            l1dtl          1
     O                                           41 'Margin/lb'
     O                       yvmgpr        k     85
      *
     O          e            l1dtl          2
     O                                           41 'Total Margin Amount'
     O                       yvamgam       k     85
     O                       yvsmgam       k    107
     O                       yvtmgam       k    129
      *
     O          e            l1dtl          1
     O                                           24 'Avg Increm Margin/lb '
     O                                           41 'of Carcass Weight'
     O                       yvincmgpr     k     85
      *
     O          e            l1dtl          2
     O                                           28 'Adjusted Triumph Increm '
     O                                           41 'Margin Amount'
     O                       yvincmgam     k     85
      *
     O          e            l1dtl          2
     O                                           33 'Volume Adjustment: '
     O                                           41 'Seaboard'
     O                       yvsduam       k     58
      *
      *-------------------------------------------------------------
      * Total line
      *-------------------------------------------------------------
      *
     O          e            t1tot
     O                                           33 'Total Volume Adjustment: '
     O                                           41 'Seaboard'
     O                       t1sduam       k     58
      *
      *-------------------------------------------------------------
      * No data message line
      *-------------------------------------------------------------
      *
     O          e            nodata      1
     O                                           19 'No data meets your'
     O                                           39 'selection criteria.'
      *
      *-------------------------------------------------------------
      * No Cold Carcass Pounds
      *-------------------------------------------------------------
      *
     O          e            nocclb      2
     O                                           +1 cclbmsg1
     O          e            nocclb      1
     O                                           +1 cclbmsg2
     O          e            nocclb      1
     O                                           +1 cclbmsg3
