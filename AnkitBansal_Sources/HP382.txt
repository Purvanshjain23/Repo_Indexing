      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Hog Production
      * PROGRAM:     HP382
      * TITLE:       Workfile: Hog Group Sales for Rupture Analysis DWL
      * PROGRAMMER:  LeAnne Ramsey
      * CREATED:     08/08/11
      *
      * FUNCTION: This batch program is kicked off from the AS400 Job Scheduler.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 06/18/12  LeAnne Ramsey
      *           John Zimmerman asked that we temporarily rebuild data back
      *           to 11/20/11....instead of the standard 7 days back.
      *           So, I changed the FromDate logic and installed.
      *
      * 07/05/12  LeAnne Ramsey
      *           John Zimmerman says it is OK to set this back to the standard 7 days.
      *
      * 10/15/13  LeAnne Ramsey (E2831)
      *           Recompile only. Added field 'MTech Reference'.
      *
      * 11/03/20  ISE           (H16853)
      *           Recompile only. Increased Buy order field length from 5 to 7.
      /EJECT
      ****************************************************************
      * FILE
      ****************************************************************
      *
     Fpka1cpla  if   e           k disk
      *  Tattoo Header
      *
      *
     Fhsp018    if   e           k disk
      *  Farm site
      *
      *
     Fhsl034d   if   e           k disk
      *  Hog group
      *
      *
     Fhsl064h   if   e           k disk
      *  Check detail
      *
      *
     Fhsp084    if   e           k disk
      *  Sales movement header
      *
      *
     Fhsp382    o    e           k disk
      *    Workfile: Hog Group Sales for Rupture Analysis DWL
      *
      /EJECT
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      ****************************************************************
      * ARRAYS AND TABLES
      ****************************************************************
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Company
      *
     D wkcono          s                   like(a1hmnb) inz(360)
      *
      *
      * Workfields for date manipulation
      *
     D wkisodate       s               d   datfmt(*iso)
     D wksysdt         s              8  0
     D wksysdt7        s              7  0
     D wkfrdt          s              8  0
     D wkfrdt7         s              7  0
      *
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * Mainline
      ****************************************************************
      *
      * We are going to drive the retrieval of the Seaboard sales from the
      * HPS side using the Check Detail file. These are our "internal" hogs.
      *
     C                   exsr      $internal
      *
      * Now, retrieve the Tattoo Header records for the external Producers.
      *
     C                   exsr      $external
      *
     C                   seton                                        lr
      /EJECT
      *---------------------------------------------------------------
      * Seaboard hogs
      *---------------------------------------------------------------
      *
     C     $internal     begsr
      *
      * Process each record in the Check Detail file for the Kill Date Range
      * where the Customer is Seaboard.
      *
     C     wkfrdt        setll     hsl064h
      *
     C                   dou       *in90 = *on or cdkldt > wksysdt              Do Internals
     C                   read      hsl064h                                90
     C                   if        *in90 = *off and cdkldt <= wksysdt           If Seaboard
     C                             and cdcvno = 360516
      *
      * Retrieve Load Type and Farm Site from Sales Header...
      * Only continue processing if this is a Market Sale.
      *
     C     cdmvsn        chain     hsp084                             92
     C                   if        *in92 = *off and shstcd = 'MRKTS'            If Market Sale
     C                   move      shltcd        w1ltcd
     C                   z-add     shfscd        w1fscd
      *
     C                   z-add     wksysdt       w1sysdt
     C                   z-add     cdkldt        w1kldt
     C                   z-add     cdtano        w1tano
     C                   z-add     cdmvsn        w1mvsn
     C                   z-add     cdhgsn        w1hgsn
     C     cdlvhd        add       cddohd        w1arhd
      *
      * Retrieve Pod for Nur/Fin Farm Site
      *
     C     w1fscd        chain     hsp018                             92
     C                   if        *in92 = *off
     C                   z-add     fspod         w1pod
     C                   endif
      *
      *
      * Retrieve Hog Group info
      *
     C     cdhgsn        chain     hsl034d                            92
     C                   if        *in92 = *off
     C                   z-add     hgobfs        w1obfs
     C                   move      hgblcd        w1blcd
     C                   move      hghgcd        w1hgcd
     C                   move      hggncd        w1sicd
     C                   move      hgsxcd        w1sxcd
     C                   endif
      *
      * Retrieve Pod for Origin BGF Farm Site
      *
     C     w1obfs        chain     hsp018                             92
     C                   if        *in92 = *off
     C                   z-add     fspod         w1obpod
     C                   endif
      *
      * Set Producer equal to Seaboard
      *
     C                   eval      w1prod = 'SEABOA'
      *
      * Write a Workfile record
      *
     C                   exsr      $wrt382
      *
     C                   endif                                                  If Market Sale
     C                   endif                                                  If Seaboard
     C                   enddo                                                  Do Internals
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Retrieve Tattoo Header records for External Producers
      *---------------------------------------------------------------
      *
     C     $external     begsr
      *
      * Process each record in the Tattoo Header file for the Kill Date Range
      * for External Producers (ie: Not Seaboard). We are only doing this to
      * get the Producer Code. Our workfile records for these External Producers
      * will not have any other info.
      *
     C     key01         setll     pka1cpla
      *
     C                   dou       *in90 = *on or a1b0dt > wksysdt7             Do external
     C     wkcono        reade     pka1cpla                               90
     C                   if        *in90 = *off and a1b0dt <= wksysdt7          If external
     C                             and a1kast = 'E'
      *
     C                   z-add     wksysdt       w1sysdt
     C                   move      a1abcd        w1prod
     C                   z-add     a1cvnb        w1tano
      *
     C     *cymd         move      a1b0dt        wkisodate
     C                   move      wkisodate     w1kldt
      *
     C                   exsr      $wrt382
      *
     C                   endif                                                  If external
     C                   enddo                                                  Do external
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Write a Workfile record
      *---------------------------------------------------------------
      *
     C     $wrt382       begsr
      *
     C                   write     w1rec
     C                   clear                   w1rec
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Initialization
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Key lists
      *
     C     key01         klist
     C                   kfld                    wkcono
     C                   kfld                    wkfrdt7
      *
      *
      * Retrieve System Date
      *
     C     *mdy          move      udate         wkisodate
     C                   move      wkisodate     wksysdt
     C     *cymd         move      wkisodate     wksysdt7
      *
      * We want a date range for our extraction. So, subtract 7 days from the
      * system date.
     C                   subdur    7:*days       wkisodate
     C                   move      wkisodate     wkfrdt
     C     *cymd         move      wkisodate     wkfrdt7
      *
     C                   endsr
      /EJECT
