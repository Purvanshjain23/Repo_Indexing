      *****************  RPG PROGRAM HEADING  ************************
      * SYSTEM:      HOG PRODUCTION
      * PROGRAM:     HP380
      * TITLE:       BUILD WORKFILE FOR SALES ACTIVITY REPORTS
      * PROGRAMMER:  ROSE CENTONZE
      * CREATED:     6/17/94
      *
      * FUNCTION:    THIS PROGRAM BUILDS A WORKFILE TO BE READ IN:
      *              HP680  SALES ACTIVITY REPORT
      *              THIS PROGRAM IS SUBMITTED FROM HP480CL
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 01/11/99  LeAnne Fedor
      *           Recompile only. New field 'scheduled kill date' added
      *           to sales movement header file.
      *
      * 10/17/00  LeAnne Fedor
      *           Recompile only. New field 'load type' added to the sales
      *           movement head file.
      *
      * 06/28/01  LeAnne Fedor
      *           Recompile only.
      *           Manager and supervisor fields removed from Hog Group file.
      *
      * 12/18/02  LeAnne Fedor
      *           Production Phase/Type were removed from Sales Movement Header file.
      *           Modified logic here to use group production phase/type.
      *
      * 07/06/09  LeAnne Ramsey
      *           Recompile only. Added new field 'Continuous Flow Flag' to Hog Group file.
      /eject
      ****************************************************************
      *
     FHSL027A   IF   E           K DISK
      *    IOWA TOP MARKET PRICE
      *
      *
     FHSL034D   IF   E           K DISK
      *    HOG GROUP
      *
      *
     FHSL064B   IF   E           K DISK
      *    CHECK DETAIL   (records selected in OPNQRY)
      *
      *
     FHSP084    IF   E           K DISK
      *  SALE HEADER      (records selected in OPNQRY)
      *
      *
     FHSP085    IF   E           K DISK
      *  SALE DETAIL      (records selected in OPNQRY)
      *
      *
     FHSP380    O    E             DISK
      *  SALES ACTIVTY REPORT WORKFILE
      *
      /EJECT
      ****************************************************************
      * Definition Specifications
      ****************************************************************
      *
      *----------------------------------------------------------------
      * LDA
      *----------------------------------------------------------------
      *
     D                UDS
     D  LDFSBO                 1      5
     D  LDFYMD                 6     13  0
     D  LDTYMD                14     21  0
     D  LDPTCD                22     26
     D  LDPPCD                27     31
     D  LDSTCD                32     36
     D  LDFSCD                37     41  0
     D  LDCVNO                42     49  0
     D  LDHGSN                50     56  0
     D  LDRPFL                57     57
      *
     D  LDBODS                52     87
     D  LDFSNM                88    112
     D  LDHGCD               113    119
     D  LDCNAM               120    149
     D  LDSTDS               150    169
     D  LDPPDS               170    199
     D  LDFMDY               200    205  0
     D  LDTMDY               206    211  0
     D  LDPTDS               212    221
      *
     D  LDBSCD               266    270
     D  LDBSDS               271    290
      *
      *
      *---------------------------------------------------------------
      * STANDARD PROGRAM STATUS DATA STRUCTURE
      *---------------------------------------------------------------
      *    EXTERNALLY DEFINED AS UTPGFR (RECORD FORMAT: PGMDSR)
     D PGMDS         ESDS                  EXTNAME(UTPGFR)
      *
      *---------------------------------------------------------------
      *  NAMED CONSTANTS
      *---------------------------------------------------------------
      *
     D YES             C                   CONST('Y')
     D NO              C                   CONST('N')
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * MAIN LINE
      ****************************************************************
      *
      * PROCESS EACH SALES MOVEMENT SELECTED BY THE OPEN QUERY
      *
     C     *LOVAL        SETLL     HSP084
      *
     C     *IN90         DOUEQ     *ON                                          MAIN DO
     C                   READ      HSP084                                 90
     C     *IN90         IFEQ      *OFF                                         IF NOT EOF
      *
      * PROCESS EACH SALES DETAIL RECORD SELECTED BY THE OPEN QUERY.
      *
     C     SHMVSN        SETLL     HSP085
      *
     C     *IN91         DOUEQ     *ON                                          DO DETAIL
     C     SHMVSN        READE     HSP085                                 91
     C     *IN91         IFEQ      *OFF                                         IF DETAIL
      *
      *
      * RETRIEVE THE GROUP INFORMATION AND COMPARE TO USER'S SELECTIONS.
      *
     c                   exsr      $hgcd
      *
      * Process record
      *
     C     PROCFL        IFEQ      YES                                          IF PROCESS
     C                   MOVEL     HGBSCD        WFBSCD
     C                   MOVEL     HGHGCD        WFHGCD
     C                   MOVEL     HGPTCD        WFPTCD
     C                   MOVEL     HGPPCD        WFPPCD
     C                   Z-ADD     HGOPDT        WFOPDT
      *
      * READ EACH CHECK DETAIL RECORD ASSOCATED WITH THIS
      * MOVEMENT/LINE NUMBER AND WRITE A WORKFILE RECORD
      * FOR EACH. (AN OPEN QUERY WAS USED TO SELECTED THE
      * PAYMENT TYPE SPECIFIED BY THE USER.)
      *
     C     K1L064        SETLL     HSL064B
     C     *IN93         DOUEQ     *ON                                          DO CHECKS
     C     K1L064        READE     HSL064B                                93
     C     *IN93         IFEQ      *OFF                                         IF CHECKS
     C                   EXSR      $WRITE
     C                   ENDIF                                                  IF CHECKS
     C                   ENDDO                                                  DO CHECKS
      *
     C                   ENDIF                                                  IF PROCESS
     C                   ENDIF                                                  IF DETAIL
     C                   ENDDO                                                  DO DETAIL
      *
     C                   ENDIF                                                  IF NOT EOF
     C                   ENDDO                                                  MAIN DO
      *
     C                   SETON                                        LR
      /EJECT
      *----------------------------------------------------------------
      * WRITE WORKFILE RECORD
      *----------------------------------------------------------------
      *
     C     $WRITE        BEGSR
      *
      * SALES HEADER
      *
     C                   MOVEL     SHFSBO        WFFSBO
     C                   Z-ADD     SHFSCD        WFFSCD
     C                   Z-ADD     SHMVSN        WFMVSN
     C                   MOVEL     SHSTCD        WFSTCD
     C                   Z-ADD     SHCVNO        WFCVNO
     C                   Z-ADD     SHRCDT        WFRCDT
      *
      * SALES DETAIL
      *
     C                   Z-ADD     SGHGSN        WFHGSN
      *
      * CHECK DETAIL
      *
     C                   MOVEL     CDCHDT        WFCHDT
     C                   MOVEL     CDCHNO        WFCHNO
     C                   MOVEL     CDLNNO        WFLNNO
     C                   MOVEL     CDTPCD        WFTPCD
     C                   Z-ADD     CDLVHD        WFLVHD
      *
      * CONVERT LIVE POUNDS INTO THE HUNDERD WEIGHT VALUE
      *
     C                   Z-ADD     CDLVLB        WFLVLB
     C     WFLVLB        DIV(H)    100           WFLVCW
      *
     C                   Z-ADD     CDDOHD        WFDOHD
     C                   Z-ADD     CDDOLB        WFDOLB
     C                   Z-ADD     CDGRAM        WFGRAM
     C                   Z-ADD     CDTANO        WFTANO
     C                   Z-ADD     CDCRLB        WFCRLB
      *
      * CONVERT CARCASS POUNDS INTO THE HUNDERD WEIGHT VALUE
      *
     C     WFCRLB        DIV(H)    100           WFCRCW
      *
     C                   Z-ADD     CDBAAM        WFBAAM
     C                   Z-ADD     CDBFAM        WFBFAM
      *
      * CALCULATE LEAN PERCENT AMOUNT
      *
     C                   Z-ADD     CDLNPR        WFLNPR
     C     WFLNPR        MULT(H)   WFCRCW        WFLNAM
      *
      * LOIN EYE
     C                   Z-ADD     CDLEAM        WFLEAM
      *
      * CALCULATE GROUP AGE IN WEEKS/DAYS USING OPEN DATE
      *
     C                   Z-ADD     0             WFAGWK
     C                   Z-ADD     0             WFAGDY
      *
     C                   MOVEL     HGBSCD        WFBSCD
     C                   MOVEL     HGHGCD        WFHGCD
     C                   Z-ADD     HGOPDT        WFOPDT
     C                   EXSR      $DAYS
      *
      * RETRIEVE IOWA MARKET PRICE FOR THE MARKET HOG SALES.
      * (DO NOT RETRIEVE FOR THE CULL OR FEEDER PIG SALES.)
      * (DO NOT RETRIEVE WHEN ONLY DOA HEAD ARE ON MARKET SALE RECORD)
      * SETLL ON IOWA TOP MARKET PRICE FILE BASED ON RECEIVED
      * DATE. IF RECORD NOT FOUND FOR THAT DATE, READ PRIOR TO GET
      * THE MOST RECENT RATE.
      *
     C                   Z-ADD     0             WFITRT
     C     WFSTCD        IFEQ      'MRKTS'                                      IF MARKET
     C     WFLVHD        ANDNE     0
     C                   SETOFF                                       8384
      *
     C     WFRCDT        SETLL     HSL027A                                84
      *
     C     *IN84         IFEQ      *ON
     C     WFRCDT        READE     HSL027A                                83
     C                   ELSE
     C                   READP     HSL027A                                83
     C                   ENDIF
      *
     C     *IN83         IFEQ      *OFF                                         IF PRICE
     C                   Z-ADD     ITITRT        WFITRT
     C                   ENDIF                                                  IF PRICE
     C                   ENDIF                                                  IF MARKET
      *
     C                   WRITE     WFREC
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * Retrieve group info to compare to user's selections
      *---------------------------------------------------------------
      *
     C     $hgcd         begsr
      *
     C     sghgsn        chain     hsl034d                            92
     C                   select
     C                   when      *in92 = *on
     C                   move      no            procfl
      *
     C                   when      (ldbscd <> *blank and hgbscd <> ldbscd) or
     C                             (ldptcd <> *blank and hgptcd <> ldptcd) or
     C                             (ldppcd <> *blank and hgppcd <> ldppcd)
     C                   move      no            procfl
     C                   other
      *
     C                   move      yes           procfl
     C                   endsl
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * $DAYS - CALCULATE THE DAYS IN THE PHASE FOR A GROUP
      *---------------------------------------------------------------
      *
     C     $DAYS         BEGSR
      *
      * FIND THE ELAPSED DAYS BETWEEN THE OPEN DATE OF THE GROUP
      * AND THE 'FROM RECEIVED DATE' SPECIFIED BY THE USER.
      *
     C                   Z-ADD     WFOPDT        PFRM8
     C                   Z-ADD     WFRCDT        PTO8
     C                   MOVEL     'CYMD'        PFRFMT
     C                   MOVEL     'CYMD'        PTOFMT
     C                   Z-ADD     0             PDAYS
     C                   MOVEL     'W'           PCODE
     C                   MOVE      *BLANK        PRTRN
     C                   EXSR      $DATE
      *
      * IF THERE ARE NO ERRORS, CONVERT ELAPSED DAYS TO WEEKS/DAYS
      *
     C     PRTRN         IFEQ      *BLANK
     C     PDAYS         ANDGT     0
     C     PDAYS         DIV       7             WFAGWK
     C                   MVR                     WFAGDY
     C                   ENDIF
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $DATE -  MANIPULATE DATES WITH DATE UTILITY
      *----------------------------------------------------------------
      *
     C     $DATE         BEGSR
      *
     C                   CALL      'UT80060R'
     C                   PARM                    PFRM8             8 0
     C                   PARM                    PTO8              8 0
     C                   PARM                    PFRFMT            4
     C                   PARM                    PTOFMT            4
     C                   PARM                    PDAYS             4 0
     C                   PARM                    PCODE             1
     C                   PARM                    PRTRN             1
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * *INZSR - INITIALIZATION SUBROUTINE
      *----------------------------------------------------------------
      *
     C     *INZSR        BEGSR
      *
     C     K1L064        KLIST
     C                   KFLD                    SHMVSN
     C                   KFLD                    SGHGSN
      *
     C                   MOVEL     YES           PROCFL            1
      *
     C                   ENDSR
      /EJECT
