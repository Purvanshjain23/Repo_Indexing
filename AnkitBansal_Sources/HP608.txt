      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      HOG PRODUCTION
      * PROGRAM:     HP608
      * TITLE:       LISTING OF FEED DELIVERY ZONES FOR A SELECTED
      *              BUSINESS OFFICE
      * PROGRAMMER:  MARY PALMER
      * CREATED:     4/06/94
      *
      * FUNCTION: BATCH PROGRAM TO LIST THE RECORDS IN THE FEED DELI-
      *           VERY ZONE FILE FOR A SELECTED BUSINESS OFFICE
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 09/12/94  LEANNE FEDOR
      *           RECOMPILE ONLY. REMOVED NEXT MOVEMENT NUMBER FROM
      *           BUSINESS OFFICE FILE.
      *
      * 02/21/95  LEANNE FEDOR
      *           CHANGED LOGIC TO USE LDA INSTEAD OF ENTRY PARMS
      *
      * 04/20/95  LEANNE FEDOR
      *           CHANGED FARM SITE CODE FROM ALPHA TO NUMERIC
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     FHSP013    IF   E           K DISK
      *    FEED DELIVERY ZONE        (KEY: DZFMBO DZDZCD)
      *
     FQPRINT    O    F  132        PRINTER OFLIND(*INOF)
      *
      /EJECT
      ****************************************************************
      * INPUT SPECS
      ****************************************************************
      *
      *---------------------------------------------------------------
      * STANDARD PROGRAM STATUS DATA STRUCTURE
      *---------------------------------------------------------------
      *    EXTERNALLY DEFINED AS UTPGFR (RECORD FORMAT: PGMDSR)
     D PGMDS         ESDS                  EXTNAME(UTPGFR)
      *
      *---------------------------------------------------------------
      *  LDA -  WITH THE REQUESTED PRINT SELECTIONS
      *---------------------------------------------------------------
     D                UDS
     D  LDFMBO                 1      5
     D  LDBODS                 6     35
      *
      *---------------------------------------------------------------
      *  NAMED CONSTANTS
      *---------------------------------------------------------------
      *
     D YES             C                   CONST('Y')
     D NO              C                   CONST('N')
      *
      /EJECT
      ****************************************************************
      * MAIN LINE
      ****************************************************************
      *
      * PRINT HEADINGS FOR FIRST TIME THROUGH
      *
     C                   EXCEPT    HRHDR
     C     LDFMBO        SETLL     HSP013
      *
      * READ AND PRINT ALL RECORDS IN THE FEED DELIVERY ZONE MASTER
      * FOR THE BUSINESS OFFICE.
      *
     C     *IN92         DOUEQ     *ON                                          MAIN DO
     C     LDFMBO        READE     HSP013                                 92
      *
     C     *IN92         IFEQ      *OFF                                         IF NOT EOF
     C     *INOF         IFEQ      *ON
     C                   EXCEPT    HRHDR
     C                   ENDIF
      *
      * PRINT FEED DELIVERY ZONE LINE
      *
     C                   EXCEPT    DTLINE
     C                   MOVEL     YES           PRTFLG
      *
     C                   ENDIF                                                  IF NOT EOF
     C                   ENDDO                                                  MAIN DO
      *
      *----------------------------------------------------------------
      * EOF PROCESSING
      *----------------------------------------------------------------
      *  IF NO RECORDS WERE PRINTED,
      *  PRINT THE STANDARD 'NO DATA...' ERROR MESSAGE
      *
     C     PRTFLG        IFEQ      NO
     C                   EXCEPT    NODATA
     C                   ENDIF
      *
     C                   SETON                                        LR
      /EJECT
      *---------------------------------------------------------------
      * *INZSR - INITIALIZATION SUBROUTINE
      *---------------------------------------------------------------
      *
     C     *INZSR        BEGSR
      *
      * SET PRINT FLAG TO DETERMINE IF ANY RECORDS WERE PRINTED
      * RETRIEVE TIME FOR REPORT HEADING
      *
     C                   MOVEL     NO            PRTFLG            1
     C                   MOVEL     *ALL'-'       DASH             80
     C                   TIME                    RTIME             6 0
      *
     C                   ENDSR
      /EJECT
      *-------------------------------------------------------------
      * REPORT HEADING LINES
      *-------------------------------------------------------------
      *
     OQPRINT    E            HRHDR          1 01
     O                       SDPGM               10
     O                                           48 'HOG PRODUCTION SYSTEM'
     O                                           70 'DATE'
     O                       UDATE         Y     80
      *
     O          E            HRHDR          1
     O                       SDUSR               10
     O                                           41 'LISTING OF FEED DE'
     O                                           53 'LIVERY ZONES'
     O                                           70 'TIME'
     O                       RTIME               80 '  :  :  '
      *
     O          E            HRHDR          1
     O                                           70 'PAGE'
     O                       PAGE          Z     80
      *
     O          E            HRHDR          2
     O                                           16 'BUSINESS OFFICE:'
     O                       LDFMBO              23
     O                       LDBODS              55
      *
     O          E            HRHDR          1
     O                                            8 'DELIVERY'
     O                                           15 'ZONE'
     O                                           44 'MILES FROM'
     O                                           55 'RATE PER'
      *
     O          E            HRHDR          1
     O                                            4 'ZONE'
     O                                           22 'DESCRIPTION'
     O                                           43 'FEED MILL'
     O                                           51 'TRIP'
      *
     O          E            HRHDR          1
     O                       DASH                80
      *
      *-------------------------------------------------------------
      * DETAIL LINE
      *-------------------------------------------------------------
      *
     O          E            DTLINE      1
     O                       DZDZCD               5
     O                       DZDZDS              31
     O                       DZDZMI        M     40
     O                       DZDZRT        M     54
      *
      *-------------------------------------------------------------
      * NO DATA MESSAGE LINE
      *-------------------------------------------------------------
      *
     O          E            NODATA      1
     O                                           19 'NO DATA MEETS YOUR'
     O                                           38 'SELECTION CRITERIA'
      /EJECT
