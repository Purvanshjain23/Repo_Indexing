      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Triumph Foods
      * PROGRAM:     TF660 - Listing of Adjustments from ARS
      * PROGRAMMER:  LeAnne Ramsey
      * CREATED:     05/22/08
      *
      * Function:    This program prints the Listing of Adjustments from the Accounts
      *              Receivable System.
      *
      *              This report was created as a result of the prolonged and excruciating
      *              reconcilation that went on in 2007-2009 between the TFS Cash Distribution
      *              data and the Accounts Receivable System data.
      *
      *              The listing is entirely over Synon files; so, it could have been created
      *              in the ARS model. But, to make it consistant with other TFS reports, we
      *              wrote this in TFS.
      *
      *              The listing is generated:
      *                1) on demand
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 09/15/09  LeAnne Ramsey
      *           Added AR Customer as a selection on the report.
      *
      * 10/01/09  LeAnne Ramsey
      *           Added a new selection on ITC-DTF (DueTo/DueFrom) accounts.
      *
      * 09/18/16  Danny Nguyen (E6933)
      *           On detail version report, added '*' next to the Actual Ship Date if
      *           and when A/R Header Ext field 'AR Ord Ref Actual Ship Date' is not 0.
      *           Add constant heading to let user know the meaning behind '*'.
      *           The '*' identifies Actual Ship Date that was derived from the 'old' logic
      *           in the '$getdt' routine in TF213 program.
      *           A conversion process was built to populate the 'AR Ord Ref Actual Ship Date'
      *           when the Order does not have a Remaining Bal Due in the A/R Header record.
      *           Conversion process will go live on late Sun night, 11/27/16. TF program changes
      *           will go live on Mon, 11/28/16, after WE Revenue Close is completed.
      *
      * 05/31/17  Danny Nguyen (T9996)
      *           Added field to TFP360 - ARS Adjustments Workfile:
      *             W1INSF - Invoice Suffix
      *           Added Invoice Suffix to report.
      *
      * 03/02/18  Danny Nguyen (S12308)
      *           Created new LF TFL360A -  ARS Adjustments Workfile to include W1AID as part
      *           of the key. Move W1AID Account ID to the p1cc (cost center) print field only
      *           if & when cost center is blank.
      /eject
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
12308F**tfp360    if   e           k disk
12308Ftfl360a   if   e           k disk
      *  Workfile: Adjustments from the Accounts Receivable System
      *
      *
E6933FPBC0CPL1  if   e           k disk
  |   *   AR Header Ext
  |   *
E6933 *
     Fprint198  o    f  198        printer oflind(*inof)
      *
      /eject
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      ****************************************************************
      * ARRAYS AND TABLES
      ****************************************************************
      *
     D artram          s             11  2 dim(4)
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Standard fields
      *
     D dash            s            198    inz(*all'-')
     d rtime           s              6  0
E6933 **
  |   ** Go live date for Actual Ship Date derived from Original Invoice Ref. Is the date
  |   ** when the conversion program to build the A/R Header Ext goes live.
E6933D w_asdgolivedte  s              8    inz('11/27/16')
      *
      *
      * Control fields
      *
     D first           s              1    inz('Y')
     D svcono          s                   like(w1cono)
     D svrcexfl        s                   like(w1rcexfl)
     D svcc            s                   like(w1cc)
     D svobj           s                   like(w1obj)
     D svsub           s                   like(w1sub)
12308D svaid           s                   like(w1aid)
      *
      *
      * Workfields for date manipulation
      *
     D wkisodate       s               d   datfmt(*iso)
      *
      *
      * Workfields for accumulating
      *
     D wktrnonam       s                   like(w1tram)
     D wktrexam        s                   like(w1tram)
      *
      *
      * Print fields
      *
     d l1yn            s              3
     d l1asdtmdy       s              6  0
     d l1dpdtmdy       s              6  0
     d l1tfsdtmdy      s              6  0
E6933d l1asterisk      s              1
      *
E6933d h1asteriskcmt1  s            198
E6933d h1asteriskcmt2  s            198
      *
     d t1desc          s             22
12308d p1cc            s                   like(w1cc)
      *
      *
      * Array index
      *
     D x               s              3  0
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *---------------------------------------------------------------
      * Local data area
      *---------------------------------------------------------------
      *
     d lda            uds                  dtaara(*lda)
     D  ldfdt                  1      8  0
     D  ldtdt                  9     16  0
     D  ldfmdy                17     22  0
     D  ldtmdy                23     28  0
     D  ldfsyndt              29     35  0
     D  ldtsyndt              36     42  0
      *
     D  ldiecd                43     43
     D  ldieds                44     69
      *
     D  ldcono                70     72  0
      *
     D  ldajty                73     75
     D  ldajact               76     76
     D  ldajactds             77    106
      *
     D  ldcuno               107    113  0
     D  ldcunm               114    143
      *
     D  lddtffl              144    144
     D  lddtfds              145    151
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      *************************************************************************************
      * Mainline
      *************************************************************************************
      *
      * Print headings
     C                   exsr      $h1hdr
      *
      * Read each AR Adjustment record that was written to the Workfile.
      * If the user made a selection on AR Customer, check for a match before
      * processing the record.
      *
12308C     *loval        setll     tfl360a
      *
     C                   dou       *in90 = *on                                  Main do
12308C                   read      tfl360a                                90
     C                   if        *in90 = *off and                             If not EOF
     C                             (ldcuno = 0 or ldcuno = w1arcuno)
      * Control breaks
     C                   select
     C                   when      first = yes
     C                   move      no            first
     C                   z-add     w1cono        svcono
     C                   move      w1rcexfl      svrcexfl
     C                   move      w1cc          svcc
     C                   move      w1obj         svobj
     C                   move      w1sub         svsub
12308C                   move      w1aid         svaid
      * Company
     C                   when      svcono <> w1cono
     C                   exsr      $account
     C                   exsr      $rcexfl
     C                   exsr      $cono
      * Record exempt
     C                   when      svrcexfl <> w1rcexfl
     C                   exsr      $account
     C                   exsr      $rcexfl
      * Account
     C                   when      svcc  <> w1cc  or
     C                             svobj <> w1obj or
12308C                             svsub <> w1sub or
12308C                             svaid <> w1aid
     C                   exsr      $account
     C                   endsl
      *
      * Accumulate Transaction Amount into total array
      *
     C                   add       w1tram        artram
      *
      * Print the AR Adjustment line
      *
     C                   exsr      $l1dtl
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Main do
      *
      *-----------------
      * EOF processing
      *-----------------
      *
      * No adjustments
     C                   select
     C                   when      first = yes
     C                   except    nodata
     C                   other
      *
      * Print last totals
     C                   exsr      $account
     C                   exsr      $rcexfl
     C                   exsr      $cono
      *
      * Print Report Summary Section
      *
     C                   exsr      $t2tot
     C                   endsl
      *
     C                   seton                                        lr
      /eject
      *---------------------------------------------------------------
      * Account Total
      *---------------------------------------------------------------
      *
     C     $account      begsr
      *
     C                   except    underscore
      *
     C                   z-add     1             x
      *
     C                   eval      t1desc = '        Account Total:'
      *
      * Print total, clear array entry, save control fields
      *
     C                   exsr      $t1tot
     C                   exsr      $clear
     C                   move      w1cc          svcc
     C                   move      w1obj         svobj
     C                   move      w1sub         svsub
12308C                   move      w1aid         svaid
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Record Exempt Total
      *---------------------------------------------------------------
      *
     C     $rcexfl       begsr
      *
     C                   z-add     2             x
      *
     C                   select
     C                   when      svrcexfl = yes
     C                   eval      t1desc = '         Exempt Total:'
     C                   add       artram(x)     wktrexam
     C                   other
      *
     C                   eval      t1desc = '     Non-Exempt Total:'
     C                   add       artram(x)     wktrnonam
     C                   endsl
      *
      * Print total, clear array entry, save control field
      *
     C                   exsr      $t1tot
     C                   exsr      $clear
     C                   move      w1rcexfl      svrcexfl
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Company Total
      *---------------------------------------------------------------
      *
     C     $cono         begsr
      *
     C                   z-add     3             x
      *
     C                   eval      t1desc = '        Company Total:'
      *
      * Print total, clear array entry, save control field
      *
     C                   exsr      $t1tot
     C                   exsr      $clear
     C                   z-add     w1cono        svcono
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Clear array entries for specified index
      *---------------------------------------------------------------
      *
     C     $clear        begsr
      *
     C                   z-add     0             artram(x)
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Print the AR Adjustment Detail line
      *---------------------------------------------------------------
      *
     C     $l1dtl        begsr
      *
     C                   select
     C                   when      w1rcexfl = yes
     C                   eval      l1yn = 'Yes'
      *
     C                   when      w1rcexfl = no
     C                   eval      l1yn = 'No '
     C                   endsl
      *
E6933 *
  |   ** Print '*' if 'AR Ord Ref Actual Ship Date' <> 0.
  |  C                   eval      l1asterisk = *blank
  |  C     key01         chain     PBC0CPL1                           92
  |  C                   if        *in92 = *off
  |  C                   if        C0V8DT <> 0
  |  C                   eval      l1asterisk = '*'                             Print '*'
  |  C                   endif
  |  C                   endif
E6933 *
      * Flip Actual Ship Date
      *
     C     *iso          test(d)                 w1asdt                 92
     C                   if        *in92 = *off                                 If OK date
     C                   move      w1asdt        wkisodate
     C     *mdy          move      wkisodate     l1asdtmdy
     C                   else
     C                   z-add     0             l1asdtmdy
     C                   endif                                                  If OK date
      *
      * Flip Deposit Date
      *
     C     *iso          test(d)                 w1dpdt                 92
     C                   if        *in92 = *off                                 If good date
     C                   move      w1dpdt        wkisodate
     C     *mdy          move      wkisodate     l1dpdtmdy
     C                   else
     C                   z-add     0             l1dpdtmdy
     C                   endif                                                  If good date
      *
      * Flip TFS Adj Week-Ending Date
      *
     C     *iso          test(d)                 w1tfsdt                92
     C                   if        *in92 = *off                                 If OK date
     C                   move      w1tfsdt       wkisodate
     C     *mdy          move      wkisodate     l1tfsdtmdy
     C                   else
     C                   z-add     0             l1tfsdtmdy
     C                   endif                                                  If OK date
      *
      *
     C                   if        *inof = *on
     C                   exsr      $h1hdr
     C                   endif
      *
12308C                   if        w1cc <> *blank
  |  C                   move      w1cc          p1cc
  |  C                   else
  |  C                   move      w1aid         p1cc
12308C                   endif
      *
     C                   except    l1dtl
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Print report heading
      *---------------------------------------------------------------
      *
     C     $h1hdr        begsr
      *
     C                   except    h1hdr
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Print Total line
      *---------------------------------------------------------------
      *
     C     $t1tot        begsr
      *
     C                   if        *inof = *on
     C                   exsr      $h1hdr
     C                   endif
      *
     C                   except    t1tot
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Print Report Total Summary Section
      *---------------------------------------------------------------
      *
     C     $t2tot        begsr
      *
     C                   if        *inof = *on
     C                   exsr      $h1hdr
     C                   endif
      *
     C                   z-add     4             x
     C                   except    t2tot
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Initialization
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
     C                   time                    rtime
      *
E6933 * Key lists
  |   *
  |  C     key01         klist
  |  C                   kfld                    W1CONO                         Company
  |  C                   kfld                    W1INNO                         Invoice #
E6933 *
      * Set up heading selections:
      *
E6933 *
  |  ** Add verbiage tp denote asterisk for Actual Ship Date.
  |  C                   eval      h1asteriskcmt1 = '(*) Pre ' + w_asdgolivedte
  |  C                             + ' - Sales History Actual Ship Date derived'
  |  C                             + ' from AR Invoice Ref. After '
  |  C                             + w_asdgolivedte + ' - Sales History Actual'
  |  C                             + ' Ship Date derived from Original Invoice'
  |  C                             + ' Ref,'
  |  C                   eval      h1asteriskcmt2 = 'which ensures each AR'
  |  C                             + ' transaction is tied back to original'
  |  C                             + ' Order/Invoice within the applicable'
E6933C                             + ' week ending.'
      * Company
     C                   if        ldcono = 0
     C                   seton                                        80
     C                   endif
      * Adjustment Type
     C                   if        ldajty = *blank
     C                   seton                                        81
     C                   endif
      * A/R Customer
     C                   if        ldcuno = 0
     C                   seton                                        82
     C                   endif
      * ITC-DTF
     C                   if        lddtffl = *blank
     C                   seton                                        83
     C                   endif
      *
     C                   endsr
      /eject
      *-------------------------------------------------------------
      * Report heading lines
      *-------------------------------------------------------------
      *
     Oprint198  e            h1hdr          1 01
     O                       sdpgm               10
     O                                           90 'Listing of Adjustments '
     O                                          109 'from the A/R System'
     O                                          188 'DATE'
     O                       udate         y    198
      *
     O          e            h1hdr          1
     O                       sdusr               10
     O                                          188 'TIME'
     O                       rtime              198 '  :  :  '
      *
     O          e            h1hdr          1
     O                                          188 'PAGE'
     O                       page          z    198
      *
      *
     O          e            h1hdr          1
     O                                           26 'From payment/deposit date:'
     O                       ldfmdy              36 '  /  /  '
      *
     O                                           85 'Company:'
     O               80                          90 'All'
     O              n80      ldcono        z     90
      *
     O                                          137 'ITC-DTFs:'
     O               83                         142 'All'
     O              n83      lddtfds            146
      *
     O          e            h1hdr          1
     O                                           26 'To payment/deposit date:'
     O                       ldtmdy              36 '  /  /  '
      *
     O                                           85 'A/R customer:'
     O               82                          90 'All'
     O              n82      ldcuno        z     94
     O              n82      ldcunm             126
      *
E6993O**        e            h1hdr          2
E6933O          e            h1hdr          1
     O                                           26 'Exempt/Non-Exempt/Both:'
     O                       ldiecd              29
     O                       ldieds              62
      *
     O                                           85 'Adjustment type:'
     O               81                          90 'All'
     O              n81      ldajty              90
     O              n81      ldajactds          126
      *
E6933 *
  |  O          e            h1hdr          1
  |  O                       h1asteriskcmt1     198
  |   *
  |  O          e            h1hdr          2
  |  O                       h1asteriskcmt2     198
E6933 *
      *-------------------------------------------------------------
      * Column headings
      *-------------------------------------------------------------
      *
     O          e            h1hdr       1  1
     O                                            4 'Comp'
     O                                           12 'Exempt'
T9996O**                                         54 'Adjust'
  |  O                                           50 'Adj'
T9996O                                           55 'Inv'
     O                                           64 'Deposit'
     O                                           78 'TFS Adj Week'
     O                                           96 'Transaction'
     O                                          106 'Invoice'
     O                                          116 'Actual'
     O                                          128 'A/R Cust'
      *
     O          e            h1hdr          1
     O                                            3 'Nbr'
     O                                           12 'Record'
     O                                           26 'Cost Center'
     O                                           34 'Object'
     O                                           46 'Subsidiary'
T9996O**                                         53 'Type'
  |  O                                           50 'Typ'
T9996O                                           55 'Sfx'
     O                                           63 'Date'
     O                                           78 'Ending Date'
     O                                           96 'Amount'
     O                                          106 'Number'
     O                                          117 'Ship Date'
     O                                          128 'Number'
     O                                          148 'A/R Customer Name'
     O                                          175 'Explanation'
      *
     O          e            h1hdr          1
     O                       dash               198
      *
      *-------------------------------------------------------------
      * Detail line one
      *-------------------------------------------------------------
      *
     O          e            l1dtl       1  0
      *
     O                       w1cono        z      3
     O                       l1yn                10
12308O**                     w1cc                26
12308O                       p1cc                26
     O                       w1obj               34
     O                       w1sub               45
T9996O**                     w1ajty              52
  |  O                       w1ajty              50
T9996O                       w1insf              54
     O                       l1dpdtmdy      b    64 '  /  /  '
     O                       l1tfsdtmdy     b    76 '  /  /  '
     O                       w1tram        k     97
     O                       w1inno        z    106
E6933O                       l1asterisk         108
     O                       l1asdtmdy      b   117 '  /  /  '
     O                       w1arcuno      z    128
     O                       w1arcunm           161
     O                       w1explan           194
      *
      *-------------------------------------------------------------
      * Total line
      *-------------------------------------------------------------
      *
     O          e            t1tot          1
     O                       t1desc              78
     O                       artram(x)     k     97
      *
      *-------------------------------------------------------------
      * Report Total Summary Section
      *-------------------------------------------------------------
      *
      *
     O          e            t2tot       2  1
     O                                           65 'Report Summary:'
     O                                           78 'Non-Exempt:'
     O                       wktrnonam     k     97
      *
     O          e            t2tot          1
     O                                           78 'Exempt:'
     O                       wktrexam      k     97
      *
     O          e            t2tot          1
     O                                           78 'Total:'
     O                       artram(x)     k     97
      *
      /eject
      *-------------------------------------------------------------
      * Underscores for total line
      *-------------------------------------------------------------
      *
     O          e            underscore  1  1
     O                                           96 '--------------'
      /eject
      *-------------------------------------------------------------
      * No data message line
      *-------------------------------------------------------------
      *
     O          e            nodata      1
     O                                           19 'No data meets your'
     O                                           39 'selection criteria.'
      /eject
