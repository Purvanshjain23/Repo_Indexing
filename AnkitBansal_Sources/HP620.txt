      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      HOG PRODUCTION
      * PROGRAM:     HP620
      * TITLE:       LISTING OF DEDUCTION/ADD ON CODES
      * PROGRAMMER:  ROSE CENTONZE
      * CREATED:     4/22/94
      *
      * FUNCTION: BATCH PROGRAM TO LIST THE RECORDS IN THE
      *           DEDUCTION/ADD ON CODE FILE
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      * 06/18/97  LEANNER FEDOR
      *           ADDED USER ID IN REPORT HEADING
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     FHSP044    IF   E           K DISK
      *    DEDUCTION/ADD ON CODE MASTER FILE  (KEY: DADACD)
      *
     FHSP006    IF   E           K DISK
      *    REPORT CODE MASTER (KEY: RPRPCD)
      *
     FQPRINT    O    F  132        PRINTER OFLIND(*INOF)
      *
      /EJECT
      ****************************************************************
      * INPUT SPECS
      ****************************************************************
      *---------------------------------------------------------------
      * STANDARD PROGRAM STATUS DATA STRUCTURE
      *---------------------------------------------------------------
      *    EXTERNALLY DEFINED AS UTPGFR (RECORD FORMAT: PGMDSR)
     D PGMDS         ESDS                  EXTNAME(UTPGFR)
      *
      *
      *---------------------------------------------------------------
      *  NAMED CONSTANTS
      *---------------------------------------------------------------
      *
     D YES             C                   CONST('Y')
     D NO              C                   CONST('N')
      *
      /EJECT
      ****************************************************************
      * MAIN LINE
      ****************************************************************
      *
      * PREPARE TO READ THE DEDUCTION/ADD ON MASTER FILE AND
      * PRINT HEADINGS FOR FIRST TIME THROUGH
      *
     C     *LOVAL        SETLL     HSP044
     C                   EXCEPT    HDRPT
      *
      * READ AND PRINT ALL RECORDS IN THE MASTER.
      *
     C     *IN92         DOUEQ     *ON                                          MAIN DO
     C                   READ      HSP044                                 92
      *
     C     *IN92         IFEQ      *OFF                                         IF NOT EOF
      *
     C     *INOF         IFEQ      *ON
     C                   EXCEPT    HDRPT
     C                   ENDIF
      *
      * IF THERE IS A REPORT CODE, THEN
      * CHAIN TO REPORT CODE MASTER FILE FOR LONG DESCRIPTION
      *
     C                   MOVE      *BLANK        WKRLDS
     C     DARPCD        IFNE      *BLANK
     C     DARPCD        CHAIN     HSP006                             95
     C     *IN95         IFEQ      *OFF
     C                   MOVEL     RPRLDS        WKRLDS
     C                   ELSE
     C                   MOVEL     'UNKNOWN'     WKRLDS
     C                   ENDIF
     C                   ENDIF
      *
      * PRINT LINE
      *
     C                   EXCEPT    DTTYPE
     C                   MOVEL     YES           PRTFLG
      *
     C                   ENDIF                                                   IF NOT EOF
      *
      *  IF THE MASTER FILE WAS EMPTY AND NO RECORDS WERE PRINTED,
      *  PRINT THE STANDARD 'NO DATA...' ERROR MESSAGE
      *
     C     PRTFLG        IFEQ      NO
     C                   EXCEPT    NODATA
     C                   ENDIF
      *
     C                   ENDDO                                                  MAIN DO
      *
     C                   SETON                                        LR
      /EJECT
      *---------------------------------------------------------------
      * *INZSR - INITIALIZATION SUBROUTINE
      *---------------------------------------------------------------
      *
     C     *INZSR        BEGSR
      *
     C     *LIKE         DEFINE    RPRLDS        WKRLDS
      *
      * SET PRINT FLAG TO DETERMINE IF ANY RECORDS WERE PRINTED
      * RETRIEVE TIME FOR REPORT HEADING
      *
     C                   MOVEL     NO            PRTFLG            1
     C                   MOVEL     *ALL'-'       DASH             80
     C                   TIME                    RTIME             6 0
      *
     C                   ENDSR
      *
      /EJECT
      *-------------------------------------------------------------
      * REPORT LAYOUT
      *-------------------------------------------------------------
      *
      *-------------------------------------------------------------
      * REPORT HEADING LINES
      *-------------------------------------------------------------
      *
     OQPRINT    E            HDRPT          1 01
     O                       SDPGM               10
     O                                           48 'HOG PRODUCTION SYSTEM'
     O                                           66 'DATE'
     O                       UDATE         Y     76
      *
     O          E            HDRPT          1
     O                       SDUSR               10
     O                                           41 ' LISTING OF DEDUCTION'
     O                                           55 '/ADD-ON CODES '
     O                                           66 'TIME'
     O                       RTIME               76 '  :  :  '
      *
     O          E            HDRPT          2
     O                                           66 'PAGE'
     O                       PAGE          Z     71
      *
     O          E            HDRPT          1
     O                                           22 '--DEDUCTION/ADD-ON---'
     O                                           42 'REPORT'
     O                                           64 '------REPORT------'
      *
     O          E            HDRPT          1
     O                                            5 'CODE'
     O                                           22 'DESCRIPTION'
     O                                           41 'FLAG'
     O                                           64 'CODE     LONG NAME'
      *
     O          E            HDRPT          1
     O                       DASH                80
      *
      *-------------------------------------------------------------
      *  DETAIL LINE
      *-------------------------------------------------------------
      *
     O          E            DTTYPE      1
     O                       DADACD               6
     O                       DADADS              31
     O                       DARPFL              39
     O                       DARPCD              51
     O                       WKRLDS              75
      *
      *-------------------------------------------------------------
      * NO DATA MESSAGE LINE
      *-------------------------------------------------------------
      *
     O          E            NODATA      1
     O                                           19 'NO DATA MEETS YOUR'
     O                                           38 'SELECTION CRITERIA'
