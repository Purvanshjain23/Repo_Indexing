     A*%%TS  SD  20040331  164653  ISLRAMS     REL-V5R2M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD1715
     A*  TITLE:         MAINTAIN BUDGET QUANTITY ON FARM BUDGETS
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  CREATED:       03/10/04
     A*
     A*  INDICATOR 60 IS ON WHEN IN REVISE MODE
     A*  INDICATOR 61 IS ON WHEN IN CREATE MODE
     A*
     A*  INDICATOR 63 IS ON WHEN THE SUBFILE FIELDS ARE NON-DISPLAY
     A*               BECAUSE THE RECORD WAS SUCCESSFULLY DELETED
     A*               (THE HIDDEN SUBFILE FIELD HDNDFL = YES)
     A*
     A*  INDICATOR 64 IS ON WHEN A SUBFILE RECORD FIELDS MUST BE PROTECTED.
     A*               (THE HIDDEN SUBFILE FIELD HDPFL = YES). FIELDS ARE
     A*               PROTECTED IF:
     A*               1) THE SUBFILE RECORD WAS SUCCESSFULLY CREATED
     A*
     A*  INDICATOR 90 IS ON WHEN THE BUDGET ITEM THE USER SELECTED
     A*               FROM THE WORK WITH HAS BEEN DELETED BY ANOTHER USER
     A**************************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A N90                                  CF04(04 'PROMPT')
     A N90                                  CF05(05 'REFRESH')
     A N90                                  CF10(10 'CHANGE MODES')
     A*
     A*---------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP1715S1                  SFL
     A*%%TS  SD  20040310  104347  ISLRAMS     REL-V5R2M0  5722-WDS
     A  85                                  SFLNXTCHG
     A            HDRRN1         4Y 0H
     A            HDPFL     R        H      REFFLD(@@FLAG)
     A            HDNDFL    R        H      REFFLD(@@FLAG)
     A            D1OPT          2A  B 15  2CHGINPDFT(UL)
     A  61
     AO 64
     AO 90                                  DSPATR(PR)
     A  61
     AO 64
     AO 90                                  DSPATR(ND)
     A  30                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A N30N61N64
     A N90                                  DSPATR(HI)
     A  60N30N64
     A N90                                  DSPATR(UL)
     A            D1FBSN    R        B 15  6REFFLD(HSRREF/@@FBSN *LIBL/HSPREF)
     A                                      EDTCDE(M)
     A  60
     AO 64
     AO 90                                  DSPATR(PR)
     A  63                                  DSPATR(ND)
     A  33
     AO 53                                  DSPATR(PC)
     A  33                                  DSPATR(RI)
     A N33N60N64
     A N90                                  DSPATR(HI)
     A N33N60N64
     A N90                                  DSPATR(UL)
     A            D1BGQT    R        B 15 15REFFLD(HSRREF/@@BGQT *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A  64
     AO 90                                  DSPATR(PR)
     A  63                                  DSPATR(ND)
     A  34                                  DSPATR(PC)
     A  34                                  DSPATR(RI)
     A N34N64N90                            DSPATR(HI)
     A N34N64N90                            DSPATR(UL)
     A            D1FSCD    R        O 15 32REFFLD(HSRREF/@@FSCD *LIBL/HSPREF)
     A                                      EDTCDE(Z)
     A            D1FBSCD   R        O 15 41REFFLD(HSRREF/@@FBSCD *LIBL/HSPREF)
     A            D1BTCD    R        O 15 47REFFLD(HSRREF/@@BTCD *LIBL/HSPREF)
     A            D1BTDS    R        O 15 55REFFLD(HSRREF/@@BTDS *LIBL/HSPREF)
     A*
     A*---------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*---------------------------------------------------------------
     A*
     A          R HP1715C1                  SFLCTL(HP1715S1)
     A*%%TS  SD  20040331  164653  ISLRAMS     REL-V5R2M0  5722-WDS
     A                                      SFLSIZ(0100)
     A                                      SFLPAG(0006)
     A                                      SETOF(30)
     A                                      SETOF(33)
     A                                      SETOF(34)
     A                                      SETOF(50)
     A                                      SETOF(51)
     A                                      SETOF(52)
     A                                      SETOF(53)
     A                                      SETOF(54)
     A                                      SETOF(63)
     A N82N90                               ROLLUP(25 'PAGE FORWARD')
     A                                      SFLCSRRRN(&HDRR1)
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND(*MORE)
     A                                      OVERLAY
     A                                      RTNCSRLOC(&RCD &FLD)
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            HDRR1          5S 0H
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 28'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A            C1MODE         6A  O  2  2DSPATR(HI)
     A                                  2 20'MAINTAIN BUDGET ITEM ON FARM BUDGE-
     A                                      TS'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  4  2'Budget item:'
     A                                      DSPATR(HI)
     A            C1BGIT    R        O  4 16REFFLD(HSRREF/@@BGIT *LIBL/HSPREF)
     A                                  5  2'Description:'
     A                                      DSPATR(HI)
     A            C1DESC    R        O  5 16REFFLD(HSRREF/@@ITDS *LIBL/HSPREF)
     A                                  6  2'UOM .......:'
     A                                      DSPATR(HI)
     A            C1UOM     R        O  6 16REFFLD(HSRREF/@@UOM *LIBL/HSPREF)
     A                                  7  2'UOW .......:'
     A                                      DSPATR(HI)
     A            C1UOW     R        O  7 16REFFLD(HSRREF/@@UOW *LIBL/HSPREF)
     A*
     A N61N90                           9  2'4=Delete'
     A                                 11  9'?'
     A                                      DSPATR(HI)
     A N61N90                          11 35'?'
     A                                      DSPATR(HI)
     A N61N90                          11 41'?'
     A                                      DSPATR(HI)
     A N61N90                          11 49'?'
     A                                      DSPATR(HI)
     A                                 12  7'Budget'
     A                                      DSPATR(HI)
     A                                 12 18'Farm Budget'
     A                                      DSPATR(HI)
     A                                 12 33'Farm'
     A                                      DSPATR(HI)
     A                                 12 39'Budget'
     A                                      DSPATR(HI)
     A                                 12 47'-------- Budget Template --------'
     A                                      DSPATR(HI)
     A N61N64N90                       13  2'Opt'
     A                                      DSPATR(HI)
     A                                 13  7'Number'
     A                                      DSPATR(HI)
     A                                 13 21'Quantity'
     A                                      DSPATR(HI)
     A                                 13 33'Site'
     A                                      DSPATR(HI)
     A                                 13 39'Status'
     A                                      DSPATR(HI)
     A                                 13 47'Code'
     A                                      DSPATR(HI)
     A                                 13 55'Description'
     A                                      DSPATR(HI)
     A N90        C1FBSN    R        B 14  6REFFLD(HSRREF/@@FBSN *LIBL/HSPREF)
     A                                      EDTCDE(M)
     A                                      DSPATR(HI)
     A  61                                  DSPATR(ND)
     A  61                                  DSPATR(PR)
     A  51                                  DSPATR(PC)
     A N90        C1FSCD    R        B 14 32REFFLD(HSRREF/@@FSCD *LIBL/HSPREF)
     A                                      EDTCDE(M)
     A                                      DSPATR(HI)
     A  61                                  DSPATR(ND)
     A  61                                  DSPATR(PR)
     A  50                                  DSPATR(PC)
     A*
     A N90        C1FBSCD   R        B 14 41REFFLD(HSRREF/@@FBSCD *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A  61                                  DSPATR(ND)
     A  61                                  DSPATR(PR)
     A  52                                  DSPATR(PC)
     A*
     A N90        C1BTCD    R        B 14 47REFFLD(HSRREF/@@BTCD *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A  61                                  DSPATR(ND)
     A  61                                  DSPATR(PR)
     A  54                                  DSPATR(PC)
     A*
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R HP1715K1
     A*%%TS  SD  20010125  155711  ISLRAMS     REL-V4R4M0  5769-PW1
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A                                      DSPATR(HI)
     A                                 23  2'F3=Exit'
     A N90                             23 11'F4=Prompt'
     A N90                             23 22'F5=Refresh'
     A  60N90                          23 34'F10=Go to Create'
     A  61N90                          23 34'F10=Go to Revise'
     A*
     A*---------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE FORMAT
     A*---------------------------------------------------------------
     A          R HP1715ES                  SFL SFLMSGRCD(24)
     A*
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
     A*
     A*---------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*---------------------------------------------------------------
     A          R HP1715EC                  SFLCTL(HP1715ES)
      *
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
