     A*%%TS  SD  20060321  084747  ISLRAMS     REL-V5R3M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        TRIUMPH FOODS
     A*  DISPLAY FILE:  TFD427
     A*  TITLE:         SPECIFY CARRIER INVOICE CLOSE
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  CREATED:       03/16/06
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/TFPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A                                      CF08(08 'SUBMIT')
     A                                      CF17(17 'PRINT OPTIONS')
     A                                      CF20(20 'WRKSPLF')
     A*
     A*---------------------------------------------------------------
     A* RECORD FORMAT ONE
     A*---------------------------------------------------------------
     A          R TF427R1
     A*%%TS  SD  20060321  084747  ISLRAMS     REL-V5R3M0  5722-WDS
     A                                      OVERLAY
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 22'SPECIFY CARRIER INVOICE CLOSE'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USR)
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@JOB)
     A                                      DSPATR(HI)
     A                                  4  2'You can only do this ONE TIME for -
     A                                      a Period. So, BE SURE you are ready-
     A                                       to'
     A                                      COLOR(PNK)
     A                                      DSPATR(HI)
     A                                  5  2'close the carrier invoices before -
     A                                      you take F8.'
     A                                      COLOR(PNK)
     A                                      DSPATR(HI)
     A                                  9  2'Take F8=Submit to create a snapsho-
     A                                      t of the Load data for Accounting t-
     A                                      o use'
     A                                      DSPATR(HI)
     A                                 10  2'in calculating the Seaboard/Triump-
     A                                      h Freight Variance for the Period.'
     A                                      DSPATR(HI)
     A                                 12  9'Year/period:'
     A                                      DSPATR(HI)
     A            D1YR      R        O 12 22REFFLD(@@YR)
     A                                      DSPATR(HI)
     A                                      DSPATR(RI)
     A                                      COLOR(PNK)
     A            D1PE      R        O 12 28REFFLD(@@PE)
     A                                      EDTCDE(M)
     A                                      DSPATR(HI)
     A                                      DSPATR(RI)
     A                                      COLOR(PNK)
     A            D1PBMDY   R     Y  O 12 35REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  ')
     A                                 12 44'to'
     A            D1PEMDY   R     Y  O 12 47REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  ')
     A*
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R TF427K1
     A*%%TS  SD  20060320  082851  ISLRAMS     REL-V5R3M0  5722-WDS
     A  43                             21  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A                                 23 11'F8=Submit'
     A                                 23 22'F17=Print Options'
     A                                 23 41'F20=Spool Files'
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *---------------------------------------------------------------
     A          R TF427ES                   SFL SFLMSGRCD(24)
      *
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
     A*---------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*---------------------------------------------------------------
     A*
     A          R TF427EC                   SFLCTL(TF427ES)
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
