     A*****************************************************************
     A*  SYSTEM:       HOG PRODUCTION
     A*  DISPLAY FILE: HPD555 - SELECT GROUP FROM SALES MOVEMENT DETAIL
     A*  CREATED BY:   LEANNE FEDOR
     A*  DATE:         06/19/95
     A*****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA12(12 'PREVIOUS')
     A N82                                  ROLLUP(25 'PAGE FORWARD')
     A*---------------------------------------------------------------
     A* REFERENCE WINDOW RECORD FORMAT 1
     A*---------------------------------------------------------------
     A          R HP555R1
     A*%%TS  SD  19950620  143406  ISLRAMS     REL-V3R0M5  5763-PW1
     A                                      TEXT('Reference window')
     A                                      WINDOW(*DFT 17 75)
     A                                      WDWBORDER((*DSPATR RI) (*CHAR '    -
     A                                          '))
     A*---------------------------------------------------------------
     A* RECORD FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP555R2
     A                                      TEXT('Written in RPG pgm to prevent-
     A                                       screen from clearing')
     A                                      CLRL(*NO)
     A                                      OVERLAY
     A*---------------------------------------------------------------
     A* RECORD FORMAT TWO
     A*---------------------------------------------------------------
     A* THIS FORMAT IS NOT WRITTEN IN THE RPG PGM.  IT PREVENTS THE
     A* CALLING_PROGRAM_SCREEN FROM BEING CLEARED.
     A*---------------------------------------------------------------
     A          R HP002R3
     A                                      TEXT('Dummy format not written in R-
     A                                      PG pgm.')
     A                                      ASSUME
     A                                 11  1' '
     A*---------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP555S1                   SFL
     A            HDRRN1         4S 0H
     A            DFMVSN    R        O  7  1REFFLD(SHMVSN)
     A                                      EDTCDE(M)
     A            DFHGCD    R        O  7 12REFFLD(HGHGCD)
     A            DFRMDY    R        O  7 21REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  ')
     A            DFCVNO    R        O  7 31REFFLD(SHCVNO)
     A                                      EDTCDE(M)
     A            DFSHHD    R     Y  O  7 42REFFLD(SGSHHD)
     A                                      EDTCDE(K)
     A            DFLVHD    R     Y  O  7 50REFFLD(SGLVHD)
     A                                      EDTCDE(K)
     A            DFDOHD    R     Y  O  7 58REFFLD(SGDOHD)
     A                                      EDTCDE(K)
     A            DFMSCD    R        O  7 66REFFLD(SHMSCD)
     A            DFSTCD    R        O  7 70REFFLD(SHSTCD)
     A*--------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*--------------------------------------------------------------
     A          R HP555C1                   SFLCTL(HP555S1)
     A*%%TS  SD  19950620  153930  ISLRAMS     REL-V3R0M5  5763-PW1
     A                                      SFLSIZ(0050)
     A                                      SFLPAG(0008)
     A                                      WINDOW(HP555R1)
     A                                      OVERLAY
     A                                      SFLCSRRRN(&HDRRN)
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND(*MORE)
     A                                      RMVWDW
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            HDRRN          5S 0H
     A                                  1 17'SELECT A GROUP FROM A SALES MOVEME-
     A                                      NT'
     A                                      DSPATR(HI)
     A                                  2  1'Position Cursor, Press Enter'
     A                                  3 49'Payment Received'
     A                                      DSPATR(HI)
     A                                  4 21'Received'
     A                                      DSPATR(HI)
     A                                  4 31'Customer'
     A                                      DSPATR(HI)
     A                                  4 41'Shipped'
     A                                      DSPATR(HI)
     A                                  4 52'Live'
     A                                      DSPATR(HI)
     A                                  4 61'DOA'
     A                                      DSPATR(HI)
     A                                  4 70'Sales'
     A                                      DSPATR(HI)
     A                                  5  1'Movement'
     A                                      DSPATR(HI)
     A                                  5 12'Group'
     A                                      DSPATR(HI)
     A                                  5 23'Date'
     A                                      DSPATR(HI)
     A                                  5 31'Number'
     A                                      DSPATR(HI)
     A                                  5 43'Head'
     A                                      DSPATR(HI)
     A                                  5 52'Head'
     A                                      DSPATR(HI)
     A                                  5 61'Head'
     A                                      DSPATR(HI)
     A                                  5 66'St'
     A                                      DSPATR(HI)
     A                                  5 70'Type'
     A                                      DSPATR(HI)
     A            C1MVSN    R     Y  B  6  1REFFLD(SHMVSN)
     A                                      DSPATR(HI)
     A                                      EDTCDE(M)
     A            C1HGCD    R        B  6 12REFFLD(HGHGCD)
     A                                      DSPATR(HI)
     A            C1CVNO    R        B  6 31REFFLD(SHCVNO)
     A                                      DSPATR(HI)
     A                                      EDTCDE(M)
     A            C1MSCD    R        B  6 66REFFLD(MSMSCD)
     A                                      DSPATR(HI)
     A            C1STCD    R        B  6 70REFFLD(FSPTCD)
     A                                      DSPATR(HI)
     A*---------------------------------------------------------------
     A          R HP555K1
     A*---------------------------------------------------------------
     A                                      OVERLAY
     A                                      WINDOW(HP555R1)
     A                                 16  1'F12=Previous'
