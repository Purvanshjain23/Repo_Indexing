      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Hog Production
      * PROGRAM:     HP6600
      * TITLE:       Expense Class/Subclass/Code Listing: By Sequence
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     06/25/03
      *
      * FUNCTION: Batch program to list Expense Classes/Subclasses/Expense Codes.  The
      *           listing is sorted by the 'sequence'field.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fhsl050a   if   e           k disk
      *   Expense classes
      *
      *
     Fhsl051b   if   e           k disk
      *   Expense subclasses
      *
      *
     Fhsl3410a  if   e           k disk
      *   Workfile
      *
      *
     Fqprint    o    f  198        printer oflind(*inof)
      *
      /EJECT
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      *--------------------------------------------------------------
      * CONSTANTS
      *--------------------------------------------------------------
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      *
      *--------------------------------------------------------------
      * STANDALONE FIELDS
      *--------------------------------------------------------------
      *
      * Standard fields
      *
     D prtfl           s              1    inz('N')
     D dash            s            198    inz(*all'-')
     d rtime           s              6  0
      *
      *
      * Control flags
      *
     D escdfl          s              1    inz('N')
     D excdfl          s              1    inz('N')
      *
      *--------------------------------------------------------------
      * DATA STRUCTURES
      *--------------------------------------------------------------
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *
      /EJECT
      ****************************************************************
      * MAINLINE
      ****************************************************************
      *
      * Print headings first time through
      *
     C                   exsr      $h1hdr
      *
      * Process each Expense Class record.
      *
     C                   dou       *in90 = *on                                  Main do
     C                   read      hsl050a                                90
     C                   if        *in90 = *off                                 If not EOF
      *
     C                   seton                                        9798
      *
      * Process all Subclasses assigned to the Class
      *
     C                   exsr      $subclass
      *
      * If this Class did not have ANY Subclasses set up, then
      * you need to print the detail line here.
      *
     C                   if        escdfl = no
     C                   seton                                        96
     C                   exsr      $d1dtl
     C                   endif
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Main do
      *
      *---------------------------------------------------------------
      * EOF processing
      *---------------------------------------------------------------
      *
     C                   if        prtfl = no
     C                   except    nodata
     C                   endif
      *
     C                   seton                                        lr
      /EJECT
      *---------------------------------------------------------------
      * Process all Subclasses for a Class
      *---------------------------------------------------------------
      *
     C     $subclass     begsr
      *
     C                   move      no            escdfl
      *
     C     ececcd        setll     hsl051b
      *
     C                   dou       *in91 = *on                                  Do subs
     C     ececcd        reade     hsl051b                                91
     C                   if        *in91 = *off                                 If not EOF
     C                   seton                                        98
     C                   move      yes           escdfl
     C                   exsr      $codes
      *
      * If this Subclass had NO Expense Codes, you need to print
      * the detail line here....it just won't have an Expense Code.
      *
     C                   if        excdfl = no
     C                   seton                                        95
     C                   exsr      $d1dtl
     C                   endif
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Do subs
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Process all Expense Codes for a Subclass
      *---------------------------------------------------------------
      *
     C     $codes        begsr
      *
     C                   move      no            excdfl
     C     key01         setll     hsl3410a
      *
     C                   dou       *in93 = *on                                  Do codes
     C     key01         reade     hsl3410a                               93
     C                   if        *in93 = *off                                 If not EOF
     C                   move      yes           excdfl
     C                   exsr      $d1dtl
     C                   endif                                                  If not EOF
     C                   enddo                                                  Do codes
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Print headings
      *---------------------------------------------------------------
      *
     C     $h1hdr        begsr
      *
     C                   seton                                        9798
     C                   except    h1hdr
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Print detail line
      *---------------------------------------------------------------
      *
     C     $d1dtl        begsr
      *
     C                   select
     C                   when      *inof = *on
     C                   exsr      $h1hdr
      *
     C                   when      *in97 = *on and *in98 = *on
     C                   except    blank
     C                   except    blank
      *
     C                   when      *in98 = *on
     C                   except    blank
     C                   endsl
      *
      * Print detail line
     C                   except    d1dtl
     C                   move      yes           prtfl
     C                   setoff                                       959697
     C                   setoff                                       98
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Key lists
      *
     C     key01         klist
     C                   kfld                    ececcd
     C                   kfld                    esescd
      *
      *
     C                   time                    rtime
      *
     C                   endsr
      /EJECT
      *-------------------------------------------------------------
      * REPORT HEADING LINES
      *-------------------------------------------------------------
      *
     Oqprint    e            h1hdr          1 01
     O                       sdpgm               10
     O                                           88 'HOG PRODUCTION SYSTEM'
     O                                          188 'DATE'
     O                       udate         Y    198
      *
      *
     O          e            h1hdr          1
     O                       sdusr               10
     O                                           80 'LISTING OF EXPENSE CL'
     O                                           98 'ASS/SUBCLASS/CODES'
     O                                          188 'TIME'
     O                       rtime              198 '  :  :  '
      *
      *
     O          e            h1hdr          2
     O                                           82 'BY SEQUENCE'
     O                                          188 'PAGE'
     O                       page          Z    198
      *
      *
     O          e            h1hdr          1
     O                                           25 '----------- EXPENSE CLASS'
     O                                         +  0 ' -----------'
      *
     O                                           64 '--------- EXPENSE SUBCLAS'
     O                                         +  0 'S ----------'
      *
     O                                          103 '----------- EXPENSE CODE '
     O                                         +  0 '------------'
      *
     O                                          138 '--------- REPORT ---'
     O                                         +  0 '-------'
      *
      *
     O          e            h1hdr          1
      * Class
     O                                            8 'SEQUENCE'
     O                                           14 'CODE'
     O                                           28 'DESCRIPTION'
      * Subclass
     O                                           47 'SEQUENCE'
     O                                           53 'CODE'
     O                                           67 'DESCRIPTION'
      * Expense Code
     O                                           86 'SEQUENCE'
     O                                           92 'CODE'
     O                                          106 'DESCRIPTION'
      * Report code
     O                                          122 'CODE'
     O                                          136 'DESCRIPTION'
      *
     O                                          166 '-------------------'
     O                                         +  0 ' G/L ACCOUNT'
     O                                         +  0 '--------------------'
      *
      *
     O          e            h1hdr          1
     O                       dash               198
      *
      *-------------------------------------------------------------
      * Detail line
      *-------------------------------------------------------------
      *
     O          e            d1dtl       1
      *
      * Expense class
     O               97      ececseq       l      8
     O               97      ececcd              15
     O               97      ececds              37
      *
      * Expense subclass
      *
     O               98n96   esesseq       l     47
     O               98n96   esescd              54
     O               98n96   esesds              76
      *
      * Expense code
     O              n95n96   w1exseq       4     86
     O              n95n96   w1excd              93
     O              n95n96   w1exds             115
     O              n95n96   w1rpcd             123
     O              n95n96   w1rlds             145
      *
     O              n95n96   w1glds             198
      *
      *-------------------------------------------------------------
      * Blank line
      *-------------------------------------------------------------
      *
     O          e            blank       1
      *
      *-------------------------------------------------------------
      * No data message line
      *-------------------------------------------------------------
      *
     O          e            nodata      1
     O                                           19 'No data meets your'
     O                                           39 'selection criteria.'
