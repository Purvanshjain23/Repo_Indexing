/***************************************************************** */
/*                                                                 */
/*  PROGRAM NUMBER:  POPHUPR                                       */
/*  PROGRAM NAME:    PURGE INVENTORY TRANSACTIONS TO HISTORY       */
/*  PROGRAMMER:      PURVA DROGE                                   */
/*  CREATE DATE:     04/03/02                                      */
/*                                                                 */
/*  FUNCTION:        THIS CL WILL COPY RECORDS TO PRKFHST FROM     */
/*                   INVENTORY TRANSACTIONS.                       */
/*                                                                 */
/*******************************************************************/
/*  Modification History                                           */
/*  RMC 8/15/02 USE SNDPGRMSG A LIEU SNDDST                        */
/*******************************************************************/
             PGM        PARM(&PDTE &HDTE)

/* INPUT PARAMETERS */
             DCL        VAR(&PDTE) TYPE(*DEC)  LEN(7 0)   /* PURGE DATE   */
             DCL        VAR(&HDTE) TYPE(*DEC)  LEN(7 0)   /* HISTORY DATE */

/* FUNCTION SPECIFIC PARMS */
             DCL        VAR(&PDTEX) TYPE(*CHAR) LEN(7)    /* PURGE DATE   */
             DCL        VAR(&HDTEX) TYPE(*CHAR) LEN(7)    /* HISTORY DATE */
             DCL        VAR(&SEL)  TYPE(*CHAR) LEN(512)   /* SELECT       */

/*-----------------------------------------------------------------*/
/* INITIALIZE                                                      */
/*-----------------------------------------------------------------*/

             CHGVAR     VAR(&PDTEX) VALUE(&PDTE)
             CHGVAR     VAR(&HDTEX) VALUE(&HDTE)

/*-----------------------------------------------------------------*/
/* MAINLINE                                                        */
/*-----------------------------------------------------------------*/

/* Insert into To HISTORY */
             CHGVAR     VAR(&SEL) VALUE('Select * from ' *CAT +
                          'CAB7CPP  where B7B4DT<' *CAT &HDTEX)

             INSERT     INTO(CAB7CPPHST) SQL(&SEL)
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(INSERTHST))

/* Delete from PRODUCTION */
             CHGVAR     VAR(&SEL) VALUE('from ' *CAT 'CAB7CPP  where +
                          B7B4DT<' *CAT &HDTEX)

             DELETE     SQL(&SEL)
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(DELETEPRD))

/* Delete from HISTORY */
             CHGVAR     VAR(&SEL) VALUE('from ' *CAT 'CAB7CPPHST +
                          where B7B4DT<' *CAT &PDTEX)

             DELETE     SQL(&SEL)
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(DELETEHST))

/* Successful Completion */
             GOTO       CMDLBL(PGMMSG)
/*-----------------------------------------------------------------*/
/*                     ERROR MESSAGES                              */
/*-----------------------------------------------------------------*/

INSERTHST:
/*           SNDDST     TYPE(*MSG) TOUSRID((PRKPURGE BIGBYTE)) +
                          DSTD('Item Transaction Detail Purge') +
                          MSG('INSERT ERROR into file CAB7CPPHST') */
             SNDPGRMSG  TOPGR(PRKPURGE) MSG('INSERT ERROR into file +
                          CAB7CPPHST') CONFMSG(*NO)
             GOTO       CMDLBL(ERROR)

DELETEPRD:
/*           SNDDST     TYPE(*MSG) TOUSRID((PRKPURGE BIGBYTE)) +
                          DSTD('Item Transaction Detail Purge') +
                          MSG('DELETE ERROR from file CAB7CPP') */
             SNDPGRMSG  TOPGR(PRKPURGE) MSG('DELETE ERROR from file +
                          CAB7CPP') CONFMSG(*NO)
             GOTO       CMDLBL(ERROR)

DELETEHST:
 /*          SNDDST     TYPE(*MSG) TOUSRID((PRKPURGE BIGBYTE)) +
                          DSTD('Item Transaction Detail Purge') +
                          MSG('DELETE ERROR from file CAB7CPPHST') */
             SNDPGRMSG  TOPGR(PRKPURGE) MSG('DELETE ERROR from file +
                          CAB7CPPHST') CONFMSG(*NO)
             GOTO       CMDLBL(ERROR)

ERROR:
/*           SNDDST     TYPE(*MSG) TOUSRID((PRKPURGE BIGBYTE)) +
                          DSTD('Item Transaction Detail Purge') +
                          MSG('Item Transaction Detail Purge to +
                          History Ended Abnormally')              */
             SNDPGRMSG  TOPGR(PRKPURGE) MSG('Item Transaction Detail +
                          Purge to History Ended Abnormally') +
                          CONFMSG(*NO)

             CHGJOB     LOG(4 00 *SECLVL)

             GOTO       CMDLBL(ENDPGM)
/*-----------------------------------------------------------------*/
/*                      PROGRAM END                                */
/*-----------------------------------------------------------------*/
PGMMSG:
/*           SNDDST     TYPE(*MSG) TOUSRID((PRKPURGE BIGBYTE)) +
                          DSTD('Item Transaction Detail Purge') +
                          MSG('Item  Transaction Detail Purge to +
                          History Ended Normally.')             */
             SNDPGRMSG  TOPGR(PRKPURGE) MSG('Item  Transaction +
                          Detail Purge to History Ended Normally.') +
                          CONFMSG(*NO)

ENDPGM:
             ENDPGM
