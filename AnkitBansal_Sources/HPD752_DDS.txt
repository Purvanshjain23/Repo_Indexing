     A*%%TS  SD  20060117  074242  ISLRAMS     REL-V5R3M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD752
     A*  TITLE:         VIEW ROOMS
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  CREATED:       03/18/94
     A*
     A*  INDICATOR 90 IS ON WHEN THE BUILDING THE USER HAS SELECTED
     A*               FROM THE WORK WITH HAS BEEN DELETED BY ANOTHER
     A*               USER
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A N90                                  CF05(05 'REFRESH')
     A                                      CA12(12 'PREVIOUS')
     A N82N90                               ROLLUP(25 'PAGE FORWARD')
     A*
     A*--------------------------------------------------------------*
     A* SUBFILE FORMAT ONE
     A*--------------------------------------------------------------*
     A          R HP752S1                   SFL
     A*%%TS  SD  20060117  074242  ISLRAMS     REL-V5R3M0  5722-WDS
     A                                      SETOF(30)
     A  85                                  SFLNXTCHG
     A            HDRRN1         4Y 0H
     A            DFOPT          2A  B 11  2CHGINPDFT(UL)
     A  90                                  DSPATR(PR)
     A  90                                  DSPATR(ND)
     A  30                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A N30N90                               DSPATR(HI)
     A N30N90                               DSPATR(UL)
     A            DFRMCD    R        O 11  8REFFLD(BRRMCD)
     A            DFRMST    R        O 11 18REFFLD(BRREC/BRRMST *LIBL/HSP020)
     A            DFBICD1   R        O 11 26REFFLD(HSRREF/RBBICD *LIBL/HSPREF)
     A            DFBICD2   R        O 11 36REFFLD(HSRREF/RBBICD *LIBL/HSPREF)
     A            DFRMSQFT  R        O 11 47REFFLD(HSRREF/@@RMSQFT *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A            DFWANINO  R        O 11 60REFFLD(HSRREF/@@WANINO *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A            DFFDSPIN  R        O 11 71REFFLD(HSRREF/@@FDSPIN *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A*
     A*--------------------------------------------------------------*
     A* SUBFILE CONTROL FORMAT ONE
     A*--------------------------------------------------------------*
     A          R HP752C1                   SFLCTL(HP752S1)
     A*%%TS  SD  20060117  074242  ISLRAMS     REL-V5R3M0  5722-WDS
     A                                      SFLSIZ(0100)
     A                                      SFLPAG(0011)
     A                                      OVERLAY
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 25'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  2 30'VIEW ROOMS'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  4  2'Farm site ..:'
     A            C1FSCD    R        O  4 17REFFLD(FSFSCD)
     A                                      EDTCDE(M)
     A            C1FSNM    R        O  4 24REFFLD(FSFSNM)
     A                                  5  2'Building ...:'
     A            C1BLCD    R        O  5 17REFFLD(FBBLCD)
     A            C1BLDS    R        O  5 24REFFLD(FBBLDS)
     A                                  7  2'10=View Equipment'
     A                                  7 47'Total'
     A                                      DSPATR(HI)
     A                                  7 59'Number'
     A                                      DSPATR(HI)
     A                                  7 71'Total'
     A                                      DSPATR(HI)
     A                                  8 47'Square'
     A                                      DSPATR(HI)
     A                                  8 61'of'
     A                                      DSPATR(HI)
     A                                  8 69'Inches of'
     A                                      DSPATR(HI)
     A                                  9 46'Feet in'
     A                                      DSPATR(HI)
     A                                  9 59'Water'
     A                                      DSPATR(HI)
     A                                  9 71'Feeder'
     A                                      DSPATR(HI)
     A                                 10  2'Opt'
     A                                      DSPATR(HI)
     A                                 10  8'Room'
     A                                      DSPATR(HI)
     A                                 10 16'Status'
     A                                      DSPATR(HI)
     A                                 10 25'---- Bin Sets ----'
     A                                      DSPATR(HI)
     A                                 10 48'Room'
     A                                      DSPATR(HI)
     A                                 10 58'Nipples'
     A                                      DSPATR(HI)
     A                                 10 71'Space'
     A                                      DSPATR(HI)
     A*
     A*
     A*--------------------------------------------------------------*
     A* COMMAND KEY RECORD 1
     A*--------------------------------------------------------------*
     A          R HP752K1
     A*%%TS  SD  19940321  095839  ISLRAMS     REL-V2R2M0  5738-PW1
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A                                      DSPATR(HI)
     A                                 23  2'F3=Exit'
     A N90                             23 11'F5=Refresh'
     A                                 23 23'F12=Previous'
     A*
     A*--------------------------------------------------------------*
     A* ERROR MESSAGE SUBFILE FORMAT
     A*--------------------------------------------------------------*
     A          R HP752ES                   SFL SFLMSGRCD(24)
     A*
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
     A*                                                                *********
     A*--------------------------------------------------------------*
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*--------------------------------------------------------------*
     A          R HP752EC                   SFLCTL(HP752ES)
     A*
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
