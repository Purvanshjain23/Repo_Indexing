      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Triumph Foods
      * PROGRAM:     TF207--Weekly Revenue Close: Edit with Error Listing
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     09/20/04
      *
      * Function:    This function edits the Sales data and prints an error listing.
      *
      *              The Weekly Revenue Close is all-or-nothing. So, if there are any
      *              errors, the process will terminate with this step.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      /eject
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Ftfp001    if   e           k disk
      *    Preliminary/final
      *
      *
     Fopbfcpmb  if   e           k disk
      *    Order header (records selected in open query)
      *
      *
     Fprint132  o    f  132        printer oflind(*inof)
      *
      /eject
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      *----------------------------------------------------------------
      * CONSTANTS
      *----------------------------------------------------------------
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      *----------------------------------------------------------------
      * ARRAYS AND TABLES
      *----------------------------------------------------------------
      *
      *----------------------------------------------------------------
      * STANDALONE FIELDS
      *----------------------------------------------------------------
      *
      * Standard fields
      *
     D dash            s            132    inz(*all'-')
     d rtime           s              6  0
      *
      *
      * Control fields
      *
     d first           s              1    inz('Y')
      *
      *
      * Workfileds
      *
     d wkerr1          s              1    inz('N')
      *
      *
      * Print fields
      *
     d h1pfds          s                   like(pfpfds)
      *
      *
      * Parm fields
      *
     d xxerror         s              1
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *---------------------------------------------------------------
      *  Local data area
      *---------------------------------------------------------------
      *
     d lda            uds                  dtaara(*lda)
     D  ldcesyn               30     36  0
     D  ldpfcd                37     37
      *
     D  ldfmdy                39     44  0
     D  ldtmdy                45     50  0
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * Mainline
      ****************************************************************
      *
      * Check:
      *  1) Orders
      *
     C                   exsr      $orders
      *
      * EOF processing
      *    If there was an error on Orders, processing will terminate
      *
     C                   if        wkerr1 = yes
     C                   move      yes           xxerror
     C                   endif
      *
     C                   seton                                        lr
      /eject
      *-----------------------------------------------------------------------------------
      * Check Orders
      *-----------------------------------------------------------------------------------
      *
      * Error conditions:
      *     1) Order not invoiced in the Order Header file
      *
     C     $orders       begsr
      *
     C                   exsr      $h1hdr
      *
      * Process all Order Header records selected by the Open Query:
      *   1) BEGNDT-Actual ship date in date range
      *   2) BEGWST-Order Header Status not 'X' (cancelled order)
      *   3) BEJNCD-Order Type not 'TR'         (transfer order)
      *
     C     *loval        setll     opbfcpmb
      *
     C                   dou       *in91 = *on                                  Do orders
     C                   read      opbfcpmb                               91
     C                   if        *in91 = *off                                 If not EOF
     C                   move      no            first
      *
     C                   if        begwst <> 'C'                                If not invoiced
     C                   move      yes           wkerr1
     C                   exsr      $l1dtl
     C                   endif                                                  If not invoiced
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Do orders
      *
      * EOF processing for Orders
      *
     C                   select
     C                   when      first = yes
     C                   except    nodata
     C                   move      yes           wkerr1
     C
     C                   when      wkerr1 = no
     C                   except    noerror
     C                   endsl
      *
     C                   endsr
      /eject
      *-----------------------------------------------------------------------------------
      * Print headings
      *-----------------------------------------------------------------------------------
      *
     C     $h1hdr        begsr
      *
     C                   except    h1hdr
      *
     C                   endsr
      /eject
      *-----------------------------------------------------------------------------------
      * Print detail line
      *-----------------------------------------------------------------------------------
      *
     C     $l1dtl        begsr
      *
     C                   if        *inof = *on
     C                   exsr      $h1hdr
     C                   endif
      *
     C                   except    l1dtl
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Parm lists
      *
     C     *entry        plist
     C                   parm                    xxerror
      *
      *
      * Set up heading info.
      *
     C                   time                    rtime
      *
      * Retrieve 'preliminary/final' description
      *
     C     ldpfcd        chain     tfp001                             92
     C                   select
     C                   when      *in92 = *on
     C                   eval      h1pfds = 'Unknown'
      *
     C                   when      ldpfcd = 'P'
     C                   movel     pfpfds        h1pfds
      *
     C                   when      ldpfcd = 'F'
     C                   eval      %subst(h1pfds:4:5) = %subst(pfpfds:1:5)
     C                   endsl
      *
     C                   endsr
      /eject
      *-------------------------------------------------------------
      * Report heading lines
      *-------------------------------------------------------------
      *
     Oprint132  e            h1hdr          1 01
     O                       sdpgm               10
     O                                           75 'Weekly Revenue Close'
     O                                          122 'Date'
     O                       udate         y    132
      *
     O          e            h1hdr          1
     O                       sdusr               10
     O                                           71 'Error Listing'
     O                                          122 'Time'
     O                       rtime              132 '  :  :  '
      *
     O          e            h1hdr          1
     O                       h1pfds              70
     O                                          122 'Page'
     O                       page          z    132
      *
      *
     O          e            h1hdr       0  2
     O                                           74 'Orders Not Invoiced'
      *
      *
     O          e            h1hdr          1
     O                                           10 'From date:'
     O                       ldfmdy              20 '  /  /  '
      *
      *
     O          e            h1hdr          2
     O                                           10 'To date .:'
     O                       ldtmdy              20 '  /  /  '
      *
      *
      *-------------------------------------------------------------
      * Column headings
      *-------------------------------------------------------------
      *
     O          e            h1hdr       0  1
     O                                            7 'Company'
     O                                           16 'Order'
     O                                           38 'Bill To Customer'
      *
     O          e            h1hdr       0  1
     O                       dash               132
      *
      *-------------------------------------------------------------
      * Detail line
      *-------------------------------------------------------------
      *
     O          e            l1dtl          2
     O                       beaic3        z      5                             Company
     O                       bec4nb        z     16                             Order
     O                       beb0c7        z     29                             Bill to Cust #
     O                       beaetx              61                             Bill to Cust Name
      *
      *
      *-------------------------------------------------------------
      * There were no errors.
      *-------------------------------------------------------------
      *
     O          e            noerror        2
     O                                           26 'There are no order errors.'
      *-------------------------------------------------------------
      * There was no data.
      *-------------------------------------------------------------
      *
     O          e            nodata         2
     O                                           21 'There were no orders.'
      /eject
