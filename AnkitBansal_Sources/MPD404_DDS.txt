     A*%%TS  SD  20140306  133410  ISLRAMS     REL-V6R1M0  5761-WDS
     A*****************************************************************
     A* ENVIRONMENT:  PORK DIVISION
     A* SYSTEM:       AS/400
     A* FILE:         MPD404
     A* DESCRIPTION:  MP2: WORK WITH INVOICES FOR A PURCHASE ORDER
     A* PROGRAMMER:   LEANNE FEDOR
     A* DATE:         04/01/02
     A*
     A*----------------------------------------------------------------
     A* MODIFICATIONS LOG:
     A*----------------------------------------------------------------
     A* 05/26/21 S17068 Danny Nguyen
     A*          Changed screen size from 24 x 80 to 27 x 132 to accomodate
     A*          amount fields increase in length.
     A*          Shifted field SDUSR & SDJOB to the right of the screen.
     A*          Changed the following fields to use the full length (from
     A*          9.2 to 11.2) & shifted the fields to display properly:
     A*          Invoiced Amount, Amount To Apply, Applied Amount &
     A*          Remaining Amount to Apply.
     A*
     A*****************************************************************
     A*%%EC
17068A**                                    DSPSIZ(24 80 *DS3)
17068A                                      DSPSIZ(27 132 *DS4)
     A                                      REF(*LIBL/MP2REF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A                                      CF05(05 'REFRESH')
     A                                      CF06(06 'CREATE')
     A*
     A*
     A*---------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*---------------------------------------------------------------
     A          R MP404S1                   SFL
     A*%%TS  SD  20020924  140540  ISLRAMS     REL-V5R1M0  5722-WDS
     A  85                                  SFLNXTCHG
     A            HDRRN1         4Y 0H
     A            HDPOSN    R        H      REFFLD(@@POSN)
     A            D1OPT     R        B 12  2REFFLD(@@OPT)
     A                                      CHGINPDFT(UL)
     A  30                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A N30                                  DSPATR(HI)
     A            D1INNO    R        O 12  5REFFLD(@@INNO)
     A            D1INMDY        6Y 0O 12 26EDTWRD('  /  /  ')
17068A**          D1INAM         8Y 2O 12 35EDTCDE(M)
  |  A            D1INAM        11Y 2O 12 35EDTCDE(M)
  |  A**          D1TAAM         8Y 2O 12 46EDTCDE(M)
  |  A            D1TAAM        11Y 2O 12 50EDTCDE(M)
  |  A**          D1APAM         8Y 2O 12 57EDTCDE(M)
  |  A            D1APAM        11Y 2O 12 65EDTCDE(M)
  |  A**          D1RMAM         8Y 2O 12 68EDTCDE(M)
  |  A            D1RMAM        11Y 2O 12 80EDTCDE(M)
  |  A**          D1POFL    R        O 12 79REFFLD(@@POFL)
17068A            D1POFL    R        O 12 97REFFLD(@@POFL)
     A*
     A*---------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*---------------------------------------------------------------
     A          R MP404C1                   SFLCTL(MP404S1)
     A*%%TS  SD  20140306  133410  ISLRAMS     REL-V6R1M0  5761-WDS
     A                                      SFLSIZ(0050)
     A                                      SFLPAG(0008)
     A N82                                  ROLLUP(25 'PAGE FORWARD')
     A                                      OVERLAY
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND(*MORE)
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            SDPGM         10A  O  1  2DSPATR(HI)
17068A**                                1 26'MP2 INTERFACE TO JDE'
17068A                                  1 34'MP2 INTERFACE TO JDE'
     A                                      DSPATR(HI)
17068A**          SDUSR         10A  O  1 70DSPATR(HI)
  |  A            SDUSR         10A  O  1 95DSPATR(HI)
  |  A**                                2 17'WORK WITH INVOICES FOR A PURCHASE -
17068A                                  2 26'WORK WITH INVOICES FOR A PURCHASE -
     A                                      ORDER'
     A                                      DSPATR(HI)
17068A**          SDJOB         10A  O  2 70DSPATR(HI)
17068A            SDJOB         10A  O  2 95DSPATR(HI)
     A                                  4  2'Vendor .......:'
     A            C1JDVNNO  R        O  4 18REFFLD(@@JDVNNO)
     A                                      EDTCDE(M)
     A            C1JDVNNM      30A  O  4 28
     A                                  5  2'Purchase order:'
     A            C1PONO    R        O  5 18REFFLD(@@PONO)
     A** S17068-Commented out & moved below.
17068A**                                5 74'Posted'
17068A**                                    DSPATR(HI)
     A                                  6  2'Purchasing center/Accounting compa-
     A                                      ny:'
     A            C1PCID    R        O  6 40REFFLD(@@PCID)
     A            C1ACONO   R    3 00O  6 46REFFLD(@@ACONO)
     A                                      EDTCDE(Z)
     A** S17068-Commented out & moved below.
17068A**                                6 74'to JDE'
17068A**                                    DSPATR(HI)
     A                                  8  2'2=Revise'
     A                                  8 12'5=View'
17068A**                                8 50'Total'
17068A                                  8 56'Total'
     A                                      DSPATR(HI)
17068A**                                8 68'Remaining'
17068A                                  8 84'Remaining'
     A                                      DSPATR(HI)
     A                                  9 26'Invoice'
     A                                      DSPATR(HI)
17068A**                                9 36'Invoiced'
17068A                                  9 39'Invoiced'
     A                                      DSPATR(HI)
17068A**                                9 49'Amount'
17068A                                  9 56'Amount'
     A                                      DSPATR(HI)
17068A**                                9 59'Applied'
17068A                                  9 70'Applied'
     A                                      DSPATR(HI)
17068A**                                9 71'Amount'
17068A                                  9 87'Amount'
     A                                      DSPATR(HI)
17068A                                  9 95'Posted'
17068A                                      DSPATR(HI)
     A                                 10  5'Invoice Number'
     A                                      DSPATR(HI)
     A                                 10 28'Date'
     A                                      DSPATR(HI)
17068A**                               10 38'Amount'
17068A                                 10 41'Amount'
     A                                      DSPATR(HI)
17068A**                               10 47'To Apply'
17068A                                 10 54'To Apply'
     A                                      DSPATR(HI)
17068A**                               10 60'Amount'
17068A                                 10 71'Amount'
     A                                      DSPATR(HI)
17068A**                               10 69'To Apply'
17068A                                 10 85'To Apply'
     A                                      DSPATR(HI)
17068A                                 10 95'to JDE'
  |  A                                      DSPATR(HI)
  |  A**          C1POFL    R        B 11 79REFFLD(@@POFL)
17068A            C1POFL    R        B 11 97REFFLD(@@POFL)
     A                                      DSPATR(HI)
     A*
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R MP404K1
     A*%%TS  SD  20020522  110208  ISLRAMS     REL-V5R1M0  5722-WDS
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 22  2'F3=Exit'
     A                                 22 11'F5=Refresh'
     A                                 22 23'F6=Create'
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *---------------------------------------------------------------
     A          R MP404ES                   SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE CONTROL FORMAT
      *---------------------------------------------------------------
     A          R MP404EC                   SFLCTL(MP404ES)
      *
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
