     A*%%TS  SD  20060112  141241  ISLRAMS     REL-V5R3M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION
     A*  DISPLAY FILE:  HPD4440
     A*  TITLE:         SPECIFY GROUP PRINT/DOWNLOAD SELECTIONS
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  DATE:          12/29/05
     A*
     A*  NOTE:  THIS IS A POP-UP WINDOW. THE WINDOW DEFINITION IS IN
     A*         RECORD FORMAT HP4440EC
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL)
     A                                      PRINT
     A                                      CF03(03 'EXIT')
     A                                      CF08(08 'SUBMIT')
     A                                      CF17(17 'PRINT OPTIONS')
     A                                      CF20(20 'WRKSPLF')
     A*
     A*----------------------------------------------------------------
     A* RECORD FORMAT ONE
     A*----------------------------------------------------------------
     A          R HP4440R1
     A*%%TS  SD  20060112  135353  ISLRAMS     REL-V5R3M0  5722-WDS
     A                                      WINDOW(HP4440EC)
     A                                      TEXT('INPUT SCREEN')
     A                                      OVERLAY
     A                                      SETOF(30)
     A                                      SETOF(31)
     A                                      SETOF(32)
     A                                      SETOF(33)
     A                                      SETOF(34)
     A                                      SETOF(35)
     A            SDPGM     R        O  1  1REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 17'SPECIFY GROUP REPORT/DOWNLOAD SELE-
     A                                      CTIONS'
     A                                      DSPATR(HI)
     A                                  3  1'Hog group:'
     A            D1HGCD    R        O  3 12REFFLD(HSRREF/@@HGCD *LIBL/HSPREF)
     A                                  5  1'Enter'
     A                                      DSPATR(HI)
     A                                  5  7'1'
     A                                      DSPATR(HI)
     A                                      COLOR(PNK)
     A                                  5  9'for each of the Reports/Downloads -
     A                                      you wish to generate:'
     A                                      DSPATR(HI)
     A            D1OPT1         1A  B  7  1
     A N31                                  DSPATR(HI)
     A  31                                  DSPATR(RI)
     A  31                                  DSPATR(PC)
     A                                  7  4'Print Review with Costs'
     A            D1OPT2         1A  B  8  1
     A N32                                  DSPATR(HI)
     A  32                                  DSPATR(RI)
     A  32                                  DSPATR(PC)
     A                                  8  4'Print Review without Costs'
     A            D1OPT3         1A  B  9  1
     A N33                                  DSPATR(HI)
     A  33                                  DSPATR(RI)
     A  33                                  DSPATR(PC)
     A                                  9  4'Print Movements'
     A            D1OPT4         1A  B 10  1
     A N34                                  DSPATR(HI)
     A  34                                  DSPATR(RI)
     A  34                                  DSPATR(PC)
     A                                 10  4'Download Movements'
     A                                 10 25'An E-Mail Address is required for -
     A                                      the Downloads:'
     A                                      COLOR(PNK)
     A            D1OPT5         1A  B 11  1
     A N35                                  DSPATR(HI)
     A  35                                  DSPATR(RI)
     A  35                                  DSPATR(PC)
     A                                 11  4'Download Feed'
     A            D1EMAIL   R        B 11 25REFFLD(HSRREF/@@EMAIL *LIBL/HSPREF)
     A                                      CHGINPDFT(UL LC FE)
     A N30                                  DSPATR(HI)
     A  30                                  DSPATR(RI)
     A  30                                  DSPATR(PC)
     A*
     A*----------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*----------------------------------------------------------------
     A          R HP4440K1
     A*%%TS  SD  20060112  141241  ISLRAMS     REL-V5R3M0  5722-WDS
     A                                      WINDOW(HP4440EC)
     A                                      TEXT('COMMAND KEYS')
     A                                      OVERLAY
     A  43                             13  2'Message program failed. Call IS.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 14  1'F3=Exit'
     A                                 14 10'F8=Submit'
     A                                 14 21'F17-Print Options'
     A                                 14 40'F20=Spool Files'
      *
      *----------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *----------------------------------------------------------------
     A          R HP4440ES
     A                                      TEXT('Error message subfile')
     A                                      SFL SFLMSGRCD(15)
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
     A*
     A*----------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT WITH WINDOW DEFINITION
     A*----------------------------------------------------------------
     A          R HP4440EC                  SFLCTL(HP4440ES)
     A*%%TS  SD  20000628  103649  ISLRAMS     REL-V4R2M0  5769-PW1
     A                                      TEXT('Error message subfile')
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A                                      WINDOW(*DFT 16 75)
     A                                      WDWBORDER((*DSPATR RI) (*CHAR '    -
     A                                          '))
     A            MSGPGM                    SFLPGMQ(10)
     A*
     A*----------------------------------------------------------------
     A* ASSUME FORMAT - REQUIRED FOR WINDOWS
     A*----------------------------------------------------------------
     A*
     A          R ASSUME
     A*%%TS  SD  20000627  095625  ISLRAMS     REL-V4R2M0  5769-PW1
     A                                      TEXT('ASSUME FORMAT FOR WINDOWS')
     A                                      ASSUME
     A                                      OVERLAY
     A                                 24 79' '
