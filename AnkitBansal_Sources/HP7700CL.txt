/***************************************************************** */
/*                                                                 */
/*  PROGRAM NUMBER:  HP7700CL                                      */
/*  PROGRAM NAME:    LISTING OF FARM/TEMPLATE LINKS                */
/*  PROGRAMMER:      LEANNE FEDOR                                  */
/*  CREATE DATE:     02/07/05                                      */
/*                                                                 */
/*  FUNCTION:        THIS CL IS SUBMITTED WITH QCMDEXC FROM        */
/*                   HP7700-VIEW FARM/TEMPLATE LINKDS.             */
/*                   OPTIONS ARE PASSED THROUGH THE LDA.           */
/*                                                                 */
/*                                                                 */
/*******************************************************************/
/*  MODIFIED:                                                      */
/*******************************************************************/

             PGM

/* DEFINE THE VARIABLE REQUIRED IN THE COMMAND TO OVERRIDE THE     */
/* PRINT FILE TO A LASER PRINTER                                   */

             DCL        VAR(&CMD) TYPE(*CHAR) LEN(512)
             DCL        VAR(&QDATA) TYPE(*CHAR) LEN(61)

             DCL        VAR(&LDFSCD) TYPE(*DEC)  LEN(5)
             DCL        VAR(&LDBTCD) TYPE(*CHAR) LEN(6)

             DCL        VAR(&HOLD) TYPE(*CHAR) LEN(10)
             DCL        VAR(&COPYS) TYPE(*DEC) LEN(2)
             DCL        VAR(&OUTQ) TYPE(*CHAR) LEN(10)

             CHGVAR     VAR(&LDFSCD) VALUE(%SST(*LDA 1 5))
             CHGVAR     VAR(&LDBTCD) VALUE(%SST(*LDA 6 6))


/*------------------------------------------------------------------*/
/*  SET UP PRINT FILE OVERRIDES                                     */
/*------------------------------------------------------------------*/

 HOLD:       IF         COND(%SST(*LDA 411 1) = 'Y') THEN(CHGVAR +
                          VAR(&HOLD) VALUE(*YES))
             ELSE       CMD(CHGVAR VAR(&HOLD) VALUE(*NO))

 COPYS:      CHGVAR     VAR(&COPYS) VALUE(%SST(*LDA 412 1))
             IF         COND(&COPYS = 0) THEN(CHGVAR VAR(&COPYS) +
                          VALUE(1))

 OUTQ:       CHGVAR     VAR(&OUTQ) VALUE(%SST(*LDA 413 10))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(%SST(*LDA 401 10)))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(*JOB))


/*------------------------------------------------------------------*/
/* SET UP OPEN QUERY                                                */
/*------------------------------------------------------------------*/

             CHGVAR     VAR(%SST(&QDATA 1 14)) VALUE('TLBTCD *NE +
                          " "')

/*  IF A SINGLE TEMPLATE                                         */

             IF         COND(&LDBTCD *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 16 17)) VALUE('*AND TLBTCD *EQ "')
             CHGVAR     VAR(%SST(&QDATA 33 6)) VALUE(&LDBTCD)
             CHGVAR     VAR(%SST(&QDATA 39 1)) VALUE('"')
             ENDDO

/*  IF FARM SITE                                                 */

             IF         COND(&LDFSCD *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 41 15)) VALUE('*AND TLFSCD +
                          *EQ')
             CHGVAR     VAR(%SST(&QDATA 57 5)) VALUE(&LDFSCD)
             ENDDO

/*------------------------------------------------------------------*/
/* RUN OPEN QUERY                                                   */
/*------------------------------------------------------------------*/


             OVRDBF     FILE(HSL187A) SHARE(*YES)
             OPNQRYF    FILE((HSL187A)) QRYSLT(&QDATA) KEYFLD(*FILE)

             SBDPRTOVR  CMDVAR(&CMD) FILE(QPRINT) OUTQ(&OUTQ) +
                          WIDTH(132) LANDSCAPE(*PRTF) DRAWER(*PRTF) +
                          COPIES(&COPYS) HOLD(&HOLD)
             CALL       PGM(QCMDEXC) PARM(&CMD 512)

             CALL       PGM(HP6707)

             CLOF       OPNID(HSL187A)
             DLTOVR     FILE(*ALL)

             ENDPGM

