/********************************************************************/
/*                                                                  */
/*  SYNOPSIS -  THIS CL IS CALLED FROM THE SCHEDULER TO BUILD THE   */
/*              CARRIER INTERNET LOAD STOP DATA FILE.              */
/*                                                                  */
/*  SYSTEM    - ORDER MANAGEMENT SYSTEM                             */
/*  PROGRAM   - BUILD LOAD STOP DATA FILE                          */
/*  DATE      - 02/09/98                                            */
/*                                                                  */
/*  DESCRIPTION -  BUILD THE LOAD STOP DATA FILE TO BE DOWNLOADED    */
/*                 TO THE INTERNET AND SET THE LASTRRN DTAARA        */
/*                 POSITIONS 1-5 (LAST RRN PROCESSED FOR LOAD STOP)  */
/*                 TO THE RECORD COUNT IN THE NEWLY BUILT LOAD STOP.*/
/*                 SET POSITIONS 6-10 (LAST RRN PROCESSED            */
/*                 FROM THE LOAD COMMENT UPL FILE) TO ZEROS.         */
/*                                                                  */
/*      PARAMETERS:                                                 */
/*                                                                  */
/*     O    &RETURN     - RETURN CODE                               */
/*                                                                  */
/********************************************************************/
/*  REVISIONS                                                       */
/*  RMC 1//23/2001  WHEN COPYING PDL0CPP TO SVPDL0CPP, *REPLACE     */
/*                  INSTEAD OF *ADDING RECORDS.                     */
/********************************************************************/

 PDITUPC:    PGM        PARM(&RETURN)
/* PARMS                                                                      */
        DCL    &RETURN     *CHAR   (7)           /* Return Code               */

/* PROGRAM VARIABLE                         */
             DCL        VAR(&RCDCNT) TYPE(*CHAR) LEN(5) +
                          VALUE('00000') /* ReCORD +
                          COUNT              */

/* ERROR MESSAGE FIELDS                                                       */
        DCL    &MSG        *CHAR   (512)         /* message                   */
        DCL    &MSGID      *CHAR   (7)           /* message id                */
        DCL    &MSGL       *DEC    (5 0)

/********************************************************************/
/*   INITIALIZE VARIABLES                                           */
/********************************************************************/

             CHGVAR     VAR(&RETURN) VALUE('       ')
/********************************************************************/
/*   MONITOR FOR ALL MESSAGES AT PROGRAM LEVEL                      */
/********************************************************************/

             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(MSGTRAP))

/********************************************************************/
/*   SAVE THE LOAD STOP DATA FILE BEFORE REBUILDING            */
/********************************************************************/
             CPYF       FROMFILE(PDL0CPP) TOFILE(SVPDL0CPP) +
                          MBROPT(*REPLACE) FMTOPT(*MAP *DROP)
             CLRPFM     FILE(PDL0CPP)

/********************************************************************/
/*   BUILD THE FILE TO BE DOWNLOADED TO THE INTERNET             */
/********************************************************************/

             CALL       PGM(PDGSXFR) PARM(&RETURN &RCDCNT)
             IF COND(&RETURN *NE '      ') THEN(GOTO USERQUIT)

/********************************************************************/
/*   RESET THE RRN DATA AREA TO ZERO AND TO LOAD STOP RCD COUNT  */
/********************************************************************/
             CHGDTAARA  DTAARA(LASTRRN) VALUE('0000000000')
             CHGDTAARA  DTAARA(LASTRRN (1 5)) VALUE(&RCDCNT)

             GOTO       CMDLBL(MSGTRAP)

/*-------------------------USER QUIT--------------------------------*/
 USERQUIT:   CHGVAR     VAR(&RETURN) VALUE('Y2U0005')
             GOTO       CMDLBL(END)

/*----------------------RECEIVE MESSAGES----------------------------*/

 MSGTRAP:    RCVMSG     MSGTYPE(*EXCP) MSGDTA(&MSG) MSGDTALEN(&MSGL) +
                          MSGID(&MSGID)
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(END))

             SNDPGMMSG  MSGID(&MSGID) MSGF(QCPFMSG) MSGDTA(%SST(&MSG +
                          1 &MSGL)) MSGTYPE(*ESCAPE)
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(END))

 END:        ENDPGM

