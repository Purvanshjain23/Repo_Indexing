/***************************************************************** */
/*                                                                 */
/*  PROGRAM NUMBER:  HP4708CL                                      */
/*  PROGRAM NAME:    ITEM LISTING                                  */
/*  PROGRAMMER:      LEANNE FEDOR                                  */
/*  CREATE DATE:     07/19/04                                      */
/*                                                                 */
/*  FUNCTION:        THIS CL IS SUBMITTED WITH QCMDEXC FROM        */
/*                   HP4708-SPECIFY.                               */
/*                                                                 */
/*                   OPTIONS ARE PASSED THROUGH THE LDA.           */
/*                                                                 */
/*******************************************************************/
/*  MODIFIED:                                                      */
/*******************************************************************/

             PGM

             DCL        VAR(&CMD) TYPE(*CHAR) LEN(512)
             DCL        VAR(&QDATA) TYPE(*CHAR) LEN(233)

             DCL        VAR(&LDVER) TYPE(*CHAR) LEN(1)
             DCL        VAR(&LDITCD) TYPE(*CHAR) LEN(25)
             DCL        VAR(&LDBGIT) TYPE(*CHAR) LEN(25)
             DCL        VAR(&LDITTYCD) TYPE(*CHAR) LEN(6)
             DCL        VAR(&LDAIST) TYPE(*CHAR) LEN(1)
             DCL        VAR(&LDBGFL) TYPE(*CHAR) LEN(1)
             DCL        VAR(&LDCRITFL) TYPE(*CHAR) LEN(1)

             DCL        VAR(&HOLD) TYPE(*CHAR) LEN(10)
             DCL        VAR(&COPYS) TYPE(*DEC) LEN(2)
             DCL        VAR(&OUTQ) TYPE(*CHAR) LEN(10)

             CHGVAR     VAR(&LDVER) VALUE(%SST(*LDA 1 1))
             CHGVAR     VAR(&LDITCD) VALUE(%SST(*LDA 2 25))
             CHGVAR     VAR(&LDBGIT) VALUE(%SST(*LDA 27 25))
             CHGVAR     VAR(&LDITTYCD) VALUE(%SST(*LDA 52 6))
             CHGVAR     VAR(&LDAIST) VALUE(%SST(*LDA 58 1))
             CHGVAR     VAR(&LDBGFL) VALUE(%SST(*LDA 59 1))
             CHGVAR     VAR(&LDCRITFL) VALUE(%SST(*LDA 60 1))


/*-------------------------------------------------------------------*/
/* SET UP/RUN OPEN QUERY TO SELECT THE RECORDS                       */
/*-------------------------------------------------------------------*/

/* SET UP A DEFAULT STARTING POINT                                      */

             CHGVAR     VAR(%SST(&QDATA 1 14)) VALUE('IMITCD *NE " "')

/*  ITEM NUMBER                                                         */

             IF         COND(&LDITCD *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 16 17)) VALUE('*AND IMITCD *EQ "')
             CHGVAR     VAR(%SST(&QDATA 33 25)) VALUE(&LDITCD)
             CHGVAR     VAR(%SST(&QDATA 58 1)) VALUE('"')
             ENDDO

/*  BUDGET ITEM                                                   */

             IF         COND(&LDBGIT *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 60 17)) VALUE('*AND IMBGIT *EQ "')
             CHGVAR     VAR(%SST(&QDATA 77 25)) VALUE(&LDBGIT)
             CHGVAR     VAR(%SST(&QDATA 102 1)) VALUE('"')
             ENDDO

/*  ITEM TYPE CODE                                                */

             IF         COND(&LDITTYCD *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 104 19)) VALUE('*AND IMITTYCD *EQ "')
             CHGVAR     VAR(%SST(&QDATA 123 6)) VALUE(&LDITTYCD)
             CHGVAR     VAR(%SST(&QDATA 129 1)) VALUE('"')
             ENDDO

/* ACTIVE/INACTIVE STATUS                                          */

             IF         COND(&LDAIST *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 131 17)) VALUE('*AND IMAIST *EQ "')
             CHGVAR     VAR(%SST(&QDATA 148 1)) VALUE(&LDAIST)
             CHGVAR     VAR(%SST(&QDATA 149 1)) VALUE('"')
             ENDDO

/* BUDGET FLAG                                                     */

             IF         COND(&LDBGFL *EQ 'Y') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 151 19)) VALUE('*AND IMBGFL +
                          *EQ "Y"')
             ENDDO

             IF         COND(&LDBGFL *EQ 'N') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 171 19)) VALUE('*AND IMBGFL +
                          *NE "Y"')
             ENDDO


/* CRITICAL FLAG                                                */

             IF         COND(&LDCRITFL *EQ 'Y') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 191 21)) VALUE('*AND IMCRITFL +
                          *EQ "Y"')
             ENDDO

             IF         COND(&LDCRITFL *EQ 'N') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 213 21)) VALUE('*AND IMCRITFL +
                          *NE "Y"')
             ENDDO


/* VERSION 1 LISTING--SORTED BY ITEM                     */

             IF         COND(&LDVER *EQ '1') THEN(DO)
             OVRDBF     FILE(HSP181) SHARE(*YES)
             OPNQRYF    FILE((HSP181)) QRYSLT(&QDATA) KEYFLD(*FILE) +
                          SEQONLY(*YES)
             ENDDO

/* VERSION 2 LISTING--SORTED BY BUDGET ITEM              */

             IF         COND(&LDVER *EQ '2') THEN(DO)
             OVRDBF     FILE(HSP181) SHARE(*YES)
             OPNQRYF    FILE((HSP181)) QRYSLT(&QDATA) +
                          KEYFLD((IMBGIT)) SEQONLY(*YES)
             ENDDO

/*------------------------------------------------------------------*/
/*  SET UP PRINT FILE OVERRIDES                                      */
/*------------------------------------------------------------------*/

/* SET PARAMETERS FROM LDA                                           */

 HOLD:       IF         COND(%SST(*LDA 411 1) = 'Y') THEN(CHGVAR +
                          VAR(&HOLD) VALUE(*YES))
             ELSE       CMD(CHGVAR VAR(&HOLD) VALUE(*NO))

 COPYS:      CHGVAR     VAR(&COPYS) VALUE(%SST(*LDA 412 1))
             IF         COND(&COPYS = 0) THEN(CHGVAR VAR(&COPYS) +
                          VALUE(1))

 OUTQ:       CHGVAR     VAR(&OUTQ) VALUE(%SST(*LDA 413 10))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(%SST(*LDA 401 10)))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(*JOB))

/*------------------------------------------------------------------*/
/* OVERRIDE PRINT FILE                                              */
/*------------------------------------------------------------------*/

/* USE THE CUSTOMIZED SEABOARD COMMAND TO OVERRIDE THE PRINT FILE.  */
/* THIS COMMAND ALLOWS THE REPORT TO BE GENERATED ON A STANDARD LINE*/
/* PRINTER OR A LASER PRINTER. TO USE THIS OVERRIDE YOU MUST DECLARE*/
/* A COMMAND VARIABLE AND FOLLOW THE OVERRIDE WITH A CALL TO        */
/* QCMDEXC.                                                         */


             SBDPRTOVR  CMDVAR(&CMD) FILE(QPRINT) OUTQ(&OUTQ) +
                          WIDTH(198) LANDSCAPE(*PRTF) DRAWER(*PRTF) +
                          COPIES(&COPYS) HOLD(&HOLD)

             CALL       PGM(QCMDEXC) PARM(&CMD 512)


/*------------------------------------------------------------------*/
/*  CALL PRINT PROGRAM                                               */
/*------------------------------------------------------------------*/

             CALL       PGM(HP6708)

             CLOF       OPNID(HSP181)
             DLTOVR     FILE(HSP181)

             ENDPGM
