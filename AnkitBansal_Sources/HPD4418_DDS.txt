     A*%%TS  SD  20030217  113040  ISLRAMS     REL-V5R1M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION
     A*  DISPLAY FILE:  HPD4418
     A*  TITLE:         SPECIFY FARM HEALTH STATUS LISTING OPTIONS
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  DATE:          02/18/03
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CF03(03 'EXIT')
     A                                      CF04(04 'PROMPT')
     A                                      CF08(08 'SUBMIT')
     A*
     A*----------------------------------------------------------------
     A* RECORD FORMAT ONE
     A*----------------------------------------------------------------
     A          R HP4418R1
     A*%%TS  SD  20030217  113040  ISLRAMS     REL-V5R1M0  5722-WDS
     A                                      WINDOW(HP4418EC)
     A                                      TEXT('INPUT SCREEN')
     A                                      SETOF(30)
     A                                      SETOF(31)
     A                                      SETOF(32)
     A                                      SETOF(33)
     A                                      SETOF(34)
     A                                      SETOF(35)
     A                                      SETOF(36)
     A                                      SETOF(50)
     A                                      SETOF(51)
     A                                      SETOF(55)
     A                                      SETOF(56)
     A                                      OVERLAY
     A                                      RTNCSRLOC(&RCD &FLD)
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  1REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 14'SPECIFY FARM HEALTH STATUS LISTING-
     A                                       OPTIONS'
     A                                      DSPATR(HI)
     A                                  3  1'Show only the last status ...'
     A                                  3 31'?'
     A            D1YNFL    R        B  3 33REFFLD(HSRREF/@@FLAG *LIBL/HSPREF)
     A  30
     AO 50                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A N30                                  DSPATR(HI)
     A                                  4  1'From effective date .........'
     A            D1FMDY    R        B  4 33REFFLD(HSRREF/@@DT06 *LIBL/HSPREF)
     A                                      EDTWRD('  /  /  ')
     A  32                                  DSPATR(PC)
     A  32                                  DSPATR(RI)
     A N32                                  DSPATR(HI)
     A                                  4 43'(Blank for all)'
     A                                  5  1'To effective date ...........'
     A            D1TMDY    R        B  5 33REFFLD(HSRREF/@@DT06 *LIBL/HSPREF)
     A                                      EDTWRD('  /  /  ')
     A  33                                  DSPATR(PC)
     A  33                                  DSPATR(RI)
     A N33                                  DSPATR(HI)
     A                                  5 43'(Blank for all)'
     A                                  6  1'Health code .................'
     A                                  6 31'?'
     A            D1HCCD    R        B  6 33REFFLD(HSRREF/@@HCCD *LIBL/HSPREF)
     A  35
     AO 55                                  DSPATR(PC)
     A  35                                  DSPATR(RI)
     A N35                                  DSPATR(HI)
     A                                  6 43'(Blank for all)'
     A                                  7  1'Health status ...............'
     A                                  7 31'?'
     A            D1HSFL    R        B  7 33REFFLD(HSRREF/@@HSFL *LIBL/HSPREF)
     A  36
     AO 56                                  DSPATR(PC)
     A  36                                  DSPATR(RI)
     A N36                                  DSPATR(HI)
     A                                  7 43'(Blank for all)'
     A                                  8  1'Cell ........................'
     A            D1CELL    R        B  8 33REFFLD(HSRREF/@@CELL *LIBL/HSPREF)
     A                                      EDTCDE(M)
     A  34                                  DSPATR(PC)
     A  34                                  DSPATR(RI)
     A N34                                  DSPATR(HI)
     A                                  8 43'(Blank for all)'
     A                                  9  1'Farm site ...................'
     A                                  9 31'?'
     A            D1FSCD    R        B  9 33REFFLD(HSRREF/@@FSCD *LIBL/HSPREF)
     A                                      EDTCDE(M)
     A  31
     AO 51                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A N31                                  DSPATR(HI)
     A                                  9 43'(Blank for all)'
     A*
     A*----------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*----------------------------------------------------------------
     A          R HP4418K1
     A*%%TS  SD  20000605  133618  ISLRAMS     REL-V4R2M0  5769-PW1
     A                                      WINDOW(HP4418EC)
     A                                      TEXT('COMMAND KEYS')
     A                                      OVERLAY
     A  43                             10  2'Message program failed. Call IS.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 11  2'F3=Exit'
     A                                 11 11'F4=Prompt'
     A                                 11 22'F8=Submit'
      *
      *----------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *----------------------------------------------------------------
     A          R HP4418ES
     A                                      TEXT('Error message subfile')
     A                                      SFL SFLMSGRCD(12)
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
     A*
     A*----------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT WITH WINDOW DEFINITION
     A*----------------------------------------------------------------
     A          R HP4418EC                  SFLCTL(HP4418ES)
     A*%%TS  SD  20000628  103649  ISLRAMS     REL-V4R2M0  5769-PW1
     A                                      TEXT('Error message subfile')
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A                                      WINDOW(*DFT 13 70)
     A                                      WDWBORDER((*DSPATR RI) (*CHAR '    -
     A                                          '))
     A            MSGPGM                    SFLPGMQ(10)
     A*
     A*----------------------------------------------------------------
     A* ASSUME FORMAT - REQUIRED FOR WINDOWS
     A*----------------------------------------------------------------
     A*
     A          R ASSUME
     A*%%TS  SD  20000627  095625  ISLRAMS     REL-V4R2M0  5769-PW1
     A                                      TEXT('ASSUME FORMAT FOR WINDOWS')
     A                                      ASSUME
     A                                      OVERLAY
     A                                 24 79' '
