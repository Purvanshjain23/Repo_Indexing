/***************************************************************** */
/*  PROGRAM NUMBER --> HP498CL                                     */
/*  PROGRAM NAME ----> CLOSED HOG RATION REPORT                    */
/*  PROGRAMMER ------> LEANNE FEDOR                                */
/*  DATE ------------> 09/18/95                                    */
/*                                                                 */
/*  PROGRAM PURPOSE -> THIS CL CALLS THE PROGRAM TO PRINT THE      */
/*                     CLOSED GROUP RATION REPORT.                 */
/*                     VALUES ARE PASSED THRU THE LDA FROM         */
/*                     SPECIFY HP498.                              */
/*                                                                 */
/***************************************************************** */
/*  MODIFICATIONS:                                                 */
/*                                                                 */
/* 04/24/96  LEANNE FEDOR                                          */
/*           ADDED CUSTOMIZED PRINTER OVERRIDE COMMAND TO FORCE    */
/*           REPORT TO PRINT ON LASER PRINTER.                     */
/*                                                                 */
/* 05/21/96  LEANNE FEDOR                                          */
/*           IN PREPARATION FOR HAVING TWO BOXES (CISC AND RISC),  */
/*           REPLACED STANDARD CREATE DUPLICATE OBJECT COMMAND     */
/*           WITH A COMMAND CUSTOMIZED BY JOHN ERICSON. THIS       */
/*           COMMAND WILL RETRIEVE/USE THE FILE FROM THE APPRO-    */
/*           PRIATE LIBRARY INSTEAD OF USING HARDCODED 'PRKFLIB'.  */
/*           THE CUSTOMIZED COMMAND IS CRTDUPOBJL FOR 'CREATE      */
/*           DUPLICATE OBJECT FROM LIBRARY'.                       */
/*                                                                 */
/* 06/19/97 LEANNE FEDOR                                           */
/*          CHANGE OUT QUEUE LOGIC.                                */
/*                                                                 */
/* 05/31/07 LEANNE RAMSEY                                          */
/*          REPLACED QPRINT WITH PRINT198. REPLACED OLD CUSTOMIZED */
/*          PRINT OVERRIDE COMMAND WITH NEW STANDARD COMMAND.      */
/*******************************************************************/

             PGM

             DCL        VAR(&QDATA)  TYPE(*CHAR) LEN(240)
             DCL        VAR(&LDFSBO) TYPE(*CHAR) LEN(5)
             DCL        VAR(&LDFYMD) TYPE(*DEC)  LEN(8)
             DCL        VAR(&LDTYMD) TYPE(*DEC)  LEN(8)
             DCL        VAR(&LDPPCD) TYPE(*CHAR) LEN(5)
             DCL        VAR(&LDPTCD) TYPE(*CHAR) LEN(5)
             DCL        VAR(&LDFSCD) TYPE(*DEC) LEN(5)
             DCL        VAR(&LDGTCD) TYPE(*CHAR) LEN(1)

             DCL        VAR(&HOLD) TYPE(*CHAR) LEN(10)
             DCL        VAR(&COPYS) TYPE(*DEC) LEN(2)
             DCL        VAR(&OUTQ) TYPE(*CHAR) LEN(10)

             CHGVAR     VAR(&LDFSBO) VALUE(%SST(*LDA 1 5))
             CHGVAR     VAR(&LDFYMD) VALUE(%SST(*LDA 6 8))
             CHGVAR     VAR(&LDTYMD) VALUE(%SST(*LDA 14 8))
             CHGVAR     VAR(&LDPPCD) VALUE(%SST(*LDA 27 5))
             CHGVAR     VAR(&LDPTCD) VALUE(%SST(*LDA 22 5))
             CHGVAR     VAR(&LDFSCD) VALUE(%SST(*LDA 32 5))
             CHGVAR     VAR(&LDGTCD) VALUE(%SST(*LDA 47 1))


/*------------------------------------------------------------------*/
/* SET UP QUERY STATEMENT                                           */
/*------------------------------------------------------------------*/

/* ALWAYS SELECT ONLY CLOSED HOG GROUPS                           */

             CHGVAR     VAR(%SST(&QDATA 1 12)) VALUE('HGGSCD *EQ "')
             CHGVAR     VAR(%SST(&QDATA 13 2)) VALUE('CL')
             CHGVAR     VAR(%SST(&QDATA 15 1)) VALUE('"')

/* ALWAYS SELECT ONLY GROUPS WITH A CLOSE DATE IN THE SELECTED RANGE */

             CHGVAR     VAR(%SST(&QDATA 17 15)) VALUE('*AND HGCLDT +
                          *GE')
             CHGVAR     VAR(%SST(&QDATA 33 8)) VALUE(&LDFYMD)
             CHGVAR     VAR(%SST(&QDATA 42 15)) VALUE('*AND HGCLDT +
                          *LE')
             CHGVAR     VAR(%SST(&QDATA 58 8)) VALUE(&LDTYMD)

/*  IF BUSINESS OFFICE                                              */

             IF         COND(&LDFSBO *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 67 17)) VALUE('*AND HGFSBO +
                          *EQ "')
             CHGVAR     VAR(%SST(&QDATA 84 5)) VALUE(&LDFSBO)
             CHGVAR     VAR(%SST(&QDATA 89 1)) VALUE('"')
             ENDDO

/*  IF PRODUCTION TYPE                                         */

             IF         COND(&LDPTCD *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 91 17)) VALUE('*AND HGPTCD +
                          *EQ "')
             CHGVAR     VAR(%SST(&QDATA 108 5)) VALUE(&LDPTCD)
             CHGVAR     VAR(%SST(&QDATA 113 1)) VALUE('"')
             ENDDO

/*  IF PRODUCTION PHASE                                          */

             IF         COND(&LDPPCD *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 115 17)) VALUE('*AND HGPPCD +
                          *EQ "')
             CHGVAR     VAR(%SST(&QDATA 132 5)) VALUE(&LDPPCD)
             CHGVAR     VAR(%SST(&QDATA 137 1)) VALUE('"')
             ENDDO

/*  IF FARM SITE                                                 */

             IF         COND(&LDFSCD *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 139 4)) VALUE('*AND')
             CHGVAR     VAR(%SST(&QDATA 144 10)) VALUE('HGFSCD *EQ')
             CHGVAR     VAR(%SST(&QDATA 155 5)) VALUE(&LDFSCD)
             ENDDO

/*  ALWAYS OMIT "BGF" PRODUCTION PHASE GROUPS                    */

             CHGVAR     VAR(%SST(&QDATA 163 23)) VALUE('*AND HGPPCD +
                          *NE "BGF  "')


/*  IF GROUP TYPE                                               */

             IF         COND(&LDGTCD *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 187 17)) VALUE('*AND HGGTCD +
                          *EQ "')
             CHGVAR     VAR(%SST(&QDATA 204 1)) VALUE(&LDGTCD)
             CHGVAR     VAR(%SST(&QDATA 205 1)) VALUE('"')
             ENDDO

/* RUN QUERY OVER THE HOG GROUP FILE                                 */

             OVRDBF     FILE(HSP034) SHARE(*YES)
             OPNQRYF    FILE((HSP034)) QRYSLT(&QDATA) +
                          KEYFLD((HGPPCD) (HGFSCD) (HGCLDT) +
                          (HGHGCD)) SEQONLY(*YES)

/*------------------------------------------------------------------*/
/*  CREATE THREE WORKFILES USED FOR CLOSED GROUPS                   */
/*  AND CALL THE PROGRAM TO BUILD DATA INTO THE FILES               */
/*------------------------------------------------------------------*/

/* NOTE: THIS CLOSED GROUP RATION REPORT HAS SEVERAL OF THE SAME    */
/*       FIELDS AS OTHER CLOSED GROUP REPORTS. SO, INSTEAD OF       */
/*       DUPLICATING THE CODE FOR THESE FIELDS IN NEW WORKFILES     */
/*       WITH A NEW PROGRAM, WE SIMPLY CALL THE EXISTING WORK       */
/*       PROGRAM EVEN THOUGH THE CLOSED GROUP RATION REPORT DOES    */
/*       NOT REQUIRE ALL THE WORKFILES/FIELDS GENERATED BY THE      */
/*       EXISTING WORK PROGRAM.                                     */

             CRTDUPOBJL  OBJ(HSP387) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)

             CRTDUPOBJL  OBJ(HSP388) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)

             CRTDUPOBJL  OBJ(HSP389) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)

             CALL       PGM(HP388)

/*------------------------------------------------------------------*/
/*  CREATE THE RATION WORKFILES REQUIRED FOR THIS REPORT            */
/*------------------------------------------------------------------*/

/* NOTE: THIS STEP CREATES THE WORKFILES THAT ARE UNIQUE TO THE     */
/*       CLOSED GROUP RATION REPORT.                                */

             CRTDUPOBJL  OBJ(HSP307) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)
             CRTDUPOBJL  OBJ(HSL307A) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)

             CRTDUPOBJL  OBJ(HSP308) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)
             CRTDUPOBJL  OBJ(HSL308A) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)

             CALL       PGM(HP398)

/*------------------------------------------------------------------*/
/* SETUP THE REPORT PRINT PARAMETERS                                */
/*------------------------------------------------------------------*/

 HOLD:       IF         COND(%SST(*LDA 411 1) = 'Y') THEN(CHGVAR +
                          VAR(&HOLD) VALUE(*YES))
             ELSE       CMD(CHGVAR VAR(&HOLD) VALUE(*NO))

 COPYS:      CHGVAR     VAR(&COPYS) VALUE(%SST(*LDA 412 1))
             IF         COND(&COPYS = 0) THEN(CHGVAR VAR(&COPYS) +
                          VALUE(1))

 OUTQ:       CHGVAR     VAR(&OUTQ) VALUE(%SST(*LDA 413 10))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(%SST(*LDA 401 10)))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(*JOB))

/*------------------------------------------------------------------*/
/* OVERRIDE PRINT FILE                                              */
/*------------------------------------------------------------------*/

             OVRPRTF    FILE(PRINT198) OUTQ(&OUTQ) COPIES(&COPYS) +
                          HOLD(&HOLD)


/*------------------------------------------------------------------*/
/*  CALL PROGRAM TO PRINT REPORT                                    */
/*------------------------------------------------------------------*/

             OVRDBF     FILE(HSP387) SHARE(*YES)
             OPNQRYF    FILE((HSP387)) KEYFLD((WGPPCD) (WGPTCD) +
                          (WGFSCD) (WGHGCD)) SEQONLY(*YES)
             CALL PGM(HP698)

EXIT:        CLOF       OPNID(HSP034)
             CLOF       OPNID(HSP387)
             DLTOVR     FILE(*ALL)
             ENDPGM

