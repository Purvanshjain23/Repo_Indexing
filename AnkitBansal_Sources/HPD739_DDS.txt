     A*%%TS  SD  20111215  100719  ISLRAMS     REL-V6R1M0  5761-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION
     A*  DISPLAY FILE:  HPD739
     A*  TITLE:         VIEW TRANSFER
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  DATE:          04/10/95
     A*
     A*----------------------------------------------------------------
     A*  INDICATORS:
     A*
     A*  90  THE TRANSFER THE USER HAS SELECTED FROM THE WORK WITH HAS
     A*      BEEN DELETED BY ANOTHER USER SINCE SELECTED
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A N90                                  CF05(05 'REFRESH')
     A                                      CA12(12 'PREVIOUS')
     A                                      CF13(13 'FOLD/DROP')
     A*---------------------------------------------------------------
     A* SUBFILE FORMAT 1
     A*---------------------------------------------------------------
     A*
     A          R HP739S1                   SFL
     A*%%TS  SD  20111215  100446  ISLRAMS     REL-V6R1M0  5761-WDS
     A  85                                  SFLNXTCHG
     A            HDRRN1         4Y 0H
     A            HDLNNO    R        H      REFFLD(MDLNNO)
     A            DFORBLCD  R        O 18  2REFFLD(HSRREF/@@BLCD *LIBL/HSPREF)
     A            DFORRMCD  R        O 18  8REFFLD(HSRREF/@@RMCD *LIBL/HSPREF)
     A            DFORHGCD  R        O 18 14REFFLD(HGHGCD)
     A            DFSCHD         4Y 0O 18 25EDTCDE(Z)
     A            DFSHHD         4Y 0O 18 30EDTCDE(Z)
     A            DFDNBLCD  R        O 18 37REFFLD(HSRREF/@@BLCD *LIBL/HSPREF)
     A            DFDNRMCD  R        O 18 43REFFLD(HSRREF/@@RMCD *LIBL/HSPREF)
     A            DFDNHGCD  R     A  O 18 49REFFLD(HGHGCD)
     A            DFQLHD         4Y 0O 18 58EDTCDE(Z)
     A            DFKOAHD        4Y 0O 18 63EDTCDE(Z)
     A            DFDOAHD        4Y 0O 18 68EDTCDE(Z)
     A            DFSEXDS        7A  O 18 73
     A            DFORPTCD  R        O 19  2REFFLD(HSRREF/@@PTCD *LIBL/HSPREF)
     A            DFORPPCD  R        O 19  8REFFLD(HSRREF/@@PPCD *LIBL/HSPREF)
     A            DFORGTCD  R        O 19 14REFFLD(HSRREF/@@GTCD *LIBL/HSPREF)
     A            DFORGSCD  R        O 19 16REFFLD(HGGSCD)
     A            DFORSDCD  R        O 19 19REFFLD(HSRREF/@@SDCD *LIBL/HSPREF)
     A            DFORSMCD  R        O 19 21REFFLD(HSRREF/@@SMCD *LIBL/HSPREF)
     A                                 19 23'PTyp/Phs/GTyp/Stat/StkDen/StayMove'
     A                                      COLOR(BLU)
     A            DFDNPTCD  R        O 19 58REFFLD(HSRREF/@@PTCD *LIBL/HSPREF)
     A            DFDNPPCD  R        O 19 64REFFLD(HSRREF/@@PPCD *LIBL/HSPREF)
     A            DFDNGTCD  R        O 19 70REFFLD(HSRREF/@@GTCD *LIBL/HSPREF)
     A            DFDNGSCD  R        O 19 73REFFLD(HGGSCD)
     A            DFDNSDCD  R        O 19 76REFFLD(HSRREF/@@SDCD *LIBL/HSPREF)
     A            DFDNSMCD  R        O 19 78REFFLD(HSRREF/@@SMCD *LIBL/HSPREF)
     A*
     A*----------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT  1
     A*----------------------------------------------------------------
     A*
     A          R HP739C1                   SFLCTL(HP739S1)
     A*%%TS  SD  20111215  100719  ISLRAMS     REL-V6R1M0  5761-WDS
     A                                      SFLSIZ(0100)
     A                                      SFLPAG(0002)
     A N90N82                               ROLLUP(25 'PAGE FORWARD')
     A                                      SFLCSRRRN(&HDRR1)
     A                                      OVERLAY
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A  88                                  SFLFOLD(CF13)
     A  89                                  SFLDROP(CF13)
     A                                      SFLMODE(&FOLD)
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            HDRR1          5S 0H
     A            FOLD           1A  H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 26'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  2 30'VIEW TRANSFER'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  4  2'Movement number .....:'
     A            C1MVSN    R        O  4 27REFFLD(@@MVSN)
     A                                      EDTCDE(Z)
     A                                  4 41'Status ................'
     A            C1MSCD    R     A  O  4 67REFFLD(MHMSCD)
     A                                  5  2'Scheduled ship date ..'
     A            C1MDY     R     S  O  5 27REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  ')
     A                                      TEXT('received date')
     A                                  5 41'Trucker ...............'
     A            C1TICD    R        O  5 67REFFLD(MHTICD)
     A                                  6  2'Shipped date .........'
     A            C1SMDY    R     S  O  6 27REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  ')
     A                                      TEXT('received date')
     A                                  6 41'Shipped time in/out ...'
     A            C1SITM    R        O  6 67REFFLD(MHSITM)
     A                                      EDTCDE(Z)
     A            C1SOTM    R        O  6 75REFFLD(MHSOTM)
     A                                      EDTCDE(Z)
     A                                  7  2'Received date ........'
     A            C1RMDY    R     S  O  7 27REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  ')
     A                                      TEXT('received date')
     A                                  7 41'Received time in/out ..'
     A            C1RITM    R        O  7 67REFFLD(MHRITM)
     A                                      EDTCDE(Z)
     A            C1ROTM    R        O  7 75REFFLD(MHROTM)
     A                                      EDTCDE(Z)
     A                                  8  2'Origin farm ..........'
     A            C1ORFS    R        O  8 27REFFLD(FSFSCD)
     A                                      EDTCDE(K)
     A                                  8 41'Destination farm ......'
     A            C1DNFS    R        O  8 67REFFLD(FSFSCD)
     A                                      EDTCDE(K)
     A            C1ORNM    R        O  9  3REFFLD(FSFSNM)
     A            C1DNNM    R        O  9 49REFFLD(FSFSNM)
     A                                 11  2'Scheduled'
     A                                      COLOR(BLU)
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 11 13'   Shipped   '
     A                                      COLOR(BLU)
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 11 29'     Arrived      '
     A                                      COLOR(BLU)
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 11 49'   KOA/DOA    '
     A                                      COLOR(BLU)
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 11 65'   Received    '
     A                                      COLOR(BLU)
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 12  4'Head'
     A                                 12 14'Head'
     A                                 12 20'Pounds'
     A                                 12 30'Head'
     A                                 12 36'Pounds'
     A                                 12 44'Avg'
     A                                 12 51'Head'
     A                                 12 57'Pounds'
     A                                 12 68'Head'
     A                                 12 74'Pounds'
     A            C1SCHD    R        O 13  3REFFLD(MDSCHD)
     A                                      EDTCDE(M)
     A            C1SHHD    R        O 13 13REFFLD(MDSHLB)
     A                                      EDTCDE(M)
     A            C1SHLB    R        O 13 20REFFLD(MDSHLB)
     A                                      EDTCDE(K)
     A            C1ARHD    R        O 13 29REFFLD(MDSHHD)
     A                                      EDTCDE(M)
     A            C1ARLB    R        O 13 36REFFLD(MDSHLB)
     A                                      EDTCDE(K)
     A            C1AVARLB  R        O 13 44REFFLD(HSRREF/@@NU30 *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A            C1DOHD    R        O 13 50REFFLD(MDDOHD)
     A                                      EDTCDE(M)
     A            C1DOLB    R        O 13 57REFFLD(MDDOLB)
     A                                      EDTCDE(K)
     A            C1RCHD    R        O 13 67REFFLD(MDSHHD)
     A                                      EDTCDE(M)
     A            C1RCLB    R        O 13 74REFFLD(MDSHLB)
     A                                      EDTCDE(K)
     A                                 14  2'---------- ORIGIN -----------------
     A                                       ----------------- DESTINATION -----
     A                                      ---------'
     A                                      DSPATR(HI)
     A                                 15 24'Sched'
     A                                      DSPATR(HI)
     A                                 15 30'Ship'
     A                                      DSPATR(HI)
     A                                 15 58'Qual'
     A                                      DSPATR(HI)
     A                                 15 64'KOA'
     A                                      DSPATR(HI)
     A                                 15 69'DOA'
     A                                      DSPATR(HI)
     A                                 16  2'Bldg'
     A                                      DSPATR(HI)
     A                                 16  8'Room'
     A                                      DSPATR(HI)
     A                                 16 14'Group'
     A                                      DSPATR(HI)
     A                                 16 25'Head'
     A                                      DSPATR(HI)
     A                                 16 30'Head'
     A                                      DSPATR(HI)
     A                                 16 37'Bldg'
     A                                      DSPATR(HI)
     A                                 16 43'Room'
     A                                      DSPATR(HI)
     A                                 16 49'Group'
     A                                      DSPATR(HI)
     A                                 16 58'Head'
     A                                      DSPATR(HI)
     A                                 16 63'Head'
     A                                      DSPATR(HI)
     A                                 16 68'Head'
     A                                      DSPATR(HI)
     A                                 16 73'Sex'
     A                                      DSPATR(HI)
     A*----------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*----------------------------------------------------------------
     A          R HP739K1
     A*%%TS  SD  20021202  124828  ISLRAMS     REL-V5R1M0  5722-WDS
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A N90                             23 11'F5=Refresh'
     A                                 23 23'F12=Previous'
     A                                 23 37'F13=Fold/Unfold'
      *----------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *----------------------------------------------------------------
     A          R HP739ES                   SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
     A*----------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*----------------------------------------------------------------
     A*
     A          R HP739EC                   SFLCTL(HP739ES)
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
