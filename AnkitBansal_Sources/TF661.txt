      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Triumph Foods
      * PROGRAM:     TF661 - AR Adjustment Report by G/L Post Account
      * PROGRAMMER:  LeAnne Ramsey
      * CREATED:     10/07/09
      *
      * Function:    This program prints data from the AR Adjustment GL Post file.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 02/11/11  LeAnne Ramsey  (E1332)
      *           Added the new fields in TFP025 to the layout.
      /eject
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Ftfp002    if   e           k disk
      *  G/L Post methods
      *
      *
     Ftfp025    if   e           k disk
      *  AR Adjustment G/L Post (records selected with open query)
      *
      *
     Fprint198  o    f  198        printer oflind(*inof)
      *
      /eject
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      ****************************************************************
      * ARRAYS AND TABLES
      ****************************************************************
      *
     D arpostam        s             11  2 dim(2)
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Standard fields
      *
     D dash            s            198    inz(*all'-')
     D dash42          s             42    inz(*all'-')
     d rtime           s              6  0
      *
      *
      * Control fields
      *
     D first           s              1    inz('Y')
     D first960        s              1    inz('Y')
     D count           s              3  0
     D svpcono         s                   like(appcono)
     D svpcc           s                   like(appcc)
     D svpobj          s                   like(appobj)
     D svpsub          s                   like(appsub)
     D svpmcd          s                   like(appmcd)
     D svpmds          s                   like(pmpmds)
     D svpaid          s                   like(appaid)
     D svpaidds        s                   like(appaidds)
     D svglcode        s                   like(apglcode)
      *
      *
      * Workfields for date manipulation
      *
     D wkisodate       s               d   datfmt(*iso)
      *
      *
      * Parm fields
      *
     d xxdsfl          s              1
      *
      *
      * Print fields
      *
     d l1mdy           s              6  0
     d l1pmds          s                   like(pmpmds)
      *
      *
      * Array index
      *
     D x               s              1  0
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *---------------------------------------------------------------
      * Local data area
      *---------------------------------------------------------------
      *
     d lda            uds                  dtaara(*lda)
     D  ldpfcd                37     37
      *
     D  ldfwemdy              39     44  0
     D  ldtwemdy              45     50  0
     D  ldfwecymd             52     59  0
     D  ldtwecymd             60     67  0
      *
     D  ldpcono               71     73  0
     D  ldpcc                 77     88
     D  ldpobj                89     94
     D  ldpsub                95    102
      *
     D  lddsfl               140    140
      *
     D  ldpmcd               141    141
     D  ldpmds               142    171
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      *************************************************************************************
      * Mainline
      *************************************************************************************
      *
      * Print headings
     C                   exsr      $h1hdr
      *
      * The users may elect to print the Detail Report or the Summary Report or Both.
      * We control this from the CL and send in the parm of D=Detail or S=Summary.
      * (Note: Records were selected with an OPNQRY)
      *
     C     *start        setll     tfp025
      *
     C                   dou       *in90 = *on                                  Do main
     C                   read      tfp025                                 90
     C                   if        *in90 = *off                                 If not EOF
      * Control break
     C                   select
     C                   when      first = yes
     C                   move      no            first
     C                   exsr      $save
      *
     C                   when      appcono <> svpcono or
     C                             appcc   <> svpcc   or
     C                             appobj  <> svpobj  or
     C                             appsub  <> svpsub  or
     C                             appmcd  <> svpmcd
     C                   z-add     1             x
     C                   exsr      $t1tot
     C                   endsl
      *
      * Accumulate Post Amount into total array
      *
     C                   add       appostam      arpostam
      *
      * Print the Detail Line if this is the "Detail" version
      *
     C   87              exsr      $l1dtl
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Do main
      *
      *------------------
      * End of Processing
      *------------------
     C                   if        first = no
     C                   z-add     1             x
     C                   exsr      $t1tot
     C                   else
     C                   except    nodata
     C                   endif
      *
     C                   seton                                        lr
      /EJECT
      *---------------------------------------------------------------
      * Print the Detail line
      *---------------------------------------------------------------
      *
     C     $l1dtl        begsr
      *
      * Flip Revenue Week-Ending Date
      *
     C     *iso          test(d)                 aprwedt                92
     C                   if        *in92 = *off                                 If OK date
     C                   move      aprwedt       wkisodate
     C     *mdy          move      wkisodate     l1mdy
     C                   else
     C                   z-add     0             l1mdy
     C                   endif                                                  If OK date
      *
      *
      * Retrieve GL Post Method Description
      *
     C     appmcd        chain     tfp002                             92
     C                   if        *in92 = *off
     C                   move      pmpmds        l1pmds
     C                   endif
      *
      * Print detail
     C                   if        *inof = *on
     C                   exsr      $h1hdr
     C                   endif
      *
     C                   except    l1dtl
      *
     C                   add       1             count
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Save Control Fields
      *---------------------------------------------------------------
      *
     C     $save         begsr
      *
     C                   z-add     appcono       svpcono
     C                   move      appcc         svpcc
     C                   move      appobj        svpobj
     C                   move      appsub        svpsub
     C                   move      appmcd        svpmcd
     C                   move      appaid        svpaid
     C                   move      appaidds      svpaidds
     C                   move      apglcode      svglcode
      *
      * Retrieve GL Post Method Description
      *
     C     appmcd        chain     tfp002                             92
     C                   if        *in92 = *off
     C                   move      pmpmds        svpmds
     C                   else
     C                   move      *blank        svpmds
     C                   endif
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Print report heading
      *---------------------------------------------------------------
      *
     C     $h1hdr        begsr
      *
     C                   except    h1hdr
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Print Total line
      *---------------------------------------------------------------
      *
     C     $t1tot        begsr
      *
     C                   select
     C                   when      count > 1 or *in87 = *off
     C                   if        *inof = *on
     C                   exsr      $h1hdr
     C                   endif
     C                   except    t1tot
     C                   other
      *
     C                   except    blank
     C                   endsl
      *
      * Clear Count and Array entry
      *
     C                   z-add     0             count
     C                   z-add     0             arpostam(x)
      *
      * Save control fields
      *
     C                   exsr      $save
      *
      * If 1) this is the Summary version and
      *    2) the next thing that you will print is for Posting Company 960
      * Alice wants a "dashed" line printed across the page
      *
     C                   if        svpcono = 960 and first960 = yes
     C                   move      no            first960
     C                   except    dash2
     C                   endif
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Initialization
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Parm lists
      *
     C     *entry        plist
     C                   parm                    xxdsfl
      *
     C                   time                    rtime
      *
      * We will control our Detail vs Summary Report logic using indicator 87
      *
     C                   if        xxdsfl = 'D'
     C                   seton                                        87
     C                   endif
      *
      * Set up heading selections:
      *
      * Post company
     C                   if        ldpcono = 0
     C                   seton                                        80
     C                   endif
      * Post cost center
     C                   if        ldpcc = *blank
     C                   seton                                        81
     C                   endif
      * Post object
     C                   if        ldpobj = *blank
     C                   seton                                        82
     C                   endif
      * Post subsidiary
     C                   if        ldpsub = *blank
     C                   seton                                        83
     C                   endif
      * Post method
     C                   if        ldpmcd = *blank
     C                   seton                                        84
     C                   endif
      *
     C                   endsr
      /eject
      *-------------------------------------------------------------
      * Report heading lines
      *-------------------------------------------------------------
      *
     Oprint198  e            h1hdr          1 01
     O                       sdpgm               10
     O                                          100 'AR Adjustment Report by'
     O                                          117 ' G/L Post Account'
     O                                          188 'DATE'
     O                       udate         y    198
      *
     O          e            h1hdr          1
     O                       sdusr               10
     O               87                         102 'Detail'
     O              n87                         102 'Summary'
     O                                          188 'TIME'
     O                       rtime              198 '  :  :  '
      *
     O          e            h1hdr          1
     O                                          188 'PAGE'
     O                       page          z    198
      *
      *
     O          e            h1hdr          1
     O                                           21 'From WeekEnding date:'
     O                       ldfwemdy            31 '  /  /  '
      *
     O                                           55 'Post cost center:'
     O               81                          60 'All'
     O              n81      ldpcc               69
      *
     O                                           92 'G/L Post method:'
     O               84                          97 'All'
     O              n84      ldpmds             124
      *
     O          e            h1hdr
     O                                           21 'To WeekEnding date:'
     O                       ldtwemdy            31 '  /  /  '
      *
     O                                           55 'Post object:'
     O               82                          60 'All'
     O              n82      ldpobj              63
      *
     O          e            h1hdr          2
     O                                           21 'Post company:'
     O               80                          26 'All'
     O              n80      ldpcono       z     27
      *
     O                                           55 'Post subsidiary:'
     O               83                          60 'All'
     O              n83      ldpsub              65
      *
      *-------------------------------------------------------------
      * Column headings
      *-------------------------------------------------------------
      *
     O          e            h1hdr       1  1
     O                                          118 'Week'
     O                                          160 'G/L'
     O          e            h1hdr          1
     O                       dash42              42
     O                                           67 ' P O S T     V A L U E S '
     O                       dash42             109
      *
     O                                          119 'Ending'
     O                                          131 '--------- '
     O                                          139 'Original'
     O                                          150 ' ----------'
     O                                          160 'Batch'
     O                                          172 '--------- '
     O                                          187 'G/L Post Method'
     O                                          198 ' ----------'
      *
     O          e            h1hdr          1
     O                                            4 'Comp'
     O                                           18 'Cost Center'
     O                                           26 'Object'
     O                                           31 'Sub'
     O                                           48 'Account ID'
     O                                           61 'Description'
     O                                           89 'GL Code'
     O                                          108 'Amount'
     O                                          118 'Date'
     O                                          128 'Company'
     O                                          142 'Cost Center'
     O                                          150 'Object'
     O                                          160 'Number'
     O                                          166 'Code'
     O                                          179 'Description'
      *
     O          e            h1hdr          1
     O                       dash               198
      *
      *-------------------------------------------------------------
      * Detail line one
      *-------------------------------------------------------------
      *
     O          e            l1dtl       1  0
      *
     O                       appcono       z      3
     O                       appcc               18
     O                       appobj              26
     O                       appsub              36
     O                       appaid              47
     O                       appaidds            80
     O                       apglcode            90
     O                       appostam      k    109
      *
     O                       l1mdy              119 '  /  /  '
     O                       apocono       z    126
     O                       apocc              142
     O                       apoobj             150
     O                       apglbatno     m    161
     O                       appmcd             164
     O                       l1pmds         b   198
      *
      *-------------------------------------------------------------
      * Total line
      *-------------------------------------------------------------
      *
      *
     O          e    87      t1tot       1  1
     O                                          108 '--------------'
      *
     O          e   n87      t1tot       1
      *
     O          e            t1tot          1
     O              n87      svpcono       z      3
     O              n87      svpcc               18
     O              n87      svpobj              26
     O              n87      svpsub              36
     O              n87      svpaid              47
     O              n87      svpaidds            80
     O              n87      svglcode            90
     O                       arpostam(x)   k    109
     O              n87      svpmcd             164
     O              n87      svpmds             198
      /eject
      *-------------------------------------------------------------
      * Blank
      *-------------------------------------------------------------
      *
     O          e            dash2       1  1
     O                       dash               198
      /eject
      *-------------------------------------------------------------
      * Blank
      *-------------------------------------------------------------
      *
     O          e            blank       1
      /eject
      *-------------------------------------------------------------
      * No data message line
      *-------------------------------------------------------------
      *
     O          e            nodata      1
     O                                           19 'No data meets your'
     O                                           39 'selection criteria.'
      /eject
