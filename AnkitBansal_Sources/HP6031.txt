      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Hog Production System
      * PROGRAM:     HP6031
      * TITLE:       Reported Daily Activity--No Data Report
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     11/06/02
      *
      * FUNCTION:  Batch program to print farms that have open groups but no records in
      *            the Reported Daily Activity file for a specified date.
      *
      *            This listing is submitted from HP4030-Work with Reported Daily
      *            Activity.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 08/24/05  LeAnne Fedor
      *           Recompile only. New field "Man Hours" added to:
      *              Reported Daily Activity file
      *
      * 05/15/09  LeAnne Ramsey
      *           Recompile only. Per Sami Wilson, we have removed Projected Destroyed Head
      *           from HSP103-Projected Daily Activity.
      *
      * 07/06/09  LeAnne Ramsey
      *           Recompile only. Added new field 'Continuous Flow Flag' to Hog Group file.
      *
      * 10/15/13  LeAnne Ramsey (E2831)
      *           Recompile only. Added field 'MTech Reference'.
      *
      * 10/16/13  LeAnne Ramsey (E2831)
      *           Recompile only. Added field 'MTech Reference'.
      *
      * 01/21/21  ISE            TSN593
      *           Replaced *loval with *start
      *
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fhsp011    if   e           k disk
      *    Production phase
      *
      *
     Fhsp018    if   e           k disk
      *    Farm site (records selected/keyed in open query)
      *
      *
     Fhsp016    if   e           k disk
      *    Manager
      *
      *
     Fhsl034e   if   e           k disk
      *    Hog group (records selected in open query)
      *
      *
     Fhsp103    if   e           k disk
      *    Reported daily activity
      *
      *
     Fqprint    o    f  132        printer oflind(*inof)
      *
      /EJECT
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Standard fields
      *
     D prtfl           s              1    inz('N')
     D dash            s            132    inz(*all'-')
     d rtime           s              6  0
      *
      *
      * Control fields
      *
     D first           s              1    inz('Y')
     D svcell          s                   like(fscell)
     D svppcd          s                   like(fsppcd)
      *
      *
      * Print fields
      *
     D h1ppds          s                   like(ppppds)
      *
     D l1pmgnm         s                   like(mgmgnm)
     D l1cmgnm         s                   like(mgmgnm)
     D l1mmgnm         s                   like(mgmgnm)
     D l1fsnm          s             15
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *---------------------------------------------------------------
      *  LDA
      *---------------------------------------------------------------
      *
     Dlda             uds                  dtaara(*lda)
     D  ldcell                64     65  0
     D  ldcymd                66     73  0
     D  ldmdy                 74     79  0
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * MAINLINE
      ****************************************************************
      *
      * Prepare to read the file and print headings the first time through.
      *
593  C*    *loval        setll     hsp018
593  C     *start        setll     hsp018
      *
      * Read each Farm selected by the open query.
      *
     C                   dou       *in90 = *on                                  Main do
     C                   read      hsp018                                 90
     C                   if        *in90 = *off                                 If not EOF
      *
      * Does the Farm have a Reported Daily Activity record for the
      * specified date? If it does, you're done with this farm; if not,
      * continue.
      *
     C     key01         chain     hsp103                             92
     C                   if        *in92 = *on                                  If no activity
      *
      * There is no Reported Activity Record for the farm. Should there be?
      * Check to see if there are any open groups on the farm on the
      * specified date. If there are NO open groups, you're done with this
      * farm. If there are Open groups, retrieve manager names and
      * print the farm on the report.
      *
     C     fsfscd        chain     hsl034e                            92
     C                   if        *in92 = *off                                 If open groups
     C                   exsr      $detail
     C                   endif                                                  If open groups
      *
     C                   endif                                                  If no activity
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Main do
      *
      *---------------------------------------------------------------
      * EOF processing
      *---------------------------------------------------------------
      * If the file was empty, print the standard 'no data...' message
      *
     C                   if        prtfl = no
     C                   exsr      $h1hdr
     C                   except    nodata
     C                   endif
      *
     C                   seton                                        lr
      /EJECT
      *---------------------------------------------------------------
      * Detail processing
      *---------------------------------------------------------------
      *
     C     $detail       begsr
      *
      * Check for control breaks
      *
     C                   select
     C                   when      first = yes
     C                   move      no            first
     C                   exsr      $ppds
     C                   exsr      $h1hdr
      *
     C                   when      svppcd <> fsppcd
     C                   exsr      $ppds
     C                   exsr      $h1hdr
      *
     C                   when      svcell <> fscell
     C                   except    blank
     C                   z-add     fscell        svcell
     C                   endsl
      *
      * Retrieve 'manager' names for the detail line
      *
     C                   exsr      $mgnm
      *
      * Print the detail line
      *
     C                   exsr      $l1dtl
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Print detail line
      *---------------------------------------------------------------
      *
     C     $l1dtl        begsr
      *
     C                   if        *inof = *on
     C                   except    h1hdr
     C                   endif
      *
     C                   movel     fsfsnm        l1fsnm
      *
     C                   except    l1dtl
     C                   movel     yes           prtfl
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Print names of the 3 managers for the farm
      *---------------------------------------------------------------
      *
     C     $mgnm         begsr
      *
      * Production manager
      *
     C     fspmgcd       chain     hsp016                             92
     C                   if        *in92 = *off
     C                   eval      l1pmgnm = mgmgnm
     C                   endif
      *
      * Cell manager
      *
     C     fscmgcd       chain     hsp016                             92
     C                   if        *in92 = *off
     C                   eval      l1cmgnm = mgmgnm
     C                   endif
      *
      * Multisite manager
      *
     C     fsmmgcd       chain     hsp016                             92
     C                   if        *in92 = *off
     C                   eval      l1mmgnm = mgmgnm
     C                   endif
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Retrieve Production Phase description
      *---------------------------------------------------------------
      *
     C     $ppds         begsr
      *
     C     fsppcd        chain     hsp011                             92
     C                   if        *in92 = *off
     C                   move      ppppds        h1ppds
     C                   else
     C                   eval      h1ppds = 'Unknown'
     C                   endif
      *
     C                   move      fsppcd        svppcd
     C                   z-add     fscell        svcell
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Print headings
      *---------------------------------------------------------------
      *
     C     $h1hdr        begsr
      *
     C                   except    h1hdr
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Key lists
      *
     C     key01         klist
     C                   kfld                    fsfscd
     C                   kfld                    ldcymd
      *
      *
      * Set up report heading fields.
      *
      * All cells
      *
     C                   if        ldcell = 0
     C                   seton                                        88
     C                   endif
      *
      * Retrieve time for report heading.
      *
     C                   time                    rtime
      *
     C                   endsr
      /EJECT
      *-------------------------------------------------------------
      * Report heading lines
      *-------------------------------------------------------------
      *
     Oqprint    e            h1hdr          1 01
     O                       sdpgm               10
     O                                           67 'HOG PRODUCTION SYSTEM'
     O                                          122 'DATE'
     O                       udate         y    132
      *
     O          e            h1hdr          1
     O                       sdusr               10
     O                                           62 'REPORTED DAILY ACTIVITY--'
     O                                           76 'NO DATA REPORT'
     O                                          122 'TIME'
     O                       rtime              132 '  :  :  '
      *
     O          e            h1hdr          2
     O                                          122 'PAGE'
     O                       page          z    132
      *
      *
      *
     O          E            h1hdr          1
     O                                           14 'Daily date ..:'
     O                       ldmdy         y     24
      *
      *
      *
     O          e            h1hdr          1
     O                                           14 'Cell ........:'
     O              n88      ldcell        z     18
     O               88                          19 'All'
      *
      *
      *
     O          e            h1hdr          1
     O                                           11 'FARM'
     O                                           44 'CELL MANAGER'
     O                                           84 'PRODUCTION MANAGER'
     O                                          117 'MULTISITE MANAGER'
      *
      *
     O          e            h1hdr          1
     O                                            4 'CELL'
     O                                           11 'SITE'
     O                                           23 'FARM NAME'
     O                                           36 'CODE'
     O                                           43 'NAME'
     O                                           70 'CODE'
     O                                           77 'NAME'
     O                                          104 'CODE'
     O                                          111 'NAME'
      *
     O          e            h1hdr          1
     O                       dash               132
      *
      *-------------------------------------------------------------
      * Phase heading
      *-------------------------------------------------------------
      *
     O          e            h1hdr       0  1
     O                                           17 'Production Phase:'
     O                       h1ppds              48
      *
      *-------------------------------------------------------------
      * Detail line
      *-------------------------------------------------------------
      *
     O          e            l1dtl       1
     O                       fscell        z      2
     O                       fsfscd        z     11
     O                       l1fsnm         b    29
     O                       fscmgcd             37
     O                       l1cmgnm        b    64
     O                       fspmgcd             71
     O                       l1pmgnm        b    98
     O                       fsmmgcd            105
     O                       l1mmgnm        b   132
      *
      *
      *-------------------------------------------------------------
      * Blank line
      *-------------------------------------------------------------
      *
     O          e            blank       1
      *
      *-------------------------------------------------------------
      * No data message line
      *-------------------------------------------------------------
      *
     O          e            nodata      1
     O                                           19 'NO DATA MEETS YOUR'
     O                                           38 'SELECTION CRITERIA'
