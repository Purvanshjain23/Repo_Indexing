     A*%%TS  SD  20040713  162208  ISLRAMS     REL-V5R2M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD4095
     A*  TITLE:         SPECIFY BOLS FOR RESET OF PRINT COUNT
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  DATE:          05/10/01
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A N99                                  CF04(04 'PROMPT')
     A N99 44                               CF08(08 'SUBMIT')
     A N99                                  CF20(20 'WRKSPLF')
     A*
     A*---------------------------------------------------------------
     A* RECORD FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP4095R1
     A*%%TS  SD  20040713  162208  ISLRAMS     REL-V5R2M0  5722-WDS
     A                                      SETOF(30)
     A                                      SETOF(31)
     A                                      SETOF(32)
     A                                      SETOF(33)
     A                                      SETOF(34)
     A                                      SETOF(35)
     A                                      SETOF(36)
     A                                      SETOF(41)
     A                                      SETOF(45)
     A                                      SETOF(51)
     A                                      SETOF(54)
     A                                      SETOF(55)
     A                                      SETOF(56)
     A                                      SETOF(61)
     A                                      SETOF(65)
     A                                      OVERLAY
     A                                      RTNCSRLOC(&RCD &FLD)
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 27'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  2 13'SPECIFY BILL-OF-LADINGS FOR RESET -
     A                                      OF PRINT COUNT'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  4  2'From scheduled ship date ..'
     A            DFFMDY    R        B  4 30REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A N32                                  DSPATR(HI)
     A  32                                  DSPATR(PC)
     A  32                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A                                  5  2'To scheduled ship date ....'
     A            DFTMDY    R        B  5 30REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A N33                                  DSPATR(HI)
     A  33                                  DSPATR(PC)
     A  33                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A                                  7  2'Business office .....'
     A                                  7 24'?'
     A            DFFSBO    R        B  7 26REFFLD(BOBOBO)
     A N31                                  DSPATR(HI)
     A  31                                  DSPATR(RI)
     A  31
     AO 51                                  DSPATR(PC)
     A  99                                  DSPATR(PR)
     A            DFBODS    R        O  7 34REFFLD(BOBODS)
     A                                  7 66'(Blank for all)'
     A                                  8  2'Production type .....'
     A                                  8 24'?'
     A            DFPTCD    R        B  8 26REFFLD(PTPTCD)
     A N34                                  DSPATR(HI)
     A  34                                  DSPATR(RI)
     A  34
     AO 54                                  DSPATR(PC)
     A  99                                  DSPATR(PR)
     A            DFPTDS    R        O  8 34REFFLD(PTPTDS)
     A                                  8 66'(Blank for all)'
     A                                  9  2'Production phase ....'
     A                                  9 24'?'
     A            DFPPCD    R        B  9 26REFFLD(PPPPCD)
     A N35                                  DSPATR(HI)
     A  35                                  DSPATR(RI)
     A  35
     AO 55                                  DSPATR(PC)
     A  99                                  DSPATR(PR)
     A            DFPPDS    R        O  9 34REFFLD(PPPPDS)
     A                                  9 66'(Blank for all)'
     A                                 10  2'Cell ................'
     A            DFCELL    R        B 10 26REFFLD(HSRREF/@@CELL *LIBL/HSPREF)
     A                                      EDTCDE(M)
     A N30                                  DSPATR(HI)
     A  30                                  DSPATR(RI)
     A  30                                  DSPATR(PC)
     A  99                                  DSPATR(PR)
     A                                 10 66'(Blank for all)'
     A                                 11  2'Farm site ...........'
     A                                 11 24'?'
     A            DFFSCD    R        B 11 26REFFLD(FSFSCD)
     A                                      EDTCDE(M)
     A N36                                  DSPATR(HI)
     A  36                                  DSPATR(RI)
     A  36
     AO 56                                  DSPATR(PC)
     A  99                                  DSPATR(PR)
     A            DFFSNM    R        O 11 34REFFLD(FSFSNM)
     A                                 11 66'(Blank for all)'
     A                                 13  2'Include in the reset:'
     A                                 14  4'Transfer movements ..'
     A                                 14 26'?'
     A            DFTRAN    R        B 14 28REFFLD(@@FLAG)
     A N41                                  DSPATR(HI)
     A  41                                  DSPATR(RI)
     A  41
     AO 61                                  DSPATR(PC)
     A  99                                  DSPATR(PR)
     A                                 15  4'Sales movements .....'
     A                                 15 26'?'
     A            DFSALE    R        B 15 28REFFLD(@@FLAG)
     A N45                                  DSPATR(HI)
     A  45                                  DSPATR(RI)
     A  45
     AO 65                                  DSPATR(PC)
     A  99                                  DSPATR(PR)
     A*
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R HP4095K1
     A*%%TS  SD  20010510  155035  ISLRAMS     REL-V4R4M0  5769-PW1
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A N99                             23 11'F4=Prompt'
     A  44N99                          23 22'F8=Submit'
     A N99                             23 52'F20=Spool Files'
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *---------------------------------------------------------------
     A          R HP4095ES                  SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
     A*                                                                *********
     A*---------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*---------------------------------------------------------------
     A*
     A          R HP4095EC                  SFLCTL(HP4095ES)
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
