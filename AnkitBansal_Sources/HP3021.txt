      *
      * ENVIRONMENT: Pork Division
      * SYSTEM:      HPS---Datamart
      * PROGRAM:     HP3021
      * TITLE:       Datamart FIN: Build Workfile of Weeks in Date Range
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     11/01/04
      *
      * FUNCTION:  Batch program to build workfile of weeks in the date range being
      *            processed.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fhsp3021   o    e           k disk
      *  Datamart FIN: Workfile of weeks in date range
      *
      /EJECT
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Control fields
      *
     D procfl          s              1    inz('Y')
     D first           s              1    inz('Y')
      *
      *
      * Parms
      *
     D xxcurhist       s              1a
     D xxccyymmdd      s              8  0
     D xxcdyr          s              4  0
     D xxcdwk          s              2  0
      *
      *
      * Work fields for date manipulation
      *
     D wkisodate       s               D   datfmt(*iso)
     D wkfcymd         s              8  0
     D wktcymd         s              8  0
     D wkccyymmdd      s              8  0
      *
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *---------------------------------------------------------------
      * Definition for external data area
      *---------------------------------------------------------------
     D
     Ddafin            ds                  dtaara(dafin)
     D  dahwkenddt            15     22  0
     D  dacwkenddt            38     45  0
     D  dadays                47     49  0
     D  dasbmfl               62     62
      *
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * MAINLINE
      ****************************************************************
      *
      * Perform a loop from the FROM date thru the TO date writing a record
      * for each week.
      *
     C                   dou       procfl = no                                  Main do
      * First time
     C                   select                                                 Select 1
     C                   when      first = yes
     C                   move      no            first
     C     *iso          move      wkfcymd       wkisodate
     C                   move      wkisodate     wkccyymmdd
     C                   exsr      $wrt3021
     C                   other
      *
      * Increment last date processed by 7 days.
      *
     C                   adddur    7:*days       wkisodate
     C                   move      wkisodate     wkccyymmdd
      *
      * Continue processing with new date.
      *
     C                   select                                                 Select 2
     C                   when      wkccyymmdd = wktcymd
     C                   exsr      $wrt3021
     C                   move      no            procfl
      *
     C                   when      wkccyymmdd > wktcymd
     C                   move      no            procfl
     C                   other
      *
     C                   exsr      $wrt3021
     C                   endsl                                                  Select 2
      *
     C                   endsl                                                  Select 1
     C                   enddo                                                  Main do
      *
      * EOF processing
     C                   seton                                        lr
      /EJECT
      *--------------------------------------------------------------------------
      * Write workfile record
      *--------------------------------------------------------------------------
      *
     C     $wrt3021      begsr
      *
      * Call a program that will return the calendar year/week for this
      * date.
     C                   call      'HP8010'
     C     w1wedt        parm      wkccyymmdd    xxccyymmdd
     C     w1cdyr        parm      0             xxcdyr
     C     w1cdwk        parm      0             xxcdwk
      *
     C                   write     w1rec
     C                   clear                   w1rec
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Find the Saturday after the system date
      *---------------------------------------------------------------
      *
     C     $nextsat      begsr
      *
     C                   move      *date         wkisodate
     C                   move      wkisodate     xxccyymmdd
      *
     C                   call      'HP8007'
     C                   parm                    xxccyymmdd
     C                   parm      0             wktcymd
     C                   parm      0             xxcdyr
     C                   parm      0             xxcdwk
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Parm lists
      *
     C     *entry        plist
     C                   parm                    xxcurhist
      *
      *
      * Bring in the data area that contains the Week-Ending Date for the first week.
      * Set the 'FROM' date based on whether this run a History or Current run.
      *
     C                   in        dafin
      * History
     C                   select
     C                   when      xxcurhist = 'H'
     C                   z-add     dahwkenddt    wkfcymd
      * Current
     C                   when      xxcurhist = ' '
     C                   z-add     dacwkenddt    wkfcymd
     C                   endsl
      *
      * Find the Saturday AFTER the system date; you will continue processing thru this
      * date.
     C                   exsr      $nextsat
      *
     C                   endsr
      /EJECT
