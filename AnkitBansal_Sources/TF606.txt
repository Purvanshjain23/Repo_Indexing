      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Triumph Foods
      * PROGRAM:     TF606
      * TITLE:       Cash: Cash Distribution Balance Report
      *              Sales Expenses
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     10/05/05
      *
      * FUNCTION:  Batch program to print the Cash Distribution Balance records with
      *            their associated Cash Distribution Detail records.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 11/06/06  LeAnne Ramsey
      *           Recompile only. New fields were added to TFP011-Cash Distribution Balance.
      *
      * 04/24/08  LeAnne Ramsey
      *           Recompile only. Added 2 fields to Cash Distribution Detail:
      *             1) system generated flag (yes/no)
      *             2) adjustment comment
      *
      * 04/13/09  LeAnne Ramsey
      *           Added a "stop date" selection (optional). If the user enters a Stop Date,
      *           the report will not print any data beyond this date.
      *
      * 05/20/09  LeAnne Ramsey
      *           Added Totals (Exempt, Non-Exempt, Report Total lines only) for 4 fields:
      *               TF Total Sales Expense Balance
      *               TF Absorbed Freight Balance
      *               TF Other Sales Expense Balance
      *               TF Broker Commission Balance
      *
      * 09/10/09  Alice Brownfield
      *           E00493-recompile only.....fields changed in TFP011-Cash Distribution Balance.
      *                  change to allocate AR Adjustments at gross vs net; i.e. give some
      *                  to Sales Expense then the NET to TF & SBF.
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Ftfl011d   if   e           k disk
      *    Cash distribution balance (records selected with open query)
      *
      *
     Ftfl013b   if   e           k disk
      *    Cash distribution detail (records selected with open query)
      *
      *
     Fprint198  o    f  198        printer oflind(*inof)
      *
      /EJECT
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Standard fields
      *
     D dash            s            198    inz(*all'-')
     d rtime           s              6  0
      *
      *
      * Control fields
      *
     D first           s              1    inz('Y')
     D haddata         s              1    inz('N')
     D count           s              5  0
      *
      *
      *
      * Workfields
      *
     D wkrcexfl        s              1
      *
      *
      * Workfields for Date Manipulation
      *
     D wkisodate       s               d   datfmt(*iso)
      *
      *
      * Print fields
      *
     d l1rwemdy        s              6  0
     d l1ssxpc         s                   like(cbtsxpc)
      *
     d l2cdmdy         s              6  0
     d l2ssxcdam       s                   like(cdasxcdam)
     d l2tsxbalam      s                   like(cdasxcdam)
     d l2tafbalam      s                   like(cdasxcdam)
     d l2toxbalam      s                   like(cdasxcdam)
     d l2tbcbalam      s                   like(cdasxcdam)
      *
     d t1desc          s             17
      *
      *
      * Array index
      *
     d x               s              1  0
      *
      ****************************************************************
      * ARRAYS AND TABLES
      ****************************************************************
      *
     D arasxcdam       s             11  2 dim(3)
     D arssxcdam       s             11  2 dim(3)
     D artsxcdam       s             11  2 dim(3)
     D artafcdam       s             11  2 dim(3)
     D artoxcdam       s             11  2 dim(3)
     D artbccdam       s             11  2 dim(3)
      *
     D artbsxam        s             11  2 dim(3)
     D artbafam        s             11  2 dim(3)
     D artboxam        s             11  2 dim(3)
     D artbbcam        s             11  2 dim(3)
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *
      *---------------------------------------------------------------
      * Local data area.
      *---------------------------------------------------------------
      *
     Dlda             uds                  dtaara(*lda)
      *
     D  ldfmdy                 1      6  0
     D  ldfdt                  7     14  0
     D  ldfwemdy              15     20  0
     D  ldfwedt               21     28  0
      *
     D  ldiecd                29     29
     D  ldieds                30     55
      *
     D  ldrtcd                56     56
     D  ldrtds                57     73
      *
     D  ldtmdy                74     79  0
     D  ldtdt                 80     87  0
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ***************************************************************************************
      * MAINLINE
      ***************************************************************************************
      *
     C                   select
     C                   when      ldiecd = 'E'
     C                   exsr      $exempt
      *
     C                   when      ldiecd = 'N'
     C                   exsr      $nonexempt
      *
     C                   when      ldiecd = 'B'
     C                   exsr      $exempt
     C                   exsr      $nonexempt
     C                   endsl
      *
      * If you did both Exempt/Non-Exempt and had data on either section,
      * print a report total.
      *
     C                   if        ldiecd = 'B' and haddata = yes
     C                   eval      t1desc = '    Report Total:'
     C                   seton                                        80
     C                   z-add     3             x
     C                   exsr      $t1tot
     C                   endif
      *
      * EOF processing
      *
     C                   seton                                        lr
      /EJECT
      *---------------------------------------------------------------
      * Exempt data
      *---------------------------------------------------------------
      *
     C     $exempt       begsr
      *
     C                   move      yes           first
     C                   move      yes           wkrcexfl
     C                   seton                                        87
     C                   exsr      $h1hdr
      *
     C                   exsr      $process
      *
     C                   setoff                                       87
     C                   if        first = yes
     C                   except    nodata
     C                   else
      *
     C                   move      yes           haddata
     C                   seton                                        80
     C                   z-add     2             x
     C                   eval      t1desc = '    Exempt Total:'
     C                   exsr      $t1tot
     C                   exsr      $clear
     C                   endif
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Non-exempt data
      *---------------------------------------------------------------
      *
     C     $nonexempt    begsr
      *
     C                   move      yes           first
     C                   move      no            wkrcexfl
     C                   seton                                        88
     C                   exsr      $h1hdr
      *
     C                   exsr      $process
      *
     C                   setoff                                       88
     C                   if        first = yes
     C                   except    nodata
     C                   else
      *
     C                   move      yes           haddata
     C                   z-add     2             x
     C                   seton                                        80
     C                   eval      t1desc = 'Non-Exempt Total:'
     C                   exsr      $t1tot
     C                   exsr      $clear
     C                   endif
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Process the Cash Distribution Balance and Detail
      *---------------------------------------------------------------
      *
     C     $process      begsr
      *
     C                   setoff                                       80
      *
      * Read each Cash Distribution Balance record selected by the open query.
      *
     C     wkrcexfl      setll     tfl011d
      *
     C                   dou       *in91 = *on                                  Do loop
     C     wkrcexfl      reade     tfl011d                                91
     C                   if        *in91 = *off                                 If not eof
      *
     C                   move      no            first
      *
      * Print the Cash Distribution Balance record
      *
     C                   exsr      $l1dtl
      *
      * Accumulate values for the "Remaining Balances" total line.
      *
     C                   add       cbtsxam       artbsxam
     C                   add       cbtafam       artbafam
     C                   add       cbtoxam       artboxam
     C                   add       cbtbcam       artbbcam
      *
      * Retrieve/print each Cash Distribution Detail line.
      *
     C                   exsr      $detail
      *
      * If you had more than a single 'detail' record, print the total
      * for this 'balance' record.
      *
     C                   z-add     1             x
      *
     C                   if        count > 1
     C                   seton                                        85
     C                   eval      t1desc = '           Total:'
     C                   exsr      $t1tot
     C                   setoff                                       85
     C                   endif
      *
     C                   exsr      $clear
      *
     C                   endif                                                  If not eof
     C                   enddo                                                  Do loop
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Retrieve/print each Cash Distribution Detail record
      *---------------------------------------------------------------
      *
     C     $detail       begsr
      *
     C                   seton                                        89
     C                   z-add     0             count
      *
      * Initialize the 'balance' fields to the values from the Cash
      * Distribution Balance record.
      *
     C                   z-add     cbtsxam       l2tsxbalam
     C                   z-add     cbtafam       l2tafbalam
     C                   z-add     cbtoxam       l2toxbalam
     C                   z-add     cbtbcam       l2tbcbalam
      *
     C     key01         setll     tfl013b
      *
     C                   dou       *in93 = *on                                  Do detail
     C     key01         reade     tfl013b                                93
     C                   if        *in93 = *off                                 If not eof
      *
     C                   add       1             count
      *
      * Flip the Cash Week-Ending date into MMDDYY format.
      *
     C     *iso          test(d)                 cdddt                  92
     C                   if        *in92 = *off                                 If OK
     C                   move      cdddt         wkisodate
     C     *mdy          move      wkisodate     l2cdmdy
     C                   endif                                                  If OK
      *
      * Calculate SBF Sales Expense Amount as:
      *   Aggregate Sales Expense Cash Distributed Amount -
      *          TF Sales Expense Cash Distributed Amount
      *
     C     cdasxcdam     sub       cdtsxcdam     l2ssxcdam
      *
      *
      * Calculate TF Remaining Balances
      *
     C                   sub       cdtsxcdam     l2tsxbalam
     C                   sub       cdtafcdam     l2tafbalam
     C                   sub       cdtoxcdam     l2toxbalam
     C                   sub       cdtbccdam     l2tbcbalam
      *
      * Accumulate values for Totals:
      *
     C                   add       cdasxcdam     arasxcdam
     C                   add       l2ssxcdam     arssxcdam
     C                   add       cdtsxcdam     artsxcdam
     C                   add       cdtafcdam     artafcdam
     C                   add       cdtoxcdam     artoxcdam
     C                   add       cdtbccdam     artbccdam
      *
      *
      * Reduce the "Remaining Balances" amounts that will print in the Total Line.
      *
     C                   sub       cdtsxcdam     artbsxam
     C                   sub       cdtafcdam     artbafam
     C                   sub       cdtbccdam     artbbcam
     C                   sub       cdtoxcdam     artboxam
      *
      *
      * If this is a Manual Adjustment record, print the word Manual in the
      * right-hand margin.
     C                   if        cdsysfl = no
     C                   seton                                        86
     C                   else
     C                   setoff                                       86
     C                   endif
      *
      * Print Cash Distribution Detail line.
      *
     C                   exsr      $l2dtl
      *
     C                   endif                                                  If not eof
     C                   enddo                                                  Do detail
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Clear array for specified index
      *---------------------------------------------------------------
      *
     C     $clear        begsr
      *
     C                   z-add     0             arasxcdam(x)
     C                   z-add     0             arssxcdam(x)
     C                   z-add     0             artsxcdam(x)
     C                   z-add     0             artafcdam(x)
     C                   z-add     0             artoxcdam(x)
     C                   z-add     0             artbccdam(x)
      *
     C                   z-add     0             artbsxam (x)
     C                   z-add     0             artbafam (x)
     C                   z-add     0             artboxam (x)
     C                   z-add     0             artbbcam (x)
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Print detail line one
      *---------------------------------------------------------------
      *
     C     $l1dtl        begsr
      *
     C                   if        *inof = *on
     C                   exsr      $h1hdr
     C                   endif
      *
      * Flip the Week-ending Revenue date into MMDDYY format.
      *
     C     *iso          test(d)                 cbwedt                 92
     C                   if        *in92 = *off                                 If OK
     C                   move      cbwedt        wkisodate
     C     *mdy          move      wkisodate     l1rwemdy
     C                   endif                                                  If OK
      *
      * Calculate SBF % of all Sales Expenses as:
      *    100 - TF % of all Sales Expenses
      *
     C     100           sub       cbtsxpc       l1ssxpc
      *
      * Print blank lines
     C                   except    blank
      *
      * Print the detail line twice to make it bold.
      *
     C                   except    l1dtl
     C                   except    l1dtl
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Print detail line two
      *---------------------------------------------------------------
      *
     C     $l2dtl        begsr
      *
     C                   if        *inof = *on
     C                   exsr      $l1dtl
     C                   endif
      *
     C                   except    l2dtl
      *
     C                   setoff                                       89
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Print report heading
      *---------------------------------------------------------------
      *
     C     $h1hdr        begsr
      *
     C                   except    h1hdr
     C                   seton                                        89
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Print total line for a Cash Distribution Balance record
      *---------------------------------------------------------------
      *
     C     $t1tot        begsr
      *
     C                   if        *inof = *on
     C                   exsr      $h1hdr
     C                   exsr      $l1dtl
     C                   endif
      *
     C                   except    t1tot
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Key lists
      *
     C     key01         klist
     C                   kfld                    cbwedt
     C                   kfld                    wkrcexfl
      *
      * Retrieve time for report heading.
      *
     C                   time                    rtime
      *
      * Set indicators to control printing:
      *
      *      Start date
     C                   if        ldfmdy <> 0
     C                   seton                                        83
     C                   endif
      *      Stop date
     C                   if        ldtdt > 0 and ldtdt < 99999999
     C                   seton                                        84
     C                   endif
      *
     C                   endsr
      /EJECT
      *-------------------------------------------------------------
      * Report heading lines
      *-------------------------------------------------------------
      *
     Oprint198  e            h1hdr          1 01
     O                       sdpgm               10
     O                                          105 'CASH DISTRIBUTION BALANCE'
     O                                          112 ' REPORT'
     O                                          188 'DATE'
     O                       udate         y    198
      *
     O          e            h1hdr          1
     O                       sdusr               10
     O               87                         107 'Sales Expenses--Exempt'
     O               88                         107 'Sales Expenses--Non-Exempt'
     O                                          188 'TIME'
     O                       rtime              198 '  :  :  '
      *
     O          e            h1hdr          1
     O                                          188 'PAGE'
     O                       page          z    198
      *
      *
     O          e            h1hdr          1
     O                                           23 'Start date:'
     O               83      ldfmdy              33 '  /  /  '
     O                                           53 'Week-ending date:'
     O                       ldfwemdy            63 '  /  /  '
      *
      *
     O          e            h1hdr          1
     O                                           23 'Stop date:'
     O               84      ldtmdy              33 '  /  /  '
      *
      *
      *
     O          e            h1hdr          1
     O                                           23 'Exempt/Non-Exempt/Both:'
     O                       ldiecd              26
      *
     O          e            h1hdr          2
     O                                           23 'Report code:'
     O                       ldrtcd              26
     O                       ldrtds              45
      *
      *-------------------------------------------------------------
      * Column headings
      *-------------------------------------------------------------
      *
     O          e            h1hdr          1
     O                                           16 'Week'
     O                                           34 'Cash to'
     O                                          188 'TF Broker'
      *
     O          e            h1hdr          1
     O                                           17 'Ending'
     O                                           34 'Distribute to'
     O                                           51 'SBF % of All'
     O                                           68 'TF % of All'
     O                                           86 'TF Total Sales'
     O                                          103 'TF Absorbed'
     O                                          120 'TF Absorbed'
     O                                          137 'TF Other Sales'
     O                                          154 'TF Other Sales'
     O                                          171 'TF Broker'
     O                                          188 'Commission'
      *
     O          e            h1hdr          1
     O                                            6 'Source'
     O                                           16 'Date'
     O                                           34 'Sales Expenses'
     O                                           51 'Sales Expenses'
     O                                           68 'Sales Expenses'
     O                                           86 'Expense Balance'
     O                                          103 'Freight %'
     O                                          120 'Freight Balance'
     O                                          137 'Expense %'
     O                                          154 'Expense Balance'
     O                                          171 'Commission %'
     O                                          188 'Balance'
      *
     O          e            h1hdr          0
     O                       dash               198
      *
      *
      *-------------------------------------------------------------
      * Detail line for Cash Distribution Balance records
      *-------------------------------------------------------------
      *
     O          e            l1dtl       0  0
     O                                            8 'Revenue:'
     O                       l1rwemdy            18 '  /  /  '
     O                       cbagsxpc            35 '  0.    -%'
     O                       l1ssxpc             52 '  0.    -%'
     O                       cbtsxpc             69 '  0.    -%'
     O                       cbtsxam       k     87
     O                       cbtafpc            104 '  0.    -%'
     O                       cbtafam       k    121
     O                       cbtoxpc            138 '  0.    -%'
     O                       cbtoxam       k    155
     O                       cbtbcpc            172 '  0.    -%'
     O                       cbtbcam       k    189
      *
      *
      *-------------------------------------------------------------
      * Detail line for Cash Distribution Detail records
      *-------------------------------------------------------------
      *
     O          e            l2dtl       1  0
     O               89                           8 'Cash:'
     O                       l2cdmdy             18 '  /  /  '
     O                       cdasxcdam     k     35
     O                       l2ssxcdam     k     52
     O                       cdtsxcdam     k     69
     O                       l2tsxbalam    j     87
     O                       cdtafcdam     k    104
     O                       l2tafbalam    j    121
     O                       cdtoxcdam     k    138
     O                       l2toxbalam    j    155
     O                       cdtbccdam     k    172
     O                       l2tbcbalam    j    189
     O               86                         198 'Manual'
      *
      *-------------------------------------------------------------
      * Total line
      *-------------------------------------------------------------
      *
     O          e    85      t1tot       1  1
     O                                           35 '--------------'
     O                                           52 '--------------'
     O                                           69 '--------------'
     O                                           87 '--------------'
     O                                          104 '--------------'
     O                                          121 '--------------'
     O                                          138 '--------------'
     O                                          155 '--------------'
     O                                          172 '--------------'
     O                                          189 '--------------'
      *
     O          e   n85      t1tot       3
      *
     O          e            t1tot          0
     O                       t1desc              19
     O                       arasxcdam(x)  kb    35
     O                       arssxcdam(x)  kb    52
     O                       artsxcdam(x)  kb    69
     O               80      artbsxam (x)  kb    87
     O                       artafcdam(x)  kb   104
     O               80      artbafam (x)  kb   121
     O                       artoxcdam(x)  kb   138
     O               80      artboxam (x)  kb   155
     O                       artbccdam(x)  kb   172
     O               80      artbbcam (x)  kb   189
      *
      *-------------------------------------------------------------
      * Blank
      *-------------------------------------------------------------
      *
     O          e            blank       3
      *
      *-------------------------------------------------------------
      * No data message line
      *-------------------------------------------------------------
      *
     O          e            nodata      1
     O                                           19 'No data meets your'
     O                                           39 'selection criteria.'
      /eject
