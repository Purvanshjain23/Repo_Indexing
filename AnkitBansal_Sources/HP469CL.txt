/***************************************************************** */
/*                                                                 */
/*  PROGRAM NUMBER --> HP469CL                                     */
/*  PROGRAM NAME ----> INVENTORY REPORTS BY SITE AND AGE           */
/*  PROGRAMMER ------> MARY PALMER               06/15/94          */
/*                                                                 */
/*  PROGRAM PURPOSE -> GENERATE THE INVENTORY REPORTS.             */
/*                     REPORT VALUES ARE PASSED THRU THE LDA FROM  */
/*                     HP469.                                      */
/*                                                                 */
/*                     INCLUDE OPEN GROUPS THAT OPENED BEFORE OR   */
/*                     ON THE CUT-OFF DATE.   IF A GROUP IS        */
/*                     DISPOSED OR CLOSED, INCLUDE IT IF THE       */
/*                     DISPOSED DATE IS GREATER THAN CUT-OFF DATE. */
/*                                                                 */
/***************************************************************** */
/*                                                                 */
/* 12/01/99 LEANNE FEDOR                                           */
/*          THE INVENTORY REPORT BY SITE CAN NOW BE GENERATED AS   */
/*          DETAIL OR SUMMARY.                                     */
/*                                                                 */
/* 05/08/01 LEANNE FEDOR                                           */
/*          ADDED SELECTION ON GROUP TYPE.                         */
/*                                                                 */
/* 12/13/01 LEANNE FEDOR                                           */
/*          CHANGED THE CALL TO HP369 TO HAVE PARMS SO THAT THE    */
/*          WORKFILE CAN BE BUILT FROM OTHER FUNCTIONS. PRIOR TO   */
/*          THIS HP369 USED THE LDA VALUES FOR SOME PROCESSING     */
/*          DECISIONS....NOW IT WILL USE THE PARM VALUES.          */
/*                                                                 */
/* 05/31/07 LEANNE RAMSEY                                          */
/*          REPLACED QPRINT WITH PRINT198. REPLACED OLD CUSTOMIZED */
/*          PRINT OVERRIDE COMMAND WITH NEW STANDARD COMMAND.      */
/***************************************************************** */

             PGM

             DCL        VAR(&QDATA) TYPE(*CHAR) LEN(280)
             DCL        VAR(&LDFSBO) TYPE(*CHAR) LEN(5)
             DCL        VAR(&LDCYMD) TYPE(*DEC) LEN(8)
             DCL        VAR(&LDPTCD) TYPE(*CHAR) LEN(5)
             DCL        VAR(&LDPPCD) TYPE(*CHAR) LEN(5)
             DCL        VAR(&LDGTCD) TYPE(*CHAR) LEN(1)
             DCL        VAR(&LDFSCD) TYPE(*DEC) LEN(5)
             DCL        VAR(&LDHGSN) TYPE(*DEC) LEN(7)
             DCL        VAR(&LDRPFL) TYPE(*CHAR) LEN(1)
             DCL        VAR(&LDCELL) TYPE(*DEC) LEN(2)

             DCL        VAR(&USER) TYPE(*CHAR) LEN(10)

             DCL        VAR(&HOLD) TYPE(*CHAR) LEN(10)
             DCL        VAR(&COPYS) TYPE(*DEC) LEN(2)
             DCL        VAR(&OUTQ) TYPE(*CHAR) LEN(10)

             CHGVAR     VAR(&LDFSBO) VALUE(%SST(*LDA 1 5))
             CHGVAR     VAR(&LDCYMD) VALUE(%SST(*LDA 6 8))
             CHGVAR     VAR(&LDPTCD) VALUE(%SST(*LDA 14 5))
             CHGVAR     VAR(&LDPPCD) VALUE(%SST(*LDA 19 5))
             CHGVAR     VAR(&LDGTCD) VALUE(%SST(*LDA 47 1))
             CHGVAR     VAR(&LDFSCD) VALUE(%SST(*LDA 24 5))
             CHGVAR     VAR(&LDHGSN) VALUE(%SST(*LDA 29 7))
             CHGVAR     VAR(&LDRPFL) VALUE(%SST(*LDA 36 1))
             CHGVAR     VAR(&LDCELL) VALUE(%SST(*LDA 45 2))

/*  SET UP A DEFAULT STARTING POINT FOR THE QUERY                */

             CHGVAR     VAR(%SST(&QDATA 1 14)) VALUE('HGFSBO *NE +
                          " "')

/*  IF BUSINESS OFFICE IS SELECTED                               */

             IF         COND(&LDFSBO *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 1 12)) VALUE('HGFSBO *EQ "')
             CHGVAR     VAR(%SST(&QDATA 13 5)) VALUE(&LDFSBO)
             CHGVAR     VAR(%SST(&QDATA 18 1)) VALUE('"')
             ENDDO

/*  IF PRODUCTION TYPE IS NOT BLANK, THEN ADD A TYPE SELECTION    */

             IF         COND(&LDPTCD *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 20 17)) VALUE('*AND HGPTCD +
                          *EQ "')
             CHGVAR     VAR(%SST(&QDATA 37 5)) VALUE(&LDPTCD)
             CHGVAR     VAR(%SST(&QDATA 42 1)) VALUE('"')
             ENDDO

/*  IF PRODUCTION PHASE IS NOT BLANK, THEN ADD A PHASE SELECTION   */

             IF         COND(&LDPPCD *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 44 17)) VALUE('*AND HGPPCD +
                          *EQ "')
             CHGVAR     VAR(%SST(&QDATA 61 5)) VALUE(&LDPPCD)
             CHGVAR     VAR(%SST(&QDATA 66 1)) VALUE('"')
             ENDDO

/*  IF GROUP TYPE IS NOT BLANK, ADD A GROUP TYPE SELECTION         */

             IF         COND(&LDGTCD *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 68 17)) VALUE('*AND HGGTCD +
                          *EQ "')
             CHGVAR     VAR(%SST(&QDATA 85 1)) VALUE(&LDGTCD)
             CHGVAR     VAR(%SST(&QDATA 86 1)) VALUE('"')
             ENDDO

/*  IF FARM SITE IS NOT A BLANK, THEN ADD A FARM SITE SELECTION  */

             IF         COND(&LDFSCD *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 88 15)) VALUE('*AND HGFSCD +
                          *EQ')
             CHGVAR     VAR(%SST(&QDATA 104 5)) VALUE(&LDFSCD)
             ENDDO

/*  IF HOG GROUP IS NOT BLANK, THEN ADD A GROUP SELECTION           */

             IF         COND(&LDHGSN *NE 0000000) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 110 15)) VALUE('*AND HGHGSN +
                          *EQ')
             CHGVAR     VAR(%SST(&QDATA 126 7)) VALUE(&LDHGSN)
             ENDDO

             ELSE (DO)

/*  HOG GROUP IS BLANK, OPENED DATE GREATER THAN ZERO AND LESS THAN */
/*  CUT-OFF DATE                                                    */

             CHGVAR     VAR(%SST(&QDATA 134 15)) VALUE('*AND HGOPDT +
                          *GT')
             CHGVAR     VAR(%SST(&QDATA 150 8)) VALUE(00000000)
             CHGVAR     VAR(%SST(&QDATA 159 15)) VALUE('*AND HGOPDT +
                          *LE')
             CHGVAR     VAR(%SST(&QDATA 175 8)) VALUE(&LDCYMD)

/*  HOG GROUP IS BLANK, SELECT DISPOSED DATE EQUAL TO ZERO          */
/*  OR GREATER THAN THE CUT OFF DATE.                               */

             CHGVAR     VAR(%SST(&QDATA 184 16)) VALUE('*AND (HGDSDT +
                          *EQ')
             CHGVAR     VAR(%SST(&QDATA 201 8)) VALUE(00000000)
             CHGVAR     VAR(%SST(&QDATA 210 14)) VALUE('*OR HGDSDT +
                          *GE')
             CHGVAR     VAR(%SST(&QDATA 225 8)) VALUE(&LDCYMD)
             CHGVAR     VAR(%SST(&QDATA 233 1)) VALUE(')')
             ENDDO


/*  ALWAYS OMIT HOG GROUPS WITH A STATUS OF VOIDED               */

             CHGVAR     VAR(%SST(&QDATA 235 20)) VALUE('*AND HGGSCD +
                          *NE "VD"')

/*  ALWAYS OMIT "BGF" PRODUCTION PHASE GROUPS                    */

             CHGVAR     VAR(%SST(&QDATA 256 23)) VALUE('*AND HGPPCD +
                          *NE "BGF  "')


/*------------------------------------------------------------------*/
/*  RUN QUERY FOR BUILDING OF WORKFILE FOR INVENTORY REPORTS        */
/*------------------------------------------------------------------*/

             OVRDBF     FILE(HSL034K) SHARE(*YES)
             OPNQRYF    FILE((HSL034K)) QRYSLT(&QDATA)

/*------------------------------------------------------------------*/
/*  CREATE WORKFILE                                             */
/*------------------------------------------------------------------*/

             CRTDUPOBJL OBJ(HSP369) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)

             CRTDUPOBJL OBJ(HSL369A) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)

             CRTDUPOBJL OBJ(HSL369B) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)


/*  RUN PROGRAM TO BUILD WORKFILE                               */

             CALL       PGM(HP369) PARM(&LDRPFL &LDCELL &LDCYMD)

/*------------------------------------------------------------------*/
/* SET OVERRIDES FOR PRINT FILES                                    */
/*------------------------------------------------------------------*/

 HOLD:       IF         COND(%SST(*LDA 411 1) = 'Y') THEN(CHGVAR +
                          VAR(&HOLD) VALUE(*YES))
             ELSE       CMD(CHGVAR VAR(&HOLD) VALUE(*NO))

 COPYS:      CHGVAR     VAR(&COPYS) VALUE(%SST(*LDA 412 1))
             IF         COND(&COPYS = 0) THEN(CHGVAR VAR(&COPYS) +
                          VALUE(1))

 OUTQ:       CHGVAR     VAR(&OUTQ) VALUE(%SST(*LDA 413 10))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(%SST(*LDA 401 10)))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(*JOB))

/*------------------------------------------------------------------*/
/* OVERRIDE PRINT FILE                                              */
/*------------------------------------------------------------------*/

             OVRPRTF    FILE(PRINT198) OUTQ(&OUTQ) COPIES(&COPYS) +
                          HOLD(&HOLD)

/*------------------------------------------------------------------*/
/*  INVENTORY REPORT BY SITE (SUMMARY AND DETAIL)                   */
/*------------------------------------------------------------------*/

             IF         COND(&LDRPFL *EQ 'S' *OR &LDRPFL *EQ 'D') +
                          THEN(DO)
             CALL       PGM(HP669)
             ENDDO

/*------------------------------------------------------------------*/
/*  INVENTORY REPORT BY AGE OR BY WEEKS/DAYS IN PHASE               */
/*------------------------------------------------------------------*/

             IF         COND(&LDRPFL *EQ 'A' *OR &LDRPFL *EQ 'P') +
                          THEN(DO)
             CALL       PGM(HP679)
             ENDDO

             CLOF       OPNID(HSL034K)
             DLTOVR     FILE(*ALL)

             ENDPGM

