     A*%%TS  SD  20091029  083159  ISLRAMS     REL-V5R4M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD438
     A*  TITLE:         SPECIFY BATCH EDIT LISTING OF UPLOADED FEED TICKETS
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  DATE:          04/28/97
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A                                      CF08(08 'SUBMIT')
     A                                      CF17(17 'PRINT OPTIONS')
     A                                      CF20(20 'WRKSPLF')
     A*
     A*--------------------------------------------------------------
     A* RECORD FORMAT ONE
     A*--------------------------------------------------------------
     A          R HP438R1
     A*%%TS  SD  20091029  083159  ISLRAMS     REL-V5R4M0  5722-WDS
     A                                      OVERLAY
     A                                      RTNCSRLOC(&RCD &FLD)
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 27'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  2 14'SPECIFY BATCH EDIT LISTING OF UPLO-
     A                                      ADED FEED TICKETS'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  5  3'Take F8-Submit to submit the batch-
     A                                       edit.'
     A*
     A*--------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*--------------------------------------------------------------
     A          R HP438K1
     A*%%TS  SD  19970428  131626  ISLRAMS     REL-V3R6M0  5716-PW1
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A                                 23 11'F8=Submit'
     A                                 23 22'F17=Print Options'
     A                                 23 41'F20=Spool Files'
      *
      *--------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *--------------------------------------------------------------
     A          R HP438ES                   SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
     A*
     A*--------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*--------------------------------------------------------------
     A*
     A          R HP438EC                   SFLCTL(HP438ES)
     A*%%TS  SD  19940415  103325  ISRCENT     REL-V2R2M0  5738-PW1
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
