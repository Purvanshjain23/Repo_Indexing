      *****************  RPG PROGRAM HEADING  ***********************
      *
      * SYSTEM:      HOG PRODUCTION
      * PROGRAM:     HP146
      * TITLE:       MAINTAIN SALE
      * PROGRAMMER:  LEANNE FEDOR
      * CREATED:     06/08/95
      *
      * FUNCTION: THIS PROGRAM USES A SUBFILE TO MAINTAIN RECORDS
      *           IN THE SALES FILES (HEADER AND DETAIL)
      *           FOR SALES MOVEMENTS.
      *
      *           THE PROGRAM WILL CREATE, DELETE AND UPDATE RECORDS
      *           FOR A SPECIFIC MOVEMENT NUMBER.
      *
      *           THIS PROGRAM IS CALLED FROM:
      *                HP446-WORK WITH SALES.
      *
      *           IT DOES NOT HAVE SELECTORS OR POSITIONERS.
      *           IT HAS F4-PROMPT LOGIC.
      *           IT USES THE STANDARD MESSAGE HANDLING PROGRAM.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 02/13/96  LEANNE FEDOR
      *           JANNA SHERREL AT THE GUYMON TRUCK SHOP REQUESTED
      *           THAT, IN CREATE MODE, THE USER ENTRIES BE LEFT IN THE
      *           CONTROL RECORD FIELDS SO THEY DO NOT HAVE TO BE
      *           REKEYED.
      *
      * 03/27/96  LEANNE FEDOR
      *           ADDED SECURITY CHECK TO DETERMINE IF USER IS
      *           AUTHORIZED TO DELETE MOVEMENT.
      *
      * 04/01/96  LEANNE FEDOR
      *           MODIFIED THE DELETION LOGIC TO:
      *            1) CALL A CONFIRM DELETE PROGRAM WHERE A COMMENT
      *               MUST BE ENTERED.
      *            2) WRITE AUDIT INFO FOR THE DELETED MOVEMENT TO
      *               THE NEW 'DELETED MOVEMENT' FILE.
      *
      * 04/04/96  LEANNE FEDOR
      *           CHANGED THE SHIPPED POUNDS DISTRIBUTION LOGIC. WHEN
      *           A SALES MOVEMENT WAS REVISED, THE ORIGINAL LOGIC
      *           DID NOT DISTRIBUTE ANY CHANGE IN SHIPPED POUNDS TO
      *           CLOSED OR DISPOSED GROUPS ON THE MOVEMENT. WE REMOVED
      *           THIS RESTIRCTION FOR DISPOSED GROUPS.  DISPOSED
      *           GROUPS CAN NOW RECEIVE DISTRIBUTED SHIPPED POUNDS.
      *
      * 04/30/96  LEANNE FEDOR
      *           CHANGED DELETION SECURITY LOGIC. DELETION ERROR
      *           MESSAGES ARE NOW ISSUED FROM THE DELETION SECURITY
      *           PROGRAM.
      *
      * 05/08/96  LEANNE FEDOR
      *           MADE CHANGES TO FACILITATE NEW JDE RELEASE CONVERSION
      *           CHANGED SEARCH TYPE TO 3A; CALL NEW GENERIC
      *           PROGRAM HPJDEV TO VALIDATE INSTEAD OF HPJDEA SINCE
      *           NO VENDOR ADDRESS IS REQUIRED.
      *
      * 10/10/97  LEANNE FEDOR
      *           CHANGED F10=ACCEPT TO F9=ACCEPT SO THAT THE ACCEPT KEY
      *           IS PHYSICALLY ON THE KEYBOARD FARTHER FROM THE F11
      *           DELETE KEY.
      *
      * 02/05/98  LEANNE FEDOR
      *           Changed logic to update the event date in the Movement
      *           Event file.  Old logic moved 'scheduled' date to event
      *           date when movement was "SC" and 'shipped' date to
      *           event date when status was "SH". It did nothing when
      *           the status was NC, FP, PP.  New logic moves shipped
      *           date into event date for all statuses other than
      *           scheduled.
      *
      * 01/11/99  LeAnne Fedor
      *           New field 'scheduled kill date' was added to sales movement
      *           header file. Added logic to a) edit and b) populate field.
      *
      * 11/03/99  LeAnne Fedor
      *           Reworked.  1) Building/room were added to subfile line with logic
      *           that attempts to default in a group or vice/versa a building/room
      *           depending on which values the user keyed. 2) Increased subfile
      *           records per page to 4.
      *
      * 01/03/99  LeAnne Fedor
      *           Changed logic to allow disposed groups to be changed
      *           on 'Scheduled' movements. (Scheduled movements will not have
      *           closed groups.)
      *           Also, removed protect on 'scheduled kill date'. This can be
      *           changed at any time since no logic is based on this date.
      *
      * 10/17/00  LeAnne Fedor
      *           Added new 'load type' field to screen. (New field is in
      *           sales movement header file.)
      *
      * 01/31/01  LeAnne Fedor
      *           In preparation for allowing groups on a farm to have different
      *           production types, we added the group 'production type' as output only
      *           in the subfile. Also, we added an edit that all groups on a movement
      *           must have the same production type----it does not have to match the
      *           production type of the farm in the farm master.
      *
      * 07/17/02  LeAnne Fedor
      *           In preparation for allowing groups on a farm to have different
      *           production phases, we added the group 'production phase' as output only
      *           in the subfile. Also, we added an edit that all groups on a movement
      *           must have the same production phase---it does not have to match the
      *           production phase of the farm in the farm master.
      *
      * 08/01202  LeAnne Fedor
      *           Added edit to prevent a sale out of a group whose 'open' date is after
      *           any/all of the following dates: scheduled ship date, scheduled kill date,
      *           shipped date, received date.
      *
      * 12/17/02  LeAnne Fedor
      *           We removed 'production phase/type' from the Sales Movement Header. So,
      *           we removed the edit that all groups on a movement must have the same
      *           type/phase.
      *
      * 01/07/03  LeAnne Fedor
      *           Added edit that 'trucker' must be active when movement status is 'scheduled'.
      *
      * 01/15/03  LeAnne Fedor
      *           Removed 'sex' and 'genetic line' from Sales Movement Detail file.
      *           Removed 'shipped in/out times' from Sales Movement Header file.
      *
      * 01/22/03  LeAnne Fedor
      *           Added logic for 2 'versions': full and restricted. The incoming parm
      *           controls the version. Add call to SYNON program for Edit Delivery Tracking.
      *
      *           Also, load type is now a required field.
      *
      * 12/23/03  Rose Centonze
      *           Added parm to call of pla0xfr.
      *
      * 04/13/04  LeAnne Fedor
      *           You can no longer have Nursery groups on Market Sales.
      *
      * 01/18/06  LeAnne Fedor
      *           Recompile only.
      *           Field "Bin Set" was removed from HSP020-Building Rooms file
      *
      * 09/29/08  LeAnne Ramsey
      *           We discovered that the out-of-synch problem between Checks and
      *           Sales Movements was caused by Jessica LePlatt having the Check
      *           up in one session and a Movement from the check up on a second
      *           session. She was updating/exiting the check and (without
      *           refreshing) updating the Movement. The update logic used the
      *           Live/DOA Head from the output only fields on the screen. So,
      *           when Jessica did not refresh, the update plopped the "old" screen
      *           values into the record overriding the "new" values that had
      *           been populated from the Check function on her other session.
      *
      *           So, we changed the code to update the appropriate values using
      *           the check detail file. We also now reset the Movement Status
      *           and Type of Payment.
      *
      * 11/17/08  LeAnne Ramsey
      *           Added a clear of the Movement Detail record in subroutine $crtsd.
      *           Since we are now retrieving our HEAD values from the Check Detail
      *           file, we were duping up on HEAD when there were multiple subfile lines.
      *
      *           Also, added logic in the $status routine to NOT reset to NC=
      *           Not Completely Paid if the user has specifically set an NC movement
      *           to FP=Fully Paid.
      *
      * 07/02/09  LeAnne Ramsey
      *           Recompile only. Added new field 'Continuous Flow Flag' to Hog Group file.
      *
      * 06/17/10  LeAnne Ramsey  (E817)
      *           In some scenarios, a Movement is not updated thru Delivery Tracking but does
      *           hit HPE. So, when the HPE-to-HPS interface runs, the Movement is still in a
      *           SC=Scheduled status and the HPS users cannot get to it to change it.
      *           So, I will now open up SC movements to HPS users if the Movement is in the
      *           Tattoo header.  (see $restricted subroutine)
      *
      * 08/11/10  Purva Droge/Susan Mason (e1648)
      *           Add a parameter for Arrival Status
      *
      * 10/15/13  LeAnne Ramsey (E2831)
      *           Recompile only. Added field 'MTech Reference'.
      *
      * 10/21/13  LeAnne Ramsey (E2831)
      *           Added MTech Reference as a parm on the calls to:
      *               HP538-Select Trucker
      *
      *  6/04/18  Brad Baden    (E13137)
      *           Added update to Scheduled Load Plan file.  Whenever
      *           the Scheduled Ship Date and Scheduled Kill date
      *           values are changed on the screen, the same values
      *           will be updated in the Scheduled Load Plan file
      *           HSP196.
      *
      * 11/04/20  ISE           (H16853)
      *           Recompile only. Increased Buy Order Number field length from 5 to 7.
      *
      *  3/02/21  ISE           (H16853)
      *           Increase the length of the Buy Order Number field
      *           xxsynbuy from 5 to 7 in length.
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     FHPD146    CF   E             WORKSTN SFILE(HP146S1:HDRRN1)
     F                                     INFDS(IOFEED)
      *
     FHSP018    IF   E           K DISK
      *    FARM SITE
      *
      *
     Fhsp019    if   e           k disk
      *    Farm buildings
      *
      *
     Fhsp020    if   e           k disk
      *    building rooms
      *
      *
     FHSP034    IF   E           K DISK
      *  HOG GROUPS
      *
      *
     Fhsl034p   if   e           k disk    rename(hgrec:hgrecp)
      *  Hog groups
      *
      *
     Fhsl034r   if   e           k disk    rename(hgrec:hgrecr)
      *  Hog groups
      *
      *
     FHSL034D   IF   E           K DISK    rename(hgrec:hgrecd)
      *  HOG GROUPS
      *
      *
     FHSP046    IF   E           K DISK
      *    TRUCKER ID
      *
      *
     FHSP058    UF A E           K DISK
      *  MOVEMENT EVENT
      *
      *
     FHSP066    IF   E           K DISK
      *    SALES TYPE
      *
      *
     Fhsp067    if   e           k disk
      *    Load type
      *
      *
     FHSP077    IF   E           K DISK
      *    VALID STATUS CODES
      *
      *
      *
     FHSL064C   IF   E           K DISK
      *  CHECK DETAIL
      *
      *
     Fhsl064i   if   e           k disk    rename(cdrec:cdreci)
      *  Check detail
      *
      *
     FHSP080    O    E             DISK
      *  DELETED MOVEMENTS
      *
      *
     FHSP084    UF A E           K DISK
      *  SALE HEADER
      *
      *
     FHSP085    UF A E           K DISK
      *  SALE DETAIL
      *
      *
     FHSL196B   UF   E           K DISK
      *  SCHEDULED LOAD PLAN
      *
      *
     Fpka1cpk2  if   e           k disk
      *  Synon: Tattoo header
      *
      /EJECT
      ****************************************************************
      * TABLE AND ARRAY SPECIFICATIONS
      ****************************************************************
      *
      *---------------------------------------------------------------
      * HOG GROUP ARRAY USED TO VALIDATE THAT A GROUP IS ON A MOVEMENT
      * ONLY ONE TIME
      *---------------------------------------------------------------
      *
     D GRP             S              7    DIM(50)                              GROUPS
      /EJECT
      ****************************************************************
      * INPUT SPECIFICATIONS
      ****************************************************************
      *
      *---------------------------------------------------------------
      * STANDARD ERROR MESSAGE HANDLING DATA STRUCTURES
      *---------------------------------------------------------------
      *
      * FOR ERROR MESSAGE HANDLING, A PACKED INDEX (E) IS REQUIRED.
      * FOR PROGRAM READABILITY, DEFINE A CORRESPONDING ERROR COUNT
      * FIELD CALLED 'ERROR'. ALSO DEFINE CURSOR LOCATION FIELDS.
      *
     D                 DS                  INZ
     D  ERROR                  1      2P 0
     D  E                      1      2P 0
      *
      * THIS DATA STRUCTURE SUPPLIES THE NAME OF THE MESSAGE FILE TO
      * THE MESSAGE HANDLING CL PROGRAM.  THE FIELD NAME MSGFIL MUST BE
      * CONSTANT. THE VALUE IN QUOTES IS THE NAME OF THE SPECIFIC
      * MESSAGE FILE CONTAINING THE USER DEFINED MESSAGES.
      *
     D                 DS                  INZ
     D  MSGFIL                 1     10    INZ('HSMSGF    ')
      *
      * THE FOLLOWING 3 DATA STRUCTURES ARE USED TO SPEED MESSAGE
      * HANDLING SINCE IT IS FASTER TO CLEAR DATA STRUCTURES THAN
      * ARRAYS.  EACH IS ASSOCIATED WITH A STANDARD MESSAGE ARRAY.
      *
     D MGI             DS           140    INZ
     D  MGID                   1    140
     D                                     DIM(20)                              MSG ID ARRAY
      *
     D MGD             DS          1000    INZ
     D  MGDT                   1   1000
     D                                     DIM(20)                              MSG PARMS
      *
     D MGWK            DS            50    INZ
     D  MGW                    1     50
     D                                     DIM(50)                              MSG WORK PARMS
      *
      *---------------------------------------------------------------
      * STANDARD PROGRAM STATUS DATA STRUCTURE
      *---------------------------------------------------------------
      *    EXTERNALLY DEFINED AS UTPGFR (RECORD FORMAT: PGMDSR)
     D PGMDS         ESDS                  EXTNAME(UTPGFR)
      *
      *---------------------------------------------------------------
      * STANDARD WORKSTATION INFORMATION DATA STRUCTURE
      *---------------------------------------------------------------
      *    EXTERNALLY DEFINED AS UTWSFR (RECORD FORMAT: UTIDFRR)
     D IOFEED        E DS                  EXTNAME(UTWSFR)
      *
      *---------------------------------------------------------------
      * STANDARD DATABASE FILE INFORMATION DATA STRUCTURE
      *---------------------------------------------------------------
      *    EXTERNALLY DEFINED AS UTDBFR (RECORD FORMAT: FDBCKD)
     D DBFEED        E DS                  EXTNAME(UTDBFR)
      *
      *---------------------------------------------------------------
      *  DATE FORMATS
      *---------------------------------------------------------------
      *
      * DATE IN MMDDYY FORMAT
      *
     D                 DS
     D  WKMDY                  1      6  0
     D  WKMM                   1      2  0
     D  WKDD                   3      4  0
     D  WKYY                   5      6  0
      *
      * DATE IN CCYYMMDD FORMAT
      *
     D                 DS
     D  WKCYMD                 1      8  0
     D  WKCEN                  1      2  0
     D  WKYER                  3      4  0
     D  WKMON                  5      6  0
     D  WKDAY                  7      8  0
      *
      *---------------------------------------------------------------
      * TIME FORMAT FOR RETRIEVAL OF SYSTEM TIME FOR RECORD TIME STAMP
      *---------------------------------------------------------------
      *
     D                 DS
     D  HHMMSS                 1      6  0
     D  HHMM                   1      4  0
      *
      *---------------------------------------------------------------
      *  DATA AREA FOR NEXT MOVEMENT NUMBER
      *---------------------------------------------------------------
      *
     D                 DS
     D  DAMVSN                 1      7  0
      *
      *---------------------------------------------------------------
      *  DEFINITION FOR EXTERNAL DATA AREA 'DAAPER'
      *---------------------------------------------------------------
     D                 DS
     D  DAAPER                 1     24
     D  DACCYY                 1      4
     D  DAPER                  5      6
     D  DABPDT                 7     14  0
     D  DAEPDT                15     22  0
     D  DAPGFL                23     23
     D  DAPPFL                24     24
      *
      *---------------------------------------------------------------
      * Standalone fields
      *---------------------------------------------------------------
      *
     D editfl          s              1
     D xxbocd          s                   like(lpbocd)
      *
      *---------------------------------------------------------------
      *  NAMED CONSTANTS
      *---------------------------------------------------------------
      *
     D CREATE          C                   CONST('CREATE')
     D REVISE          C                   CONST('REVISE')
     D DELETE          C                   CONST('DELETE')
     D VIEW            C                   CONST('VIEW  ')
     D SELECT          C                   CONST('SELECT')
     D YES             C                   CONST('Y')
     D NO              C                   CONST('N')
     D SET1            C                   CONST('SET1  ')
     D EDIT1           C                   CONST('EDIT1 ')
     D SCRN1           C                   CONST('SCRN1 ')
     D EXIT            C                   CONST('EXIT  ')
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * MAINLINE
      ****************************************************************
      *
     C                   MOVEL     SET1          ROUTNE
      *
     C     *INLR         DOWEQ     *OFF                                         MAIN DO LOOP
      *
     C     ROUTNE        CASEQ     SET1          $SET1
     C     ROUTNE        CASEQ     SCRN1         $SCRN1
     C     ROUTNE        CASEQ     EDIT1         $EDIT1
      *
     C     ROUTNE        CASEQ     EXIT          $EXIT
     C                   ENDCS
     C                   ENDDO                                                  MAIN DO LOOP
      /EJECT
      *----------------------------------------------------------------
      *  SET ENVIRONMENT FOR SCREEN 1
      *----------------------------------------------------------------
      *
     C     $SET1         BEGSR
      *
      * IF THE USER IS IN CREATE MODE
      *
     C                   SELECT
     C     MODE          WHENEQ    CREATE                                       WH CREATE
     C                   setoff                                       3537
     C                   Z-ADD     0             C1MVSN
     C                   MOVEL     'CREATE'      DFMODE
     C                   OTHER
      *
      * IF THE USER IS REVISING, VERIFY THAT THE SALES MOVEMENT
      *    THAT THE USER SELECTED STILL EXISTS AND SETUP THE HEADER
      *    INFO FOR THE CONTROL RECORD
      *
     C     C1MVSN        CHAIN(N)  HSP084                             90
     C     *IN90         IFEQ      *OFF                                         IF FOUND
     C                   EXSR      $HDR
     C                   ELSE
     C                   MOVEL     YES           NOTFLG
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS00592'     MGID(E)
     C                   CLEAR                   MGW
     C                   MOVEL     C1MVSN        XXMVSN
     C                   MOVEA     XXMVSN        MGW(1)
     C                   MOVEA     MGW           MGDT(E)
     C                   ENDIF
     C                   ENDIF                                                  IF FOUND
     C                   ENDSL
      *
      *  CLEAR/FILL THE SUBFILE
      *
     C                   EXSR      $CLR1
     C                   EXSR      $FRSH1
      *
      *  SET ROUTINE TO DISPLAY SCREEN
      *
     C                   MOVEL     SCRN1         ROUTNE
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * PERFORM OPERATOR I/O FOR SCREEN 1
      *---------------------------------------------------------------
      *
     C     $SCRN1        BEGSR
      *
      * SET THE PAGE OF THE SUBFILE TO DISPLAY
      *
     C                   SELECT
     C     WKFLOW        WHENEQ    0
     C     ERROR         ANDEQ     0
     C                   Z-ADD     1             HDPAG1
      *
     C     RESET         WHENEQ    YES
     C     WKFLOW        IFGT      HDRRN1
     C                   ELSE
     C                   Z-ADD     WKFLOW        HDPAG1
     C                   ENDIF
     C                   ENDSL
      *
      * SET THE INDICATORS TO CONTROL SCREEN FIELDS
      *
     C                   EXSR      $C1IND
      *
      * IF THE USER HAS EDITED THE DATA, THEN ISSUE THE 'ACCEPT
      * CHANGES' MESSAGE.
      *
     C                   if        editfl = yes and
     C                             (error = 0 or error = warmsg)
     C                   add       1             ERROR
     C                   MOVEL     'HS09026'     MGID(E)
     C                   ENDIF
      *
      * WRITE THE ERROR MESSAGES FROM THE ERROR ARRAYS TO THE
      * ERROR MESSAGE SUBFILE
      *
     C                   EXSR      $WRMSG
      *
      *  WRITE SCREEN 1 TO CRT
      *
     C                   WRITE     HP146K1
     C                   WRITE     HP146EC
     C                   EXFMT     HP146C1
      *
      * RESET THE COUNT FIELD FOR WARNING MESSAGES
      *
     C                   Z-ADD     0             WARMSG
      *
      * RETRIEVE THE LOWEST SUBFILE RRN ON THE SCREEN FROM THE DSPF
      * FEEDBACK AREA (IDFLOW) AND MOVE IT TO A WORK FIELD.  THIS VALUE
      * WILL BE USED LATER TO CONTROL THE REPOSITIONING OF THE SUBFILE.
      *
     C                   Z-ADD     IDFLOW        WKFLOW
     C                   MOVEL     NO            RESET
     C                   MOVEL     NO            EDITFL
      *
      * CLEAR ALL MESSAGES
      *
     C                   EXSR      $CLMSG
      *
      * GET USER'S ENTRY AND RESET STANDARD RESPONSE
      *
     C                   SELECT
     C     *IN03         WHENEQ    *ON                                          F3-EXIT
     C                   MOVEL     '03'          RTNCD
     C                   MOVEL     EXIT          ROUTNE
      *
     C     *IN04         WHENEQ    *ON                                          F4-PROMPT
     C                   EXSR      $F4S1
     C                   MOVEL     SCRN1         ROUTNE
      *
     C     *IN05         WHENEQ    *ON                                          F5-REFRESH
     C                   MOVEL     YES           RESET
     C                   MOVEL     SET1          ROUTNE
      *
     C     *IN09         WHENEQ    *ON                                          F9-ACCEPT
      *
      *----------------------------------------------------------------
      * REVISING AN EXISTING SALES MOVEMENT
      *----------------------------------------------------------------
      *
     C     MODE          IFEQ      REVISE
     C                   EXSR      $EDIT1
     C                   if        error = 0 or error = warmsg
      *
      * DELETE EXISTING SALES HEADER AND ALL SALES DETAIL RECORDS
      * AND ALL MOVEMENT EVENT RECORDS
      *
     C                   EXSR      $DLTSH
     C                   EXSR      $DLTSD
     C                   EXSR      $DLTME
      *
      * CREATE NEW SALES HEADER AND DETAIL RECORDS AND NEW
      * MOVEMENT EVENT RECORDS FOR EACH DETAIL RECORD.
      *
     C                   EXSR      $CRTSH
     C                   EXSR      $CRTSD
      *
     C                   if        c1cvno = 360516 and c1stcd = 'MRKTS'
     C                   call      'PLA0XFR'
     C                   parm      *blank        xxsynrtn          7
     C                   parm      shmvsn        xxsynmvsn         7 0
     C                   parm      360           xxsyncono         3 0
     C*********          parm      0             xxsynbuy          5 0
     C                   parm      0             xxsynbuy          7 0
     C                   parm      0             xxsynload         5 0
     C                   parm      'Y'           xxsynsts          1
     C                   parm      ' '           xxsynast          1
     C                   endif
      *
      * Recalculate the Movement Status and movement Type of Payment code
      * for the header record.
      *
     C                   exsr      $status
      *
     C                   MOVEL     EXIT          ROUTNE
     C                   ELSE
     C                   MOVEL     SCRN1         ROUTNE
     C                   ENDIF
      *
     C                   ENDIF
      *
      *----------------------------------------------------------------
      * CREATING A NEW SALES MOVEMENT
      *----------------------------------------------------------------
      *
     C     MODE          IFEQ      CREATE
     C                   EXSR      $EDIT1
     C                   if        error = 0 or error = warmsg
      *
      * 1) CREATE THE SALES HEADER RECORD
      * 2) CREATE ALL DETAIL RECORDS FOR THIS SALE
      *    WITH A MOVEMENT EVENT RECORD FOR EACH DETAIL RECORD.
      *
     C                   EXSR      $CRTSH
     C                   EXSR      $CRTSD
      *
      * ISSUE THE MESSAGE THAT THE SALES MOVEMENT WAS CREATED
      *
     C     ERROR         IFLT      MAXMSG
     C                   exsr      $clmsg
     C                   ADD       1             ERROR
     C                   MOVEL     'HS00594'     MGID(E)
     C                   CLEAR                   MGW
     C                   MOVEL     DAMVSN        XXMVSN
     C                   MOVEA     XXMVSN        MGW(1)
     C                   MOVEA     MGW           MGDT(E)
     C                   ENDIF
      *
     C                   MOVEL     SET1          ROUTNE
     C                   ELSE
     C                   MOVEL     SCRN1         ROUTNE
     C                   ENDIF
     C                   ENDIF
      *
      *----------------------------------------------------------------
      * DELETING A SALES MOVEMENT
      *----------------------------------------------------------------
      *
     C     *IN11         WHENEQ    *ON                                          F11-DELETE
     C                   EXSR      $DLT11
      *
     C     *IN12         WHENEQ    *ON                                          F12-PREVIOUS
     C                   MOVEL     '12'          RTNCD
     C                   MOVEL     EXIT          ROUTNE
      *
     C     *IN13         WHENEQ    *ON                                          F13-VIEW
     C                   CALL      'HP706'
     C                   PARM                    C1MVSN
     C                   PARM                    C1CVNO
     C                   PARM                    C1STCD
     C                   PARM                    RTNCD
     C                   MOVEL     SCRN1         ROUTNE
      *
     C     *IN25         WHENEQ    *ON                                          F25-ROLLUP
     C     IDFLOW        ADD       WKPLIN        WKDIFF
     C     HDRRN1        IFGE      WKDIFF                                       IF SOMETHING
     C                   ELSE
     C                   Z-ADD     COUNT         HDRRN1
     C                   EXSR      $SETOF
     C                   EXSR      $CLRLN
     C                   DO        WKPLIN                                       DO FILL
     C                   EXSR      $BLANK
     C                   ENDDO                                                  DO FILL
     C                   ENDIF                                                  IF SOMETHING
     C                   MOVEL     SCRN1         ROUTNE
      *
     C     NOTFLG        WHENEQ    YES
     C                   MOVEL     SET1          ROUTNE
     C                   OTHER
      *
      * USER PRESSED 'ENTER'
      *
     C                   EXSR      $EDIT1
     C                   ENDSL
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * Update the sales movement status
      *----------------------------------------------------------------
      *
      * Retrieve the sales detail lines for the movement.  Sum the 'live and DOA'
      * head into a field for 'arrived' head.  Sum the shipped head.
      * Then, reset the status based on the comparison of the 'shipped' and
      * 'arrived' head.
      *
     C     $status       begsr
      *
     C                   z-add     0             wkshhd
     C                   z-add     0             wkarhd
      *
      * Retrieve Sales Movement Header record
      *
     C     c1mvsn        chain     hsp084                             92
     C                   if        *in92 = *off                                 If header
      *
      * Count shipped and arrived head for this movement
      *
     C     c1mvsn        setll     hsp085
      *
     C                   dou       *in94 = *on                                  Do head
     C     c1mvsn        reade(n)  hsp085                                 94
     C                   if        *in94 = *off
     C                   add       sgshhd        wkshhd
     C                   add       sglvhd        wkarhd
     C                   add       sgdohd        wkarhd
     C                   endif
     C                   enddo                                                  Do head
      *
      * Set up movement status
      *
     C                   select
      * No checks
     C                   when      wkarhd = 0
     C                   if        wkshhd = 0
     C                   eval      shmscd = 'SC'
     C                   else
     C                   eval      shmscd = 'SH'
     C                   endif
      * Fully paid
     C                   when      wkarhd >= wkshhd
     C                   eval      shmscd = 'FP'
      *
      * When the Movement (coming into this function) was NC=Not Completely Paid
      * but the user has entered FP=Fully Paid (ie: The user is forcing it to
      * Fully Paid.)
     C                   when      svmscd = 'NC' and c1mscd = 'FP'
     C                   eval      shmscd = 'FP'
     C                   other
      *
      * Not completely paid
     C                   eval      shmscd = 'NC'
     C                   endsl
      *
      *
      * Evaluate type of payment for this movement by reading all
      * check detail records for the movement.
      *
     C                   eval      paylivefl = *blank
     C                   eval      paycarcfl = *blank
      *
     C     c1mvsn        setll     hsl064c
      *
     C                   dou       *in94 = *on                                  Do pay type
     C     c1mvsn        reade     hsl064c                                94
     C                   if        *in94 = *off
      *
     C                   select
     C                   when      cdtpcd = 'L'
     C                   eval      paylivefl = yes
      *
     C                   when      cdtpcd = 'C'
     C                   eval      paycarcfl = yes
     C                   endsl
      *
     C                   endif
     C                   enddo                                                  Do pay type
      *
      * Set up Type of Payment field.
      *
     C                   select
     C                   when      paylivefl = yes and
     C                             paycarcfl = yes
     C                   eval      shtpcd = 'M'
      *
     C                   when      paylivefl = yes
     C                   eval      shtpcd = 'L'
      *
     C                   when      paycarcfl = yes
     C                   eval      shtpcd = 'C'
     C                   other
      *
     C                   eval      shtpcd = *blank
     C                   endsl
      *
      *
      * Update Sale Movement Header
      *
     C                   update    shrec
     C                   endif                                                  If header
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * EDIT INPUT FIELDS FOR SCREEN 1
      *---------------------------------------------------------------
      *
     C     $EDIT1        BEGSR
      *
     C                   MOVEL     YES           EDITFL
      *
      * EDIT THE CONTROL RECORD
      *
     C                   EXSR      $EDC1
      *
      * EDIT THE SUBFILE IF THERE ARE NO ERRORS IN THE CONTROL
      * RECORD. IF THERE ARE ERRORS IN THE CONTROL RECORD, YOU
      * NEED TO REWRITE ANY SUBFILE RECORDS WITH THE ERROR INDICATORS
      * OFF.
      *
     C                   if        error = 0 or error = warmsg
     C                   EXSR      $EDS1
     C                   ELSE
     C                   EXSR      $OFFIN
     C                   ENDIF
      *
      * IF THERE ARE NO ERRORS IN THE SUBFILE, THEN RE-EDIT THE
      * MOVEMENT STATUS IN THE CONTROL RECORD SINCE THE VALID VALUES
      * ARE AFFECTED BY THE SUBFILE ENTRIES MADE BY THE USER
      *
     C                   if        error = 0 or error = warmsg                  IF NO ERROR
     C                   EXSR      $MSCD
     C                   Z-ADD     1             HDPAG1
     C                   ENDIF                                                  IF NO ERROR
      *
     C                   MOVEL     SCRN1         ROUTNE
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * EDIT THE CONTROL RECORD INFO ON SCREEN 1
      *---------------------------------------------------------------
      *
     C     $EDC1         BEGSR
      *
      * Initialize date workfields.
      *
     C                   z-add     0             wkscdt
     C                   z-add     0             wksckldt
     C                   z-add     0             wkshdt
     C                   z-add     0             wkrcdt
      *
      *
      * MOVEMENT STATUS.
     C                   EXSR      $MSCD
      *
      * SCHEDULED SHIP DATE
      *
     C     C1MDY         IFNE      0
     C     C1MSCD        OREQ      'SC'
     C                   EXSR      $SCDT
     C                   ENDIF
      *
      * SALES TYPE
      *
      * IF THERE ARE ANY CHECKS AGAINST THIS MOVEMENT (IE: ANY
      * HEAD HAVE BEEN RECEIVED), THEN SALES TYPE IS PROTECTED. SO
      * ONLY EDIT SALES TYPE IF NO HEAD HAVE BEEN RECEIVED AGAINST
      * THIS SALES MOVEMENT.
      *
     C     C1LVHD        IFEQ      0                                            IF NO RECV'D
     C     C1DOHD        ANDEQ     0
      *
     C                   SELECT
     C     C1STCD        WHENEQ    *BLANK                                       WH NO TYPE
     C                   SETON                                        47
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS09001'     MGID(E)
     C                   ENDIF
     C                   OTHER
      *
     C     C1STCD        CHAIN     HSP066                             92
     C     *IN92         IFEQ      *ON                                          IF BAD TYPE
     C                   SETON                                        47
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS00635'     MGID(E)
     C                   CLEAR                   MGW
     C                   MOVEA     C1STCD        MGW(1)
     C                   MOVEA     MGW           MGDT(E)
     C                   ENDIF
     C                   ENDIF                                                  IF BAD TYPE
     C                   ENDSL
     C                   ENDIF                                                  IF NO RECV'D
      *
      * Scheduled kill date (if sales type is valid)
      *
     C                   if        *in47 = *off
     C                   exsr      $sckldt
     C                   endif
      *
      * Load type
      *
     C                   exsr      $ltcd
      *
      * SHIPPED DATE
      *
     C     C1SMDY        IFNE      0
     C     C1MSCD        OREQ      'SH'
     C                   EXSR      $SHDT
     C                   ENDIF
      *
      * RECEIVED DATE WILL DEFAULT TO SHIPPED DATE IF THE USER
      * DOES NOT ENTER A RECEIVED DATE.
      *
     C                   SELECT
     C     C1RMDY        WHENNE    0
     C                   EXSR      $RCDT
      *
     C     C1RMDY        WHENEQ    0
     C     C1SMDY        ANDNE     0
     C     *IN38         ANDEQ     *OFF
     C                   Z-ADD     C1SMDY        C1RMDY
     C                   Z-ADD     wkshdt        wkrcdt
     C                   ENDSL
      *
      * Validate:
      *   1) trucker
      *   2) farm site
      *   3) customer
     C                   EXSR      $TICD
     C                   EXSR      $FSCD
     C                   EXSR      $CVNO
      *
      * SCHEDULED HEAD TO SHIP
      *
     C     C1SCHD        IFLT      0
     C                   SETON                                        79
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS09000'     MGID(E)
     C                   ENDIF
     C                   ENDIF
      *
      * SHIPPED HEAD
      *
     C                   SELECT
     C     C1SHHD        WHENNE    0
     C     C1MSCD        ANDEQ     'SC'
     C                   SETON                                        36
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS00612'     MGID(E)
     C                   ENDIF
      *
     C     C1SHHD        WHENEQ    0
     C     C1MSCD        ANDEQ     'SH'
     C                   SETON                                        36
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS00621'     MGID(E)
     C                   ENDIF
      *
     C     C1SHHD        WHENLT    0
     C                   SETON                                        36
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS09000'     MGID(E)
     C                   ENDIF
     C                   ENDSL
      *
      * SHIPPED POUNDS
      *   1) MUST BE 0 WHEN STATUS IS 'SCHEDULED'
      *   2) CANNOT BE 0 WHEN STATUS IS 'SHIPPED'
      *   3) CAN NEVER BE NEGATIVE
      *   4) CANNOT BE LESS THAN THE 'SAVED' POUNDS THAT HAVE
      *      ALREADY BEEN DISTRIBUTED TO CLOSED GROUPS
      *   5) IF THE SHIPPED POUNDS ENTERED BY THE USER EQUAL THE
      *      'SAVED' POUNDS BUT THE SHIPPED HEAD ENTERED BY THE USER
      *      DOES NOT EQUAL THE 'SAVED' HEAD, THEN THE USER HAS
      *      INCREASED OR DECREASED HEAD WITHOUT CHANGING POUNDS
      *
      *
     C                   SELECT
     C     C1SHLB        WHENNE    0
     C     C1MSCD        ANDEQ     'SC'
     C                   SETON                                        37
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS00616'     MGID(E)
     C                   ENDIF
      *
     C     C1SHLB        WHENEQ    0
     C     C1MSCD        ANDEQ     'SH'
     C                   SETON                                        37
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS00620'     MGID(E)
     C                   ENDIF
      *
     C     C1SHLB        WHENLT    0
     C                   SETON                                        37
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS09000'     MGID(E)
     C                   ENDIF
      *
     C     C1SHLB        WHENLT    SVSHLB
     C                   SETON                                        37
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS00643'     MGID(E)
     C                   CLEAR                   MGW
     C                   MOVE      C1SHLB        XXLB
     C                   MOVEA     XXLB          MGW(1)
     C                   MOVE      SVSHLB        XXLB
     C                   MOVEA     XXLB          MGW(6)
     C                   MOVEA     MGW           MGDT(E)
     C                   ENDIF
      *
     C     C1SHLB        WHENEQ    SVSHLB
     C     C1SHHD        ANDNE     SVSHHD
     C                   SETON                                        37
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS00644'     MGID(E)
     C                   CLEAR                   MGW
     C                   MOVE      SVSHLB        XXLB
     C                   MOVEA     XXLB          MGW(1)
     C                   MOVE      C1SHLB        XXLB
     C                   MOVEA     XXLB          MGW(6)
     C                   MOVEA     MGW           MGDT(E)
     C                   ENDIF
      *
     C                   ENDSL
      *
      * Calculate average shipped pounds to show in control record as a visual
      * check for user that shipped pounds value is realistic.
      *
     c                   z-add     0             c1avshlb
     C                   if        *in37 = *off and c1shhd <> 0                 If OK so far
     C     c1shlb        div(h)    c1shhd        c1avshlb
      *
     C                   select
     C                   when      c1stcd = 'MRKTS' and c1avshlb > 350
     C                   seton                                        3537
     C                   if        error < maxmsg
     C                   add       1             error
     C                   add       1             warmsg
     C                   movel     'HS00359'     mgid(e)
     C                   endif
      *
     C                   when      c1stcd = 'CULLS' and c1avshlb > 800
     C                   seton                                        3537
     C                   if        error < maxmsg
     C                   add       1             error
     C                   add       1             warmsg
     C                   movel     'HS00360'     mgid(e)
     C                   endif
     C                   endsl
      *
     C                   endif                                                  If OK so far
      *
      * If there are no errors so far, make additional edits when:
      *   1) the Version is restricted
      *   2) the customer is Seaboard
      *   3) the sale type is a market sale
      *
     C                   if        error = 0 and
     C                             version = 'R' and
     C                             c1cvno = 360516 and
     C                             c1stcd = 'MRKTS'
     C                   exsr      $restricted
     C                   endif
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------------
      * Validations exclusive to the 'restricted' version of the function
      *----------------------------------------------------------------------
      *
      * These edits only apply when:
      *   1) it is the 'restricted' version
      *   2) the customer is Seaboard
      *   3) it is a market sale
      *
     C     $restricted   begsr
      *
      * If this is a Scheduled Movement, check to see if it is in the Tattoo Header.
      * If it is, then we will let the user Revise it.
      *
     C                   move      yes           editfl
      *
     C                   if        c1mscd='SC'
     C     c1mvsn        chain     pka1cpk2                           92
     C                   if        *in92 = *off
     C                   move      no            editfl
     C                   endif
     C                   endif
      *
      *
      * Check Delivery Tracking
      *
     C                   if        editfl = yes                                 If edit more
     C                   call      'HP279'
     C                   parm                    wksckldt
     C                   parm      *blank        xxokforhps
      *
     C                   if        xxokforhps = no
     C                   seton                                        324778
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS00885'     mgid(e)
     C                   clear                   mgw
     C                   movea     dfmode        mgw(1)
     C                   movea     c1stcd        mgw(7)
     C                   movel     c1cvno        xxalphcvno
     C                   movea     xxalphcvno    mgw(12)
     C                   movea     mgw           mgdt(e)
     C                   endif
     C                   endif
     C                   endif                                                  If edit more
      *
     C                   endsr
      /EJECT
      *----------------------------------------------------------------
      * VALIDATE MOVEMENT STATUS CODE
      *----------------------------------------------------------------
      *
     C     $MSCD         BEGSR
      *
     C     C1LVHD        ADD       C1DOHD        WKARHD
      *
     C                   SELECT                                                 MAIN SELECT
     C     C1MSCD        WHENEQ    *BLANK
     C                   SETON                                        78
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS09001'     MGID(E)
     C                   ENDIF
      *
     C                   when      c1mscd = 'PP'
     C                   seton                                        78
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS09006'     mgid(e)
     C                   endif
     C                   other
      *
     C     K2P077        CHAIN     HSP077                             92
     C     *IN92         IFEQ      *ON                                          IF BAD CODE
     C                   SETON                                        78
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS00676'     MGID(E)
     C                   CLEAR                   MGW
     C                   MOVEA     C1MSCD        MGW(1)
     C                   MOVEA     MGW           MGDT(E)
     C                   ENDIF
     C                   ELSE
      *
      * THE MOVEMENT STATUS CODE IS VALID, SO MAKE FURTHER EDITS.
      *
      * WHERE YOU ARE REVISING AND THERE ARE RECEIVED HEAD
      *
     C                   SELECT                                                 SELECT 2
     C     MODE          WHENEQ    REVISE
     C     WKARHD        ANDNE     0
      *
     C     C1SHHD        IFLE      WKARHD                                       IF FULL PAID
     C     C1MSCD        ANDNE     'FP'
     C                   SETON                                        78
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS00634'     MGID(E)
     C                   ENDIF
     C                   ELSE
      *
     C     C1MSCD        IFNE      'NC'                                         IF PARTIAL
     C     C1MSCD        ANDNE     'FP'
     C                   SETON                                        78
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS00632'     MGID(E)
     C                   ENDIF
     C                   ENDIF                                                  IF PARTIAL
     C                   ENDIF                                                  IF FULL PAID
      *
      * YOU ARE REVISING AND THERE ARE NO RECEIVED HEAD
      *
     C     MODE          WHENEQ    REVISE
     C     WKARHD        ANDEQ     0
      *
     C     C1MSCD        IFEQ      'NC'
     C     C1MSCD        OREQ      'FP'
     C                   SETON                                        78
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS00642'     MGID(E)
     C                   ENDIF
     C                   ENDIF
      *
      * YOU ARE CREATING A NEW SALES MOVEMENT
      *
     C     MODE          WHENEQ    CREATE
     C     C1MSCD        ANDNE     'SH'
     C     C1MSCD        ANDNE     'SC'
     C                   SETON                                        78
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS00598'     MGID(E)
     C                   ENDIF
     C                   ENDSL                                                  SELECT 2
      *
      * IF THE STATUS IS OK SO FAR, PERFORM THE FOLLOWING
      * EDIT REGARDLESS OF MODE.
      *
     C     *IN78         IFEQ      *OFF
     C     C1SHHD        ANDNE     0
     C     C1MSCD        ANDEQ     'SC'
     C                   SETON                                        78
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS00633'     MGID(E)
     C                   ENDIF
     C                   ENDIF
      *
     C                   ENDIF                                                  IF BAD CODE
     C                   ENDSL                                                  MAIN SELECT
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * VALIDATE CUSTOMER NUMBER
      *----------------------------------------------------------------
      *
     C     $CVNO         BEGSR
      *
      * VALIDATE THE CUSTOMER A/R NUMBER
      *
     C                   MOVE      *BLANK        C1CNAM
     C     C1CVNO        IFEQ      0                                            IF NO CUST
     C                   SETON                                        32
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS09001'     MGID(E)
     C                   ENDIF
     C                   ELSE
      *
     C                   Z-ADD     C1CVNO        INAJD
     C                   MOVEL(P)  'C'           INAT1
     C                   EXSR      $JDEV
      *
     C     JDEFL         IFEQ      YES
     C                   SETON                                        32
     C                   ELSE
     C                   MOVEL     OUALPH        C1CNAM
     C                   ENDIF
     C                   ENDIF                                                  IF NO CUST
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $FSCD - EDIT FARM SITE
      *----------------------------------------------------------------
      *
     C     $FSCD         BEGSR
      *
     C                   MOVE      *BLANK        C1FSNM
     C     C1FSCD        IFEQ      0                                            IF NO FARM
     C                   SETON                                        31
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS09001'     MGID(E)
     C                   ENDIF
     C                   ELSE
      *
      * CHECK THAT FARM SITE EXISTS IN THE FARM SITE MASTER FILE
      *
     C     C1FSCD        CHAIN     HSP018                             92
     C     *IN92         IFEQ      *OFF                                         IF GOOD FARM
     C                   MOVEL     FSFSNM        C1FSNM
     C                   ELSE
      *
      * INVALID FARM SITE
     C                   SETON                                        31
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS00131'     MGID(E)
     C                   CLEAR                   MGW
     C                   MOVE      C1FSCD        xxalphfscd
     C                   MOVEA     xxalphfscd    MGW(1)
     C                   MOVEA     MGW           MGDT(E)
     C                   ENDIF
     C                   ENDIF                                                  IF GOOD FARM
     C                   ENDIF                                                  IF NO FARM
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $SCDT - EDIT SCHEDULED SHIP DATE
      *----------------------------------------------------------------
      *
     C     $SCDT         BEGSR
      *
     C                   SELECT
     C     C1MDY         WHENEQ    0
     C                   SETON                                        76
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVE      'HS00607'     MGID(E)
     C                   ENDIF
     C                   OTHER
      *
      * VALIDATE THAT THE DATE IS VALID AND ADD CENTURY VALUES
      * TO THE WORK FIELD FOR LATER USE IN THE UPDATING ROUTINE.
      *
     C                   Z-ADD     C1MDY         PFRM8
     C                   EXSR      $DATE
     C     PRTRN         IFNE      *BLANK                                       IF INVALID
     C                   SETON                                        76
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVE      'HS09004'     MGID(E)
     C                   ENDIF
     C                   ELSE
     C                   Z-ADD     PTO8          WKSCDT
     C                   ENDIF                                                  IF INVALID
      *
      * IF THE DATE IS VALID,
      *     CHECK THAT IT IS NOT IN A CLOSED PERIOD
      *     IF IT IS IN THE CURRENT PERIOD,
      *         CHECK THAT THE PERIOD CLOSE PROGRAMS ARE NOT RUNNING
      *
     C     *IN76         IFEQ      *OFF                                         IF GOOD DATE
      *
     C                   SELECT
     C     WKSCDT        WHENLT    DABPDT
     C                   SETON                                        76
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS09007'     MGID(E)
     C                   ENDIF
      *
     C     WKSCDT        WHENGE    DABPDT
     C     WKSCDT        ANDLE     DAEPDT
     C     DAPPFL        ANDNE     *BLANK
     C                   SETON                                        76
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS09016'     MGID(E)
     C                   ENDIF
     C                   ENDSL
      *
     C                   ENDIF                                                  IF GOOD DATE
     C                   ENDSL
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $sckldt - Edit scheduled kill date
      *----------------------------------------------------------------
      *
      * Validations:
      *  1) date must be blank if the sale is not a 'market' sale
      *  2) date is required for market sales when the customer is Guymon
      *  3) date is optional for market sales for customers other than Guymon
      *  4) if entered, date must be valid and
      *       a) cannot be in a closed period
      *       b) cannot be earlier than scheduled ship date
      *
      *
     C     $sckldt       begsr
      *
      *
      * Date must be blank if the sale is not a 'market' sale
      *
     C                   select
     C                   when      c1stcd <> 'MRKTS' and
     C                             c1scklmdy <> 0
     C                   seton                                        75
     C                   if        error < maxmsg
     C                   add       1             error
     C                   move      'HS00760'     mgid(e)
     C                   endif
      *
      * Date is required for Guymon 'market' sales
      *
     C                   when      c1stcd = 'MRKTS' and
     C                             c1scklmdy = 0 and
     C                             c1cvno = 360516
     C                   seton                                        75
     C                   if        error < maxmsg
     C                   add       1             error
     C                   move      'HS00761'     mgid(e)
     C                   endif
      *
      * Validate that the date is valid and add century values to
      * the workfield for later use in the updating routine.
      *
     C                   when      c1scklmdy <> 0
     C                   z-add     c1scklmdy     pfrm8
     C                   exsr      $date
     C                   if        prtrn <> *blank                              If invalid
     C                   seton                                        75
     C                   if        error < maxmsg
     C                   add       1             error
     C                   move      'HS09004'     mgid(e)
     C                   endif
     C                   else
     C                   z-add     pto8          wksckldt
      *
      * If the date is valid,
      *   1) if scheduled ship date was valid, check that scheduled
      *      kill date is not prior to scheduled ship date.
      *   2) check that it is not in a closed period
      *   3) If it is in the current period,
      *      check that the period close programs are not running
      *
      *
     C                   select
     C                   when      *in76 = *off and
     C                             wksckldt < wkscdt
     C                   seton                                        75
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS00762'     mgid(e)
     C                   endif
      *
     C                   when      wksckldt < dabpdt
     C                   seton                                        75
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS09007'     mgid(e)
     C                   endif
      *
     C                   when      wksckldt >= dabpdt and
     C                             wksckldt <= daepdt and
     C                             dappfl <> *blank
     C                   seton                                        75
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS09016'     mgid(e)
     C                   endif
     C                   endsl
      *
     C                   endif                                                  If invalid
     C                   endsl
      *
     C                   endsr
      /EJECT
      *----------------------------------------------------------------
      * Validate Load Type
      *----------------------------------------------------------------
      *
      * Validations:
      *   1) optional
      *   2) must be valid in load type master
      *
     C     $ltcd         begsr
      *
     C                   eval      c1ltcd = %triml(c1ltcd)
      *
     C                   select
     C                   when      c1ltcd = *blank
     C                   seton                                        30
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS09001'     mgid(e)
     C                   endif
     C                   other
      *
     C     c1ltcd        chain     hsp067                             92
     C                   if        *in92 = *on                                  If no hit
     C                   seton                                        30
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS00188'     mgid(e)
     C                   clear                   mgw
     C                   movea     c1ltcd        mgw(1)
     C                   movea     mgw           mgdt(e)
     C                   endif
     C                   endif                                                  If no hit
      *
     C                   endsl
      *
     C                   endsr
      /EJECT
      *----------------------------------------------------------------
      * $SHDT - EDIT SHIPPED DATE
      *----------------------------------------------------------------
      *
     C     $SHDT         BEGSR
      *
     C                   SELECT
     C     C1SMDY        WHENEQ    0
     C     C1MSCD        ANDEQ     'SH'
     C                   SETON                                        38
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVE      'HS00599'     MGID(E)
     C                   ENDIF
      *
     C     C1SMDY        WHENNE    0
     C     C1MSCD        ANDEQ     'SC'
     C                   SETON                                        38
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVE      'HS00600'     MGID(E)
     C                   ENDIF
     C                   OTHER
      *
      * VALIDATE THAT THE SHIPPED DATE IS VALID AND ADD CENTURY VALUES
      * TO THE WORK FIELD FOR LATER USE IN THE UPDATING ROUTINE.
      *
     C                   Z-ADD     C1SMDY        PFRM8
     C                   EXSR      $DATE
     C     PRTRN         IFNE      *BLANK                                       IF INVALID
     C                   SETON                                        38
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVE      'HS09004'     MGID(E)
     C                   ENDIF
     C                   ELSE
     C                   Z-ADD     PTO8          WKSHDT
     C                   ENDIF                                                  IF INVALID
      *
      * IF THE DATE IS VALID,
      *     CHECK THAT IT IS NOT GREATER THAN TODAY'S DATE
      *     CHECK THAT IT IS NOT IN A CLOSED PERIOD
      *     IF IT IS IN THE CURRENT PERIOD,
      *         CHECK THAT THE PERIOD CLOSE PROGRAMS ARE NOT RUNNING
      *
     C     *IN38         IFEQ      *OFF                                         IF GOOD DATE
      *
     C                   SELECT
     C     WKSHDT        WHENGT    WKUDAT
     C                   SETON                                        38
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS00426'     MGID(E)
     C                   ENDIF
      *
     C     WKSHDT        WHENLT    DABPDT
     C                   SETON                                        38
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS09007'     MGID(E)
     C                   ENDIF
      *
     C     WKSHDT        WHENGE    DABPDT
     C     WKSHDT        ANDLE     DAEPDT
     C     DAPPFL        ANDNE     *BLANK
     C                   SETON                                        38
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS09016'     MGID(E)
     C                   ENDIF
     C                   ENDSL
      *
      * THE SHIPPED DATE CANNOT BE AFTER THE CHECK DATE OF ANY
      * CHECKS THAT MAY HAVE BEEN ENTERED AGAINST THIS MOVEMENT.
      * (ONLY PERFORM THIS EDIT IF THE USER IS REVISING A MOVEMENT.)
      *
     C     MODE          IFNE      CREATE                                       IF NOT CREAT
     C     C1MVSN        SETLL     HSL064C
     C                   Z-ADD     0             SVCVNO
     C                   Z-ADD     0             SVCHDT
     C                   Z-ADD     0             SVCHNO
      *
     C     *IN93         DOUEQ     *ON                                          DO CHECKS
     C     C1MVSN        READE     HSL064C                                93
     C     *IN93         IFEQ      *OFF                                         IF NOT EOF
     C     SVCVNO        IFNE      CDCVNO                                       IF DIFF
     C     SVCHNO        ORNE      CDCHNO
     C     SVCHDT        ORNE      CDCHDT
      *
     C     WKSHDT        IFGT      CDCHDT                                       IF LATER
     C                   SETON                                        38
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS00701'     MGID(E)
     C                   CLEAR                   MGW
      * FLIP CHECK DATE
     C                   Z-ADD     CDCHDT        WKCYMD
     C                   EXSR      $TOMDY
     C                   MOVEL     WKMDY         XXMDY
     C                   MOVEA     XXMDY         MGW(1)
     C                   MOVEL     CDCVNO        XXCVNO
     C                   MOVEA     XXCVNO        MGW(7)
     C                   MOVEL     CDCHNO        XXCHNO
     C                   MOVEA     XXCHNO        MGW(15)
     C                   MOVEA     MGW           MGDT(E)
     C                   ENDIF
     C                   ENDIF                                                  IF LATER
      *
     C                   Z-ADD     CDCVNO        SVCVNO
     C                   Z-ADD     CDCHNO        SVCHNO
     C                   Z-ADD     CDCHDT        SVCHDT
     C                   ENDIF                                                  IF DIFF
     C                   ENDIF                                                  IF NOT EOF
     C                   ENDDO                                                  DO CHECKS
     C                   ENDIF                                                  IF NOT CREAT
      *
     C                   ENDIF                                                  IF GOOD DATE
     C                   ENDSL
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $RCDT - EDIT RECEIVED DATE
      *----------------------------------------------------------------
      *
     C     $RCDT         BEGSR
      *
      * RECEIVED DATE MUST BE BLANK IF THE STATUS IS SCHEDULED
      *
     C                   SELECT
     C     C1RMDY        WHENNE    0
     C     C1MSCD        ANDEQ     'SC'
     C                   SETON                                        39
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVE      'HS00606'     MGID(E)
     C                   ENDIF
     C                   OTHER
      *
      * VALIDATE THAT THE RECEIVED DATE IS VALID AND ADD CENTURY VALUES
      * TO THE WORK FIELD FOR LATER USE IN THE UPDATING ROUTINE.
      *
     C                   Z-ADD     C1RMDY        PFRM8
     C                   EXSR      $DATE
     C     PRTRN         IFNE      *BLANK                                       IF INVALID
     C                   SETON                                        39
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVE      'HS09004'     MGID(E)
     C                   ENDIF
     C                   ELSE
     C                   Z-ADD     PTO8          WKRCDT
     C                   ENDIF                                                  IF INVALID
      *
      * IF THE DATE IS VALID,
      *     CHECK THAT IT IS NOT LESS THAN THE SHIPPED DATE
      *     CHECK THAT IT IS NOT IN A CLOSED PERIOD
      *     IF IT IS IN THE CURRENT PERIOD,
      *         CHECK THAT THE PERIOD CLOSE PROGRAMS ARE NOT RUNNING
      *     VERIFY THAT NO CHECKS HAVE BEEN ENTERED AGAINST THIS
      *         MOVEMENT WITH A CHECK DATE PRIOR TO THE RECEIVE DATE
      *
      * (NOTE: THIS LOGIC ALLOWS FOR A FUTURE RECEIVED DATE)
      *
     C     *IN39         IFEQ      *OFF                                         IF GOOD DATE
      *
     C                   SELECT
     C     WKRCDT        WHENLT    WKSHDT
     C     *IN38         ANDEQ     *OFF
     C                   SETON                                        39
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS09015'     MGID(E)
     C                   ENDIF
      *
     C     WKRCDT        WHENLT    DABPDT
     C                   SETON                                        39
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS09007'     MGID(E)
     C                   ENDIF
      *
     C     WKRCDT        WHENGE    DABPDT
     C     WKRCDT        ANDLE     DAEPDT
     C     DAPPFL        ANDNE     *BLANK
     C                   SETON                                        39
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS09016'     MGID(E)
     C                   ENDIF
     C                   ENDSL
      *
      * THE RECEIVED DATE CANNOT BE AFTER THE CHECK DATE OF ANY
      * CHECKS THAT MAY HAVE BEEN ENTERED AGAINST THIS MOVEMENT.
      * (ONLY PERFORM THIS EDIT IF THE USER IS REVISING A MOVEMENT.)
      *
     C     MODE          IFNE      CREATE                                       IF NOT CREAT
     C     C1MVSN        SETLL     HSL064C
     C                   Z-ADD     0             SVCVNO
     C                   Z-ADD     0             SVCHDT
     C                   Z-ADD     0             SVCHNO
      *
     C     *IN93         DOUEQ     *ON                                          DO CHECKS
     C     C1MVSN        READE     HSL064C                                93
     C     *IN93         IFEQ      *OFF                                         IF NOT EOF
     C     SVCVNO        IFNE      CDCVNO                                       IF DIFF
     C     SVCHNO        ORNE      CDCHNO
     C     SVCHDT        ORNE      CDCHDT
      *
     C     WKRCDT        IFGT      CDCHDT                                       IF LATER
     C                   SETON                                        39
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS00702'     MGID(E)
     C                   CLEAR                   MGW
      * FLIP CHECK DATE
     C                   Z-ADD     CDCHDT        WKCYMD
     C                   EXSR      $TOMDY
     C                   MOVEL     WKMDY         XXMDY
     C                   MOVEA     XXMDY         MGW(1)
     C                   MOVEL     CDCVNO        XXCVNO
     C                   MOVEA     XXCVNO        MGW(7)
     C                   MOVEL     CDCHNO        XXCHNO
     C                   MOVEA     XXCHNO        MGW(15)
     C                   MOVEA     MGW           MGDT(E)
     C                   ENDIF
     C                   ENDIF                                                  IF LATER
      *
     C                   Z-ADD     CDCVNO        SVCVNO
     C                   Z-ADD     CDCHNO        SVCHNO
     C                   Z-ADD     CDCHDT        SVCHDT
     C                   ENDIF                                                  IF DIFF
     C                   ENDIF                                                  IF NOT EOF
     C                   ENDDO                                                  DO CHECKS
     C                   ENDIF                                                  IF NOT CREAT
      *
     C                   ENDIF                                                  IF GOOD DATE
     C                   ENDSL
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * Validate Trucker
      *---------------------------------------------------------------
      *
      * Validations:
      *   1) must exist in Trucker master file
      *   2) must be 'active' when movement status is 'scheduled'
      *
     C     $ticd         begsr
      *
     C                   select
     C                   when      c1ticd = *blank
     C                   other
      *
     C     C1TICD        CHAIN     HSP046                             92
     C     *IN92         IFEQ      *ON                                          IF BAD
     C                   SETON                                        33
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS00235'     MGID(E)
     C                   CLEAR                   MGW
     C                   MOVEA     C1TICD        MGW(1)
     C                   MOVEA     MGW           MGDT(E)
     C                   ENDIF
     C                   ELSE
      *
     C                   if        tiaist <> 'A' and c1mscd = 'SC'              If not active
     C                   seton                                        33
     C                   if        error < maxmsg
     C                   add       1             error
     C                   exsr      $err1
     C                   eval      mgid(e) = 'HS00231'
     C                   clear                   mgw
     C                   movea     c1ticd        mgw(1)
     C                   movea     mgw           mgdt(e)
     C                   endif
     C                   endif                                                  If not active
     C                   ENDIF                                                  IF BAD
     C                   endsl
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * $CLR1 - SUBROUTINE TO CLEAR THE SUBFILE ON SCREEN 1
      *---------------------------------------------------------------
      *
     C     $CLR1         BEGSR
      *
      * CLEAR CONTROL RECORD ACCUMULATORS
      *
     C                   Z-ADD     0             C1SCHD
     C                   Z-ADD     0             C1SHHD
     C                   Z-ADD     0             C1SHLB
     C                   Z-ADD     0             C1AVSHLB
     C                   Z-ADD     0             C1LVHD
     C                   Z-ADD     0             C1DOHD
      *
      * CLEAR WORKFIELD ACCUMULATORS
      *
     C                   Z-ADD     0             WKSCHD
     C                   Z-ADD     0             WKSHHD
      *
     C                   Z-ADD     0             HDRRN1
     C                   Z-ADD     0             COUNT
      *
      * CLEAR ACCUMULATORS THAT HOLD TOTALS FOR A MOVEMENT
      * ASSOCIATED WITH PROTECTED SUBFILE LINES.
      *
     C                   MOVEL     NO            CLOSFL
     C                   Z-ADD     0             SVSHHD
     C                   Z-ADD     0             SVSHLB
      *
     C                   CLEAR                   HP146S1
     C                   SETON                                        83        SFLCLR
     C                   WRITE     HP146C1
     C                   SETOFF                                       818283
      *                                                    81=SFLDSP
      *                                                    82=SFLEND
      *                                                    83=SFLCLR
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * $FRSH1 - SUBROUTINE TO REFRESH/FILL THE SUBFILE ON SCREEN 1
      *---------------------------------------------------------------
      *
     C     $FRSH1        BEGSR
      *
      * SET INDICATORS TO CONTROL/PROTECT SCREEN FIELDS
      * IN SUBFILE AND ON CONTROL RECORD.
      *
     C                   EXSR      $SFIND
     C                   EXSR      $C1IND
      *
      * LOADING THE SUBFILE IN THIS PROGRAM IS DIFFERENT THAN IN THE
      * SHELL PROGRAMS AND DEPENDS ON THE 'MODE'.
      * FOR CREATE MODE, WE LOAD A PAGE OF BLANK LINES.
      * FOR REVISE MODE, WE LOAD A PAGE OF DATABASE RECORDS AND, IF WE
      *                  HIT EOF, WE FINISH THE PAGE WITH BLANK LINES.
      *
     C                   SELECT
     C     MODE          WHENEQ    CREATE
     C                   EXSR      $SETOF
     C                   EXSR      $CLRLN
     C                   DO        WKPLIN
     C                   EXSR      $BLANK
     C                   ENDDO
      *
     C     MODE          WHENEQ    REVISE
     C     C1MVSN        SETLL     HSP085
     C     *IN82         DOUEQ     *ON
     C                   EXSR      $LOAD1
     C                   ENDDO
      *
      * FILL UP THE SUBFILE PAGE WITH BLANK LINES IF NECESSARY
      *
     C     WKPLIN        IFNE      0
     C     WKPLIN        ANDNE     COUNT
     C     COUNT         DIV       WKPLIN        WKFLD
     C                   MVR                     WKRMDR
     C     WKPLIN        SUB       WKRMDR        WKDIFF
     C                   EXSR      $SETOF
     C                   EXSR      $CLRLN
     C                   DO        WKDIFF
     C                   EXSR      $BLANK
     C                   ENDDO
     C                   ENDIF
      *
     C                   ENDSL
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * $HDR - SETUP THE SALES HEADER DATA FOR THE CONTROL RECORD
      *---------------------------------------------------------------
      *
     C     $HDR          BEGSR
      *
     C                   MOVEL     SHMSCD        C1MSCD
     C                   MOVEL     SHMSCD        SVMSCD
     C                   MOVEL     SHTPCD        HDTPCD
     C                   MOVEL     SHSTCD        C1STCD
     C                   MOVEL     SHLTCD        C1LTCD
     C                   Z-ADD     SHFSCD        C1FSCD
     C                   Z-ADD     SHCVNO        C1CVNO
     C                   MOVEL     SHTICD        C1TICD
      *
      * RETRIEVE FARM SITE NAME
      *
     C     C1FSCD        CHAIN     HSP018                             92
     C     *IN92         IFEQ      *OFF
     C                   MOVEL     FSFSNM        C1FSNM
     C                   ELSE
     C                   MOVEL(P)  'UNKNOWN'     C1FSNM
     C                   ENDIF
      *
      * RETRIEVE JDE CUSTOMER NAME
      *
     C     SHCVNO        IFNE      0
     C                   Z-ADD     SHCVNO        INAJD
     C                   MOVEL(P)  'C'           INAT1
     C                   EXSR      $JDEV
     C                   MOVEL     OUALPH        C1CNAM
     C                   ENDIF
      *
      * FLIP SCHEDULED SHIP DATE TO MDY FORMAT
      *
     C                   Z-ADD     SHSCDT        WKCYMD
     C                   EXSR      $TOMDY
     C                   Z-ADD     WKMDY         C1MDY
      *
      * FLIP SCHEDULED KILL DATE TO MDY FORMAT
      *
     C                   Z-ADD     SHSCKLDT      WKCYMD
     C                   EXSR      $TOMDY
     C                   Z-ADD     WKMDY         C1SCKLMDY
      *
      * FLIP SHIPPED DATE TO MDY FORMAT
      *
     C                   Z-ADD     SHSHDT        WKCYMD
     C                   EXSR      $TOMDY
     C                   Z-ADD     WKMDY         C1SMDY
      *
      * FLIP RECEIVED DATE TO MDY FORMAT
      *
     C                   Z-ADD     SHRCDT        WKCYMD
     C                   EXSR      $TOMDY
     C                   Z-ADD     WKMDY         C1RMDY
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * $LOAD1- SUBROUTINE TO LOAD THE SUBFILE ON SCREEN 1
      *---------------------------------------------------------------
      *
     C     $LOAD1        BEGSR
      *
      *  RETRIEVE SALE DETAIL RECORDS FOR THE SALES MOVEMENT.
      *
     C     C1MVSN        READE(N)  HSP085                                 82
     C     *IN82         IFEQ      *OFF                                         IF NOT EOF
     C                   SETON                                        85
      *
      * CLEAR FIELDS IN SUBFILE LINE
      *
     C                   EXSR      $CLRLN
      *
      * ACCUMULATE HEAD VALUES FOR CONTROL RECORD
      *
     C                   ADD       SGSCHD        C1SCHD
     C                   ADD       SGSHHD        C1SHHD
     C                   ADD       SGLVHD        C1LVHD
     C                   ADD       SGDOHD        C1DOHD
      *
      * ACCUMULATE POUNDS VALUES FOR CONTROL RECORD
      *
     C                   ADD       SGSHLB        C1SHLB
      *
      *  SET UP HIDDEN SUBFILE LINE FIELDS
      *
     C                   Z-ADD     SGHGSN        HDHGSN
      *
      *  SET UP SUBFILE LINE FIELDS
      *
     C                   Z-ADD     SGSCHD        DFSCHD
     C                   Z-ADD     SGSHHD        DFSHHD
     C                   Z-ADD     SGLVHD        DFLVHD
     C                   Z-ADD     SGDOHD        DFDOHD
      *
      * RETRIEVE ALPHA HOG GROUP CODES AND GROUP STATUS
      *
     C                   EXSR      $GRPCD
      *
      * RESET INDICATORS
     C                   EXSR      $SFIND
      *
     C                   ADD       1             COUNT
     C                   ADD       1             HDRRN1
      *
     C                   WRITE     HP146S1
     C                   SETON                                        81
     C                   Z-ADD     HDRRN1        HDPAG1
     C                   ENDIF                                                  IF NOT EOF
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $GRPCD - RETRIEVE THE USER-ASSIGNED ALPHA HOG GROUP CODE
      *----------------------------------------------------------------
      *
     C     $GRPCD        BEGSR
      *
     C     SGHGSN        CHAIN     HSL034D                            92
     C     *IN92         IFEQ      *ON
     C                   MOVEL     'UNKNOWN'     DFHGCD
     C                   ELSE
     C                   MOVEL     HGBLCD        DFBLCD
     C                   MOVEL     HGRMCD        DFRMCD
     C                   MOVEL     HGHGCD        DFHGCD
     C                   MOVEL     HGGSCD        DFGSCD
     C                   MOVEL     HGPTCD        DFPTCD
     C                   MOVEL     HGPPCD        DFPPCD
     C                   MOVEL     HGGSCD        HDGSCD
     C                   ENDIF
      *
      * IF THE HOG GROUP IS CLOSED, MOVE THE
      * SHIPPED POUNDS TO A HIDDEN SUBFILE FIELD AND ALSO
      * ACCUMULATE POUNDS AND HEAD FOR LATER POUNDS DISTRIBUTION
      * LOGIC.
      *
     C     HDGSCD        IFEQ      'CL'
     C                   Z-ADD     SGSHLB        HDSHLB
     C                   ADD       SGSHLB        SVSHLB
     C                   ADD       SGSHHD        SVSHHD
     C                   ENDIF
      *
      *
      * SET FLAGS FOR CONTROLLING PRODUCTION TYPE CONSISTENCY AND INDICATORS
      * IN THE CONTROL RECORD.
      *
     C                   if        c1mscd <> 'SC' and
     C                             (hdgscd = 'CL' or hdgscd = 'DS')
     C                   MOVEL     YES           CLOSFL
     C                   ENDIF
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * $BLANK- SUBROUTINE TO LOAD BLANK SUBFILE RECORDS FOR SCREEN 1
      *---------------------------------------------------------------
      *
     C     $BLANK        BEGSR
      *
     C                   EXSR      $SFIND
      *
     C                   ADD       1             COUNT
     C                   ADD       1             HDRRN1
      *
     C                   WRITE     HP146S1
     C                   SETON                                        81
     C                   Z-ADD     HDRRN1        HDPAG1
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * $CLRLN - CLEAR SUBFILE LINE
      *---------------------------------------------------------------
      *
      * THIS SUBROUTINE INITIALIZES THE FIELDS IN THE SUBFILE RECORD
      *
     C     $CLRLN        BEGSR
      *
     C                   Z-ADD     0             HDHGSN
     C                   Z-ADD     0             HDSHLB
      *
     C                   MOVE      *BLANK        HDGSCD
      *
     C                   MOVE      *BLANK        DFBLCD
     C                   MOVE      *BLANK        DFRMCD
     C                   MOVE      *BLANK        DFHGCD
     C                   MOVE      *BLANK        DFGSCD
     C                   MOVE      *BLANK        DFPTCD
     C                   MOVE      *BLANK        DFPPCD
     C                   Z-ADD     0             DFSCHD
     C                   Z-ADD     0             DFSHHD
     C                   Z-ADD     0             DFLVHD
     C                   Z-ADD     0             DFDOHD
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * $OFFIN - REWRITE THE SUBFILE RECORDS WITH THE INDICATORS OFF
      *---------------------------------------------------------------
      *
      * THIS SUBROUTINE KEEPS THE CURSOR FROM BEING IN THE SUBFILE
      * WHILE THE DISPLAYED ERROR MESSAGE IS FOR THE CONTROL RECORD.
      *
     C     $OFFIN        BEGSR
      *
      * READ/PROCESS ALL THE CHANGED SUBFILE RECORDS
      *
     C                   SETOFF                                       80
     C     *IN80         DOUEQ     *ON                                          DO PROCESS
     C                   READC     HP146S1                                80
     C     *IN80         IFEQ      *OFF                                         IF REC READ
      *
      * SETON MDT
     C                   SETON                                        85
      *
      * SET OFF THE ERROR INDICATORS IN THE SUBFILE
      * RESET INDICATORS
      *
     C                   EXSR      $SETOF
     C                   EXSR      $SFIND
     C                   UPDATE    HP146S1
     C                   ENDIF                                                  IF REC READ
     C                   ENDDO                                                  DO PROCESS
      * SET MDT
     C                   SETOFF                                       85
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * READ AND EDIT CHANGED SUBFILE RECORDS FOR SCREEN 1
      *---------------------------------------------------------------
      *
     C     $EDS1         BEGSR
      *
      * INITIALIZE THE GROUP ARRAY AND INDEXES
      *
     C                   MOVE      *BLANK        GRP
     C                   Z-ADD     0             Y
     C                   Z-ADD     0             X
      *
      * CLEAR THE TOTAL ACCUMULATORS AT THE TOP OF THE SCREEN
      *
     C                   Z-ADD     0             WKSCHD
     C                   Z-ADD     0             WKSHHD
     C                   MOVEL     NO            WKEDIT
      *
      *
      * READ/PROCESS ALL THE CHANGED SUBFILE RECORDS
      *
     C                   SETOFF                                       80
     C     *IN80         DOUEQ     *ON                                          DO PROCESS
     C                   READC     HP146S1                                80
     C     *IN80         IFEQ      *OFF                                         IF REC READ
      *
      * SETON MDT
     C                   SETON                                        85
      *
      * SET OFF THE ERROR INDICATORS IN THE SUBFILE
      *
     C                   EXSR      $SETOF
      *
      * IF THERE ARE NO USER ENTRIES FOR THE CHANGED SUBFILE RECORD,
      * CLEAR THE OUTPUT-ONLY SUBFILE FIELDS
      *
     C     DFBLCD        IFEQ      *BLANK                                       IF ENTRY
     C     DFRMCD        ANDEQ     *BLANK
     C     DFHGCD        ANDEQ     *BLANK
     C     DFSCHD        ANDEQ     0
     C     DFSHHD        ANDEQ     0
     C                   MOVE      *BLANK        DFGSCD
     C                   MOVE      *BLANK        DFPTCD
     C                   MOVE      *BLANK        DFPPCD
     C                   ELSE
      *
      *
      * ACCUMULATE SCHEDULED AND SHIPPED HEAD INTO WORKFIELDS FOR
      * COMPARISON WITH THE TOTAL VALUES KEYED BY THE USER
      *
     C                   ADD       DFSCHD        WKSCHD
     C                   ADD       DFSHHD        WKSHHD
      *
     C                   MOVEL     YES           WKEDIT
      *
      * PERFORM VALIIDATIONS IF:
      *   a) the movement status is 'scheduled', or
      *   b) the hog group is not closed/disposed.
      *
     C                   if        c1mscd = 'SC' or                             If edit
     C                             (hdgscd <> 'CL' and hdgscd <> 'DS')
      *
      * If group is blank, clear output only group status.
      *
     C                   if        dfhgcd = *blank
     C                   move      *blank        dfgscd
     C                   move      *blank        dfptcd
     C                   move      *blank        dfppcd
     C                   endif
      *
      * The user must at least enter a building or a group. If neither are
      * entered, issue message; otherwise, validate and attempt to retrieve/
      * default in the missing values.
      *
     C
     C                   if        dfblcd = *blank and                          If no bldg/group
     C                             dfhgcd = *blank
     C                   seton                                        4944
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS09132'     mgid(e)
     C                   exsr      $err1
     C                   endif
     C                   else
      * Validate:
      *  1) Building
      *  2) Room
      *  3) Hog group
      *
     C                   exsr      $blcd
      *
     C                   if        dfrmcd <> *blank
     C                   exsr      $rmcd
     C                   endif
      *
     C                   exsr      $hgcd
     C                   endif                                                  If no bldg/group
      *
      * VALIDATE SCHEDULED HEAD
      *
     C                   exsr      $schd
      *
      *
      * VALIDATE SHIPPED HEAD
      *
     C                   SELECT
     C     DFSHHD        WHENNE    0
     C     C1MSCD        ANDEQ     'SC'
     C                   SETON                                        71
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS00612'     MGID(E)
     C                   EXSR      $ERR1
     C                   ENDIF
      *
     C     DFSHHD        WHENLT    0
     C                   SETON                                        71
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS09000'     MGID(E)
     C                   EXSR      $ERR1
     C                   ENDIF
      *
     C     DFSHHD        WHENEQ    0
     C     C1MSCD        ANDNE     'SC'
     C                   SETON                                        71
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS00630'     MGID(E)
     C                   EXSR      $ERR1
     C                   ENDIF
     C                   ENDSL
     C                   ENDIF                                                  If edit
     C                   ENDIF                                                  IF ENTRY
      *
      * RESET INDICATORS
     C                   EXSR      $SFIND
     C                   UPDATE    HP146S1
     C                   EXSR      $SETOF
     C                   ENDIF                                                  IF REC READ
     C                   ENDDO                                                  DO PROCESS
      * SET MDT
     C                   SETOFF                                       85
      *
      *---------------------------------------------------------------
      * AFTER ALL SUBFILE RECORDS HAVE BEEN EDITED,
      * ISSUE AN ERROR MESSAGE IF THERE ARE NO SUBFILE ENTRIES
      *---------------------------------------------------------------
      *
     C     WKEDIT        IFEQ      NO                                           IF NO ENTRY
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS00593'     MGID(E)
     C                   ENDIF
     C                   ENDIF                                                  IF NO ENTRY
      *
      * IF THERE ARE NO ERRORS ON THE SUBFILE, CHECK THAT THE
      * ACCUMULATED HEAD VALUES MATCH THE USER-KEYED VALUES.
      *
     C                   if        error = 0 or error = warmsg                  IF NO ERROR
      *
     C     C1SCHD        IFNE      WKSCHD                                       IF NE SCHED
     C                   SETON                                        79
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS00624'     MGID(E)
     C                   CLEAR                   MGW
     C                   MOVE      C1SCHD        XXHD
     C                   MOVEA     XXHD          MGW(1)
     C                   MOVE      WKSCHD        XXHD6
     C                   MOVEA     XXHD6         MGW(6)
     C                   MOVEA     MGW           MGDT(E)
     C                   ENDIF
     C                   ENDIF                                                  IF NE SCHED
      *
     C     C1SHHD        IFNE      WKSHHD                                       IF NE SHIP
     C                   SETON                                        36
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS00625'     MGID(E)
     C                   CLEAR                   MGW
     C                   MOVE      C1SHHD        XXHD
     C                   MOVEA     XXHD          MGW(1)
     C                   MOVE      WKSHHD        XXHD6
     C                   MOVEA     XXHD6         MGW(6)
     C                   MOVEA     MGW           MGDT(E)
     C                   ENDIF
     C                   ENDIF                                                  IF NE SHIP
     C                   ENDIF                                                  IF NO ERROR
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * Validate building
      *----------------------------------------------------------------
      *
     C     $blcd         begsr
      *
      * If building is blank and hog group is entered, default building and
      * room from hog group.
      *
     C                   if        dfblcd = *blank and dfhgcd <> *blank
     C     dfhgcd        chain     hsp034                             92
     C                   if        *in92 = *off
     C                   move      hgblcd        dfblcd
     C                   move      hgrmcd        dfrmcd
     C                   endif
     C                   endif
      *
      * Validations:
      *  1) building is required
      *  2) building must exist on the specified farm site
      *
     C                   select
     C                   when      dfblcd = *blank
     C                   seton                                        49
     C                   if        error < maxmsg
     C                   add       1             error
     C                   exsr      $err1
     C                   eval      mgid(e) = 'HS09001'
     C                   endif
     C                   other
      *
     C     key01         chain     hsp019                             92
     C                   if        *in92 = *on                                  If not found
     C                   seton                                        49
     C                   if        error < maxmsg
     C                   add       1             error
     C                   exsr      $err1
     C                   eval      mgid(e) = 'HS00116'
     C                   clear                   mgw
     C                   movea     dfblcd        mgw(1)
     C                   move      c1fscd        xxalphfscd
     C                   movea     xxalphfscd    mgw(6)
     C                   movea     mgw           mgdt(e)
     C                   endif
     C                   endif                                                  If not found
     C                   endsl
      *
      * If building is valid and group is blank, attempt to default in a group
      *
     C                   if        *in49 = *off and dfhgcd = *blank             If valid building
      *
     C                   if        dfrmcd <> *blank
     C                   exsr      $rmcd
     C                   endif
      *
     C                   if        *in49 = *off
     C                   exsr      $group
     C                   endif
     C                   endif                                                  If valid building
      *
     C                   endsr
      /EJECT
      *----------------------------------------------------------------
      * $rmcd - validate room
      *----------------------------------------------------------------
      *
      * Validations:
      *  1) required
      *  2) must exist in the building on the farm site
      *  3) must have an active status
      *
     C     $rmcd         begsr
      *
     C                   select
     C                   when      dfrmcd = *blank
     C                   seton                                        46
     C                   if        error < maxmsg
     C                   add       1             error
     C                   exsr      $err1
     C                   eval      mgid(e) = 'HS09001'
     C                   endif
     C                   other
      *
      * Check that the room exists in the building on the farm site
     *
     C     key02         chain     hsp020                             92
     C                   if        *in92 = *on                                  If no hit
     C                   seton                                        46
     C                   if        error < maxmsg
     C                   add       1             error
     C                   exsr      $err1
     C                   eval      mgid(e) = 'HS00117'
     C                   clear                   mgw
     C                   movea     dfrmcd        mgw(1)
     C                   movea     dfblcd        mgw(6)
     C                   move      c1fscd        xxalphfscd
     C                   movea     xxalphfscd    mgw(11)
     C                   movea     mgw           mgdt(e)
     C                   endif
     C                   else
     *
      * Room must have an 'active' status
     *
     C                   if        brrmst <> 'A'                                If not active
     C                   seton                                        46
     C                   if        error < maxmsg
     C                   add       1             error
     C                   exsr      $err1
     C                   eval      mgid(e) = 'HS00726'
     C                   clear                   mgw
     C                   movea     dfrmcd        mgw(1)
     C                   movea     mgw           mgdt(e)
     C                   endif
     C                   endif                                                  If not active
     C                   endif                                                  If no hit
      *
     C                   endsl
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Attempt to find a default group in a building
      *---------------------------------------------------------------
      *
      * If only one group is open in that building or building/room, default
      * it in. But, if multiple groups are open, issue a message to the
      * user to select a group.
      *
     C     $group        begsr
      *
      * If you find an open group, repeat the 'find' to see if there is a
      * 'second' open group.
      *
     C                   exsr      $findone
     C                   if        *in92 = *off                                 If open group
     C                   exsr      $findtwo
      *
     C                   if        *in92 = *off                                 If mult group
     C                   seton                                        44
     C                   if        error < maxmsg
     C                   add       1             error
     C                   clear                   mgw
     C                   movea     dfblcd        mgw(1)
      *
     C                   select
     C                   when      dfrmcd = *blank
     C                   movel     'HS00721'     mgid(e)
     C                   other
     C                   movel     'HS00765'     mgid(e)
     C                   movea     dfrmcd        mgw(6)
     C                   endsl
      *
     C                   movea     mgw           mgdt(e)
     C                   exsr      $err1
     C                   endif
     C                   else
      *
      *
      * Only one group is open; default in its values---unless this is a
      * Market Sale/Nursery Group combination--which we no longer allow.
      *
     C                   if        c1stcd = 'MRKTS' and hgppcd = 'NUR  '        If MRKTS/NUR
     C                   else
     C                   movel     hghgcd        dfhgcd
     C                   movel     hgrmcd        dfrmcd
     C                   endif                                                  If MRKTS/NUR
      *
     C                   endif                                                  If mult group
     C                   else
      *
      * No groups are open in this building/room
      *
     C                   seton                                        44
     C                   if        error < maxmsg
     C                   add       1             error
     C                   clear                   mgw
     C                   move      c1fscd        xxalphfscd
     C                   movea     xxalphfscd    mgw(1)
     C                   movea     dfblcd        mgw(6)
      *
     C                   select
     C                   when      dfrmcd = *blank
     C                   movel     'HS00722'     mgid(e)
     C                   other
     C                   movel     'HS00766'     mgid(e)
     C                   movea     dfrmcd        mgw(11)
     C                   endsl
      *
     C                   movea     mgw           mgdt(e)
     C                   exsr      $err1
     C                   endif
     C                   endif                                                  If open group
      *
     C                   endsr
      /eject
      *----------------------------------------------------------------
      * Find the first open group
      *----------------------------------------------------------------
      *
      * Chain with farm/building or farm/building/room using:
      *   KEY 1: farm/building when room is blank
      *   KEY 2: farm/building/room when room is populated.
      *
      * The file you chain to depends on the Type of Sale value:
      *   MRKTS:        chain to a file that has only BGF and GF groups---no Nursery groups
      *   FPS or CULLS: chain to a file that has all groups--BGF, NUR, GF
      *
     C     $findone      begsr
      *
      * Room is blank
      *
     C                   select
     C                   when      dfrmcd = *blank
      *
     C                   if        c1stcd = 'MRKTS'
     C     key01         chain     hsl034r                            92
     C                   else
     C     key01         chain     hsl034p                            92
     C                   endif
     C                   other
      *
      * Room is populated
      *
     C                   if        c1stcd = 'MRKTS'
     C     key02         chain     hsl034r                            92
     C                   else
     C     key02         chain     hsl034p                            92
     C                   endif
     C                   endsl
      *
     C                   endsr
      /EJECT
      *----------------------------------------------------------------
      * Find the second open group
      *----------------------------------------------------------------
      *
      * Read equal with farm/building or farm/building/room using:
      *   KEY 1: farm/building when room is blank
      *   KEY 2: farm/building/room when room is populated.
      *
      * The file you read depends on the Type of Sale value:
      *   MRKTS:        read file that has only BGF and GF groups---no Nursery groups
      *   FPS or CULLS: read file that has all groups--BGF, NUR, GF
      *
     C     $findtwo      begsr
      *
      * Room is blank
      *
     C                   select
     C                   when      dfrmcd = *blank
      *
     C                   if        c1stcd = 'MRKTS'
     C     key01         reade     hsl034r                                92
     C                   else
     C     key01         reade     hsl034p                                92
     C                   endif
     C                   other
      *
      * Room is populated
      *
     C                   if        c1stcd = 'MRKTS'
     C     key02         reade     hsl034r                                92
     C                   else
     C     key02         reade     hsl034p                                92
     C                   endif
     C                   endsl
      *
     C                   endsr
      /eject
      *----------------------------------------------------------------
      * EDIT HOG GROUP
      *----------------------------------------------------------------
      *
     C     $HGCD         BEGSR
      *
     C                   MOVE      *BLANK        DFGSCD
     C                   MOVE      *BLANK        DFPTCD
     C                   MOVE      *BLANK        DFPPCD
      *
     C                   SELECT
     C     DFHGCD        WHENEQ    *BLANK
     C                   SETON                                        44
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS09001'     MGID(E)
     C                   EXSR      $ERR1
     C                   ENDIF
     C                   OTHER
      *
     C     DFHGCD        CHAIN     HSP034                             92
     C     *IN92         IFEQ      *ON                                          IF BAD GROUP
     C                   SETON                                        44
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS00261'     MGID(E)
     C                   CLEAR                   MGW
     C                   MOVEA     DFHGCD        MGW(1)
     C                   MOVEA     MGW           MGDT(E)
     C                   EXSR      $ERR1
     C                   ENDIF
     C                   ELSE
      *
      * Populate output-only fields
      *
     C                   MOVEL     HGGSCD        DFGSCD
     C                   MOVEL     HGPTCD        DFPTCD
     C                   MOVEL     HGPPCD        DFPPCD
      *
      * Nursery groups are not allowed on Market Sales.
      *
     C                   if        dfppcd = 'NUR  ' and c1stcd = 'MRKTS'
     C                   seton                                        44
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS01211'     mgid(e)
     C                   clear                   mgw
     C                   movea     dfhgcd        mgw(1)
     C                   movea     mgw           mgdt(e)
     C                   exsr      $err1
     C                   endif
     C                   endif
      *
      *
      * GROUP MUST ALWAYS HAVE A STATUS OF OPENED WITH AN OPEN DATE ON OR BEFORE
      * ALL DATES THAT ARE NOT ZERO.
      *
     C                   SELECT
     C                   WHEN      HGGSCD <> 'OP'
     C                   SETON                                        44
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS00603'     MGID(E)
     C                   CLEAR                   MGW
     C                   MOVEA     DFHGCD        MGW(1)
     C                   MOVEA     HGGSCD        MGW(8)
     C                   MOVEA     MGW           MGDT(E)
     C                   EXSR      $ERR1
     C                   ENDIF
     C                   OTHER
      *
     C                   IF        C1MDY <> 0 AND                               If sched
     C                             HGOPDT > WKSCDT
     C                   SETON                                        44
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS00488'     MGID(E)
     C                   CLEAR                   MGW
      *
     C                   MOVEA     DFHGCD        MGW(1)
      *
     C                   Z-ADD     HGOPDT        WKCYMD
     C                   EXSR      $TOMDY
     C                   MOVE      WKMDY         XXDT06
     C                   MOVEA     XXDT06        MGW(8)
      *
     C                   MOVE      C1MDY         XXDT06
     C                   MOVEA     XXDT06        MGW(14)
     C                   MOVEA     MGW           MGDT(E)
     C                   EXSR      $ERR1
     C                   ENDIF
     C                   ENDIF                                                  If sched
      *
     C                   IF        C1SCKLMDY <> 0 and                           If kill date
     C                             HGOPDT > WKSCKLDT
     C                   SETON                                        44
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS00455'     MGID(E)
     C                   CLEAR                   MGW
      *
     C                   MOVEA     DFHGCD        MGW(1)
      *
     C                   Z-ADD     HGOPDT        WKCYMD
     C                   EXSR      $TOMDY
     C                   MOVE      WKMDY         XXDT06
     C                   MOVEA     XXDT06        MGW(8)
      *
     C                   MOVE      C1SCKLMDY     XXDT06
     C                   MOVEA     XXDT06        MGW(14)
     C                   MOVEA     MGW           MGDT(E)
     C                   EXSR      $ERR1
     C                   ENDIF
     C                   ENDIF                                                  If kill date
      *
     C                   IF        C1SMDY <> 0 AND                              If ship
     C                             HGOPDT > WKSHDT
     C                   SETON                                        44
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS00460'     MGID(E)
     C                   CLEAR                   MGW
      *
     C                   MOVEA     DFHGCD        MGW(1)
      *
     C                   Z-ADD     HGOPDT        WKCYMD
     C                   EXSR      $TOMDY
     C                   MOVE      WKMDY         XXDT06
     C                   MOVEA     XXDT06        MGW(8)
      *
     C                   MOVE      C1SMDY        XXDT06
     C                   MOVEA     XXDT06        MGW(14)
     C                   MOVEA     MGW           MGDT(E)
     C                   EXSR      $ERR1
     C                   ENDIF
     C                   ENDIF                                                  If ship
      *
     C                   IF        C1RMDY <> 0 AND                              If received
     C                             HGOPDT > WKRCDT
     C                   SETON                                        44
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS00457'     MGID(E)
     C                   CLEAR                   MGW
      *
     C                   MOVEA     DFHGCD        MGW(1)
      *
     C                   Z-ADD     HGOPDT        WKCYMD
     C                   EXSR      $TOMDY
     C                   MOVE      WKMDY         XXDT06
     C                   MOVEA     XXDT06        MGW(8)
      *
     C                   MOVE      C1RMDY        XXDT06
     C                   MOVEA     XXDT06        MGW(14)
     C                   MOVEA     MGW           MGDT(E)
     C                   EXSR      $ERR1
     C                   ENDIF
     C                   ENDIF                                                  If received
      *
     C                   ENDSL
      *
      *
      * GROUP MUST BE ON THE FARM
      *
     C     HGFSCD        IFNE      C1FSCD                                       IF DIFF FARM
     C                   SETON                                        44
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS00468'     MGID(E)
     C                   CLEAR                   MGW
     C                   MOVEA     DFHGCD        MGW(1)
     C                   MOVE      HGFSCD        xxalphfscd
     C                   MOVEA     xxalphfscd    MGW(8)
     C                   MOVE      C1FSCD        xxalphfscd
     C                   MOVEA     xxalphfscd    MGW(13)
     C                   MOVEA     MGW           MGDT(E)
     C                   EXSR      $ERR1
     C                   ENDIF
     C                   ENDIF                                                  IF DIFF FARM
      *
      *
      * Group building must match subfile building---if there is a building
      * on the subfile.
     C                   select
     C                   when      dfblcd = *blank
     C                   movel     hgblcd        dfblcd
     C                   other
     C                   if        dfblcd <> hgblcd                             If mismatch building
     C                   seton                                        44
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS00481'     mgid(e)
     C                   clear                   mgw
     C                   movea     dfhgcd        mgw(1)
     C                   movea     hgblcd        mgw(8)
     C                   movea     dfblcd        mgw(13)
     C                   movea     mgw           mgdt(e)
     C                   exsr      $err1
     C                   endif
     C                   endif                                                  If mismatch building
     C                   endsl
      *
      *
      * Group room must match subfile room---if there is a room
      * on the subfile.
     C                   select
     C                   when      dfrmcd = *blank
     C                   movel     hgrmcd        dfrmcd
     C                   other
     C                   if        dfrmcd <> hgrmcd                             If mismatch room
     C                   seton                                        44
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS00482'     mgid(e)
     C                   clear                   mgw
     C                   movea     dfhgcd        mgw(1)
     C                   movea     hgrmcd        mgw(8)
     C                   movea     dfrmcd        mgw(13)
     C                   movea     mgw           mgdt(e)
     C                   exsr      $err1
     C                   endif
     C                   endif                                                  If mismatch room
     C                   endsl
      *
      * SEE IF GROUP HAS ALREADY BEEN ENTERED ON MOVEMENT.
      * GROUPS CAN ONLY BE ON SALES MOVEMENT ONE TIME.
      *
     C                   Z-ADD     1             X
     C     DFHGCD        LOOKUP    GRP(X)                                 98
     C     *IN98         IFEQ      *ON
     C                   SETON                                        44
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS00640'     MGID(E)
     C                   CLEAR                   MGW
     C                   MOVEA     DFHGCD        MGW(1)
     C                   MOVEA     MGW           MGDT(E)
     C                   EXSR      $ERR1
     C                   ENDIF
     C                   ENDIF
      *
     C                   ENDIF                                                  IF BAD GROUP
     C                   ENDSL
      *
      *----------------------------------------------------------------
      * NO ERRORS WERE FOUND ON THE GROUP
      *
     C     *IN44         IFEQ      *OFF                                         IF ALL OK
     C                   Z-ADD     HGHGSN        HDHGSN
     C                   ADD       1             Y
     C                   MOVEL     DFHGCD        GRP(Y)
     C                   ENDIF                                                  IF ALL OK
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * Validate scheduled head
      *----------------------------------------------------------------
      *
     C     $schd         begsr
      *
     C     DFSCHD        IFLT      0
     C                   SETON                                        74
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS09000'     MGID(E)
     C                   EXSR      $ERR1
     C                   ENDIF
     C                   ENDIF
      *
     C                   endsr
      /EJECT
      *----------------------------------------------------------------
      * $DSTSH - CALCULATE THE VALUES FOR DISTRIBUTING SHIPPED POUNDS
      *----------------------------------------------------------------
      *
     C     $DSTSH        BEGSR
      *
      *  CALCULATE THE SHIPPED POUNDS TO BE DISTRIBUTED TO THE SALES
      *  DETAIL RECORDS.  ANY RECORDS THAT ARE ASSOCIATED WITH
      *  CLOSED GROUPS WILL NOT RECEIVE DISTRIBUTED POUNDS BUT WILL,
      *  INSTEAD, USE THE ORIGINAL POUNDS THAT WERE RETRIEVED AND
      *  PLACED IN A HIDDEN SHIPPED POUNDS FIELD IN THE SUBFILE RECORD.
      *  THIS LOGIC KEEPS THE POUNDS AND, THUS, THE
      *  COSTS ASSOCIATED WKTH CLOSED GROUPS FROM BEING CONTAMINATED.
      *
      *  CALCULATE THE SHIPPED VALUES FOR DISTRIBUTION  =  TOTAL VALUES
      *  LESS THE SAVED VALUES ASSOCIATED WITH PROTECTED RECORDS
      *
     C     C1SHLB        SUB       SVSHLB        WKSHLB
     C     C1SHHD        SUB       SVSHHD        WKSHHD
      *
      *  CALCULATE THE AVERAGE SHIPPED POUNDS TO DISTRIBUTE PER HEAD
      *
     C                   Z-ADD     0             WKASLB
     C     WKSHHD        IFNE      0
     C     WKSHLB        DIV(H)    WKSHHD        WKASLB
     C                   ENDIF
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $C1IND - RESET INDICATORS IN CONTROL RECORD
      *----------------------------------------------------------------
      *
     C     $C1IND        BEGSR
      *
     C     NOTFLG        IFEQ      YES                                          IF NOT FOUND
     C                   SETON                                        90
     C                   ELSE
     C                   SETOFF                                       90
     C                   ENDIF                                                  IF NOT FOUND
      *
      * PROTECT THE SALES TYPE FIELD IN THE CONTROL RECORD IF
      * ANY HOGS HAVE BEEN RECEIVED AGAINST THIS MOVEMENT (IE: ANY
      * CHECKS HAVE BEEN ENTERED FOR THE MOVEMENT.)
      *
     C     C1LVHD        IFNE      0
     C     C1DOHD        ORNE      0
     C                   SETON                                        68
     C                   ELSE
     C                   SETOFF                                       68
     C                   ENDIF
      *
      *
      * IF ANY GROUP ON THE MOVEMENT IS DISPOSED OR CLOSED,
      * PROTECT THE FOLLOWING FIELDS:
      *   1) SALES TYPE CODE
      *   2) FARM SITE CODE
      *   3) SHIPPED IN TIME
      *   4) SHIPPED OUT TIME
      *   5) SHIPPED DATE
      *   6) SCHEDULED SHIP DATE
      *
     C     CLOSFL        IFEQ      YES
     C                   SETON                                        63
     C                   ELSE
     C                   SETOFF                                       63
     C                   ENDIF
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $SFIND - RESET INDICATORS IN SUBFILE
      *----------------------------------------------------------------
      *
     C     $SFIND        BEGSR
      *
      * THE DSPATR LOGIC USED IN THE DSPF REQUIRES AT LEAST ONE
      * INDICATOR TO BE ON.
     C                   SETON                                        42
      *
      * IF THE SALE WAS DELETED OUT FROM UNDER THE USER, THEN SET
      * *IN90 BACK ON TO CONTROL AVAILABLE FUNCTION KEYS AND INPUT
      * FIELDS.
      *
     C     NOTFLG        IFEQ      YES                                          IF NOT FOUND
     C                   SETON                                        90
     C                   ELSE
      *
      *  SET UP FOR USER'S ACTION
      *
     C                   SELECT
     C     DFMODE        WHENEQ    REVISE                                       WH REVISE
     C                   SETON                                        60
     C     DFMODE        WHENEQ    CREATE                                       WH CREATE
     C                   SETON                                        61
     C                   ENDSL
      *
      *
      * PROTECT THE GROUP CODE AND SOME FIELDS IN THE
      * CONTROL RECORD IF:
      *    a) the group is disposed or closed AND
      *    b) the movement status is not 'scheduled'
      *
     C                   if        c1mscd <> 'SC' and
     C                             (hdgscd = 'CL' or hdgscd = 'DS')
     C                   SETON                                        64
     C                   ELSE
     C                   SETOFF                                       64
     C                   ENDIF
      *
      * PROTECT THE GROUP CODE IF THERE ARE ANY RECEIVED
      * HOGS FOR THE GROUP (IE: PAYMENTS)
      *
     C     DFLVHD        IFNE      0
     C     DFDOHD        ORNE      0
     C                   SETON                                        67
     C                   ELSE
     C                   SETOFF                                       67
     C                   ENDIF
      *
     C                   ENDIF                                                  IF NOT FOUND
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $SETOF - SET OFF THE ERROR INDICATORS ON THE SUBFILE
      *----------------------------------------------------------------
      *
     C     $SETOF        BEGSR
      *
     C                   SETOFF                                       444649
     C                   SETOFF                                       545659
     C                   SETOFF                                         6467
     C                   SETOFF                                         7174
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $DATE -  MANIPULATE DATES WITH DATE UTILITY
      *----------------------------------------------------------------
      *
     C     $DATE         BEGSR
      *
     C                   Z-ADD     0             PTO8
     C                   MOVEL     'MDY '        PFRFMT
     C                   MOVEL     'CYMD'        PTOFMT
     C                   Z-ADD     0             PDAYS
     C                   MOVE      *BLANK        PCODE
     C                   MOVE      *BLANK        PRTRN
      *
     C                   CALL      'UT80060R'
     C                   PARM                    PFRM8             8 0
     C                   PARM                    PTO8              8 0
     C                   PARM                    PFRFMT            4
     C                   PARM                    PTOFMT            4
     C                   PARM                    PDAYS             4 0
     C                   PARM                    PCODE             1
     C                   PARM                    PRTRN             1
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $JDEV - SUBROUTINE TO CALL THE EXTERNAL PROGRAM TO VALIDATE JDE
      *         ADDRESS NUMBERS.
      *----------------------------------------------------------------
      *
     C     $JDEV         BEGSR
      *
     C                   MOVEL     NO            JDEFL                          JDE ERRORS
      *
     C                   CALL      'HPJDEV'
     C                   PARM                    MGI                            MSG ID'S
     C                   PARM                    MGD                            MSG DATA FLD
     C                   PARM                    ERROR                          # ERR MSGS
     C                   PARM                    INAJD                          JDE ADDR #
     C                   PARM                    INAT1                          JDE ADDR TYP
     C                   PARM                    JDEFL                          ERROR FLAG
     C                   PARM      *BLANK        OUALPH                         JDE NAME
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $DLT11 - DELETE A MOVEMENT WHEN THE USER TAKES F11=DELETE
      *----------------------------------------------------------------
      *
     C     $DLT11        BEGSR
      *
      * FIRST, CHECK USER'S AUTHORITY TO DELETE MOVEMENTS
      *
     C                   CALL      'HPSECD'
     C                   PARM                    MGID
     C                   PARM                    MGDT
     C                   PARM                    ERROR
     C                   PARM                    SDUSR
     C                   PARM                    SHMSCD
      *
     C     ERROR         IFNE      0                                            IF CAN'T DLT
     C                   MOVEL     SCRN1         ROUTNE
     C                   ELSE
      *
      * THE USER IS AUTHORIZED TO DELETE MOVEMENT, SO
      * CALL THE 'CONFIRM DELETE' PROGRAM WHERE THE USER
      * IS REQUIRED TO ENTER A DELETION COMMENT.
      *
     C                   CALL      'HP113'
     C                   PARM                    C1MVSN
     C                   PARM      *BLANK        XXDMCM
     C                   PARM      *BLANK        RTNCD
      *
      * IF THE RETURN CODE IS ZERO, THEN THE USER IS DELETING.
      * WHEN DELETING THE HEADER, WRITE THE AUDIT INFO TO
      * THE 'DELETED MOVEMENT' FILE.
      *
     C     RTNCD         IFEQ      '00'                                         IF DELETE
     C                   EXSR      $DLTSH
     C                   EXSR      $DLTSD
     C                   EXSR      $DLTME
     C                   MOVEL     EXIT          ROUTNE
     C                   ELSE
     C                   MOVEL     SCRN1         ROUTNE
     C                   ENDIF                                                  IF DELETE
      *
     C                   ENDIF                                                  IF CAN'T DLT
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $DLTSH - DELETE THE SALES MOVEMENT HEADER RECORD
      *----------------------------------------------------------------
      *
     C     $DLTSH        BEGSR
      *
     C     C1MVSN        CHAIN     HSP084                             92
     C     *IN92         IFEQ      *OFF                                         IF FOUND
      *
      * IF THE USER IS DELETING THE MOVEMENT (F11 HAS BEEN TAKEN),
      * WRITE THE AUDIT INFO TO THE 'DELETED MOVEMENT' FILE PRIOR
      * TO DELETING THE HEADER RECORD.
      *
     C     *IN11         IFEQ      *ON                                          IF F11
     C                   EXSR      $CRTDM
     C                   ENDIF                                                  IF F11
      * DELETE HEADER
     C                   DELETE    SHREC
     C                   ENDIF                                                  IF FOUND
      *
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $DLTSD - DELETE THE SALES MOVEMENT DETAIL RECORDS
      *----------------------------------------------------------------
      *
     C     $DLTSD        BEGSR
      *
     C     C1MVSN        SETLL     HSP085
     C                   SETOFF                                       95
     C     *IN95         DOUEQ     *ON                                          DO DETAIL
     C     C1MVSN        READE     HSP085                                 95
     C     *IN95         IFEQ      *OFF                                         IF FOUND
     C                   DELETE    SGREC
     C                   ENDIF                                                  IF FOUND
     C                   ENDDO                                                  DO DETAIL
      *
     C                   clear                   sgrec
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $DLTME - DELETE ANY/ALL MOVEMENT EVENT RECORDS
      *----------------------------------------------------------------
      *
     C     $DLTME        BEGSR
      *
     C                   SETOFF                                       95
     C     C1MVSN        SETLL     HSP058
      *
     C     *IN95         DOUEQ     *ON                                          DO EVENTS
     C     C1MVSN        READE     HSP058                                 95
     C     *IN95         IFEQ      *OFF                                         IF EVENT
     C                   DELETE    MEREC
     C                   ENDIF                                                  IF EVENT
     C                   ENDDO                                                  DO EVENTS
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $CRTDM - CREATE AN AUDIT RECORD FOR THE DELETED MOVEMENT
      *----------------------------------------------------------------
      *
     C     $CRTDM        BEGSR
      *
     C                   Z-ADD     SHMVSN        DMMVSN
     C                   MOVEL     SDUSR         DMUSCD
     C                   Z-ADD     WKUDAT        DMDMDT
     C                   TIME                    HHMMSS
     C                   Z-ADD     HHMM          DMDMTM
     C                   MOVEL     XXDMCM        DMDMCM
      *
     C                   MOVEL     'S'           DMMTCD
     C                   MOVEL     SHMSCD        DMMSCD
     C                   Z-ADD     SHSCDT        DMSCDT
     C                   Z-ADD     SHSHDT        DMSHDT
     C                   Z-ADD     SHRCDT        DMRCDT
     C                   Z-ADD     SHCVNO        DMCVNO
      *
     C                   MOVEL     SHFSBO        DMORBO
     C                   Z-ADD     SHFSCD        DMORFS
     C                   MOVEL     *BLANK        DMORPT
     C                   MOVEL     *BLANK        DMORPP
      *
     C                   MOVE      *BLANK        DMDNBO
     C                   MOVE      *BLANK        DMDNPT
     C                   MOVE      *BLANK        DMDNPP
     C                   Z-ADD     0             DMDNFS
      *
     C                   WRITE     DMREC
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $CRTSH - CREATE A SALES MOVEMENT HEADER RECORD
      *----------------------------------------------------------------
      *
     C     $CRTSH        BEGSR
      *
      * IF YOU ARE CREATING A NEW SALES MOVEMENT, ASSIGN A MOVEMENT
      * NUMBER.
     C     MODE          IFEQ      CREATE
     C     *LOCK         IN        DAMVSN
     C                   ADD       1             DAMVSN
     C                   OUT       DAMVSN
     C                   Z-ADD     DAMVSN        SHMVSN
     C                   ELSE
     C                   Z-ADD     C1MVSN        SHMVSN
     C                   ENDIF
      *
     C                   MOVEL     C1MSCD        SHMSCD
     C                   MOVEL     C1STCD        SHSTCD
     C                   MOVEL     C1LTCD        SHLTCD
     C                   MOVEL     HDTPCD        SHTPCD
      *
     C                   MOVEL     C1FSCD        SHFSCD
     C                   MOVEL     FSFSBO        SHFSBO
      *
     C                   Z-ADD     C1CVNO        SHCVNO
     C                   MOVEL     C1TICD        SHTICD
     C                   Z-ADD     WKSCDT        SHSCDT
     C                   z-add     wksckldt      shsckldt
     C                   Z-ADD     WKSHDT        SHSHDT
     C                   Z-ADD     WKRCDT        SHRCDT
      *
     C                   Z-ADD     0             SHEPDT
     C                   Z-ADD     0             SHPODT
     C                   Z-ADD     0             SHPTCT
      *
     C                   Z-ADD     WKUDAT        SHUPDT
     C                   TIME                    SHUPTM
     C                   MOVEL     SDPGM         SHPGM
      *
     C                   WRITE     SHREC
      *
     C     mode          ifeq      revise
     C                   exsr      $chgslp
     C                   endif
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $CRTSD - CREATE SALES MOVEMENT DETAIL RECORDS
      *----------------------------------------------------------------
      *
     C     $CRTSD        BEGSR
      *
      * CALCULATE THE VALUES TO BE USED IN DISTRIBUTING SHIPPED POUNDS
      *
     C                   EXSR      $DSTSH
      *
      *---------------------------------------------------------------
      * READ/PROCESS ALL THE CHANGED SUBFILE RECORDS
      *---------------------------------------------------------------
      *
     C                   SETOFF                                       80
     C     *IN80         DOUEQ     *ON                                          DO READ C
     C                   READC     HP146S1                                80
     C     *IN80         IFEQ      *OFF                                         IF REC READ
     C     DFHGCD        ANDNE     *BLANK
      *
      * Clear Movement Detail record
      *
     C                   clear                   sgrec
      *
     C     MODE          IFEQ      CREATE
     C                   Z-ADD     DAMVSN        SGMVSN
     C                   ELSE
     C                   Z-ADD     C1MVSN        SGMVSN
     C                   ENDIF
      *
      * Retrieve the head/pounds values for this Group/Movement from Check Detail
      * records.
      *
     C     key03         setll     hsl064i
     C                   dou       *in93 = *on                                  Do detail
     C     key03         reade     hsl064i                                93
     C                   if        *in93 = *off                                 If found
     C                   add       cdlvhd        sglvhd
     C                   add       cdlvlb        sglvlb
     C                   add       cddohd        sgdohd
     C                   add       cddolb        sgdolb
     C                   add       cdcdhd        sgcdhd
     C                   add       cdcdlb        sgcdlb
     C                   add       cdydhd        sgydhd
     C                   add       cdydlb        sgydlb
     C                   endif                                                  If found
     C                   enddo                                                  Do detail
      *
     C                   Z-ADD     HDHGSN        SGHGSN
     C                   Z-ADD     DFSCHD        SGSCHD
     C                   Z-ADD     DFSHHD        SGSHHD
      *
      * IF THE GROUP IS NOT CLOSED (SHIPPED POUNDS IN THE
      * HIDDEN SUBFILE FIELD IS ZERO), DISTRIBUTE POUNDS TO THIS
      * DATABASE RECORD; OTHERWISE, MOVE THE SAVED SHIPPED POUNDS INTO
      * THE DATABASE RECORD.
      *
     C     HDSHLB        IFEQ      0
     C                   EXSR      $SHLB
     C                   ELSE
     C                   Z-ADD     HDSHLB        SGSHLB
     C                   ENDIF
      *
     C                   WRITE     SGREC
      *
      * CREATE A MOVEMENT EVENT RECORD FOR THE GROUP
      *
     C                   EXSR      $CRTME
      *
     C                   ENDIF                                                  IF REC READ
     C                   ENDDO                                                  DO READ C
      *
      *----------------------------------------------------------------
      * DUMP ALL SHIPPED POUNDS THAT HAVE NOT ALREADY BEEN DISTRIBUTED
      * INTO THE LAST RECORD THAT RECEIVED DISTRIBUTED POUNDS.
      *----------------------------------------------------------------
      *
     C     WKSHLB        IFNE      0                                            IF LEFTOVER
     C     K1P085        CHAIN     HSP085                             92
     C     *IN92         IFEQ      *OFF                                         IF DETAIL
     C                   ADD       WKSHLB        SGSHLB
     C                   UPDATE    SGREC
     C                   ENDIF                                                  IF DETAIL
     C                   ENDIF                                                  IF LEFTOVER
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $CRTME - CREATE MOVEMENT EVENT RECORDS
      *----------------------------------------------------------------
      *
     C     $CRTME        BEGSR
      *
      *
      * SET UP UPDATE TIME STAMP
      *
     C                   Z-ADD     WKUDAT        MEUPDT
     C                   TIME                    MEUPTM
     C                   MOVEL     SDUSR         MEUSCD
     C                   MOVEL     SDPGM         MEPGM
      *
      * SETUP THE MOVEMENT NUMBER AND STATUS
      *
     C     MODE          IFEQ      CREATE
     C                   Z-ADD     DAMVSN        MEMVSN
     C                   ELSE
     C                   Z-ADD     C1MVSN        MEMVSN
     C                   ENDIF
      *
     C                   Z-ADD     0             MEHSN
     C                   Z-ADD     C1CVNO        MECVNO
     C                   Z-ADD     0             MELNNO
      *
      * WRITE AN EVENT RECORD FOR THE GROUP
      *
     C                   Z-ADD     HDHGSN        MEHGSN
     C                   MOVEL     'S'           MEMTCD
     C                   MOVEL     'O'           MEODFL
      *
     C                   select
     C                   when      c1mscd = 'SC'
     C                   if        c1stcd = 'MRKTS' and wksckldt <> 0
     C                   z-add     wksckldt      memedt
     C                   else
     C                   z-add     wkscdt        memedt
     C                   endif
     C                   other
      *
     C                   z-add     wkrcdt        memedt
     C                   endsl
      *
     C                   write     merec
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $chgslp - Update Scheduled Load Plan record with new dates
      *----------------------------------------------------------------
      *
     C     $chgslp       begsr
      *
      * Retrieve existing Scheduled Load Plan record for Movement#
     C     key04         chain     hsl196b
     C                   if        %found(hsl196b)
     C                   eval      lpscshdt = wkscdt
     C                   eval      lpsckldt = wksckldt
      *
     C                   eval      lpupdt  = wkudat
     C                   eval      lpuptm  = shuptm
     C                   eval      lpupusr = sdusr
      *
     C                   update    lprec
      *
     C                   endif
      *
     C                   endsr
      /EJECT
      *----------------------------------------------------------------
      * $SHLB - DISTRIBUTE SHIPPED POUNDS TO THIS DETAIL RECORD
      *----------------------------------------------------------------
      *
     C     $SHLB         BEGSR
      *
     C                   Z-ADD     0             SGSHLB
      *
     C     WKSHLB        IFGT      0                                            IF LBS LEFT
     C     SGSHHD        MULT      WKASLB        WKLB
     C     WKLB          IFGT      WKSHLB
     C                   Z-ADD     WKSHLB        SGSHLB
     C                   Z-ADD     0             WKSHLB
     C                   ELSE
     C                   Z-ADD     WKLB          SGSHLB
     C     WKSHLB        SUB       WKLB          WKSHLB
     C                   ENDIF
     C                   ENDIF                                                  IF LBS LEFT
      *
      * SAVE THE MOVEMENT/GROUP NUMBER FOR POSSIBLE RETRIEVAL
      * OF THIS RECORD TO DUMP IN LEFTOVER SHIPPED POUNDS.
      *
     C                   Z-ADD     SGMVSN        SVMVSN
     C                   Z-ADD     HDHGSN        SVHGSN
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $F4S1 - F4 PROMPT FOR SCREEN 1
      *----------------------------------------------------------------
      *
     C     $F4S1         BEGSR
      *
     C                   SELECT
      *
      *----------------------------------------------------------------
      * PROMPTED FIELDS IN THE CONTROL RECORD
      *----------------------------------------------------------------
      *
      * MOVEMENT STATUS
      *
     C     FLD           WHENEQ    'C1MSCD'
     C                   CALL      'HP599'
     C                   PARM      'S'           XXMTCD
     C                   PARM      *blank        XXMSCD
     C                   PARM      *blank        XXMSDS
     C                   SETON                                        58
     C     XXMSCD        IFNE      *BLANK                                       IF SELECTED
     C                   MOVEL     XXMSCD        C1MSCD
     C                   ENDIF                                                  IF SELECTED
      *
      *
      * SALES TYPE
      *
     C     FLD           WHENEQ    'C1STCD'
     C     *IN63         ANDEQ     *OFF
     C     *IN68         ANDEQ     *OFF
     C                   CALL      'HP544'
     C                   PARM      *blank        XXSTCD
     C                   PARM      *blank        XXSTDS
     C                   SETON                                        57
     C     XXSTCD        IFNE      *BLANK                                       IF SELECTED
     C                   MOVEL     XXSTCD        C1STCD
     C                   ENDIF                                                  IF SELECTED
      *
      *
      * LOAD TYPE
      *
     C     FLD           WHENEQ    'C1LTCD'
     C                   CALL      'HP5012'
     C                   PARM      *blank        XXLTCD
     C                   PARM      *blank        XXLTDS
     C                   SETON                                        50
     C                   if        xxltcd <> *blank                             IF SELECTED
     C                   MOVEL     XXLTCD        C1LTCD
     C                   ENDIF                                                  IF SELECTED
      *
      *
      * TRUCKER
      *
     C     FLD           WHENEQ    'C1TICD'
      *
     C                   if        c1mscd = 'SC'
     C                   move      'A'           xxaist
     C                   else
     C                   move      *blank        xxaist
     C                   endif
      *
     C                   CALL      'HP538'
     C                   PARM                    XXAIST
     C                   PARM      *blank        XXTICD
     C                   PARM      *blank        XXTINM
     C                   PARM      *blank        XXMTECHREF
     C                   SETON                                        53
     C     XXTICD        IFNE      *BLANK                                       IF SELECTED
     C                   MOVEL     XXTICD        C1TICD
     C                   ENDIF                                                  IF SELECTED
      *
      *  FARM SITE
      *
     C     FLD           WHENEQ    'C1FSCD'
     C     *IN63         ANDEQ     *OFF
     C                   CALL      'HP5040'
     C                   PARM      'A'           XXAIST
     C                   PARM      0             xxfscd
     C                   PARM      *blank        XXFSNM
     C                   SETON                                        51
     C     xxfscd        IFNE      0                                            IF SELECTED
     C                   Z-ADD     xxfscd        C1FSCD
     C                   MOVEL     XXFSNM        C1FSNM
     C                   ENDIF                                                  IF SELECTED
      *
      * CUSTOMER
      *
     C     FLD           WHENEQ    'C1CVNO'
     C     *IN68         ANDEQ     *OFF
     C                   MOVEL(P)  'C'           INAT1                          SEARCH TYPE
     C                   CALL      'HP513'
     C                   PARM      WKCOCD        XXCOCD                         COMPANY
     C                   PARM                    INAT1                          SEARCH TYPE
     C                   PARM      *ZERO         OUAJD                          SELECTED NO.
     C                   PARM      *BLANK        OUALPH                         JDE NAME
     C                   SETON                                        52
     C     OUAJD         IFNE      0                                            IF SELECTED
     C                   Z-ADD     OUAJD         C1CVNO
     C                   MOVEL     OUALPH        C1CNAM
     C                   ENDIF                                                  IF SELECTED
      *
      *----------------------------------------------------------------
      * PROMPTED FIELDS IN THE SUBFILE
      *----------------------------------------------------------------
      *
      *--------------
      * Building
      *--------------
     C                   when      fld = 'DFBLCD'
     C                   exsr      $rrn1
     C                   if        *in97 = *off                                 If hit
      *
     C                   if        c1mscd = 'SC' or                             IF NOT PROTECTED
     C                             (hdgscd <> 'CL' and hdgscd <> 'DS' and
     C                             dflvhd = 0 and dfdohd = 0)
     C                   call      'HP523'
     C                   parm                    c1fscd
     C                   parm      *blank        xxblcd
     C                   parm      *blank        xxblds
     C                   if        xxblcd <> *blank
     C                   movel     xxblcd        dfblcd
     C                   endif
     C                   else
      *
      * THE GROUP IS PROTECTED; YOU CANNOT PROMPT ON BUILDING.
      *
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS09011'     MGID(E)
     C                   ENDIF
     C                   ENDIF                                                  IF NOT PROTECTED
      *
     C                   exsr      $sfind
     C                   seton                                        8559
     C                   update    hp146s1
     C                   setoff                                       8559
      *
      * RESET THE SUBFILE RELATIVE RECORD NUMBER TO WHAT IT WAS BEFORE
      * THE USER TOOK F4 (IT WILL BE OUT-OF-SYNCH IF THE USER HAS
      * SELECTED A RECORD THUS CAUSING THE SUBFILE RECORD TO BE
      * RETRIEVED AND UPDATED.)  BUT, SET THE PAGE OF THE SUBFILE TO
      * BE DISPLAYED TO THE PAGE ON WHICH THE USER TOOK F4.
      *
     C                   z-add     count         hdrrn1
     C                   z-add     hdrr1         hdpag1
     C                   endif                                                  If hit
      *
      *--------------
      * Room
      *--------------
     C                   when      fld = 'DFRMCD'
     C                   exsr      $rrn1
     C                   if        *in97 = *off                                 If hit
      *
     C                   if        c1mscd = 'SC' or                             IF NOT PROTECTED
     C                             (hdgscd <> 'CL' and hdgscd <> 'DS' and
     C                             dflvhd = 0 and dfdohd = 0)
     C                   call      'HP524'
     C                   parm                    c1fscd
     C                   parm                    dfblcd
     C                   parm      *blank        xxrmcd
     C                   if        xxrmcd <> *blank
     C                   movel     xxrmcd        dfrmcd
     C                   endif
     C                   ELSE
      *
      * THE GROUP IS PROTECTED; YOU CANNOT PROMPT ON ROOM
      *
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS09011'     MGID(E)
     C                   ENDIF
     C                   ENDIF                                                  IF NOT PROTECTED
      *
     C                   exsr      $sfind
     C                   seton                                        8556
     C                   update    hp146s1
     C                   setoff                                       8556
      *
      * RESET THE SUBFILE RELATIVE RECORD NUMBER TO WHAT IT WAS BEFORE
      * THE USER TOOK F4 (IT WILL BE OUT-OF-SYNCH IF THE USER HAS
      * SELECTED A RECORD THUS CAUSING THE SUBFILE RECORD TO BE
      * RETRIEVED AND UPDATED.)  BUT, SET THE PAGE OF THE SUBFILE TO
      * BE DISPLAYED TO THE PAGE ON WHICH THE USER TOOK F4.
      *
     C                   z-add     count         hdrrn1
     C                   z-add     hdrr1         hdpag1
     C                   endif                                                  If hit
      *-------------
      * HOG GROUP
      *-------------
      *
     C     FLD           WHENEQ    'DFHGCD'
     C                   EXSR      $RRN1
     C     *IN97         IFEQ      *OFF                                         IF HIT
      *
     C                   if        c1mscd = 'SC' or                             IF NOT PROTECTED
     C                             (hdgscd <> 'CL' and hdgscd <> 'DS' and
     C                             dflvhd = 0 and dfdohd = 0)
      *
     C                   select
     C                   when      c1stcd = 'MRKTS'
     C                   call      'HP5951'
     C                   parm                    c1fscd
     C                   parm                    c1fsnm
     C                   parm      dfblcd        xxblcd
     C                   parm      dfrmcd        xxrmcd
     C                   parm      *blank        xxhgcd
     C                   parm      *blank        xxgscd
     C                   parm      *blank        xxptcd
     C                   parm      *blank        xxppcd
     C                   other
      *
     C                   CALL      'HP5950'
     C                   PARM                    c1fscd
     C                   PARM                    c1fsnm
     C                   PARM      dfblcd        XXBLCD
     C                   PARM      dfrmcd        XXRMCD
     C                   PARM      *blank        XXHGCD
     C                   PARM      *blank        XXGSCD
     C                   PARM      *blank        XXPTCD
     C                   PARM      *blank        XXPPCD
     C                   endsl
      *
     C     XXHGCD        IFNE      *BLANK                                       IF SELECTED
     C                   MOVEL     XXHGCD        DFHGCD
     C                   MOVEL     XXBLCD        DFBLCD
     C                   MOVEL     XXRMCD        DFRMCD
     C                   MOVEL     XXGSCD        DFGSCD
     C                   MOVEL     XXPTCD        DFPTCD
     C                   MOVEL     XXPPCD        DFPPCD
     C                   ENDIF                                                  IF SELECTED
     C                   ELSE
      *
      * THE GROUP IS PROTECTED; YOU CANNOT PROMPT ON IT.
      *
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS09011'     MGID(E)
     C                   ENDIF
     C                   ENDIF                                                  IF NOT PROTECTED
      *
      * RESET SUBFILE RECORD INDICATORS AND UPDATE SUBFILE
      * RECORD.
      *
     C                   EXSR      $SFIND
     C                   SETON                                        8554
     C                   UPDATE    HP146S1
     C                   SETOFF                                       8554
      *
      * RESET THE SUBFILE RELATIVE RECORD NUMBER TO WHAT IT WAS BEFORE
      * THE USER TOOK F4 (IT WILL BE OUT-OF-SYNCH IF THE USER HAS
      * SELECTED A RECORD THUS CAUSING THE SUBFILE RECORD TO BE
      * RETRIEVED AND UPDATED.)  BUT, SET THE PAGE OF THE SUBFILE TO
      * BE DISPLAYED TO THE PAGE ON WHICH THE USER TOOK F4.
      *
      *
     C                   Z-ADD     COUNT         HDRRN1
     C                   Z-ADD     HDRR1         HDPAG1
     C                   ENDIF                                                  IF HIT
     C                   OTHER
      *
      * F4 NOT VALID FOR THIS FIELD SO ISSUE MESSAGE
      *
     C     ERROR         IFLT      MAXMSG                                       IF FIRST ERR
     C                   ADD       1             ERROR
     C                   MOVEL     'HS09011'     MGID(E)
     C                   ENDIF                                                  IF FIRST ERR
     C                   ENDSL
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * $ERR1  - SUBROUTINE TO SET THE SUBFILE TO REDISPLAY AT
      *          THE FIRST SCREEN THAT HAS ERRORS
      *---------------------------------------------------------------
      *
     C     $ERR1         BEGSR
      *
     C     ERROR         IFEQ      1
     C                   Z-ADD     HDRRN1        HDPAG1
     C                   ENDIF
      *
     C                   ENDSR
      *
      *---------------------------------------------------------------
      * $EXIT - SUBROUTINE TO SET ON LR AND END JOB
      *---------------------------------------------------------------
      *
     C     $EXIT         BEGSR
     C                   SETON                                        LR
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * $TOMDY - FLIP DATE FROM CCYYMMDD TO MMDDYY
      *---------------------------------------------------------------
      *
     C     $TOMDY        BEGSR
      *
     C                   Z-ADD     WKMON         WKMM
     C                   Z-ADD     WKDAY         WKDD
     C                   Z-ADD     WKYER         WKYY
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * $WRMSG - SUBROUTINE TO WRITE ERROR MESSAGES
      *---------------------------------------------------------------
      *
     C     $WRMSG        BEGSR
      *
     C                   CALL      'UT80045J'                           43
     C                   PARM                    MGID
     C                   PARM                    MGDT
     C                   PARM                    ERROR
     C                   PARM      SDPGM         PGM
     C                   PARM      MSGFIL        MSGFL
      *
      * IF CALL TO UT80045J FAILED, A MESSAGE HARDCODED IN THE DSPF
      * COMMAND KEY RECORD WILL BE SET ON. SO, REDISPLAY SCREEN TO USER
      *
     C     *IN43         IFEQ      *ON
     C                   MOVEL     SET1          ROUTNE
     C                   ENDIF
      *
     C                   ENDSR
      *
      *---------------------------------------------------------------
      * $CLMSG - SUBROUTINE TO CLEAR MESSAGES
      *---------------------------------------------------------------
      *
     C     $CLMSG        BEGSR
      *
     C                   CALL      'UT80045J'                           43
     C                   PARM      *BLANK        MGI
     C                   PARM      *BLANK        MGD
     C                   PARM      *ZERO         ERROR
     C                   PARM      SDPGM         PGM
     C                   PARM      MSGFIL        MSGFL
      *
      * IF CALL TO UT80045J FAILED, A MESSAGE HARDCODED IN THE DSPF
      * COMMAND KEY RECORD WILL BE SET ON. SO, REDISPLAY SCREEN TO USER
      *
     C     *IN43         IFEQ      *ON
     C                   MOVEL     SET1          ROUTNE
     C                   ENDIF
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $RRN1  - ROUTINE TO FIND/RETRIEVE THE SUBFILE RECORD TO UPDATE
      *          WHEN RETURNING FROM A SELECT PROGRAM ON SCREEN 1
      *----------------------------------------------------------------
      *
      * HDRR1 IS THE HIDDEN FIELD ASSOCIATED WITH THE KEYWORD SFLCSRRRN
      * ON THE CONTROL RECORD IN THE DSPF.
      * HDRRN1 IS THE VALUE ASSOCIATED WITH THE KEYWORD 'KSFILE' IN
      * THE F-SPECS FOR THE WORKSTATION.
      *
     C     $RRN1         BEGSR
      *
     C                   SETOFF                                       97
      *
     C     HDRR1         IFNE      0
     C                   Z-ADD     HDRR1         HDRRN1
     C     HDRRN1        CHAIN     HP146S1                            97
     C                   ENDIF
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * INITIALIZATION SUBROUTINE
      *---------------------------------------------------------------
      *
     C     *INZSR        BEGSR
      *
      * PARM LISTS
      *
     C     *ENTRY        PLIST
     C                   PARM                    MODE
     C                   PARM                    WKCOCD            3
     C                   PARM                    C1MVSN
     C                   PARM                    C1MSCD
     C                   PARM                    XXBOCD
     C                   PARM                    RTNCD
     C                   parm                    version           1
     C                   parm                    c1version
      *
      * KEY LISTS
      *
      *
     C     key01         klist
     C                   kfld                    c1fscd
     C                   kfld                    dfblcd
      *
     C     key02         klist
     C                   kfld                    c1fscd
     C                   kfld                    dfblcd
     C                   kfld                    dfrmcd
      *
     C     key03         klist
     C                   kfld                    hdhgsn
     C                   kfld                    sgmvsn
      *
     C     key04         klist
     C                   kfld                    xxbocd
     C                   kfld                    shmvsn
      *
     C     K1P085        KLIST
     C                   KFLD                    SVMVSN
     C                   KFLD                    SVHGSN
      *
     C     K2P077        KLIST
     C                   KFLD                    WKMTCD
     C                   KFLD                    C1MSCD
      *
     C     *LIKE         DEFINE    MEMTCD        WKMTCD
     C                   MOVEL     'S'           WKMTCD
      *
      * GLOBAL DEFINES
      *
     C                   MOVE      *BLANK        PGM              10            USED IN MSG
     C                   MOVE      *BLANK        MSGFL            10            USED IN MSG
     C                   MOVE      *BLANK        ROUTNE            6            ROUTINE
     C                   Z-ADD     0             MAXRRN            4 0          REPOSITION
     C                   MOVEL     NO            RESET             1            REPOSITION
     C                   MOVEL     NO            NOTFLG            1
     C                   MOVEL     '00'          RTNCD             2            RETURN CODE
     C     *LIKE         DEFINE    DFMODE        MODE                           SFL POSITION
     C     *LIKE         DEFINE    IDFLOW        WKFLOW                         SFL POSITION
     C                   MOVEL     NO            EDITFL            1
     C                   MOVEL     NO            CLOSFL            1
     C                   Z-ADD     0             WARMSG            2 0
      *
      * ARRAY INDEXES
     C                   Z-ADD     0             X                 3 0
     C                   Z-ADD     0             Y                 3 0
      *
      *
      * ALPHA AND NUMERIC FIELDS FOR USE AS PARMS
      *
     C     *LIKE         DEFINE    DMDMCM        XXDMCM
     C                   MOVE      *BLANK        XXDT06            6
     C                   MOVE      *BLANK        XXMDY             6
     C                   MOVE      *BLANK        XXCVNO            8
     C                   MOVE      *BLANK        XXCHNO            7
     C                   MOVE      *BLANK        XXMVSN            7
     C                   MOVE      *BLANK        XXCOCD            3
     C     *LIKE         DEFINE    FBBLCD        XXBLCD
     C     *LIKE         DEFINE    FBBLDS        XXBLDS
     C     *LIKE         DEFINE    HGRMCD        XXRMCD
     C     *LIKE         DEFINE    HGHGCD        XXHGCD
     C     *LIKE         DEFINE    HGGSCD        XXGSCD
     C     *LIKE         DEFINE    HGBLCD        XXBLCD
     C     *LIKE         DEFINE    HGPTCD        XXPTCD
     C     *LIKE         DEFINE    HGPPCD        XXPPCD
     C     *LIKE         DEFINE    FSFSCD        xxfscd
     C                   MOVE      *BLANK        xxalphfscd        5
     C                   MOVE      *BLANK        xxalphcvno        8
     C     *LIKE         DEFINE    FSFSNM        XXFSNM
     C     *LIKE         DEFINE    TITICD        XXTICD
     C     *LIKE         DEFINE    TITINM        XXTINM
     C                   MOVE      *BLANK        XXAIST            1
     C                   MOVE      *BLANK        XXLB              5
     C                   MOVE      *BLANK        XXHD              5
     C                   MOVE      *BLANK        XXHD6             6
     C     *LIKE         DEFINE    C1STCD        XXSTCD
     C                   MOVE      *BLANK        XXSTDS           20
     C     *LIKE         DEFINE    C1LTCD        XXLTCD
     C                   MOVE      *BLANK        XXLTDS           25
     C                   MOVE      *BLANK        XXMTCD            1
     C     *LIKE         DEFINE    C1MSCD        XXMSCD
     C                   MOVE      *BLANK        XXMSDS           20
     C                   MOVE      *BLANK        XXOKFORHPS        1
     C     *LIKE         DEFINE    FSMTECHREF    xxmtechref
      *
      * WORK FIELDS
      *
     C     *LIKE         DEFINE    SHSCKLDT      wksckldt
     C     *LIKE         DEFINE    SHSCDT        WKSCDT
     C     *LIKE         DEFINE    SHSHDT        WKSHDT
     C     *LIKE         DEFINE    SHRCDT        WKRCDT
     C                   move      no            paylivefl         1
     C                   move      no            paycarcfl         1
      *
      *
      * ACCUMULATORS FOR SUBFILE HEAD
      *
     C     *LIKE         DEFINE    SGSCHD        WKSCHD          + 1
     C     *LIKE         DEFINE    SGSHHD        WKSHHD          + 1
     C     *LIKE         DEFINE    SGSHLB        WKLB
     C     *LIKE         DEFINE    SGSHLB        WKSHLB
     C     *LIKE         DEFINE    SGSHHD        WKARHD
      *
      * AVERAGE SHIPPED POUNDS
      *
     C                   Z-ADD     0             WKASLB           15 9
      *
      * Move status
     C     *like         define    shmscd        svmscd
      *
      * SAVE FIELDS USED IN POUNDS DISTRIBUTION
      *
     C     *LIKE         DEFINE    SGMVSN        SVMVSN
     C     *LIKE         DEFINE    SGHGSN        SVHGSN
     C     *LIKE         DEFINE    SGSHHD        SVSHHD
     C     *LIKE         DEFINE    SGSHLB        SVSHLB
      *
      * SAVE FIELDS USED IN EDITING SHIPPED/RECEIVED DATES AGAINST
      * CHECK FILE.
      *
     C     *LIKE         DEFINE    CDCVNO        SVCVNO
     C     *LIKE         DEFINE    CDCHDT        SVCHDT
     C     *LIKE         DEFINE    CDCHNO        SVCHNO
      *
      * JDE FIELDS
     C                   MOVE      *BLANK        JDEFL             1            JDE ERR FLAG
     C                   MOVE      *BLANK        INAT1             3            JDE ADDR TYP
     C                   MOVE      *BLANK        OUALPH           40            JDE NAME
     C     *LIKE         DEFINE    SHCVNO        INAJD                          JDE ADDR IN
     C     *LIKE         DEFINE    SHCVNO        OUAJD                          JDR ADDR OUT
      *
     C                   MOVEL     NO            WKEDIT            1            SFL RECORDS
      *
      * TO SUPPORT F5-REFRESH, LOGIC REQUIRES THE NUMBER OF LINES ON
      * THE SUBFILE PAGE AND ALSO THE NUMBER OF LINES LESS 1.  BY
      * SETTING THESE VALUES UP AS CONSTANT WORK FIELDS, THE PROCESSING
      * LOGIC CAN BE STANDARDIZED BETWEEN PROGRAMS.
      *
     C                   Z-ADD     04            WKPLIN            2 0          SFLPAG
     C     WKPLIN        SUB       1             WKRLIN            2 0          SFLPAG - 1
     C                   Z-ADD     0             COUNT             2 0
     C     *LIKE         DEFINE    COUNT         WKDIFF
     C     *LIKE         DEFINE    COUNT         WKRMDR
     C     *LIKE         DEFINE    COUNT         WKFLD
      *
      * THIS PROGRAM DIFFERS FROM THE SHELL PROGRAMS IN THAT F5=REFRESH
      * ALWAYS PLACES THE USER AT THE FIRST PAGE OF THE SUBFILE
      *
     C                   Z-ADD     WKPLIN        MAXRRN
      *
      * THE USER CAN COME INTO THIS PROGRAM SEVERAL DIFFERENT WAYS.
      * CHECK THE ENTRY MODE AND SET THE APPROPRIATE INDICATORS TO
      * CONTROL THE SCREEN DISPLAY.
      *
     C                   SELECT
     C     MODE          WHENEQ    CREATE
     C                   MOVEL     CREATE        DFMODE
     C                   SETON                                        61
      *
     C     MODE          WHENEQ    REVISE
     C                   MOVEL     REVISE        DFMODE
     C                   SETON                                        60
     C                   ENDSL
      *
      * THE SYSTEM DATE IS REQUIRED IN CCYYMMDD FORMAT FOR
      * EDITING AND UPDATING; SO, FLIP THE DATE.
      *
     C                   Z-ADD     UDATE         PFRM8
     C                   EXSR      $DATE
     C     PRTRN         IFEQ      *BLANK                                       IF VALID
     C                   Z-ADD     PTO8          WKUDAT            8 0
     C                   ELSE
     C                   Z-ADD     0             WKUDAT
     C                   ENDIF                                                  IF VALID
      *
      * BRING IN THE DATA AREA THAT CONTAINS THE CURRENT ACCOUNTING
      * PERIOD
      *
     C     *DTAARA       DEFINE                  DAAPER
     C                   IN        *DTAARA
      *
      * DEFINE THE DATA AREA THAT CONTAINS THE MOVEMENT NUMBER
      *
     C     *DTAARA       DEFINE                  DAMVSN
      *
      * THE FOLLOWING STANDARD CODE MUST BE INCLUDED TO MAKE THE
      * STANDARD ERROR MESSAGE HANDLING PROGRAM FUNCTION PROPERLY.
      * THIS CODE SETS AND CLEARS THE PROGRAM MESSAGE QUEUE.
      *
     C                   Z-ADD     20            MAXMSG            2 0
     C                   MOVEL     '*'           MSGPGM                         SET QUEUE
     C                   EXSR      $CLMSG                                       CLEAR MSG
      *
     C                   ENDSR
      /EJECT
