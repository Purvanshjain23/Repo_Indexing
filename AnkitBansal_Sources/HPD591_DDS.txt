     A*%%TS  SD  20030113  075512  ISLRAMS     REL-V5R1M0  5722-WDS
     A*****************************************************************
     A*
     A*  SYSTEM:        HOG PRODUCTION
     A*  DISPLAY FILE:  HPD591 - SELECT BGF FARM SITE WITH FARM SITE
     A*                          POSITIONER
     A*  CREATED BY:    LEANNE FEDOR
     A*  DATE:          03/13/95
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA12(12 'PREVIOUS')
     A N82                                  ROLLUP(25 'PAGE FORWARD')
     A*--------------------------------------------------------------*
     A* REFERENCE WINDOW RECORD FORMAT 1
     A*--------------------------------------------------------------*
     A          R HP591R1
     A*%%TS  SD  20010521  165911  ISLRAMS     REL-V4R4M0  5769-PW1
     A                                      TEXT('Reference window')
     A                                      WINDOW(*DFT 17 50)
     A                                      WDWBORDER((*DSPATR RI) (*CHAR '    -
     A                                          '))
     A*--------------------------------------------------------------*
     A* RECORD FORMAT ONE
     A*--------------------------------------------------------------*
     A          R HP591R2
     A                                      TEXT('Written in RPG pgm to prevent-
     A                                       screen from clearing')
     A                                      CLRL(*NO)
     A                                      OVERLAY
     A*--------------------------------------------------------------*
     A* RECORD FORMAT TWO
     A*--------------------------------------------------------------*
     A* THIS FORMAT IS NOT WRITTEN IN THE RPG PGM.  IT PREVENTS THE
     A* CALLING_PROGRAM_SCREEN FROM BEING CLEARED.
     A*--------------------------------------------------------------*
     A          R HP591R3
     A                                      TEXT('Dummy format not written in R-
     A                                      PG pgm.')
     A                                      ASSUME
     A                                 11  1' '
     A*--------------------------------------------------------------*
     A* SUBFILE FORMAT ONE
     A*--------------------------------------------------------------
     A          R HP591S1                   SFL
     A*%%TS  SD  20030113  075512  ISLRAMS     REL-V5R1M0  5722-WDS
     A                                      TEXT('subfile record format one')
     A            HDRRN1         4S 0H
     A            DFFSCD    R        O  7  1REFFLD(HSRREF/HGFSCD *LIBL/HSPREF)
     A                                      EDTCDE(M)
     A            DFFSNM    R        O  7 10REFFLD(HSRREF/FSFSNM *LIBL/HSPREF)
     A            DFCELL    R        O  7 37REFFLD(HSRREF/@@CELL *LIBL/HSPREF)
     A                                      EDTCDE(Z)
     A            DFFSBO    R        O  7 44REFFLD(HSRREF/HGFSBO *LIBL/HSPREF)
     A*--------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*--------------------------------------------------------------
     A          R HP591C1                   SFLCTL(HP591S1)
     A*%%TS  SD  20030113  075512  ISLRAMS     REL-V5R1M0  5722-WDS
     A                                      SFLSIZ(0050)
     A                                      SFLPAG(0008)
     A                                      WINDOW(HP591R1)
     A                                      TEXT('control format one')
     A                                      OVERLAY
     A                                      SFLCSRRRN(&HDRRN)
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND(*MORE)
     A                                      RMVWDW
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            HDRRN          5S 0H
     A                                  1 16'SELECT BGF FARM SITE'
     A                                      DSPATR(HI)
     A                                  2  1'Position Cursor, Press Enter'
     A                                  4  2'Farm'
     A                                      DSPATR(HI)
     A                                  4 43'Business'
     A                                      DSPATR(HI)
     A                                  5  2'Site'
     A                                      DSPATR(HI)
     A                                  5 10'Name'
     A                                      DSPATR(HI)
     A                                  5 36'Cell'
     A                                      DSPATR(HI)
     A                                  5 44'Office'
     A                                      DSPATR(HI)
     A            C1FSCD    R        B  6  1REFFLD(HSRREF/HGFSCD *LIBL/HSPREF)
     A                                      EDTCDE(M)
     A                                      DSPATR(HI)
     A            C1CELL    R        B  6 37REFFLD(HSRREF/@@CELL *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A                                      EDTCDE(M)
     A            C1FSBO    R        B  6 44REFFLD(HSRREF/HGFSBO *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A          R HP591K1
     A*%%TS  SD  19940511  110146  ISRCENT     REL-V2R2M0  5738-PW1
     A                                      OVERLAY
     A                                      WINDOW(HP591R1)
     A                                 16  1'F12=Previous'
