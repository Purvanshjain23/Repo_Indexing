/********************************************************************/
/*                                                                  */
/*  SYNOPSIS -  JOB CONTROL PROMPT FOR PRINTER OVERRIDE INFORMATION */
/*              GENERIC PROMPTING FUNCTION. CAUSE PRINTER OVERRIDE  */
/*              PROMPTING AND PASS PARMS FOR SALES DETAIL BY ITEM   */
/*              STRUCTURE.                                          */
/*                                                                  */
/*  SYSTEM    - ORDER MANAGEMENT SYSTEM                             */
/*  PROGRAM   - PRINT REPORTS                                       */
/*  DATE      - 07/19/95                                            */
/*                                                                  */
/*      PARAMETERS:                                                 */
/*                                                                  */
/*     O    &RETURN     - RETURN CODE                               */
/*     I    &APPLIC     - APPLICATION CODE                          */
/*     I    &CLP        - CL PROGRAM TO SUBMIT                      */
/*     I    &PGM        - RPG PROGRAM TO SUBMIT                     */
/*     I    &DESC       - PRINT PROGRAM DESCRIPTION (FOR PROMPT     */
/*                         SCREEN)                                  */
/*     B    &OUTQ       -  OUTPUT QUEUE                             */
/*                         IF THIS IS BLANK AT START, PROMPT        */
/*                         PGM IS CALLED; OTHERWISE, THE PRINT CL   */
/*                         IS SUBMITTED WITHOUT PROMPTING           */
/*     B    &HOLD       -  HOLD STATUS                              */
/*     B    &SAVE       -  SAVE STATUS                              */
/*     B    &NQSTAT     -  INDICATOR WHETHER MUST RUN IN NIGHT Q    */
/*     B    &NIGHTQ     -  NIGHT JOB QUEUE                          */
/*     B    &COMPNYN    - COMPANY NUMBER (PASSED)                   */
/*     B    &FRMDATN    - FROM DATE      (PASSED)                   */
/*     B    &THRDATN    - THRU DATE      (PASSED)                   */
/*     B    &SLSTYP     - SALES TYPE     (PASSED)                   */
/*     B    &SLSGRP     - SALES GROUP    (PASSED)                   */
/*     B    &SLSCLS     - SALES CLASS    (PASSED)                   */
/*     B    &COPYN      - NO. OF COPIES  (PASSED)                   */
/*     B    &DTLSUM     - DETAIL OR SUMMARY FLAG                    */
/*     B    &EXPSLS     - EXPORT SALES                              */
/*     B    &FLAG1      - INCLUDE ALL PRODUCTS                      */
/*     B    &FLAG2      - INCLUDE CM/DM  (PASSED)                   */
/********************************************************************/
/* 8/15/00  RMC  ADDED FTP STATUS AND DOCUMENT TO PARMS           */
/* 2/22/02  RMC  PASS IN WAREHOUSE TYPE AND WHSE CODE PARMS        */
/* 10/03/03 RMC  PASS IN ITEM CODE                                */
/* 02/08/06 RMC  PASS IN 'PRT CUSTOMER # NAME" PARM &PRTCUS       */
/* 01/02/08 RMC  PASS IN EMAIL ADDRESS - PASS  2-25 POS FIELDS  */
/********************************************************************/

 PDT9UPR:    PGM        PARM(&RETURN &APPLIC &CLP &PGM &DESC &OUTQ +
                          &HOLD &SAVE &NQSTAT &NIGHTQ &COMPNYN +
                          &FRMDATN &THRDATN &SLSTYP &SLSGRP &SLSCLS +
                    &COPYN &ITEM &DTLSUM &EXPSLS &FTP  &FLAG1 &FLAG2 +
                          &SALRT &DOCUM &PRTCUS &WHSTYP &WHSCOD &EMAIL1 &EMAIL2)
/* PARMS                                                                      */
        DCL    &RETURN     *CHAR   (7)           /* Return Code               */
        DCL    &APPLIC     *CHAR   (06)          /* Application Code          */
        DCL    &CLP        *CHAR   (10)          /* CL to Submit              */
        DCL    &PGM        *CHAR   (10)          /* RPG TO SUBMIT             */
        DCL    &DESC       *CHAR   (25)          /* PRT Pgm Description       */
        DCL    &COPYN      *DEC                  /* No. of Copies             */
        DCL    &OUTQ       *CHAR   (10)          /* Output Queue              */
        DCL    &HOLD       *CHAR   (4)           /* Hold Status               */
        DCL    &SAVE       *CHAR   (4)           /* Save Status               */
        DCL    &NQSTAT     *CHAR   (01)          /* If MUST run in Night Q    */
        DCL    &NIGHTQ     *CHAR   (10)          /* Night Job Queue           */
        DCL    &COMPNYN    *DEC                  /* Company Number (Passed)   */
        DCL    &FRMDATN    *DEC                  /* FROM DATE      (Passed)   */
        DCL    &THRDATN    *DEC                  /* THROUGH DATE   (Passed)   */
        DCL    &SLSTYP     *DEC                  /* SALES TYPE     (Passed)   */
        DCL    &SLSGRP     *DEC                  /* SALES GROUP    (Passed)   */
        DCL    &SLSCLS     *DEC                  /* SALES CLASS    (Passed)   */
        DCL    &ITEM       *DEC                  /* SALES CLASS    (Passed)   */
        DCL    &DTLSUM     *CHAR    (1)
        DCL    &PRTCUS     *CHAR    (1)
        DCL    &EXPSLS     *CHAR    (1)
        DCL    &FLAG1      *CHAR    (1)
        DCL    &FLAG2      *CHAR    (1)
        DCL    &SALRT      *CHAR    (3)
        DCL    &FTP        *CHAR    (1)
        DCL    &DOCUM      *CHAR    (8)
        DCL    &WHSTYP     *CHAR   (2)           /* WAREHOUSE TYPE            */
        DCL    &WHSCOD     *CHAR   (3)           /* WAREHOUSE CODE            */
        DCL    &EMAIL1     *CHAR   (25)          /* EMAIL ADDR - 1ST 25 POS.  */
        DCL    &EMAIL2     *CHAR   (25)          /* EMAIL ADDR - 2ND 25 POS.  */
/*  HERE  */
        DCL    &PAPER      *CHAR    (1)
        DCL    &HOLDQ      *CHAR    (1)
        DCL    &JOBL       *CHAR    (1)
        DCL    &HOLDL      *CHAR    (1)
        DCL    &LOG        *CHAR    (1)

/* INTERNAL FIELDS                                                            */
        DCL    &COPY       *DEC    (2 0)         /* Number of copies          */

/* ERROR MESSAGE FIELDS                                                       */
        DCL    &MSG        *CHAR   (512)         /* message                   */
        DCL    &MSGID      *CHAR   (7)           /* message id                */
        DCL    &MSGL       *DEC    (5 0)

/********************************************************************/
/*   INITIALIZE VARIABLES                                           */
/********************************************************************/

             CHGVAR     VAR(&RETURN) VALUE('       ')
             CHGVAR     VAR(&COPY) VALUE(&COPYN)
/********************************************************************/
/*   MONITOR FOR ALL MESSAGES AT PROGRAM LEVEL                      */
/********************************************************************/

             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(MSGTRAP))

/********************************************************************/
/*   CALL PROMPT PROGRAM                                            */
/********************************************************************/
 CHGVAR &PAPER VALUE('2')
 CHGVAR &COPY VALUE(01)

             CALL       PGM(PDV8PVR) PARM(&RETURN &DESC &OUTQ &HOLD +
                          &SAVE &COPY &NQSTAT &NIGHTQ &APPLIC &PAPER)
             CHGVAR     VAR(&COPYN) VALUE(&COPY)
             IF         COND(&RETURN *NE ' ') THEN(GOTO +
                          CMDLBL(USERQUIT))

/********************************************************************/
/*   SUBMIT PRINT CL PROGRAM                                        */
/********************************************************************/

/*           CALL PGM(&CLP) PARM(&PGM &COMPNYN +                  */
/*                        &FRMDATN &THRDATN &SLSTYP &SLSGRP +     */
/*                        &SLSCLS &OUTQ &HOLD &SAVE &COPYN +      */
/*                        &NIGHTQ &DTLSUM &EXPSLS &FLAG1 &FLAG2 + */
/*                        &SALRT)                                 */
 SUBMIT:     SBMJOB     CMD(CALL PGM(&CLP) PARM(&PGM &COMPNYN +
                          &FRMDATN &THRDATN &SLSTYP &SLSGRP &SLSCLS +
                          &ITEM &OUTQ &HOLD &SAVE &COPYN &NIGHTQ +
                          &DTLSUM &EXPSLS &FLAG1 &FLAG2 &SALRT &FTP +
                          &DOCUM &WHSTYP &WHSCOD &PRTCUS &EMAIL1 &EMAIL2)) +
                          JOB(&PGM) JOBQ(&NIGHTQ)
             GOTO       CMDLBL(END)

/*-------------------------USER QUIT--------------------------------*/
 USERQUIT:   CHGVAR     VAR(&RETURN) VALUE('Y2U0005')
             GOTO       CMDLBL(END)

/*----------------------RECEIVE MESSAGES----------------------------*/

 MSGTRAP:    RCVMSG     MSGTYPE(*EXCP) MSGDTA(&MSG) MSGDTALEN(&MSGL) +
                          MSGID(&MSGID)
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(END))

             SNDPGMMSG  MSGID(&MSGID) MSGF(QCPFMSG) MSGDTA(%SST(&MSG +
                          1 &MSGL)) MSGTYPE(*ESCAPE)
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(END))

 END:        ENDPGM

