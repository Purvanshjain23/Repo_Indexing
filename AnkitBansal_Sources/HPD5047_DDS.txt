     A*%%TS  SD  20040915  110702  ISLRAMS     REL-V5R2M0  5722-WDS
     A*****************************************************************
     A*
     A*  SYSTEM:        HOG PRODUCTION
     A*  DISPLAY FILE:  HPD5047 - SELECT SCHEDULED TRANSFER MOVEMENT
     A*  CREATED BY:    LEANNE FEDOR
     A*  DATE:          09/14/04
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      PRINT
     A                                      CF04(04 'PROMPT')
     A                                      CA12(12 'PREVIOUS')
     A N82                                  ROLLUP(25 'PAGE FORWARD')
     A*---------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP5047S1                  SFL
     A*%%TS  SD  20040914  155102  ISLRAMS     REL-V5R2M0  5722-WDS
     A            HDRRN1    R        H      REFFLD(@@RRN)
     A            D1SCMDY   R        O  9  1REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  ')
     A            D1MVSN    R        O  9 11REFFLD(@@MVSN)
     A                                      EDTCDE(M)
     A            D1ORTOCD  R        O  9 20REFFLD(@@TOCD)
     A            D1ORPTCD  R        O  9 26REFFLD(@@PTCD)
     A            D1ORPPCD  R        O  9 32REFFLD(@@PPCD)
     A            D1ORCELL  R        O  9 39REFFLD(HSRREF/@@CELL *LIBL/HSPREF)
     A                                      EDTCDE(Z)
     A            D1ORFSCD  R        O  9 43REFFLD(@@FSCD)
     A                                      EDTCDE(Z)
     A            D1DNPTCD  R        O  9 51REFFLD(@@PTCD)
     A            D1DNPPCD  R        O  9 57REFFLD(@@PPCD)
     A            D1DNFSCD  R        O  9 63REFFLD(@@FSCD)
     A                                      EDTCDE(Z)
     A            D1PTCT    R        O  9 71REFFLD(HSRREF/@@PTCT *LIBL/HSPREF)
     A                                      EDTCDE(Z)
     A*
     A*---------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP5047C1                  SFLCTL(HP5047S1)
     A*%%TS  SD  20040914  160450  ISLRAMS     REL-V5R2M0  5722-WDS
     A                                      SFLSIZ(0100)
     A                                      SFLPAG(0009)
     A                                      WINDOW(HP5047EC)
     A                                      SETOF(51)
     A                                      SETOF(52)
     A                                      SETOF(53)
     A                                      SETOF(54)
     A                                      SETOF(55)
     A                                      SETOF(56)
     A                                      SETOF(57)
     A                                      RTNCSRLOC(&RCD &FLD)
     A                                      OVERLAY
     A                                      SFLCSRRRN(&HDRRN)
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A            HDPAG1    R        H      REFFLD(@@PAG)
     A                                      SFLRCDNBR
     A            RCD           10A  H
     A            FLD           10A  H
     A            HDRRN     R        H      REFFLD(@@RRN)
     A            SDPGM     R        O  1  1REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 22'SELECT SCHEDULED TRANSFER MOVEMENT'
     A                                      DSPATR(HI)
     A                                  3  1'Position Cursor, Press Enter'
     A                                  4 22'?'
     A                                      DSPATR(HI)
     A                                  4 27'?'
     A                                      DSPATR(HI)
     A                                  4 33'?'
     A                                      DSPATR(HI)
     A                                  4 45'?'
     A                                      DSPATR(HI)
     A                                  4 53'?'
     A                                      DSPATR(HI)
     A                                  4 58'?'
     A                                      DSPATR(HI)
     A                                  4 65'?'
     A                                      DSPATR(HI)
     A                                  5 20'---------- Origin ----------'
     A                                      DSPATR(HI)
     A                                  5 51'-- Destination --'
     A                                      DSPATR(HI)
     A                                  6  1'Scheduled'
     A                                      DSPATR(HI)
     A                                  6 11'Movemnt'
     A                                      DSPATR(HI)
     A                                  6 20'Tran'
     A                                      DSPATR(HI)
     A                                  6 27'Production'
     A                                      DSPATR(HI)
     A                                  6 44'Farm'
     A                                      DSPATR(HI)
     A                                  6 52'Production'
     A                                      DSPATR(HI)
     A                                  6 64'Farm'
     A                                      DSPATR(HI)
     A                                  6 71'Print'
     A                                      DSPATR(HI)
     A                                  7  1'Ship Date'
     A                                      DSPATR(HI)
     A                                  7 12'Number'
     A                                      DSPATR(HI)
     A                                  7 20'Offce'
     A                                      DSPATR(HI)
     A                                  7 26'Type'
     A                                      DSPATR(HI)
     A                                  7 32'Phase'
     A                                      DSPATR(HI)
     A                                  7 38'Cell'
     A                                      DSPATR(HI)
     A                                  7 44'Site'
     A                                      DSPATR(HI)
     A                                  7 51'Type'
     A                                      DSPATR(HI)
     A                                  7 57'Phase'
     A                                      DSPATR(HI)
     A                                  7 64'Site'
     A                                      DSPATR(HI)
     A                                  7 71'Count'
     A                                      DSPATR(HI)
     A*
     A            C1SCMDY   R        B  8  1REFFLD(@@DT06)
     A                                      DSPATR(HI)
     A                                      EDTWRD('  /  /  -')
     A            C1ORTOCD  R        B  8 20REFFLD(HSRREF/@@TOCD *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A  51                                  DSPATR(PC)
     A            C1ORPTCD  R        B  8 26REFFLD(@@PTCD)
     A                                      DSPATR(HI)
     A  52                                  DSPATR(PC)
     A            C1ORPPCD  R        B  8 32REFFLD(@@PPCD)
     A                                      DSPATR(HI)
     A  53                                  DSPATR(PC)
     A            C1ORCELL  R        B  8 39REFFLD(HSRREF/@@CELL *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A            C1ORFSCD  R        B  8 43REFFLD(@@FSCD)
     A  54                                  DSPATR(PC)
     A                                      DSPATR(HI)
     A                                      EDTCDE(M)
     A            C1DNPTCD  R        B  8 51REFFLD(@@PTCD)
     A                                      DSPATR(HI)
     A  55                                  DSPATR(PC)
     A            C1DNPPCD  R        B  8 57REFFLD(@@PPCD)
     A                                      DSPATR(HI)
     A  56                                  DSPATR(PC)
     A            C1DNFSCD  R        B  8 63REFFLD(@@FSCD)
     A                                      DSPATR(HI)
     A  57                                  DSPATR(PC)
     A                                      EDTCDE(M)
     A*---------------------------------------------------------------
     A* COMMAND LINE LINE
     A*---------------------------------------------------------------
     A          R HP5047K1
     A                                      WINDOW(HP5047EC)
     A                                      OVERLAY
     A  43                             18  1'Message program failed. Call IS.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 19  1'F4=Prompt'
     A                                 19 12'F12=Previous'
      *
     A*-------------------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
     A*-------------------------------------------------------------------------
     A          R HP5047ES                  SFL SFLMSGRCD(19)
      *
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
     A*
     A*-------------------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*-------------------------------------------------------------------------
     A*
     A          R HP5047EC                  SFLCTL(HP5047ES)
     A*%%TS  SD  19980327  105628  ISLRAMS     REL-V3R7M0  5716-PW1
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A                                      WINDOW(*DFT 20 75)
     A                                      WDWBORDER((*DSPATR RI) (*CHAR '    -
     A                                          '))
     A            MSGPGM                    SFLPGMQ(10)
     A*
     A*---------------------------------------------------------------
     A* ASSUME FORMAT -- REQUIRED FOR WINDOWS
     A*---------------------------------------------------------------
     A          R ASSUME
     A*%%TS  SD  20011025  160559  ISLRAMS     REL-V4R4M0  5769-PW1
     A                                      ASSUME
     A                                      OVERLAY
     A                                 24 79' '
