     A*%%TS  SD  20090923  130520  ISLRAMS     REL-V5R4M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION
     A*  DISPLAY FILE:  HPD1131
     A*  TITLE:         REVISE FARM FEED TICKET OVERRIDE
     A*  PROGRAMMER:    LEANNE RAMSEY
     A*  DATE:          09/23/09
     A*
     A*  INDICATOR 60 IS ON WHEN IN REVISE MODE
     A*  INDICATOR 61 IS ON WHEN IN CREATE MODE
     A*  INDICATOR 62 IS ON WHEN IN DELETE MODE
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A N62                                  CF04(04 'PROMPT')
     A  60N62                               CF05(05 'REFRESH')
     A N62                                  CF09(09 'ACCEPT')
     A  60N62                               CF11(11 'DELETE')
     A*
     A*--------------------------------------------------------------*
     A* RECORD FORMAT ONE
     A*--------------------------------------------------------------*
     A          R HP1131R1
     A*%%TS  SD  20090923  130520  ISLRAMS     REL-V5R4M0  5722-WDS
     A                                      SETOF(31)
     A                                      SETOF(32)
     A                                      SETOF(33)
     A                                      SETOF(51)
     A                                      SETOF(52)
     A                                      SETOF(53)
     A                                      RTNCSRLOC(&RCD &FLD)
     A                                      OVERLAY
     A            RCD       R        H      REFFLD(@@RCD)
     A            FLD       R        H      REFFLD(@@FLD)
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 29'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A            D1MODE         6A  O  2  2DSPATR(HI)
     A                                  2 23'REVISE FARM FEED TICKET OVERRIDE'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  5  2'Farm site ..'
     A  61                              5 15'?'
     A            D1FFSCD   R        B  5 17REFFLD(@@FFSCD)
     A  61N31                               DSPATR(HI)
     A  31
     AO 51                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A  60
     AO 62                                  DSPATR(PR)
     A  61                                  DSPATR(UL)
     A            D1FFSNM   R        O  5 26REFFLD(HSRREF/FSFSNM *LIBL/HSPREF)
     A                                  7  2'Override to:'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                      COLOR(BLU)
     A                                  8  2'Farm site ..'
     A                                  8 15'?'
     A            D1TFSCD   R        B  8 17REFFLD(@@TFSCD)
     A N62N32                               DSPATR(HI)
     A  32
     AO 52                                  DSPATR(PC)
     A  32                                  DSPATR(RI)
     A  62                                  DSPATR(PR)
     A N62                                  DSPATR(UL)
     A            D1TFSNM   R        O  8 26REFFLD(HSRREF/FSFSNM *LIBL/HSPREF)
     A                                  9  2'Bin code ...'
     A                                  9 15'?'
     A            D1BNCD    R        B  9 17REFFLD(HSRREF/@@BNCD *LIBL/HSPREF)
     A N62N33                               DSPATR(HI)
     A  33
     AO 53                                  DSPATR(PC)
     A  33                                  DSPATR(RI)
     A  62                                  DSPATR(PR)
     A N62                                  DSPATR(UL)
     A*
     A*--------------------------------------------------------------*
     A* COMMAND KEY RECORD 1
     A*--------------------------------------------------------------*
     A          R HP1131K1
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A  61                             23 11'F4=Prompt'
     A N62N61                          23 23'F5=Refresh'
     A N62                             23 35'F9=Accept'
     A N62N61                          23 46'F11=Delete'
      *
      *--------------------------------------------------------------*
      * ERROR MESSAGE SUBFILE FORMAT
      *--------------------------------------------------------------*
     A          R HP1131ES                  SFL SFLMSGRCD(24)
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
      *--------------------------------------------------------------*
      * ERROR MESSAGE SUBFILE CONTROL FORMAT
      *--------------------------------------------------------------*
     A          R HP1131EC                  SFLCTL(HP1131ES)
      *
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
