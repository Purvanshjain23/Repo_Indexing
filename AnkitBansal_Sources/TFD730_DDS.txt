     A*%%TS  SD  20050308  162324  ISLRAMS     REL-V5R2M0  5722-WDS
     A*****************************************************************
     A*
     A*  SYSTEM:        TRIUMPH FOODS
     A*  DISPLAY FILE:  TFD730
     A*  TITLE:         VIEW CHARGE CODE
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  CREATED:       03/02/05
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/TFPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CF03(03 'EXIT')
     A                                      CF05(05 'REFRESH')
     A*
     A*-----------------------------------------------------------------
     A* RECORD FORMAT ONE
     A*-----------------------------------------------------------------
     A          R TF730R1
     A*%%TS  SD  20050308  162324  ISLRAMS     REL-V5R2M0  5722-WDS
     A                                      OVERLAY
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 30'VIEW CHARGE CODE'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USR)
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@JOB)
     A                                      DSPATR(HI)
     A                                  3  2'Charge code:'
     A            D1CCCD    R        O  3 15REFFLD(TFREF/@@CCCD *LIBL/TFPREF)
     A                                      DSPATR(HI)
     A            D1CCDS    R        O  3 29REFFLD(TFREF/@@CCDS *LIBL/TFPREF)
     A                                  4 29'User-controlled:'
     A                                      COLOR(PNK)
     A            D1UCFL    R        O  4 46REFFLD(TFREF/@@UCFL *LIBL/TFPREF)
     A                                      COLOR(PNK)
     A                                  6  2'Company ................:'
     A            D1CONO    R        O  6 29REFFLD(TFREF/@@CONO *LIBL/TFPREF)
     A                                      EDTCDE(Z)
     A                                      DSPATR(HI)
     A            D1CONM    R        O  6 39REFFLD(TFREF/@@CONM *LIBL/TFPREF)
     A                                  7  2'G/L code - debit  ......:'
     A            D1GLDBCD  R        O  7 29REFFLD(TFREF/@@GLDBCD *LIBL/TFPREF)
     A                                      DSPATR(HI)
     A            D1GLDBCDDSR        O  7 39REFFLD(TFREF/@@GLDBCDDS *LIBL/TFPRE-
     A                                      F)
     A                                  8  2'G/L code - credit ......:'
     A            D1GLCRCD  R        O  8 29REFFLD(TFREF/@@GLCRCD *LIBL/TFPREF)
     A                                      DSPATR(HI)
     A            D1GLCRCDDSR        O  8 39REFFLD(TFREF/@@GLCRCDDS *LIBL/TFPRE-
     A                                      F)
     A                                  9  2'Active/inactive status .:'
     A            D1AIST    R        O  9 29REFFLD(TFREF/@@AIST *LIBL/TFPREF)
     A                                      DSPATR(HI)
     A                                 11  2'Charge code frequency ..:'
     A            D1CFQCD   R        O 11 29REFFLD(TFREF/@@CFQCD *LIBL/TFPREF)
     A                                      DSPATR(HI)
     A            D1CFQDS   R        O 11 39REFFLD(TFREF/@@CFQDS *LIBL/TFPREF)
     A                                 12  2'Include in invoice total:'
     A            D1INFL    R        O 12 29REFFLD(TFREF/@@INFL *LIBL/TFPREF)
     A                                      DSPATR(HI)
     A                                 13  2'System module ..........:'
     A            D1SMCD    R        O 13 29REFFLD(TFREF/@@SMCD *LIBL/TFPREF)
     A                                      DSPATR(HI)
     A            D1SMDS    R        O 13 39REFFLD(TFREF/@@SMDS *LIBL/TFPREF)
     A*
     A*-----------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*-----------------------------------------------------------------
     A          R TF730K1
     A*%%TS  SD  20040706  113301  ISLRAMS     REL-V5R2M0  5722-WDS
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A                                 23 11'F5=Refresh'
      *
      *------------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *-----------------------------------------------------------------
     A          R TF730ES                   SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
      *------------------------------------------------------------------
      * ERROR MESSAGE SUBFILE CONTROL FORMAT
      *-----------------------------------------------------------------
     A          R TF730EC                   SFLCTL(TF730ES)
      *
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
