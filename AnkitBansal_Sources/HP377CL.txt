/***************************************************************** */
/*                                                                 */
/*  PROGRAM NUMBER:  HP377CL                                       */
/*  PROGRAM NAME:    CLOSE A PERIOD                                */
/*  PROGRAMMER:      LEANNE FEDOR                                  */
/*  CREATE DATE:     04/24/03                                      */
/*                                                                 */
/*  FUNCTION:        THIS CL IS CALLED FROM HP477CL.               */
/*                                                                 */
/*******************************************************************/
/* MODIFICATIONS:                                                  */
/*                                                                 */
/* 07/14/03  LEANNE FEDOR                                          */
/*           WE REWROTE HP696 TO BE BY GROUP INSTEAD OF FARM. SO,  */
/*           WE HAD TO MOVE IT DOWN IN THE RUN SO THAT THE NEW     */
/*           GROUP FILE OVERRIDE WOULD NOT SCREW UP EXISTING STUFF.*/
/*                                                                 */
/* 08/13/08  LeAnne Ramsey                                         */
/*           Moved the Royalty Payment logic to here from CLP      */
/*           HPBLDCD$ which was part of the Datamart FIN build.    */
/*                                                                 */
/* 02/29/16    Rose Centonze    E5290                              */
/*             Added company code parm and add to opnqry select    */
/*                                                                 */
/*******************************************************************/

             PGM        PARM(&ERROR &PPCOCD)

             DCL        VAR(&PPCOCD) TYPE(*CHAR) LEN(3)            /* E5290 */

             DCL        VAR(&CMD) TYPE(*CHAR) LEN(512)
             DCL        VAR(&USER) TYPE(*CHAR) LEN(10)
             DCL        VAR(&GRPPRF) TYPE(*CHAR) LEN(10)           /* E5290 */
             DCL        VAR(&ERROR) TYPE(*CHAR) LEN(1)
             DCL        VAR(&PASS) TYPE(*CHAR) LEN(1)

             DCL        VAR(&QDATA) TYPE(*CHAR) LEN(116)

             DCL        VAR(&LDBPDT) TYPE(*DEC) LEN(8)
             DCL        VAR(&LDEPDT) TYPE(*DEC) LEN(8)
             DCL        VAR(&LDEFFL) TYPE(*CHAR) LEN(1)
             DCL        VAR(&LDERFL) TYPE(*CHAR) LEN(1)

             DCL        VAR(&HOLD) TYPE(*CHAR) LEN(4)
             DCL        VAR(&SAVE) TYPE(*CHAR) LEN(4)
             DCL        VAR(&COPYS) TYPE(*DEC) LEN(2)
             DCL        VAR(&OUTQ) TYPE(*CHAR) LEN(10)

             CHGVAR     VAR(&LDBPDT) VALUE(%SST(*LDA 1 8))
             CHGVAR     VAR(&LDEPDT) VALUE(%SST(*LDA 9 8))
             CHGVAR     VAR(&LDEFFL) VALUE(%SST(*LDA 19 1))
             CHGVAR     VAR(&LDERFL) VALUE(%SST(*LDA 80 1))

             CHGVAR     VAR(&PASS) VALUE('L')


/*********************************************************************/
/*  CLEAR WORKFILES                                                  */
/*********************************************************************/

             CLRPFM     FILE(HSP320)
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(CANTCLEAR))

             CLRPFM     FILE(HSP339)
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(CANTCLEAR))

             CLRPFM     FILE(HSP395)
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(CANTCLEAR))

/*********************************************************************/
/*  ROLL UP COSTED MOVEMENTS INTO PERIOD COST RECORDS FOR THE PERIOD */
/*********************************************************************/
/*  SETUP OPNQRY STATEMENT FOR EXPENSE TICKETS                       */

HSL034L:     CHGVAR     VAR(&QDATA) VALUE(' ')
             CHGVAR     VAR(%SST(&QDATA 1 15)) VALUE('HGCOCD +
                          *EQ')
             CHGVAR     VAR(%SST(&QDATA 17 3)) VALUE(&PPCOCD)

             OVRDBF     FILE(HSL034L) SHARE(*YES)
             OPNQRYF    FILE((HSL034L)) QRYSLT(&QDATA) +
                          KEYFLD((HGCLDT)) SEQONLY(*YES)

             CALL       PGM(HP322)

             CLOF       OPNID(HSL034L)
             DLTOVR     FILE(HSL034L)

/*********************************************************************/
/*  SETUP OPNQRY STATEMENTS AND DO FILE OVERRIDES                    */
/*********************************************************************/

/*  SETUP OPNQRY STATEMENT FOR EXPENSE TICKETS                       */

             CHGVAR     VAR(&QDATA) VALUE(' ')
             CHGVAR     VAR(%SST(&QDATA 1 10)) VALUE('XTXEDT *GE')
             CHGVAR     VAR(%SST(&QDATA 12 8)) VALUE(&LDBPDT)
             CHGVAR     VAR(%SST(&QDATA 21 15)) VALUE('*AND XTXEDT +
                          *LE')
             CHGVAR     VAR(%SST(&QDATA 37 8)) VALUE(&LDEPDT)
             CHGVAR     VAR(%SST(&QDATA 46 15)) VALUE('*AND XTCOCD +
                          *EQ')                                 /* E5290  */
             CHGVAR     VAR(%SST(&QDATA 62 3)) VALUE(&PPCOCD)

             OVRDBF     FILE(HSP048) SHARE(*YES)
             OPNQRYF    FILE((HSP048)) QRYSLT(&QDATA) +
                          KEYFLD((XTFSCD) (XTRPCD) (XTXTSN)) +
                          SEQONLY(*YES)

/*  SETUP OPNQRY STATEMENT FOR FEED TICKET HEADER                 */

             CHGVAR     VAR(&QDATA) VALUE(' ')
             CHGVAR     VAR(%SST(&QDATA 1 10)) VALUE('FHTKDT *GE')
             CHGVAR     VAR(%SST(&QDATA 12 8)) VALUE(&LDBPDT)
             CHGVAR     VAR(%SST(&QDATA 21 15)) VALUE('*AND FHTKDT +
                          *LE')
             CHGVAR     VAR(%SST(&QDATA 37 8)) VALUE(&LDEPDT)

/*   SELECT COMPANY IN PARM    E5290   - JOIN BUS OFFICE DB TO GET COMPANY */

             CHGVAR     VAR(%SST(&QDATA 46 15)) VALUE('*AND BOCOCD +
                          *EQ')                                 /* E5290  */
             CHGVAR     VAR(%SST(&QDATA 62 3)) VALUE(&PPCOCD)

             OVRDBF     FILE(HSP037) SHARE(*YES)
/*           OPNQRYF    FILE((HSP037)) QRYSLT(&QDATA) +        ORIGINAL  +
                          KEYFLD((FHFSCD) (FHTKNO) (FHTRCD)) +
                          SEQONLY(*YES)                           */
             OPNQRYF    FILE((HSP037) (HSP009)) FORMAT(HSP037) +
                          QRYSLT(&QDATA) KEYFLD((FHFSCD) (FHTKNO) +
                          (FHTRCD)) JFLD((HSP037/FHFMBO +
                          HSP009/BOBOBO)) SEQONLY(*YES)


/*  SETUP OPNQRY STATEMENT FOR FEED TICKET DETAIL RECORDS         */

             CHGVAR     VAR(&QDATA) VALUE(' ')
             CHGVAR     VAR(%SST(&QDATA 1 10)) VALUE('FDTKDT *GE')
             CHGVAR     VAR(%SST(&QDATA 12 8)) VALUE(&LDBPDT)
             CHGVAR     VAR(%SST(&QDATA 21 15)) VALUE('*AND FDTKDT +
                          *LE')
             CHGVAR     VAR(%SST(&QDATA 37 8)) VALUE(&LDEPDT)

/*   SELECT COMPANY IN PARM    E5290   - JOIN BUS OFFICE DB TO GET COMPANY */

             CHGVAR     VAR(%SST(&QDATA 46 15)) VALUE('*AND BOCOCD +
                          *EQ')                                 /* E5290  */
             CHGVAR     VAR(%SST(&QDATA 62 3)) VALUE(&PPCOCD)

             OVRDBF     FILE(HSL038N) SHARE(*YES)
 /*          OPNQRYF    FILE((HSL038N)) QRYSLT(&QDATA) +         ORIGINAL +
                          KEYFLD(*FILE) SEQONLY(*YES)                    */
             OPNQRYF    FILE((HSL038N) (HSP009)) FORMAT(HSL038N) +
                          QRYSLT(&QDATA) KEYFLD((FDFSCD) (FDBLCD) +
                          (FDRMCD)) JFLD((HSL038N/FDFMBO +
                          HSP009/BOBOBO)) SEQONLY(*YES)


/*  SETUP OPNQRY STATEMENT FOR DISPOSED HOG GROUPS                */

             CHGVAR     VAR(&QDATA) VALUE(' ')
             CHGVAR     VAR(%SST(&QDATA 1 10)) VALUE('HGDSDT *GE')
             CHGVAR     VAR(%SST(&QDATA 12 8)) VALUE(&LDBPDT)
             CHGVAR     VAR(%SST(&QDATA 21 15)) VALUE('*AND HGDSDT +
                          *LE')
             CHGVAR     VAR(%SST(&QDATA 37 8)) VALUE(&LDEPDT)

/*   SELECT COMPANY IN PARM    E5290                                       */

             CHGVAR     VAR(%SST(&QDATA 46 15)) VALUE('*AND HGCOCD +
                          *EQ')                                 /* E5290  */
             CHGVAR     VAR(%SST(&QDATA 62 3)) VALUE(&PPCOCD)

             OVRDBF     FILE(HSL034G) SHARE(*YES)
             OPNQRYF    FILE((HSL034G)) QRYSLT(&QDATA) +
                          KEYFLD((HGHGCD)) SEQONLY(*YES)


/*  SETUP OPNQRY STATEMENT FOR MOVEMENT EVENT FILE                */

             CHGVAR     VAR(&QDATA) VALUE(' ')
             CHGVAR     VAR(%SST(&QDATA 1 10)) VALUE('MEMEDT *GE')
             CHGVAR     VAR(%SST(&QDATA 12 8)) VALUE(&LDBPDT)
             CHGVAR     VAR(%SST(&QDATA 21 15)) VALUE('*AND MEMEDT +
                          *LE')
             CHGVAR     VAR(%SST(&QDATA 37 8)) VALUE(&LDEPDT)

/*   SELECT COMPANY IN PARM    E5290   - JOIN SYS HOG GROUP TO GET COMPANY  */

             CHGVAR     VAR(%SST(&QDATA 46 15)) VALUE('*AND HGCOCD +
                          *EQ')                                 /* E5290  */
             CHGVAR     VAR(%SST(&QDATA 62 3)) VALUE(&PPCOCD)

             OVRDBF     FILE(HSP058) SHARE(*YES)
 /*          OPNQRYF    FILE((HSP058)) QRYSLT(&QDATA) +           ORIGINAL  +
                          KEYFLD((MEMVSN) (MEODFL *DESCEND) +
                          (MEHGSN)) SEQONLY(*YES)                      */
             OPNQRYF    FILE((HSP058) (HSL034D)) FORMAT(HSP058) +
                          QRYSLT(&QDATA) KEYFLD((MEMVSN) (MEODFL +
                          *DESCEND) (MEHGSN)) JFLD((MEHGSN HGHGSN)) +
                          SEQONLY(*YES)


/*********************************************************************/
/*  SET UP PRINT FILE OVERRIDES                                      */
/*********************************************************************/

/* AS OF MARCH 2004, USERS ASKED US TO ALWAYS PUT OUTPUT ON SAVE/HOLD */

             CHGVAR     VAR(&SAVE) VALUE(*YES)
             CHGVAR     VAR(&HOLD) VALUE(*YES)

/* SET PARAMETERS FROM LDA                                           */

COPYS:       CHGVAR     VAR(&COPYS) VALUE(%SST(*LDA 412 1))
             IF         COND(&COPYS = 0) THEN(CHGVAR VAR(&COPYS) +
                          VALUE(1))

OUTQ:        CHGVAR     VAR(&OUTQ) VALUE(%SST(*LDA 413 10))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(%SST(*LDA 401 10)))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(*JOB))

/*********************************************************************/
/*  CALL PROGRAM TO DO PRELIMINARY EDITS AND BUILD MOVEMENT WORKFILE */
/*********************************************************************/


             OVRPRTF    FILE(QPRINT) OUTQ(&OUTQ) COPIES(&COPYS) +
                          HOLD(&HOLD) SAVE(&SAVE) SHARE(*YES)
             CALL       PGM(HP320) PARM(&ERROR &PASS)

             DLTOVR     FILE(QPRINT)

             CLOF       OPNID(HSL034G)
             CLOF       OPNID(HSP058)

             DLTOVR     FILE(HSL034G)
             DLTOVR     FILE(HSP058)

/*********************************************************************/
/*  IF THERE ARE NO ERRORS, THEN PROCEED WITH PROCESSING             */
/*********************************************************************/

             CHGVAR     VAR(&LDERFL) VALUE(%SST(*LDA 80 1))
             IF         COND(&LDERFL *EQ 'N') THEN(DO)     /* IF NO ERRORS */


/********************************************************************/
/*  CALL PROGRAMS TO PRINT REPORTS                                  */
/********************************************************************/

/*------------------------------------------------------------------*/
/*  SETUP OPNQRY STATEMENT FOR GROUPS THAT WILL BE USED IN:         */
/*------------------------------------------------------------------*/
/*       1) HP695-INVENTORY RECAP AND                               */
/*       2) HP629-OPEN GROUP COST OF SALES REPORT                   */
/*  THE OPEN QUERY WILL:                                            */
/*         1) OMIT ALL "BGF" PRODUCTION PHASE GROUPS                */
/*         2) OMIT ALL HOG GROUPS WITH A STATUS OF VOIDED           */
/*         3) SELECT ONLY GROUPS THAT:                              */
/*             A) HAVE AN CLOSED DATE GREATER THAN THE EOP DATE OR  */
/*             B) HAVE A CLOSED DATE OF ZERO (IE: ARE STILL OPEN)   */
/* E5290       C) COMPANY MATCHES PARM                              */

             CHGVAR     VAR(&QDATA) VALUE(' ')
             CHGVAR     VAR(%SST(&QDATA 1 18)) VALUE('HGPPCD +
                          *NE "BGF  "')

             CHGVAR     VAR(%SST(&QDATA 20 20)) VALUE('*AND HGGSCD +
                          *NE "VD"')

             CHGVAR     VAR(%SST(&QDATA 41 16)) VALUE('*AND (HGCLDT +
                          *GT')
             CHGVAR     VAR(%SST(&QDATA 58 8)) VALUE(&LDEPDT)
             CHGVAR     VAR(%SST(&QDATA 67 15)) VALUE(' *OR HGCLDT +
                          *EQ')
             CHGVAR     VAR(%SST(&QDATA 83 8)) VALUE(00000000)
             CHGVAR     VAR(%SST(&QDATA 91 1)) VALUE(')')

/*   SELECT COMPANY IN PARM    E5290                                        */

             CHGVAR     VAR(%SST(&QDATA 93 15)) VALUE('*AND HGCOCD +
                          *EQ')                                 /* E5290  */
             CHGVAR     VAR(%SST(&QDATA 109 3)) VALUE(&PPCOCD)

/*  (NOTE: LEAVE OVERRIDE OF HOG GROUP FILE IN AFFECT UNTIL YOU HAVE */
/*         PRINTED BOTH VERSIONS OF HP695-INVENTORY RECAP REPORT) */

             OVRDBF     FILE(HSP034) SHARE(*YES)
             OPNQRYF    FILE((HSP034)) QRYSLT(&QDATA) +
                        KEYFLD((HGPPCD) (HGFSCD) (HGHGCD)) +
                          SEQONLY(*YES)

/*-------------------------------------------------------------------*/
/*  OPEN GROUP COST OF SALES REPORT                                  */
/*-------------------------------------------------------------------*/

             SBDPRTOVR  CMDVAR(&CMD) FILE(QPRINT) OUTQ(&OUTQ) +
                          WIDTH(198) LANDSCAPE(*YES) DRAWER(2) +
                          COPIES(&COPYS) HOLD(&HOLD) SAVE(&SAVE)
             CALL       PGM(QCMDEXC) PARM(&CMD 512)
             CALL       PGM(HP629)

/*-------------------------------------------------------------------*/
/*  CLOSED GROUP COST OF SALES REPORT                                */
/*-------------------------------------------------------------------*/
 /* E5290 USE OPEN QRY TO JOIN TO HOG GROUP TO GET THE COMPANY       */
 HSL055C:    CHGVAR     VAR(&QDATA) VALUE(' ')
             CHGVAR     VAR(%SST(&QDATA 1 15)) VALUE('HGCOCD +
                          *EQ')
             CHGVAR     VAR(%SST(&QDATA 17 3)) VALUE(&PPCOCD)

             OVRDBF     FILE(HSL055C) SHARE(*YES)
             OPNQRYF    FILE((HSL055C) (HSL034D)) FORMAT(HSL055C) +
                          QRYSLT(&QDATA) KEYFLD((PCACYR) (PCACPE) +
                          (PCPPCD) (PCFSCD)) JFLD((PCHGSN HGHGSN)) +
                          SEQONLY(*YES)

             CALL       PGM(HP631)

             CLOF       OPNID(HSL055C)
             DLTOVR     FILE(HSL055C)


/*--------------------------------------------------------------------*/
/* BUILD THE WORKFILE FOR BOTH VERSIONS OF THE INVENTORY RECAP REPORT */
/*--------------------------------------------------------------------*/

             CALL       PGM(HP395)

/* PRINT THE 'PHASE' VERSION OF THE INVENTORY RECAP REPORT  */

             CALL       PGM(HP695) PARM('PHASE')

/*  INVENTORY RECAP---STAGE VERSION (GROW FINISH GROUPS ONLY)  */

             OVRDBF     FILE(HSP395) SHARE(*YES)
             OPNQRYF    FILE((HSP395)) QRYSLT('WGPPCD = "GF   "') +
                          KEYFLD((WGNUMSTG) (WGTOTDAY)) +
                          SEQONLY(*YES)
             CALL       PGM(HP695) PARM('STAGE')

             CLOF       OPNID(HSP395)
             DLTOVR     FILE(HSP395)

             CLOF       OPNID(HSP034)
             DLTOVR     FILE(HSP034)


/*-------------------------------------------------------------------*/
/*  CLOSING PERIOD CHANGE-IN-INVENTORY REPORT                        */
/*-------------------------------------------------------------------*/

/*  ALWAYS OMIT HOG GROUPS WITH A STATUS OF VOIDED */

             CHGVAR     VAR(&QDATA) VALUE(' ')
             CHGVAR     VAR(%SST(&QDATA 1 15)) VALUE('HGGSCD +
                          *NE "VD"')

/*  ONLY SELECT GROUPS WHERE:                                    */
/*   1) CLOSE DATE IS ZERO OF ON/AFTER BEGINNING PERIOD DATE     */

             CHGVAR     VAR(%SST(&QDATA 17 16)) VALUE('*AND (HGCLDT +
                          *EQ')
             CHGVAR     VAR(%SST(&QDATA 34 8)) VALUE(00000000)
             CHGVAR     VAR(%SST(&QDATA 43 14)) VALUE('*OR HGCLDT +
                          *GE')
             CHGVAR     VAR(%SST(&QDATA 58 8)) VALUE(&LDBPDT)
             CHGVAR     VAR(%SST(&QDATA 66 1)) VALUE(')')

/*   SELECT COMPANY IN PARM    E5290                                        */

             CHGVAR     VAR(%SST(&QDATA 68 15)) VALUE('*AND HGCOCD +
                          *EQ')                                 /* E5290  */
             CHGVAR     VAR(%SST(&QDATA 84 3)) VALUE(&PPCOCD)

             OVRDBF     FILE(HSP034) SHARE(*YES)
             OPNQRYF    FILE((HSP034)) QRYSLT(&QDATA) +
                        KEYFLD((HGPPCD) (HGFSCD) (HGHGCD)) +
                          SEQONLY(*YES)

/*  PRINT THE REPORT TWICE                                    */
/*      1) ALL GROUPS                                         */
/*      2) UNMATCHED GROUPS (IE: GROUP PHASE <> FARM PHASE)   */

             CALL       PGM(HP696) PARM('A')
             CALL       PGM(HP696) PARM('U')

             CLOF       OPNID(HSP034)
             DLTOVR     FILE(HSP034)

             DLTOVR     FILE(QPRINT)


/*-------------------------------------------------------------------*/
/*  COSTED TRANSFERS INTO BGF FARMS                                  */
/*-------------------------------------------------------------------*/
             SBDPRTOVR  CMDVAR(&CMD) FILE(QPRINT) OUTQ(&OUTQ) +
                          WIDTH(132) LANDSCAPE(*PRTF) DRAWER(*PRTF) +
                          COPIES(&COPYS) HOLD(&HOLD) SAVE(&SAVE)
             CALL       PGM(QCMDEXC) PARM(&CMD 512)

             CALL       PGM(HP339)
             CALL       PGM(HP639)

             DLTOVR     FILE(QPRINT)


/*-------------------------------------------------------------------*/
/*  IF 'FINAL' RUN:                                                  */
/*     1) UPDATE HPS DATABASE                                        */
/*     2) SEND USER A MESSAGE SO HE CAN START LOOKING AT REPORTS     */
/*     3) GENERATE/DOWNLOAD ROYALTY PAYMENT SPREADSHEET              */
/*-------------------------------------------------------------------*/

             IF         COND(&LDEFFL *EQ 'F') THEN(DO)            /* If Final */

   /* E5290 USE OPEN QRY ALREADY SET FOR HSP037 AND HSP048  - PASS CO PARM*/

             CALL       PGM(HP323) PARM(&PPCOCD) /* Update database */

             RTVJOBA    USER(&USER)
             SNDMSG     MSG('***** The Period Close reports are +
                          ready. Final processing is in progress. +
                          *****') TOUSR(&USER)

/* Change the LDA to hold the Email Distribution List for Royalties  */
/* Call the Royalty CL program --                                             */
/*      E5290 -- *CAT THE COMPANY FOR THE CORRECT DIST LIST       */
   /*        CHGDTAARA  DTAARA(*LDA (300 50)) VALUE('HPS-ROYALTY.PAY')    */

    CHGDTAARA  DTAARA(*LDA (300 50)) VALUE('HPS-ROYALTY')
    CHGDTAARA  DTAARA(*LDA (311 3)) VALUE(&PPCOCD)
    RTVUSRPRF  GRPPRF(&GRPPRF)                              /*FOR TEST,DEV  */
    IF         COND(&GRPPRF *EQ SBDPGMR) THEN(DO)
    CHGDTAARA  DTAARA(*LDA (300 50)) VALUE('PRKDEVROY')
    ENDDO

             CALL       PGM(HP4057CL)
             ENDDO                                                /* If Final */

             ENDDO                                          /* IF NO ERRORS */

/*******************************************************************/
/*  DELETE REMAINING FILE OVERRIDES                                */
/*******************************************************************/

             CLOF       OPNID(HSP048)
             CLOF       OPNID(HSP037)

             DLTOVR     FILE(HSP048)
             DLTOVR     FILE(HSP037)

             GOTO       CMDLBL(EXIT)

 CANTCLEAR:
             RTVJOBA    USER(&USER)
             SNDMSG     MSG('***** Your CLOSE PERIOD function did +
                          not run. Please submit your job again. +
                          And, please call IS in Kansas City to +
                          alert them to the problem. Thank you. +
                          *****') TOUSR(&USER)
 EXIT:
             ENDPGM

