      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      HOG PRODUCTION
      * PROGRAM:     HP618
      * TITLE:       LISTING OF FARM SITES FOR A FEED DELIVERY ZONE
      * PROGRAMMER:  MARY PALMER
      * CREATED:     4/06/94
      *
      * FUNCTION: BATCH PROGRAM SUBMITTED FROM WORK WITH FEED DELIVERY
      *           ZONES (HP408).  A SEPARATE REPORT WILL PRINT FOR EACH
      *           FEED DELIVERY ZONE SELECTED.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 09/12/94  LEANNE FEDOR
      *           RECOMPILE ONLY. REMOVED NEXT MOVEMENT NUMBER FROM
      *           BUSINESS OFFICE FILE.
      *
      * 02/21/95  LEANNE FEDOR
      *           CHANGED TO USE LDA INSTEAD OF PARMS
      *
      * 04/20/95  LEANNE FEDOR
      *           CHANGED FARM SITE CODE FROM ALPHA TO NUMERIC
      *
      * 04/13/98  LeAnne Fedor
      *           Recompile only. New field 'state' added to farm site file.
      *
      * 06/05/01  LeAnne Fedor
      *           Recompile only. New field 'multisite' added to Farm Site file.
      *
      * 06/28/01  LeAnne Fedor
      *           Recompile only.
      *           Three fields (manager/supervisor/multisite) renamed in Farm Site file.
      *
      * 09/04/01  LeAnne Fedor
      *           Recompile only. New 'square feet' fields added to Farm Site file.
      *
      * 10/16/13  LeAnne Ramsey (E2831)
      *           Recompile only. Added field 'MTech Reference'.
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     FHSL018D   IF   E           K DISK
      *    FARM SITE                 (KEY: FSFMBO FSDZCD FSFSCD)
      *
     FQPRINT    O    F  132        PRINTER OFLIND(*INOF)
      *
      /EJECT
      ****************************************************************
      * INPUT SPECS
      ****************************************************************
      *
      *---------------------------------------------------------------
      * STANDARD PROGRAM STATUS DATA STRUCTURE
      *---------------------------------------------------------------
      *    EXTERNALLY DEFINED AS UTPGFR (RECORD FORMAT: PGMDSR)
     D PGMDS         ESDS                  EXTNAME(UTPGFR)
      *
      *---------------------------------------------------------------
      *  LDA -  WITH THE REQUESTED PRINT SELECTIONS
      *---------------------------------------------------------------
     D                UDS
     D  LDFMBO                 1      5
     D  LDBODS                 6     35
     D  LDDZCD                36     40
     D  LDDZDS                41     60
      *
      *---------------------------------------------------------------
      *  NAMED CONSTANTS
      *---------------------------------------------------------------
      *
     D YES             C                   CONST('Y')
     D NO              C                   CONST('N')
      *
      /EJECT
      ****************************************************************
      * MAIN LINE
      ****************************************************************
      *
      * PRINT HEADINGS FOR FIRST TIME THROUGH
      *
     C                   EXCEPT    HRHDR
     C     K1L018        SETLL     HSL018D
      *
      * READ FARM SITE RECORDS FOR SELECTED BUSINESS OFFICE AND FEED
      * DELIVERY ZONE
      *
     C     *IN92         DOUEQ     *ON                                          MAIN DO
     C     K1L018        READE     HSL018D                                92
     C     *IN92         IFEQ      *OFF                                         IF NOT EOF
      *
     C     *INOF         IFEQ      *ON
     C                   EXCEPT    HRHDR
     C                   ENDIF
      *
     C                   EXCEPT    DTLINE
     C                   MOVEL     YES           PRTFLG
     C                   ENDIF                                                  IF NOT EOF
     C                   ENDDO                                                  MAIN DO
      *
      *---------------------------------------------------------------
      * EOF PROCESSING
      *---------------------------------------------------------------
      *  IF NO RECORDS WERE PRINTED, PRINT STANDARD 'NO DATA..' MESSAGE
      *
     C     PRTFLG        IFEQ      NO
     C                   EXCEPT    NODATA
     C                   ENDIF
      *
     C                   SETON                                        LR
      /EJECT
      *---------------------------------------------------------------
      * *INZSR - INITIALIZATION SUBROUTINE
      *---------------------------------------------------------------
      *
     C     *INZSR        BEGSR
      *
      * KEY LIST FOR FARM SITE
      *
     C     K1L018        KLIST
     C                   KFLD                    LDFMBO
     C                   KFLD                    LDDZCD
      *
      * SET PRINT FLAG TO DETERMINE IF ANY RECORDS WERE PRINTED
      * SET UP HEADING UNDERLINE FIELD FOR PRINT
      * RETRIEVE TIME FOR REPORT HEADING
      *
     C                   MOVEL     NO            PRTFLG            1
     C                   MOVE      *ALL'-'       DASH             80
     C                   TIME                    RTIME             6 0
      *
     C                   ENDSR
      /EJECT
      *-------------------------------------------------------------
      * REPORT HEADING LINES
      *-------------------------------------------------------------
      *
     OQPRINT    E            HRHDR          1 01
     O                       SDPGM               10
     O                                           48 'HOG PRODUCTION SYSTEM'
     O                                           70 'DATE'
     O                       UDATE         Y     80
      *
     O          E            HRHDR          1
     O                       SDUSR               10
     O                                           34 'LISTING OF FARM SITES'
     O                                           59 'FOR A FEED DELIVERY ZONE'
     O                                           70 'TIME'
     O                       RTIME               80 '  :  :  '
      *
     O          E            HRHDR          1
     O                                           70 'PAGE'
     O                       PAGE          Z     80
      *
     O          E            HRHDR          1
     O                                           22 'BUSINESS OFFICE .....:'
     O                       LDFMBO              29
     O                       LDBODS              61
      *
     O          E            HRHDR          2
     O                                           22 'FEED DELIVERY ZONE ..:'
     O                       LDDZCD              29
     O                       LDDZDS              51
      *
     O          E            HRHDR          1
     O                                            5 'FARM'
     O                                           54 '--PRODUCTION--'
      *
     O          E            HRHDR          1
     O                                            5 'SITE'
     O                                           24 'FARM SITE NAME'
     O                                           54 'PHASE     TYPE'
      *
     O          E            HRHDR          1
     O                       DASH                80
      *
      *-------------------------------------------------------------
      * FARM SITE DETAIL LINE
      *-------------------------------------------------------------
      *
     O          E            DTLINE      1
     O                       FSFSCD        M      6
     O                       FSFSNM              35
     O                       FSPPCD              45
     O                       FSPTCD              55
      *
      *-------------------------------------------------------------
      * NO DATA MESSAGE LINE
      *-------------------------------------------------------------
      *
     O          E            NODATA      1
     O                                           19 'NO DATA MEETS YOUR'
     O                                           38 'SELECTION CRITERIA'
