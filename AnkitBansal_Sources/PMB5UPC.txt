/*********************************************************************/
/*                                                                   */
/*  SYNOPSIS -  BUILD WORK FILE IN QTEMP AND LOGICALS                */
/*              ADD RECORDS TO THE TEMP FILE                         */
/*                                                                   */
/*  SYSTEM    - ORDER MANAGEMENT                                     */
/*  PROGRAM   - PRINT BANK LETTER OF CREDIT FEES REPORT              */
/*  DATE      - 11/07/2006                                           */
/*                                                                   */
/*      PARAMETERS:                                                  */
/*                                                                   */
/*     I    &BANK       - BANK CODE                                  */
/*     I    &APPLIC     - APPLICATION CODE                           */
/*     I    &OUTQ       - OUTPUT QUEUE                               */
/*     I    &HOLD       - HOLD STATUS                                */
/*     I    &SAVE       - SAVE STATUS                                */
/*     I    &NQSTS      - NIGHT QUEUE STATUS                         */
/*     I    &NQ         - NIGHT QUEUE                                */
/*     I    &YEARN      - FISCAL YEAR - PASS                         */
/*     I    &FPERN      - FROM FISCAL PERIOD PASS                    */
/*     I    &TPERN      - THRU FISCAL PERIOD PASS                    */
/*     I    &FYEARN     - FROM FISCAL YEAR - PASS                    */
/*     I    &TYEARN     - THRU FISCAL DATE - PASS                    */
/*     I    &COPYN      - NO. OF COPIES (PASSED)                     */
/*     I    &DTLSUM     - DETAIL OR SUMMARY FLAG                     */
/*     I    &EMAILFMT   - EMAIL FORMAT                               */
/*     I    &EMAIL      - EMAIL ADDRESS                              */
/*********************************************************************/
/* MODIFICATIONS:                                                    */
/* 11/19/2008 LJB S00566  CHANGE PRKDEVQRY TO *LIBL                  */
/* 04/30/24 PIO  REPLACE OPNQRYF WITH OPNSQLF                        */
/*               ADDED NEEDED OVRDBF AROUND THE COMMAND              */
/*               CHANGE *AND TO AND, *EQ TO =, *GE TO >=, *LE TO <=  */
/*               *GT TO > IN &QDATA                                  */
/*********************************************************************/
 PMB5UPC:    PGM        PARM(&BANK &APPLIC &OUTQ &HOLD &SAVE &NQSTS +
                          &NQ &YEARN &FPERN &TPERN &FYEARN &TYEARN +
                          &COPYN &DTLSUM &EMAILFMT &EMAIL)

/* PARMS                                                             */
        DCL    &BANK       *CHAR   (6)           /* BANK CODE        */
        DCL    &APPLIC     *CHAR   (10)          /* APPLICATION CODE */
        DCL    &OUTQ       *CHAR   (10)          /* Output Queue     */
        DCL    &HOLD       *CHAR   (4)           /* Hold Status      */
        DCL    &SAVE       *CHAR   (4)           /* Save Status      */
        DCL    &NQSTS      *CHAR   (1)           /* NQ RUN STATUS    */
        DCL    &NQ         *CHAR   (7)           /* NIGHT QUEUE      */
        DCL    &YEARN      *DEC                  /* FISCAL YEAR PASS */
        DCL    &FPERN      *DEC                  /* FROM PERD PASS   */
        DCL    &TPERN      *DEC                  /* TO PERIOD PASS   */
        DCL    &FYEARN     *DEC                  /* FROM YEAR PASS   */
        DCL    &TYEARN     *DEC                  /* TO YEAR PASS     */
        DCL    &COPYN      *DEC                  /* # COPIES PASS    */
        DCL    &DTLSUM     *CHAR   (1)           /* DETAIL/SUMMARY   */
        DCL    &EMAIL      *CHAR   (50)          /* EMAIL ADDRESS    */
        DCL    &EMAILFMT   *CHAR   (5)           /* EMAIL FORMAT     */
        DCL    &TEXT       *CHAR   (50)          /* DESCRIPTION      */
        DCL    &PRTF       *CHAR   (10)          /* PRTF TO EMAIL    */

/* INTERNAL FIELDS - CHAR FIELDS ARE TO CONCATONATE THE QUERY STMT   */
        DCL    &RTNCDE     *CHAR   (7)          /* RETURN CODE       */
        DCL    &COPY       *DEC    (2 0)        /* Number of copies  */
        DCL    &YEAR       *DEC    (4 0)        /* FISCAL YEAR       */
        DCL    &FPER       *DEC    (2 0)        /* FISCAL PERIOD     */
        DCL    &TPER       *DEC    (2 0)        /* FISCAL PERIOD     */
        DCL    &FYEAR      *DEC    (4 0)        /* FISCAL YEAR       */
        DCL    &TYEAR      *DEC    (4 0)        /* FISCAL YEAR       */
        DCL    &YEARA      *CHAR   (4)          /* FISCAL YEAR       */
        DCL    &FPERA      *CHAR   (2)          /* FISCAL PERIOD     */
        DCL    &TPERA      *CHAR   (2)          /* FISCAL PERIOD     */
        DCL    &FYEARA     *CHAR   (4)          /* FISCAL YEAR       */
        DCL    &TYEARA     *CHAR   (4)          /* FISCAL YEAR       */

/* OPNQRY FILE STATEMENT PARAMETERS                                  */

        DCL     &A        *CHAR   20              /* QRY YEAR        */
        DCL     &B        *CHAR   25              /* QRY YEAR        */
        DCL     &C        *CHAR   25              /* QRY PERIOD      */
        DCL     &D        *CHAR   25              /* QRY PERIOD      */
        DCL     &E        *CHAR   20              /* QRY BANK CODE   */
        DCL     &H        *CHAR  200              /* QRY ALL         */
        DCL     &J        *CHAR  400              /* QRY             */
        DCL     &K        *CHAR  600              /* EXECUTE SQL     */

/*********************************************************************/
/* CONVERT 15.5 VARIABLES TO 2.0 & 4.0, CONVERT NUMERIC TO CHARACTER */
/*********************************************************************/
             MONMSG     MSGID(CPF0000 CPF9999)

/*********************************************************************/
/* CONVERT 15.5 VARIABLES TO 2.0 & 4.0, CONVERT NUMERIC TO CHARACTER */
/*********************************************************************/
             CHGVAR     VAR(&YEAR) VALUE(&YEARN)
             CHGVAR     VAR(&YEARA) VALUE(&YEAR)
             CHGVAR     VAR(&FPER) VALUE(&FPERN)
             CHGVAR     VAR(&FPERA) VALUE(&FPER)
             CHGVAR     VAR(&TPER) VALUE(&TPERN)
             CHGVAR     VAR(&TPERA) VALUE(&TPER)
             CHGVAR     VAR(&FYEAR) VALUE(&FYEARN)
             CHGVAR     VAR(&FYEARA) VALUE(&FYEAR)
             CHGVAR     VAR(&TYEAR) VALUE(&TYEARN)
             CHGVAR     VAR(&TYEARA) VALUE(&TYEAR)
             CHGVAR     VAR(&COPY) VALUE(&COPYN)
             CHGVAR     VAR(&RTNCDE) VALUE('       ')

/*-------------------------------------------------------------------*/
/* CREATE A DUPLICATE OF THE WORK AR DETAIL FILE IN QTEMP            */
/*-------------------------------------------------------------------*/
             CRTDUPOBJ  OBJ(PMACCPP) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP) DATA(*NO)
             MONMSG     MSGID(CPF0000)

/*-------------------------------------------------------------------*/
/* COPY A/R DETAIL RECORDS TO WORK FILE                              */
/* CALL PGM TO PUT THE FISCAL YEAR AND FISCAL PERIOD INTO THE RECS   */
/* ADD THE LOGICALS TO QTEMP                                         */
/*-------------------------------------------------------------------*/
             CPYF       FROMFILE(ARBECPP) TOFILE(PMACCPP) +
                          MBROPT(*REPLACE) FMTOPT(*NOCHK)

             CRTDUPOBJ  OBJ(PMACCPL0) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)
             MONMSG     MSGID(CPF0000)
             CRTDUPOBJ  OBJ(PMACCPL1) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)
             MONMSG     MSGID(CPF0000)
             CRTDUPOBJ  OBJ(PMACCPL2) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)
             MONMSG     MSGID(CPF0000)

/* ADD THE YEAR AND PERIOD TO THE FILE IN QTEMP                      */
             CALL       PGM(PMB7XFR) PARM(' ')
             MONMSG     MSGID(CPF0000)

/*********************************************************************/
/* CREATE OPEN QUERY FILE FOR FISCAL PERIOD RANGE REQUESTED AND      */
/*********************************************************************/
             IF         COND(&YEARN *EQ 0000) THEN(DO)
             CHGVAR     VAR(&A) VALUE('ACANNB >= ' *CAT +
                         &FYEARA)

    /*PIO    CHGVAR     VAR(&B) VALUE('*AND ACANNB <= ' *CAT + +
                         &TYEARA)                            */
    /*PIO*/  CHGVAR     VAR(&B) VALUE(' AND ACANNB <= ' *CAT +
                         &TYEARA)

    /*PIO    CHGVAR     VAR(&C) VALUE('*AND ACAMNB >= ' *CAT + +
                         &FPERA)                               */
    /*PIO*/  CHGVAR     VAR(&C) VALUE(' AND ACAMNB >= ' *CAT +
                         &FPERA)

    /*PIO    CHGVAR     VAR(&D) VALUE('*AND ACAMNB <= ' *CAT +  +
                         &TPERA)                               */
    /*PIO*/  CHGVAR     VAR(&D) VALUE(' AND ACAMNB <= ' *CAT +
                         &TPERA)

             CHGVAR     VAR(&H) VALUE(&A *BCAT &B *BCAT &C *BCAT &D)
             ENDDO

/*********************************************************************/
/* IF THE FISCAL YEAR WAS ENTERED, INCLUDE THAT IN THE OPEN          */
/* QUERY FILE STATEMENT                                              */
/*********************************************************************/
             IF         COND(&YEARN *NE 0000) THEN(DO)
             CHGVAR     VAR(&A) VALUE('ACANNB = ' *CAT &YEARA)
             CHGVAR     VAR(&H) VALUE(&A)
             ENDDO
/*********************************************************************/
/*   OVERRIDE THE FILE AND OPEN FOR THE QUERY SELECTION              */
/*********************************************************************/
   /*PIO     OVRDBF     FILE(PMACCPL2) SHARE(*YES)  +
             OPNQRYF    FILE(PMACCPL2) +            +
                        QRYSLT(&H) +                +
                        KEYFLD(*FILE)              */

   /*PIO*/    OVRDBF     FILE(PMACCPP) TOFILE(PMACCPP) LVLCHK(*NO)
             OPNSQLF    SQL('Select * from PMACCPP where  ' *BCAT +
                          &H      *BCAT ' Order by ACG4CD asc, +
                          ACANNB asc, ACAMNB asc, ACCPTX asc, +
                          ACUHCD asc') OPNID(PMACCPL2) +
                          OPTION(*ALL) SEQONLY(*NO) RCDFMT(@ACCPHI) +
                          ALWCPY(*IFRQD) /*PIO*/
    /*PIO*/  OVRDBF     FILE(PMACCPL2) TOFILE(PMACCPP) LVLCHK(*NO) +
                        OVRSCOPE(*JOB) SHARE(*YES) OPNSCOPE(*JOB)


/* FOR TEST ONLY   +
CPYF PMACCPP OMSDEVGEN/PMACCPPSAV MBROPT(*REPLACE) CRTFILE(*YES) */

/********************************************************************/
/* OVERRIDE THE PRINTER FILE. TO EMAIL, THE PRINTER FILE MUST BE    */
/* HOLD = *YES AND SAVE = *YES.                                     */
/********************************************************************/
             IF         COND(&EMAIL *NE '               ') THEN(DO)
             CHGVAR     VAR(&HOLD) VALUE(*YES)
             CHGVAR     VAR(&SAVE) VALUE(*YES)
             ENDDO

             OVRPRTF    FILE(PMB3PFR$) OUTQ(&OUTQ) COPIES(&COPY) +
                          HOLD(&HOLD) SAVE(&SAVE)

             CALL       PGM(PMB3PFR) PARM(&RTNCDE &BANK &APPLIC +
                          &OUTQ &HOLD &SAVE &NQSTS &NQ &YEARN +
                          &FPERN &TPERN &FYEARN &TYEARN &COPYN +
                          &DTLSUM)

/*-------------------------------------------------------------------*/
/* EMAIL THE REPORT IF REQUESTED                                     */
/*-------------------------------------------------------------------*/
             IF         COND(&EMAIL *NE '               ') THEN(DO)

/* EMAIL IF *XLS IS REQUESTED                                        */
/* EXCEL SPREADSHEET HAS TO BE CREATED                               */
             IF         COND(&EMAILFMT *EQ '*XLS') THEN(DO)

             IF         COND(&YEARN *NE 0000) THEN(DO)
             EXECUTE    VIEW(*LIBL/PMACCPPA) PCFMT(*XLS) +
                          REPLACE(*YES) RECIPIENT(&EMAIL) +
                          EMLMSG('Bank Letter of Credit Fees in +
                          *XLS') SETVAR((&BANK &BANK) (&YEAR &YEARA))
             ENDDO

             IF         COND(&YEARN *EQ 0000) THEN(DO)
             EXECUTE    VIEW(*LIBL/PMACCPPB) PCFMT(*XLS) +
                          REPLACE(*YES) RECIPIENT(&EMAIL) +
                          EMLMSG('Bank Letter of Credit Fees in +
                          *XLS') SETVAR((&BANK &BANK) (&FYEAR +
                          &FYEARA) (&TYEAR &TYEARA) (&FPER &FPERA) +
                          (&TPER &TPERA))
             ENDDO
             ENDDO

/* EMAIL IF *PDF OR *RTF ARE REQUESTED                               */
/* THESE WILL RECEIVE THE CREATED REPORT AS AN ATTACHMENT            */
             ELSE       CMD(DO)
             CHGVAR     VAR(&PRTF) VALUE('PMB3PFR$')
             CHGVAR     VAR(&TEXT) VALUE('Bank Letter of Credit Fees +
                          Report in' *BCAT &EMAILFMT)
             CALL       PGM(PMB8UPC) PARM(&PRTF &TEXT &EMAIL &EMAILFMT)
             ENDDO
             ENDDO

             CLOF       OPNID(PMACCPL2)
  /*PIO      DLTOVR     FILE(PMACCPL2)  */
             DLTOVR     FILE(PMACCPL2) LVL(*JOB) /*PIO*/

EXIT:  ENDPGM
