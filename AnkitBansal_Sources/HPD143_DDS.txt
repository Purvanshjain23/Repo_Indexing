     A*%%TS  SD  19950313  143137  ISLRAMS     REL-V3R0M5  5763-PW1
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION
     A*  DISPLAY FILE:  HPD143
     A*  TITLE:         REOPEN A VOIDED HOG GROUP
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  DATE:          10/24/94
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A                                      CA12(12 'PREVIOUS')
     A*
     A*---------------------------------------------------------------
     A* RECORD FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP143R1
     A*%%TS  SD  19950313  143137  ISLRAMS     REL-V3R0M5  5763-PW1
     A                                      OVERLAY
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 28'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A            DFMODE         7A  O  2  2DSPATR(HI)
     A                                  2 27'REOPEN A VOIDED HOG GROUP'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  4  2'Business office .......:'
     A            DFFSBO    R        O  4 28REFFLD(HGFSBO)
     A            DFBODS    R        O  4 38REFFLD(BOBODS)
     A                                  5  2'Hog group .............:'
     A            DFHGCD    R        O  5 28REFFLD(HGHGCD)
     A                                  6  2'Hog group number ......:'
     A            DFHGSN    R        O  6 28REFFLD(HGHGSN)
     A                                      EDTCDE(M)
     A                                  7  2'Group status ..........:'
     A            DFGSCD    R        O  7 28REFFLD(HGGSCD)
     A                                  8  2'Farm site .............:'
     A            DFFSCD    R        O  8 28REFFLD(HGFSCD)
     A                                      EDTCDE(M)
     A            DFFSNM    R        O  8 38REFFLD(FSFSNM)
     A                                  9  2'Building ..............:'
     A            DFBLCD    R        O  9 28REFFLD(HGBLCD)
     A            DFBLDS    R        O  9 38REFFLD(FBBLDS)
     A                                 10  2'Room ..................:'
     A            DFRMCD    R        O 10 28REFFLD(HGRMCD)
     A                                 12  2'Genetic line ..........:'
     A            DFGNCD    R        O 12 28REFFLD(HGGNCD)
     A                                 13  2'Breed stock ...........:'
     A            DFBSCD    R        O 13 28REFFLD(HGBSCD)
     A            DFBSDS    R        O 13 38REFFLD(BSBSDS)
     A                                 15  2'Date created ..........:'
     A            DFCRDT    R        O 15 28REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A                                 16  2'Date opened ...........:'
     A            DFOPDT    R        O 16 28REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A                                 17  2'Date voided ...........:'
     A            DFVDDT    R        O 17 28REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A*
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R HP143K1
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A                                 23 11'F12=Previous'
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *---------------------------------------------------------------
     A          R HP143ES                   SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE CONTROL FORMAT
      *---------------------------------------------------------------
     A          R HP143EC                   SFLCTL(HP143ES)
      *
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
