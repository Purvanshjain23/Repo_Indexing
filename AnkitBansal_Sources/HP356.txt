      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Hog Production System
      * PROGRAM:     Workfile: Grow Finish Groups--BGF Sources Total
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     11/13/02
      *
      * FUNCTION:    This function builds the summarized BGF Source line records for each
      *              grow finish group in the workfile GF Groups--BGF sources.
      *
      *              It calculates the % of total head received into the group that is
      *              represented by each origin BGF farm. An 'unknown farm' record is written
      *              for all unaccounted for head.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 10/15/13  LeAnne Ramsey (E2831)
      *           Recompile only. Added field 'MTech Reference'.
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
      *
     Fhsp018    if   e           k disk
      *  Farm site
      *
      *
     Fhsp324    if   e           k disk
      *  Workfile: Grow finish groups--BGF sources
      *
      *
     Fhsp356    uf a e           k disk
      *  Workfile: Grow finish groups--BGF source total
      *
      /EJECT
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      ****************************************************************
      * ARRAYS AND TABLES
      ****************************************************************
      *
     D arrfscd         s              5s 0 dim(50)
     D arrrchd         s              5s 0 dim(50)
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      *
      * Control fields
      *
     D first           s              1    inz('Y')
     D svdnhgsn        s                   like(w8dnhgsn)
     D svdnhgcd        s                   like(w8dnhgcd)
     D svdnppcd        s                   like(w8dnppcd)
     D svdnfscd        s                   like(w8dnfscd)
     D svdncell        s                   like(w8dncell)
      *
      *
      * Workfields
      *
     D wkhd            s                   like(w8rchd)
     D wkrchd          s                   like(w8rchd)
     D wkarrhd         s                   like(w8rchd)
     D wkpr            s             15  8
     D wkrempr         s              5  2
      *
     D wksysdt         s              8  0
      *
      *
      * Workfields for date manipulation
      *
     D wkcymdiso       s               D   datfmt(*iso)
      *
      *
      *
      * Define parms for the generic movement program
      *
     D zzcymd          s              8  0
      *
     D @@nu50          s              5  0
     D gpihd           s                   like(@@nu50)
     D pinhd           s                   like(@@nu50)
     D tinhd           s                   like(@@nu50)
     D tisphd          s                   like(@@nu50)
     D tidphd          s                   like(@@nu50)
     D pouhd           s                   like(@@nu50)
     D touhd           s                   like(@@nu50)
     D tosphd          s                   like(@@nu50)
     D todphd          s                   like(@@nu50)
     D rinhd           s                   like(@@nu50)
     D rouhd           s                   like(@@nu50)
     D qinhd           s                   like(@@nu50)
     D qouhd           s                   like(@@nu50)
     D morhd           s                   like(@@nu50)
     D inahd           s                   like(@@nu50)
      *
     D @@nu92          s              9  2
     D pinlb           s                   like(@@nu92)
     D tinlb           s                   like(@@nu92)
     D tisplb          s                   like(@@nu92)
     D tidplb          s                   like(@@nu92)
     D poulb           s                   like(@@nu92)
     D toulb           s                   like(@@nu92)
     D tosplb          s                   like(@@nu92)
     D todplb          s                   like(@@nu92)
     D rinlb           s                   like(@@nu92)
     D roulb           s                   like(@@nu92)
     D qinlb           s                   like(@@nu92)
     D qoulb           s                   like(@@nu92)
     D morlb           s                   like(@@nu92)
      *
      *
      *
      * Array indexes
      *
     D x               s              3  0
     D y               s              3  0
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ***************************************************************************************
      * MAINLINE
      ***************************************************************************************
      *
      * Process each destination group in the Workfile: GF Groups-BGF Sources
      *
     C     *loval        setll     hsp324
      *
     C                   dou       *in90 = *on                                  Main do loop
     C                   read      hsp324                                 90
     C                   if        *in90 = *off                                 If not EOF
      *
      * First time
     C                   if        first = yes
     C                   exsr      $clear
     C                   move      no            first
     C                   endif
      *
      * Break on destination group
      *
     C                   if        w8dnhgsn <> svdnhgsn
     C                   exsr      $break
     C                   endif
      *
      * Detail processing when you have 'received' head and 'origin' farm site.
      *  1) accumulate head received into array
      *
     C                   if        w8rchd <> 0 and w8orfscd <> 0
     C                   exsr      $detail
     C                   endif
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Main do loop
      *
      * Last record processing for last grow finish group
      *
     C                   if        first = no
     C                   exsr      $break
     C                   endif
      *
      * EOF Processing
      *
     C                   seton                                        lr
      /eject
      *---------------------------------------------------------------------
      * Detail processing
      *---------------------------------------------------------------------
      *
     C     $detail       begsr
      *
     C                   z-add     1             x
     C     w8orfscd      lookup    arrfscd(x)                             92
     C                   if        *in92 = *on                                  If exists
     C                   add       w8rchd        arrrchd(x)
     C                   else
     C                   add       1             y
     C                   z-add     w8orfscd      arrfscd(y)
     C                   z-add     w8rchd        arrrchd(y)
     C                   endif                                                  If exists
      *
     C                   endsr
      /eject
      *----------------------------------------------------------------
      * Control break on group
      *----------------------------------------------------------------
      *
     C     $break        begsr
      *
      * Find the total head in the head array for the group
      *
     C                   xfoot     arrrchd       wkarrhd
      *
      * Find the total head 'received' into the group as of the system date.
      *
     C                   exsr      $rchd
      *
      *
      * Set up fields that do not change per record.
      *
     C                   z-add     svdnfscd      wbdnfscd
     C                   z-add     svdncell      wbdncell
     C                   z-add     svdnhgsn      wbdnhgsn
     C                   move      svdnhgcd      wbdnhgcd
     C                   move      svdnppcd      wbdnppcd
      *
      *
      * Start the percent out out 100%.
      *
     C                   z-add     100           wkrempr
      *
      * Write a record for each element in the origin BGF farm array.
      *
     C                   do        y             x
     C                   z-add     arrfscd(x)    wborfscd
     C                   z-add     arrrchd(x)    wbrchd
      *
      * Calculate percent of total
      *
     C                   if        wkrchd <> 0
     C     wbrchd        div       wkrchd        wkpr
     C     wkpr          mult(h)   100           wbhdpr
     C                   sub       wbhdpr        wkrempr
     C                   else
     C                   z-add     0             wbhdpr
     C                   endif
      *
      * Retrieve cell for origin farm site
      *
     C     wborfscd      chain     hsp018                             92
     C                   if        *in92 = *off
     C                   z-add     fscell        wborcell
     C                   else
     C                   z-add     0             wborcell
     C                   endif
      *
     C                   write     wbrec
     C                   enddo
      *
      * Now, you have finished processing the arrays, if you have 'remaining' percent,
      * 1) add it to the last record written if remaining head is 0
      * 2) write an 'unknown bgf source' record if remaining head is not zero
      *
     C                   if        wkrempr <> 0                                 If leftovers
     C     wkrchd        sub       wkarrhd       wkhd
      *
     C                   select
     C                   when      wkhd <> 0
     C                   z-add     0             wborfscd
     C                   z-add     0             wborcell
     C                   z-add     wkrempr       wbhdpr
     C                   z-add     wkhd          wbrchd
     C                   write     wbrec
     C                   other
      *
     C     key01         chain     hsp356                             92
     C                   if        *in92 = *off
     C                   add       wkhd          wbrchd
     C                   add       wkrempr       wbhdpr
     C                   update    wbrec
     C                   endif
     C                   endsl
      *
     C                   endif                                                  If leftovers
      *
      * Clear/initialize for next group
      *
     C                   exsr      $clear
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------------
      * Retrieve 'total head received' into the group as of the system date
      *---------------------------------------------------------------------
      *
     C     $rchd         begsr
      *
     C                   call      'HPMOVE'      plist01
     C     qinhd         add       rinhd         wkrchd
      *
     C                   endsr
      /eject
      *----------------------------------------------------------------
      * Clear routine
      *----------------------------------------------------------------
      *
     C     $clear        begsr
      *
      * Clear arrays and set array indexes back to zero
      *
     C                   z-add     0             arrfscd
     C                   z-add     0             arrrchd
     C                   z-add     0             x
     C                   z-add     0             y
      *
      *
      * Set save fields
     C                   z-add     w8dnhgsn      svdnhgsn
     C                   move      w8dnhgcd      svdnhgcd
     C                   move      w8dnppcd      svdnppcd
     C                   z-add     w8dnfscd      svdnfscd
     C                   z-add     w8dncell      svdncell
      *
     C                   endsr
      /eject
      *----------------------------------------------------------------
      * Initialization subroutine
      *----------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Parm lists
      *
     C     plist01       plist
     C                   parm                    svdnhgsn
     C                   parm                    wksysdt
      * Head
     C                   parm                    gpihd                          GROUP INV
     C                   parm                    pinhd                          PIG IN
     C                   parm                    tinhd                          TRSF IN
     C                   parm                    tisphd                         SAME PHASE
     C                   parm                    tidphd                         DIFF PHASE
     C                   parm                    pouhd                          PIGS OUT
     C                   parm                    touhd                          TRSF OUT
     C                   parm                    tosphd                         SAME PHASE
     C                   parm                    tosphd                         DIFF PHASE
     C                   parm                    rinhd                          REJECT IN
     C                   parm                    rouhd                          REJECT OUT
     C                   parm                    qinhd                          QUALITY IN
     C                   parm                    qouhd                          QUALITY OUT
     C                   parm                    morhd                          MORTALITY
     C                   parm                    inahd                          INV ADJ
      * Pounds
     C                   parm                    pinlb                          PIG IN
     C                   parm                    tinlb                          TRSF IN
     C                   parm                    tisplb                         SAME PHASE
     C                   parm                    tidplb                         DIFF PHASE
     C                   parm                    poulb                          PIG OUT
     C                   parm                    toulb                          TRSF OUT
     C                   parm                    tosplb                         SAME PHASE
     C                   parm                    todplb                         DIFF PHASE
     C                   parm                    rinlb                          REJECT IN
     C                   parm                    roulb                          REJECT OUT
     C                   parm                    qinlb                          QUALITY IN
     C                   parm                    qoulb                          QUALITY OUT
     C                   parm                    morlb                          MORTALITY
      *
      *
      * Key lists
      *
     C     key01         klist
     C                   kfld                    svdnhgsn
     C                   kfld                    wborfscd
      *
      * Put system date into work field.
      *
     C     *mdy          movel     udate         wkcymdiso
     C                   movel     wkcymdiso     wksysdt
      *
     C                   endsr
      /EJECT
