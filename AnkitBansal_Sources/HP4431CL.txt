/***************************************************************** */
/*  PROGRAM NUMBER --> HP4431CL                                    */
/*  PROGRAM NAME ----> REPORTED DAILY ACTIVITY-NO DATA REPORT      */
/*  PROGRAMMER ------> LEANNE FEDOR                                */
/*  DATE ------------> 11/05/02                                    */
/*                                                                 */
/*  PROGRAM PURPOSE -> THIS CL CALLS THE PROGRAM TO PRINT A        */
/*                     LISTING OF FARMS THAT HAVE NO 'REPORTED     */
/*                     DAILY ACTIVITY' RECORDS FOR A GIVEN DATE.   */
/*                                                                 */
/*                     VALUES ARE PASSED THRU THE LDA FROM         */
/*                                                                 */
/***************************************************************** */
/*  MODIFIED:                                                      */
/*                                                                 */
/***************************************************************** */

             PGM

/* DEFINE THE VARIABLE REQUIRED IN THE COMMAND TO OVERRIDE THE     */
/* PRINT FILE TO A LASER PRINTER                                   */

             DCL        VAR(&CMD) TYPE(*CHAR) LEN(512)

             DCL        VAR(&QDATA) TYPE(*CHAR) LEN(110)

             DCL        VAR(&LDCYMD) TYPE(*DEC) LEN(8)
             DCL        VAR(&LDCELL) TYPE(*DEC) LEN(2)

             DCL        VAR(&HOLD) TYPE(*CHAR) LEN(10)
             DCL        VAR(&COPYS) TYPE(*DEC) LEN(2)
             DCL        VAR(&OUTQ) TYPE(*CHAR) LEN(10)

             CHGVAR     VAR(&LDCYMD) VALUE(%SST(*LDA 66 8))
             CHGVAR     VAR(&LDCELL) VALUE(%SST(*LDA 64 2))

/*******************************************************************/
/*  SELECT GROW FINISH AND NURSERY FARMS FROM THE FARM MASTER      */
/*******************************************************************/

             CHGVAR     VAR(%SST(&QDATA 1 18)) VALUE('FSPPCD *NE "BGF  "')

/*  IF CELL  */

             IF         COND(&LDCELL *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 20 16)) VALUE('*AND FSCELL +
                          *EQ ')
             CHGVAR     VAR(%SST(&QDATA 36 5)) VALUE(&LDCELL)
             ENDDO

             OVRDBF     FILE(HSP018)  SHARE(*YES)
             OPNQRYF    FILE((HSP018)) QRYSLT(&QDATA) +
                          KEYFLD((FSPPCD) (FSCELL) (FSFSCD))


/*******************************************************************/
/*  SELECT ALL GROW FINISH AND NURSERY GROUPS THAT WERE 'OPEN' ON  */
/*  THE SPECIFIED DATE                                             */
/*******************************************************************/

             CHGVAR     VAR(&QDATA) VALUE(' ')

/*  ALWAYS OMIT HOG GROUPS WITH A STATUS OF VOID                 */

             CHGVAR     VAR(&QDATA) VALUE(' ')
             CHGVAR     VAR(%SST(&QDATA 1 15)) VALUE('HGGSCD +
                          *NE "VD"')

/*  ALWAYS OMIT GROUPS THAT OPENED AFTER THE DATE SPECIFIED         */

             CHGVAR     VAR(%SST(&QDATA 17 15)) VALUE('*AND HGOPDT +
                          *LE')
             CHGVAR     VAR(%SST(&QDATA 33 8)) VALUE(&LDCYMD)


/*  ALWAYS OMIT DISPOSED GROUPS WITH A DISPOSE DATE BEFORE OR ON     */
/*  THE BEGINNING PERIOD DATE                                        */

             CHGVAR     VAR(%SST(&QDATA 42 18)) VALUE('*AND (HGDSDT +
                          *EQ 0')
             CHGVAR     VAR(%SST(&QDATA 61 14)) VALUE('*OR HGDSDT +
                          *GT')
             CHGVAR     VAR(%SST(&QDATA 76 8)) VALUE(&LDCYMD)
             CHGVAR     VAR(%SST(&QDATA 84 1)) VALUE(')')

/*  ALWAYS OMIT BGF GROUPS                                       */

             CHGVAR     VAR(%SST(&QDATA 86 23)) VALUE('*AND HGPPCD +
                          *NE "BGF  "')

             OVRDBF     FILE(HSL034E) SHARE(*YES)
             OPNQRYF    FILE((HSL034E)) QRYSLT(&QDATA) KEYFLD(*FILE)

/*------------------------------------------------------------------*/
/* SET UP PRINT PARMS                                               */
/*------------------------------------------------------------------*/

 HOLD:       IF         COND(%SST(*LDA 411 1) = 'Y') THEN(CHGVAR +
                          VAR(&HOLD) VALUE(*YES))
             ELSE       CMD(CHGVAR VAR(&HOLD) VALUE(*NO))

 COPYS:      CHGVAR     VAR(&COPYS) VALUE(%SST(*LDA 412 1))
             IF         COND(&COPYS = 0) THEN(CHGVAR VAR(&COPYS) +
                          VALUE(1))

 OUTQ:       CHGVAR     VAR(&OUTQ) VALUE(%SST(*LDA 413 10))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(%SST(*LDA 401 10)))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(*JOB))

/*------------------------------------------------------------------*/
/* OVERRIDE PRINT FILE                                              */
/*------------------------------------------------------------------*/

/* USE THE CUSTOMIZED SEABOARD COMMAND TO OVERRIDE THE PRINT FILE.  */
/* THIS COMMAND ALLOWS THE REPORT TO BE GENERATED ON A STANDARD LINE*/
/* PRINTER OR A LASER PRINTER. TO USE THIS OVERRIDE YOU MUST DECLARE*/
/* A COMMAND VARIABLE AND FOLLOW THE OVERRIDE WITH A CALL TO        */
/* QCMDEXC.                                                         */

/* THIS REPORT IS STANDARD 10 CPI WITH 132 COLUMNS OF PRINT          */

             SBDPRTOVR  CMDVAR(&CMD) FILE(QPRINT) OUTQ(&OUTQ) +
                          WIDTH(132) COPIES(&COPYS) HOLD(&HOLD)

             CALL       PGM(QCMDEXC) PARM(&CMD 512)


/*------------------------------------------------------------------*/
/* GENERATE REPORT                                                 */
/*------------------------------------------------------------------*/

             CALL PGM(HP6031)

             CLOF       OPNID(HSP018)
             CLOF       OPNID(HSL034E)
             DLTOVR     FILE(*ALL)

             ENDPGM

