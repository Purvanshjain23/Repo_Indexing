      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      HOG PRODUCTION
      * PROGRAM:     HP342
      * TITLE:       BUILD WORKFILES FOR FACILITY UTILIZATION REPORT
      * PROGRAMMER:  LEANNE FEDOR
      * CREATED:     07/12/96
      *
      *  FUNCTION:   THIS BATCH PROGRAM IS CALLED FROM HP442CL.
      *              IT BUILDS THREE WORKFILES:
      *                 HSP341 - WEEKS
      *                 HSP342 - PIGDAYS PER WEEK PER FARM
      *                 HSP343 - FARM
      *
      *              THE USER'S SUBMISSION CRITERIA ARE PASSED IN THE
      *              LDA FROM HP442.  OPEN QUERIES ARE USED TO MAKE AN
      *              INITIAL SELECTION OF DATA.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      * 10/10/96  LEANNE FEDOR
      *           ADDED LOGIC TO:
      *           1) ADDRESS THE USER'S SELECTION ON CELL.
      *           2) HANDLE A VARIABLE NUMBER OF WEEKS ON THE REPORT
      *
      * 11/01/96  LEANNE FEDOR
      *           AS REQUESTED BY JAY KAYSER, CHANGED THE CALCULATION
      *           OF 'AVERAGE PERCENT FULL PER FARM SITE' TO NOT USE
      *           THE USER'S REQUESTED 'NUMBER OF WEEKS' BUT INSTEAD
      *           TO DETERMINE THE FIRST WEEK ON THE REPORT WHERE THE
      *           WEEKLY PERCENT WAS NOT ZERO AND USE THE TOTAL COUNT
      *           OF WEEKS FROM THERE OUT.
      *
      * 06/03/97  LEANNE FEDOR
      *           RECOMPILE ONLY. NEW FIELDS 'PURGE FLAG' AND 'DATE OF
      *           FIRST PURGE' ADDED TO HOG GROUP FILE.
      *
      * 04/13/98  LeAnne Fedor
      *           Recompile only. New field 'state' added to farm site file.
      *
      * 06/05/01  LeAnne Fedor
      *           Recompile only. New field 'multisite' added to Farm Site file.
      *
      * 06/28/01  LeAnne Fedor
      *           Recompile only.
      *           Three fields (manager/supervisor/multisite) renamed in Farm Site file.
      *           Manager and supervisor fields removed from Hog Group file.
      *
      * 09/04/01  LeAnne Fedor
      *           'Hog capacity' removed from Farm Buildings file.
      *           'Square feet' fields added to Farm Site file.
      *           Changed the way 'hog capacity' per farm is calculated.
      *
      * 07/02/09  LeAnne Ramsey
      *           Recompile only. Added new field 'Continuous Flow Flag' to Hog Group file.
      *
      * 10/15/13  LeAnne Ramsey (E2831)
      *           Recompile only. Added field 'MTech Reference'.
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     FHSP018    IF   E           K DISK
      *    FARM SITE                 (KEY: FSFSCD)
      *
     FHSP034    IF   E           K DISK
      *    HOG GROUP                 (KEY: HGHGCD)
      *    (RECORDS SELECTED BY OPEN QUERY)
      *
     FHSP341    UF A E           K DISK
      *  WORKFILE OF WEEKS           (KEY: WWWEEK)
      *
     FHSP342    UF A E           K DISK
      *  WORKFILE OF PIGDAYS         (KEY: WPFSCD WPWEEK)
      *
     FHSP343    UF A E           K DISK
      *  WORKFILE OF FARMS           (KEY: WFPTCD WFPPCD WFFSCD)
      *
      /EJECT
      ****************************************************************
      * TABLE AND ARRAY SPECIFICATIONS
      ****************************************************************
      *
      *---------------------------------------------------------------
      * ALTERNATING ARRAYS--FARM SITE CODE AND TOTAL PERCENTS
      *---------------------------------------------------------------
     D FSCD            S              5  0 DIM(999)
     D FLPR            S              7  0 DIM(999)
      *
      ****************************************************************
      * INPUT SPECS
      ****************************************************************
      *----------------------------------------------------------------
      * LDA - REPORT SELECTIONS
      *----------------------------------------------------------------
      *
     D                UDS
     D  LDFSBO                 1      5
     D  LDEYMD                 6     13  0
     D  LDBYMD                14     21  0
     D  LDPTCD                22     26
     D  LDPPCD                27     31
     D  LDFSCD                32     36  0
      *
     D  LDBMDY                37     42  0
     D  LDEMDY                42     47  0
      *
     D  LDCELL                49     50  0
     D  LDWK                  51     52  0
      *
     D  LDBODS                72    101
     D  LDPTDS               102    111
     D  LDPPDS               112    141
     D  LDFSNM               142    166
      *
      *
      *---------------------------------------------------------------
      *  NAMED CONSTANTS
      *---------------------------------------------------------------
      *
     D YES             C                   CONST('Y')
     D NO              C                   CONST('N')
      *
      /EJECT
      ****************************************************************
      * MAINLINE
      ****************************************************************
      *
      * CREATE THE WEEK RECORDS WITH BEGINNING/ENDING DATES IN THE
      * WEEKS WORKFILE. THE NUMBER OF WEEKS IS VARIABLE BASED ON
      * THE USER'S ENTRY.
      *
     C                   EXSR      $WKS
      *
      * READ EACH HOG GROUP SELECTED BY THE OPNQRY AND CREATE
      * THE PIG DAY RECORDS FOR THE CORRESPONDING FARM SITE.
      * DO NOT PROCESS THE GROUP IF THE USER HAS LIMITED THE
      * REPORT TO A SINGLE CELL AND THE GROUP IS NOT ON A FARM
      * IN THAT CELL.
      *
     C     *IN90         DOUEQ     *ON                                          MAIN DO
     C                   READ      HSP034                                 90
     C     *IN90         IFEQ      *OFF                                         IF NOT EOF
      *
     C                   SELECT
     C     LDCELL        WHENEQ    0
     C                   EXSR      $GROUP
     C                   OTHER
      *
     C     HGFSCD        CHAIN     HSP018                             92
     C     *IN92         IFEQ      *OFF
     C     FSCELL        ANDEQ     LDCELL
     C                   EXSR      $GROUP
     C                   ENDIF
     C                   ENDSL
      *
     C                   ENDIF                                                  IF NOT EOF
     C                   ENDDO                                                  MAIN DO
      *
      *----------------------------------------------------------------
      * EOF PROCESSING
      *----------------------------------------------------------------
      *
      * REREAD THE PIGDAYS WORKFILE. MAKE THE CAPACITY CALCULATIONS
      * OF PERCENT FULL AND UPDATE THE PIGDAYS WORKFILE. ALSO,
      * WRITE A FARM RECORD TO THE FARM WORKFILE.
      *
     C                   EXSR      $FULL
      *
      *
      * REREAD THE FARM WORKFILE. USING THE TOTAL PERCENT VALUES
      * STORED IN ARRAYS IN THE PREVIOUS SUBROUTINE, CALCULATE THE
      * AVERAGE FULL PERCENT FOR THE FARM AND UPDATE THE FARM
      * WORKFILE RECORD.
      *
      *
     C                   EXSR      $AVG
      *
      *
     C                   SETON                                        LR
      /EJECT
      *----------------------------------------------------------------
      * $GROUP - RETRIEVE PIG DAYS FOR EACH WEEK FOR A GROUP
      *----------------------------------------------------------------
      *
     C     $GROUP        BEGSR
      *
     C     *LOVAL        SETLL     HSP341
      *
     C     *IN91         DOUEQ     *ON                                          DO GROUP WEEKS
     C                   READ      HSP341                                 91
     C     *IN91         IFEQ      *OFF                                         IF A WEEK
     C                   Z-ADD     0             XXPDAY
      *
      *
      * IF THE GROUP HAD ACTIVITY DURING THIS WEEK, THEN
      * FIND THE PIG DAYS FOR THE GROUP FOR THE WEEK.
      *
     C                   MOVEL     YES           PROCFL
      *
     C     HGOPDT        IFGT      WWEYMD                                       IF NOT OPEN
     C                   MOVEL     NO            PROCFL
     C                   ENDIF                                                  IF NOT OPEN
      *
     C     HGDSDT        IFLT      WWBYMD                                       IF ALL GONE
     C     HGDSDT        ANDNE     0
     C                   MOVEL     NO            PROCFL
     C                   ENDIF                                                  IF ALL GONE
      *
     C     PROCFL        IFEQ      YES                                          IF PIG DAYS
     C                   CALL      'HPPDAY'                                                   002
     C                   PARM                    HGHGSN                                       002
     C                   PARM                    WWBYMD                                       002
     C                   PARM                    WWEYMD                                       002
     C                   PARM      0             XXPDAY                                       002
     C                   ENDIF                                                  IF PIG DAYS
      *
      *
      * WRITE A WORKFILE RECORD FOR THE GROUP FOR THE WEEK
      * EVEN IF PIG DAYS WAS ZERO.
      *
     C     K1P342        CHAIN     HSP342                             92
     C     *IN92         IFEQ      *OFF
     C                   ADD       XXPDAY        WPPDAY
     C                   UPDATE    WPREC
     C                   ELSE
     C                   Z-ADD     WWWEEK        WPWEEK
     C                   Z-ADD     HGFSCD        WPFSCD
     C                   Z-ADD     XXPDAY        WPPDAY
     C                   Z-ADD     0             WPFLPR
     C                   WRITE     WPREC
     C                   ENDIF
      *
     C                   ENDIF                                                  IF A WEEK
     C                   ENDDO                                                  DO GROUP WEEKS
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $FULL - CALCULATE THE PERCENT FULL PER WEEK FOR EACH FARM
      *----------------------------------------------------------------
      *
      * REREAD THE PIGDAYS WORKFILE. DO THE FOLLOWING:
      *  1) MAKE THE CAPACITY CALCULATIONS OF PERCENT FULL AND
      *     UPDATE THE PIGDAYS WORKFILE
      *  2) WRITE A FARM RECORD TO THE FARM WORKFILE
      *  3) SAVE EACH FARM AND ITS TOTAL WEEKLY PERCENTS INTO
      *     ARRAYS SO THAT YOU CAN CALCULATE THE 'AVERAGE' FULL
      *     PERCENT FOR THE FARM LATER.
      *
     C     $FULL         BEGSR
      *
     C     *LOVAL        SETLL     HSP342
      *
     C     *IN90         DOUEQ     *ON                                          DO PIGDAYS
     C                   READ      HSP342                                 90
     C     *IN90         IFEQ      *OFF                                         IF NOT EOF
      *
     C     WPFSCD        IFNE      SVFSCD
     C                   EXSR      $FARM
     C                   Z-ADD     WPFSCD        SVFSCD
     C                   ENDIF
      *
      * CALCULATE PERCENT FULL FOR WEEKLY RECORD
      *
     C                   Z-ADD     0             WPFLPR
     C     WFCPPD        IFNE      0
     C     WPPDAY        DIV       WFCPPD        WKFLD
     C     100           MULT(H)   WKFLD         WPFLPR
     C                   ENDIF
      *
      * ACCUMULATE THE WEEK'S PERCENT FULL VALUE IN THE ARRAY FOR
      * THE FARM SITE.
     C                   Z-ADD     1             X
     C     WPFSCD        LOOKUP    FSCD(X)                                93
     C     *IN93         IFEQ      *ON                                          IF FOUND
     C                   ADD       WPFLPR        FLPR(X)
     C                   ELSE
     C                   ADD       1             Y
     C                   Z-ADD     WPFSCD        FSCD(Y)
     C                   Z-ADD     WPFLPR        FLPR(Y)
     C                   ENDIF                                                  IF FOUND
      *
     C                   UPDATE    WPREC
      *
     C                   ENDIF                                                  IF NOT EOF
     C                   ENDDO                                                  DO PIGDAYS
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $FARM - RETRIEVE/CALCULATE THE CAPACITY FOR THE FARM.
      *----------------------------------------------------------------
      *
     C     $FARM         BEGSR
      *
     C                   CLEAR                   WFREC
      *
      * SET UP FARM SITE CODE FOR FARM WORKFILE
      *
     C                   Z-ADD     WPFSCD        WFFSCD
      *
      * RETRIEVE FARM INFORMATION
      *
     C     WFFSCD        CHAIN     HSP018                             92
     C     *IN92         IFEQ      *OFF
     C                   MOVEL(P)  FSFSBO        WFFSBO
     C                   MOVEL(P)  FSPTCD        WFPTCD
     C                   MOVEL(P)  FSPPCD        WFPPCD
     C                   MOVEL(P)  FSFSNM        WFFSNM
     C                   ELSE
     C                   MOVE(P)   *BLANK        WFFSBO
     C                   MOVE(P)   *BLANK        WFPTCD
     C                   MOVE(P)   *BLANK        WFPPCD
     C                   MOVE(P)   *BLANK        WFFSNM
     C                   ENDIF
      *
      *
      * Call the generic program to retrieve total square feet from active
      * rooms on the farm.
      *
     C                   call      'HP8003'
     C                   parm                    fsfscd
     C                   parm      0             xxfssqft
      *
      *  Calculate hog capacity for the farm as:
      *    total square feet on the farm / target square feet per pig
      *
     C                   if        fshdsqft <> 0
     C     xxfssqft      div(h)    fshdsqft      wkcphd
     C                   else
     C                   z-add     0             wkcphd
     C                   endif
      *
      *
      * CALCULATE THE 'PIG DAY CAPACITY' FOR THE FARM FOR
      * A WEEK BY MULTIPLYING THE HEAD CAPACITY BY 7 DAYS
      *
     C     WKCPHD        MULT      7             WFCPPD
     C                   Z-ADD     WKCPHD        WFCPHD
      *
      * WRITE THE FARM RECORD
      *
     C                   WRITE     WFREC
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $AVG - CALCULATE THE AVERAGE PERCENT FULL FOR EACH FARM SITE
      *----------------------------------------------------------------
      *
     C     $AVG          BEGSR
      *
     C     *LOVAL        SETLL     HSP343
      *
     C     *IN90         DOUEQ     *ON                                          DO AVERAGE
     C                   READ      HSP343                                 90
     C     *IN90         IFEQ      *OFF                                         IF NOT EOF
      *
      * DETERMINE THE WEEK VALUE TO USE AS A DIVISOR IN
      * THE CALCULATION. THE VALUE SHOULD BE THE NUMBER OF
      * THE FIRST WEEK ON THE REPORT THAT HAD ACTUAL PIGS
      * AND, THEREFORE, PIG DAYS ON THE FARM.
      *
     C                   Z-ADD     0             WKWEEK
     C     WFFSCD        SETLL     HSP342
     C     *IN91         DOUEQ     *ON                                          DO WEEKS
     C     WPPDAY        ORNE      0
     C     WFFSCD        READE     HSP342                                 91
     C     *IN91         IFEQ      *OFF                                         IF NOT EOF
     C     WPPDAY        ANDNE     0
     C     LDWK          SUB       WPWEEK        WKWEEK
     C                   ADD       1             WKWEEK
     C                   ENDIF                                                  IF NOT EOF
     C                   ENDDO                                                  DO WEEKS
      *
      *
      * RETRIEVE THE ACCUMULATED FULL PERCENT VALUES FROM
      * ALL WEEKS FROM THE ARRAY.
      *
     C                   Z-ADD     1             X
     C     WFFSCD        LOOKUP    FSCD(X)                                93
     C     *IN93         IFEQ      *ON                                          IF FOUND
     C                   Z-ADD     FLPR(X)       WFFLPR
     C     WKWEEK        IFNE      0
     C     FLPR(X)       DIV(H)    WKWEEK        WFAVPR
     C                   ENDIF
     C                   ENDIF                                                  IF FOUND
      *
     C                   UPDATE    WFREC
     C                   ENDIF                                                  IF NOT EOF
     C                   ENDDO                                                  DO AVERAGE
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $WKS - CREATE THE WEEK RECORDS IN THE WEEKS WORKFILE
      *----------------------------------------------------------------
      *
      * THIS SUBROUTINE WILL START WITH THE 'BEGINNING' DATE FROM THE
      * LOCAL DATA AREA AND CREATE A VARIABLE NUMBER OF WEEK RECORDS
      * BASED ON THE USER'S SELECTIONS.
      *
     C     $WKS          BEGSR
      *
      * DEFAULT THE BEGINNING DATE FROM THE LOCAL DATA AREA
      *
     C                   Z-ADD     LDBYMD        WKBYMD
      *
      *
     C                   Z-ADD     0             X
      *
     C                   DO        LDWK          X                              DO WEEKS
      *
      * FIND THE ENDING DATE FOR THE WEEK BY ADDING SIX DAYS
      * TO THE BEGINNING DATE.
      *
     C                   Z-ADD     WKBYMD        PFRM8
     C                   Z-ADD     6             PDAYS
     C                   EXSR      $DATE
     C                   Z-ADD     PFRM8         WWBYMD
     C                   Z-ADD     PTO8          WWEYMD
     C                   Z-ADD     X             WWWEEK
     C                   WRITE     WWREC
      *
      * FIND THE NEXT BEGINNING DATE BY ADDING SEVEN DAYS TO
      * THE CURRENT BEGINNING DATE.
      *
     C                   Z-ADD     7             PDAYS
     C                   EXSR      $DATE
     C                   Z-ADD     PTO8          WKBYMD
     C                   ENDDO                                                  DO WEEKS
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * $DATE -  MANIPULATE DATES WITH DATE UTILITY
      *---------------------------------------------------------------
      *
     C     $DATE         BEGSR
      *
     C                   MOVEL     'CYMD'        PFRFMT
     C                   MOVEL     'CYMD'        PTOFMT
     C                   MOVEL     'I'           PCODE
     C                   MOVE      *BLANK        PRTRN
      *
     C                   CALL      'UT80060R'
     C                   PARM                    PFRM8             8 0
     C                   PARM                    PTO8              8 0
     C                   PARM                    PFRFMT            4
     C                   PARM                    PTOFMT            4
     C                   PARM                    PDAYS             4 0
     C                   PARM                    PCODE             1
     C                   PARM                    PRTRN             1
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * *INZSR - INITIALIZATION SUBROUTINE
      *---------------------------------------------------------------
      *
     C     *INZSR        BEGSR
      *
      * KEY LISTS
      *
     C     K1P342        KLIST
     C                   KFLD                    HGFSCD
     C                   KFLD                    WWWEEK
      *
      * CONTROL PROCESSING FLAGS AND FIELDS
      *
     C                   MOVEL     YES           FIRST             1
     C                   MOVEL     YES           PROCFL            1
     C                   Z-ADD     0             X                 3 0
     C                   Z-ADD     0             Y                 3 0
      *
      * SAVE FIELDS
     C     *LIKE         DEFINE    WFFSCD        SVFSCD
      *
      * WORKFIELDS
     C     *LIKE         DEFINE    WPWEEK        WKWEEK
     C     *LIKE         DEFINE    WWBYMD        WKBYMD
     C     *LIKE         DEFINE    WFCPHD        WKCPHD
     C                   Z-ADD     0             WKFLD            15 5
      *
      * PARMS
     C                   Z-ADD     0             XXPDAY           11 0
     C                   Z-ADD     0             XXFSSQFT          7 0
      *
      *
     C                   ENDSR
      /EJECT
