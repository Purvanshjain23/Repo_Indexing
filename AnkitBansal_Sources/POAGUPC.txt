/*********************************************************************/
/*     Program: POAGUPC                                              */
/*       Title: Find last name in first name/last name field         */
/*        Date: 05/21/99                                             */
/* Description: - Input field is searched for second string of text  */
/*                which is assumed to be the last name.              */
/*********************************************************************/
/* Modifications:                                                    */
/*                                                                   */
/*   Date     Pgmr     Description                                   */
/* -------- ---------- --------------------------------------------  */
/*                                                                   */
/*********************************************************************/
             PGM PARM(&FULLNAME &LASTNAME)

   /*-------------------*/
   /* Declare Variables */
   /*-------------------*/

             /* Parameter Fields */
             DCL VAR(&FULLNAME) TYPE(*CHAR) LEN(30)
             DCL VAR(&LASTNAME) TYPE(*CHAR) LEN(20)

             /* Qclscan Fields */
             DCL  VAR(&SRCHSTRING) TYPE(*CHAR) LEN(30)
             DCL  VAR(&SRCHSTRLEN) TYPE(*DEC)  LEN(3 0)
             DCL  VAR(&STARTPOS)   TYPE(*DEC)  LEN(3 0)
             DCL  VAR(&PATTERN)    TYPE(*CHAR) LEN(1)
             DCL  VAR(&PATTERNLEN) TYPE(*DEC)  LEN(3 0)
             DCL  VAR(&TRANSLATE)  TYPE(*CHAR) LEN(1)   VALUE(' ')
             DCL  VAR(&TRIM)       TYPE(*CHAR) LEN(1)   VALUE(' ')
             DCL  VAR(&WILD)       TYPE(*CHAR) LEN(1)   VALUE(' ')
             DCL  VAR(&RESULT)     TYPE(*DEC)  LEN(3 0)

             /* Internal Work Fields */
             DCL  VAR(&LSTNAMESTR) TYPE(*DEC)  LEN(3 0)
             DCL  VAR(&LSTNAMEEND) TYPE(*DEC)  LEN(3 0)
             DCL  VAR(&LSTNAMELEN) TYPE(*DEC)  LEN(3 0)

   /*----------------------------------------*/
   /* Full Name is blank, Last Name is blank */
   /*----------------------------------------*/
             IF COND(&FULLNAME *EQ ' ') THEN(DO)
                CHGVAR VAR(&LASTNAME) VALUE(&FULLNAME)
                GOTO CMDLBL(END)
             ENDDO

   /*------------------*/
   /* Load scan fields */
   /*------------------*/
             CHGVAR VAR(&SRCHSTRING) VALUE(&FULLNAME)
             CHGVAR VAR(&SRCHSTRLEN) VALUE(30)
             CHGVAR VAR(&STARTPOS)   VALUE(1)
             CHGVAR VAR(&PATTERN)    VALUE(' ')
             CHGVAR VAR(&PATTERNLEN) VALUE(1)
             CHGVAR VAR(&RESULT)     VALUE(0)

   /*-----------------------------------------------------*/
   /* Scan for first blank, starting postion of last name */
   /*-----------------------------------------------------*/
AGAIN1:
             CALL PGM(QCLSCAN) PARM(&SRCHSTRING &SRCHSTRLEN +
                                    &STARTPOS +
                                    &PATTERN &PATTERNLEN +
                                    &TRANSLATE +
                                    &TRIM &WILD +
                                    &RESULT)
             IF COND(&RESULT *GT 0) THEN(DO)
                /* LEADING BLANKS, increment start position */
                IF COND(&RESULT *EQ &STARTPOS) THEN(DO)
                   CHGVAR VAR(&STARTPOS) VALUE(&STARTPOS + 1)
                   CHGVAR VAR(&RESULT)   VALUE(0)
                   GOTO CMDLBL(AGAIN1)
                ENDDO
                /* BLANK FOUND (but not before other chars) */
                CHGVAR VAR(&LSTNAMESTR) VALUE(&RESULT + 1)
             ENDDO
             ELSE CMD(DO)
                IF COND(&RESULT *LE 0) THEN(DO)
                   CHGVAR VAR(&LSTNAMESTR) VALUE(1)
                ENDDO
             ENDDO

   /*------------------------*/
   /* Reposition scan fields */
   /*------------------------*/
AGAIN2:
             CHGVAR VAR(&SRCHSTRING) VALUE(&FULLNAME)
             CHGVAR VAR(&SRCHSTRLEN) VALUE(30)
             CHGVAR VAR(&STARTPOS)   VALUE(&LSTNAMESTR)
             CHGVAR VAR(&PATTERN)    VALUE(' ')
             CHGVAR VAR(&PATTERNLEN) VALUE(1)
             CHGVAR VAR(&RESULT)     VALUE(0)

   /*----------------------------------------------------*/
   /* Scan for second blank, ending postion of last name */
   /*----------------------------------------------------*/
             CALL PGM(QCLSCAN) PARM(&SRCHSTRING &SRCHSTRLEN +
                                    &STARTPOS +
                                    &PATTERN &PATTERNLEN +
                                    &TRANSLATE +
                                    &TRIM &WILD +
                                    &RESULT)
             IF COND(&RESULT *GT 0) THEN(DO)
                /* LEADING BLANKS, increment start position */
                IF COND(&RESULT *EQ &STARTPOS) THEN(DO)
                   CHGVAR VAR(&LSTNAMESTR) VALUE(&LSTNAMESTR + 1)
                   CHGVAR VAR(&RESULT)     VALUE(0)
                   GOTO CMDLBL(AGAIN2)
                ENDDO
                /* BLANK FOUND (but not before other chars) */
                CHGVAR VAR(&LSTNAMEEND) VALUE(&RESULT - 1)
             ENDDO
             ELSE CMD(DO)
                IF COND(&RESULT *LE 0) THEN(DO)
                   CHGVAR VAR(&LSTNAMESTR) VALUE(30)
                ENDDO
             ENDDO

   /*-------------------------------------------*/
   /* Substring last name from full name fields */
   /*-------------------------------------------*/
             CHGVAR VAR(&LSTNAMELEN) +
                    VALUE(&LSTNAMEEND - &LSTNAMESTR + 1)
             IF COND(&LSTNAMELEN *LE 0) THEN(DO)
                CHGVAR VAR(&LASTNAME) VALUE(&FULLNAME)
             ENDDO
             ELSE CMD(DO)
                CHGVAR VAR(&LASTNAME) +
                       VALUE(%SST(&FULLNAME &LSTNAMESTR &LSTNAMELEN))
             ENDDO

END:
             ENDPGM
