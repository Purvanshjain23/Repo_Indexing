     A*%%TS  SD  20041103  134607  ISLRAMS     REL-V5R2M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION
     A*  DISPLAY FILE:  HPD141
     A*  TITLE:         REVISE CLOSED HOG GROUP
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  DATE:          08/22/02
     A*
     A*  INDICATOR 65 IS ON WHEN THE GROUP IS NURSERY WEAN-TO-FINISH
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A                                      CF04(04 'PROMPT')
     A                                      CF05(05 'REFRESH')
     A                                      CA12(12 'PREVIOUS')
     A*
     A*---------------------------------------------------------------
     A* RECORD FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP141R1
     A*%%TS  SD  20041103  134607  ISLRAMS     REL-V5R2M0  5722-WDS
     A                                      SETOF(30)
     A                                      SETOF(31)
     A                                      SETOF(36)
     A                                      SETOF(39)
     A                                      SETOF(51)
     A                                      SETOF(56)
     A                                      SETOF(59)
     A                                      RTNCSRLOC(&RCD &FLD)
     A                                      OVERLAY
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 28'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A            D1MODE    R        O  2  2REFFLD(@@MODE)
     A                                      DSPATR(HI)
     A                                  2 27'REVISE CLOSED HOG GROUP'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  4  2'Business office .....:'
     A            D1FSBO    R        O  4 27REFFLD(HSRREF/@@BOCD *LIBL/HSPREF)
     A                                  5  2'Hog group ...........:'
     A            D1HGCD    R        O  5 27REFFLD(HGHGCD)
     A                                  5 36'Hog group number:'
     A            D1HGSN    R        O  5 54REFFLD(HGHGSN)
     A                                      EDTCDE(M)
     A                                  6  2'Group status code ...:'
     A            D1GSCD    R        O  6 27REFFLD(HGGSCD)
     A                                  7  2'Farm site ...........:'
     A            D1FSCD    R        O  7 27REFFLD(HGFSCD)
     A                                      EDTCDE(M)
     A            D1FSNM    R        O  7 36REFFLD(FSFSNM)
     A                                  8  2'Building ............:'
     A            D1BLCD    R        O  8 27REFFLD(HGBLCD)
     A            D1BLDS    R        O  8 36REFFLD(FBBLDS)
     A                                  9  2'Room ................:'
     A            D1RMCD    R        O  9 27REFFLD(HGRMCD)
     A                                 10  2'Production type .....:'
     A            D1PTCD    R        O 10 27REFFLD(PTREC/PTPTCD *LIBL/HSP010)
     A            D1PTDS    R        O 10 36REFFLD(PTREC/PTPTDS *LIBL/HSP010)
     A                                 11  2'Production phase ....:'
     A            D1PPCD    R        O 11 27REFFLD(HSRREF/@@PPCD *LIBL/HSPREF)
     A            D1PPDS    R        O 11 36REFFLD(HSRREF/PPPPDS *LIBL/HSPREF)
     A                                 12  2'Group type ..........:'
     A            D1GTCD    R        O 12 27REFFLD(@@GTCD)
     A            D1GTDS    R        O 12 36REFFLD(GTGTDS)
     A                                 13  2'Stocking density .....'
     A  65                                  DSPATR(HI)
     A  65                             13 25'?'
     A                                      DSPATR(HI)
     A            D1SDCD    R        B 13 27REFFLD(HSRREF/@@SDCD *LIBL/HSPREF)
     A N65                                  DSPATR(PR)
     A  65 31
     AO 65 51                               DSPATR(PC)
     A  65 31                               DSPATR(RI)
     A  65N31                               DSPATR(HI)
     A  65N31                               DSPATR(UL)
     A            D1SDDS    R        O 13 36REFFLD(HSRREF/@@SDDS *LIBL/HSPREF)
     A                                 14  2'Breed stock ..........'
     A                                      DSPATR(HI)
     A                                 14 25'?'
     A                                      DSPATR(HI)
     A            D1BSCD    R        B 14 27REFFLD(HGBSCD)
     A  36
     AO 56                                  DSPATR(PC)
     A  36                                  DSPATR(RI)
     A N36                                  DSPATR(HI)
     A N36                                  DSPATR(UL)
     A            D1BSDS    R        O 14 36REFFLD(BSBSDS)
     A                                 15  2'Sex ..................'
     A                                      DSPATR(HI)
     A                                 15 25'?'
     A                                      DSPATR(HI)
     A            D1SXCD         1A  B 15 27
     A  39
     AO 59                                  DSPATR(PC)
     A  39                                  DSPATR(RI)
     A N39                                  DSPATR(HI)
     A N39                                  DSPATR(UL)
     A            D1SXDS    R        O 15 36REFFLD(SXSXDS)
     A                                 16  2'Genetic line ........:'
     A            D1GNCD    R        O 16 27REFFLD(HGGNCD)
     A                                 17  2'Feed group ..........:'
     A            D1FGCD    R        O 17 27REFFLD(HSRREF/@@FGCD *LIBL/HSPREF)
     A            D1FGDS    R        O 17 36REFFLD(HSRREF/@@FGDS *LIBL/HSPREF)
     A                                 18  2'Average days old IN ..'
     A                                      DSPATR(HI)
     A            D1ADOI    R        B 18 27REFFLD(HGADOI HSPREF)
     A                                      EDTCDE(K)
     A  30                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A N30                                  DSPATR(HI)
     A N30                                  DSPATR(UL)
     A                                 18 51'Date created ..:'
     A            D1CRMDY   R        O 18 68REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A                                 19  2'Average days old OUT :'
     A            D1ADOO    R        O 19 27REFFLD(HGADOO)
     A                                      EDTCDE(K)
     A                                 19 51'Date opened ...:'
     A            D1OPMDY   R        O 19 68REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A                                 20  2'Origin BGF farm .....:'
     A            D1OBFS    R        O 20 27REFFLD(HGFSCD)
     A                                      EDTCDE(M)
     A                                 20 51'Date disposed .:'
     A            D1DSMDY   R        O 20 68REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A                                 21  2'Origin nursery farm .:'
     A            D1ONFS    R        O 21 27REFFLD(HGFSCD)
     A                                      EDTCDE(M)
     A                                 21 51'Date closed ...:'
     A            D1CLMDY   R        O 21 68REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A*
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R HP141K1
     A*%%TS  SD  20020822  153621  ISLRAMS     REL-V5R1M0  5722-WDS
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A                                 23 11'F4=Prompt'
     A                                 23 22'F5=Refresh'
     A                                 23 34'F12=Previous'
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *---------------------------------------------------------------
     A          R HP141ES                   SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE CONTROL FORMAT
      *---------------------------------------------------------------
     A          R HP141EC                   SFLCTL(HP141ES)
      *
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
