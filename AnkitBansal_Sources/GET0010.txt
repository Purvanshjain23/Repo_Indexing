       //----------------------------------------------------------------------
       // Program To Consume the GetNextNextNo  Web Service
       // Using the RPGLE Stub Service Program "NextNumber"
       //  ˆRMC -  changed E1INT1 to E1IDEVGEN - WILL PROMOTE TO E1ITESTP THEN PROD
       //  ˆRMC -  changed GetNxtNo to GET0010 - WILL PROMOTE TO E1ITESTP THEN PROD
       // Create using: CRTRPGMOD MODULE(e1idevgen/get0010)
       //                         SRCFILE(e1idevgen/QRPGLESRC)
       //                         SRCMBR(get0010)
       //                         REPLACE(*NO)
       //                           INCDIR('/e1int/nn')
       //
       //    ˆMarine:          CRTPGM PGM(e1int1/GetNxtNo) BNDSRVPRM(E1INT1/NextNo)
       //     Foods :          CRTPGM PGM(e1idevgen/GET0010)
       //                       BNDSRVPGM((E1IPLIB/NEXTNO) (QSYSDIR/QAXIS10CC))
       //
       //
       //‚----------------------------------------------------------------------
       //‚             M o d i f i c a t i o n   J o u r n a l
       //‚----------------------------------------------------------------------
       //‚Date        Name             Description
       //‚----------  ---------------  ---------------------------------------
       //‚01/09/2023  Davey Webster    Add Logging for Debug Purposes
       //‚
       //‚
       //‚--------------------------------------------------------------------

     FF55CONFIG IF   E           K DISK

       //‚Log File for Debugging Problem with Duplicate Keys from Calling Pgm
     fGET0010F  uf a e             disk    prefix(log_) infds(log_Infds)

       //----------------------------------------------------------------------
     d MainRoutine     PR                  Extpgm('GETNXTNO')
     d                                2A
     d                                2A
     d                               15  0
     d                                1n
     d MainRoutine     PI
     d $system                        2A
     d $index                         2A
     d $nxtno                        15  0
     d $bssv                          1n
       // The include members below are created by WSDL2RPG
      /copy /e1int/nn/GetNextNoManager.rpgleinc
      /copy /e1int/nn/GetNextNoManager_util.rpgleinc
      /copy /QIBM/ProdData/OS/WebServices/V1/client/include/Axis.rpgleinc
     d ws_ds           ds                  likeds(This_t) inz
     d m1              ds                  likeds(InputGetNextNo_t)
     d m2              ds                  likeds(OutputGetNextNo_t)
     d m3              ds                  likeds(xsd_int)
     d m4              ds                  likeds(xsd_string)

       //‚------------------------------------------------------------------
       //‚File Information Data Structure for Log File GET0010F
       //‚------------------------------------------------------------------
     d log_Infds       ds
     d  log_File              83     92a
     d  log_Library           93    102a
     d  log_RecordLen        125    126b 0
     d  log_MbrName          129    138a
     d  log_NbrCurRcd        156    159i 0
     d  log_Format           261    270a
     d  log_MemberNum        395    396b 0
     d  log_Member           395    396a
     d  log_Record#          397    400b 0
     d  log_Record           397    400a

       //‚------------------------------------------------------------------
       //‚Program Status Data Structure
       //‚------------------------------------------------------------------
     d PgmStsDs       sds           429    NoOpt
     d  PgmName                      10a                                        Program Name
     d  PgmStatus                     5s 0
     d  PgmPrvSts                     5s 0
     d  PgmStmt                       8a
     d  PgmSubr                       8a
     d  PgmParm#                      3s 0
     d  PgmMsgType                    3a
     d  PgmMsgNbr                     4a
     d  PgmMchIns                     4a
     d  PgmWrkArea                   30a
     d  PgmLib                       10a
     d  PgmMsgData                   80a
     d  PgmMsgId                      4a
     d  PgmUnUsed                    26a
     d  PgmFile                       8a
     d  PgmFileSts                   35a
     d  PgmJob                       10a
     d  PgmUser                      10a
     d  PgmJobNbr                     6s 0
     d  PgmJobDate                    6s 0
     d  PgmRunDate                    6s 0
     d  PgmRunTime                    6s 0
     d  PgmCrtDate                    6a
     d  PgmCrtTime                    6a
     d  PgmCrtLvl                     4a
     d  PgmSrcFile                   10a
     d  PgmSrcLib                    10a
     d  PgmSrcMbr                    10a
     d  PgmProc                      10a
     d  PgmModProc                   10a
     d  PgmUnUsed2                   76

       //‚Local Variable Declaration
     d X_A1            S             10I 0 inz
     d X_A2            S             10A   inz
     d X_A7            S             80A   inz
     d X_ENDPOINT      S            100A   inz
     d X_USER          S             10A   inz
     d X_EV01          S              1A   inz
     d X_ENV           S             10A   inz
     d X_CSTR          S             40A   inz
     d X_PASS          S             30A   inz
     d NULLSTR         s              1
     d MaxAttempts     s             10i 0 inz(128)


     d rtnInd          s              1n
     D phb             S               *
     D BNode1          S               *
     D BNode2          S               *
     D BNode3          S               *
     D uriWSSE         C                   'http://docs.oasis-open.org/wss-
     D                                     /2004/01/oasis-200401-wss-wssec-
     D                                     urity-secext-1.0.xsd'
     D uriWSU          C                   'http://docs.oasis-open.org/wss-
     D                                     /2004/01/oasis-200401-wss-wssec-
     D                                     urity-utility-1.0.xsd'
     D uriToken        C                   'http://docs.oasis-open.org/wss-
     D                                     /2004/01/oasis-200401-wss-usern-
     D                                     ame-token-profile-1.0#PasswordT-
     D                                     ext'
     D uriEncry        C                   'http://docs.oasis-open.org/wss-
     D                                     /2004/01/oasis-200401-wss-soap-
     D                                     message-security-1.0#Base64Bina-
     D                                     ry'
      /free
          setll *start F55CONFIG;
          READ F55CONFIG;
          Dow Not %EOF(F55CONFIG);
             X_A7=S1DS01;
             X_USER=S1USER;
             X_PASS=S1DL01;
             X_ENV=S1ENVD;
             READ F55CONFIG;
          EndDo;

       //‚Initialize Variables & Start Connection Attempts
          clear log_Attempts;                                                   //‚Number of Attempt
          for log_Attempts = 1 to MaxAttempts;

             X_ENDPOINT=%TRIML(%TRIMR(X_A7)) +'/GetNextNoManager';
             X_CSTR='DN='+ %TRIML(%TRIMR(X_USER)) + ',ENV=' +
                     %TRIML(%TRIMR(X_ENV)) + ',ROLE=*ALL';
       //‚Create a connection to the web service
             ws_ds.endpoint = %TRIML(%TRIMR(X_ENDPOINT));
             if stub_create_GetNextNoManager(ws_ds);
                m1.nextNumberingIndexNumber.value=%int($index);
                m1.productCode.value=$system;
                m1.isNil_InputGetNextNo_t='0';
                $nxtno=0;
                axiscStubSetSecure(ws_ds.handle:
                                  '/QIBM/UserData/ICSS/Cert/Server/DEFAULT.KDB':
                                  '':'':'NONE':'NONE':'':'':'true');
                phb = axiscStubCreateSOAPHeaderBlock(ws_ds.handle:'Security':
                                                     uriWSSE:'wsse');
                axiscHeaderBlockCreateNamespaceDeclINamespace(phb:'wsu':uriWSU);
                axiscHeaderBlockCreateStdAttribute(phb:
                                                AXISC_ATTR_MUST_UNDERSTAND_TRUE:
                                                AXISC_SOAP_VER_1_1);
                Bnode1=axiscHeaderBlockCreateChildBasicNode(phb:
                                                            AXISC_ELEMENT_NODE:
                                                            'UsernameToken':
                                                            'wsse':*NULL:*NULL);
                axiscHeaderBlockAddChild(phb:Bnode1);
                Bnode2=axiscHeaderBlockCreateChildBasicNode(phb:
                                                            AXISC_ELEMENT_NODE:
                                                            'Username':'wsse':
                                                            *NULL: *NULL);
                axiscBasicNodeAddChild(Bnode1:Bnode2);
                Bnode3=axiscHeaderBlockCreateChildBasicNode(phb:
                                                        AXISC_CHARACTER_NODE:
                                                        *NULL:*NULL:*NULL:
                                                        %TRIML(%TRIMR(X_CSTR)));
                axiscBasicNodeAddChild(Bnode2:Bnode3);
                Bnode2=axiscHeaderBlockCreateChildBasicNode(phb:
                                                         AXISC_ELEMENT_NODE:
                                                         'Password':'wsse':
                                                         *NULL: *NULL);
                axiscBasicNodeCreateAttribute(Bnode2:'Type':*NULL:*NULL:
                                              uriToken);
                axiscBasicNodeAddChild(Bnode1:Bnode2);
                Bnode3=axiscHeaderBlockCreateChildBasicNode(phb:
                                                        AXISC_CHARACTER_NODE:
                                                        *NULL:*NULL:*NULL:
                                                        %TRIML(%TRIMR(X_PASS)));
                axiscBasicNodeAddChild(Bnode2:Bnode3);

       //‚Call the web service NextNumber operation
                rtnInd = stub_op_GetNextNoX0010(ws_ds: m1: m2);

                $bssv=rtnInd;
       //‚Display The Returned Value of Next Number
                m3.value=m2.nextNumberRange1.value;
                X_A2=%char(m3.value);
                $nxtno=m3.value;

       //‚Close the web service connection
                stub_destroy_GetNextNoManager(ws_ds);
             else;
                dsply %subst(ws_ds.excString:1:50);
             endif;

       //‚If The $nxtno is *zero or is same as it was Previously, Generate Log
             if $nxtno = *zero or $nxtno = log_FinalNext#;
                if log_Attempts = 1;
                   log_FirstStamp   =   %timestamp;                             //‚First Time Stamp
                   log_LastStamp    =   %timestamp;                             //‚Last Time Stamp
                   log_FinalNext#   =   $nxtno;                                 //‚Final Next Number
                   log_JobName      =   PgmJob;                                 //‚Job Name
                   log_JobUser      =   PgmUser;                                //‚Job User Profile
                   log_JobNbr       =   PgmJobNbr;                              //‚Job Number
                   log_Job = 'job(' + %editc(PgmJobNbr:'X') + '/' +             //‚Full Job Name
                             %trim(PgmUser) + '/' + %trim(PgmJob) + ')';
                endif;

                if log_Attempts = 1;
                   write GET0010F#;
                else;
                   chain log_Record# GET0010F;
                   if %found(GET0010F);
                      log_LastStamp    =   %timestamp;                          //‚Last Time Stamp
                      log_Attempts    += 1;                                     //‚# of Attempts
                      log_FinalNext#   =   $nxtno;                              //‚Final Next Number
                      update GET0010F# %fields(log_LastStamp:
                                               log_Attempts:
                                               log_FinalNext#);
                   else;
                      log_FirstStamp   =   %timestamp;                          //‚First Time Stamp
                      log_LastStamp    =   %timestamp;                          //‚Last Time Stamp
                      log_FinalNext#   =   $nxtno;                              //‚Final Next Number
                      log_JobName      =   PgmJob;                              //‚Job Name
                      log_JobUser      =   PgmUser;                             //‚Job User Profile
                      log_JobNbr       =   PgmJobNbr;                           //‚Job Number
                      log_Job = 'job(' + %editc(PgmJobNbr:'X') + '/' +          //‚Full Job Name
                                %trim(PgmUser) + '/' + %trim(PgmJob) + ')';
                      write GET0010F#;
                   endif;

                endif;
             else;
                leave;
             endif;
          endfor;
          log_FinalNext# = $nxtno;                                              //‚Final Next Number

          return;
      /end-free
