/*********************************************************************/
/*                                                                 */
/*  System:          Hog Procurement System  (HPS)                 */
/*  Program Number:  HPSDIBWCL                                     */
/*  Program Name:    Safe Data BGF Weekly Upload                   */
/*  Programmer:      Brad Baden                                    */
/*  Create Date:     08/19/2021                                    */
/*                                                                 */
/*  Function:        This CL is submitted from program HPSDIBWDCL  */
/*                   to upload from SafeData production SOW Weekly */
/*                   data.                                         */
/*                                                                  */
/*  NOTE:  The ORIGINAL version of this program was FTPSDIRTVW.     */
/*         Since we are no longer FTPing from the AS400, we wanted  */
/*         to remove FTP from the name of the program.  Informatica */
/*         executes what we call a wrapper CLP that will execute    */
/*         this CLP that does all the processing.  Processing was   */
/*         also added to make sure we had records to process.  If   */
/*         not, an email is sent to notify us we didn't receive     */
/*         any records to assist us in investagating why.           */
/*                                                                  */
/*                                                                 */
/*******************************************************************/
/*  MODIFIED BY     ->                                             */
/*******************************************************************/

             PGM

             DCL        VAR(&GRPPRFL)  TYPE(*CHAR)  LEN(10)
             DCL        VAR(&NBRRCDS)   TYPE(*DEC)  LEN(10 0)
             DCL        VAR(&RECIPIENT) TYPE(*CHAR) LEN(25)

             CHGJOB     LOG(4 99 *SECLVL) LOGCLPGM(*YES)

/*******************************************************************/
/*  Save the current library list                                  */
/*******************************************************************/
             PSHLIBL35

/*********************************************************************/
/*  Retrieve the user's Group Profile.  Set the library list         */
/*  based on the group profile.                                      */
/*********************************************************************/

             RTVUSRPRF  GRPPRF(&GRPPRFL)

             SELECT
               WHEN       COND(&GRPPRFL = 'SBDPGMR ') THEN(DO)
                 SETLIBL    JOBD(PRKINTT)
                 CHGVAR     VAR(&RECIPIENT) +
                              VALUE('ftpsdirtv_cpyf_fail_test')
               ENDDO

               OTHERWISE  CMD(DO)
                 SETLIBL JOBD(PRKPRDGUY)
                 CHGVAR     VAR(&RECIPIENT) +
                              VALUE('ftpsdirtv_cpyf_fail')
               ENDDO

             ENDSELECT

/*********************************************************************/
/*  Call program to process the daily production data                */
/*********************************************************************/

             CALL       PGM(HP267CL)

          /* Check if any records exist in file HSP267 */
             RTVMBRD    FILE(HSP267) NBRCURRCD(&NBRRCDS)

             SELECT
               WHEN     COND(&NBRRCDS *GT 0) THEN(DO)

                 CPYF   FROMFILE(HSP267) TOFILE(HSP267SV) +
                          MBROPT(*REPLACE)

                 CLRPFM FILE(HSP267)
               ENDDO

               OTHERWISE  CMD(DO)
                 ESNDMAIL   RECIPIENT(&RECIPIENT) +
                             SUBJECT('Weekly SOW Receiving Failed') +
                             MSG('The weekly SOW Receiving file +
                             (HSP267) contained no records.  Determine +
                             the reason no records were sent from +
                             Safedata.')
                ENDDO
             ENDSELECT

/*********************************************************************/
/*  Restore saved library list in case this CL called interactively  */
/*********************************************************************/
             POPLIBL35

             RETURN

             ENDPGM
