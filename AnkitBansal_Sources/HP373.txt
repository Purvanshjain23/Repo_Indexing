      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      HOG PRODUCTION
      * PROGRAM:     HP373 - WRITE CLOSE HOG GROUP FILES TO DATABASE
      * PROGRAMMER:  LEANNE FEDOR
      * CREATED:     06/27/94
      *
      * FUNCTION: THIS PROGRAM READS THE CLOSED HOG GROUP WORKFILES
      *           AND WRITES THE CLOSED GROUP RECORDS TO THE DATABASE
      *           FOR ALL GROUPS THAT HAD NO ERRORS.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 06/28/01  LeAnne Fedor
      *           Manager and supervisor fields removed from Hog Group file.
      *           So, we removed logic that populated these fields.
      *
      * 07/06/09  LeAnne Ramsey
      *           Recompile only. Added new field 'Continuous Flow Flag' to Hog Group file.
      *
      * 10/15/13  LeAnne Ramsey (E2831)
      *           Recompile only. Added field 'MTech Reference'.
      *
      * 03/26/20  Danny Nguyen - P405 - Farm Number Increase
      *           Recompile only. Increased Bin Code (@@BNCD) in HSPREF file from 5A to 6A.
      *
      * 05/10/22 Eric L SDN736 Recompiled ticket nbr increase (TKNO & RTNO 7.0 TO 9.0)
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     FHSP018    IF   E           K DISK
      *    FARM SITE
      *
      *
     FHSP023    IF   E           K DISK
      *    INSTALLED EQUIPMENT
      *
      *
     FHSP033    O    E           K DISK
      *   CLOSED HOG GROUP
      *
      *
     FHSL034D   UF   E           K DISK
      *    HOG GROUP
      *
      *
     FHSL038E   IF   E           K DISK
      *    FEED TICKET DETAIL
      *
      *
     FHSP039    O    E           K DISK
      *   CLOSED EQUIPMENT
      *
      *
     FHSP371    IF   E           K DISK
      *    GROUP WORKFILE
      *
      *
     FHSP373    IF   E           K DISK
      *    WORKFILE FOR GROUP HEADER
      *
      /EJECT
      ****************************************************************
      * INPUT SPECS
      ****************************************************************
      *
      *---------------------------------------------------------------
      *  NAMED CONSTANTS
      *---------------------------------------------------------------
      *
     D YES             C                   CONST('Y')
     D NO              C                   CONST('N')
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *---------------------------------------------------------------
      *  LOCAL DATA AREA
      *---------------------------------------------------------------
      *
     D                UDS
     D  LDBPDT                 1     08  0
     D  LDEPDT                 9     16  0
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * MAINLINE
      ****************************************************************
      *
      * PROCESS EACH GROUP HEADER RECORD IN THE WORKFILE.
      *
      *
     C     *IN90         DOUEQ     *ON                                          MAIN DO
     C                   READ      HSP373                                 90
      *
     C     *IN90         IFEQ      *OFF                                         IF CLOSED
      *
     C     WHHGSN        SETLL     HSP371
     C                   MOVEL     YES           FIRST
      *
     C     *IN91         DOUEQ     *ON                                          DO DETAIL
     C     WHHGSN        READE     HSP371                                 91
     C     *IN91         IFEQ      *OFF                                         IF DETAIL
     C     FIRST         IFEQ      YES                                          IF FIRST
     C                   EXSR      $FARM
     C                   EXSR      $FEED
     C                   EXSR      $GROUP
     C                   Z-ADD     WGHGSN        CGHGSN
     C                   MOVEL     NO            FIRST
     C                   ENDIF                                                  IF FIRST
      *
      * WRITE A CLOSED GROUP RECORD FOR EACH WORKFILE RECORD
      *
     C                   MOVEL     WGCACD        CGCACD
     C                   MOVEL     WGRPCD        CGRPCD
     C                   Z-ADD     WGCGAM        CGCGAM
     C                   Z-ADD     WGCGHD        CGCGHD
     C                   Z-ADD     WGCGLB        CGCGLB
     C                   Z-ADD     WGCRLB        CGCRLB
     C                   Z-ADD     WGCGQT        CGCGQT
     C                   Z-ADD     WGSHHD        CGSHHD
     C                   Z-ADD     WGSHLB        CGSHLB
     C                   Z-ADD     WGRCHD        CGRCHD
     C                   Z-ADD     WGRCLB        CGRCLB
     C                   Z-ADD     WGDOHD        CGDOHD
     C                   Z-ADD     WGDOLB        CGDOLB
     C                   z-add     wgcdhd        cgcdhd
     C                   z-add     wgcdlb        cgcdlb
     C                   z-add     wgydhd        cgydhd
     C                   z-add     wgydlb        cgydlb
     C                   WRITE     CGREC
      *
     C                   ENDIF                                                  IF DETAIL
     C                   ENDDO                                                  DO DETAIL
      *
     C                   ENDIF                                                  IF CLOSED
     C                   ENDDO                                                  MAIN DO
      *
     C                   SETON                                        LR
      /EJECT
      ****************************************************************
      * UPDATE THE HOG GROUP RECORD IN THE HOG MASTER FILE
      ****************************************************************
      *
      * THE FOLLOWING FIELDS ARE UPDATED FOR THE HOG GROUP
      *    MANAGER CODE
      *    SUPERVISOR CODE
      *    COMPANY RELATIONSHIP CODE
      *    FEED ALLOCATED FLAG
      *    GROUP STATUS
      *    CLOSED DATE
      *
     C     $GROUP        BEGSR
      *
     C     WGHGSN        CHAIN     HSL034D                            94
     C     *IN94         IFEQ      *OFF
     C                   MOVEL     WKCRCD        HGCRCD
     C                   MOVEL     WKFAFL        HGFAFL
     C                   MOVEL     'CL'          HGGSCD
     C                   Z-ADD     LDEPDT        HGCLDT
     C                   UPDATE    HGREC
      *
     C                   EXSR      $EQUIP
      *
     C                   ENDIF
      *
     C                   ENDSR
      /EJECT
      ****************************************************************
      * RETRIEVE FARM SITE INFORMATION
      ****************************************************************
      *
     C     $FARM         BEGSR
      *
     C     WGFSCD        CHAIN     HSP018                             92
     C     *IN92         IFEQ      *OFF                                         IF FOUND
     C                   MOVEL     FSCRCD        WKCRCD
     C                   ELSE
     C                   MOVE      *BLANK        WKCRCD
     C                   ENDIF                                                  IF FOUND
      *
     C                   ENDSR
      /EJECT
      ****************************************************************
      * WRITE CLOSED EQUIPMENT FILE RECORDS FOR HOG GROUP
      ****************************************************************
      *
      * USING THE BUILDING/ROOM RETRIEVED FROM THE FARM SITE FILE,
      * READ ALL THE INSTALLED EQUIPMENT RECORDS FOR THE FARM/BLDG/ROOM
      * AND WRITE EACH TO THE CLOSED EQUIPMENT FILE FOR THIS GROUP.
      *
     C     $EQUIP        BEGSR
      *
     C                   MOVEL     WGHGSN        CEHGSN
      *
     C     KEY01         SETLL     HSP023
      *
     C     *IN93         DOUEQ     *ON                                          DO EQUIP
     C     KEY01         READE     HSP023                                 93
     C     *IN93         IFEQ      *OFF                                         IF FOUND
     C                   MOVEL     IEEQCD        CEEQCD
     C                   MOVEL     IEETCD        CEETCD
     C                   WRITE     CEREC
     C                   ENDIF                                                  IF FOUND
     C                   ENDDO                                                  DO EQUIP
      *
     C                   ENDSR
      /EJECT
      ****************************************************************
      * DETERMINE IF ANY FEED WAS ALLOCATED TO THE HOG GROUP
      ****************************************************************
      *
      * THE 'FEED ALLOCATED FLAG' IN THE HOG GROUP MASTER FILE MUST
      * BE UPDATED DURING THIS CLOSE PROCESS. SO, DETERMINE IF ANY
      * FEED DETAIL RECORD FOR THIS GROUP HAS BEEN ALLOCATED. IF SO,
      * SET THE WORKFIELD FLAG TO YES.
      *
     C     $FEED         BEGSR
      *
     C                   MOVEL     NO            WKFAFL
      *
     C     WGHGSN        SETLL     HSL038E
     C                   SETOFF                                       95
      *
     C     *IN95         DOUEQ     *ON                                          DO FEED
     C     WKFAFL        OREQ      YES
     C     WGHGSN        READE     HSL038E                                95
     C     *IN95         IFEQ      *OFF                                         IF ALLOC
     C     FDALFL        ANDEQ     'A'
     C                   MOVEL     YES           WKFAFL
     C                   ENDIF                                                  IF ALLOC
     C                   ENDDO                                                  DO FEED
      *
     C                   ENDSR
      /EJECT
      ****************************************************************
      *  INITIALIZATION SUBROUTINE
      ****************************************************************
      *
     C     *INZSR        BEGSR
      *
      * KEY FOR HSP023-INSTALLED EQUIPMENT
      *
     C     KEY01         KLIST
     C                   KFLD                    WGFSCD
     C                   KFLD                    HGBLCD
     C                   KFLD                    HGRMCD
      *
      *  WORK FIELDS
      *
     C     *LIKE         DEFINE    FSCRCD        WKCRCD
     C     *LIKE         DEFINE    HGFAFL        WKFAFL
     C                   MOVEL     YES           FIRST             1
      *
     C                   ENDSR
      /EJECT
