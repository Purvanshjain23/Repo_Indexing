/***************************************************************** */
/*                                                                 */
/*  SYSTEM:          TRIUMPH FOODS                                 */
/*  PROGRAM NUMBER:  TF431CL                                       */
/*  PROGRAM NAME:    CLAIM EXPENSE DISTRIBUTION REPORT             */
/*  PROGRAMMER:      LEANNE FEDOR                                  */
/*  CREATE DATE:     09/28/05                                      */
/*                                                                 */
/*  THIS CL IS CALLED FROM THE WORK WITH. BUT, IT IS ALSO CALLED   */
/*  AUTOMATICALLY FROM THE WEEKLY REVENUE CLOSE FUNCTION.          */
/*                                                                 */
/*******************************************************************/
/* MODIFIED:                                                       */
/*                                                                 */
/* 05/21/07  LEANNE RAMSEY                                         */
/*           ADDED A NEW DATA AREA (DATSTPRD) TO CONTROL E-MAILS.  */
/*                                                                 */
/* 05/06/09  LEANNE RAMSEY                                         */
/*           CHANGED PRINT LOGIC TO MATCH MEAT COSTING.            */
/*******************************************************************/

             PGM

             DCL        VAR(&QDATA) TYPE(*CHAR) LEN(181)
             DCL        VAR(&EMAIL) TYPE(*CHAR) LEN(38)

             DCL        VAR(&OUTQ) TYPE(*CHAR) LEN(10)
             DCL        VAR(&HOLD) TYPE(*CHAR) LEN(4)
             DCL        VAR(&SAVE) TYPE(*CHAR) LEN(4)
             DCL        VAR(&COPY) TYPE(*DEC)  LEN(1)

             DCL        VAR(&LDFWEDT) TYPE(*DEC) LEN(8)
             DCL        VAR(&LDTWEDT) TYPE(*DEC) LEN(8)
             DCL        VAR(&LDPFCD)  TYPE(*CHAR) LEN(1)
             DCL        VAR(&TSTPRDFL) TYPE(*CHAR) LEN(1)

             CHGVAR     VAR(&LDPFCD)    VALUE(%SST(*LDA 37 1))
             CHGVAR     VAR(&LDFWEDT)   VALUE(%SST(*LDA 320 8))
             CHGVAR     VAR(&LDTWEDT)   VALUE(%SST(*LDA 328 8))

/*----------------------------------------------------------------*/
/* For sending Emails:                                            */
/* Retrieve Data Area that lets us know whether we are in Test    */
/* or Production (Always default to the "TEST" Distribution List.)*/
/*----------------------------------------------------------------*/

             CHGVAR     VAR(&EMAIL) VALUE('TF Test')
             RTVDTAARA  DTAARA(DATSTPRD (1 1)) RTNVAR(&TSTPRDFL)

/*---------------------------------------------------------------------*/
/* OVERRIDE THE KEY ON THE AR CLAIM EXPENSE FILE                       */
/*---------------------------------------------------------------------*/

             OVRDBF     FILE(PPA1CPL1) SHARE(*YES)
             OPNQRYF    FILE((PPA1CPL1)) KEYFLD((A1YSNX) (A1C0C7) +
                          (A1F1AA) (A1DXNB)) SEQONLY(*YES)

/*---------------------------------------------------------------------*/
/* EXTRACT INVOICE CHARGE DETAIL RECORDS                               */
/*---------------------------------------------------------------------*/

             CHGVAR     VAR(&QDATA) VALUE(' ')

/* ALWAYS EXTRACT ONLY THE INVOICE CHARGES GENERATED FROM "CLAIMS"     */

             CHGVAR     VAR(%SST(&QDATA 1 23)) VALUE('ICCCCD *EQ +
                          "CLAIMPAY  "')
/* FROM DATE                                                           */
             CHGVAR     VAR(%SST(&QDATA 25 15)) VALUE('*AND ICICDT *GE')
             CHGVAR     VAR(%SST(&QDATA 41 8)) VALUE(&LDFWEDT)

/* TO DATE                                                             */
             CHGVAR     VAR(%SST(&QDATA 50 15)) VALUE('*AND ICICDT *LE')
             CHGVAR     VAR(%SST(&QDATA 66 8)) VALUE(&LDTWEDT)


             OVRDBF     FILE(TFP021) SHARE(*YES)
             OPNQRYF    FILE((TFP021)) QRYSLT(&QDATA) KEYFLD(*FILE) +
                          SEQONLY(*YES)

/*---------------------------------------------------------------------------*/
/* SET PARAMETERS FROM LDA FOR LISTINGS                                      */
/*---------------------------------------------------------------------------*/

 OUTQ:       CHGVAR     VAR(&OUTQ) VALUE(%SST(*LDA 401 10))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(*JOB))

 HOLD:       CHGVAR     VAR(&HOLD) VALUE(%SST(*LDA 411 4))
             IF         COND(&HOLD *NE '*YES' *AND &HOLD *NE '*NO ') +
                          THEN(CHGVAR VAR(&HOLD) VALUE(*NO))

 COPY:       CHGVAR     VAR(&COPY) VALUE(%SST(*LDA 419 1))
             IF         COND(&COPY = 0) THEN(CHGVAR VAR(&COPY) +
                          VALUE(1))

 SAVE:       CHGVAR     VAR(&SAVE) VALUE(%SST(*LDA 415 4))
             IF         COND(&LDPFCD *NE ' ') +
                          THEN(CHGVAR VAR(&SAVE) VALUE(*YES))
             IF         COND(&SAVE *NE '*YES' *AND &SAVE *NE '*NO ') +
                          THEN(CHGVAR VAR(&SAVE) VALUE(*NO))

/*---------------------------------------------------------------------*/
/* CALL THE PRINT PROGRAM                                              */
/*---------------------------------------------------------------------*/

             OVRPRTF    FILE(PRINT198) OUTQ(&OUTQ) COPIES(&COPY) +
                          HOLD(&HOLD) SAVE(&SAVE)
             CALL       PGM(TF631)

/*--------------------------------------------------------------------------*/
/* If this is a "Final" run out of the Weekly Revenue Close,                */
/*    e-mail the report to a distribution list                              */
/*--------------------------------------------------------------------------*/

             IF         COND(&LDPFCD *EQ 'F') THEN(DO)          /* If Final */

             IF         COND(&TSTPRDFL *EQ 'P') THEN(CHGVAR +
                          VAR(&EMAIL) VALUE('TF Revenue Close-Claim +
                          Expense'))

             ESNDMAIL   RECIPIENT(&EMAIL) SUBJECT('Claim Expense +
                          Distribution Report') MSG('Distribution +
                          List: TF Revenue Close-Claim Expense') +
                          ATTLIST((* *PDF *N PRINT198 *))
             MONMSG     MSGID(CPF0000)
             ENDDO                                              /* If Final */


             CLOF       OPNID(TFP021)
             CLOF       OPNID(PPA1CPL1)
             DLTOVR     FILE(*ALL)

             ENDPGM

