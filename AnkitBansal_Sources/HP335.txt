      *****************  RPG PROGRAM HEADING  ***********************
      *
      * SYSTEM:      HOG PRODUCTION
      * PROGRAM:     HP335
      * TITLE:       DISTRIBUTE CHECK DEDUCTION/ADD-ONS TO CHECK DETAIL
      * PROGRAMMER:  ROSE CENTONZE
      * CREATED:     06/08/94
      *
      * FUNCTION: THIS PROGRAM DISTRIBUTES THE DOLLAR AMOUNT OF EACH
      *           DEDUCTION/ADD-ON THAT A USER KEYED FOR A CHECK TO THE
      *           CHECK DETAIL LINES THAT THE USER ENTERED FOR THE
      *           CHECK.
      *
      *           THIS PROGRAM IS CALLED FROM:
      *                HP136-MAINTAIN CHECK DETAIL
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      * 08/02/94  R. MARTIN
      *           REMOVE REFERENCE TO CHECK B/O.
      *
      * 09/21/94  LEANNE FEDOR
      *           REWROTE.
      *
      * 10/06/94  LEANNE FEDOR
      *           AS REQUESTED BY ED BROWN, CHANGED THE DISTRIBUTION
      *           LOGIC TO NOT DISTRIBUTE DOLLARS TO CHECK DETAIL
      *           LINES REPRESENTING 'DEAD' HOGS---DETAIL LINES WITH
      *           GROSS AMOUNT OF ZERO DOLLARS.
      *
      * 06/19/95  LEANNE FEDOR
      *           CHANGED FOR MOVEMENT REWRITE.
      *
      * 02/16/98  LeAnne Fedor
      *           Recompile only. Added the following fields to the
      *           database for sales movement detail and check detail:
      *                     condemned head and pounds
      *                     yard dead head and pounds
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     FHSP064    IF   E           K DISK
      *    CHECK DETAIL              (KEY: CDCVNO CDCHDT CDCHNO CDLNNO)
      *
     FHSL065F   UF A E           K DISK    COMMIT
      *    CHECK ADJUSTMENTS
      *    (KEY: CJCHDT CHCVNO CHCHNO CJLNNO)
      *
     FHSL065A   IF   E           K DISK
     F                                     RENAME(CJREC:CJRECA)
      *    CHECK ADJUSTMENTS  (KEY: CJCHDT CHCVNO CJCHNO CJLNNO)
      *                       (SELECT: CJCJFL = 'D', CJLNNO = 0)
      *
      /EJECT
      ****************************************************************
      * INPUT SPECIFICATIONS
      ****************************************************************
      *
      *---------------------------------------------------------------
      * STANDARD DATABASE FILE INFORMATION DATA STRUCTURE
      *---------------------------------------------------------------
      *    EXTERNALLY DEFINED AS UTDBFR (RECORD FORMAT: FDBCKD)
     D DBFEED        E DS                  EXTNAME(UTDBFR)
      *
      *
     D                 DS
     D  DUMMYD                 1      1
      *  THE PRECEDING DUMMY DATA STRUCTURE IS ONLY TO KEEP THE I-SPEC
      *  INFO FROM RUNNING OVER INTO THE 'CALC SPECS' ON THE COMPILE
      *  LISTING.  IT SERVES NO OTHER PURPOSE AND CAN BE DELETED
      *  AT ANY TIME!!!
      /EJECT
      ****************************************************************
      * MAIN LINE
      ****************************************************************
      *
      * FIRST, DELETE ALL DISTRIBUTED DEDUCTION/ADD-ON RECORDS, IF ANY,
      * FOR THIS CHECK.
      *
     C                   EXSR      $DEDAD
      *
      * NEXT, SUM UP THE LIVE HEAD ON THE CHECK DETAIL RECORDS.
      * THIS WILL BE USED IN CALCULATING THE DEDUCTION AMOUNT
      * TO BE DISTRIBUTED PER HEAD.
      *
     C                   EXSR      $TOTHD
      *
      * IF YOU ARRIVED AT A TOTAL HEAD, THEN PROCEED TO DISTRIBUTE
      * THE DEDUCTION/ADD-ON DOLLARS TO THE CHECK DETAIL LINES.
      *
     C     WKLVHD        IFNE      0                                            IF HEAD
      *
      * READ/PROCESS EACH DEDUCTION RECORD KEYED BY THE USER
      * IN THE CHECK ADJUSTMENT FILE (THIS LF PRESENTS ONLY THE
      * DEDUCTION/ADD-ON ADJUSTMENTS WITH LINE NUMBERS OF ZERO).
      *
     C     K1P065        SETLL     HSL065A
     C                   SETOFF                                       80
      *
     C     *IN80         DOUEQ     *ON                                          DO ADJ LOOP
     C     K1P065        READE     HSL065A                                80
     C     *IN80         IFEQ      *OFF                                         IF ADJ READ
      *
      * SAVE THE TOTAL DOLLARS THE USER KEYED ON THIS
      * DEDUCTION. THIS IS THE DOLLAR AMOUNT THAT MUST BE
      * DISTRIBUTED TO EACH CHECK DETAIL RECORD FOR THIS
      * DEDUCTION.
      *
     C                   Z-ADD     CJCJAM        SVCJAM
     C                   MOVEL     CJCJCD        SVCJCD
     C                   Z-ADD     0             CJLNNO
     C                   Z-ADD     0             WKLNNO
     C                   Z-ADD     0             WKCJAM
     C                   Z-ADD     0             WKDIFF
      *
      * CALCULATE THE ADJUSTMENT AMOUNT PER HEAD TO BE
      * DISTRIBUTED TO EACH CHECK DETAIL RECORD FOR THIS
      * DEDUCTION. (IF WKLVHD IS ZERO, YOU WILL NOT BE IN
      * THIS LOOP.)
      *
     C     WKLVHD        IFNE      0
     C     CJCJAM        DIV(H)    WKLVHD        WKAMT
     C                   ENDIF
      *
      * READ/PROCESS EACH CHECK DETAIL RECORD MULTIPLYING
      * THE HEAD FROM THE CHECK DETAIL RECORD BY THE DISTRIBUTION
      * AMOUNT PER HEAD CALCULATED ABOVE. DO NOT DISTRIBUTE ANY
      * DEDUCTION DOLLARS TO 'DEAD' HOGS.
      *
     C     K1P065        SETLL     HSP064
     C                   SETOFF                                       81
      *
     C     *IN81         DOWEQ     *OFF                                         DO DISTRIB
     C     K1P065        READE     HSP064                                 81
     C     *IN81         IFEQ      *OFF                                         IF CHECK DTL
     C     CDLVHD        ANDNE     0
      *
     C     CDLVHD        MULT(H)   WKAMT         CJCJAM
      *
      * IF YOU HAVE ADJUSTMENT DOLLARS TO DISTRIBUTE, WRITE AN
      * ADJUSTMENT RECORD FOR THE CHECK DETAIL LINE NUMBER.
      *
     C     CJCJAM        IFNE      0                                            IF SOME $
     C                   ADD       CJCJAM        WKCJAM
     C                   Z-ADD     CDLNNO        CJLNNO
     C                   Z-ADD     CDLNNO        WKLNNO
     C                   WRITE     CJREC
     C                   ENDIF                                                  IF SOME $
     C                   ENDIF                                                  IF CHECK DTL
     C                   ENDDO                                                  DO DISTRIB
      *
      * NOW, DETERMINE WHETHER THE DEDUCTION AMOUNT KEYED
      * BY THE USER WAS DISTRIBUTED TO THE PENNY. IF NOT,
      * ADD IT TO THE LAST "DISTRIBUTED" ADJUSTMENT RECORD
      * THAT YOU WROTE FOR THIS ADJUSTMENT.
      *
     C     SVCJAM        SUB       WKCJAM        WKDIFF
     C     WKDIFF        IFNE      0                                            IF LEFTOVER
     C     K2P065        CHAIN     HSL065F                            83
     C     *IN83         IFEQ      *OFF                                         IF HIT
     C                   ADD       WKDIFF        CJCJAM
     C                   UPDATE    CJREC
     C                   ENDIF                                                  IF HIT
     C                   ENDIF                                                  IF LEFTOVER
      *
     C                   ENDIF                                                  IF ADJ READ
     C                   ENDDO                                                  DO ADJ LOOP
     C                   ENDIF                                                  IF HEAD
      *
     C                   SETON                                        LR
      /EJECT
      *---------------------------------------------------------------
      * $DEDAD - DELETE EXISTING DISTRIBUTED DEDUCTION/ADD-ON RECORDS
      *---------------------------------------------------------------
      *
     C     $DEDAD        BEGSR
      *
     C                   SETOFF                                       92
     C     K1P065        SETLL     HSL065F
      *
     C     *IN92         DOUEQ     *ON                                          DO DELETE
     C     K1P065        READE     HSL065F                                92
     C     *IN92         IFEQ      *OFF                                         IF FOUND
     C     CJCJFL        ANDEQ     'D'
     C     CJLNNO        ANDNE     0
     C                   DELETE    CJREC
     C                   ENDIF                                                  IF FOUND
     C                   ENDDO                                                  DO DELETE
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * $TOTHD - CALCULATE TOTAL LIVE HEAD ON THE CHECK DETAIL
      *---------------------------------------------------------------
      *
      * READ ALL THE CHECK DETAIL RECORDS AND ACCUMULATE LIVE HEAD.
      *
     C     $TOTHD        BEGSR
      *
     C     K1P065        SETLL     HSP064
     C                   SETOFF                                       90
     C                   Z-ADD     0             WKLVHD
      *
     C     *IN90         DOUEQ     *ON                                          DO HEAD
     C     K1P065        READE     HSP064                                 90
     C     *IN90         IFEQ      *OFF                                         IF CHECK DTL
     C                   ADD       CDLVHD        WKLVHD
     C                   ENDIF                                                  IF CHECK DTL
     C                   ENDDO                                                  DO HEAD
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * *INZSR - INITIALIZATION SUBROUTINE
      *---------------------------------------------------------------
      *
     C     *INZSR        BEGSR
      *
      * PARM LISTS
      *
     C     *ENTRY        PLIST
     C                   PARM                    SVCHDT
     C                   PARM                    SVCHNO
     C                   PARM                    SVCVNO
      *
     C     K1P065        KLIST
     C                   KFLD                    SVCHDT
     C                   KFLD                    SVCVNO
     C                   KFLD                    SVCHNO
      *
     C     K2P065        KLIST
     C                   KFLD                    SVCHDT
     C                   KFLD                    SVCVNO
     C                   KFLD                    SVCHNO
     C                   KFLD                    SVCJCD
     C                   KFLD                    WKLNNO
      *
      * SAVE FIELDS THAT CAME INTO THIS PROGRAM AS PARMS
      *
     C     *LIKE         DEFINE    CJCHDT        SVCHDT
     C     *LIKE         DEFINE    CJCVNO        SVCVNO
     C     *LIKE         DEFINE    CJCHNO        SVCHNO
      *
      * WORK FIELDS
      *
     C                   Z-ADD     0             WKAMT            15 5
     C     *LIKE         DEFINE    CDLVHD        WKLVHD
     C     *LIKE         DEFINE    CJLNNO        WKLNNO
     C     *LIKE         DEFINE    CJCJAM        WKDIFF
     C     *LIKE         DEFINE    CJCJAM        WKCJAM
      *
     C     *LIKE         DEFINE    CJCJAM        SVCJAM
     C     *LIKE         DEFINE    CJCJCD        SVCJCD
      *
     C                   ENDSR
      /EJECT
