      *****************  RPG PROGRAM HEADING  ************************
      * SYSTEM:      HOG PRODUCTION
      * PROGRAM:     HP617
      * TITLE:       LISTING OF RATIONS WITH ADDITIVES
      * PROGRAMMER:  LEANNE FEDOR
      * CREATED:     12/02/94
      *
      * FUNCTION: BATCH PROGRAM TO LIST THE RECORDS IN THE
      *           RATIONS MASTER FILE ALONG WITH THE ASSIGNED
      *           ADDITIVES.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 12/27/01  LeAnne Fedor
      *           Added new field 'Maximum age (days)' to listing.
      *
      * 05/31/07  LeAnne Ramsey
      *           Some users are having printing problems. So, we are switching
      *           from QPRINT to PRINT132 for the printer file and replacing
      *           John Erikson's customized printer override command in the CL with
      *           the standard override command.
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     FHSP071    IF   E           K DISK
      *    RATIONS                    (KEY: RNRNCD)
      *
     FHSP072    IF   E           K DISK
      *    RATION ADDITIVES           (KEY: RARNCD RAADCD)
      *
     Fprint132  O    F  132        PRINTER OFLIND(*INOF)
      *
      /EJECT
      ****************************************************************
      * INPUT SPECS
      ****************************************************************
      *
      *---------------------------------------------------------------
      * STANDARD PROGRAM STATUS DATA STRUCTURE
      *---------------------------------------------------------------
      *    EXTERNALLY DEFINED AS UTPGFR (RECORD FORMAT: PGMDSR)
     D PGMDS         ESDS                  EXTNAME(UTPGFR)
      *
      *---------------------------------------------------------------
      *  NAMED CONSTANTS
      *---------------------------------------------------------------
      *
     D YES             C                   CONST('Y')
     D NO              C                   CONST('N')
      *
     D                 DS
     D  DUMMYD                 1      1
      *  THE PRECEDING DUMMY DATA STRUCTURE IS ONLY TO KEEP THE I-SPEC
      *  INFO FROM RUNNING OVER INTO THE 'CALC SPECS' ON THE COMPILE
      *  LISTING.  IT SERVES NO OTHER PURPOSE AND CAN BE DELETED
      *  AT ANY TIME!!!
      /EJECT
      ****************************************************************
      * MAIN LINE
      ****************************************************************
      *
      * SET POSITION IN RATION FILE
      *
     C     *LOVAL        SETLL     HSP071
     C                   EXCEPT    HDRPT
      *
      * READ AND PRINT ALL RECORDS IN THE RATION MASTER.
      * FOR EACH RECORD READ, RETRIEVE THE ADDITIVES ASSIGNED TO THIS
      * RATION FROM THE RATION/ADDITIVE FILE.
      *
     C     *IN92         DOUEQ     *ON                                          MAIN DO
     C                   READ      HSP071                                 92
     C     *IN92         IFEQ      *OFF                                         IF NOT EOF
      *
     C     RNRNCD        IFNE      SVRNCD                                       IF BREAK
     C                   MOVEL     RNRNCD        SVRNCD
     C                   MOVEL     RNRNDS        SVRNDS
     C                   MOVEL     RNPPCD        SVPPCD
     C                   MOVEL     RNRTCD        SVRTCD
     C                   MOVEL     RNRNST        SVRNST
     C                   Z-ADD     RNLYPR        SVLYPR
     C                   Z-ADD     RNRBLB        SVRBLB
     C                   Z-ADD     RNMAXDAY      SVMAXDAY
     C     *INOF         IFEQ      *ON
     C                   EXCEPT    HDRPT
     C                   ENDIF
     C                   EXCEPT    HDRAT
     C                   MOVEL     YES           PRTFLG
     C                   ENDIF                                                  IF BREAK
      *
     C     *INOF         IFEQ      *ON
     C                   EXCEPT    HDRPT
     C                   EXCEPT    HDRAT
     C                   ENDIF
      *
      * RETREIVE AND PRINT ADDITIVES
      *
     C                   EXSR      $ADD
      *
     C                   ENDIF                                                  IF NOT EOF
     C                   ENDDO                                                  MAIN DO
      *
      *  IF THE MASTER FILE WAS EMPTY AND NO RECORDS WERE PRINTED,
      *  PRINT THE STANDARD 'NO DATA...' ERROR MESSAGE
      *
     C     PRTFLG        IFEQ      NO
     C                   EXCEPT    NODATA
     C                   ENDIF
      *
     C                   SETON                                        LR
      /EJECT
      *---------------------------------------------------------------
      * RETRIEVE AND PRINT ADDITIVES FOR A RATION
      *---------------------------------------------------------------
      *
     C     $ADD          BEGSR
      *
     C     SVRNCD        SETLL     HSP072
     C                   SETOFF                                       93
      *
     C     *IN93         DOUEQ     *ON                                          DO ADDITIVES
     C     SVRNCD        READE     HSP072                                 93
     C     *IN93         IFEQ      *OFF                                         IF NOT EOF
      *
     C     *INOF         IFEQ      *ON
     C                   EXCEPT    HDRPT
     C                   EXCEPT    HDRAT
     C                   ENDIF
      *
     C                   EXCEPT    DTADD
     C                   ENDIF                                                  IF NOT EOF
     C                   ENDDO                                                  DO ADDITIVES
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * *INZSR - INITIALIZATION SUBROUTINE
      *---------------------------------------------------------------
      *
     C     *INZSR        BEGSR
      *
      * FIELDS FOR CONTROL BREAK
      *
     C     *LIKE         DEFINE    RNRNCD        SVRNCD
     C     *LIKE         DEFINE    RNRNDS        SVRNDS
     C     *LIKE         DEFINE    RNPPCD        SVPPCD
     C     *LIKE         DEFINE    RNLYPR        SVLYPR
     C     *LIKE         DEFINE    RNRBLB        SVRBLB
     C     *LIKE         DEFINE    RNRTCD        SVRTCD
     C     *LIKE         DEFINE    RNRNST        SVRNST
     C     *LIKE         DEFINE    RNMAXDAY      SVMAXDAY
      *
      * SET UP HEADING DEFAULTS
      *
     C                   MOVEL     NO            PRTFLG            1
     C                   MOVE      *ALL'-'       DASH            132
     C                   TIME                    RTIME             6 0
      *
     C                   ENDSR
      /EJECT
      *-------------------------------------------------------------
      * REPORT HEADING LINES
      *-------------------------------------------------------------
      *
     Oprint132  E            HDRPT          1 01
     O                                            5 'HP617'
     O                                           71 'HOG PRODUCTION SYSTEM'
     O                                          122 'DATE'
     O                       UDATE         Y    132
      *
     O          E            HDRPT          1
     O                       SDUSR               10
     O                                           62 'LISTING OF RATIONS'
     O                                           77 'WITH ADDITIVES'
     O                                          122 'TIME'
     O                       RTIME              132 '  :  :  '
      *
     O          E            HDRPT          2
     O                                          122 'PAGE'
     O                       PAGE          Z    132
      *
     O          E            HDRPT          1
     O                                            6 'RATION'
     O                                           34 'PROD'
     O                                           47 'MAXIMUM'
     O                                           57 'LYSINE'
     O                                           65 'BATCH'
     O                                           73 'RATION'
     O                                           81 'RATION'
     O                                           95 '------------'
     O                                          104 'ADDITIVE'
     O                                          118 '-------------'
      *
     O          E            HDRPT          1
     O                                            4 'CODE'
     O                                           19 'DESCRIPTION'
     O                                           35 'PHASE'
     O                                           47 'AGE (DAYS)'
     O                                           57 'PERCENT'
     O                                           65 'POUNDS'
     O                                           72 'TYPE'
     O                                           81 'STATUS'
     O                                           87 'CODE'
     O                                          103 'DESCRIPTION'
     O                                          118 'STATUS'
      *
     O          E            HDRPT          1
     O                       DASH               132
      *
      *-------------------------------------------------------------
      * RATIONS HEADING LINE
      *-------------------------------------------------------------
      *
     O          E            HDRAT       1  1
     O                       SVRNCD               5
     O                       SVRNDS              28
     O                       SVPPCD              35
     O                       SVMAXDAY      k     45
     O                       SVLYPR        K     58
     O                       SVRBLB        K     66
     O                       SVRTCD              73
     O                       SVRNST              79
      *
      *-------------------------------------------------------------
      * ADDITIVE DETAIL LINE
      *-------------------------------------------------------------
      *
     O          E            DTADD          1
     O                       RAADCD              88
     O                       RAADDS             112
     O                       RARAST             116
      *-------------------------------------------------------------
      * NO DATA MESSAGE LINE
      *-------------------------------------------------------------
      *
     O          E            NODATA      1
     O                                           19 'NO DATA MEETS YOUR'
     O                                           38 'SELECTION CRITERIA'
      /EJECT
