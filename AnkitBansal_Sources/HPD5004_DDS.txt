     A*%%TS  SD  20020722  155838  ISLRAMS     REL-V5R1M0  5722-WDS
     A*****************************************************************
     A*
     A*  SYSTEM:        HOG PRODUCTION
     A*  DISPLAY FILE:  HPD5004- SELECT FARM SITE WITH FEED MILL
     A*                          BUSINESS OFFICE AND FARM SITE POSITIONER
     A*  CREATED BY:    LEANNE FEDOR
     A*  DATE:          05/09/97
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA12(12 'PREVIOUS')
     A N82                                  ROLLUP(25 'PAGE FORWARD')
     A*--------------------------------------------------------------*
     A* REFERENCE WINDOW RECORD FORMAT 1
     A*--------------------------------------------------------------*
     A          R HP5004R1
     A*%%TS  SD  20010521  170519  ISLRAMS     REL-V4R4M0  5769-PW1
     A                                      TEXT('Reference window')
     A                                      WINDOW(*DFT 17 60)
     A                                      WDWBORDER((*DSPATR RI) (*CHAR '    -
     A                                          '))
     A*--------------------------------------------------------------*
     A* RECORD FORMAT ONE
     A*--------------------------------------------------------------*
     A          R HP5004R2
     A                                      TEXT('Written in RPG pgm to prevent-
     A                                       screen from clearing')
     A                                      CLRL(*NO)
     A                                      OVERLAY
     A*--------------------------------------------------------------*
     A* RECORD FORMAT TWO
     A*--------------------------------------------------------------*
     A* THIS FORMAT IS NOT WRITTEN IN THE RPG PGM.  IT PREVENTS THE
     A* CALLING_PROGRAM_SCREEN FROM BEING CLEARED.
     A*--------------------------------------------------------------*
     A          R HP5004R3
     A                                      TEXT('Dummy format not written in R-
     A                                      PG pgm.')
     A                                      ASSUME
     A                                 11  1' '
     A*--------------------------------------------------------------*
     A* SUBFILE FORMAT ONE
     A*--------------------------------------------------------------
     A          R HP5004S1                  SFL
     A*%%TS  SD  20010521  170519  ISLRAMS     REL-V4R4M0  5769-PW1
     A                                      TEXT('subfile record format one')
     A            HDRRN1         4S 0H
     A            DFFMBO    R        O  7  1REFFLD(@@BOCD)
     A            DFFSCD    R        O  7  9REFFLD(@@FSCD)
     A                                      EDTCDE(M)
     A            DFFSNM    R        O  7 16REFFLD(FSFSNM)
     A            DFPPCD    R        O  7 43REFFLD(@@PPCD)
     A            DFPTCD    R        O  7 49REFFLD(@@PTCD)
     A            DFCELL    R        O  7 57REFFLD(HSRREF/@@CELL *LIBL/HSPREF)
     A                                      EDTCDE(Z)
     A*--------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*--------------------------------------------------------------
     A          R HP5004C1                  SFLCTL(HP5004S1)
     A*%%TS  SD  20020722  155838  ISLRAMS     REL-V5R1M0  5722-WDS
     A                                      SFLSIZ(0050)
     A                                      SFLPAG(0008)
     A                                      WINDOW(HP5004R1)
     A                                      TEXT('control format one')
     A                                      OVERLAY
     A                                      SFLCSRRRN(&HDRRN)
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND(*MORE)
     A                                      RMVWDW
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            HDRRN          5S 0H
     A                                  1  5'SELECT FARM SITE FOR FEED MILL BUS-
     A                                      INESS OFFICE'
     A                                      DSPATR(HI)
     A                                  2  1'Position Cursor, Press Enter'
     A                                  4  1'Business'
     A                                      DSPATR(HI)
     A                                  4 43'Production'
     A                                      DSPATR(HI)
     A                                  5  1'Office'
     A                                      DSPATR(HI)
     A                                  5 10'Farm'
     A                                      DSPATR(HI)
     A                                  5 16'Description'
     A                                      DSPATR(HI)
     A                                  5 43'Phase'
     A                                      DSPATR(HI)
     A                                  5 49'Type'
     A                                      DSPATR(HI)
     A                                  5 56'Cell'
     A                                      DSPATR(HI)
     A            C1FMBO    R        B  6  1REFFLD(@@BOCD)
     A                                      DSPATR(HI)
     A            C1FSCD    R        B  6  9REFFLD(@@FSCD)
     A                                      EDTCDE(M)
     A                                      DSPATR(HI)
     A            C1PPCD    R        B  6 43REFFLD(@@PPCD)
     A                                      DSPATR(HI)
     A            C1PTCD    R        B  6 49REFFLD(@@PTCD)
     A                                      DSPATR(HI)
     A            C1CELL    R        B  6 57REFFLD(HSRREF/@@CELL *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A                                      EDTCDE(M)
     A          R HP5004K1
     A*%%TS  SD  19940511  110146  ISRCENT     REL-V2R2M0  5738-PW1
     A                                      OVERLAY
     A                                      WINDOW(HP5004R1)
     A                                 16  1'F12=Previous'
