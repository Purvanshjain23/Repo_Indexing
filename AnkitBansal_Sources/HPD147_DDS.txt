     A*%%TS  SD  20081008  104540  ISLRAMS     REL-V5R4M0  5722-WDS
     A*****************************************************************
     A*
     A*  ENVIRONMENT:   PORK DIVISION
     A*  SYSTEM:        RESOURCE OPTIMIZATION
     A*  DISPLAY FILE:  HPD147
     A*  TITLE:         CHANGE STATUS OF SALES MOVEMENT TO FP
     A*  PROGRAMMER:    LEANNE RAMSEY
     A*  DATE:          10/08/08
     A*
     A*****************************************************************
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A                                      CF09(09 'ACCEPT')
     A*--------------------------------------------------------------
     A* RECORD FORMAT ONE
     A*--------------------------------------------------------------
     A          R HP147R1
     A*%%TS  SD  20081008  104540  ISLRAMS     REL-V5R4M0  5722-WDS
     A                                      OVERLAY
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 29'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  2 21'CHANGE STATUS OF SALES MOVEMENT TO-
     A                                       FP'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  4  2'Movement number ...:'
     A            D1MVSN    R        O  4 23REFFLD(@@MVSN)
     A                                      EDTCDE(Z)
     A                                  5  2'Scheduled ship date:'
     A            D1MDY     R     S  O  5 23REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  ')
     A                                  5 41'Sale type .....:'
     A            D1STCD    R        O  5 58REFFLD(STSTCD)
     A                                  6  2'Scheduled kill date:'
     A            D1SCKLMDY R     S  O  6 23REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  ')
     A                                  6 41'Load type .....:'
     A            D1LTCD    R        O  6 58REFFLD(HSRREF/@@LTCD *LIBL/HSPREF)
     A                                  7  2'Shipped date ......:'
     A            D1SMDY    R     S  O  7 23REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  ')
     A                                  7 41'Trucker .......:'
     A            D1TICD    R        O  7 58REFFLD(SHTICD)
     A                                  8  2'Received date .....:'
     A            D1RMDY    R     S  O  8 23REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  ')
     A                                  8 41'Customer number:'
     A            D1CVNO    R        O  8 58REFFLD(SHCVNO)
     A                                      EDTCDE(M)
     A                                  9  2'Farm ..............:'
     A            D1FSCD    R        O  9 23REFFLD(FSFSCD)
     A                                      EDTCDE(M)
     A            D1CNAM    R        O  9 50REFFLD(@@DS30)
     A            D1FSNM    R        O 10  7REFFLD(FSFSNM)
     A                                 15  6'Take F9=Accept to change the Movem-
     A                                      ent Status to FP=Fully Paid.'
     A                                      COLOR(BLU)
     A*
     A*--------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*--------------------------------------------------------------
     A          R HP147K1
     A*%%TS  SD  20071113  144933  ISLRAMS     REL-V5R4M0  5722-WDS
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A                                 23 11'F9=Accept'
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *---------------------------------------------------------------
     A          R HP147ES                   SFL SFLMSGRCD(24)
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE CONTROL FORMAT
      *---------------------------------------------------------------
     A          R HP147EC                   SFLCTL(HP147ES)
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
