/***************************************************************** */
/*                                                                 */
/*  PROGRAM NUMBER:  HP4077CL                                      */
/*  PROGRAM NAME:    IDLE FARMS REPORT                             */
/*  PROGRAMMER:      LEANNE FEDOR                                  */
/*  CREATE DATE:     06/11/01                                      */
/*                                                                 */
/*  FUNCTION:        THIS CL IS SUBMITTED WITH QCMDEXC FROM        */
/*                   HP4077-SPECIFY OPTIONS FOR IDLE FARMS REPORT  */
/*                                                                 */
/*                   OPTIONS ARE PASSED THROUGH THE LDA.           */
/*                                                                 */
/*******************************************************************/
/*  MODIFIED:                                                      */
/*******************************************************************/

/*******************************************************************/

             PGM

/* DEFINE THE VARIABLE REQUIRED IN THE COMMAND TO OVERRIDE THE     */
/* PRINT FILE TO A LASER PRINTER                                   */

             DCL        VAR(&CMD) TYPE(*CHAR) LEN(512)
             DCL        VAR(&QDATA) TYPE(*CHAR) LEN(168)
             DCL        VAR(&QDATA2) TYPE(*CHAR) LEN(123)

             DCL        VAR(&LDFYMD) TYPE(*DEC) LEN(8)
             DCL        VAR(&LDTYMD) TYPE(*DEC) LEN(8)

             DCL        VAR(&LDFSBO) TYPE(*CHAR) LEN(5)
             DCL        VAR(&LDPTCD) TYPE(*CHAR) LEN(5)
             DCL        VAR(&LDPPCD) TYPE(*CHAR) LEN(5)
             DCL        VAR(&LDCELL) TYPE(*DEC) LEN(2)
             DCL        VAR(&LDAIST) TYPE(*CHAR) LEN(1)

             DCL        VAR(&HOLD) TYPE(*CHAR) LEN(10)
             DCL        VAR(&COPYS) TYPE(*DEC) LEN(2)
             DCL        VAR(&OUTQ) TYPE(*CHAR) LEN(10)

             CHGVAR     VAR(&LDFYMD) VALUE(%SST(*LDA 13 8))
             CHGVAR     VAR(&LDTYMD) VALUE(%SST(*LDA 21 8))

             CHGVAR     VAR(&LDFSBO) VALUE(%SST(*LDA 29 5))
             CHGVAR     VAR(&LDPTCD) VALUE(%SST(*LDA 34 5))
             CHGVAR     VAR(&LDPPCD) VALUE(%SST(*LDA 39 5))
             CHGVAR     VAR(&LDCELL) VALUE(%SST(*LDA 44 2))
             CHGVAR     VAR(&LDAIST) VALUE(%SST(*LDA 46 1))


/*-------------------------------------------------------------------*/
/*  SET UP AND RUN OPEN QUERY OVER HOG GROUP MASTER                  */
/*-------------------------------------------------------------------*/

/*  ALWAYS OMIT HOG GROUPS WITH A STATUS OF VOIDED               */

             CHGVAR     VAR(%SST(&QDATA 1 15)) VALUE('HGGSCD +
                          *NE "VD"')


/*  ALWAYS OMIT HOG GROUPS WITH A DISPOSED DATE PRIOR TO THE FROM DATE  */

             CHGVAR     VAR(%SST(&QDATA 17 16)) VALUE('*AND (HGDSDT +
                          *EQ')
             CHGVAR     VAR(%SST(&QDATA 34 8)) VALUE(00000000)
             CHGVAR     VAR(%SST(&QDATA 43 14)) VALUE('*OR HGDSDT +
                          *GE')
             CHGVAR     VAR(%SST(&QDATA 58 8)) VALUE(&LDFYMD)
             CHGVAR     VAR(%SST(&QDATA 66 1)) VALUE(')')

/*  ALWAYS OMIT HOG GROUPS WITH A CLOSED DATE PRIOR TO THE FROM DATE  */

             CHGVAR     VAR(%SST(&QDATA 68 16)) VALUE('*AND (HGCLDT +
                          *EQ')
             CHGVAR     VAR(%SST(&QDATA 85 8)) VALUE(00000000)
             CHGVAR     VAR(%SST(&QDATA 94 14)) VALUE('*OR HGCLDT +
                          *GE')
             CHGVAR     VAR(%SST(&QDATA 109 8)) VALUE(&LDFYMD)
             CHGVAR     VAR(%SST(&QDATA 117 1)) VALUE(')')


/*  ALWAYS SELECT GROUPS WITH AN OPEN DATE ON OR BEFORE THE TO DATE */

             CHGVAR     VAR(%SST(&QDATA 119 16)) VALUE('*AND HGOPDT +
                          *LE ')
             CHGVAR     VAR(%SST(&QDATA 135 8)) VALUE(&LDTYMD)

             CHGVAR     VAR(%SST(&QDATA 144 16)) VALUE('*AND HGOPDT +
                          *NE ')
             CHGVAR     VAR(%SST(&QDATA 160 8)) VALUE(00000000)


             OVRDBF     FILE(HSL034A) SHARE(*YES)
             OPNQRYF    FILE((HSL034A)) QRYSLT(&QDATA) KEYFLD(*FILE) +
                          SEQONLY(*YES)

/*-------------------------------------------------------------------*/
/* SET UP AND RUN OPEN QUERY OVER FARM SITE                          */
/*-------------------------------------------------------------------*/

/*  SET A DEFAULT STARTING POINT                                     */

             CHGVAR     VAR(%SST(&QDATA2 1 12)) VALUE('FSFSCD *NE 0')

/* CELL                                                       */
             IF         COND(&LDCELL *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA2 14 16)) VALUE('*AND FSCELL +
                           *EQ ')
             CHGVAR     VAR(%SST(&QDATA2 30 2)) VALUE(&LDCELL)
             ENDDO


/* BUSINESS OFFICE                                                     */

             IF         COND(&LDFSBO *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA2 33  17)) VALUE('*AND FSFSBO *EQ "')
             CHGVAR     VAR(%SST(&QDATA2 50 5)) VALUE(&LDFSBO)
             CHGVAR     VAR(%SST(&QDATA2 55 1)) VALUE('"')
             ENDDO

/* PRODUCTION PHASE                                                      */

             IF         COND(&LDPPCD *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA2 57 17)) VALUE('*AND FSPPCD +
                          *EQ "')
             CHGVAR     VAR(%SST(&QDATA2 74 5)) VALUE(&LDPPCD)
             CHGVAR     VAR(%SST(&QDATA2 79 1)) VALUE('"')
             ENDDO

/* PRODUCTION TYPE                                         */

             IF         COND(&LDPTCD *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA2 81 17)) VALUE('*AND FSPTCD +
                          *EQ "')
             CHGVAR     VAR(%SST(&QDATA2 98 5)) VALUE(&LDPTCD)
             CHGVAR     VAR(%SST(&QDATA2 103 1)) VALUE('"')
             ENDDO

/* ACTIVE/INACTIVE STATUS                                  */

             IF         COND(&LDAIST *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA2 105 17)) VALUE('*AND FSAIST +
                          *EQ "')
             CHGVAR     VAR(%SST(&QDATA2 122 1)) VALUE(&LDAIST)
             CHGVAR     VAR(%SST(&QDATA2 123 1)) VALUE('"')
             ENDDO

             OVRDBF     FILE(HSP018) SHARE(*YES)
             OPNQRYF    FILE((HSP018)) QRYSLT(&QDATA2) KEYFLD(*FILE) +
                          SEQONLY(*YES)


/*-------------------------------------------------------------------*/
/* SET UP PRINT OVERRIDES                                            */
/*-------------------------------------------------------------------*/

 HOLD:       IF         COND(%SST(*LDA 411 1) = 'Y') THEN(CHGVAR +
                          VAR(&HOLD) VALUE(*YES))
             ELSE       CMD(CHGVAR VAR(&HOLD) VALUE(*NO))

 COPYS:      CHGVAR     VAR(&COPYS) VALUE(%SST(*LDA 412 1))
             IF         COND(&COPYS = 0) THEN(CHGVAR VAR(&COPYS) +
                          VALUE(1))

OUTQ:        CHGVAR     VAR(&OUTQ) VALUE(%SST(*LDA 413 10))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(%SST(*LDA 401 10)))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(*JOB))

/*------------------------------------------------------------------*/
/* OVERRIDE PRINT FILE                                              */
/*------------------------------------------------------------------*/

/* USE THE CUSTOMIZED SEABOARD COMMAND TO OVERRIDE THE PRINT FILE.  */
/* THIS COMMAND ALLOWS THE REPORT TO BE GENERATED ON A STANDARD LINE*/
/* PRINTER OR A LASER PRINTER. TO USE THIS OVERRIDE YOU MUST DECLARE*/
/* A COMMAND VARIABLE AND FOLLOW THE OVERRIDE WITH A CALL TO        */
/* QCMDEXC.                                                         */

/* THIS REPORT IS STANDARD PRINT AT 10 CPI:  SO, ON A LASER PRINTER */
/* IT CAN PRINT 'PORTRAIT' ON LETTER PAPER                          */

             SBDPRTOVR  CMDVAR(&CMD) FILE(QPRINT) OUTQ(&OUTQ) +
                          WIDTH(80) LANDSCAPE(*NO) DRAWER(*PRTF) +
                          COPIES(&COPYS) HOLD(&HOLD)

             CALL       PGM(QCMDEXC) PARM(&CMD 512)


/*-------------------------------------------------------------------*/
/* PRINT THE LISTING                                                 */
/*-------------------------------------------------------------------*/

             CALL       PGM(HP6077)

             CLOF       OPNID(HSL034A)
             CLOF       OPNID(HSP018)
             DLTOVR     FILE(*ALL)

             ENDPGM

