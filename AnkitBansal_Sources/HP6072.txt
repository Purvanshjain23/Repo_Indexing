      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Hog Production System
      * PROGRAM:     HP6072
      * TITLE:       Government Census: Year End Farm Inventory (BGF)
      * PROGRAMMER:  LeAnne Ramsey
      * CREATED:     05/27/08
      *
      * FUNCTION:  Batch program to generate one of the Government Census listings
      *            which Aubri Cannon needs once every five years.
      *
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE       PROGRAMMER
      *
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fhsp3072   if   e           k disk
      *  Workfile: Year End Farm Inventory (BGF)
      *
      *
     Fprint132  o    f  132        printer oflind(*inof)
      *
      /EJECT
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      ****************************************************************
      * ARRAYS AND TABLES
      ****************************************************************
      *
     D arengshd        s              7  0 dim(2)
     D arbohd          s              7  0 dim(2)
     D arinhd          s              7  0 dim(2)
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Standard fields
      *
     D dash            s            132    inz(*all'-')
     d rtime           s              6  0
      *
      *
      * Control fields
      *
     D first           s              1    inz('Y')
     D svsacd          s                   like(w1sacd)
     D svcnty          s                   like(w1cnty)
      *
      *
      * Array index
      *
     d x               s              1  0
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *---------------------------------------------------------------
      * Local data area.
      *---------------------------------------------------------------
      *
     Dlda             uds                  dtaara(*lda)
     D  ldyr                   1      4  0
     D  ldpe                   5      6  0
      *
     D  ldfdt                  9     16  0
     D  ldtdt                 17     24  0
      *
     D  ldgtcd                25     25
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * Mainline
      ****************************************************************
      *
      * Print headings
     C                   exsr      $h1hdr
      *
      * Print each record in the workfile.
      *
     C     *loval        setll     hsp3072
      *
     C                   dou       *in90 = *on                                  Main do
     C                   read      hsp3072                                90
     C                   if        *in90 = *off                                 If not EOF
      *
      * Control break
     C                   select
     C                   when      first = yes
     C                   move      no            first
     C                   move      w1sacd        svsacd
     C                   move      w1cnty        svcnty
      *
     C                   when      svsacd <> w1sacd or
     C                             svcnty <> w1cnty
     C                   exsr      $cntytot
     C                   move      w1sacd        svsacd
     C                   move      w1cnty        svcnty
     C                   endsl
      *
      * Accumulate for totals
     C                   add       w1engshd      arengshd
     C                   add       w1bohd        arbohd
     C                   add       w1inhd        arinhd
      *
     C                   exsr      $l1dtl
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Main do
      *
      *----------------
      * EOF processing
      *----------------
     C                   if        first = yes
     C                   except    nodata
     C                   else
     C                   exsr      $cntytot
     C                   exsr      $rpttot
     C                   endif
      *
     C                   seton                                        lr
      /eject
      *---------------------------------------------------------------
      * Print detail
      *---------------------------------------------------------------
      *
     C     $l1dtl        begsr
      *
      * Print headings
     C                   if        *inof = *on
     C                   exsr      $h1hdr
     C                   endif
      *
      * Print detail
     C                   except    l1dtl
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Print header
      *---------------------------------------------------------------
      *
     C     $h1hdr        begsr
      *
     C                   except    h1hdr
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Print County Total line
      *---------------------------------------------------------------
      *
     C     $cntytot      begsr
      *
     C                   if        *inof = *on
     C                   exsr      $h1hdr
     C                   endif
      *
     C                   z-add     1             x
     C                   except    t1tot
      *
      * Clear array entry
     C                   z-add     0             arengshd(x)
     C                   z-add     0             arbohd(x)
     C                   z-add     0             arinhd(x)
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Print Report Total line
      *---------------------------------------------------------------
      *
     C     $rpttot       begsr
      *
     C                   if        *inof = *on
     C                   exsr      $h1hdr
     C                   else
     C                   except    blank
     C                   endif
      *
     C                   z-add     2             x
     C                   seton                                        89
     C                   except    t1tot
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Retrieve time for report heading.
      *
     C                   time                    rtime
      *
     C                   endsr
      /EJECT
      *-------------------------------------------------------------
      * Report heading lines
      *-------------------------------------------------------------
      *
     Oprint132  e            h1hdr          1 01
     O                       sdpgm               10
     O                                           82 'HOG PRODUCTION SYSTEM'
     O                                          122 'DATE'
     O                       udate         y    132
      *
     O          e            h1hdr          1
     O                       sdusr               10
     O                                           83 'YEAR END FARM INVENTORY'
     O                                          122 'TIME'
     O                       rtime              132 '  :  :  '
      *
     O          e            h1hdr          2
     O                                           80 'BGF (SOW) FARMS'
     O                                          122 'PAGE'
     O                       page          z    132
      *
      *
     O          e            h1hdr          1
     O                                            5 'Year:'
     O                       ldyr          z     11
      *
      *-------------------------------------------------------------
      * Column headings
      *-------------------------------------------------------------
      *
     O          e            h1hdr       1  1
     O                                           45 'Farm'
     O                                           60 'Gilt & Sow'
     O                                           74 'Boar'
     O                                           87 'Total'
      *
     O          e            h1hdr          1
     O                                           45 'Site'
     O                                           60 'Inventory'
     O                                           74 'Inventory'
     O                                           87 'Inventory'
      *
     O          e            h1hdr          1
     O                                            6 'County'
     O                                           32 'State'
     O                                           45 'Code'
     O                                           60 'Head'
     O                                           74 'Head'
     O                                           87 'Head'
      *
     O          e            h1hdr          1
     O                       dash               132
      *
      *
      *-------------------------------------------------------------
      * Detail line
      *-------------------------------------------------------------
      *
     O          e            l1dtl       1
     O                       w1cnty              25
     O                       w1sacd              32
     O                       w1fscd        z     45
     O                       w1engshd      k     61
     O                       w1bohd        k     75
     O                       w1inhd        k     88
      *
      *-------------------------------------------------------------
      * Total line for County
      *-------------------------------------------------------------
      *
     O          e   n89      t1tot       1  1
     O                                           60 '---------'
     O                                           74 '---------'
     O                                           87 '---------'
      *
     O          e            t1tot       0  1
     O               89                          50 'Report Total:'
     O                       arengshd(x)   k     61
     O                       arbohd  (x)   k     75
     O                       arinhd  (x)   k     88
      *
      *-------------------------------------------------------------
      * Blank
      *-------------------------------------------------------------
      *
     O          e            blank       1
      *
      *-------------------------------------------------------------
      * No data message line
      *-------------------------------------------------------------
      *
     O          e            nodata      1
     O                                           19 'NO DATA MEETS YOUR'
     O                                           38 'SELECTION CRITERIA'
