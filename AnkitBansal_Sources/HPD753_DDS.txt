     A*%%TS  SD  19950419  101624  ISLRAMS     REL-V3R0M5  5763-PW1
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD753
     A*  TITLE:         VIEW INSTALLED EQUIPEMNT
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  CREATED:       03/21/94
     A*
     A*  INDICATOR 90 IS ON WHEN THE BUILDING/ROOM THE USER HAS
     A*               SELECTED FROM THE WORK WITH HAS BEEN DELETED
     A*               BY ANOTHER USER
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A N90                                  CF05(05 'REFRESH')
     A                                      CA12(12 'PREVIOUS')
     A N82N90                               ROLLUP(25 'PAGE FORWARD')
     A*
     A*--------------------------------------------------------------*
     A* SUBFILE FORMAT ONE
     A*--------------------------------------------------------------*
     A          R HP753S1                   SFL
     A*%%TS  SD  19940331  145622  ISLRAMS     REL-V2R2M0  5738-PW1
     A  85                                  SFLNXTCHG
     A            HDRRN1         4Y 0H
     A            DFEQCD    R        O 10  7REFFLD(BRRMCD)
     A            DFEQDS    R        O 10 15REFFLD(EQEQDS)
     A            DFETCD    R        O 10 40REFFLD(EQETCD)
     A*
     A*--------------------------------------------------------------*
     A* SUBFILE CONTROL FORMAT ONE
     A*--------------------------------------------------------------*
     A          R HP753C1                   SFLCTL(HP753S1)
     A*%%TS  SD  19950419  101624  ISLRAMS     REL-V3R0M5  5763-PW1
     A                                      SFLSIZ(0100)
     A                                      SFLPAG(0012)
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A                                      OVERLAY
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            C1PPCD    R        H      REFFLD(FSPPCD)
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 25'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  2 24'VIEW INSTALLED EQUIPMENT'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  4  2'Farm site ..:'
     A            C1FSCD    R        O  4 17REFFLD(FSFSCD)
     A                                      EDTCDE(M)
     A            C1FSNM    R        O  4 24REFFLD(FSFSNM)
     A                                  5  2'Building ...:'
     A            C1BLCD    R        O  5 17REFFLD(FBBLCD)
     A            C1BLDS    R        O  5 24REFFLD(FBBLDS)
     A                                  6  2'Room .......:'
     A            C1RMCD    R        O  6 17REFFLD(IERMCD)
     A                                  8 40'Equip'
     A                                      DSPATR(HI)
     A                                  9  7'Equip'
     A                                      DSPATR(HI)
     A                                  9 15'Equipment Description'
     A                                      DSPATR(HI)
     A                                  9 40'Type'
     A                                      DSPATR(HI)
     A*
     A*--------------------------------------------------------------*
     A* COMMAND KEY RECORD 1
     A*--------------------------------------------------------------*
     A          R HP753K1
     A*%%TS  SD  19940322  081158  ISLRAMS     REL-V2R2M0  5738-PW1
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A                                      DSPATR(HI)
     A                                 23  2'F3=Exit'
     A N90                             23 11'F5=Refresh'
     A                                 23 23'F12=Previous'
     A*
     A*--------------------------------------------------------------*
     A* ERROR MESSAGE SUBFILE FORMAT
     A*--------------------------------------------------------------*
     A          R HP753ES                   SFL SFLMSGRCD(24)
     A*
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
     A*                                                                *********
     A*--------------------------------------------------------------*
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*--------------------------------------------------------------*
     A          R HP753EC                   SFLCTL(HP753ES)
     A*
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
