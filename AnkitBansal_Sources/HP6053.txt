      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Hog Production System
      * PROGRAM:     HP6053
      * TITLE:       Wean Pig Projection Listing
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     06/17/02
      *
      *            This listing is submitted from HP4053-Specify Options for
      *            Wean Pig Projections.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 08/05/02  LeAnne Fedor
      *           We added an entire second part using QPRINT2 that prints the finisher
      *           predictions. BUT, the columns for ACTUAL head are still zero on this
      *           report....until Alice decides how/where she wants the ACTUAL data
      *           retrieved from. (Scan for RETRIEVAL?? when you are ready to add the
      *           retrival code...all total logic etc is already in place.)
      *
      *           The user elects whether he wants to print boths parts or just a
      *           single part.
      *
      * 11/21/02  LeAnne Fedor
      *           Added 'nursery based on head' column in Part 2.
      *           If a single farm is selected, we no longer print a 'total' line for the farm.
      *
      * 06/30/04  LeAnne Fedor
      *           Recompile only.
      *
      * 09/13/04  LeAnne Fedor
      *           Recompile only. We added 6 fields to the Datamart Wean Pig Predictions
      *           file. These were Week 1/Week 2 projection fields from our new BGF Weekly
      *           Wean Projections file.  No logic was added to this print program to
      *           bring in these new Datamart fields.
      *
      * 02/01/05  LeAnne Fedor
      *           Recompile only. New fields (Sire Line Company and Prime Line) added
      *           to Datamart BGF: Weekly Production file.
      *
      * 02/07/05  LeAnne Fedor
      *           Recompile only due to file changes in Datamart BGF Weekly Production file:
      *                    Added: Transferred In Open Gilts
      *                  Removed: Total Lactation Pounds
      *
      * 07/05/05  LeAnne Fedor
      *           Recompile only due to file changes in Datamart BGF Weekly Production file:
      *           Added 1 new field: Average Daily Beginning Piglet Inventory
      *
      * 08/23/05  LeAnne Fedor
      *           Recompile only. New field added to Datamart BGF Weekly Production file:
      *                Field: Man hours
      *
      * 03/23/06  LeAnne Fedor
      *           Recompile only. New fields added to Datamart BGF Weekly Production file.
      *
      * 04/24/06  LeAnne Fedor
      *           Recompile only. New fields added to Datamart BGF Weekly Production file.
      *                 Cognos Report Grouping
      *                 Weaning Cognos Report Grouping
      *                 Report PCP Cognos Report Grouping
      *                 Report Farrowed Cognos Report Grouping
      *
      * 06/15/06  LeAnne Ramsey
      *           Recompile only. New field added to Datamart BGF Weekly Production file:
      *                Field: Total Gilt Weight @ 28 Weeks
      *
      * 08/04/08  LeAnne Ramsey
      *           Recompile only. Fields deleted from Weekly Production file.
      *             BPPSGLSV - Nbr Gilts Bred w/Passed Date
      *             BPWSLB   - Weekly Ending Lbs Supplement Used
      *             BPP2LT   - Total P2 Litters Farrowed
      *             BPTDGB   - Total Days Gilt Breeding Age
      *
      * 11/07/08  LeAnne Ramsey
      *           Recompile only:
      *           Added 3 new fields to HPPB092-Datamart BGF: Weekly Production
      *            1) Target open gilt pool eligible
      *            2) Target litters farrowed/mated female/year
      *            3) Target pigs weaned/mated female/year
      *
      * 05/28/09  LeAnne Ramsey
      *           Recompile only:
      *           Added new field "Farrowing Crates" to:
      *                HPPB092-Datamart BGF: Weekly Production
      *
      * 09/01/09  LeAnne Ramsey
      *           Recompile only. Added new field "Report Total Litters Farrowed" to:
      *                 HPPB092-Datamart BGF: Weekly Production
      *
      * 01/14/10  LeAnne Ramsey
      *           Recompile only. Changed text on BPLVHD and BPLVLB in:
      *                HPPB092-Datamart BGF: Weekly Production
      *
      * 10/18/10  LeAnne Ramsey  (E1079)
      *           Recompile only. Sire Line Company is now 2-characters instead
      *           of one.
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fhplb192b  if   e           k disk
      *    Datamart BGF: Wean Pig Predictions
      *    (Records selected in open query in CL)
      *
      *
     Fhppb092   if   e           k disk
      *    Datamart BGF: Weekly Production
      *
     Fqprint    o    f  198        printer oflind(*inof)
      *
     Fqprint2   o    f  198        printer oflind(*inoa)
      *
      /EJECT
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Standard fields
      *
     D prtfl           s              1    inz('N')
     D dash            s            198    inz(*all'-')
     d rtime           s              6  0
      *
      *
      * Control fields
      *
     d count           s              3  0
     D first           s              1    inz('Y')
     D procfl          s              1    inz('Y')
      *
     d svcell          s                   like(wpcell)
     d svsvyr          s                   like(wpsvyr)
     d svsvwk          s                   like(wpsvwk)
     d svwnyr          s                   like(wpwnyr)
     d svwnwk          s                   like(wpwnwk)
      *
      *
      * Array index
      *
     D x               s              3  0
      *
      *
      * Print fields
      *
     D h1wynds         s              3
     D h1fynds         s              3
      *
     d l1townhd        s                   like(bptownhd)
     d l1dwnhd         s                   like(wpwnhd)
      *
     d t1crpr          s                   like(wpcrpr)
     d t1frpr          s                   like(wpfrpr)
     d t1fopr          s                   like(wpfopr)
     d t1ltbahd        s                   like(wpltbahd)
     d t1pmpr          s                   like(wppmpr)
      *
     d l2nbohd         s                   like(wpwnhd)
     d l2aonhd         s                   like(wponhd)
     d l2donhd         s                   like(wponhd)
      *
     d l2aofhd         s                   like(wpofhd)
     d l2dofhd         s                   like(wpofhd)
      *
     d t2nrmpr         s                   like(wpnrmpr)
     d t2frmpr         s                   like(wpfrmpr)
      *
      ****************************************************************
      * ARRAYS AND TABLES
      ****************************************************************
      *
     D arsvhd          s              7  0 dim(300)
      *
     D arcrhd          s              7  0 dim(300)
     D arcrpr          s              9  2 dim(300)
      *
     D arfrhd          s              7  0 dim(300)
     D arfrpr          s              9  2 dim(300)
     D arfopr          s              9  2 dim(300)
      *
     D artolt          s              7  0 dim(300)
     D arltbahd        s              9  2 dim(300)
     D arpmpr          s              9  2 dim(300)
      *
     D arwnhd          s              7  0 dim(300)
     D artownhd        s              7  0 dim(300)
     D ardwnhd         s              7  0 dim(300)
      *
      *
     D arnbohd         s              7  0 dim(300)
     D arnrmhd         s              7  0 dim(300)
     D arnrmpr         s              9  2 dim(300)
      *
     D aronhd          s              7  0 dim(300)
     D araonhd         s              7  0 dim(300)
     D ardonhd         s              7  0 dim(300)
      *
      *
     D arfrmhd         s              7  0 dim(300)
     D arfrmpr         s              9  2 dim(300)
      *
     D arofhd          s              7  0 dim(300)
     D araofhd         s              7  0 dim(300)
     D ardofhd         s              7  0 dim(300)
      *
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *---------------------------------------------------------------
      * Local data area.
      *---------------------------------------------------------------
      *
     Dlda             uds                  dtaara(*lda)
     D  ldsvyr                 1      4  0
     D  ldsvwk                 5      6  0
     D  ldwnyr                 7     10  0
     D  ldwnwk                11     12  0
      *
     D  ldbwfl                13     13
     D  ldweeks               14     15  0
      *
     D  ldfryr                16     19  0
     D  ldfrwk                20     21  0
     D  ldtoyr                22     25  0
     D  ldtowk                26     27  0
      *
     D  lddsfl                28     28
     D  lddsds                29     38
     D  ldcell                39     40  0
     D  ldfscd                41     45  0
     D  ldfsnm                46     70
      *
     D  ldwynfl               71     71
     D  ldfynfl               72     72
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * MAINLINE
      ****************************************************************
      *
      * Print headings
      *
     C     *loval        setll     hplb192b
     C                   exsr      $hdr
      *
      * Process all records selected by the open query.
      *
     C                   dou       *in90 = *on                                  Main do
     C                   read      hplb192b                               90
     C                   if        *in90 = *off                                 If not EOF
      *
     C                   move      no            procfl
      *
      * Does the record fall on/before the user's selected 'to' year/week
      * (NOTE: The 'from' logic was handled in the CL.)
      *
     C                   select
     C                   when      ldbwfl = 'B'
     C                   if        wpsvyr < ldtoyr or
     C                             (wpsvyr = ldtoyr and wpsvwk <= ldtowk)
     C                   move      yes           procfl
     C                   endif
      *
     C                   when      ldbwfl = 'W'
     C                   if        wpwnyr < ldtoyr or
     C                             (wpwnyr = ldtoyr and wpwnwk <= ldtowk)
     C                   move      yes           procfl
     C                   endif
     C                   endsl
      *
      * Process this record
      *
     C                   if        procfl = yes                                 If process
      *
     C                   select
      * First time
     C                   when      first = yes
     C                   z-add     wpcell        svcell
     C                   z-add     wpsvyr        svsvyr
     C                   z-add     wpsvwk        svsvwk
     C                   z-add     wpwnyr        svwnyr
     C                   z-add     wpwnwk        svwnwk
     C                   move      no            first
      *
      * Summary version control break
      *
     C                   when      lddsfl = 'S' and
     C                             svcell <> wpcell
     C                   exsr      $break
      *
      * Detail version control break
      *
     C                   when      lddsfl = 'D' and
     C                             (svcell <> wpcell or
     C                             svsvyr <> wpsvyr or
     C                             svsvwk <> wpsvwk)
     C                   exsr      $break
     C                   endsl
      *
      * Print detail line
      *
     C                   exsr      $detail
      *
     C                   endif                                                  If process
     C                   endif                                                  If not EOF
     C                   enddo                                                  Main do
      *
      *---------------------------------------------------------------
      * EOF processing
      *---------------------------------------------------------------
      * If the file was empty, print the standard 'no data...'
      * error message
      *
     C                   select
     C                   when      prtfl = no
     C                   except    nodata
     C                   other
      *
     C                   exsr      $break
     C                   endsl
      *
     C                   seton                                        lr
      /eject
      *---------------------------------------------------------------
      * Detail processing
      *---------------------------------------------------------------
      *
     C     $detail       begsr
      *
      * Retrieve the actual weaned head from the Datamart BGF: Weekly Production file.
      * Then, calculate the difference between predicted and actual wean head.
      *
     C     key01         chain     hppb092                            92
     C                   if        *in92 = *off
     C                   z-add     bptownhd      l1townhd
     C                   endif
      *
      *
      * Calculate the difference between predicted and actual wean head.
      *
     C     wpwnhd        sub       l1townhd      l1dwnhd
      *
      *
      * Move the predicted wean head into the 'nursery based on head' field.
      *
     C                   z-add     wpwnhd        l2nbohd
      *
      * Retrieval??
      * Retrieve the actual out-of-nursery and out-of-grow finish head.
      *   Add retrieval logic once AMB decides what file(s) to use...datamart or HPS.
      *   Retrieve 'actual' head and place in l2aonhd (for nursery) and l2aofhd for grow finish
      *
      *
      * Calculate the difference between predicted and actual out-of-nursery head.
      *
     C     wponhd        sub       l2aonhd       l2donhd
     C     wpofhd        sub       l2aofhd       l2dofhd
      *
      *
     C     wponhd        sub       l2aonhd       l2donhd
      *
      * Increment the count of records
      *
     C                   add       1             count
      *
      * Accumulate values for the total lines into arrays
      *
     C                   add       wpsvhd        arsvhd
      *
     C                   add       wpcrhd        arcrhd
     C                   add       wpcrpr        arcrpr
     C                   add       wpfrhd        arfrhd
     C                   add       wpfrpr        arfrpr
     C                   add       wpfopr        arfopr
     C                   add       wptolt        artolt
     C                   add       wpltbahd      arltbahd
     C                   add       wppmpr        arpmpr
     C                   add       wpwnhd        arwnhd
     C                   add       l1townhd      artownhd
     C                   add       l1dwnhd       ardwnhd
      *
     C                   add       wpnrmhd       arnrmhd
     C                   add       wpnrmpr       arnrmpr
      *
     C                   add       wponhd        aronhd
     C                   add       l2aonhd       araonhd
     C                   add       l2donhd       ardonhd
     C                   add       l2nbohd       arnbohd
      *
     C                   add       wpfrmhd       arfrmhd
     C                   add       wpfrmpr       arfrmpr
      *
     C                   add       wpofhd        arofhd
     C                   add       l2aofhd       araofhd
     C                   add       l2dofhd       ardofhd
      *
      * Set the flag that shows that you had data
      *
     C                   movel     yes           prtfl
      *
      * Print detail line if the user has elected the 'Detail' version
      *
     C                   if        lddsfl = 'D'                                 If detail
      *
     C                   if        *inof = *on
     C                   exsr      $hdr
     C                   endif
      *
      * Wean predictions
     C                   if        ldwynfl = yes
     C                   except    l1dtl
     C                   endif
      *
      * Finisher predictions
      *
     C                   if        ldfynfl = yes
     C                   except    l2dtl
     C                   endif
      *
     C                   setoff                                       85
     C                   endif                                                  If detail
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Control break processing
      *---------------------------------------------------------------
      *
     C     $break        begsr
      *
      * I don't know if we will add additional total lines later or not...that's
      * why I used arrays for the accumulations. Anyway, for now, we only have a
      * single total line; so, set the array index to 1.
      *
     C                   z-add     1             x
      *
      * For the 'averages' values, divide the array values by the 'count' of records.
      *
     C                   if        count <> 0
     C     arcrpr(x)     div(h)    count         t1crpr
     C     arfrpr(x)     div(h)    count         t1frpr
     C     arfopr(x)     div(h)    count         t1fopr
     C     arltbahd(x)   div(h)    count         t1ltbahd
     C     arpmpr(x)     div(h)    count         t1pmpr
      *
     C     arnrmpr(x)    div(h)    count         t2nrmpr
     C     arfrmpr(x)    div(h)    count         t2frmpr
     C                   endif
      *
     C                   if        *inof = *on
     C                   exsr      $hdr
     C                   endif
      *
      * If you are printing the Summary version, blank out the year/week fields.
      *
     C                   if        lddsfl = 'S'
     C                   z-add     0             svsvyr
     C                   z-add     0             svsvwk
     C                   endif
      *
      * Wean predictions
     C                   if        ldwynfl = yes and
     C                             (lddsfl = 'S' or ldfscd = 0)
     C                   except    t1tot
     C                   endif
      *
      * Finisher predictions
     C                   if        ldfynfl = yes and
     C                             (lddsfl = 'S' or ldfscd = 0)
     C                   except    t2tot
     C                   endif
      *
      * Clear the array entries and counter
      *
     C                   exsr      $clear
      *
      * Save control values
      *
     C                   z-add     wpcell        svcell
     C                   z-add     wpsvyr        svsvyr
     C                   z-add     wpsvwk        svsvwk
     C                   z-add     wpwnyr        svwnyr
     C                   z-add     wpwnwk        svwnwk
      *
      * Set on the indicator that controls printing of 'cell' on the detail line.
      * (We always want it to print after a break.)
      *
     C                   seton                                        85
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Clear array entries and counter
      *---------------------------------------------------------------
      *
     C     $clear        begsr
      *
     C                   z-add     0             count
      *
     C                   z-add     0             arsvhd(x)
     C                   z-add     0             arcrhd(x)
     C                   z-add     0             arcrpr(x)
     C                   z-add     0             arfrhd(x)
     C                   z-add     0             arfrpr(x)
     C                   z-add     0             arfopr(x)
     C                   z-add     0             artolt(x)
     C                   z-add     0             arltbahd(x)
     C                   z-add     0             arpmpr(x)
     C                   z-add     0             arwnhd(x)
     C                   z-add     0             artownhd(x)
     C                   z-add     0             ardwnhd(x)
      *
     C                   z-add     0             arnbohd(x)
     C                   z-add     0             arnrmhd(x)
     C                   z-add     0             arnrmpr(x)
      *
     C                   z-add     0             aronhd(x)
     C                   z-add     0             araonhd(x)
     C                   z-add     0             ardonhd(x)
      *
     C                   z-add     0             arfrmhd(x)
     C                   z-add     0             arfrmpr(x)
      *
     C                   z-add     0             arofhd(x)
     C                   z-add     0             araofhd(x)
     C                   z-add     0             ardofhd(x)
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Print headings
      *---------------------------------------------------------------
      *
     C     $hdr          begsr
      *
      * Print headings for 'Wean Projections' part
      *
     C                   if        ldwynfl = yes
     C                   except    h1hdr
     C                   endif
      *
      * Print headings for 'Finisher Projections' part
      *
     C                   if        ldfynfl = yes
     C                   except    h2hdr
     C                   endif
      *
      * Set on the indicator that prints 'cell'. We always want to
      * print it after a page break.
      *
     C                   seton                                        85
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      *
     C     key01         klist
     C                   kfld                    wpwnyr
     C                   kfld                    wpwnwk
     C                   kfld                    wpfscd
      *
      * Retrieve time for report heading.
      *
     C                   time                    rtime
      *
      *
      * Set up user selections.
      *
      * Detail or summary
      *
     C                   if        lddsfl = 'D'
     C                   seton                                        84
     C                   endif
      *
      * Breed or wean year/week
      *
     C                   if        ldbwfl = 'B'
     C                   seton                                        86
     C                   else
     C                   seton                                        87
     C                   endif
      * Cell
     C                   if        ldcell = 0
     C                   seton                                        89
     C                   endif
      *
      * Farm
     C                   if        ldfscd = 0
     C                   seton                                        88
     C                   endif
      *
      * Print wean pig predictions
      *
     C                   select
     C                   when      ldwynfl = yes
     C                   eval      h1wynds = 'Yes'
     C                   when      ldwynfl = no
     C                   eval      h1wynds = 'No '
     C                   endsl
      *
      * Print finisher predictions
      *
     C                   select
     C                   when      ldfynfl = yes
     C                   eval      h1fynds = 'Yes'
     C                   when      ldfynfl = no
     C                   eval      h1fynds = 'No '
     C                   endsl
      *
     C                   endsr
      /EJECT
      *-------------------------------------------------------------
      * Report heading lines for BGF
      *-------------------------------------------------------------
      *
     Oqprint    e            h1hdr          1 01
     O                       sdpgm               10
     O                                          116 'HOG PRODUCTION SYSTEM'
     O                                          188 'DATE'
     O                       udate         y    198
      *
     O          e            h1hdr          1
     O                       sdusr               10
     O                                          110 'LISTING OF WEAN PIG '
     O                                          121 'PROJECTIONS'
     O                                          188 'TIME'
     O                       rtime              198 '  :  :  '
      *
     O          e            h1hdr          2
     O                                          109 'PART 1: WEAN PIG '
     O                                          120 'PROJECTIONS'
     O                                          188 'PAGE'
     O                       page1         z    198
      *
      *
     O          e    86      h1hdr          1
     O                                           23 'From breed year/week .:'
     O                       ldsvyr        z     36
     O                       ldsvwk        z     40
      *
      *
     O          e    87      h1hdr          1
     O                                           26 'From wean year/week ......'
     O                                           30 '...:'
     O                       ldwnyr        z     36
     O                       ldwnwk        z     40
      *
      *
     O          e            h1hdr          1
     O                                           26 'Weeks to print ...........'
     O                                           30 '...:'
     O                       ldweeks       z     34
      *
      *
     O          e            h1hdr          1
     O                                           26 'Detail or summary ........'
     O                                           30 '...:'
     O                       lddsds              42
      *
      *
     O          e            h1hdr          1
     O                                           26 'Cell .....................'
     O                                           30 '...:'
     O              n89      ldcell        z     34
     O               89                          35 'All'
      *
      *
     O          e            h1hdr          1
     O                                           26 'Farm site ................'
     O                                           30 '...:'
     O              n88      ldfscd        z     37
     O              n88      ldfsnm              66
     O               88                          35 'All'
      *
      *
     O          e            h1hdr          1
     O                                           26 'Print wean pig projections'
     O                                           30 ' ..:'
     O                       h1wynds             35
      *
      *
     O          e            h1hdr          2
     O                                           26 'Print finisher projections'
     O                                           30 ' ..:'
     O                       h1fynds             35
      *
      *-------------------------------------------------------------
      * Column headings for BGF
      *-------------------------------------------------------------
      *
     O          e            h1hdr          1
     O                                           94 'TOTAL'
      *
     O          e            h1hdr          1
     O                                           15 '--BREED--'
     O                                           22 'FARM'
     O                                           38 '--SERVICES--'
      *
     O                                           60 '-----CONCEPTION-----'
     O                                           83 '-- GROUP FARROWED --'
      *
     O                                           94 'LITTERS'
     O                                          108 '--FALL OUT--'
     O                                          126 '---BORN ALIVE---'
      *
     O                                          149 '--PIGLET MORTALITY--'
      *
     O                                          161 'PREDICTED'
     O                                          173 'ACTUAL'
     O                                          186 'DIFFERENCE'
     O                                          198 '---WEAN---'
      *
     O          e            h1hdr          1
     O                                            4 'CELL'
     O                                           15 'YEAR/WEEK'
     O                                           22 'SITE'
     O                                           31 'TOTAL'
     O                                           38 'CALC'
      *
     O                                           46 'HEAD'
     O                                           54 'RATE %'
     O                                           60 'CALC'
      *
     O                                           69 'HEAD'
     O                                           77 'RATE %'
     O                                           83 'CALC'
      *
     O                                           94 'FARROWED'
      *
     O                                          102 'RATE %'
     O                                          108 'CALC'
      *
     O                                          120 'PER LITTER'
     O                                          126 'CALC'
      *
     O                                          139 'RATE %'
     O                                          146 'CALC'
      *
     O                                          161 'WEAN HEAD'
     O                                          173 'WEAN HEAD'
     O                                          186 'WEAN HEAD'
     O                                          198 'YEAR/WEEK'
      *
     O          e            h1hdr          0
     O                       dash               198
      *
      *-------------------------------------------------------------
      * Detail line for BGF
      *-------------------------------------------------------------
      *
     O          e            l1dtl       1  0
      *
     O               85      wpcell        z      3
     O               85      wpsvyr        z     10
     O               85      wpsvwk        z     14
     O                       wpfscd        z     22
      *
     O                       wpsvhd        k     32
     O                       wpsvcd              38
      *
     O                       wpcrhd        k     47
     O                       wpcrpr        k     55
     O                       wpcrcd              60
      *
     O                       wpfrhd        k     70
     O                       wpfrpr        k     78
     O                       wpfrcd              83
      *
     O                       wptolt        k     94
      *
     O                       wpfopr        k    103
     O                       wpfocd             108
      *
     O                       wpltbahd      k    119
     O                       wpbacd             126
      *
     O                       wppmpr        k    140
     O                       wppmcd             146
      *
     O                       wpwnhd        k    162
     O                       l1townhd      kb   173
     O                       l1dwnhd       kb   186
      *
     O                       wpwnyr             193
     O                       wpwnwk             197
      *
      *-------------------------------------------------------------
      * Total lines for BGF
      *-------------------------------------------------------------
      *
      *
     O          e            t1tot       2  1
     O              n84      svcell        z      3
     O              n84      svsvyr        z     10
     O              n84      svsvwk        z     14
     O               84                          16 'Tot/Avg:'
     O                       arsvhd(x)     k     32
     O                       arcrhd(x)     k     47
     O                       arfrhd(x)     k     70
     O                       artolt(x)     k     94
     O                       arwnhd(x)     k    162
     O                       artownhd(x)   k    173
     O                       ardwnhd(x)    k    186
      *
     O                       t1crpr        kb    55
     O                       t1frpr        kb    78
     O                       t1fopr        kb   103
     O                       t1ltbahd      kb   119
     O                       t1pmpr        kb   140
     O              n84      svwnyr             135
     O              n84      svwnwk             139
      *
      *-------------------------------------------------------------
      * No data message line for BGF
      *-------------------------------------------------------------
      *
     O          e            nodata      1
     O                                           19 'NO DATA MEETS YOUR'
     O                                           38 'SELECTION CRITERIA'
      /eject
      *-------------------------------------------------------------
      * Report heading lines for Nursery/Grow Finish report
      *-------------------------------------------------------------
      *
     Oqprint2   e            h2hdr          1 01
     O                       sdpgm               10
     O                                          116 'HOG PRODUCTION SYSTEM'
     O                                          188 'DATE'
     O                       udate         y    198
      *
      *
     O          e            h2hdr          1
     O                       sdusr               10
     O                                          110 'LISTING OF WEAN PIG '
     O                                          121 'PROJECTIONS'
     O                                          188 'TIME'
     O                       rtime              198 '  :  :  '
      *
     O          e            h2hdr          2
     O                                          109 'PART 2: FINISHER '
     O                                          120 'PROJECTIONS'
     O                                          188 'PAGE'
     O                       page2         z    198
      *
      *
     O          e    86      h2hdr          1
     O                                           26 'From breed year/week .....'
     O                                           30 '...:'
     O                       ldsvyr        z     36
     O                       ldsvwk        z     40
      *
      *
     O          e    87      h2hdr          1
     O                                           26 'From wean year/week ......'
     O                                           30 '...:'
     O                       ldwnyr        z     36
     O                       ldwnwk        z     40
      *
      *
     O          e            h2hdr          1
     O                                           26 'Weeks to print ...........'
     O                                           30 '...:'
     O                       ldweeks       z     34
      *
      *
     O          e            h2hdr          1
     O                                           26 'Detail or summary ........'
     O                                           30 '...:'
     O                       lddsds              42
      *
      *
     O          e            h2hdr          1
     O                                           26 'Cell .....................'
     O                                           30 '...:'
     O              n89      ldcell        z     34
     O               89                          35 'All'
      *
      *
     O          e            h2hdr          1
     O                                           26 'Farm site ................'
     O                                           30 '...:'
     O              n88      ldfscd        z     37
     O              n88      ldfsnm              66
     O               88                          35 'All'
      *
      *
     O          e            h2hdr          1
     O                                           26 'Print wean pig projections'
     O                                           30 ' ..:'
     O                       h1wynds             35
      *
      *
     O          e            h2hdr          2
     O                                           26 'Print finisher projections'
     O                                           30 ' ..:'
     O                       h1fynds             35
      *
      *
      *-------------------------------------------------------------
      * Column headings for Nursery/Grow Finish
      *-------------------------------------------------------------
     O
     O          e            h2hdr          1
     O                                           76 '--------------'
     O                                           91 'OUT-OF-NURSERY'
     O                                          105 '-------------'
      *
     O                                          149 '------------'
     O                                          167 ' OUT-OF-GROW-FINISH'
     O                                          180 '------------'
      *
      *
      *
     O          e            h2hdr          1
     O                                           15 '--BREED--'
     O                                           22 'FARM'
      *  Based on
     O                                           33 'BASED ON'
      *  Nursery removal
     O                                           58 '---NURSERY REMOVAL--'
      *  Out-of-Nursery
     O                                           71 'PREDICTED'
     O                                           82 'ACTUAL'
     O                                           94 'DIFFERENCE'
      *  Grow finish removal
     O                                          131 '--GROW FINISH REMOVAL--'
      *  Out-of-Grow finish
     O                                          146 'PREDICTED'
     O                                          156 'ACTUAL'
     O                                          169 'DIFFERENCE'
      *  Wean
     O                                          198 '---WEAN---'
      *
      *
     O          e            h2hdr          1
     O                                            4 'CELL'
     O                                           15 'YEAR/WEEK'
     O                                           22 'SITE'
      *  Based on
     O                                           33 'HEAD'
      *  Nursery Removal
     O                                           44 'HEAD'
     O                                           52 'RATE %'
     O                                           58 'CALC'
      *  Out-of-Nursery
     O                                           71 'HEAD'
     O                                           82 'HEAD'
     O                                           94 'HEAD'
     O                                          105 'YEAR/WEEK'
      *  Grow finish removal
     O                                          115 'HEAD'
     O                                          123 'RATE %'
     O                                          129 'CALC'
      *
      *  Out-of-Grow Finish
     O                                          145 'HEAD'
     O                                          156 'HEAD'
     O                                          169 'HEAD'
     O                                          180 'YEAR/WEEK'
      *  Wean
     O                                          198 'YEAR/WEEK'
      *
      *
     O          e            h2hdr          0
     O                       dash               198
      *
      *-------------------------------------------------------------
      * Detail line for nursery/grow finish
      *-------------------------------------------------------------
      *
     O          e            l2dtl       1  0
      *
     O               85      wpcell        z      3
     O               85      wpsvyr        z     10
     O               85      wpsvwk        z     14
     O                       wpfscd        z     22
      * Based on head
     O                       l2nbohd       kb    34
      * Nursery removal
     O                       wpnrmhd       k     45
     O                       wpnrmpr       k     53
     O                       wpnrmcd             58
      * Out-of-nursery
     O                       wponhd        k     72
     O                       l2aonhd       kb    83
     O                       l2donhd       kb    95
     O                       wponyr        z    100
     O                       wponwk        z    104
      * Grow finish removal
     O                       wpfrmhd       k    116
     O                       wpfrmpr       k    124
     O                       wpfrmcd            129
      * Out-of-grow finish
     O                       wpofhd        k    146
     O                       l2aofhd       kb   158
     O                       l2dofhd       kb   170
     O                       wpofyr        z    175
     O                       wpofwk        z    179
      * Wean
     O                       wpwnyr             193
     O                       wpwnwk             197
      *
      *-------------------------------------------------------------
      * Total lines for Nursery/Grow Finish
      *-------------------------------------------------------------
      *
      *
     O          e            t2tot       2  1
     O              n84      svcell        z      3
     O              n84      svsvyr        z     10
     O              n84      svsvwk        z     14
     O               84                          16 'Tot/Avg:'
      *
      * Based on head
     O                       arnbohd(x)    k     34
      * Nursery removal
     O                       arnrmhd(x)    k     45
     O                       t2nrmpr       k     53
      * Out-of-nursery
     O                       aronhd(x)     k     72
     O                       araonhd(x)    k     83
     O                       ardonhd(x)    k     95
      * Grow finish removal
     O                       arfrmhd(x)    k    116
     O                       t2frmpr       k    124
      * Out-of-grow finish
     O                       arofhd(x)     k    146
     O                       araofhd(x)    k    158
     O                       ardofhd(x)    k    170
      *
      *
      *-------------------------------------------------------------
      * No data message line for Nursery/Grow Finish
      *-------------------------------------------------------------
      *
     O          e            nodata      1
     O                                           19 'NO DATA MEETS YOUR'
     O                                           38 'SELECTION CRITERIA'
      /eject
