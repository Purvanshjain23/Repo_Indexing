     A*%%TS  SD  20040414  075355  ISLRAMS     REL-V5R2M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD7702
     A*  TITLE:         VIEW BUDGET ITEMS FOR A TEMPLATE
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  DATE:          02/04/04
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A                                      CF04(04 'PROMPT')
     A                                      CF05(05 'REFRESH')
     A*
     A*
     A*---------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP7702S1                  SFL
     A*%%TS  SD  20040414  075355  ISLRAMS     REL-V5R2M0  5722-WDS
     A  85                                  SFLNXTCHG
     A            HDRRN1         4Y 0H
     A            D1BGIT    R        O 10  2REFFLD(HSRREF/@@BGIT *LIBL/HSPREF)
     A                                      COLOR(PNK)
     A            D1DESC        43A  O 10 29COLOR(PNK)
     A            D1ITTYCD  R        O 10 74REFFLD(HSRREF/@@ITTYCD *LIBL/HSPREF)
     A                                      COLOR(PNK)
     A                                 11  8'qty:'
     A                                      COLOR(WHT)
     A            D1BTQT    R        O 11 13REFFLD(HSRREF/@@BTQT *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A                                 11 49'uom:'
     A                                      DSPATR(HI)
     A            D1UOM     R        O 11 54REFFLD(HSRREF/@@UOM *LIBL/HSPREF)
     A                                 11 64'uow:'
     A                                      DSPATR(HI)
     A            D1UOW     R        O 11 69REFFLD(HSRREF/@@UOW *LIBL/HSPREF)
     A*
     A*---------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP7702C1                  SFLCTL(HP7702S1)
     A*%%TS  SD  20040219  093315  ISLRAMS     REL-V5R2M0  5722-WDS
     A                                      SFLSIZ(0050)
     A                                      SFLPAG(0006)
     A N82                                  ROLLUP(25 'PAGE FORWARD')
     A                                      SETOF(50)
     A                                      SETOF(54)
     A                                      RTNCSRLOC(&RCD &FLD)
     A                                      OVERLAY
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 26'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  2 20'VIEW BUDGET ITEMS FOR A TEMPLATE'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  4  2'Template code:'
     A            C1BTCD    R        O  4 17REFFLD(HSRREF/@@BTCD *LIBL/HSPREF)
     A            C1BTDS    R        O  4 25REFFLD(HSRREF/@@BTDS *LIBL/HSPREF)
     A                                  6 10'?'
     A                                      DSPATR(HI)
     A                                  6 76'?'
     A                                      DSPATR(HI)
     A                                  7 74'Item'
     A                                      DSPATR(HI)
     A                                  8  2'Budget Item'
     A                                      DSPATR(HI)
     A                                  8 29'Description'
     A                                      DSPATR(HI)
     A                                  8 74'Type'
     A                                      DSPATR(HI)
     A*
     A            C1BGIT    R        B  9  2REFFLD(HSRREF/@@BGIT *LIBL/HSPREF)
     A  50                                  DSPATR(PC)
     A                                      DSPATR(HI)
     A            C1DESC        43A  B  9 29DSPATR(HI)
     A            C1ITTYCD  R        B  9 74REFFLD(HSRREF/@@ITTYCD *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A  54                                  DSPATR(PC)
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R HP7702K1
     A*%%TS  SD  20000921  074322  ISLRAMS     REL-V4R4M0  5769-PW1
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A                                 23 11'F4=Prompt'
     A                                 23 22'F5=Refresh'
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *---------------------------------------------------------------
     A          R HP7702ES                  SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE CONTROL FORMAT
      *---------------------------------------------------------------
     A          R HP7702EC                  SFLCTL(HP7702ES)
      *
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
