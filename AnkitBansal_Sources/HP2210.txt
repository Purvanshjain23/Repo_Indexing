      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      HPS Data Mart
      * PROGRAM:     HP2210 - Delete Current Records
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     08/11/04
      *
      * Function:    To keep run times to a minimum, we have implemented logic that
      *              allows us to rebuild only the 'current' data (as identified in
      *              data area DABUD).
      *
      *              This program is conditionally called from HPBLDBUD.
      *
      *              If HPBLDBUD is submitted with an incoming parm of ' ' instead of
      *              H=History, this program will be called to delete the 'current'
      *              records out of Datamart files prior to building the data.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      /eject
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fhsl8189a  uf   e           k disk
      *  DataMart BUD: Budget quantities
      *
      *
     Fhsl8193a  uf   e           k disk
      *  DataMart BUD: Weekly Orders
      *
      /eject
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
     D wkwkenddt       s               d   datfmt(*iso)
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *---------------------------------------------------------------
      * Definition for external data area DAFIN
      *---------------------------------------------------------------
     D
     Ddabud            ds                  dtaara(dabud)
     D  dacwkbegdt            36     43  0
     D  dacwkenddt            45     52  0
     D  dadays                61     63  0
     D  dathrudt              77     84  0
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * Mainline
      ****************************************************************
      *
      * Weekly Budgets/Orders
      * (Logic deletes records where the record week-ending date is equal to
      * or greater than the data area week-ending date.)
      *
     C     wkwkenddt     setll     hsl8193a
      *
     C                   dou       *in90 = *on                                  Do weekly
     C                   read      hsl8193a                               90
     C                   if        *in90 = *off                                 If not EOF
     C                   delete    d1rec
     C                   endif                                                  If not EOF
     C                   enddo                                                  Do weekly
      *
      * Budget Quantities
      * (Logic deletes records where the record week-ending date is equal to
      * or greater than the data area week-ending date.)
      *
     C     dacwkenddt    setll     hsl8189a
      *
     C                   dou       *in90 = *on                                  Do qty
     C                   read      hsl8189a                               90
     C                   if        *in90 = *off                                 If not EOF
     C                   delete    d2rec
     C                   endif                                                  If not EOF
     C                   enddo                                                  Do qty
      *
     C                   seton                                        lr
      /eject
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Bring in the data area that contains the week-beginning date and the week-ending
      * date for the first week of 'current' data.
      *
     C                   in        dabud
      *
     C                   movel     dacwkenddt    wkwkenddt
      *
     C                   endsr
      /eject
