     A*%%TS  SD  20140527  130114  ISLRAMS     REL-V6R1M0  5761-WDS
     A*****************************************************************
     A*  SYSTEM:        TRIUMPH FOODS
     A*  DISPLAY FILE:  TFD402
     A*  TITLE:         WORK WITH CASH DISTRIBUTION BALANCES
     A*  PROGRAMMER:    LEANNE RAMSEY
     A*  DATE:          05/30/08
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/TFPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A                                      CF04(04 'PROMPT')
     A                                      CF05(05 'REFRESH')
     A                                      CF15(15 'WW CD BALANCE ADJ')
     A                                      CF17(17 'PRINT OPTIONS')
     A                                      CF20(20 'WRKSPLF')
     A*
     A*-------------------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*-------------------------------------------------------------------------
     A          R TF402S1                   SFL
     A*%%TS  SD  20140527  125154  ISLRAMS     REL-V6R1M0  5761-WDS
     A  85                                  SFLNXTCHG
     A            HDRRN1    R        H      REFFLD(@@RRN)
     A            HDWEDT    R        H      REFFLD(@@WEDT)
     A            D1OPT     R        B 11  2REFFLD(@@OPT)
     A                                      CHGINPDFT(UL)
     A  30                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A N30                                  DSPATR(HI)
     A            D1WEMDY   R        O 11  6REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  ')
     A            D1RCEXFL  R        O 11 18REFFLD(@@INSTCD)
     A            D1ABALAM  R        O 11 22REFFLD(TFREF/@@ABALAM *LIBL/TFPREF)
     A                                      EDTCDE(K)
     A            D1SBALAM  R        O 11 38REFFLD(TFREF/@@SBALAM *LIBL/TFPREF)
     A                                      EDTCDE(K)
     A            D1TBALAM  R        O 11 54REFFLD(TFREF/@@TBALAM *LIBL/TFPREF)
     A                                      EDTCDE(K)
     A            D1BAFL    R        O 11 71REFFLD(TFREF/@@FLAG *LIBL/TFPREF)
     A*
     A*-------------------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*-------------------------------------------------------------------------
     A          R TF402C1                   SFLCTL(TF402S1)
     A*%%TS  SD  20140527  130114  ISLRAMS     REL-V6R1M0  5761-WDS
     A                                      SFLSIZ(0050)
     A                                      SFLPAG(0010)
     A N82                                  ROLLUP(25 'PAGE FORWARD')
     A                                      SETOF(30)
     A                                      SETOF(31)
     A                                      SETOF(51)
     A                                      SETOF(52)
     A                                      SETOF(53)
     A                                      SETOF(54)
     A                                      RTNCSRLOC(&RCD &FLD)
     A                                      OVERLAY
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 23'WORK WITH CASH DISTRIBUTION BALANC-
     A                                      ES'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USR)
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@JOB)
     A                                      DSPATR(HI)
     A                                  3  2'Include zero balances ..'
     A                                  3 27'?'
     A            C1YNFL    R        B  3 29REFFLD(@@FLAG)
     A  31
     AO 51                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A N31                                  DSPATR(HI)
     A                                  5  2'2=W/W Balance Adjustments'
     A                                  6 18'?'
     A                                      DSPATR(HI)
     A                                  6 71'?'
     A                                      DSPATR(HI)
     A                                  7  7'Revenue'
     A                                      DSPATR(HI)
     A                                  7 17'Rec'
     A                                      DSPATR(HI)
     A                                  7 70'Bal'
     A                                      DSPATR(HI)
     A                                  8  5'WeekEnding'
     A                                      DSPATR(HI)
     A                                  8 17'Exp'
     A                                      DSPATR(HI)
     A                                  8 22'---------- Remaining Balance Amoun-
     A                                      ts ---------'
     A                                      DSPATR(HI)
     A                                  8 70'Adj'
     A                                      DSPATR(HI)
     A                                  9  2'Op'
     A                                      DSPATR(HI)
     A                                  9  8'Date'
     A                                      DSPATR(HI)
     A                                  9 17'Flg'
     A                                      DSPATR(HI)
     A                                  9 27'Aggregate'
     A                                      DSPATR(HI)
     A                                  9 44'Seaboard'
     A                                      DSPATR(HI)
     A                                  9 61'Triumph'
     A                                      DSPATR(HI)
     A                                  9 70'Flg'
     A                                      DSPATR(HI)
     A            C1WEMDY   R        B 10  6REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  ')
     A                                      DSPATR(HI)
     A N31N51N52
     A N53N54                               DSPATR(PC)
     A            C1RCEXFL  R        B 10 18REFFLD(TFREF/@@INSTCD *LIBL/TFPREF)
     A  52                                  DSPATR(PC)
     A                                      DSPATR(HI)
     A            C1BAFL    R        B 10 71REFFLD(TFREF/@@INSTCD *LIBL/TFPREF)
     A  53                                  DSPATR(PC)
     A                                      DSPATR(HI)
     A*
     A*-------------------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*-------------------------------------------------------------------------
     A          R TF402K1
     A*%%TS  SD  20140522  144643  ISLRAMS     REL-V6R1M0  5761-WDS
     A  43                             21  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 22  2'F3=Exit'
     A                                 22 11'F4=Prompt'
     A                                 22 22'F5=Refresh'
     A                                 22 34'F15=CD Balance Adjustments'
     A                                 22 62'F17-Print Options'
     A                                 23 62'F20=Spool Files'
      *
     A*-------------------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
     A*-------------------------------------------------------------------------
     A          R TF402ES                   SFL SFLMSGRCD(24)
      *
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
     A*-------------------------------------------------------------------------
      * ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*-------------------------------------------------------------------------
     A          R TF402EC                   SFLCTL(TF402ES)
      *
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
