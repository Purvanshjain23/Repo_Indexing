      *****************  RPG PROGRAM HEADING  ***********************
      *
      * SYSTEM:      Hog Production System
      * PROGRAM:     HP1708
      * TITLE:       Maintain Budget Item on Templates
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     02/03/04
      *
      * Function: This program uses a subfile to maintain Budget Template Detail records
      *           for a specified Budget Item.
      *
      *           The program toggles between create mode and revise mode.
      *
      *           Also, Debra Lewis wanted the users to be able to change positioners/
      *           selectors without losing any data keyed on the screen. So, we basically
      *           execute the edit/update logic before every action. This makes the screen
      *           execute more slowly--but no data is lost.
      *
      *           The program is called from HP4701-Work with Budget Items
      *
      *
      *           Restrictors:
      *                Budget Item
      *
      *           Positioners:
      *                Budget template code
      *
      *           Selectors:
      *                Active/inactive status
      *
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 10/13/04  LeAnne Fedor
      *           Recompile only. Cost Per Unit Amount was changed from
      *           9,2 to 9,4.
      /eject
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fhpd1708   cf   e             workstn
     F                                     sfile(hp1708s1:hdrrn1)
     F                                     infds(ioFeed)
      *
      *
     Fhsl181c   if   e           k disk
      *  Items
      *
      *
     Fhsp185    if   e           k disk
      *  Budget template header
      *
      *
     Fhsl186b   uf a e           k disk    infds(dbfeed)
      *  Budget template detail
      *
      *
      /eject
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D create          c                   'CREATE'
     D revise          c                   'REVISE'
     D delete          c                   'DELETE'
     D view            c                   'VIEW  '
     D yes             c                   'Y'
     D no              c                   'N'
     D set1            c                   'SET1  '
     D edit1           c                   'EDIT1 '
     D scrn1           c                   'SCRN1 '
     D exit            c                   'EXIT  '
     D update          c                   'UPDATE'
      *
      *
      ****************************************************************
      * ARRAYS AND TABLES
      ****************************************************************
      *
     D btcd            s              6    dim(500)
      *
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Standard fields
      *
     D mode            s              6
     D pgm             s             10
     D msgfl           s             10
     D maxmsg          s              2  0  inz(20)
     D routne          s              6
     D maxrrn          s              4  0
     D reset           s              1    inz('N')
     D norecs          s              1    inz('N')
     D count           s              2  0
     D wkflow          s                   like(idflow)
     D rtncd           s              2
     D svrrn1          s                   like(hdrrn1)
      *
      *
      *
      * Workfields required for subfile paging--see initialization
      * subroutine.
      *
     D wkplin          s              2  0
     D wkrlin          s              2  0
      *
      *
      * Control fields
      *
     D procfl          s              1
     D entryfl         s              1
      *
      *
      * Array indexes
      *
     D x               s              3  0
     D y               s              3  0
      *
      *
      * Save fields for restrictors, positioners and selectors
      *
     D svbtcd          s                   like(c1btcd)
     D svaist          s                   like(c1aist)
      *
      *
      * Parm fields
      *
     D xxaist          s              1
     D xxbtcd          s                   like(d1btcd)
     D xxbtds          s                   like(d1btds)
      *
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * standard error message handling data structures
      *---------------------------------------------------------------
      *
      * For error message handling, a packed index (3) is required.
      * For program readability, define a corresponding error count
      * field called 'error'
      *
     D                 ds                  inz
     D  error                         2p 0
     D   e                            2p 0 overlay(error)
      *
      * This data structure supplies the name of the message file to
      * the message handling CL program.  The field name MSGFIL must be
      * constant. The value in quotes is the name of the specific
      * message file containing the user defined messages.
      *
     D                 ds                  inz
     D  msgfil                       10    inz('HSMSGF    ')
      *
      *---------------------------------------------------------------
      *  Standard message data structures
      *---------------------------------------------------------------
      *
      * The following 3 data structures are used to speed message
      * handling since it is faster to clear data structures than
      * arrays.  Each is associated with a standard message array.
      *
     D mgi             ds                  inz
     D  mgid                          7    dim(20)
      *
     D mgd             ds                  inz
     D  mgdt                         50    dim(20)
      *
     D mgwk            ds                  inz
     D  mgw                           1    dim(50)
      *
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *---------------------------------------------------------------
      * Standard workstation information data structure
      *---------------------------------------------------------------
      *    externally defined as UTWSFR (record format: UTIDFRR)
     D iofeed        e ds                  extname(utwsfr)
      *
      *
      *---------------------------------------------------------------
      * Standard database file information data structure
      *---------------------------------------------------------------
      *    externally defined as UTDBGR (record format: FDBCKD)
     D dbfeed        e ds                  extname(utdbfr)
      *
      *
      *---------------------------------------------------------------
      * Standard subfile option conversion
      *---------------------------------------------------------------
     D                 ds                  inz
     D d1opt                          2
     D   wkopt1                       1    overlay(d1opt)
     D   wkopt2                       1    overlay(d1opt:2)
      *
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /eject
      ****************************************************************
      * MAINLINE
      ****************************************************************
      *
     C                   eval      routne = set1
      *
     C     *inlr         doweq     *off                                         Main do loop
     C     routne        caseq     set1          $set1
     C     routne        caseq     scrn1         $scrn1
     C     routne        caseq     edit1         $edit1
     C     routne        caseq     exit          $exit
     C                   endcs
     C                   enddo                                                  Main do loop
      /eject
      *----------------------------------------------------------------
      * Set environment for screen 1
      *----------------------------------------------------------------
      *
     C     $set1         begsr
      *
      * Verify that the Budget Item the user selected still exists.
      *
     C     c1bgit        chain     hsl181c                            90
     C                   if        *in90 = *on                                  If gone
     C                   move      yes           norecs
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS04728'     mgid(e)
     C                   clear                   mgw
     C                   endif
     C                   endif                                                  If gone
      *
      *  Clear/fill the subfile
      *
     C                   exsr      $clr1
     C                   exsr      $frsh1
      *
      *
      * Set routine to display screen
      *
     C                   eval      routne = scrn1
      *
     C                   endsr
      /eject
      *----------------------------------------------------------------
      * Perform operator I/O for screen 1
      *----------------------------------------------------------------
      *
     C     $scrn1        begsr
      *
      *
      * Reset indicators in the Control Record
      *
     C                   exsr      $c1ind
      *
      * Write the error messages from the error arrays to the
      * error message subfile
      *
     C                   exsr      $wrmsg
      *
      * Write screen 1 to crt
      *
     C                   write     hp1708k1
     C                   write     hp1708ec
     C                   exfmt     hp1708c1
      *
      *
      * Set the 'RESET' flag to NO. This flag is used to determine whether to
      * reposition the user to a particular place in the subfile (reset=yes)
      * or to just place the user at the beginning of the subfile (reset=no).
      *
     C                   move      no            reset
      *
      * Save the value of the lowest subfile rrn on the screen for
      * later repositioning of the subfile
      *
     C                   z-add     idflow        wkflow
      *
      * Clear the messages from the message data structures and
      * program message queue
      *
     C                   exsr      $clmsg
      *
      *
      * Get user's entry and set next routine
      *
     C                   select
     C                   when      *in03 = *on                                  F3-exit
     C                   move      '03'          rtncd
     C                   eval      routne = exit
      *
      * If the prompt is in the control record, see if you need to refill
      * the subfile to match the new selectors; otherwise, just redisplay
      * the screen after prompting.
      *
     C                   when      *in04 = *on                                  F4-prompt
     C                   exsr      $edit1
     C                   if        error <> 0
     C                   movel     scrn1         routne
     C                   else
     C                   exsr      $f4s1
      *
     C                   if        fld = 'C1BTCD' or
     C                             fld = 'C1AIST'
     C                   exsr      $chksv
     C                   else
     C                   movel     scrn1         routne
     C                   endif
     C                   endif
      *
     C                   when      *in05 = *on                                  F5-refresh
     C                   if        mode = revise
     C                   move      yes           reset
     C                   endif
     C                   movel     set1          routne
      *
      *
     C                   when      *in10 = *on                                  F10-toggle
     C                   exsr      $edit1
     C                   if        error <> 0
     C                   movel     scrn1         routne
     C                   else
     C                   if        mode = create
     C                   movel     revise        mode
     C                   movel     revise        c1mode
     C                   else
     C                   movel     create        mode
     C                   movel     create        c1mode
     C                   endif
     C                   endif
      *
      *
      * User has changed a selector in the control record.
      *
     C                   when      svbtcd <> c1btcd or                          Selectors
     C                             svaist <> c1aist
     C                   exsr      $edit1
     C                   if        error <> 0
     C                   movel     scrn1         routne
     C                   else
     C                   exsr      $chksv
     C                   endif
      *
     C                   when      *in25 = *on                                  F25-rollup
     C                   exsr      $load1
     C                   movel     scrn1         routne
     C                   other
      *
      * User pressed 'ENTER'
      *
     C                   if        mode = revise and hdrrn1 = 0                 If nothing
     C                   else
     C                   exsr      $edit1
     C                   endif                                                  If nothing
      *
     C                   endsl
      *
     C                   endsr
      /eject
      *----------------------------------------------------------------
      * Save the control record selectors
      *----------------------------------------------------------------
      *
     C     $chksv        begsr
      *
     C                   eval      reset = no
     C                   eval      routne = set1
      *
     C                   move      c1aist        svaist
      *
      * Trim leading blanks on Budget Template code
      *
     C                   eval      c1btcd = %triml(c1btcd)
     C                   move      c1btcd        svbtcd
      *
     C                   endsr
      /eject
      *----------------------------------------------------------------
      * Edit input fields for screen 1
      *----------------------------------------------------------------
      *
     C     $edit1        begsr
      *
      * Set off any prompt indicators that may still be on
      *
     C                   exsr      $f4off
     C                   move      no            entryfl
      *
      * In revise mode, edit subfile options.
      *
     C                   select
     C                   when      mode = revise
     C                   exsr      $edop1
     C                   endsl
      *
      * If there are no errors in the control record or subfile option,
      * edit the subfile records.
      *
     C                   if        error = 0
     C                   exsr      $eds1
     C                   endif
      *
      * If there are no errors, process for updates.
      *
     C                   if        error = 0                                    If no error
     C                   exsr      $proc1
     C                   movel     set1          routne
     C                   else
     C                   movel     scrn1         routne
     C                   endif                                                  If no error
      *
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Clear subfile for screen 1
      *---------------------------------------------------------------
      *
     C     $clr1         begsr
      *
     C                   z-add     0             svrrn1
      *
     C                   clear                   hp1708s1
     C                   seton                                        83        sflclr
     C                   write     hp1708c1
     C                   setoff                                       818283
      *                                                    81=sfldsp
      *                                                    82=sflend
      *                                                    83=sflclr
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Refresh/fill the subfile for screen 1
      *---------------------------------------------------------------
      *
     C     $frsh1        begsr
      *
      * Set indicators to control the display
      *
     C                   select
     C                   when      mode = create
     C                   setoff                                       60
     C                   seton                                        61
     C                   move      *blank        c1btcd
     C                   move      *blank        c1aist
     C                   move      *blank        svbtcd
     C                   move      *blank        svaist
     C                   other
      *
     C                   setoff                                       61
     C                   seton                                        60
     C                   endsl
      *
      *
      * Position using the control recordr Budget Item and Template
      *
     C     key02         setll     hsl186b
      *
      * The field 'reset' is a flag field to indicate whether you are
      * attempting to reposition the user close to where he was in the
      * subfile.
      * The lowest subfile RRN on the screen from the DSPF feedback
      * area (IDFLOW) was retrieved earlier and put in WKFLOW.
      * add to WKFLOW the number of lines on your subfile page or the
      * number less 1 to determine a subfile RRN to reposition to.
      *
     C                   if        reset = yes
     C                   eval      maxrrn = wkflow + wkrlin
     C                   else
      *
      * Don't try to reposition the user, just start the subfile over
      *
     C                   eval      maxrrn = wkplin
     C                   endif
      *
      *
      * Load the subfile until you have the required number of records.
      * The number will depend on whether uou are repositioning the
      * subfile or not. Stop if you get to the end of the subfile.
      *
     C                   dou       svrrn1 >= maxrrn or *in82 = *on
     C                   exsr      $load1
     C                   enddo
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------------------------------
      * Load the subfile for screen 1
      *---------------------------------------------------------------------------------------
      *
     C     $load1        begsr
      *
     C                   z-add     svrrn1        hdrrn1
      *
      * Set the count of records on the subfile page to zero.
      *
     C                   z-add     0             count
      *
      * Fill the subfile until the end of the database file or
      * you have filled all the lines on the subfile page.
      *
     C                   setoff                                       82
     C                   dou       *in82 = *on or count >= wkplin               Do fill
      *
      * Clear subfile fields
      *
     C                   move      *blank        hdpfl
     C                   move      *blank        hdndfl
     C                   move      *blank        d1btcd
     C                   z-add     0             d1btqt
     C                   move      *blank        d1btds
     C                   move      *blank        d1aist
      *
      * In create mode, write a blank subfile record
      *
     C                   select
     C                   when      mode = create
     C                   add       1             count
     C                   add       1             hdrrn1
     C                   write     hp1708s1
     C                   seton                                        81
     C                   z-add     hdrrn1        hdpag1
      *
      * Im Revise mode, populate the subfile with records that match
      * the user's selectors.
      *
     C                   when      mode = revise
     C     c1bgit        reade     hsl186b                                82
     C                   if        *in82 = *off                                  If found
      *
     C                   move      yes           procfl
      *
      * Retrieve the Budget Template Header record to get comparison values.
      *
     C     tdbtcd        chain     hsp185                             92
     C                   if        *in92 = *on                                  If no hit
     C                   move      no            procfl
     C                   else
      *
      * Active/inactive
     C                   if        c1aist <> *blank and c1aist <> thaist
     C                   move      no            procfl
     C                   endif
     C                   endif                                                  If no hit
      *
     C                   if        procfl = yes                                 If process
     C                   move      thaist        d1aist
     C                   move      thbtds        d1btds
     C                   move      tdbtcd        d1btcd
     C                   z-add     tdbtqt        d1btqt
      *
     C                   exsr      $setoff
     C                   exsr      $sfind
      *
     C                   add       1             count
     C                   add       1             hdrrn1
     C                   write     hp1708s1
     C                   seton                                        81
     C                   z-add     hdrrn1        hdpag1
     C                   endif                                                  If process
     C                   endif                                                   If found
     C                   endsl
      *
     C                   enddo                                                  Do fill loop
      *
      * If no records were written to the subfile, then issue
      * the standard message 'No data to Display'.
      *
     C                   if        hdrrn1 = 0 and error < maxmsg                If no recs
     C                   add       1             error
     C                   eval      mgid(e) = 'HS09002'
     C                   endif                                                  If no recs
     C                   z-add     hdrrn1        svrrn1
      *
     C                   endsr
      /eject
      *--------------------------------------------------------------------------------------
      * Read changed records in the subfile and edit for valid subfile options on Screen 1
      *--------------------------------------------------------------------------------------
      *
     C     $edop1        begsr
      *
      * Read all the changed subfile records editing for valid options.
      *
     C                   dou       *in80 = *on                                  Do readc
     C                   readc     hp1708s1                               80
     C                   if        *in80 = *off and                             If rec read
     C                             hdpfl <> yes and
     C                             hdndfl <> yes
      *
      * Set off error indicators for subfile option
      *
     C                   seton                                        85
     C                   exsr      $setoff
      *
      * Convert option user entered to standard format. For example,
      * a ' 1', '01', and '1 ' must be standardized to ' 1'.
      *
     C                   if        wkopt2 = *blank                              If 2 = blank
     C                   eval      wkopt2 = wkopt1
     C                   eval      wkopt1 = *blank
     C                   endif                                                  If 2 = blank
      *
     C                   if        wkopt1 = *zero                               If 1 is zero
     C                   eval      wkopt1 = *blank
     C                   endif                                                  If 1 is zero
      *
     C                   if        d1opt <> *blank
     C                   move      yes           entryfl
     C                   endif
      *
      * Test for valid subfile option entry on all changed records.
      *
     C                   select
     C                   when      d1opt <> ' 4' and
     C                             d1opt <> '  '
     C                   seton                                        30
     C                   if        error < maxmsg
     C                   add       1             error
     C                   eval      mgid(e) = 'HS09006'
     C                   exsr      $err1
     C                   endif
      *
     C                   when      d1opt = ' 4'
      *                  (I don't know what/if there need to be edits here.)
     C                   endsl
      *
      * Checks completed.
     C                   exsr      $sfind
     C                   update    hp1708s1
      *
     C                   endif                                                  If rec read
     C                   enddo                                                  Do readc
      * Reset MDT
     C                   setoff                                       85
      *
     C                   endsr
      /eject
      *----------------------------------------------------------------------------------------
      * Edit changed subfile records on Screen 1
      *----------------------------------------------------------------------------------------
      *
     C     $eds1         begsr
      *
      * Initialize Budget Item array and indexes
      *
     C                   move      *blank        btcd
     C                   z-add     0             y
     C                   z-add     0             x
      *
      * Read all the changed subfile records processing the subfile selection.
      *
     C                   dou       *in80 = *on                                  Do process
     C                   readc     hp1708s1                               80
     C                   if        *in80 = *off                                 If rec read
      *
     C                   if        hdpfl <> yes and                             If not protected
     C                             hdndfl <> yes
      *
      * Set off error indicators
      *
     C                   exsr      $setoff
      *
      * You will not edit records to be deleted; so, set the modified
      * data tag on here so that they will be processed when the user
      * accepts his changes.
      *
     C                   if        d1opt = ' 4'
     C                   seton                                        85
     C                   else
     C                   setoff                                       85
     C                   endif
      *
      * Perform edit subroutine
      *
     C                   if        (mode = revise and d1opt <> ' 4') or         If edit
     C                              mode = create
      *
      * Clear output only fields
      *
     C                   if        mode = create and
     C                             d1btcd = *blank and d1btqt = 0
     C                   move      *blank        d1btds
     C                   move      *blank        d1aist
     C                   endif
      * Set on MDT
     C                   seton                                        85
     C                   move      *blank        hdpfl
     C                   move      *blank        hdndfl
      *
      * When Creating:
      *  1) Validate budget template code
      *  2) Validate quantity
      *
     C                   if        mode = create and
     C                             (d1btcd <> *blank or d1btqt <> 0)
     C                   move      yes           entryfl
     C                   exsr      $btcd
     C                   exsr      $btqt
     C                   endif
      *
      * When Revising:
      *   1) quantity
     C                   if        mode = revise
     C                   move      yes           entryfl
     C                   exsr      $btqt
     C                   endif
      *
     C                   endif                                                  If edit
     C                   endif                                                  If not protected
      *
     C                   exsr      $sfind
     C                   update    hp1708s1
      *
      * Set off the error indicators
      *
     C                   exsr      $setoff
     C                   endif                                                  If rec read
     C                   enddo                                                  Do process
      * Set MDT
     C                   setoff                                       85
      *
     C                   endsr
      /eject
      *----------------------------------------------------------------------------------------
      * Process subfile records from Screen 1
      *----------------------------------------------------------------------------------------
      *
     C     $proc1        begsr
      *
      * Read/process all changed subfile records.
      *
     C                   dou       *in80 = *on                                  Do process
     C                   readc     hp1708s1                               80
     C                   if        *in80 = *off                                 If rec read
      *
     C                   if        hdpfl <> yes and                             If not protected
     C                             hdndfl <> yes
     C                   exsr      $update
     C                   endif                                                  If not protected
      *
     C                   setoff                                       85
     C                   update    hp1708s1
      *
     C                   endif                                                  If rec read
     C                   enddo                                                  Do process
      *
      *
     C                   if        mode = revise
     C                   move      yes           reset
     C                   else
     C                   move      no            reset
     C                   endif
      *
     C                   endsr
      /eject
      *----------------------------------------------------------------
      * Update Database
      *----------------------------------------------------------------
      *
     C     $update       begsr
      *
     C                   select
      * Create
     C                   when      mode = create and d1btcd <> *blank
     C                   move      c1bgit        tdbgit
     C                   move      d1btcd        tdbtcd
     C                   z-add     d1btqt        tdbtqt
     C                   movel     yes           hdpfl
     C                   write     tdrec
      * Revise
     C                   when      mode = revise
     C     key01         chain     hsl186b                            92
     C                   if        *in92 = *off                                 If found
      *
     C                   if        d1opt = ' 4'                                 If deleting
     C                   delete    tdrec
     C                   move      *blank        d1opt
     C                   move      yes           hdpfl
     C                   move      yes           hdndfl
     C                   else
     C                   z-add     d1btqt        tdbtqt
     C                   update    tdrec
     C                   endif                                                  If deleting
     C                   endif                                                  If found
     C                   endsl
      *
     C                   endsr
      /eject
      *----------------------------------------------------------------
      * Set off subfile indicators
      *----------------------------------------------------------------
      *
     C     $setoff       begsr
      *
     C                   setoff                                       303334
     C                   setoff                                       5153
     C                   setoff                                       6364
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Validate Budget Template Code
      *---------------------------------------------------------------
      *
     C     $btcd         begsr
      *
     C                   move      *blank        d1aist
     C                   move      *blank        d1btds
      *
      * Validate budget template code:
      *   1) required
      *   2) budget template must exist in the Budget Template Header file
      *   3) budget template code cannnot already exist in the Budget Template
      *      Detail file for this Budget Item
      *   4) budget template code can only be in subfile 1 time
      *
     C                   select
     C                   when      d1btcd = *blank
     C                   seton                                        33
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS09001'     mgid(e)
     C                   exsr      $err1
     C                   endif
     C                   other
      *
      * Budget Template must exist in the Budget Template Header file
      *
     C     d1btcd        chain     hsp185                             92
     C                   if        *in92 = *off                                 If template hit
     C                   move      thaist        d1aist
     C                   move      thbtds        d1btds
     C                   else
     C                   seton                                        33
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS04729'     mgid(e)
     C                   clear                   mgw
     C                   movea     d1btcd        mgw(1)
     C                   movea     mgw           mgdt(e)
     C                   exsr      $err1
     C                   endif
     C                   endif                                                  If item hit
      *
      * Combination of budget item/template cannot already exist in the
      * Budget Template Detail file.
      *
     C     key01         chain(n)  hsl186b                            92
     C                   if        *in92 = *off                                 If already
     C                   seton                                        33
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS04730'     mgid(e)
     C                   clear                   mgw
     C                   movea     d1btcd        mgw(1)
     C                   movea     mgw           mgdt(e)
     C                   exsr      $err1
     C                   endif
     C                   endif                                                  If already
      *
      * Budget Template cannot be in subfile more than once.
      *
     C                   z-add     1             x
     C     d1btcd        lookup    btcd(x)                                92
     C                   if        *in92 = *on                                  If twice
     C                   seton                                        33
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS04731'     mgid(e)
     C                   clear                   mgw
     C                   movea     d1btcd        mgw(1)
     C                   movea     mgw           mgdt(e)
     C                   exsr      $err1
     C                   endif
     C                   endif                                                  If twice
     C                   endsl
      *
      * If all OK, then add Budget Template to the array used in determining
      * if a template is in the subfile more than 1 time.
      *
     C                   if        *in33 = *off                                 If OK
     C                   add       1             y
     C                   movel     d1btcd        btcd(y)
     C                   endif                                                  If OK
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Validate quantity
      *---------------------------------------------------------------
      *
     C     $btqt         begsr
      *
     C                   if        d1btqt < 0
     C                   seton                                        34
     C                   if        error < maxmsg
     C                   add       1             error
     C                   eval      mgid(e) = 'HS09000'
     C                   exsr      $err1
     C                   endif
     C                   endif
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Prompts on Screen 1
      *---------------------------------------------------------------
      *
     C     $f4s1         begsr
      *
      * Set off prompt indicators in all subfile records
      *
     C                   exsr      $f4off
      *
      *---------------------------------------------------------------
      * Control record prompts
      *---------------------------------------------------------------
      *
      * Budget template
     C                   select
     C                   when      fld = 'C1BTCD'
     C                   call      'HP5039'
     C                   parm      *blank        xxaist
     C                   parm      *blank        xxbtcd
     C                   parm      *blank        xxbtds
     C                   seton                                        51
     C                   if        xxbtcd <> *blank
     C                   move      xxbtcd        c1btcd
     C                   endif
      *
      * Active/inactive
     C                   when      fld = 'C1AIST'
     C                   call      'HP512'
     C                   parm      *blank        xxaist
     C                   seton                                        53
     C                   if        xxaist <> *blank
     C                   movel     xxaist        c1aist
     C                   endif
      *
      *------------------------------------------------------------------------------------
      * Subfile record prompts
      *------------------------------------------------------------------------------------
      *
      * Budget template
      *
     C                   when      fld = 'D1BTCD'
     C                   exsr      $rrn1
     C                   if        *in97 = *off                                 If hit
     C                   if        hdpfl = yes                                  If protected
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS09011'     mgid(e)
     C                   endif
     C                   else
      *
     C                   call      'HP5039'
     C                   parm      *blank        xxaist
     C                   parm      *blank        xxbtcd
     C                   parm      *blank        xxbtds
     C                   if        xxbtcd <> *blank
     C                   move      xxbtcd        d1btcd
     C                   move      xxbtds        d1btds
     C                   endif
     C                   endif                                                  If protected
      *
     C                   exsr      $setoff
     C                   exsr      $sfind
     C                   seton                                        8553
     C                   update    hp1708s1
     C                   setoff                                       8553
     C                   exsr      $synchrrn
     C                   endif                                                  If hit
     C                   other
      *
      * F4 not valid for this field. So, issue message and reset the
      * subfile relative record number.
      *
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS09011'     mgid(e)
     C                   endif
     C                   endsl
      *
     C                   endsr
      /eject
      *----------------------------------------------------------------------
      * Synch up the relative record number after an F4 prompt in the subfile
      *----------------------------------------------------------------------
      *
      * Reset the subfile relative record number to what it was before
      * the user took F4 (It will be out-of-synch if the user has
      * selected a record-- thus causing the subfile record to be
      * retrieved and updated.)  But, set the page of the subfile to
      * be displayed to the page on which the user took F4.
      *
     C     $synchrrn     begsr
     C                   z-add     svrrn1        hdrrn1
     C                   z-add     hdrr1         hdpag1
      *
     C                   endsr
      /eject
      *----------------------------------------------------------------
      * Set off the prompt indicators in the subfile records
      *----------------------------------------------------------------
      *
     C     $f4off        begsr
      *
     C                   setoff                                       80
      *
     C                   dou       *in80 = *on
     C                   readc     hp1708s1                               80
     C                   if        *in80 = *off
     C                   seton                                        85
     C                   exsr      $setoff
     C                   exsr      $sfind
     C                   update    hp1708s1
     C                   endif
     C                   enddo
      * Set MDT
     C                   setoff                                       85
      *
     C                   endsr
      /eject
      *----------------------------------------------------------------
      * Reset indicators in the control record
      *----------------------------------------------------------------
      *
     C     $c1ind        begsr
      *
      * If the Budget Template was deleted out from under the user, then
      * set on the indicator to limit function keys and input fields.
      *
     C                   if        norecs = yes
     C                   seton                                        90
     C                   endif
      *
     C                   endsr
      /eject
      *----------------------------------------------------------------
      * Reset indicators in the subfile records
      *----------------------------------------------------------------
      *
     C     $sfind        begsr
      *
      * If the template was deleted out from under the user, then set
      * indicator 90 back on to control available function keys and input
      * fields.
      *
     C                   if        norecs = yes
     C                   seton                                        90
     C                   endif
      *
      * If you are protecting subfile fields.
      *
     C                   if        hdpfl = yes
     C                   seton                                        64
     C                   endif
      *
      * If subfile fields should be non-display
      *
     C                   if        hdndfl = yes
     C                   seton                                        6364
     C                   endif
      *
     C                   endsr
      /eject
      *----------------------------------------------------------------------------
      * Set the subfile to redisplay at the first page that has errors for screen 1
      *----------------------------------------------------------------------------
      *
     C     $err1         begsr
      *
     C                   if        error = 1
     C                   eval      hdpag1 = hdrrn1
     C                   endif
      *
     C                   endsr
      *
      *----------------------------------------------------------------------------
      * Seton last record and end job
      *----------------------------------------------------------------------------
      *
     C     $exit         begsr
     C                   seton                                        lr
     C                   endsr
      *
      *----------------------------------------------------------------------------
      * Write error messages
      *----------------------------------------------------------------------------
      *
     C     $wrmsg        begsr
      *
     C                   call      'UT80045J'                           43
     C                   parm                    mgid
     C                   parm                    mgdt
     C                   parm                    error
     C                   parm      sdpgm         pgm
     C                   parm      msgfil        msgfl
      *
      * If call to UT80045J failed, a message hardcoded in the DSPF
      * command line format will be set on. So, redisplay screen.
      *
     C                   if        *in43 = *on
     C                   eval      routne = set1
     C                   endif
      *
     C                   endsr
      *
      *----------------------------------------------------------------------------
      * Clear messages
      *----------------------------------------------------------------------------
      *
     C     $clmsg        begsr
      *
     C                   call      'UT80045J'                           43
     C                   parm      *blank        mgi
     C                   parm      *blank        mgd
     C                   parm      *zero         error
     C                   parm      sdpgm         pgm
     C                   parm      msgfil        msgfl
      *
      * If call to UT80045J failed, a message hardcoded in the DSPF
      * command line format will be set on; so, redisplay screen.
      *
     C                   if        *in43 = *on
     C                   eval      routne = set1
     C                   endif
      *
     C                   endsr
      /eject
      *----------------------------------------------------------------------------
      * Find/retrieve the subfile record to update when returning from a select program
      * on screen 1.
      *----------------------------------------------------------------------------
      *
      * HDRR1 is the hidden field associated with the keyword SFLCSRRRN on the control
      * record in the DSPF.
      * HDRRN1 is the value associated with the keyword KSFILE in the F-specs for the
      * workstation.
      *
     C     $rrn1         begsr
      *
     C                   setoff                                       97
      *
     C                   if        hdrr1 <> 0
     C                   z-add     hdrr1         hdrrn1
     C     hdrrn1        chain     hp1708s1                           97
     C                   endif
      *
     C                   endsr
      /eject
      *----------------------------------------------------------------
      * Determine the 'Mode'---either Create or Revise
      *----------------------------------------------------------------
      *
     C     $mode         begsr
      *
     C     c1bgit        chain(n)  hsl186b                            92
     C                   if        *in92 = *off
     C                   movel     revise        mode
     C                   movel     revise        c1mode
     C                   else
     C                   movel     create        mode
     C                   movel     create        c1mode
     C                   endif
     C                   endsr
      /eject
      *----------------------------------------------------------------
      * Initialization subroutine
      *----------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Parm lists
      *
     C     *entry        plist
     C                   parm                    c1bgit
     C                   parm                    c1uom
     C                   parm                    c1uow
     C                   parm                    rtncd
      *
      * Key lists
      *
     C     key01         klist
     C                   kfld                    c1bgit
     C                   kfld                    d1btcd
      *
     C     key02         klist
     C                   kfld                    c1bgit
     C                   kfld                    c1btcd
      *
      * Determine the initial 'mode'---create or revise
      *
     C                   exsr      $mode
      *
      *
      * Retrieve a Budget Item Description
      *
     C     c1bgit        chain     hsl181c                            92
     C                   select
     C                   when      *in92 = *on
     C                   eval      c1desc = 'Unknown'
      *
     C                   when      imitcd = imbgit
     C                   eval      c1desc = imitds
     C                   other
      *
     C                   eval      c1desc = imbgit
     C                   endsl
      *
      *
      * To support F5-Refresh, logic requires the number of lines on
      * the subfile page and also the number of lines less 1.  By
      * setting these values up as constant work fields, the processing
      * logic can be standardized between programs.
      *
     C                   z-add     6             wkplin
     C     wkplin        sub       1             wkrlin
      *
      *
      * The following standard code must be included to make the
      * standard error message handling program function properly.
      * This code sets and clears the program message queue.
      *
     C                   movel     '*'           msgpgm
     C                   exsr      $clmsg
      *
     C                   endsr
      /eject
