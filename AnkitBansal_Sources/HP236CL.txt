/*                                                                 */
/*  PROGRAM NUMBER --> HP236CL                                     */
/*  PROGRAM NAME ----> DATAMART FIN: CALC SOLD (MKT) DAYS IN PHASE */
/*  PROGRAMMER ------> BARB GUTIERREZ                              */
/*  DATE ------------> 09/15/16                                    */
/*                                                                 */
/*                                                                 */
/***************************************************************** */
/*  MODIFIED:                                                      */
/***************************************************************** */

             PGM        PARM(&PROCFL &CURBEGDT)

             DCL        VAR(&PROCFL) TYPE(*CHAR) LEN(1)
             DCL        VAR(&CURBEGDT) TYPE(*DEC) LEN(8)
             DCL        VAR(&QDATA) TYPE(*CHAR) LEN(130)


/*-------------------------------------------------------------------*/
/* IF YOU ARE MAKING A 'CURRENT' RUN, SET UP/RUN AN OPEN QUERY       */
/* OVER THE HPS HOG GROUP FILE.                                      */
/*-------------------------------------------------------------------*/

/* FOR A 'CURRENT' RUN, ONLY SELECT GROUPS THAT:                     */
/*        1) ARE NOT CLOSED OR                                       */
/*        2) CLOSED ON/AFTER THE CURRENT WEEK-BEGINNING DATE         */

             IF         COND(&PROCFL *EQ ' ') THEN(DO)

/*  ONLY SELECT GROW FINISH GROUPS                             H     */

             CHGVAR     VAR(%SST(&QDATA 1 18)) VALUE('HGPPCD *EQ +
                          "GF   "')

/*  ALWAYS OMIT HOG GROUPS WITH A STATUS OF VOIDED               */

             CHGVAR     VAR(%SST(&QDATA 20 20)) VALUE('*AND HGGSCD +
                          *NE "VD"')

/*  ALWAYS OMIT HOG GROUPS WITH AN OPEN DATE OF ZERO             */

             CHGVAR     VAR(%SST(&QDATA 41 15)) VALUE('*AND HGOPDT +
                          *NE')
             CHGVAR     VAR(%SST(&QDATA 57 8)) VALUE(00000000)

             CHGVAR     VAR(%SST(&QDATA 66 15)) VALUE('*AND HGCLDT +
                          *EQ')
             CHGVAR     VAR(%SST(&QDATA 82 8)) VALUE(00000000)
             CHGVAR     VAR(%SST(&QDATA 91 14)) VALUE('*OR HGCLDT +
                          *GE')
             CHGVAR     VAR(%SST(&QDATA 106 8)) VALUE(&CURBEGDT)


             OVRDBF     FILE(HSP034) SHARE(*YES)
             OPNQRYF    FILE((HSP034)) QRYSLT(&QDATA) SEQONLY(*YES)
             ENDDO


/*------------------------------------------------------------------*/
/*  CALL THE PROGRAM TO CALCULATE THE SOLD (MKT) DAYS IN PHASE      */
/*------------------------------------------------------------------*/

             CALL       PGM(HP236) PARM(&PROCFL &CURBEGDT)

/*------------------------------------------------------------------*/
/*  IF YOU WERE DOING 'CURRENT' PROCESSING, REMOVE THE OVERRIDES    */
/*------------------------------------------------------------------*/

             IF         COND(&PROCFL *EQ ' ') THEN(DO)
             CLOF       OPNID(HSP034)
             DLTOVR     FILE(HSP034)
             ENDDO

             ENDPGM

