/***************************************************************** */
/*  PROGRAM NUMBER --> HP478CL                                     */
/*                                                                 */
/*  PROGRAM NAME ----> CLOSED/DISPOSED GROUP SUMMARIES             */
/*                                                                 */
/*  PROGRAM PURPOSE -> THIS CL CALLS THE PROGRAM TO PRINT ANY      */
/*                     OF THE FOLLOWING SUMMARY LEVELS FOR EITHER  */
/*                     A) DISPOSED GROUPS OR B) CLOSED GROUPS      */
/*                        1) GROUP                                 */
/*                        2) FARM                                  */
/*                        2) CELL                                  */
/*                        3) BUSINESS                              */
/*                                                                 */
/*                     VALUES ARE PASSED THRU THE LDA FROM         */
/*                     SPECIFY HP478.                              */
/*                                                                 */
/*  PROGRAMMER ------> SHARON ZURN               06/28/94          */
/*                                                                 */
/***************************************************************** */
/* MODIFICATIONS                                                   */
/***************************************************************** */
/* 05/22/01  LEANNE FEDOR                                          */
/*           REWRITTEN TO HANDLE ALL SUMMARY LEVELS (GROUP, FARM,  */
/*           CELL, BUSINESS) AND TO EXTRACT DATA FOR EITHER        */
/*           A) CLOSED GROUPS OR B) DISPOSED GROUPS.               */
/*                                                                 */
/* 06/23/10 LEANNE RAMSEY                                          */
/*          ADDED HSJ034N FOR THE DISPOSED ONLY LOGIC. WE DID THIS */
/*          TO GET HP690-DISPOSED GROUP PERFORMANCE REPORT TO      */
/*          SORT CORRECTLY. SO, SINCE THIS CL ALSO USED HSP390 AND */
/*          HP390, WE HAD TO ADD HSJ034N HERE AS WELL.             */
/*******************************************************************/

             PGM

/* DEFINE THE VARIABLE REQUIRED IN THE COMMAND TO OVERRIDE THE     */
/* PRINT FILE TO A LASER PRINTER                                   */

             DCL        VAR(&CMD) TYPE(*CHAR) LEN(512)

             DCL        VAR(&QDATA)  TYPE(*CHAR) LEN(222)
             DCL        VAR(&LDGSCD) TYPE(*CHAR) LEN(2)
             DCL        VAR(&LDFSBO) TYPE(*CHAR) LEN(5)
             DCL        VAR(&LDFROM) TYPE(*DEC)  LEN(8)
             DCL        VAR(&LDTO)   TYPE(*DEC)  LEN(8)
             DCL        VAR(&LDPPCD) TYPE(*CHAR) LEN(5)
             DCL        VAR(&LDPTCD) TYPE(*CHAR) LEN(5)
             DCL        VAR(&LDFSCD) TYPE(*DEC) LEN(5 0)
             DCL        VAR(&LDCELL) TYPE(*DEC) LEN(2 0)
             DCL        VAR(&LDHGCD) TYPE(*CHAR) LEN(7)
             DCL        VAR(&LDGTCD) TYPE(*CHAR) LEN(1)
             DCL        VAR(&LDSLCD) TYPE(*CHAR) LEN(1)
             DCL        VAR(&LDCRCD) TYPE(*CHAR) LEN(5)
             DCL        VAR(&LDPMGFL) TYPE(*CHAR) LEN(1)

             DCL        VAR(&HOLD) TYPE(*CHAR) LEN(10)
             DCL        VAR(&COPYS) TYPE(*DEC) LEN(2)
             DCL        VAR(&OUTQ) TYPE(*CHAR) LEN(10)

             CHGVAR     VAR(&LDFSBO) VALUE(%SST(*LDA 1 5))
             CHGVAR     VAR(&LDFROM) VALUE(%SST(*LDA 6 8))
             CHGVAR     VAR(&LDTO)   VALUE(%SST(*LDA 14 8))
             CHGVAR     VAR(&LDPPCD) VALUE(%SST(*LDA 27 5))
             CHGVAR     VAR(&LDPTCD) VALUE(%SST(*LDA 22 5))
             CHGVAR     VAR(&LDFSCD) VALUE(%SST(*LDA 32 5))
             CHGVAR     VAR(&LDHGCD) VALUE(%SST(*LDA 37 7))
             CHGVAR     VAR(&LDGTCD) VALUE(%SST(*LDA 46 1))
             CHGVAR     VAR(&LDCELL) VALUE(%SST(*LDA 47 2))
             CHGVAR     VAR(&LDSLCD) VALUE(%SST(*LDA 49 1))
             CHGVAR     VAR(&LDGSCD) VALUE(%SST(*LDA 62 2))
             CHGVAR     VAR(&LDCRCD) VALUE(%SST(*LDA 64 5))
             CHGVAR     VAR(&LDPMGFL) VALUE(%SST(*LDA 69 1))


/*-------------------------------------------------------------------*/
/*  CREATE WORKFILES                                                 */
/*-------------------------------------------------------------------*/

             CRTDUPOBJL OBJ(HSP378) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)

             CRTDUPOBJL OBJ(HSP379) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)


/*-------------------------------------------------------------------*/
/*  SETUP QUERY SELECTION                                            */
/*-------------------------------------------------------------------*/


/* IF THE USER SELECTED A SPECIFIC GROUP                             */

             IF         COND(&LDHGCD *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 1 12)) VALUE('HGHGCD +
                          *EQ "')
             CHGVAR     VAR(%SST(&QDATA 13 7)) VALUE(&LDHGCD)
             CHGVAR     VAR(%SST(&QDATA 20 1)) VALUE('"')
             ENDDO

/* IF THE USER SELECTED ALL GROUPS                                   */

             IF         COND(&LDHGCD *EQ ' ') THEN(DO) /* All groups */

/* SELECT CLOSED OR DISPOSED HOG GROUPS--DEPENDING ON LDA VALUE      */

             CHGVAR     VAR(%SST(&QDATA 1 12)) VALUE('HGGSCD +
                          *EQ "')
             CHGVAR     VAR(%SST(&QDATA 13 2)) VALUE(&LDGSCD)
             CHGVAR     VAR(%SST(&QDATA 15 1)) VALUE('"')


/* SELECT GROUPS FOR DATE RANGE BASED ON DISPOSED OR CLOSED DATE     */

             IF         COND(&LDGSCD *EQ 'CL') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 17 15)) VALUE('*AND HGCLDT +
                          *GE')
             CHGVAR     VAR(%SST(&QDATA 33 8)) VALUE(&LDFROM)
             CHGVAR     VAR(%SST(&QDATA 42 15)) VALUE('*AND HGCLDT +
                          *LE')
             CHGVAR     VAR(%SST(&QDATA 58 8)) VALUE(&LDTO)
             ENDDO

             IF         COND(&LDGSCD *EQ 'DS') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 17 15)) VALUE('*AND HGDSDT +
                          *GE')
             CHGVAR     VAR(%SST(&QDATA 33 8)) VALUE(&LDFROM)
             CHGVAR     VAR(%SST(&QDATA 42 15)) VALUE('*AND HGDSDT +
                          *LE')
             CHGVAR     VAR(%SST(&QDATA 58 8)) VALUE(&LDTO)
             ENDDO

/*  IF A BUSINESS OFFICE                                           */

             IF         COND(&LDFSBO *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 67 17)) VALUE('*AND HGFSBO +
                          *EQ "')
             CHGVAR     VAR(%SST(&QDATA 84 5)) VALUE(&LDFSBO)
             CHGVAR     VAR(%SST(&QDATA 89 1)) VALUE('"')
             ENDDO


/*  IF THE USER SELECTED A PRODUCTION TYPE                          */

             IF         COND(&LDPTCD *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 91 17)) VALUE('*AND HGPTCD +
                          *EQ "')
             CHGVAR     VAR(%SST(&QDATA 108 5)) VALUE(&LDPTCD)
             CHGVAR     VAR(%SST(&QDATA 113 1)) VALUE('"')
             ENDDO

/*  IF THE USER SELECTED A PRODUCTION PHASE                      */

             IF         COND(&LDPPCD *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 115 17)) VALUE('*AND HGPPCD +
                          *EQ "')
             CHGVAR     VAR(%SST(&QDATA 132 5)) VALUE(&LDPPCD)
             CHGVAR     VAR(%SST(&QDATA 137 1)) VALUE('"')
             ENDDO

/*  IF THE USER SELECTED A FARM SITE                             */

             IF         COND(&LDFSCD *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 139 15)) VALUE('*AND HGFSCD +
                          *EQ')
             CHGVAR     VAR(%SST(&QDATA 155 5)) VALUE(&LDFSCD)
             ENDDO

/*  IF THE USER SELECTED A GROUP TYPE                            */

             IF         COND(&LDGTCD *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 161 17)) VALUE('*AND HGGTCD +
                          *EQ "')
             CHGVAR     VAR(%SST(&QDATA 178 1)) VALUE(&LDGTCD)
             CHGVAR     VAR(%SST(&QDATA 179 1)) VALUE('"')
             ENDDO


/*  IF THE USER SELECTED A CELL                                  */

             IF         COND(&LDCELL *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 181 15)) VALUE('*AND FSCELL +
                          *EQ')
             CHGVAR     VAR(%SST(&QDATA 197 2)) VALUE(&LDCELL)
             ENDDO

/*  IF THE USER SELECTED A COMPANY RELATIONSHIP                  */

             IF         COND(&LDCRCD *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 200 17)) VALUE('*AND FSCRCD +
                          *EQ "')
             CHGVAR     VAR(%SST(&QDATA 217 5)) VALUE(&LDCRCD)
             CHGVAR     VAR(%SST(&QDATA 222 1)) VALUE('"')
             ENDDO

             ENDDO      /* All groups */

/*-------------------------------------------------------------------*/
/* FOR DISPOSED GROUPS                                               */
/*-------------------------------------------------------------------*/

             IF         COND(&LDGSCD *EQ 'DS') THEN(DO)

             OVRDBF     FILE(HSJ034N) SHARE(*YES)
             OPNQRYF    FILE((HSJ034N)) QRYSLT(&QDATA) KEYFLD(*FILE) +
                          SEQONLY(*YES)

             CRTDUPOBJL OBJ(HSP390) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)

             CALL       PGM(HP390)

             CLOF       OPNID(HSJ034N)
             DLTOVR     FILE(*ALL)
             ENDDO

/*-------------------------------------------------------------------*/
/* BUILD WORKFILES FOR BOTH DISPOSED AND CLOSED GROUPS               */
/*-------------------------------------------------------------------*/

             OVRDBF     FILE(HSJ018G) SHARE(*YES)
             OPNQRYF    FILE((HSJ018G)) QRYSLT(&QDATA) KEYFLD(*FILE) +
                          SEQONLY(*YES)

             CALL       PGM(HP378)

             CLOF       OPNID(HSJ018G)
             DLTOVR     FILE(HSJ018G)

/*------------------------------------------------------------------*/
/* SET PRINT PARMS FROM LDA                                         */
/*------------------------------------------------------------------*/

 HOLD:       IF         COND(%SST(*LDA 411 1) = 'Y') THEN(CHGVAR +
                          VAR(&HOLD) VALUE(*YES))
             ELSE       CMD(CHGVAR VAR(&HOLD) VALUE(*NO))

 COPYS:      CHGVAR     VAR(&COPYS) VALUE(%SST(*LDA 412 1))
             IF         COND(&COPYS = 0) THEN(CHGVAR VAR(&COPYS) +
                          VALUE(1))

 OUTQ:       CHGVAR     VAR(&OUTQ) VALUE(%SST(*LDA 413 10))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(%SST(*LDA 401 10)))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(*JOB))

/*------------------------------------------------------------------*/
/* OVERRIDE PRINTER                                                 */
/*------------------------------------------------------------------*/

/* USE THE CUSTOMIZED SEABOARD COMMAND TO OVERRIDE THE PRINT FILE.  */
/* THIS COMMAND ALLOWS THE REPORT TO BE GENERATED ON A STANDARD LINE*/
/* PRINTER OR A LASER PRINTER. TO USE THIS OVERRIDE YOU MUST DECLARE*/
/* A COMMAND VARIABLE AND FOLLOW THE OVERRIDE WITH A CALL TO        */
/* QCMDEXC.                                                         */

/* THIS REPORT CAN FIT IN 83 COLUMNS AT 10 CPI                      */

             SBDPRTOVR  CMDVAR(&CMD) FILE(QPRINT) OUTQ(&OUTQ) +
                          WIDTH(83) COPIES(&COPYS) HOLD(&HOLD)

             CALL       PGM(QCMDEXC) PARM(&CMD 512)

/*------------------------------------------------------------------*/
/*  CONDITIONALLY OVERRIDE SORT SEQUENCES                            */
/*------------------------------------------------------------------*/

/*  SORTED BY PRODUCTION MANAGER                                     */

             IF         COND(&LDPMGFL = 'Y') +
                          THEN(DO)
             OVRDBF     FILE(HSP378) SHARE(*YES)
             OPNQRYF    FILE((HSP378)) KEYFLD((WHPMGCD) (WHFSCD) +
                          (WHBLCD) (WHHGCD)) SEQONLY(*YES)
             ENDDO

/*  BY GROUP--NOT SORTED BY PRODUCTION MANAGER                       */

             IF         COND(&LDSLCD *EQ 'G' *AND &LDPMGFL = 'N') +
                          THEN(DO)
             OVRDBF     FILE(HSP378) SHARE(*YES)
             OPNQRYF    FILE((HSP378)) KEYFLD((WHPPCD) (WHLEVEL)) +
                          SEQONLY(*YES)
             ENDDO

/*------------------------------------------------------------------*/
/*  RUN REPORT                                                      */
/*------------------------------------------------------------------*/

             CALL PGM(HP678)

             DLTOVR     FILE(*ALL)

             ENDPGM

