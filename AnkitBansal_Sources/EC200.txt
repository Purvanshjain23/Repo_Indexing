     **
     **  PROGRAM NUMBER  -> EC200
     **
     **  PROGRAM NAME    -> BUILD INVOICE WORKFILE
     **
     **  PROGRAM PURPOSE -> THIS PROGRAM RECEIVES CO AND ORDER # TO
     **                     READ THE HISTORY FILE AND RETREIVE ALL THE
     **                     LINE ITEM DATA AND WRITE IT TO A WORKFILE
     **                     FROM WHICH THE INVOICE WILL BE CREATED AND
     **                     SENT EDI.
     **
     **  PROGRAMMER      -> SHARON ZURN             DATE  -> 05/11/99
     **
     **  MODIFIED BY
     **  10/26/00 PKD 1) CHANGED PROGRAM TO USE CUSTOMER ELECTRONIC
     **               COMMERCE FILE FOR RETRIEVING TRADING PARTNER
     **               INFORMATION INSTEAD OF COMPANY/CUSTOMER ELECTRONIC
     **               COMMERCE FILE.
     **               2) CHANGED APPLICATION TO WORK FOR PORK INSTEAD OF
     **                  POULTRY.
     **  04/13/01 RMC FOR ALTERNATE REMITTANCE ADDRESS, GET CUSTOMER
     **               ALT REMIT FLAG FROM COMPANY/CUSTOMER FILE AND
     **               THE ADDRESS FROM THE NEW A/R REMITTANCE ADDR FILE
     **   SCAN  RMC0413
     **  04/29/02 PKD SELECT ONLY ITEMS WITH SHIPPED QUANTITY GREATER
     **               THEN ZERO.
     **  01/11/06 SLM NAME FOR THE REMIT NAME IS IN THE REMITTANCE FILE
     **               POA7REL1.
     **  02/13/06 PKD IF UNIT OF MEASURE IS CS, HS, OR EA, THEN SEND
     **               QUANTITY IN DQTYIN, ELSE SEND WEIGHT.
     **  05/25/06 RMC ALWAYS USE 360 TO GO TO COMPANY NAME/ADDR FOR UPC
     **  08/10/06 RMC GET DPITNO FROM POBIREP-CUST ITEM REF FILE
     **               IF THE CUST ITEM REF IS "Y" IN CUSTOMER FILE.
     **   SCAN  RMC0810
     **  06/19/07 RMC RECOMP FOR PDNEREP DB CHANGE - BROKER REMOVED
     **
     **   SCAN  TPA071007
     **  07/10/07 TPA  REMIT DUNS NUMBER MOVED FROM THE CUSTOMER EXT FILE
     **                TO THE A/R REMITTANCE ADDRESS FILE
     **
     **  07/26/07 RMC DLY029 - FROM HISTORY DETAIL ACCRUAL PDICCPP:
     **               UPDATE DISC/ALLOWANCE IN HEADER AND DTL,
     **               UPDATE SALES TAX IN HEADER IF APPLICABLE.
     **   SCAN  RMC0726
     **  02/07/08 PKD FP1274 - IF UNIT OF MEASURE FOR PRICING IS CW,
     **               THEN CONVERT IT TO LB.
     **  04/10/08 SAZ FP1284   ADDED CATCH WEIGHT FLAG TO ECP200.
     **               ADD CODE TO POPULATE FIELD
     **  09/03/09 SAZ CHANGED ITEM REF STS FLAG TO I P B N INSTEAD OF
     **               Y & N.I=INV, P=PO, B=BOTH, N=NONE
     **  10/26/09 PKD ADDED CUSTOMER CHARGE ACCRUAL TO THE HEADER AND
     **               DETAIL PROCESS.
     **  12/14/09 SAZ TRADE PARTNER DAFS REQUESTED THE ITD02 TO READ
     **               '01' INSTEAD OF ' 1'. ADDED CODE TO SPECIFICALLY
     **               SEND '01' FOR DAFS ONLY.        (F335)
     **  08/01/10 SAZ ADD SHIP FROM RECORD FOR ASSOCIATED GROCERS
     **  03/15/11 SAZ SPARTAN REQUESTED CARRIER NAME FOR THE G27
     **               SEGMENT 05 ELEMENT. ADD FIELDS TO ECP001
     **  03/15/11 SAZ HANNAFORD REQUESTED PACK INFORMATON FOR THE P04
     **               SEGMENT ADD FIELDS TO ECP002
     **  04/03/12 SAZ WAL-MART HAD SWELL ALLOW THAT IS NOT GETTING TAKEN
     **               FROM THE INV TOT.  ADD SAC SEGMENT AND A FOR HALLCD
     **  05/09/12 SAZ RETRIEVE THE SCAC CODE FROM THE CARRIER NAME.
     **               THIS WAS REQUESTED FROM ASSWGH.
     **  05/18/12 SAZ COMMENTED OUT MOVE OF THE ITEM REF FLAG IN THE SHIP
     **               FROM SECTION. SHOULD ONLY BE USING THE SHIP TO #.
     **               UPDATED THE COMMENTS ON THE IF STATEMENT FOR INV OR
     **               BOTH.
     **  06/20/12 SAZ ADDED STATUS FIELD IN POBIREP TO USE FOR THE
     **               RESOURCE NUMBER TYPE.  PUBLIX WANTS CERTAIN ITEMS
     **               THAT ARE XREFERNCED TO HAVE THE CODE OF 'IN' INSTEAD
     **               OF 'UK'.
     **  12/19/12 SAZ ADDED REQUESTED DELIVERY DATE. PADDED ECP001 WITH
     **               EXTRA FIELDS.  REQUESTED DELIVERY DATE REQUESTED
     **               BY MEIJER.                                       EAD
     **  12/28/12 SAZ ADD CODE TO POPULATE FOR ORIGIN OR DESTINATION
     **               SHIPMENT METHOD OF PAYMENT.   (MEIJER)E002330
     **  08/07/13 SAZ FOODLION REQUESTED PACK INFORMATON FOR THE P04
     **               SEGMENT.
     **  09/04/13 SAZ ADD CODE TO TOTAL ALL OF THE SAC BY INVOICE.
     **  12/26/13 SAZ ADDED CODE TO POPULATE THE BOL (SMART AND FINAL)
     **  01/28/14 SAZ ADD CALL TO POPULATE ALLOWANCE/CHARGE FILES.
     **  01/28/14 SAZ ADD CODE TO POPULATE THE FILE SET CODE. NEW FILE
     **               SETS WERE CREATED TO USE THE ALL/CHG FILES. SINCE
     **               THE MAPS ARE DIFFERENT BETWEEN THE 810 & 880, HAD
     **               TO CREATE DIFFERENT FILE SETS.
     **  06/23/14 SAZ ADD ORDER NUMBER TO FIELD HI02DT FOR SYSCO OR REF VN.
     **               ADD CR CUSTOMER REF NBR HI02TX  FOR SYSCO.
     **               ADD IL INTERNAL ORD NBR HI03TX  FOR SYSCO.
     **               ADDED CODE TO POPULATE THE FOB DESTINATION OR ORIGIN
     **               FOR BASHAS.
     **               RETRIEVE GTIN FROM ITEM DEFAULT EXT FOR SYSCO.
     **  07/31/14 SAZ ADDED FIELDS TO ECP002 TO HOLD THE UNIT WEIGHT AND
     **
     **  08/14/14 LR  E001901--SYSCO FOR DAILYS
     **               To address Sysco's pricing requirements, we changed
     **               our logic for calc'ing the Unit Price (field DUPRC
     **               in ECP002-Invoice Detail.)
     **  08/29/14 SZ  E001901--SYSCO FOR DAILYS
     **               CLEAR FIELDS HI02TX & HI03TX.
     **  09/10/14 LR  E001901--SYSCO FOR DAILYS
     **               CHANGE LOGIC TO RETRIEVE GTIN FROM PBBTREP INSTEAD OF
     **               PBA2REP. (SEE $GTINR)
     **  02/25/15 SM  E003964--DSYSCO OR SYSCO TO WORK THE SAME
     **  03/09/15 SZ  E003964--DSYSCO INVOICES SENDING THE INCORRECT
     **               UNIT OF MEASURE. SAVING THE U/M-ORDERED TO USE IN
     **               THE MAP TO DETERMINE THE UNIT OF MEASURE THEY
     **               REQUIRE AND ALSO TO MATCH THE 855.
     **  06/02/15 SZ  E004114--TYSON - SEABOARD FOODS.
     **               ADD TYSON TO THE VENDOR NAME CREATION CODE.
     **  08/31/15 LB  E004180--ADD NEW TRADE PARTNER CONTROL FILE
     **               USE TRADE PARTNER FROM PDNEREP TO RETRIEVE THE
     **               EXTOL CONTROL FLAG AND UPDATE EXTOL CONVERSION
     **               TYPE IN ECP001
     **  04/04/16 SZ  IPAP ADD TO THE GTIN PROCESSING.  THIS IS THE
     **               ONLY CODE THAT THEY REQUIRE BACK.
     **  03/08/17 SZ  ADDED MISSING CODE TO POP THE FOB POINT ORIGIN
     **               FOR BASHAS  MOVE TEXT30    HI05TX.
     **  05/01/17 SZ  DAILYS LABATT ADD TO THE GTIN PROCESSING.  STILL
     **               RETRIEVING THE UPC ALSO.
     **  07/18/17 SZ  WOOD FRUITTICHER ADD TO GTIN PROCESSING.  STILL
     **               RETRIEVING THE UPC ALSO.
     **  01/26/18 SZ  REINHART FS ADD TO GTIN PROCESSING.
     **  07/17/18 DN  T13449 - ADDED 'GORDON' AS A VALID SHIP FROM
     **               ADDRESS RECORD.
     **               ALWAYS CALL '$GTINR' ROUTINE AND REMOVED SOME
     **               HARD CODED TRADE PARTNERS.
     **  08/15/18 DN  S13598 - REMOVED HARDCODING OF TRADE PARTNERS IN
     **               $GTINR ROUTINE.
     **               ADDED 'FRSHMK' AS A VALID SHIP FROM ADDRESS RCD.
     **  10/26/18 DN  R13981 - SPANISH DESC ON EXPORT DOCS
     **               RECOMPILE ONLY DUE TO ITEM DEFAULT EXTSN FILE CHANGE.
     **  12/03/18 DN  S14179 - ADDED VENDOR (VN) RECORD FOR SHAMROCK FOODS
     **               'SHAMRK'.
     **  01/17/19 RMC R14392 - OUTPUT CUSTOMER COMMENT TYPE "IN" TO
     **               FIELD Z5T1/H106TX  IN ECP001 - SCAN R14392
     **  03/13/19 DN  R14392 - ADDED 'GORDON' FOR VENDOR RECORD.
     **               REMOVED 'TYSNFD', 'TYSNFS', & 'DSYSCO' FROM VENDOR
     **               RECORD.
     **               REMOVED  'GORDON' FROM THE SHIP FROM RECORD.
     **  11/25/19 JBB E15824 - Add Delivery PO to EDI Stage Tables
     **               Get Delivery PO from Sales History EXT.  Output
     **               Delivery PO to ECO001 field HI04TX.  If blank,
     **               output blanks.
     **  03/09/21 RMC WHD 76099 - FOR "SF" RECORD - REMOVE ASSGRC,
     **               ADD ASOGBR
     **  02/03/22 DN  W89663 - ADDED 'GOLUB' FOR VENDOR RECORD.
     **                        ADDED 'KROGER' FOR VENDOR RECORD.
     **  03/11/22 RMC W89663   ADDED 'KROGER' TO  "BY" BUYER PARTY ADDRESS
     **                        WITH THEIR ADDRESS
     **  06/14/22 DN  W100559  Changed 'VN' segment creation to look at
     **                        'Invoice 810 Crt VN Name' flag in the
     **                        Trade Partner Control (PBB8REP) file so
     **                        we don't have to hardcode the Trading
     **                        Partner code.
     **                        WHD103661-Fixed bug to set NIDCDQ to '9 '
     **                        instead of '9' so subsequent records don't
     **                        retain the '1' after the 9. Per Rose, do
     **                        NOT change the '91' to a '9'.
     **                        WHD104634-Fixed bug to always populate
     **                        DBUPRC & DUPRC when DBUPRC='UM'. This
     **                        will ensure the correct Unit Price in ECP002
     **                        is set from the Sales History.
     **
     ******************************************************************
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
     H        1
      *
     FOMHSTPL9IF  E           K        DISK
      *SALES HISTORY   KEY: CO, ORDER #
      *
     FPMD0CPL1IF  E           K        DISK
      *SALES HISTORY EXT   Retrieval index.
      *
     FPDNEREL1IF  E           K        DISK
      *CUSTOMER EXT                        FILE KEY: CO, CUST
      *
     FCABBREL1IF  E           K        DISK
      *CUSTOMER
      *
     FCAABREL1IF  E           K        DISK
      *COMPANY
      *
     F*CAAQREL1IF  E           K        DISK
      *COMPANY DEFAULTS
      *
     FCAAOREL1IF  E           K        DISK
      * TERMS
      *
     FOMCQWKL1IF  E           K        DISK
      *OM ORDER FINANCE WRKFILE   KEY: CO, ORDER #, SEQ #
      *
     FCABZREL1IF  E           K        DISK
      *COMPANY/ITEM               KEY: CO, ITEM CODE
      *
     FCAEOREL1IF  E           K        DISK
      *COMPANY/CUSTOMER           KEY: CO,SHIP TO            RMC0413
      *
     FPOA7REL1IF  E           K        DISK
      *A/R REMITTANCE ADDRESS    KEY: REMITTANCE FLAG
      *
     FPOBIREL1IF  E           K        DISK
      *CUSTOMER ITEM REFERENCE   KEY: SHIP TO CUST / ITEM    RMC0810
      *
     FPOAVCPL1IF  E           K        DISK
      *CUSTOMER COMMENT          KEY: SHIP TO CUST / COMMENT TYPE   R14392
      *
     FPDICCPL1IF  E           K        DISK
      *HISTORY DTL ACCRUAL        KEY: CO,ORDER,SEQ,SEC SEQ  RMC0726
      *
     FOMBYREL1IF  E           K        DISK
      *ITEM DEFAULT              KEY: ITEM                   SAZ080410
      *
     FPBB8REL1IF  E           K        DISK
      *TRADE PARTNER CONTROL               FILE KEY: TRADE PARTNER NO.
      *
     FPDKMREL1IF  E           K        DISK
      *COMPANY VALUES                                        SAZ08042010
      *
     FOMFFREL0IF  E           K        DISK
      *CARRIER                   KEY: CARRIER CODE
      *
     F***PNGQREL1IF  E           K        DISK
      *BILL TO INTERCHANGE       KEY: BILL TO, INTERCHANGE CODE
      *
     FPBBTREL1IF  E           K        DISK
      *ITEM DEFAULT EXT          KEY: ITEM CODE
      *
     FECP004L8IF  E           K        DISK
      *EDO PO  HEADER            KEY: PO NUMBER, TRADE PARTNER CODE
      *
     FECP001  O   E                    DISK
      *EDI INVOICE HEADER WORK FILE
      *
     FECP002  O   E                    DISK
      *EDI INVOICE DETAIL WORK FILE
      *
     FECP003  O   E                    DISK
      *EDI INVOICE NAME WORK FILE
      *****************************************************************
      *ARRAYS TO CONCAT DUNS AND WAREHOUSE TOGETHER.
     E                    A20        20  1
     E                    A40        40  1
      *
      * Data structures:
      *SUBSTRING SCAC CODE OUT OF THE CARRIER NAME
     I           IDS
     I                                        1  30 CNAME
     I                                        1   4 SCAC
      *
     I                                        1  12 UPCCDE
     I                                        1   7 UPC2
     I                                        8  12 UPC3
      *
     I           IDS
     I                                        1  20 DUNSST
     I                                        1   9 DUN
     I                                       10  13 SFX
     I                                       14  20 FILL1
      *
     I           IDS
     I                                        1  20 DUNSBT
     I                                        1   9 DUNBT
     I                                       10  13 SFXBT
     I                                       14  20 FILL2
      *CONSTANTS
     I              'DUNSNUM'             C         DUNS
     I              'SYSCO CORPORATION'   C         SYSNM
     I              '1095276680000'       C         SYSDUN
     I              '1390 ENCLAVE PARKWAY'C         SYSADD
     I*
     I              'KROGER PORK GROUP'   C         KROGNM
     I              '0069995280320'       C         KROGDU
     I              '1014 VINE ST        'C         KROGAD
     I              'CINCINNATI         ' C         KROGCI
      ****************************************************************
      * MAIN LINE
      ****************************************************************
     C                     MOVE *OFF      FIRST   1
     C                     MOVE *OFF      PROCSS  1
      *CLEAR WORK FIELDS
     C                     Z-ADD*ZEROS    WKFRT            FRT AMT
     C                     Z-ADD*ZEROS    WKWGTI           WEIGHT
     C                     Z-ADD*ZEROS    WKAMTI           AMOUNT
     C                     Z-ADD*ZEROS    WKQTYI           QUANTITY
     C                     Z-ADD*ZEROS    HLITCT           LINE ITEM COUNT
      *
      *READ ALL THE HISTORY DETAIL RECORDS
      *
     C           KEYOMH    SETLLOMHSTPL9
     C           *IN99     DOUEQ*ON                        DO READE
     C           KEYOMH    READEOMHSTPL9                 99
     C           *IN99     IFEQ *OFF                       IF REC READ
      *
      *************************  DO THIS ROUTINE ONCE **********************
      *
     C           FIRST     IFEQ *OFF
      *
      *GET THE PO SURROGATE NUMBER TO TIE THE RECORDS TOGETHER
     C                     Z-ADDF4CONB    NPOSUR           INVOICE NUMBER NAME
     C                     Z-ADDF4CONB    DPOSUR           INVOICE NUMBER DETAIL
     C                     Z-ADDF4CONB    HPOSUR           INVOICE NUMBER HEADER
      *
      *GET THE LOAD ID FOR BOL INFORMATION REQUESTED BY SMART AND FINAL
     C                     Z-ADDF4RMNB    HI06DT           LOAD ID     ER NAME
      *
      *POPULATE THE TRDER NUMBER.
     C                     Z-ADDF4C4NB    HI02DT           ORDER NUMBER     ME
      *
      *BUILD KEY FOR PARMS PASSED TO ALLOW/CHG BUILD.
     C                     Z-ADDF4AIC3    P1PARM  30       COMPANY NUMBER
     C                     Z-ADDF4C4NB    P2PARM  70       ORDER NUMBER
     C                     Z-ADDF4CONB    P3PARM  90       EDI SURROGATE(INVME
     C                     Z-ADDF4ANC7    P4PARM  70       BILL TO CUSTOMER
     C                     MOVE *BLANKS   P5PARM  6        FILE SET
      *
      *CHAIN TO THE CUSTOMER EXT FILE TO RETRIEVE THE DUNS
      *NUMBER FOR THE SHIP TO CUSTOMER OF THE ORDER
     C                     Z-ADDF4BKC7    KCUST   70       SHIPTO CUSTOMER #
     C           KEYEC     CHAINPDNEREL1             89
     C           *IN89     IFEQ *OFF                       IF REC READ
      *
      *MOVE SHIP TO DUNS NUMBER TO DATA STRUCTURE
     C                     MOVELNEU5TX    DUNSST           SHIPTO DUNS+SUFFIX
     C                     MOVELDUN       HCEDID           DUNS NUMBER
      *
      *SET PROCSS TO ON TO CONTINUE PROCESSING
     C                     MOVE *ON       PROCSS
     C                     ENDIF                           *IN89 OFF
      *
      *CHAIN TO THE CUSTOMER ELECTRONIC COMM X-REF FILE TO RETRIEVE TRADING
      *PARTNER CODE AND THE REMIT TO DUNS NUMBER FOR THE AR CUSTOMER FOR
      *THE ORDER.
     C                     Z-ADDF4ANC7    KCUST   70       AR CUSTOMER #
     C           KEYEC     CHAINPDNEREL1             89
     C           *IN89     IFEQ *OFF                       IF REC READ
      *
      *MOVE TRADING PARTNER CODE TO HEADER FIELD
     C                     MOVE NEJ5NX    HTPCD            TRADING PARTNER CODE
     C                     MOVELNEU5TX    DUNSBT           BILL TO DUNS+SUFFIX
      *
4180  *CHAIN TO THE TRADING PARTNER CONTROL FILE TO RETRIEVE THE
 |    *EXTOL CONTROL FLAG AND PUT THAT INTO THE EXTOL CONVERSION
 |    *TYPE IN EDI INVOICE HEADER
 |   C           KEYTPC    CHAINPBB8REL1             91
 |   C           *IN91     IFEQ *OFF                       IF REC READ
 |   C                     MOVELB8CBSC    HI01FL           EXTOL CONV TYPE
4180 C                     ENDIF                           *IN91 OFF
     C                     ENDIF                           *IN89 OFF
     C****
     C* BEG: R14392   RMC 1/17/2019
      *CHAIN TO THE CUSTOMER COMMENT TO GET THE 'IN' COMMENT
      *
     C                     Z-ADDF4BKC7    AVBKC7           SHIPTO CUSTOMER #
     C                     MOVE 'IN'      AVI1SX
     C           KEYAVC    CHAINPOAVCPL1             89
     C           *IN89     IFEQ *OFF                       IF REC READ
      *
      *MOVE SHIP TO DUNS NUMBER TO DATA STRUCTURE
     C                     MOVELAVXRTX    HI06TX           COMMENT
     C                     ENDIF                           *IN89 OFF
      * END: R14392
      *
      *PROCESS THE NAME RECORD
     C                     EXSR $NAME
      * SETON FIRST
     C                     MOVE *ON       FIRST
     C                     ENDIF                           FIRST OFF
      *
      **********************************************************************
      *
     C           PROCSS    IFEQ *ON
      *
      *WRITE A RECORD FOR EVERY DETAIL
      *PROCESS THE DETAIL RECORD
     C                     EXSR $DETL
     C                     ENDIF                           PROCESS=ON
     C                     ENDIF                           *IN99       S
     C                     ENDDO                           END DO
      *
      *WRITE A RECORD FOR THE HEADER
      *
     C           PROCSS    IFEQ *ON
     C                     EXSR $SUMM
     C                     ENDIF
      *
      *EOF PROCESSING
     C                     RETRN
      /EJECT
      ****************************************************************
      * SUBROUTINES
      ****************************************************************
      *---------------------------------------------------------------
      * $NAME- GET THE REMIT TO, BILL TO, AND SHIP TO ADDRESSES
      *---------------------------------------------------------------
     C           $NAME     BEGSR
      *
      ********************************
      *SHIP TO CUSTOMER
      ********************************
     C                     MOVE 'ST '     NNTYPC           SHIP TO
     C                     MOVE *BLANKS   NADD1
     C                     MOVE *BLANKS   NADD2
      *
     C           F4BKC7    CHAINCABBREL1             88
     C           *IN88     IFEQ *OFF
     C                     MOVELBBACNA    NADD1            ADDRESS
      *
     C           BBADNA    IFNE *BLANKS
     C                     MOVELBBADNA    NADD2            ADDRESS
     C                     ENDIF
     C                     ENDIF
      *
     C                     MOVELF4BRTX    NNAME            NAME
      ** W100559 - Change to populate '9 '.
     C**                   MOVEL9         NIDCDQ           ID CODE QUALIFIER
     C                     MOVEL'9 '      NIDCDQ           ID CODE QUALIFIER
     C                     MOVE *BLANKS   NNAME1
     C                     MOVE *BLANKS   NNAME2
     C                     MOVELF4HNNA    NCITY
     C                     MOVE F4DBCD    NSTATE
     C                     MOVELF4CKTX    NZIP
     C                     MOVELDUNSST    NIDCDE           SHIPTO DUNS+SUFFIX
      *
     C                     WRITEECR003
      *
      ********************************
      *BILL TO (A/R CUST)
      ********************************
     C                     MOVEL'BT '     NNTYPC           BILL TO
     C                     MOVE *BLANKS   NADD1
     C                     MOVE *BLANKS   NADD2
      *
     C           F4ANC7    CHAINCABBREL1             86
     C           *IN86     IFEQ *OFF
     C                     MOVELBBACNA    NADD1            ADDRESS
      *
     C           BBADNA    IFNE *BLANKS
     C                     MOVELBBADNA    NADD2            ADDRESS
     C                     ENDIF
     C                     ENDIF
      *
     C                     MOVELF4AQTX    NNAME
      ** W100559 - Change to populate '9 '.
     C**                   MOVEL9         NIDCDQ           ID CODE QUALIFIER
     C                     MOVEL'9 '      NIDCDQ           ID CODE QUALIFIER
     C                     MOVELDUNSBT    NIDCDE           BILL TO DUNS+SUFFIX
     C                     MOVEL*BLANKS   NNAME1
     C                     MOVE *BLANKS   NNAME2
     C                     MOVELF4MONA    NCITY
     C                     MOVELF4RJCD    NSTATE
     C                     MOVELF4GPTX    NZIP
      *
     C                     WRITEECR003
      *
      ********************************
      *GET THE REMIT NAME (OUR BILLING ADDRESS)
      ********************************
      *
     C                     MOVE 'RE '     NNTYPC           REMIT TO
     C                     MOVE *BLANKS   NADD1
     C                     MOVE *BLANKS   NADD2
     C                     MOVE *BLANKS   NNAME
      *
     C                     Z-ADD000       CMPUPC  30
     C           CMPUPC    CHAINCAABREL1             88
     C           *IN88     IFEQ *OFF
     C************         MOVELABADTX    NNAME
      *FEDERAL TAX ID FIELD
     C                     MOVELABA2NA    UPC2
     C                     ENDIF
     C** BEGIN: RMC0413
      *REMIT TO ADDRESS
      ***
      *  GET REMITTANCE ADDRESS FLAG FROM COMPANY/CUSTOMER
     C                     MOVEL*BLANKS   NNAME
     C           KEYEO     CHAINCAEOREL1             88
     C           *IN88     IFEQ *OFF
     C           EOQGST    IFNE ' '
     C           EOQGST    CHAINPOA7REL1             50
     C           *IN50     IFEQ *OFF
     C                     MOVELA7HQT1    NNAME
     C                     MOVELA7PJNA    NADD1            ADDRESS
     C                     MOVELA7PMNA    NCITY
     C                     MOVELA7YNTX    NSTATE
     C                     MOVELA7YOTX    NZIP
     C** BEGIN: TPA071007
     **         REMIT DUNS NUMBER NOW ON A/R REMITTANCE ADDRESS
     **         SO MOVE IT HERE
     C                     MOVE A7KYT1    NIDCDE           REMIT TO DUNS+SUFFIX
     C** END: TPA071007
      *
     C           A7PKNA    IFNE *BLANKS
     C                     MOVELA7PKNA    NADD2            ADDRESS
     C                     ENDIF
     C                     ENDIF
     C                     ENDIF
     C                     ENDIF
     C** END: RMC0413
      *
      *MOVE THE REMIT TO CUSTOMER DUN NUMBER TO FIELD
      ** W100559 - Change to populate '9 '.
     C**                   MOVEL9         NIDCDQ           ID CODE QUALIFIER
     C                     MOVEL'9 '      NIDCDQ           ID CODE QUALIFIER
     C** BEGIN: TPA071007
     **         REMIT DUNS NUMBER MOVED ABOVE SO LINE COMMENTED OUT
     C*                    MOVE NEW0TX    NIDCDE           REMIT TO DUNS+SUFFIX
     C** END: TPA071007
      *
     C                     MOVE *BLANKS   NNAME1
     C                     MOVE *BLANKS   NNAME2
      *
     C                     WRITEECR003
      ********************************
      *SHIP FROM CUSTOMER
      ********************************
      *
     C** BEG: SZ08042010
      *ASSGRC HAS REQUESTED A SHIP FROM RECORD.
      * T13449 - ADDED 'GORDON' TO SHIP FROM.
      * R14392 - REMOVED 'GORDON'.
      * WHD 76099 REMOVED 'ASSGRC' AND ADD 'ASOGBR'
     C           HTPCD     IFEQ 'ASOGBR'
13598C           HTPCD     OREQ 'FRSHMK'
     C                     MOVE 'SF '     NNTYPC           SHIP TO
     C                     MOVE *BLANKS   NADD1
     C                     MOVE *BLANKS   NADD2
      *
      *RTV COMPANY ADDRESS    RT - COMPANY NAME AND ADDRESS  *
     C           PCO       CHAINCAABREL1             88
     C           *IN88     IFEQ *OFF
     C                     MOVELABACNA    NADD1            ADDRESS
      *
     C           BBADNA    IFNE *BLANKS
     C                     MOVELABADNA    NADD2            ADDRESS
     C                     ENDIF
      *
      *RTV ALPHA VALUE        RT - COMPANY VALUES  *
     C           KEYPDK    CHAINPDKMREL1             88
     C           *IN88     IFEQ *OFF
     C                     MOVELKMBXTX    WKDCDE 20
     C                     MOVE PCO       WKCO    4
     C           WKDCDE    CAT  WKCO      WKDCDE
     C                     MOVELWKDCDE    NIDCDE           DUNS NUMBER      X
     C                     ENDIF
      *
     C                     MOVELABADTX    NNAME            NAME
      ** W100559 - Change to populate '9 '.
     C**                   MOVEL9         NIDCDQ           ID CODE QUALIFIER
     C                     MOVEL'9 '      NIDCDQ           ID CODE QUALIFIER
     C                     MOVE *BLANKS   NNAME1
     C                     MOVE *BLANKS   NNAME2
     C                     MOVELABAJNA    NCITY
     C                     MOVE ABBGCD    NSTATE
     C                     MOVELABBTTX    NZIP
      *
     C                     WRITEECR003
     C                     ENDIF
     C                     ENDIF
     C** END: SZ08042010
      *
      ********************************
      *VENDOR
      ********************************
     C** BEG: SZ06242014
      *SYSCO HAS REQUESTED A VENDOR RECORD
     C** BEG: SZ02192015
      *TYSON HAS REQUESTED A VENDOR RECORD
      ** W100559 - Get 'Invoice 810 Crt VN Name' Flag from Trade Partner
      **           Control file so we don't have to hard code the Trading
      *            Partner when creating the 'VN' Vendor record.
      *VENDOR RECORD
      ** W100559 - Commented Out Hard Coding of Trading Partner.
14392C**         HTPCD     IFEQ 'GORDON'
     C**         HTPCD     OREQ 'SYSCO '
14179C**         HTPCD     OREQ 'SHAMRK'
89663C**         HTPCD     OREQ 'GOLUB'
89663C**         HTPCD     OREQ 'KROGER'
      **
      ** W100559 - Check Trade Partner Control File. If
      *            'Invoice 810 Crt VN Name = Y' Then Create 'VN' Record.
     C           HTPCD     CHAINPBB8REL1             91
     C           *IN91     IFEQ *OFF
     C           B8CISC    ANDEQ'Y'                        Inv 810 Crt VN
      *
     C** BEG: SZ02192015
     C                     MOVE 'VN '     NNTYPC           SHIP TO
     C                     MOVE *BLANKS   NADD1
     C                     MOVE *BLANKS   NADD2
      *
      *POPULATE THE COMPANY KEY. IF IT IS 960 OR 360 WANT TO USE 360 TO
      *GET THE 360 ADDRESS AND INFORMATION.
      *
     C           HTPCD     IFEQ 'SYSCO'
     C                     Z-ADD360       KEYCO
     C                     ELSE
     C                     Z-ADDPCO       KEYCO
     C                     ENDIF
      *
      *RTV COMPANY ADDRESS    RT - COMPANY NAME AND ADDRESS  *
     C** BEG: SZ03172016 CHANGED PCO KEY TO KEYCO
     C           KEYCO     CHAINCAABREL1             88
 B2  C           *IN88     IFEQ *OFF
     C                     MOVELABACNA    NADD1            ADDRESS
      *
 B3  C           BBADNA    IFNE *BLANKS
     C                     MOVELABADNA    NADD2            ADDRESS
 E3  C                     ENDIF
      *
      *RTV ALPHA VALUE        RT - COMPANY VALUES  *
     C           KEYPDK    CHAINPDKMREL1             88
 B3  C           *IN88     IFEQ *OFF
     C                     MOVELKMBXTX    WKDCDE 20
     C                     MOVE PCO       WKCO    4
     C           WKDCDE    CAT  WKCO      WKDCDE
     C                     MOVELWKDCDE    NIDCDE           DUNS NUMBER      X
 E3  C                     ENDIF
      *
     C                     MOVELABADTX    NNAME            NAME
      ** W100559 - Change to populate '9 '.
     C**                   MOVEL9         NIDCDQ           ID CODE QUALIFIER
     C                     MOVEL'9 '      NIDCDQ           ID CODE QUALIFIER
     C                     MOVE *BLANKS   NNAME1
     C                     MOVE *BLANKS   NNAME2
     C                     MOVELABAJNA    NCITY
     C                     MOVE ABBGCD    NSTATE
     C                     MOVELABBTTX    NZIP
      *
     C                     WRITEECR003
 E2  C                     ENDIF
 E1  C                     ENDIF
      *SYSCO HAS REQUESTED A BUYING PARTY RECORD
      ********************************
      *BUYER PARTY- DSYSCO
      ********************************
B1   C           HTPCD     IFEQ 'DSYSCO'
B1   C           HTPCD     OREQ 'SYSCO '
     C                     MOVE 'BY '     NNTYPC           SHIP TO
     C                     MOVELSYSADD    NADD1     P
     C                     MOVE *BLANKS   NADD2
     C                     MOVELSYSNM     NNAME     P      NAME
      ** W100559 - UNDO Change to populate '9' instead of '91' for NIDCDQ.
      **           Per Rose, do NOT change.
     C                     MOVE '91'      NIDCDQ           ID CODE QUALIFIER
     C                     MOVE *BLANKS   NNAME1
     C                     MOVE *BLANKS   NNAME2
     C                     MOVEL'HOUSTON' NCITY     P
     C                     MOVE 'TX'      NSTATE
     C                     MOVEL'77077'   NZIP      P
     C                     MOVELSYSDUN    NIDCDE    P      SHIPTO DUNS+SUFFIX
      *
     C                     WRITEECR003
      *
 E1  C                     ENDIF
     C** END: SZ06242014
      *KROGER HAS REQUESTED A BUYING PARTY RECORD     3-11-2022
      ********************************
      *BUYER PARTY- KROGER
      ********************************
BK   C           HTPCD     IFEQ 'KROGER'
     C                     MOVE 'BY '     NNTYPC           SHIP TO
     C                     MOVELKROGAD    NADD1     P
     C                     MOVE *BLANKS   NADD2
     C                     MOVELKROGNM    NNAME     P      NAME
      ** W100559 - UNDO Change to populate '9' instead of '91' for NIDCDQ.
      **           Per Rose, do NOT change.
     C                     MOVE '91'      NIDCDQ           ID CODE QUALIFIER
     C                     MOVE *BLANKS   NNAME1
     C                     MOVE *BLANKS   NNAME2
     C                     MOVELKROGCI    NCITY     P
     C                     MOVE 'OH'      NSTATE
     C                     MOVEL'45202'   NZIP      P
     C                     MOVELKROGDU    NIDCDE    P      SHIPTO DUNS+SUFFIX
      *
     C                     WRITEECR003
      *
 EK  C                     ENDIF
     C                     ENDSR
      *---------------------------------------------------------------
      * $DETL- GET THE DETAIL RECORDS
      *---------------------------------------------------------------
     C           $DETL     BEGSR
     C                     CLEARDUOM
     C                     CLEARDSHUOM
      *
     C                     Z-ADDF4DPNB    DOSEQ1           ORDER SEQ #
     C                     Z-ADDF4UENB    DOSEQ2           ORDER SEC SEQ #
      *
      *POPULATE THE UNIT WGT ORDERED U/M AND THE UNIT PRC ORDERED U/M
     C                     Z-ADDF4AVWG    DIUWGT           UNIT WGT ORDED U/
     C                     Z-ADDF4AUPR    DIUPRO           UNIT PRC ORDED U/
      *
      *SHIPPED INFO
     C                     MOVEL'CA'      DSHUOM           UOM ORDERED
     C                     Z-ADDF4EXQT    DUNSHP           QTY BILLED
      *
      *BILLING INFO
     C                     MOVELF4CRCD    DUOM             UM - BASE PRICE
     C           F4CRCD    IFEQ 'CS'
     C           F4CRCD    OREQ 'HS'
     C           F4CRCD    OREQ 'EA'
     C                     Z-ADDF4EXQT    DQTYIN           QTY BILLED
     C                     ELSE
     C                     Z-ADDF4BYWG    DQTYIN           WGT BILLED
     C                     ENDIF
      *
      *ACCUMULATE QTY SHIPPED
     C                     ADD  F4EXQT    WKQTYI 152       QTY BILLED
      *
      *ACCUMULATE WGT INVOICED
     C                     ADD  F4BYWG    WKWGTI 152       WGT BILLED
      *
      *ACCUMULATE AMT INVOICED
     C                     ADD  F4AYVA    WKAMTI 152       EXTENDED QUANTITY
      *
      *UPC CODE
     C                     MOVE *BLANKS   DUPCNO
      *
     C                     MOVELF4S5NB    KITEM            ITEM CODE
     C           KEYITM    CHAINCABZREL1             83
     C           *IN83     IFEQ *OFF
     C                     Z-ADDF4S5NB    WRKCDE  50       ITEM CODE
     C                     MOVE WRKCDE    UPC3
     C                     MOVELUPCCDE    DUPCNO           UPC CODE
     C                     ELSE
     C                     MOVEL*BLANKS   DUPCNO           UPC CODE
     C                     ENDIF
      *
      *THIS CODE MAY BE OVERRIDDEN IN THE MAPPING
     C                     MOVE 'UA'      DUPCCQ
      *
      *IF SYSCO  RETRIEVE THE GTIN FROM THE FILE.
      *SLM 022515 E003964
      *IF DYSCO OR SYSCO RETRIEVE THE GTIN FROM THE FILE.
      *T13449 - ALWAYS CALL '$GTINR' ROUTINE. REMOVED TRADE PARTNERS.
13449C**         HTPCD     IFEQ 'DSYSCO'
  |  C**         HTPCD     OREQ 'SYSCO '
  |  C**         HTPCD     OREQ 'DLABAT'
  |  C**         HTPCD     OREQ 'IPAP  '
  |  C**         HTPCD     OREQ 'WOODFR'
13449C**         HTPCD     OREQ 'REINHT'
     C                     EXSR $GTINR
13449C**                   ENDIF
      *
      *PURCHASER'S (THEIR) CODE                            RMC0810
     C                     MOVE *BLANKS   DPITNO
     C                     MOVE *BLANKS   DPICDQ
     C                     MOVE *BLANKS   DBUPRC
     C                     MOVE *BLANKS   DISTS2
      *
     C                     MOVELF4BKC7    BIBKC7           SHIP TO
     C                     MOVELF4S5NB    BIHGCD           ITEM CODE
     C           KEYBI     CHAINPOBIREL1             95
     C           *IN95     IFEQ '0'
     C                     MOVELBICOAA    DPITNO
     C                     MOVELBILDSX    DPICDQ
      * CAPTURE THE PAY TYPE FOR SYSCO = PP PER POUND, PE PER EACH
      * WILL BE USED IN THE FOLLOWING CODE.
     C                     MOVELBILESX    DBUPRC
     C                     ENDIF
      *
      * NEW PRICING LOGIC (added to handle Sysco for Dailys E003131)
      *
      * Per Pound (only Sysco will have a value of PP)
      *
     C           DBUPRC    IFEQ 'PP'                       IF PP
      *
     C           F4CRCD    IFEQ 'CW'                       IF 100WGT
     C           F4AKPR    DIV  100       DUPRC     H
     C                     ELSE
      *
     C           F4CRCD    IFEQ 'CS'                       IF CASE
     C           F4AKPR    DIV  F4AVWG    DUPRC     H
     C                     ELSE
      *
     C                     Z-ADDF4AKPR    DUPRC
     C                     ENDIF                           IF CASE
     C                     ENDIF                           IF 100WGT
     C                     MOVE F4AGCD    DISTS2           U/M -ORDERED
     C                     ENDIF                           IF PP
      *
      * Per Each (only Sysco will have a value of PE)
      *
     C           DBUPRC    IFEQ 'PE'                       IF PE
      *
     C           F4CRCD    IFEQ 'CW'                       IF 100WGT
     C           F4AKPR    DIV  100       WKFLD  116
     C           WKFLD     MULT F4AVWG    DUPRC     H
     C                     ELSE
      *
     C           F4CRCD    IFEQ 'CS'                       IF CASE
     C                     Z-ADDF4AKPR    DUPRC
     C                     ELSE
      *
     C           F4AKPR    MULT F4AVWG    DUPRC     H
     C                     ENDIF                           IF CASE
     C                     ENDIF                           IF 100WGT
     C                     MOVE F4AGCD    DISTS2           U/M -ORDERED
     C                     ENDIF                           IF PE
      *
      * Every Trade Partner other than Sysco will fall here
      * W104634-Added 'UM' to Ensure Correct Unit Price is Set from
      *         the Sales History Record.
      *
     C           DBUPRC    IFEQ *BLANK                     IF blank
     C           DBUPRC    OREQ 'UM'
      *         Sales History record.
     C           F4CRCD    IFEQ 'CW'                       IF 100 WGT
     C                     MOVEL'LB'      DBUPRC
     C           F4AKPR    DIV  100       DUPRC     H
     C                     ELSE
     C                     MOVELF4CRCD    DBUPRC
     C                     Z-ADDF4AKPR    DUPRC
     C                     ENDIF                           IF 100 WGT
     C                     ENDIF                           IF blank
      *
      *
      *VENDOR'S (OUR) CODE
     C                     MOVELF4S5NB    DVITNO           ITEM CODE
     C           DVITNO    IFEQ *BLANKS
     C                     MOVE *BLANKS   DVICDQ
     C                     ELSE
     C                     MOVE 'VN'      DVICDQ
     C                     ENDIF
      *
      *F=FREEFORM PRODUCT DESCRIPTION
     C                     MOVE 'F'       DIDTYP
     C                     MOVELF4G7NA    DIDESC           ORDER DESC. LINE ONE
      *
      *SERVICE PROMOTION,ALLOW, CHARGE INFO
      *
     C                     MOVE *BLANKS   DALLCD           NONE
     C                     Z-ADD*ZEROS    DSPSCC
     C                     Z-ADD*ZEROS    DALAMT
     C                     Z-ADD*ZEROS    DALRTE
     C                     MOVE *BLANKS   DTMDDU
     C                     Z-ADD*ZEROS    WKDA   112
     C                     Z-ADD*ZEROS    WKRB   112
     C                     Z-ADD*ZEROS    WKCC   112
      **********
      ** GET THE CATCH WEIGHT STATUS FROM ITEM DEFAULT.
      **********
     C                     MOVELF4S5NB    KITEM            ITEM CODE
     C           KITEM     CHAINOMBYREL1             83
     C           *IN83     IFEQ *OFF
     C                     MOVE BYAVST    DTCTWT
      **********
      *GET PACKAGES PER CASE AND PKG LEVEL SIZE
      **********
      *ADDED THIS FOR HANNAFORD,THEY WANT THE PKG @ CASE ON FIXED WGT
      *THIS WAS ADDED FOR THE 67TH STREET BBQ(SBR) PRODUCTS.
 SZ  C                     Z-ADDBYCJNB    DPACKL           PKG LEVEL SZ
 SZ  C                     Z-ADDBYILNB    DPACK            PKG PER CASE
      *ADDED THIS FOR FOODLION, THEY WANT THE PKG @ CASE ON EVERY PROD
      *IN THE MAP WE WILL CHECK TO SEE IF DPACK IS NOT 0, IF IT IS THEN
      *CHECK DPKSZY.
 SZ  C                     Z-ADDBYBJNB    DPKSZY           BAGS PER UNI
     C                     ENDIF
      *
      ******
      * DLY029 RMC0726 IF ALLOW/REBATES EXIST IN HISTORY DTL ACCRUAL PDICCPP,
      * AND IF INVOICE PRINT TYPE IS "IT"(ITEM),THEN ACCUM HERE
      ******
     C                     SETOF                     73
     C           KEYICC    SETLL@ICCPS0
     C           *IN73     DOWEQ'0'
     C           KEYICC    READE@ICCPS0                  73
     C           *IN73     IFEQ '0'
     C           ICQSSX    IFEQ 'IT'
     C           ICOKCD    IFEQ 'DA'
     C           ICOKCD    OREQ 'PD'
     C                     ADD  ICAYVA    WKDA
     C                     END
     C           ICOKCD    IFEQ 'CC'
     C                     ADD  ICAYVA    WKCC
     C                     END
     C           ICOKCD    IFEQ 'RB'
     C                     ADD  ICAYVA    WKRB
     C                     END
     C                     END
     C                     ENDIF
     C                     ENDDO
      ******
     C                     Z-ADDWKDA      DALAMT           DISC/ALLOW
     C                     ADD  WKRB      DALAMT           REBATE
     C                     ADD  WKCC      DALAMT           CUST CHARGE
     C           WKDA      IFNE 0
     C                     MOVE 'DA'      DTMDDU
     C                     END
     C           WKRB      IFNE 0
     C                     MOVE 'RB'      DTMDDU
     C                     END
     C           WKCC      IFNE 0
     C                     MOVE 'CC'      DTMDDU
     C                     END
      ******
     C           DQTYIN    IFNE *ZEROS                     WGT BILLED>0
     C                     WRITEECR002
      *
      *COUNT THE NUMBER OF LINE ITEMS
     C                     ADD  1         HLITCT
     C                     ENDIF
      *
     C                     ENDSR
      *---------------------------------------------------------------
      * $SUMM- GET THE SUMMARY
      *---------------------------------------------------------------
     C           $SUMM     BEGSR
      *EDI PROCESSED FLAG
     C                     MOVEL'N'       HPFLAG
      *
     C                     MOVELF4ANC7    HVENDR           VENDOR #
     C                     MOVELSFX       HWARHS           WAREHOUSE
      *INVOICE TYPE
     C                     MOVEL'O'       HINVTY           O=ORIGINAL
      *INVOICE DATE & NUMBER
     C                     Z-ADDF4AWDT    HINVDT           INVOICE DATE
     C                     MOVELF4CONB    HINVNO           INVOICE NUMBER
      *PURCHASE ORDER DATE & NUMBER
     C                     Z-ADDF4A0DT    HPODTE           SCHEDULED SHIP DATE
     C                     MOVELF4BCNA    HPONO            CUST PO NUMBER
      *
 SZ   *REQUESTED DELIVERY DATE
 SZ  C                     Z-ADDF4NYDT    HI01DT           REQ DEL DTE
      *************ITD-TERMS OF SALES/DEFERED TERMS OF SALE*************
      *RETRIEVE CUSTOMER TERMS INFORMATION
     C                     MOVE F4AQCD    HTMTCD           TERMS CODE
      *
     C           HTPCD     IFEQ 'DAFS  '
     C                     MOVE '01'      HTMDCD           BASIS DATE CODE
     C                     ELSE
     C                     MOVE ' 1'      HTMDCD           BASIS DATE CODE
     C                     ENDIF
      *
     C                     MOVE F4AQCD    KTERMS           TERMS CODE
     C           KEYTRM    CHAINCAAOREL1             84
     C           *IN84     IFEQ *OFF
     C                     MOVELAOAUNA    HTMDSC           TERMS DESC
     C                     Z-ADDAOA4PC    HTMDPC           TERMS DISCOUNT PCT
     C                     Z-ADDAOANDY    HTMNDY           TERMS NET DAYS
     C                     Z-ADDAOANDY    HTMNDY           TERMS DISCOUNT DAYS
     C                     ELSE
     C                     MOVE *BLANKS   HTMDSC           TERMS DESC
     C                     Z-ADD*ZEROS    HTMDPC           TERMS DISCOUNT PCT
     C                     Z-ADD*ZEROS    HTMNDY           TERMS NET DAYS
     C                     Z-ADD*ZEROS    HTMNDY           TERMS DISCOUNT DAYS
     C                     ENDIF
      *
      *CALCULATE DUE DATE
     C                     Z-ADDF4AIC3    P1AIC3  30       COMPANY NUMBER
     C                     MOVE F4AQCD    P1AQCD  2        TERMS CODE
     C                     Z-ADD*ZEROS    P1ASDT  70       DISCOUNT DUE DATE
     C                     Z-ADD*ZEROS    P1ATDT  70       NET DUE DATE
     C                     Z-ADDF4GNDT    P2A9DT  70       ACTUAL SHIP DATE
     C                     Z-ADDWKAMTI    P3BSVA 112       ORDER VALUE TOTAL
     C                     Z-ADD*ZEROS    P4AWVA 112       DISCOUNT AMOUNT
      *
     C                     CALL 'CAC9XFR'
     C                     PARM           P0RTN   7
     C                     PARM           P1AIC3           COMPANY NUMBER
     C                     PARM           P1AQCD           TERMS CODE
     C                     PARM           P1ASDT           DISCOUNT DUE DATE
     C                     PARM           P1ATDT           NET DUE DATE
     C                     PARM           P2A9DT           ACTUAL SHIP DATE
     C                     PARM           P3BSVA           ORDER VALUE TOTAL
     C                     PARM           P4AWVA           DISCOUNT AMOUNT
      *
     C           P0RTN     IFEQ *BLANKS
     C                     Z-ADDP1ATDT    HTMNDT           NET DUE DATE
     C                     Z-ADDP4AWVA    HTMDAM           DISCOUNT AMOUNT
     C           HTMDPC    IFEQ *ZEROS
     C                     Z-ADD*ZEROS    HTMDDT           DISCOUNT DUE DATE
     C                     ELSE
     C                     Z-ADDP1ASDT    HTMDDT           DISCOUNT DUE DATE
     C                     ENDIF
     C                     ENDIF
      *
      ***************************                                      *
      *SHIPMENT METHOD OF PAYMENT
      ***************************                                      *
     C                     MOVE *BLANKS   TEXT30
     C                     SELEC
     C           F4Q2CD    WHEQ 'PT'                       SHIPPING METHOD
     C           F4Q2CD    OREQ 'PR'                       SHIPPING METHOD
     C                     MOVE 'PP'      HMOPAY           PREPAID
 SZ  C                     MOVE 'M '      HTRTYP
2330 C                     MOVE 'DE'      HLOCCD           LOCATION CODE
     C           F4HNNA    CAT  ',':0     TEXT30 30
     C                     CAT  F4DBCD:1  TEXT30
     C                     MOVE TEXT30    HI05TX           CITY ST DEST
     C                     OTHER
 SZ  C                     MOVE 'CC'      HMOPAY           COLLECT
 SZ  C                     MOVE 'H '      HTRTYP
2330 C                     MOVE 'OR'      HLOCCD           LOCATION CODE
      *
     C           F4AIC3    CHAINCAABREL1             88
     C           *IN88     IFEQ *OFF
     C           ABAJNA    CAT  ',':0     TEXT30           CITY ST ORIGIN
     C                     CAT  ABBGCD:1  TEXT30
     C                     MOVE TEXT30    HI05TX           CITY ST ORIG
     C                     ENDIF
      *
     C                     ENDSL
      *
      ******************************************
      *RETRIEVE THE CARRIER NAME BY CARRIER CODE.
      ******************************************
 SZ  C           F4BZNA    IFNE *BLANKS
     C           F4BZNA    CHAINOMFFREL0             84
     C           *IN84     IFEQ *OFF
     C                     MOVELFFADTX    HROUTE
      *SCAC CODE
     C                     MOVELFFADTX    CNAME
     C                     MOVELSCAC      HSCAC
      *
     C                     ENDIF
     C                     ELSE
     C                     MOVE *BLANKS   HROUTE
SZ   C                     ENDIF
      *
     C                     Z-ADDF4GNDT    HSHPDT           ACTUAL SHIP DATE
      *******************
      *ORDER TOTALS
      *******************
     C                     Z-ADDWKAMTI    HTIAMT           INVOICE AMOUNT
     C                     Z-ADDWKQTYI    HUNTSH           UNITS SHIPPED
     C                     MOVE 'CA'      HUOM1            SHIPPED UOM
     C                     Z-ADDWKWGTI    HWGT             WEIGHT SHIPPED
     C                     MOVE 'LB'      HUOM2            WEIGHT UOM
      *
     C                     Z-ADD*ZEROS    HVOLME           VOLUME
     C                     MOVE '  '      HUOM3            VOLUME UOM
      *
      ******************************************
      *SERVICE PROMOTION,ALLOW, CHARGE INFO
      ******************************************
      *GET THE FREIGHT, THIS IS THE ONLY THING WE WILL HAVE TO PUT
      *IN THIS SECTION.
     C           KEYOMH    SETLLOMCQWKL1
     C           *IN85     DOUEQ*ON
     C           KEYOMH    READEOMCQWKL1                 85
     C           *IN85     IFEQ *OFF
      *
      *FINANCE TYPE = F FOR FREIGHT
     C           N2G4ST    IFEQ 'F'
     C                     ADD  N2BSVA    WKFRT  152       FRT AMT
     C                     ENDIF                           CODE = F
     C                     ENDIF                           *IN85
     C                     ENDDO                           DO 85 = 1
      *
     C           WKFRT     IFEQ *ZEROS
      *NO CHARGES OR ALLOWANCES
     C                     MOVE *BLANKS   HALLCD
     C                     MOVE 'ZZZZ'    HSPSCC
     C*PKD                 Z-ADD*ZEROS    HSPSCC
     C                     Z-ADD*ZEROS    HALAMT
     C                     Z-ADD*ZEROS    HALRTE
     C                     MOVE *BLANKS   HTMDDU
      *
     C                     ELSE
      *CHARGE CODE
     C                     MOVE 'C'       HALLCD           CHARGE
      *FREIGHT CODE
     C                     MOVE 'D240'    HSPSCC
     C                     Z-ADDWKFRT     HALAMT           AMOUNT
     C                     Z-ADD*ZEROS    HALRTE
     C                     MOVE *BLANKS   HTMDDU
     C                     ENDIF
      ******
      * DLY029 RMC0726 IF ALLOW/REBATES/SALES TAX, EXIST IN HISTORY DTL ACCRUAL
      * PDICCPP AND IF INVOICE PRINT TYPE IS "IN" AND INVOICE PRINT
      ****ON INVOICE = YES...
     C                     Z-ADD*ZEROS    WKDA
     C                     Z-ADD*ZEROS    WKRB
     C                     Z-ADD*ZEROS    WKCC
     C                     Z-ADD*ZEROS    WKTX   112
     C                     SETOF                     73
     C           KEYOMH    SETLL@ICCPS0
 BD  C           *IN73     DOWEQ'0'
     C           KEYOMH    READE@ICCPS0                  73
 B0  C           *IN73     IFEQ '0'
 B1  C           ICQSSX    IFEQ 'IN'
     C           ICFLTX    ANDEQ'Y'
      *TOTAL ALL DA FOR SUMMARY
 SZ  C                     ADD  ICAYVA    WKTDA  112
SZ   C********** ICQSSX    OREQ 'IT'
      *
 B2  C           ICOKCD    IFEQ 'DA'
     C           ICOKCD    OREQ 'PD'
     C                     ADD  ICAYVA    WKDA
 E2  C                     END
 B2  C           ICOKCD    IFEQ 'RB'
     C                     ADD  ICAYVA    WKRB
 E2  C                     END
 B2  C           ICOKCD    IFEQ 'CC'
     C                     ADD  ICAYVA    WKCC
 E2  C                     END
 B2  C           ICOKCD    IFEQ 'TX'
     C                     ADD  ICAYVA    WKTX
 E2  C                     END
      *
 E1  C                     END
 E0  C                     ENDIF
ED   C                     ENDDO
     ********
     C                     Z-ADDWKTX      HATXAM           SALES TAX
     C                     Z-ADDWKDA      HALAMT           DISC/ALLOW
     C                     ADD  WKRB      HALAMT           REBATE
     C                     ADD  WKCC      HALAMT           CUST CHARGE
     C           WKDA      IFNE 0
     C                     MOVE 'DA'      HTMDDU
     C                     MOVE 'A'       HALLCD
     C                     END
     C           WKRB      IFNE 0
     C                     MOVE 'RB'      HTMDDU
     C                     END
     C           WKCC      IFNE 0
     C                     MOVE 'CC'      HTMDDU
     C                     END
      *TOTAL ALL DA FOR SUMMARY
 SZ  C                     Z-ADDWKTDA     HI1AMT
      *
      ******************************************
      *BUILD HEADER/DETAIL ALLOWANCE AND CHARGE RECORDS.
      *TP ARE STARTING TO SEND MULTIPLE D/A,SO CREATED FILES (ECP001D AND
      *ECP002D) AND ROUTINE TO CAPTURE THE DIFFERENT RECORDS.
      ******************************************
      *
     C                     CALL 'PBHQXFR'
     C                     PARM           P0RTN   7
     C                     PARM           P1PARM           CO NUMBER
     C                     PARM           P2PARM           ORD NUMBER
     C                     PARM           P3PARM           EDI SURROGATE
     C                     PARM           P4PARM           BILL TO CUSTOMER
     C                     PARM           P5PARM           FILE SET
      *
     C                     MOVELP5PARM    HI01TX           FILE SET
      *
      ******************************************
      *RETRIEVE THE PO HEADER TO GET THE CR & IL VALUES
      * BY PO # AND TRADE PARTNER.  FOR SYSCO
      ******************************************
     C           KEY04     CHAINECP004L8             89
     C           *IN89     IFEQ *OFF                       IF REC READ
     C                     MOVELBQBRU1    HI02TX           CR REF NUMBER
     C                     MOVELBQBSU1    HI03TX           IL INTERNAL ORD #
     C                     ELSE
      *
     C                     MOVEL*BLANKS   HI02TX           CR REF NUMBER
     C                     MOVEL*BLANKS   HI03TX           IL INTERNAL ORD #
     C                     ENDIF                           *IN89 OFF
      *
      *
     C                     SETOF                     89
     C                     Z-ADDF4DPNB    KORDSQ
     C                     Z-ADDF4UENB    KORDLN
     C           KEYSHE    CHAIN@D0CPTQ              89
     C           *IN89     IFEQ *OFF
     C                     MOVELD0LMT1    HI04TX
     C                     ELSE
     C                     MOVE *BLANKS   HI04TX
     C                     ENDIF
      *
     C                     WRITEECR001
      *
     C                     ENDSR
      *---------------------------------------------------------------
      * $GTINR - GET THE GTIN CODE FOR OUR ITEM
      *T13449 DN - REPLACED TRADE PARTNER DLABAT/WOODFR WITH
      *  SYSCO/REINHT.
      *S13598 DN - REMOVED TRADE PARTNER SYSCO/REINHT PROCESSING.
      *---------------------------------------------------------------
     C           $GTINR    BEGSR
     C           KITEM     CHAINPBBTREL1             89
     C           *IN89     IFEQ *OFF                       IF REC READ
13598C**         HTPCD     IFEQ 'SYSCO '
  |  C**         HTPCD     OREQ 'REINHT'
  |  C**                   MOVELBTCUU1    DUPCNO           GTIN CODE   R
  |  C**                   MOVE 'UK'      DUPCCQ
13598C**                   ENDIF                           HTPCD=SYSCO
     C                     MOVELBTCUU1    DITXT2           GTIN CODE
     C                     ENDIF                           *IN89 OFF
     C                     ENDSR
      *---------------------------------------------------------------
      * *INZSR - INITIALIZATION SUBROUTINE
      *---------------------------------------------------------------
      *
     C           *INZSR    BEGSR
      *
     C** BEG: SZ08042010
     C** BEG: SZ03172016  CHANGED THE PCO KEY TO KEYCO
      * KEY FOR FILE PDKMREL1
      *
     C           KEYPDK    KLIST
 SZ  C                     KFLD           KEYCO   30
     C                     KFLD           DUNNBR 10
     C** END: SZ08042010
      *
      * KEY FOR FILE OMHSTPL9
      *
     C           KEYOMH    KLIST
     C                     KFLD           PCO
     C                     KFLD           PORDER
      *
      * KEY FOR FILE PMD0CPL1 - Sales History EXT
      *
     C           KEYSHE    KLIST
     C                     KFLD           PCO
     C                     KFLD           PORDER
     C                     KFLD           KORDSQ  30
     C                     KFLD           KORDLN  30
      *
      * KEY FOR FILE PDNEREL1
      *
     C           KEYEC     KLIST
      * PKP                KFLD           PCO
     C                     KFLD           KCUST
      *
4180  * KEY FOR FILE PBBEREL1
 |    *
 |   C           KEYTPC    KLIST
4180 C                     KFLD           NEJ5NX
      *
      * KEY FOR FILE CAAOREP
      *
     C           KEYTRM    KLIST
     C                     KFLD           PCO
     C                     KFLD           KTERMS  2
      *
      * KEY FOR FILE CABZREP
      *
     C           KEYITM    KLIST
     C                     KFLD           PCO
     C                     KFLD           KITEM   70
      *
      * KEY FOR FILE CAEOREL1
      *
     C           KEYEO     KLIST
     C                     KFLD           F4AIC3
     C                     KFLD           F4BKC7
      *
      * KEY FOR FILE POBIREP
      *
     C           KEYBI     KLIST
     C                     KFLD           BIBKC7
     C                     KFLD           BIHGCD
      *
      * KEY FOR FILE ECP004
      *
     C           KEY04     KLIST
     C                     KFLD           HTPCD
     C                     KFLD           HPONO
      * KEY FOR FILE PDICCPP
      *
     C           KEYICC    KLIST
     C                     KFLD           PCO
     C                     KFLD           PORDER
     C                     KFLD           F4DPNB           SEQ NO
     C                     KFLD           F4UENB           SEC SEQ NO
      *
      * KEY FOR FILE POAVCPP  CUSTOMER COMMENT
      *
     C           KEYAVC    KLIST                           R14392
     C                     KFLD           AVBKC7           SHIP TO
     C                     KFLD           AVI1SX           COMMENT TYPE 'IN'
      *
      * PARM LISTS
      *
     C           *ENTRY    PLIST
     C                     PARM           PCO
     C                     PARM           PORDER
      *
     C           *LIKE     DEFN F4AIC3    PCO
     C           *LIKE     DEFN F4C4NB    PORDER
     C                     MOVELDUNS      DUNNBR
      *
     C                     ENDSR
