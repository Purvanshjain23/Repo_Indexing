     A*%%TS  SD  20140313  162742  ISLRAMS     REL-V6R1M0  5761-WDS
     A*****************************************************************
     A*  ENVIRONMENT:   PORK DIVISION
     A*  SYSTEM:        AS/400
     A*  DISPLAY FILE:  MPD405
     A*  TITLE:         MP2: WORK WITH EXCEPTION VENDORS
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  DATE:          05/31/06
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/MP2REF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A                                      CF05(05 'REFRESH')
     A                                      CF06(06 'CREATE')
     A                                      CF20(20 'WRKSPLF')
     A*
     A*
     A*---------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*---------------------------------------------------------------
     A          R MP405S1                   SFL
     A*%%TS  SD  20140312  084833  ISLRAMS     REL-V6R1M0  5761-WDS
     A  85                                  SFLNXTCHG
     A            HDRRN1         4Y 0H
     A            D1OPT          2A  B  9  2CHGINPDFT(UL)
     A  30                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A N30                                  DSPATR(HI)
     A            D1JDVNNO  R        O  9  8REFFLD(@@JDVNNO)
     A                                      EDTCDE(M)
     A            D1JDVNNM  R        O  9 19REFFLD(@@JDVNNM)
     A            D1ACONO   R        O  9 63REFFLD(@@ACONO)
     A                                      EDTCDE(M)
     A            D1GLACCD  R        O  9 72REFFLD(@@GLACCD)
     A*
     A*---------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*---------------------------------------------------------------
     A          R MP405C1                   SFLCTL(MP405S1)
     A*%%TS  SD  20140313  162657  ISLRAMS     REL-V6R1M0  5761-WDS
     A                                      SFLSIZ(0050)
     A                                      SFLPAG(0012)
     A N82                                  ROLLUP(25 'PAGE FORWARD')
     A                                      SETOF(30)
     A                                      SETOF(51)
     A                                      OVERLAY
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND(*MORE)
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 28'MP2 INTERFACE TO JDE'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USER)
     A                                      DSPATR(HI)
     A                                  2 25'WORK WITH EXCEPTION VENDORS'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USER)
     A                                      DSPATR(HI)
     A                                  4  2'2=Revise'
     A                                  4 12'4=Delete'
     A                                  6  7'JDE Vendor'
     A                                      DSPATR(HI)
     A                                  6 59'Accounting'
     A                                      DSPATR(HI)
     A                                  6 71'GL Account'
     A                                      DSPATR(HI)
     A                                  7  2'Opt'
     A                                      DSPATR(HI)
     A                                  7  9'Number'
     A                                      DSPATR(HI)
     A                                  7 19'Name'
     A                                      DSPATR(HI)
     A                                  7 61'Company'
     A                                      DSPATR(HI)
     A                                  7 74'Code'
     A                                      DSPATR(HI)
     A            C1JDVNNO  R        B  8  8REFFLD(@@JDVNNO)
     A                                      DSPATR(HI)
     A                                      EDTCDE(M)
     A            C1JDVNNM  R        B  8 19REFFLD(@@JDVNNM)
     A                                      CHECK(LC)
     A                                      DSPATR(HI)
     A            C1ACONO   R        B  8 63REFFLD(@@ACONO)
     A                                      DSPATR(HI)
     A                                      EDTCDE(M)
     A            C1GLACCD  R        B  8 72REFFLD(@@GLACCD)
     A                                      DSPATR(HI)
     A  51                                  DSPATR(PC)
     A*
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R MP405K1
     A*%%TS  SD  20140313  162742  ISLRAMS     REL-V6R1M0  5761-WDS
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A                                 23 11'F5=Refresh'
     A                                 23 23'F6=Create'
     A                                 23 34'F20=Spool Files'
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *---------------------------------------------------------------
     A          R MP405ES                   SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE CONTROL FORMAT
      *---------------------------------------------------------------
     A          R MP405EC                   SFLCTL(MP405ES)
      *
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
