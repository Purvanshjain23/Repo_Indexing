      *****************  RPG PROGRAM HEADING  ************************
      * SYSTEM:      HOG PRODUCTION
      * PROGRAM:     HP676
      * TITLE:       ACTUAL MOVEMENT LOG
      * PROGRAMMER:  SHARON ZURN
      * CREATED:     6/08/94
      *
      *  FUNCTION:   THIS PROGRAM PRINTS A REPORT OF ALL ACTUAL
      *              MOVEMENTS THAT MEETS THE USER'S SELECTION
      *              CRITERIA USING WORKFILE HSP370.  ALL SCHEDULED
      *              MOVEMENTS ARE OMITTED.
      *
      *           THIS PROGRAM IS SUBMITTED BY HP476CL WHICH IS CALLED
      *           FROM HP476 THROUGH QCMDEXC.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 09/16/94  MARY PALMER
      *           ADDED CUSTOMER REPORT SELECTION TO SORT AND PRINT
      *           AS DESTINATION OPITON, AND ABILITY TO SELECT RUN
      *           FOR A SPECIFIC CUSTOMER NUMBER.  PRINT RECEIVED
      *           DATE INSTEAD OF SCHEDULED DATE.
      *
      * 10/06/94  LEANNE FEDOR
      *           CHANGED LOGIC FOR BREAKS IN 'CUSTOMER' REPORT.
      *           AS REQUESTED BY ED BROWN, THE 'CUSTOMER' REPORT
      *           WILL BE KEYED BY CUSTOMER AND DATE.
      *
      * 11/07/94  MARY PALMER
      *           ADD AVERAGE SHIPPED WEIGHT TO PRINT ON DETAIL LINE
      *           ADD NEW REPORT CODE OF "NS" FOR NOT FULLY PAID SALE
      *
      * 11/15/94  LEANNE FEDOR
      *           CHANGED LOGIC FOR PRINTING THE 'HOG MOVEMENT CODE'
      *           ON TRANSFERS. BEFORE THIS CHANGE, THE LOGIC PRINTED
      *           THE VALUE FOR THE ORIGIN GROUP WHICH, FOR TRANSFERS,
      *           WAS ALWAYS 'TRANO'. BUT THE LINE ON THE REPORT
      *           REPRESENTED BOTH SIDES OF THE TRANSFER--BOTH TRANI
      *           AND TRANO. SO, THE LOGIC WAS CHANGED TO SIMPLY PRINT
      *           'TRAN' FOR TRANSFERS.
      *
      * 04/25/95  LEANNE FEDOR
      *           CHANGED FARM SITE CODE FROM ALPHA TO NUMERIC.
      *
      * 08/11/95  LEANNE FEDOR
      *           REWRITTEN FOR MOVEMENT REWRITE.
      *
      * 08/31/95  LEANNE FEDOR
      *           MADE BUSINESS OFFICE AN OPTIONAL SELECTION.
      *
      * 03/06/96  LEANNE FEDOR
      *           ADDED TOTALS THAT PRINT AT THE LEVEL BREAK AND A
      *           REPORT TOTAL LINE.
      *
      * 03/19/97  LEANNE FEDOR
      *           A NEW USER SELECTION OF 'UPDATE DATE' WAS ADDED.
      *           THIS DATE CORRESONDS TO THE NEW UPDATE DATE/TIME
      *           STAMP THAT WAS ADDED TO THE MOVEMENT EVENT FILE.
      *
      * 06/08/00  LeAnne Fedor
      *           As requested by Jodi Radel at Julesburg, we added 'cell' as
      *           a selector on the report options.
      *
      * 08/08/02  LeAnne Fedor
      *           Added a new 'KOA head' column. To make room, I changed the
      *           Quality column to be 'Quality and Reject'---and then put
      *           the new KOA column where the Reject column used to be.
      *
      * 05/31/07  LeAnne Ramsey
      *           Some users are having printing problems. So, we are switching
      *           from QPRINT to PRINT198 for the printer file and replacing
      *           John Erikson's customized printer override command in the CL with
      *           the standard override command.
      *
      * 05/22/09  LeAnne Ramsey
      *           Sami Wilson requested that we print Farm Site Numbers instead of
      *           descriptions.
      *
      * 05/23/11  LeAnne Ramsey  (E1556)
      *           Recompile only. Sex Code was added for the Destination Group
      *           in workfile HSP370.
      *
      * 10/16/13  LeAnne Ramsey (E2831)
      *           Recompile only. Added field 'MTech Reference'.
      *
      * 06/07/21  Danny Nguyen   (P2098)
      *           Recompile only due to HSP370 file change.
      *
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     FHSP011    IF   E           K DISK
      *    PRODUCTION PHASE
      *
     FHSP018    IF   E           K DISK
      *    FARM SITE
      *
     FHSP370    IF   E             DISK
      *   MOVEMENT WORKFILE
      *   (RECORDS SELECTED AND KEYED IN OPEN QUERY)
      *
     Fprint198  O    F  198        PRINTER OFLIND(*INOF)
      /EJECT
      ****************************************************************
      * TABLE/ARRAY SPECIFICATIONS
      ****************************************************************
      *
      *---------------------------------------------------------------
      *  STANDARD MESSAGE ARRAYS
      *---------------------------------------------------------------
     D MGID            S              7    DIM(20)                              MSG ID ARRAY
     D MGDT            S             50    DIM(20)                              MSG PARMS
     D MGW             S              1    DIM(50)                              MSG WORK PARMS
      *
      ****************************************************************
      * INPUT SPECS
      ****************************************************************
      *---------------------------------------------------------------
      *  DATE FORMATS
      *---------------------------------------------------------------
      *
      * DATE IN MMDDYY FORMAT
      *
     D                 DS
     D  WKMDY                  1      6  0
     D  WKMM                   1      2  0
     D  WKDD                   3      4  0
     D  WKYY                   5      6  0
      *
      * DATE IN CCYYMMDD FORMAT
      *
     D                 DS
     D  WKCYMD                 1      8  0
     D  WKCEN                  1      2  0
     D  WKYER                  3      4  0
     D  WKMON                  5      6  0
     D  WKDAY                  7      8  0
      *
      *---------------------------------------------------------------
      *  LDA -  WITH THE REQUESTED PRINT SELECTIONS
      *---------------------------------------------------------------
      *
     D                UDS
     D  LDFSBO                 1      5
     D  LDFYMD                 6     13  0
     D  LDTYMD                14     21  0
     D  LDPPCD                22     26
     D  LDFSCD                27     31  0
     D  LDMRCD                39     40
     D  LDHGSN                41     47  0
     D  LDCVNO                48     55  0
      *
     D  LDBODS                56     85
     D  LDFSNM                86    110
     D  LDHGCD               111    117
     D  LDCNAM               118    147
     D  LDMRDS               148    177
     D  LDPPDS               178    207
     D  LDFMDY               208    213  0
     D  LDTMDY               214    219  0
     D  LDUMDY               220    225  0
     D  LDUYMD               226    233  0
      *
     D  LDCELL               234    235  0
      *
      *---------------------------------------------------------------
      * STANDARD PROGRAM STATUS DATA STRUCTURE
      *---------------------------------------------------------------
      *    EXTERNALLY DEFINED AS UTPGFR (RECORD FORMAT: PGMDSR)
     D PGMDS         ESDS                  EXTNAME(UTPGFR)
      *
      *---------------------------------------------------------------
      *  NAMED CONSTANTS
      *---------------------------------------------------------------
      *
     D YES             C                   CONST('Y')
     D NO              C                   CONST('N')
      *
     D                 DS
     D  DUMMYD                 1      1
      *  THE PRECEDING DUMMY DATA STRUCTURE IS ONLY TO KEEP THE I-SPEC
      *  INFO FROM RUNNING OVER INTO THE 'CALC SPECS' ON THE COMPILE
      *  LISTING.  IT SERVES NO OTHER PURPOSE AND CAN BE DELETED
      *  AT ANY TIME!!!
      /EJECT
      ****************************************************************
      * MAIN LINE
      ****************************************************************
      *
     C     *IN90         DOUEQ     *ON                                          MAIN DO
     C                   READ      HSP370                                 90
     C     *IN90         IFEQ      *OFF                                         IF NOT EOF
      *
     C     FIRST         IFEQ      YES
     C                   EXSR      $FIRST
     C                   ENDIF
      *
      * CHECK FOR CONTROL BREAKS:
      *
      * ORIGIN
     C                   SELECT
     C     LDMRCD        WHENEQ    'OR'
     C     WFORPP        IFNE      SVORPP
     C     WFORFS        ORNE      SVORFS
     C                   EXSR      $LBTOT
     C     WFORPP        IFNE      SVORPP                                       IF DIFF PHAS
     C                   EXSR      $TPTOT
     C                   ENDIF                                                  IF DIFF PHAS
     C                   SETON                                        OF
     C                   MOVEL     WFORPP        WKPPCD
     C                   EXSR      $PPDS
     C                   MOVEL     WFORPP        SVORPP
     C                   MOVEL     WFORFS        SVORFS
     C                   ENDIF
      * DESTINATION
      *
     C     LDMRCD        WHENEQ    'DE'
     C     WFDNPP        IFNE      SVDNPP
     C     WFDNFS        ORNE      SVDNFS
     C                   EXSR      $LBTOT
     C     WFDNPP        IFNE      SVDNPP                                       IF DIFF PHAS
     C                   EXSR      $TPTOT
     C                   ENDIF                                                  IF DIFF PHAS
     C                   SETON                                        OF
     C                   MOVEL     WFDNPP        WKPPCD
     C                   EXSR      $PPDS
     C                   MOVEL     WFDNPP        SVDNPP
     C                   MOVEL     WFDNFS        SVDNFS
     C                   ENDIF
      * TRUCKER
     C     LDMRCD        WHENEQ    'TK'
     C     WFTICD        ANDNE     SVTICD
     C                   EXSR      $LBTOT
     C                   SETON                                        OF
     C                   MOVEL     WFTICD        SVTICD
      *
      * RECEIVED DATE
      *
     C     LDMRCD        WHENEQ    'DT'
     C     WFORPP        ANDNE     SVORPP
     C                   EXSR      $LBTOT
     C                   SETON                                        OF
     C                   MOVEL     WFORPP        WKPPCD
     C                   EXSR      $PPDS
     C                   MOVEL     WFORPP        SVORPP
      *
      * CUSTOMER
     C     LDMRCD        WHENEQ    'CU'
     C     WFCVNO        ANDNE     SVCVNO
     C                   EXSR      $LBTOT
     C                   Z-ADD     WFCVNO        SVCVNO
     C                   EXCEPT    BLANK
      *
      * NOT FULLY PAID SALES
      *
     C     LDMRCD        WHENEQ    'NS'
     C     WFCVNO        ANDNE     SVCVNO
     C                   EXSR      $LBTOT
     C                   Z-ADD     WFCVNO        SVCVNO
     C                   EXCEPT    BLANK
     C                   ENDSL
      *
      * FLIP RECEIVE DATE TO MDY FOR PRINTING
      *
     C                   Z-ADD     WFRCDT        WKCYMD
     C                   EXSR      $TOMDY
     C                   Z-ADD     WKMDY         DTRMDY
      *
      * RETRIEVE EITHER THE DESTINATION FARM SITE NAME FOR TRANSFERS
      * OR THE CUSTOMER NAME FOR SALES MOVEMENTS
      *
     C                   SELECT
     C     WFMTCD        WHENEQ    'T'
     C                   SETOFF                                       61
      *
     C     WFMTCD        WHENEQ    'S'
     C                   SETON                                        61
     C                   MOVEL(P)  WFDNNM        DTCVNM
     C                   ENDSL
      *
      * CALCULATE RECEIVED VALUES = QUALITY + REJECT
      *
     C     WFQLHD        ADD       WFRJHD        WKRCHD
     C     WFQLLB        ADD       WFRJLB        WKRCLB
      *
      * CALCULATE AVERAGE RECEIVED POUNDS
      *
     C                   Z-ADD     0             WKARLB
     C     WKRCHD        IFNE      0
     C     WKRCLB        DIV(H)    WKRCHD        WKARLB
     C                   ENDIF
      *
      * CALCULATE AVERAGE SHIPPED POUNDS
      *
     C                   Z-ADD     0             WKASLB
     C     WFSHHD        IFNE      0
     C     WFSHLB        DIV(H)    WFSHHD        WKASLB
     C                   ENDIF
      *
      * Sum quality and reject head into print field
      *
     C     WFQLHD        ADD       WFRJHD        WKQLRJHD
      *
      * ACCUMULATE QUANTITIES FOR LEVEL BREAK TOTALS
      *
     C                   ADD       WFSCHD        LBSCHD
     C                   ADD       WFSHHD        LBSHHD
     C                   ADD       WFSHLB        LBSHLB
     C                   ADD       WKRCHD        LBRCHD
     C                   ADD       WKRCLB        LBRCLB
     C                   ADD       WFDOAHD       LBDOAHD
     C                   ADD       WFKOAHD       LBKOAHD
     C                   ADD       WKQLRJHD      LBQLRJHD
      *
      * PRINT DETAIL
      *
     C     *INOF         IFEQ      *ON                                          IF OVERFLOW
     C                   EXSR      $OFLOW
     C                   ENDIF                                                  IF OVERFLOW
     C                   EXCEPT    DTLINE
      *
     C                   ENDIF                                                  IF NOT EOF
     C                   ENDDO                                                  MAIN DO
      *
      *----------------------------------------------------------------
      * EOF PROCESSING
      *----------------------------------------------------------------
      *  IF NO DETAIL RECORDS MATCHED THE SELECTION CRITERIA, THEN
      *  PRINT THE STANDARD 'NO DATA...' ERROR MESSAGE; OTHERWISE,
      *  PRINT TOTALS
      *
     C     FIRST         IFEQ      YES
     C                   EXCEPT    HRHDR
     C                   EXCEPT    NODATA
     C                   ELSE
      *
      * ALWAYS PRINT TOTALS FOR LAST LEVEL BREAK
      *
     C     *INOF         IFEQ      *ON
     C                   EXSR      $OFLOW
     C                   ENDIF
     C                   EXSR      $LBTOT
      *
      * PRINT PRODUCTION PHASE TOTALS IF THE USER IS GENERATING
      *   1) BY DESTINATION PHASE/FARM SITE, OR
      *   2) BY ORIGIN PHASE/FARMSITE
      *
     C     LDMRCD        IFEQ      'DE'
     C     LDMRCD        OREQ      'OR'
     C     *INOF         IFEQ      *ON
     C                   EXSR      $OFLOW
     C                   ENDIF
     C                   EXSR      $TPTOT
     C                   ENDIF
      *
      * ALWAYS PRINT REPORT TOTALS
      *
     C     *INOF         IFEQ      *ON
     C                   EXSR      $OFLOW
     C                   ENDIF
     C                   EXCEPT    TRTOT
     C                   ENDIF
      *
     C                   SETON                                        LR
      /EJECT
      *----------------------------------------------------------------
      * $OFLOW - LOGIC TO PRINT REPORT HEADINGS ON OVERFLOW
      *----------------------------------------------------------------
      *
      * ALWAYS PRINT THE REPORT HEADINGS
      * PRINT THE PHASE HEADING FOR REPORTS SORTED BY:
      *       PHASE/DATE
      *       PHASE/ORIGIN FARM
      *       PHASE/DESTINATION FARM
      *
      *
     C     $OFLOW        BEGSR
      *
     C                   EXCEPT    HRHDR
      *
     C     LDMRCD        IFEQ      'DT'                                         IF PHASE
     C     LDMRCD        OREQ      'OR'
     C     LDMRCD        OREQ      'DE'
     C                   EXCEPT    HPHDR
     C                   ELSE
     C                   EXCEPT    BLANK
     C                   ENDIF                                                  IF PHASE
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $LBTOT - PRINT TOTAL LINE ON LEVEL BREAK
      *----------------------------------------------------------------
      *
     C     $LBTOT        BEGSR
      *
      * IF THIS IS REPORT IS 1) BY DESTINATION PHASE/FARM SITE OR
      * 2) ORIGIN PHASE/FARM SITE, THEN ACCUMULATE VALUES FOR THE
      * PRODUCTION PHASE TOTAL LINE.
      *
     C     LDMRCD        IFEQ      'DE'
     C     LDMRCD        OREQ      'OR'
     C                   ADD       LBSCHD        TPSCHD
     C                   ADD       LBSHHD        TPSHHD
     C                   ADD       LBSHLB        TPSHLB
     C                   ADD       LBRCHD        TPRCHD
     C                   ADD       LBRCLB        TPRCLB
     C                   ADD       LBDOAHD       TPDOAHD
     C                   ADD       LBKOAHD       TPKOAHD
     C                   ADD       LBQLRJHD      TPQLRJHD
     C                   ENDIF
      *
      * ACCUMULATE LEVEL BREAK TOTALS INTO TOTALS FOR REPORT
      *
     C                   ADD       LBSCHD        TRSCHD
     C                   ADD       LBSHHD        TRSHHD
     C                   ADD       LBSHLB        TRSHLB
     C                   ADD       LBRCHD        TRRCHD
     C                   ADD       LBRCLB        TRRCLB
     C                   ADD       LBDOAHD       TRDOAHD
     C                   ADD       LBKOAHD       TRKOAHD
     C                   ADD       LBQLRJHD      TRQLRJHD
      *
      * PRINT LEVEL BREAK TOTAL LINES
      *
     C     *INOF         IFEQ      *ON                                          IF OVERFLOW
     C                   EXSR      $OFLOW
     C                   ENDIF                                                  IF OVERFLOW
     C                   EXCEPT    LBTOT
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $TPTOT - PRINT PRODUCTION PHASE TOTAL LINES
      *----------------------------------------------------------------
      * NOTE: THIS SUBROUTINE ONLY EXECUTES ON TWO OF THE REPORTS:
      *       1) BY ORIGIN PHASE/FARM SITE
      *       2) BY DESTINATION PHASE/FARM SITE
      *
     C     $TPTOT        BEGSR
      *
     C     *INOF         IFEQ      *ON                                          IF OVERFLOW
     C                   EXSR      $OFLOW
     C                   ENDIF                                                  IF OVERFLOW
     C                   EXCEPT    TPTOT
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $PPDS - RETRIEVE PRODUCTION PHASE DESCRIPTION
      *----------------------------------------------------------------
      *
     C     $PPDS         BEGSR
      *
     C     WKPPCD        CHAIN     HSP011                             92
     C     *IN92         IFEQ      *OFF                                         IF FOUND
     C                   MOVEL(P)  PPPPDS        HPPPDS
     C                   ELSE
     C                   MOVEL(P)  'UNKNOWN'     HPPPDS
     C                   ENDIF                                                  IF FOUND
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $TOMDY - FLIP DATE FROM YYMMDD TO MMDDYY
      *----------------------------------------------------------------
      *
     C     $TOMDY        BEGSR
      *
     C                   Z-ADD     WKMON         WKMM
     C                   Z-ADD     WKDAY         WKDD
     C                   Z-ADD     WKYER         WKYY
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $FIRST - FIRST TIME ROUTINE
      *----------------------------------------------------------------
      *
      * SAVE CONTROL BREAK FIELDS
      * RETRIEVE PRODUCTION PHASE DESCRIPTION
      * PRINT REPORT HEADINGS
      *
     C     $FIRST        BEGSR
      *
     C                   SELECT
     C     LDMRCD        WHENEQ    'OR'                                         ORIGIN
     C                   MOVEL     WFORPP        WKPPCD
     C                   EXSR      $PPDS
     C                   MOVEL     WFORPP        SVORPP
     C                   MOVEL     WFORFS        SVORFS
      *
     C     LDMRCD        WHENEQ    'DE'                                         DESTINATION
     C                   MOVEL     WFDNPP        WKPPCD
     C                   EXSR      $PPDS
     C                   MOVEL     WFDNPP        SVDNPP
     C                   MOVEL     WFDNFS        SVDNFS
      *
     C     LDMRCD        WHENEQ    'TK'                                         TRUCKER
     C                   MOVEL     WFTICD        SVTICD
      *
     C     LDMRCD        WHENEQ    'DT'                                         RECEIVE DATE
     C                   MOVEL     WFORPP        WKPPCD
     C                   EXSR      $PPDS
     C                   MOVEL     WFORPP        SVORPP
      *
     C     LDMRCD        WHENEQ    'CU'                                         CUSTOMER
     C     LDMRCD        OREQ      'NS'                                         NOT PAID
     C                   Z-ADD     WFCVNO        SVCVNO
     C                   ENDSL
      *
     C                   MOVEL     NO            FIRST
     C                   SETON                                        OF
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * *INZSR - INITIALIZATION SUBROUTINE
      *----------------------------------------------------------------
      *
     C     *INZSR        BEGSR
      *
      * DEFINE PRINT FIELDS FOR PRODUCTION PHASE HEADING
      *
     C     *LIKE         DEFINE    PPPPDS        HPPPDS
      *
      * DEFINE PRINT FIELDS FOR DETAIL LINE
      *
     C     *LIKE         DEFINE    LDFMDY        DTRMDY                         RECEIVE DATE
     C                   MOVE      *BLANK        DTCVNM           25            CUSTOMER NAM
      *
      * WORKFIELDS
      *
     C     *LIKE         DEFINE    PPPPCD        WKPPCD
     C     *LIKE         DEFINE    WFQLHD        WKQLRJHD
     C     *LIKE         DEFINE    WFQLHD        WKRCHD
     C     *LIKE         DEFINE    WFQLLB        WKRCLB
     C                   Z-ADD     0             WKASLB            5 2
     C                   Z-ADD     0             WKARLB            5 2
      *
      * CONTROL BREAK FIELDS
      *
     C     *LIKE         DEFINE    WFDNFS        SVDNFS
     C     *LIKE         DEFINE    WFDNPP        SVDNPP
     C     *LIKE         DEFINE    WFORFS        SVORFS
     C     *LIKE         DEFINE    WFORPP        SVORPP
     C     *LIKE         DEFINE    WFTICD        SVTICD
     C     *LIKE         DEFINE    WFCVNO        SVCVNO
      *
      * PRINT FIELDS FOR LEVEL BREAK
      *
     C                   Z-ADD     0             LBSCHD           15 0
     C                   Z-ADD     0             LBSHHD           15 0
     C                   Z-ADD     0             LBSHLB           15 0
     C                   Z-ADD     0             LBRCHD           15 0
     C                   Z-ADD     0             LBRCLB           15 0
     C                   Z-ADD     0             LBDOAHD          15 0
     C                   Z-ADD     0             LBKOAHD          15 0
     C                   Z-ADD     0             LBQLRJHD         15 0
      *
      * PRINT FIELDS FOR PRODUCTION PHASE TOTALS
      *
     C                   Z-ADD     0             TPSCHD           15 0
     C                   Z-ADD     0             TPSHHD           15 0
     C                   Z-ADD     0             TPSHLB           15 0
     C                   Z-ADD     0             TPRCHD           15 0
     C                   Z-ADD     0             TPRCLB           15 0
     C                   Z-ADD     0             TPDOAHD          15 0
     C                   Z-ADD     0             TPKOAHD          15 0
     C                   Z-ADD     0             TPQLRJHD         15 0
      *
      * PRINT FIELDS FOR REPORT TOTAL
      *
     C                   Z-ADD     0             TRSCHD           15 0
     C                   Z-ADD     0             TRSHHD           15 0
     C                   Z-ADD     0             TRSHLB           15 0
     C                   Z-ADD     0             TRRCHD           15 0
     C                   Z-ADD     0             TRRCLB           15 0
     C                   Z-ADD     0             TRDOAHD          15 0
     C                   Z-ADD     0             TRKOAHD          15 0
     C                   Z-ADD     0             TRQLRJHD         15 0
      *
      *----------------------------------------------------------------
      * SET UP REPORT HEADINGS
      *----------------------------------------------------------------
      *
      * ALL BUSINESS OFFICES
      *
     C     LDFSBO        IFEQ      *BLANK
     C                   MOVEL(P)  'ALL'         LDFSBO
     C                   ENDIF
      *
      *
      * ALL FROM/TO DATES
      *
     C     LDFYMD        IFEQ      0
     C                   SETON                                        85
     C                   ENDIF
      *
      *
      * ALL UPDATE DATES
      *
     C     LDUYMD        IFEQ      0
     C                   SETON                                        84
     C                   ENDIF
      *
      * ALL FARMS
     C     LDFSCD        IFEQ      0                                                         B001
     C                   SETON                                        81
     C                   ENDIF
      *
      * ALL CELLS
     C     LDCELL        IFEQ      0                                                         B001
     C                   SETON                                        82
     C                   ENDIF
      *
      * ALL GROUPS
      *
     C     LDHGCD        IFEQ      *BLANK                                                    B001
     C                   MOVEL(P)  'ALL'         LDHGCD
     C                   ENDIF
      *
      * ALL PHASES
      *
     C     LDPPCD        IFEQ      *BLANK
     C                   MOVEL(P)  'ALL'         LDPPCD
     C                   ENDIF
      *
      * ALL CUSTOMERS
      *
     C     LDCVNO        IFEQ      0
     C                   SETON                                        89
     C                   ENDIF
      *
      * SET INDICATORS TO CONTROL PRINTING OF CORRECT COLUMN
      * HEADINGS:
      *
      * REPORT INCLUDES ONLY SALES MOVEMENTS
      *
     C                   SELECT
     C     LDMRCD        WHENEQ    'CU'
     C     LDMRCD        OREQ      'NS'
     C                   SETON                                        87
      *
      * REPORT INCLUDES ONLY TRANSFER MOVEMENTS
      *
     C     LDMRCD        WHENEQ    'DE'
     C                   SETON                                        86
     C                   OTHER
      *
      * REPORT INCLUDES BOTH SALES AND TRANSFER MOVEMENTS
      *
     C                   SETON                                        88
     C                   ENDSL
      *
      * RETRIEVE TIME FOR REPORT HEADING
      * SET UP A FIRST TIME FLAG
      * SET UP A DASHED LINE FOR UNDERLINING THE HEADING
      *
     C                   TIME                    RTIME             6 0
     C                   MOVEL     YES           FIRST             1
     C                   MOVEL     *ALL'-'       DASH            198
     C                   ENDSR
      /EJECT
      *-------------------------------------------------------------
      * REPORT HEADING LINES
      *-------------------------------------------------------------
      *
     Oprint198  E            HRHDR          1 01
     O                       SDPGM               10
     O                                          103 'HOG PRODUCTION SYSTEM'
     O                                          185 'DATE'
     O                       UDATE         Y    198
      *
     O          E            HRHDR          1
     O                       SDUSR               10
     O                                          102 'ACTUAL MOVEMENT LOG'
     O                                          185 'TIME'
     O                       RTIME              198 '  :  :  '
      *
     O          E            HRHDR          1
     O                                          185 'PAGE'
     O                       PAGE          Z    198
      *
     O          E            HRHDR          1
     O                                           22 'BUSINESS OFFICE ......'
     O                       LDFSBO              29
     O                       LDBODS              64
      *
     O          E            HRHDR          1
     O                                           22 'FROM DATE ............'
     O              N85      LDFMDY        Y     32
     O               85                          27 'ALL'
      *
     O          E            HRHDR          1
     O                                           22 'TO DATE ..............'
     O              N85      LDTMDY        Y     32
     O               85                          27 'ALL'
      *
     O          E            HRHDR          1
     O                                           22 'UPDATE DATE ..........'
     O              N84      LDUMDY        Y     32
     O               84                          27 'ALL'
      *
     O          E            HRHDR          1
     O                                           22 'PRODUCTION PHASE .....'
     O                       LDPPCD              29
     O                       LDPPDS              64
      *
     O          E            HRHDR          1
     O                                           22 'CELL .................'
     O              N82      LDCELL        M     27
     O               82                          27 'ALL'
      *
     O          E            HRHDR          1
     O                                           22 'FARM SITE ............'
     O              N81      LDFSCD        M     30
     O               81                          27 'ALL'
     O                       LDFSNM              59
      *
     O          E            HRHDR          1
     O                                           22 'HOG GROUP ............'
     O                       LDHGCD              31
      *
     O          E            HRHDR          1
     O                                           22 'CUSTOMER NUMBER ......'
     O               89                          27 'ALL'
     O              N89      LDCVNO        Z     32
     O                       LDCNAM              65
      *
     O          E            HRHDR          2
     O                                           22 'REPORT ...............'
     O                       LDMRDS              54
      *
      *-------------------------------------------------------------
      * COLUMN HEADING LINES
      *-------------------------------------------------------------
      *
     O          E            HRHDR          1
     O                                          144 'AVERAGE'
     O                                          172 'AVERAGE'
     O                                          198 'REJECT &'
      *
     O          E            HRHDR          1
     O                                            8 'MOVEMENT'
     O                                           17 'RECEIVED'
     O                                           34 '---------------'
     O                                           42 'ORIGIN'
     O                                           59 '---------------'
      *
      * FOR REPORTS THAT HAVE ONLY TRANSFER MOVEMENTS
      *
     O               86                          72 '----------  '
     O               86                          83 'DESTINATION'
     O               86                          95 '  ----------'
      *
      * FOR ALL REPORTS
     O              N87                         100 'MOVE'
     O               87                         101 'SALES'
     O                                          109 'SCHED'
      *
     O                                          128 'SHIPPED'
     O                                          136 'SHIPPED'
     O                                          144 'SHIPPED'
     O                                          154 'RECEIVED'
     O                                          164 'RECEIVED'
     O                                          173 'RECEIVED'
     O                                          178 'KOA'
     O                                          186 'DOA'
     O                                          198 'QUALITY'
     O          E            HRHDR          1
     O                                            6 'NUMBER'
     O                                           15 'DATE'
     O                                           28 'FARM SITE'
     O                                           49 'BLDG/ROOM'
     O                                           57 'GROUP'
      * BOTH SALES AND TRANSFERS
      *
     O               88                          72 '----------  '
     O               88                          83 'DESTINATION'
     O               88                          95 '  ----------'
      * ONLY TRANSFERS
      *
     O               86                          69 'FARM SITE'
     O               86                          84 'BLDG/ROOM'
     O               86                          93 'GROUP'
      * ONLY SALES
     O               87                          65 '---  '
     O               87                          89 'CUSTOMER NAME AND NUMBER'
     O               87                          94 '  ---'
     O                                          100 'TYPE'
     O                                          108 'HEAD'
     O                                          118 'TRUCKER'
      *
     O                                          127 'HEAD'
     O                                          135 'POUNDS'
     O                                          144 'POUNDS '
     O                                          152 'HEAD'
     O                                          162 'POUNDS'
     O                                          171 'POUNDS'
     O                                          178 'HEAD'
     O                                          186 'HEAD'
     O                                          198 'HEAD IN'
      *
     O          E            HRHDR          1
     O                       DASH               198
      *
      *-------------------------------------------------------------
      * PHASE HEADING
      *-------------------------------------------------------------
      *
     O          E            HPHDR          2
     O                                            5 'PHASE'
     O                       HPPPDS              37
      *
      *-------------------------------------------------------------
      * DETAIL LINE
      *-------------------------------------------------------------
      *
     O          E            DTLINE         1
     O                       WFMVSN        Z      7
     O                       DTRMDY              17 '  /  /  '
      * ORIGIN INFO
     O                       WFORFS        z     25
     O                       WFORBL              45
     O                                           46 '/'
     O                       WFORRM              51
     O                       WFORHG              59
      *
      * DESTINATION INFO FOR A TRANSFER
      *
     O              N61      WFDNFS        z     66
     O              N61      WFDNBL              80
     O              N61                          81 '/'
     O              N61      WFDNRM              86
     O              N61      WFDNHG              95
      * FOR A SALE
     O               61      DTCVNM              85
     O               61      WFCVNO        Z     94
      *
     O                       WFCODE             101
     O                       WFSCHD        2    109
     O                       WFTICD             117
      *
     O                       WFSHHD        2    128
     O                       WFSHLB        2    136
     O                       WKASLB        2    144
      *
     O                       WKRCHD        2    153
     O                       WKRCLB        2    163
     O                       WKARLB        2    172
     O                       WFKOAHD       2    178
     O                       WFDOAHD       2    185
     O                       WKQLRJHD      2    198
      *
      *-------------------------------------------------------------
      * TOTAL LINE FOR THE LEVEL BREAK (PRINTS STAGGERED ON 2 LINES)
      *-------------------------------------------------------------
      *
     O          E            LBTOT       1
      *
     O                                           88 'TOTALS:'
     O                       LBSCHD        2B   109
     O                       LBSHLB        2B   136
     O                       LBRCLB        2B   163
     O                       LBDOAHD       2B   185
      *
     O          E            LBTOT       1  1
      *
     O                       LBSHHD        2B   128
     O                       LBRCHD        2B   153
     O                       LBKOAHD       2B   178
     O                       LBQLRJHD      2B   198
      *
      *
      *--------------------------------------------------------------
      * TOTAL LINE FOR PRODUCTION PHASE (PRINTS STAGGERED ON 2 LINES)
      *--------------------------------------------------------------
      * THIS TOTAL LINE IS ONLY VALID FOR TWO OF THE REPORTS:
      *   BY ORIGIN PHASE/FARM SITE
      *   BY DESTINATION PHASE/FARM SITE
      *
      *
     O          E            TPTOT       1
      *
     O                                           88 'PHASE TOTALS:'
     O                       TPSCHD        2B   109
     O                       TPSHLB        2B   136
     O                       TPRCLB        2B   163
     O                       TPDOAHD       2B   185
      *
     O          E            TPTOT       1  1
      *
     O                       TPSHHD        2B   128
     O                       TPRCHD        2B   153
     O                       TPKOAHD       2B   178
     O                       TPQLRJHD      2B   198
      *
      *-------------------------------------------------------------
      * TOTAL LINE FOR THE REPORT
      *-------------------------------------------------------------
      *
     O          E            TRTOT       1
      *
     O                                           88 'REPORT TOTALS:'
     O                       TRSCHD        2B   109
     O                       TRSHLB        2B   136
     O                       TRRCLB        2B   163
     O                       TRDOAHD       2B   185
      *
     O          E            TRTOT       1
      *
     O                       TRSHHD        2B   128
     O                       TRRCHD        2B   153
     O                       TRKOAHD       2B   178
     O                       TRQLRJHD      2B   198
      *
      *
      *-------------------------------------------------------------
      * BLANK LINE
      *-------------------------------------------------------------
     O          E            BLANK       1
      *
      *-------------------------------------------------------------
      * NO DATA MESSAGE LINE
      *-------------------------------------------------------------
     O          E            NODATA         1
     O                                           19 'NO DATA MEETS YOUR'
     O                                           38 'SELECTION CRITERIA'
