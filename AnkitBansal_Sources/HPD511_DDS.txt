     A*%%TS  SD  19940330  082038  ISRCENT     REL-V2R2M0  5738-PW1
     A*****************************************************************
     A*
     A*  SYSTEM:        HOG PRODUCTION
     A*  DISPLAY FILE:  HPD511 - SELECT FEED MILL DELIVERY ZONE
     A*  RPG PROGRAM:   HP511  - SELECT FEED MILL DELIVERY ZONE
     A*  CREATED BY:    ROSE CENTONZE
     A*  DATE:          03/28/94
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      PRINT
     A                                      CA12(12 'PREVIOUS')
     A N82                                  ROLLUP(25 'PAGE FORWARD')
     A*--------------------------------------------------------------*
     A* REFERENCE WINDOW RECORD FORMAT 1
     A*--------------------------------------------------------------*
     A          R HP511R1
     A*%%TS  SD  19940330  082038  ISRCENT     REL-V2R2M0  5738-PW1
     A                                      TEXT('Reference window')
     A                                      WINDOW(*DFT 16 51)
     A                                      WDWBORDER((*DSPATR RI) (*CHAR '    -
     A                                          '))
     A*--------------------------------------------------------------*
     A* RECORD FORMAT ONE
     A*--------------------------------------------------------------*
     A          R HP511R2
     A*%%TS  SD  19940316  134645  ISRCENT     REL-V2R2M0  5738-PW1
     A                                      TEXT('Written in RPG pgm to prevent-
     A                                       screen from clearing')
     A                                      CLRL(*NO)
     A                                      OVERLAY
     A*--------------------------------------------------------------*
     A* RECORD FORMAT TWO
     A*--------------------------------------------------------------*
     A* THIS FORMAT IS NOT WRITTEN IN THE RPG PGM.  IT PREVENTS THE
     A* CALLING_PROGRAM_SCREEN FROM BEING CLEARED.
     A*--------------------------------------------------------------*
     A          R HP511R3
     A*%%TS  SD  19940315  135528  ISRCENT     REL-V2R2M0  5738-PW1
     A                                      TEXT('Dummy format not written in R-
     A                                      PG pgm.')
     A                                      ASSUME
     A                                 11  1' '
     A*--------------------------------------------------------------*
     A* SUBFILE FORMAT ONE
     A*--------------------------------------------------------------*
     A          R HP511S1                   SFL
     A*%%TS  SD  19940330  082038  ISRCENT     REL-V2R2M0  5738-PW1
     A                                      TEXT('subfile record format one')
     A            HDRRN1         4S 0H
     A            DFFMBO    R        O  5  1REFFLD(HSRREF/DZFMBO *LIBL/HSPREF)
     A            DFDZCD    R        O  5  8REFFLD(HSRREF/DZDZCD *LIBL/HSPREF)
     A            DFDZDS    R        O  5 15REFFLD(HSRREF/DZDZDS *LIBL/HSPREF)
     A            DFDZMI    R        O  5 37REFFLD(HSRREF/DZDZMI *LIBL/HSPREF)
     A                                      EDTCDE(Z)
     A            DFDZRT    R        O  5 44REFFLD(HSRREF/DZDZRT *LIBL/HSPREF)
     A                                      EDTCDE(4)
     A*--------------------------------------------------------------*
     A* SUBFILE CONTROL FORMAT ONE
     A*--------------------------------------------------------------*
     A          R HP511C1                   SFLCTL(HP511S1)
     A*%%TS  SD  19940330  082038  ISRCENT     REL-V2R2M0  5738-PW1
     A                                      SFLSIZ(0050)
     A                                      SFLPAG(0009)
     A                                      WINDOW(HP511R1)
     A                                      TEXT('control format one')
     A                                      OVERLAY
     A                                      SFLCSRRRN(&HDRRN)
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND(*MORE)
     A                                      RMVWDW
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            HDRRN          5S 0H
     A                                  1  8'SELECT FEED MILL DELIVERY ZONE'
     A                                      DSPATR(HI)
     A                                  2  1'Position Cursor, Press Enter.'
     A                                  3  1'Feed'
     A                                      DSPATR(HI)
     A                                  3 37'Miles'
     A                                      DSPATR(HI)
     A                                  3 44'Delivery'
     A                                      DSPATR(HI)
     A                                  4 44'Rate'
     A                                      DSPATR(HI)
     A                                  4  1'Mill'
     A                                      DSPATR(HI)
     A                                  4  8'Zone'
     A                                      DSPATR(HI)
     A                                  4 15'Description'
     A                                      DSPATR(HI)
     A                                  4 37'From'
     A                                      DSPATR(HI)
     A          R HP511K1
     A*%%TS  SD  19940328  150330  ISRCENT     REL-V2R2M0  5738-PW1
     A                                      TEXT('command line 1')
     A                                      OVERLAY
     A                                      WINDOW(HP511R1)
     A                                 15  1'F12=Previous'
