     A*%%TS  SD  20030227  083802  ISLRAMS     REL-V5R1M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD4013
     A*  TITLE:         WORK WITH LOAD CREW
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  DATE:          12/23/02
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A                                      CF04(04 'PROMPT')
     A                                      CF05(05 'REFRESH')
     A                                      CF06(06 'CREATE')
     A                                      CF07(07 'LISTING')
     A                                      CF17(17 'PRINT OPTIONS')
     A                                      CF20(20 'WRKSPLF')
     A*
     A*
     A*---------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP4013S1                  SFL
     A*%%TS  SD  20030226  160257  ISLRAMS     REL-V5R1M0  5722-WDS
     A  85                                  SFLNXTCHG
     A            HDRRN1         4Y 0H
     A            D1OPT          2A  B  9  2CHGINPDFT(UL)
     A  30                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A N30                                  DSPATR(HI)
     A            D1LCCD    R        O  9  8REFFLD(HSRREF/@@LCCD *LIBL/HSPREF)
     A            D1LCDS    R        O  9 19REFFLD(HSRREF/@@LCDS *LIBL/HSPREF)
     A            D1TOCD    R        O  9 48REFFLD(HSRREF/@@TOCD *LIBL/HSPREF)
     A            D1SHIFT   R        O  9 59REFFLD(HSRREF/@@SHIFT *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A            D1AIST    R        O  9 71REFFLD(HSRREF/@@AIST *LIBL/HSPREF)
     A*
     A*---------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP4013C1                  SFLCTL(HP4013S1)
     A*%%TS  SD  20030227  083802  ISLRAMS     REL-V5R1M0  5722-WDS
     A                                      SFLSIZ(0050)
     A                                      SFLPAG(0012)
     A N82                                  ROLLUP(25 'PAGE FORWARD')
     A                                      SETOF(30)
     A                                      SETOF(51)
     A                                      SETOF(52)
     A                                      RTNCSRLOC(&RCD &FLD)
     A                                      OVERLAY
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 26'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  2 26'WORK WITH LOAD CREWS'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  4  2'2=Revise'
     A                                  4 12'4=Delete'
     A                                  5 50'?'
     A                                      DSPATR(HI)
     A                                  5 71'?'
     A                                      DSPATR(HI)
     A                                  6  6'Load Crew'
     A                                      DSPATR(HI)
     A                                  6 48'Trans'
     A                                      DSPATR(HI)
     A                                  6 56'Default'
     A                                      DSPATR(HI)
     A                                  6 65'Active/Inactive'
     A                                      DSPATR(HI)
     A                                  7  2'Opt'
     A                                      DSPATR(HI)
     A                                  7  8'Code'
     A                                      DSPATR(HI)
     A                                  7 19'Description'
     A                                      DSPATR(HI)
     A                                  7 48'Office'
     A                                      DSPATR(HI)
     A                                  7 57'Shift'
     A                                      DSPATR(HI)
     A                                  7 70'Status'
     A                                      DSPATR(HI)
     A            C1LCCD    R        B  8  8REFFLD(HSRREF/@@LCCD *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A            C1LCDS    R        B  8 19REFFLD(HSRREF/@@LCDS *LIBL/HSPREF)
     A                                      CHECK(LC)
     A                                      DSPATR(HI)
     A            C1TOCD    R        B  8 48REFFLD(HSRREF/@@TOCD *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A  51                                  DSPATR(PC)
     A            C1SHIFT   R        B  8 59REFFLD(HSRREF/@@SHIFT *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A                                      EDTCDE(K)
     A            C1AIST    R        B  8 71REFFLD(HSRREF/@@AIST *LIBL/HSPREF)
     A  52                                  DSPATR(PC)
     A                                      DSPATR(HI)
     A*
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R HP4013K1
     A*%%TS  SD  20011024  165925  ISLRAMS     REL-V4R4M0  5769-PW1
     A  43                             21  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 22  2'F3=Exit'
     A N99                             22 11'F4=Prompt'
     A                                 22 22'F5=Refresh'
     A                                 22 34'F6=Create'
     A                                 22 45'F7=Listing'
     A                                 22 57'F17=Print Options'
     A                                 23 57'F20=Spool Files'
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *---------------------------------------------------------------
     A          R HP4013ES                  SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE CONTROL FORMAT
      *---------------------------------------------------------------
     A          R HP4013EC                  SFLCTL(HP4013ES)
      *
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
