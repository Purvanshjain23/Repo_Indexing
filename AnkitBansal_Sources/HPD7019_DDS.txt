     A*%%TS  SD  20040713  085027  ISLRAMS     REL-V5R2M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD7019
     A*  TITLE:         VIEW BGF WEEKLY WEAN PROJECTIONS--ALL FARMS/WEEKS
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  DATE:          07/06/04
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A                                      CF04(04 'PROMPT')
     A                                      CF05(05 'REFRESH')
     A*
     A*
     A*---------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP7019S1                  SFL
     A*%%TS  SD  20040713  081812  ISLRAMS     REL-V5R2M0  5722-WDS
     A  85                                  SFLNXTCHG
     A            HDRRN1         4Y 0H
     A            D1PWCDYR  R        O  8  2REFFLD(HSRREF/@@CDYR *LIBL/HSPREF)
     A                                      EDTCDE(Z)
     A            D1PWCDWK  R        O  8  8REFFLD(HSRREF/@@CDWK *LIBL/HSPREF)
     A                                      EDTCDE(Z)
     A            D1PWWEMDY R        O  8 12REFFLD(HSRREF/@@DT06 *LIBL/HSPREF)
     A                                      EDTWRD('  /  /  ')
     A            D1CELL    R        O  8 23REFFLD(FSREC/FSCELL *LIBL/HSP018)
     A                                      EDTCDE(Z)
     A            D1FSCD    R        O  8 26REFFLD(@@FSCD)
     A                                      EDTCDE(Z)
     A            D1TOPWHD  R        O  8 35REFFLD(HSRREF/@@TOPWHD *LIBL/HSPREF)
     A                                      EDTCDE(Z)
     A            D1PWSOHD  R        O  8 42REFFLD(HSRREF/@@PWSOHD *LIBL/HSPREF)
     A                                      EDTCDE(Z)
     A            D1SOCUHD  R        O  8 48REFFLD(HSRREF/@@SOCUHD *LIBL/HSPREF)
     A                                      EDTCDE(Z)
     A            D1BOCUHD  R        O  8 54REFFLD(HSRREF/@@BOCUHD *LIBL/HSPREF)
     A                                      EDTCDE(Z)
     A            D1RPCDYR  R        O  8 61REFFLD(HSRREF/@@CDYR *LIBL/HSPREF)
     A                                      EDTCDE(Z)
     A            D1RPCDWK  R        O  8 67REFFLD(HSRREF/@@CDWK *LIBL/HSPREF)
     A                                      EDTCDE(Z)
     A            D1RPWEMDY R        O  8 71REFFLD(HSRREF/@@DT06 *LIBL/HSPREF)
     A                                      EDTWRD('  /  /  ')
     A*
     A*
     A*---------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP7019C1                  SFLCTL(HP7019S1)
     A*%%TS  SD  20040713  085027  ISLRAMS     REL-V5R2M0  5722-WDS
     A                                      SFLSIZ(0050)
     A                                      SFLPAG(0014)
     A N82                                  ROLLUP(25 'PAGE FORWARD')
     A                                      SETOF(52)
     A                                      RTNCSRLOC(&RCD &FLD)
     A                                      OVERLAY
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 28'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  2 14'VIEW BGF WEEKLY WEAN PROJECTIONS---
     A                                      ALL FARMS/WEEKS'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  4  2'--- Projections ---'
     A                                      DSPATR(HI)
     A                                  4 28'?'
     A                                      DSPATR(HI)
     A                                  4 35'Total'
     A                                      DSPATR(HI)
     A                                  4 61'----- Report ------'
     A                                      DSPATR(HI)
     A                                  5  2'Calendar'
     A                                      DSPATR(HI)
     A                                  5 12'Wk-ending'
     A                                      DSPATR(HI)
     A                                  5 27'Farm'
     A                                      DSPATR(HI)
     A                                  5 33'Piglets'
     A                                      DSPATR(HI)
     A                                  5 41'Weaned'
     A                                      DSPATR(HI)
     A                                  5 50'Sow'
     A                                      DSPATR(HI)
     A                                  5 55'Boar'
     A                                      DSPATR(HI)
     A                                  5 61'Calendar'
     A                                      DSPATR(HI)
     A                                  5 71'Wk-ending'
     A                                      DSPATR(HI)
     A                                  6  2'Year'
     A                                      DSPATR(HI)
     A                                  6  8'Wk'
     A                                      DSPATR(HI)
     A                                  6 14'Date'
     A                                      DSPATR(HI)
     A                                  6 22'Cell'
     A                                      DSPATR(HI)
     A                                  6 27'Site'
     A                                      DSPATR(HI)
     A                                  6 34'Weaned'
     A                                      DSPATR(HI)
     A                                  6 43'Sows'
     A                                      DSPATR(HI)
     A                                  6 48'Culls'
     A                                      DSPATR(HI)
     A                                  6 54'Culls'
     A                                      DSPATR(HI)
     A                                  6 61'Year'
     A                                      DSPATR(HI)
     A                                  6 67'Wk'
     A                                      DSPATR(HI)
     A                                  6 73'Date'
     A                                      DSPATR(HI)
     A            C1PWCDYR  R        B  7  2REFFLD(HSRREF/@@CDYR *LIBL/HSPREF)
     A                                      EDTCDE(Z)
     A                                      DSPATR(HI)
     A            C1PWCDWK  R        B  7  8REFFLD(HSRREF/@@CDWK *LIBL/HSPREF)
     A                                      EDTCDE(Z)
     A                                      DSPATR(HI)
     A            C1PWWEMDY R        B  7 12REFFLD(HSRREF/@@DT06 *LIBL/HSPREF)
     A                                      EDTWRD('  /  /  -')
     A                                      DSPATR(HI)
     A            C1CELL    R        B  7 23REFFLD(HSRREF/@@CELL *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A                                      EDTCDE(Z)
     A            C1FSCD    R        B  7 26REFFLD(@@FSCD)
     A                                      EDTCDE(Z)
     A                                      DSPATR(HI)
     A  52                                  DSPATR(PC)
     A            C1RPCDYR  R        B  7 61REFFLD(HSRREF/@@CDYR *LIBL/HSPREF)
     A                                      EDTCDE(Z)
     A                                      DSPATR(HI)
     A            C1RPCDWK  R        B  7 67REFFLD(HSRREF/@@CDWK *LIBL/HSPREF)
     A                                      EDTCDE(Z)
     A                                      DSPATR(HI)
     A            C1RPWEMDY R        B  7 71REFFLD(HSRREF/@@DT06 *LIBL/HSPREF)
     A                                      EDTWRD('  /  /  -')
     A                                      DSPATR(HI)
     A*
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R HP7019K1
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A                                 23 11'F4=Prompt'
     A                                 23 22'F5=Refresh'
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *---------------------------------------------------------------
     A          R HP7019ES                  SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE CONTROL FORMAT
      *---------------------------------------------------------------
     A          R HP7019EC                  SFLCTL(HP7019ES)
      *
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
