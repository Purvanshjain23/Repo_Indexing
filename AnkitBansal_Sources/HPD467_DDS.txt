     A*%%TS  SD  19950206  091932  ISLRAMS     REL-V3R0M5  5763-PW1
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD467
     A*  TITLE:         SPECIFY EXPENSE TICKET ALLOCATION
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  DATE:          06/02/94
     A*****************************************************************
     A* 03/04/16  Rose Centonze   E5290
     A*           Added company to the screen
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A N99                                  CF04(04 'PROMPT')
     A N99 44                               CF08(08 'SUBMIT')
     A N99                                  CF17(17 'PRINT OPTIONS')
     A N99                                  CF20(20 'WRKSPLF')
     A*
     A*--------------------------------------------------------------
     A* RECORD FORMAT ONE
     A*--------------------------------------------------------------
     A          R HP467R1
     A*%%TS  SD  19950206  091932  ISLRAMS     REL-V3R0M5  5763-PW1
     A                                      SETOF(32)
     A                                      SETOF(33)
     A                                      SETOF(35)
     A                                      OVERLAY
     A                                      RTNCSRLOC(&RCD &FLD)
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 27'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  2 22'SPECIFY EXPENSE TICKET ALLOCATION'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  4  2'Company   .............'
     A            D1COCD    R        B  4 28REFFLD(@@COCD)
     A N35                                  DSPATR(HI)
     A  35                                  DSPATR(PC)
     A  35                                  DSPATR(RI)
     A                                  6  2'Cut-off date ..........'
     A            DFMDY     R     Y  B  6 28REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A N32                                  DSPATR(HI)
     A  32                                  DSPATR(PC)
     A  32                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A                                  8  2'Edit or final .........'
     A                                  8 26'?'
     A            DFEFFL    R        B  8 28REFFLD(@@FLAG)
     A N33                                  DSPATR(HI)
     A  33                                  DSPATR(RI)
     A  33
     AO 52                                  DSPATR(PC)
     A  99                                  DSPATR(PR)
     A            DFEFDS        10A  O  8 32
     A*
     A*--------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*--------------------------------------------------------------
     A          R HP467K1
     A*%%TS  SD  19940419  093319  ISRCENT     REL-V2R2M0  5738-PW1
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A N99                             23 11'F4=Prompt'
     A  44N99                          23 22'F8=Submit'
     A  44N99     DFEFDS        10A  O 23 32
     A N99                             23 44'F17=Print Options'
     A N99                             23 63'F20=Spool Files'
      *
      *--------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *--------------------------------------------------------------
     A          R HP467ES                   SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
     A*
     A*--------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*--------------------------------------------------------------
     A*
     A          R HP467EC                   SFLCTL(HP467ES)
     A*%%TS  SD  19940415  103325  ISRCENT     REL-V2R2M0  5738-PW1
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
