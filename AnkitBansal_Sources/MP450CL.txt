/***************************************************************** */
/*                                                                 */
/*  ENVIRONMENT:     PORK DIVISION                                 */
/*  SYSTEM:          INTERFACE MP2 TO JDE                          */
/*  PROGRAM NUMBER:  MP450CL                                       */
/*  PROGRAM NAME:    MP2: LIST OF PURCHASE ORDERS WITH OPEN ITEMS  */
/*  PROGRAMMER:      LEANNE FEDOR                                  */
/*  CREATE DATE:     04/30/02                                      */
/*                                                                 */
/*  FUNCTION:        THIS CL IS SUBMITTED WITH QCMDEXC FROM        */
/*                   MP450-SPECIFY LIST OF PURCHASE ORDERS WITH    */
/*                   OPEN ITEMS.                                   */
/*                   OPTIONS ARE PASSED THROUGH THE LDA.           */
/*                                                                 */
/*******************************************************************/
/* MODIFICATIONS:                                                  */
/*                                                                 */
/* 03/13/08  LEANNE RAMSEY                                         */
/*           REPLACE QPRINT WITH PRINT132 LOGIC.                   */
/*                                                                 */
/* 03/06/14  LEANNE RAMSEY                                         */
/*           ADDED ACCOUNTING COMPANY AS A REQUIRED SELECTION.     */
/*******************************************************************/

             PGM


             DCL        VAR(&QDATA) TYPE(*CHAR) LEN(65)
             DCL        VAR(&LDACONO) TYPE(*DEC) LEN(3)
             DCL        VAR(&LDJDVNNO) TYPE(*DEC) LEN(8)
             DCL        VAR(&HOLD) TYPE(*CHAR) LEN(10)
             DCL        VAR(&COPYS) TYPE(*DEC) LEN(2)
             DCL        VAR(&OUTQ) TYPE(*CHAR) LEN(10)

             CHGVAR     VAR(&LDJDVNNO) VALUE(%SST(*LDA 1 8))
             CHGVAR     VAR(&LDACONO)  VALUE(%SST(*LDA 49 3))

/*------------------------------------------------------------------*/
/* SET OVERRIDES FOR PRINT FILES                                    */
/*------------------------------------------------------------------*/

 HOLD:       IF         COND(%SST(*LDA 411 1) = 'Y') THEN(CHGVAR +
                          VAR(&HOLD) VALUE(*YES))
             ELSE       CMD(CHGVAR VAR(&HOLD) VALUE(*NO))

 COPYS:      CHGVAR     VAR(&COPYS) VALUE(%SST(*LDA 412 1))
             IF         COND(&COPYS = 0) THEN(CHGVAR VAR(&COPYS) +
                          VALUE(1))

 OUTQ:       CHGVAR     VAR(&OUTQ) VALUE(%SST(*LDA 413 10))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(%SST(*LDA 401 10)))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(*JOB))

/*------------------------------------------------------------------*/
/* OVERRIDE PRINT FILE                                              */
/*------------------------------------------------------------------*/

             OVRPRTF    FILE(PRINT132) OUTQ(&OUTQ) COPIES(&COPYS) +
                          HOLD(&HOLD)

/*------------------------------------------------------------------*/
/* Extract data from PO header                                      */
/*------------------------------------------------------------------*/

             CHGVAR     VAR(&QDATA) VALUE(' ')

/* Accounting Company                                               */
             CHGVAR     VAR(%SST(&QDATA 1 11)) VALUE('PHACONO *EQ')
             CHGVAR     VAR(%SST(&QDATA 13 3)) VALUE(&LDACONO)

/* JDE Vendor                                                       */
             IF         COND(&LDJDVNNO *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 17 17)) VALUE('*AND PHJDVNNO +
                          *EQ')
             CHGVAR     VAR(%SST(&QDATA 35 8)) VALUE(&LDJDVNNO)
             ENDDO

             OVRDBF     FILE(MPL102C) SHARE(*YES)
             OPNQRYF    FILE((MPL102C)) QRYSLT(&QDATA) KEYFLD(*FILE) +
                          SEQONLY(*YES)

/*------------------------------------------------------------------*/
/* CALL PRINT PROGRAM                                               */
/*------------------------------------------------------------------*/

             CALL       PGM(MP650)

             CLOF       OPNID(MPL102C)
             DLTOVR     FILE(MPL102C)

             ENDPGM

