/***************************************************************** */
/*                                                                 */
/*  PROGRAM NUMBER:  HP3081CL                                      */
/*  PROGRAM NAME:    NEGATIVE INVENTORY EXCEPTION REPORT           */
/*  PROGRAMMER:      LEANNE FEDOR                                  */
/*  CREATE DATE:     05/19/04                                      */
/*                                                                 */
/*  FUNCTION:        THIS CL IS SUBMITTED WITH QCMDEXC FROM        */
/*                     HP471-SPECIFY PRELIMINARY EOP EDITS         */
/*                     HP477-SPECIFY PERIOD CLOSE                  */
/*                                                                 */
/*                   OPTIONS ARE PASSED THROUGH THE LDA.           */
/*                                                                 */
/*******************************************************************/
/*  MODIFIED:                                                      */
/*                                                                 */
/* 02/29/16    Rose Centonze    E5290                              */
/*             Added company code parm and add to opnqry select    */
/*******************************************************************/

             PGM  PARM(&PPCOCD)

             DCL        VAR(&PPCOCD) TYPE(*CHAR) LEN(3)            /* E5290 */

/* DEFINE THE VARIABLE REQUIRED IN THE COMMAND TO OVERRIDE THE     */
/* PRINT FILE TO A LASER PRINTER                                   */

             DCL        VAR(&CMD) TYPE(*CHAR) LEN(512)
             DCL        VAR(&QDATA) TYPE(*CHAR) LEN(160) /* E5290-WAS 140 */

             DCL        VAR(&LDFCYMD) TYPE(*DEC) LEN(8)
             DCL        VAR(&LDTCYMD) TYPE(*DEC) LEN(8)

             DCL        VAR(&HOLD) TYPE(*CHAR) LEN(10)
             DCL        VAR(&SAVE) TYPE(*CHAR) LEN(4)
             DCL        VAR(&COPYS) TYPE(*DEC) LEN(2)
             DCL        VAR(&OUTQ) TYPE(*CHAR) LEN(10)

             CHGVAR     VAR(&LDFCYMD) VALUE(%SST(*LDA 1 8))
             CHGVAR     VAR(&LDTCYMD) VALUE(%SST(*LDA 9 8))


/*-------------------------------------------------------------------*/
/*  SET UP AND RUN OPEN QUERY OVER HOG GROUP                         */
/*-------------------------------------------------------------------*/

             CHGVAR     VAR(&QDATA) VALUE(' ')

/*  ALWAYS OMIT HOG GROUPS WITH A STATUS OF VOIDED               */

             CHGVAR     VAR(%SST(&QDATA 1 15)) VALUE('HGGSCD +
                          *NE "VD"')

/*  ALWAYS OMIT BGF HOG GROUPS                                   */

             CHGVAR     VAR(%SST(&QDATA 17 23)) VALUE('*AND HGPPCD +
                          *NE "BGF  "')

/*  ALWAYS SELECT ONLY GROUPS WITH AN OPEN DATE:                       */
/*    1) GREATER THAN ZERO                                             */
/*    2) ON/BEFORE THE 'TO' DATE                                       */

             CHGVAR     VAR(%SST(&QDATA 41 15)) VALUE('*AND HGOPDT +
                          *GT')
             CHGVAR     VAR(%SST(&QDATA 57 8)) VALUE(00000000)
             CHGVAR     VAR(%SST(&QDATA 66 15)) VALUE('*AND HGOPDT +
                          *LE')
             CHGVAR     VAR(%SST(&QDATA 82 8)) VALUE(&LDTCYMD)


/*  ALWAYS SELECT ONLY GROUPS WITH A DISPOSED DATE:                    */
/*    1) EQUAL TO ZERO OR                                              */
/*    2) ON/AFTER THE 'FROM' DATE                                      */

             CHGVAR     VAR(%SST(&QDATA 91 16)) VALUE('*AND (HGDSDT +
                          *EQ')
             CHGVAR     VAR(%SST(&QDATA 108 8)) VALUE(00000000)
             CHGVAR     VAR(%SST(&QDATA 117 14)) VALUE('*OR HGDSDT +
                          *GE')
             CHGVAR     VAR(%SST(&QDATA 132 8)) VALUE(&LDFCYMD)
             CHGVAR     VAR(%SST(&QDATA 140 1)) VALUE(')')

/*   SELECT COMPANY IN PARM          E5290                     */

             CHGVAR     VAR(%SST(&QDATA 142 15)) VALUE('*AND HGCOCD +
                          *EQ')
             CHGVAR     VAR(%SST(&QDATA 158 3)) VALUE(&PPCOCD)


             OVRDBF     FILE(HSL034M) SHARE(*YES)
             OPNQRYF    FILE((HSL034M)) QRYSLT(&QDATA) +
                          KEYFLD((HGPPCD) (HGFSCD) (HGHGCD)) +
                          SEQONLY(*YES)


/*-------------------------------------------------------------------*/
/* SET UP PRINT OVERRIDES                                            */
/*-------------------------------------------------------------------*/

 HOLD:       IF         COND(%SST(*LDA 411 1) = 'Y') THEN(CHGVAR +
                          VAR(&HOLD) VALUE(*YES))
             ELSE       CMD(CHGVAR VAR(&HOLD) VALUE(*NO))

 COPYS:      CHGVAR     VAR(&COPYS) VALUE(%SST(*LDA 412 1))
             IF         COND(&COPYS = 0) THEN(CHGVAR VAR(&COPYS) +
                          VALUE(1))

OUTQ:        CHGVAR     VAR(&OUTQ) VALUE(%SST(*LDA 413 10))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(%SST(*LDA 401 10)))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(*JOB))


/* AS OF MARCH 2004, USERS ASKED US TO ALWAYS PUT OUTPUT ON SAVE/HOLD */

             CHGVAR     VAR(&SAVE) VALUE(*YES)
             CHGVAR     VAR(&HOLD) VALUE(*YES)


/*------------------------------------------------------------------*/
/* OVERRIDE PRINT FILE                                              */
/*------------------------------------------------------------------*/

/* USE THE CUSTOMIZED SEABOARD COMMAND TO OVERRIDE THE PRINT FILE.  */
/* THIS COMMAND ALLOWS THE REPORT TO BE GENERATED ON A STANDARD LINE*/
/* PRINTER OR A LASER PRINTER. TO USE THIS OVERRIDE YOU MUST DECLARE*/
/* A COMMAND VARIABLE AND FOLLOW THE OVERRIDE WITH A CALL TO        */
/* QCMDEXC.                                                         */

             SBDPRTOVR  CMDVAR(&CMD) FILE(QPRINT) OUTQ(&OUTQ) +
                          WIDTH(198) LANDSCAPE(*PRTF) DRAWER(*PRTF) +
                          COPIES(&COPYS) HOLD(&HOLD) SAVE(&SAVE)

             CALL       PGM(QCMDEXC) PARM(&CMD 512)


/*-------------------------------------------------------------------*/
/* PRINT THE REPORT                                                  */
/*-------------------------------------------------------------------*/

             CALL       PGM(HP6081)

             CLOF       OPNID(HSL034M)
             DLTOVR     FILE(*ALL)

             ENDPGM

