      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Hog Production System
      * PROGRAM:     HP654
      * TITLE:       Ration Report
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     08/14/01
      *
      * FUNCTION:  Batch program to print summarized feed ticket data. A single
      *            line is printed for each ration within feed mill.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 12/09/02  LeAnne Fedor
      *           Removed 'production phase' from the Feed Ticket Detail file. So, the
      *           query selection in the CL would no longer work.  We added a check here
      *           to determine whether to include the record.
      *
      * 10/08/02  LeAnne Fedor
      *           Added 'additive' column to report. We now print a line for each
      *           unique ration/additive.
      *
      * 04/20/05  LeAnne Fedor
      *           Added logic for new user selections:
      *               Company relationship
      *               Feed delivery zone
      *               Produced flag
      *
      * 05/31/07  LeAnne Ramsey
      *           Some users are having printing problems. So, we are switching
      *           from QPRINT to PRINT132 for the printer file and replacing
      *           John Erikson's customized printer override command in the CL with
      *           the standard override command.
      *
      * 04/23/09  LeAnne Ramsey
      *           Recompile only. Costed Time/User added to Feed Ticket Header.
      *
      * 05/12/09  LeAnne Ramsey
      *           Recompile only. PickUp Ticket Type added to Feed Ticket Header.
      *
      * 07/06/09  LeAnne Ramsey
      *           Recompile only. Added new field 'Continuous Flow Flag' to Hog Group file.
      *
      * 09/21/09  LeAnne Ramsey
      *           Recompile only. Added new field 'Upload Date' to Feed Ticket Header.
      *
      * 08/18/10  LeAnne Ramsey   (E936)
      *           Per Debbie Deen. Added 2 new calculated columns:
      *             1) $'s per Ton
      *             2) Cost per LB
      *
      * 10/16/13  LeAnne Ramsey (E2831)
      *           Recompile only. Added field 'MTech Reference'.
      *
      * 06/22/20  Danny Nguyen - P405 - Farm Number Increase
      *           Increased Bin Code (@@BNCD) in HSPREF file from 5A to 6A.
      *           Recompile only.
      *
      * 01/21/21  ISE            TSN593
      *           Replaced *loval with *start
      *
      * 05/10/22 Eric L SDN736 Recompiled ticket nbr increase (TKNO & RTNO 7.0 TO 9.0)
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fhsp018    if   e           k disk
      *    Farm site
      *
      *
     Fhsl034d   if   e           k disk
      *    Hog group
      *
      *
     Fhsp037    if   e           k disk
      *   Feed ticket header
      *
      *
     Fhsp038    if   e           k disk
      *   Feed ticket detail (records selected/keyed in open query)
      *
      *
     Fprint132  o    f  132        printer oflind(*inof)
      *
      /EJECT
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      ****************************************************************
      * TABLE AND ARRAY SPECIFICATIONS
      ****************************************************************
      *
      *  Print arrays for totals
      *
     D arfdlb          s             12  0 DIM(3)
     D arinam          s             13  2 DIM(3)
      *
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Standard fields
      *
     D dash            s            100
     d rtime           s              6  0
      *
      * Control fields
      *
     D first           s              1    inz('Y')
     D procfl          s              1    inz('Y')
      *
      *
      * Save fields
      *
     D svfmbo          s                   like(fdfmbo)
     D svrncd          s                   like(fdrncd)
     D svadcd          s                   like(fdadcd)
      *
      *
      * Print fields
      *
     D h1crcd          s                   like(fscrcd)
     D h1fmbo          s                   like(fdfmbo)
     D h1dzcd          s                   like(fsdzcd)
     D h1ppcd          s                   like(hgppcd)
     D h1prfl          s              3
     D h1rncd          s                   like(fdrncd)
     D h1hgcd          s                   like(hghgcd)
     D h1blcd          s                   like(hgblcd)
     D h1rmcd          s                   like(hgrmcd)
     D h1trds          s             10
      *
     D t1desc          s             16
     D t1tonrt         s                   like(fdinam)
     D t1lbrt          s              5  4
      *
      * Array index
      *
     D x               s              1  0
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *---------------------------------------------------------------
      * Local data area.
      *---------------------------------------------------------------
      *
     Dlda             uds                  dtaara(*lda)
     D  ldfmbo                 1      5
     D  ldfymd                 6     13  0
     D  ldtymd                14     21  0
     D  ldppcd                27     31
     D  ldfscd                32     36  0
     D  ldrncd                37     41
     D  ldhgsn                42     48  0
     D  ldtrcd                49     49
     D  ldbods                50     79
     D  ldppds                90    119
     D  ldfsnm               120    144
     D  ldrnds               145    164
     D  ldfmdy               165    170  0
     D  ldtmdy               171    176  0
      *
     D  ldcell               179    180  0
     D  ldtrds               181    190
      *
     D  ldprfl               191    191
     D  ldcrcd               192    196
     D  ldcrds               197    206
     D  lddzcd               207    211
     D  lddzds               212    231
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * MAINLINE
      ****************************************************************
      *
      * Prepare to read the file and print headings the first time thru.
      *
593  C*    *loval        setll     hsp038
593  C     *start        setll     hsp038
     C                   exsr      $h1hdr
      *
      * Read all records in the file that were selected by the
      * open query. Summarize them to the feed mill/ration level and print.
      *
     C                   dou       *in90 = *on                                  Main do
     C                   read      hsp038                                 90
     C                   if        *in90 = *off                                 If not EOF
      *
      * Check for matches on User Selections.
      *
     C                   move      yes           procfl
     C                   exsr      $matches
      *
      * If selections matched, continue processing.
      *
     C                   if        procfl = yes                                 If process
      *
     C                   if        first = yes
     C                   movel     fdfmbo        svfmbo
     C                   movel     fdrncd        svrncd
     C                   movel     fdadcd        svadcd
     C                   move      no            first
     C                   endif
      *
      * Check for control breaks
      *
     C                   select
     C                   when      svfmbo <> fdfmbo
     C                   exsr      $totrncd
     C                   exsr      $totfmbo
      *
     C                   when      svrncd <> fdrncd or
     C                             svadcd <> fdadcd
     C                   exsr      $totrncd
     C                   endsl
      *
      * Detail processing
      *
     C                   add       fdfdlb        arfdlb
     C                   add       fdinam        arinam
      *
     C                   endif                                                  If process
     C                   endif                                                  If not EOF
     C                   enddo                                                  Main do
      *
      *---------------------------------------------------------------
      * EOF processing
      *---------------------------------------------------------------
      * If the file was empty, print the standard 'no data...'
      * error message
      *
     C                   if        first = yes
     C                   except    nodata
     C                   else
     C                   exsr      $totrncd
     C                   exsr      $totfmbo
     C                   exsr      $totrpt
     C                   endif
      *
     C                   seton                                        lr
      /EJECT
      *--------------------------------------------------------------------------------------
      * Check for matches on User Selections
      *--------------------------------------------------------------------------------------
      *
     C     $matches      begsr
      *
      * Check Production Phase
      *
     C                   if        ldppcd <> *blank
     C                   exsr      $ppcd
     C                   endif
      *
      *
      * If OK so far, check for the Farm Site for Company Relationship and/or Delivery Zone
      *
     C                   if        procfl = yes and
     C                             (ldcrcd <> *blank or lddzcd <> *blank)
     C                   exsr      $fscd
     C                   endif
      *
      *
      * If OK so far, check for "Produced Flag" value in the Ticket Header.
      *
     C                   if        procfl = yes and
     C                             ldprfl <> *blank
     C                   exsr      $prfl
     C                   endif
      *
     C                   endsr
      /EJECT
      *--------------------------------------------------------------------------------------
      * Check for selection matches on Production Phase
      *--------------------------------------------------------------------------------------
      *
     C     $ppcd         begsr
      *
      * Retrieve 'production phase' associated with a) group or b) farm
      *
     C                   select
     C                   when      fdhgsn <> 0
     C     fdhgsn        chain     hsl034d                            92
     C                   if        *in92 = *on or
     C                             hgppcd <> ldppcd
     C                   move      no            procfl
     C                   endif
     C                   other
      *
     C     fdfscd        chain     hsp018                             92
     C                   if        *in92 = *on or
     C                             fsppcd <> ldppcd
     C                   move      no            procfl
     C                   endif
     C                   endsl
      *
     C                   endsr
      /EJECT
      *--------------------------------------------------------------------------------------
      * Make selection matches based on Farm Site
      *--------------------------------------------------------------------------------------
      *  Check for selection matches on:
      *     1) Company relationship
      *     2) Delivery zone
      *
     C     $fscd         begsr
      *
     C     fdfscd        chain     hsp018                             92
     C                   if        *in92 = *on or
     C                             (ldcrcd <> *blank and fscrcd <> ldcrcd) or
     C                             (lddzcd <> *blank and fsdzcd <> lddzcd)
     C                   move      no            procfl
     C                   endif
      *
     C                   endsr
      /EJECT
      *--------------------------------------------------------------------------------------
      * Check the Produced Flag in the Feed Ticket Header for a match on user's selection
      *--------------------------------------------------------------------------------------
      *
     C     $prfl         begsr
      *
     C     key01         chain     hsp037                             92
     C                   if        *in92 = *on or
     C                             (ldprfl <> *blank and fhprfl <> ldprfl)
     C                   move      no            procfl
     C                   endif
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Print ration total line
      *---------------------------------------------------------------
      *
     C     $totrncd      begsr
      *
     C                   z-add     1             x
      *
      * Calc Cost/Lb and $/ton:
      *
     C                   if        arfdlb(x) <> 0
     C                   eval(h)   t1lbrt  = arinam(x) / arfdlb(x)
     C                   eval(h)   t1tonrt = t1lbrt * 2000
     C                   endif
      *
     C                   if        *inof = *on
     C                   exsr      $h1hdr
     C                   endif
      *
     C                   except    t1tot
      *
     C                   move      fdrncd        svrncd
     C                   move      fdadcd        svadcd
     C                   setoff                                       97
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Print feed mill total line
      *---------------------------------------------------------------
      *
     C     $totfmbo      begsr
      *
     C                   if        *inof = *on
     C                   exsr      $h1hdr
     C                   endif
      *
      *
     C     'Total for '  cat       svfmbo        t1desc
      *
     C                   z-add     2             x
     C                   seton                                        98
     C                   except    t1tot
     C                   setoff                                       98
      *
     C                   move      fdfmbo        svfmbo
     C                   seton                                        97
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Print report total line
      *---------------------------------------------------------------
      *
     C     $totrpt       begsr
      *
     C                   if        *inof = *on
     C                   exsr      $h1hdr
     C                   endif
      *
     C                   eval      t1desc = 'Total for Report'
     C                   z-add     3             x
     C                   setoff                                       97
      *
     C                   seton                                        98
     C                   except    t1tot
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Print headings
      *---------------------------------------------------------------
      *
     C     $h1hdr        begsr
      *
     C                   seton                                        97
     C                   except    h1hdr
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Key lists
      *
     C     key01         klist
     C                   kfld                    fdfmbo
     C                   kfld                    fdtkno
     C                   kfld                    fdtrcd
      *
      *
      * Set up a line of underscores and retrieve time for report heading.
      *
     C                   movel     *all'-'       dash
     C                   time                    rtime
      *
      *
      * Set up report headings
      *
      * Feed mill business office
      *
     C                   if        ldfmbo = *blank
     C                   movel     'All'         h1fmbo
     C                   else
     C                   movel     ldfmbo        h1fmbo
     C                   endif
      *
      * Feed delivery zone
     C                   if        lddzcd = *blank
     C                   movel     'All'         h1dzcd
     C                   else
     C                   movel     lddzcd        h1dzcd
     C                   endif
      *
      * Production phase
     C                   if        ldppcd = *blank
     C                   movel     'All'         h1ppcd
     C                   else
     C                   movel     ldppcd        h1ppcd
     C                   endif
      *
      *
      * Company relationship
      *
     C                   if        ldcrcd = *blank
     C                   movel     'All'         h1crcd
     C                   else
     C                   movel     ldcrcd        h1crcd
     C                   endif
      *
      *  Cell
     C                   if        ldcell = 0
     C                   seton                                        82
     C                   endif
      *
      *  Farm site
     C                   if        ldfscd = 0
     C                   seton                                        81
     C                   endif
      *
      *
      * Transaction code
     C                   if        ldtrcd = *blank
     C                   movel     'All'         h1trds
     C                   else
     C                   movel     ldtrds        h1trds
     C                   endif
      *
      * Ration
     C                   if        ldrncd = *blank
     C                   movel     'All'         h1rncd
     C                   else
     C                   movel     ldrncd        h1rncd
     C                   endif
      *
      * Produced flag
     C                   select
     C                   when      ldprfl = *blank
     C                   movel     'All'         h1prfl
      *
     C                   when      ldprfl = yes
     C                   movel     'Yes'         h1prfl
      *
     C                   when      ldprfl = no
     C                   movel     'No '         h1prfl
     C                   endsl
      *
      * Hog group
     C                   if        ldhgsn = 0
     C                   movel     'All'         h1hgcd
     C                   else
     C     ldhgsn        chain     hsl034d                            92
     C                   if        *in92 = *off
     C                   movel     hghgcd        h1hgcd
     C                   movel     hgblcd        h1blcd
     C                   movel     hgrmcd        h1rmcd
     C                   seton                                        96
     C                   else
     C                   movel     'Unknown'     h1hgcd
     C                   endif
     C                   endif
      *
     C                   endsr
      /EJECT
      *-------------------------------------------------------------
      * Report heading lines
      *-------------------------------------------------------------
      *
     Oprint132  e            h1hdr          1 01
     O                       sdpgm               12
     O                                           64 'HOG PRODUCTION SYSTEM'
     O                                           92 'DATE'
     O                       udate         y    102
      *
     O          e            h1hdr          1
     O                       sdusr               12
     O                                           60 'RATION REPORT'
     O                                           92 'TIME'
     O                       rtime              102 '  :  :  '
      *
     O          e            h1hdr          2
     O                                           92 'PAGE'
     O                       page          z    102
      *
      *
     O          e            h1hdr          1
     O                                           24 'From date ............'
     O                                           28 '...:'
     O                       ldfmdy        Y     37
      *
     O          e            h1hdr          1
     O                                           24 'To date ..............'
     O                                           28 '...:'
     O                       ldtmdy        Y     37
      *
     O          e            h1hdr          1
     O                                           24 'Feed mill business off'
     O                                           28 'ice:'
     O                       h1fmbo              35
     O                       ldbods              68
     O          e            h1hdr          1
     O                                           24 'Production phase .....'
     O                                           28 '...:'
     O                       h1ppcd              35
     O                       ldppds              68
      *
     O          e            h1hdr          1
     O                                           24 'Cell .................'
     O                                           28 '...:'
     O              N82      ldcell        m     36
     O               82                          33 'ALL'
      *
      *
     O          e            h1hdr          1
     O                                           24 'Farm site ............'
     O                                           28 '...:'
     O              N81      ldfscd        M     36
     O               81                          33 'ALL'
     O              N81      ldfsnm              63
      *
     O          e            h1hdr          1
     O                                           25 'Company relationship .'
     O                                           28 '...:'
     O                       h1crcd              35
     O                       ldcrds              48
      *
     O          e            h1hdr          1
     O                                           24 'Feed delivery zone ...'
     O                                           28 '...:'
     O                       h1dzcd              35
     O                       lddzds              58
      *
      *
     O          e            h1hdr          1
     O                                           24 'Ration code ..........'
     O                                           28 '...:'
     O                       h1rncd              35
      *
      *
     O          e            h1hdr          1
     O                                           24 'Produced flag ........'
     O                                           28 '...:'
     O                       h1prfl              33
      *
      *
     O          e            h1hdr          1
     O                                           25 'Hog group/building/room'
     O                                           28 '.:'
     O                       h1hgcd              37
     O               96                          39 '/'
     O                       h1blcd              45
     O               96                          47 '/'
     O                       h1rmcd              53
      *
      *
     O          e            h1hdr          2
     O                                           24 'Transaction code .....'
     O                                           28 '...:'
     O                       h1trds              40
      *
      *-------------------------------------------------------------
      * Column headings
      *-------------------------------------------------------------
      *
     O          e            h1hdr          1
     O                                           11 'FEED MILL'
     O                                           28 'RATION'
     O                                           38 'ADDITIVE'
     O                                           57 'FEED'
     O                                           77 'INGREDIENT'
     O                                          101 'COST'
     O          e            h1hdr          1
     O                                           17 'BUSINESS OFFICE'
     O                                           27 'CODE'
     O                                           36 'CODE'
     O                                           57 'POUNDS'
     O                                           77 'AMOUNT'
     O                                           93 '$ PER TON'
     O                                          101 'PER LB'
      *
     O          e            h1hdr          1
     O                       dash               102
      *
      *-------------------------------------------------------------
      * Total line
      *-------------------------------------------------------------
      *
     O          e            t1tot       1  1
     O               97      svfmbo               7
     O               98      t1desc              27
     O              n98      svrncd              28
     O              n98      svadcd              37
     O                       arfdlb(x)     kb    58
     O                       arinam(x)     kb    78
     O                       t1tonrt       kb    92
     O                       t1lbrt        kb   102
      *
      *-------------------------------------------------------------
      * Blank line
      *-------------------------------------------------------------
      *
     O          e            blank       1
      *
      *-------------------------------------------------------------
      * No data message line
      *-------------------------------------------------------------
      *
     O          e            nodata      1
     O                                           21 'NO DATA MEETS YOUR'
     O                                           40 'SELECTION CRITERIA'
