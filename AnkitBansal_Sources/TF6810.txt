     h option(*SRCSTMT:*NODEBUGIO)
      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Triumph Foods
      * PROGRAM:     TF6810
      * TITLE:       Yields: ByProduct Weekly Yield Report (Summary) STF
      * PROGRAMMER:  Danny Nguyen
      * PROJECT:     DO2484 - STF Variance Reporting
      * CREATED:     02/10/22
      *
      * FUNCTION:  Batch program to print the report at the summarized level.
      *
      *             ***This program was clone from TF681 and will print for STF 440 data only***
      *                It will be called in TF477CL right after call of TF681.
      *                Deleted all codes that pertain to Guymon & Triumph Foods.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER / COMMENTS
      * --------  ----------------------------------------------------
      * 02/10/22  Danny Nguyen  (DO2484 - WI479 STF Variance Reporting)
      *           DBFC on TFP076 file. Added the following fields:
      *             YWXPULB   - STF PRODUCED LBS
      *             YWXPUSLB  - STF PRODUCED START WEIGHT
      *             YWXSTDPLB - STF STANDARD PRODUCTION LBS
      *           Added the 3 STF fields to report. Report is specifically for Company
      *           440 STF only.
      *           TFP076 file will now include calc of STF Produced Lbs w/ Yield %.
      *
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fome8rel0  if   e           k disk
      *    OM Product type
      *
      *
     Fomfarel1  if   e           k disk
      *    OM Product class
      *
      *
     Fpmaprel1  if   e           k disk
      *    TF Meat cost group
      *
      *
     Ftfl076a   if   e           k disk
      *  Weekly yields (records selected with open query)
      *
      *
     Fprint198  o    f  198        printer oflind(*inof)
      *
      /EJECT
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Standard fields
      *
     D dash            s            198    inz(*all'-')
     d rtime           s              6  0
      *
      * Control fields
      *
     D first           s              1    inz('Y')
     D newisty         s              1    inz('Y')
     d svtfclcd        s                   like(ywtfclcd)
     d svsumlev        s                   like(ywsumlev)
     d cnt             s              7  0
      *
      *
      * Workfields
      *
     D wkalph          s              3
     D wklb            s                   like(ywsstdplb)
     d wkpc            s             15  6
     D wktfcgcd        s                   like(ywtfcgcd) inz('BP')
      *
      *
      * Parms
      *
     D xxorigin        s              1
      *
      *
      * Print fields
      *
     d h1demand        s             35
     d h2desc          s             34
      *
      *
     d l1desc          s             58
2484 d l1xpulb         s                   like(ywxpulb)
      *
     d t1desc          s             74
2484 d t1xdiffpc       s                   like(ywcalcypc)
      *
      *
      * Array index
      *
     d x               s              1  0
      *
      ****************************************************************
      * ARRAYS AND TABLES
      ****************************************************************
      *
2484 D arxpulb         s             11  2 dim(3)
  |  D arxstdplb       s             11  2 dim(3)
2484 D arxdifflb       s             11  2 dim(3)
      *
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *---------------------------------------------------------------
      * Local data area.
      *---------------------------------------------------------------
      *
     Dlda             uds                  dtaara(*lda)
     D  ldpfcd                 1      1
     D  ldyr                   2      5  0
     D  ldwk                   6      7  0
      *
     D  ldwedt                29     36  0
     D  ldwemdy               44     49  0
      *
     D  ldtfcgcd             139    140
     D  ldtfcgds             141    155
      *
     D  ldtfclcd             156    158
     D  ldtfclds             159    188
      *
     D  ldistycd             189    191  0
     D  ldistyds             192    221
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ***************************************************************************************
      * MAINLINE
      ***************************************************************************************
      *
      * Print headings
      *
     C                   exsr      $h1hdr
      *
      * We drive this by reading the master file of Item Structure Types
      *
     C     *loval        setll     ome8rel0
      *
     C                   dou       *in90 = *on                                  Main do
     C                   read      ome8rel0                               90
     C                   if        *in90 = *off and                             If not eof
     C                             (ldistycd = 0 or ldistycd = e8rgnb)
      *
     C                   z-add     0             cnt
     C                   move      yes           newisty
      *
     C                   move      e8rgnb        wkalph
     C                   eval      h2desc = %trim(wkalph) + '-'
     C                                      + %trim(e8dctx)
      *
      * Print BPs
     C                   exsr      $rtv076
      *
      * Print Item Structure Type total
      *
     C                   exsr      $t1tot
      *
     C                   endif                                                  If not eof
     C                   enddo                                                  Main do
      *
      * EOF processing
     C                   if        first = yes
     C                   except    nodata
     C                   else
     C                   exsr      $t2tot
     C                   endif
      *
     C                   seton                                        lr
      /EJECT
      *---------------------------------------------------------------
      * Retrieve Weekly Yield Records
      *---------------------------------------------------------------
      *
     C     $rtv076       begsr
      *
      * Retrieve all BP Weekly Yield records for this:
      *  1) Item Structure Type
      *  2) TF Class Group = 'BP'
      *  Exclude the following (even though they are ByProducts, they are printed on the
      *  PRIMAL reports:
      *    1) Picnic Cushions
      *    2) Kill Offal
      *
     C     key01         setll     tfl076a
      *
     C                   dou       *in91 = *on                                  Do loop
     C     key01         reade     tfl076a                                91
     C                   if        *in91 = *off                                 If not EOF
      *
      * Exclude Picnic Cushions, Kill Offal, Render
      *
     C                   if        (ywistycd = 600 and ywisgrcd = 630 and       If exclude
     C                             ywtfclcd = 'BP5') or
     C                             (ywistycd = 740 and ywtfclcd = 'OFF' and
     C                              ywtfcgcd = 'BP') or
     C                             (ywistycd = 750 and ywtfcgcd = 'BP')
     C                   else
      * Break logic
     C                   select
     C                   when      newisty = yes
     C                   move      no            newisty
     C                   exsr      $h2hdr
     C                   exsr      $save
      *
     C                   when      ywsumlev <> svsumlev
     C                   exsr      $l1dtl
     C                   exsr      $save
     C                   endsl
      *
      * Detail processing
      *
      * Seaboard Triumph.
     C                   add       ywxpulb       arxpulb
     C                   add       ywxstdplb     arxstdplb
     C     ywxpulb       sub       ywxstdplb     wklb
     C                   add       wklb          arxdifflb
      *
     C                   endif                                                  If exclude
     C                   endif                                                  If not EOF
     C                   enddo                                                  Do loop
      *
      * Print last summarized record
      *
     C                   if        newisty = no
     C                   exsr      $l1dtl
     C                   endif
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Save control fields and setup new "description"
      *---------------------------------------------------------------
      *
     C     $save         begsr
      *
      * Save control fields
     C                   move      ywtfclcd      svtfclcd
     C                   move      ywsumlev      svsumlev
      *
      * The 'descriptions' on the report aren't really making much sense. So, we keep
      * trying to come up with some combination of fields that are "descriptive".
      *
      * Currently, the "descpription" will be one of these:
      *  1) Summary Level
      *  2) Meat Cost Group Description + Summary Level
      *  3) Item Structure Class Description + Summary Level
      *
      * Default to Summary Level
      *
     C                   eval      l1desc = %trim(svsumlev)
      *
      * Then, override to the Meat Cost Group Code Description
      *
     C     ywcgcd        chain     pmaprel1                           92
     C                   if        *in92 = *off                                 If hit
      *
     C                   eval      l1desc = %trim(aprnna) + ': ' +
     C                             %trim(svsumlev)
      *
      * Then, override again to the Item Structure Class Description when the
      * Meat Cost Group Summary Level is 'CL'
      *
     C                   if        apeetx = 'CL'                                If cl
     C     ywisclcd      chain     omfarel1                           92
     C                   if        *in92 = *off
     C                   eval      l1desc = %trim(fadetx) + ': ' +
     C                             %trim(svsumlev)
     C                   endif
     C                   endif                                                  If cl
     C                   endif                                                  If hit
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Print detail line
      *---------------------------------------------------------------
      *
     C     $l1dtl        begsr
      *
     C                   if        *inof = *on
     C                   exsr      $h1hdr
     C                   exsr      $h2hdr
     C                   endif
      *
     C                   z-add     1             x
     C                   exsr      $calc
      *
     C                   except    l1dtl
     C                   move      no            first
     C                   exsr      $clear
      *
      * To control printing of subtotals, count:
      *
     C                   add       1             cnt
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Print total line for Item Structure Type
      *---------------------------------------------------------------
      *
     C     $t1tot        begsr
      *
     C                   z-add     2             x
      *
     C                   if        cnt > 0                                      If something
     C                   exsr      $calc
      *
     C                   evalr     t1desc = 'Total for ' + %trim(h2desc) + ':'
      *
     C                   if        *inof = *on
     C                   exsr      $h1hdr
     C                   endif
     C                   except    t1tot
     C                   endif                                                  If something
      *
      * Clear/initialize
     C                   exsr      $clear
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Print Report Total
      *---------------------------------------------------------------
      *
     C     $t2tot        begsr
      *
     C                   seton                                        89
     C                   z-add     3             x
      *
     C                   evalr     t1desc = 'Report Total:'
     C                   exsr      $calc
      *
     C                   if        *inof = *on
     C                   exsr      $h1hdr
     C                   else
     C                   except    blank
     C                   endif
      *
     C                   except    t1tot
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Calcs for Total Line
      *---------------------------------------------------------------
      *
     C     $calc         begsr
      *
      * Calc the Difference in Pounds as a Percent of Standard Production Pounds
      *
      *         Seaboard Triumph
2484 C                   if        arxstdplb(x) <> 0
  |  C                   eval      wkpc = (arxdifflb(x) / arxstdplb(x)) * 100
  |  C                   if        wkpc < 999
  |  C                   z-add(h)  wkpc          t1xdiffpc
  |  C                   endif
2484 C                   endif
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Clear arrays
      *---------------------------------------------------------------
      *
     C     $clear        begsr
      *
2484 C                   z-add     0             arxstdplb(x)
  |  C                   z-add     0             arxpulb  (x)
2484 C                   z-add     0             arxdifflb(x)
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Print report heading
      *---------------------------------------------------------------
      *
     C     $h1hdr        begsr
      *
     C                   except    h1hdr
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Item Structure Header
      *---------------------------------------------------------------
      *
     C     $h2hdr        begsr
      *
     C                   if        *inof = *on
     C                   exsr      $h1hdr
     C                   endif
      *
     C                   except    h2hdr
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Parm lists
      *
     C     *entry        plist
     C                   parm                    xxorigin
      *
      * Key Lists
      *
     C     key01         klist
     C                   kfld                    e8rgnb
     C                   kfld                    wktfcgcd
      *
      * Retrieve time for report heading.
      *
     C                   time                    rtime
      *
      * This report is generated:
      *  1) From Inventory Stock Closing
      *  2) As a Preliminary/Final from Margin Adjustment Close
      *  3) On-demand
      *
     C                   select
     C                   when      xxorigin = 'N'
     C                   eval      %subst(h1demand:4:28) =
     C                             'From Inventory Stock Closing'
      *
     C                   when      ldpfcd = 'D'
     C                   eval      %subst(h1demand:13:9) = 'On-Demand'
      *
     C                   when      ldpfcd = 'F'
     C                   eval      %subst(h1demand:4:29) =
     C                             'Final Margin Adjustment Close'
      *
     C                   when      ldpfcd = 'P'
     C                   eval      %subst(h1demand:1:35) =
     C                             'Preliminary Margin Adjustment Close'
     C                   endsl
      *
      *
      * Set up Report Headings
      *
     C                   if        ldistycd = 0
     C                   seton                                        95
     C                   endif
      *
     C                   if        ldtfcgcd = *blank
     C                   seton                                        94
     C                   endif
      *
     C                   if        ldtfclcd = *blank
     C                   seton                                        93
     C                   endif
      *
     C                   endsr
      /EJECT
      *-------------------------------------------------------------------------------------
      * Report heading lines
      *-------------------------------------------------------------------------------------
      *
     Oprint198  e            h1hdr          1 01
     O                       sdpgm               10
     O                                           95 'BYPRODUCT WEEKLY YIELD '
     O                                          109 'REPORT-SUMMARY'
2484 O                                          125 'FOR COMPANY STF'
     O                                          188 'DATE'
     O                       udate         y    198
      *
     O          e            h1hdr          1
     O                       sdusr               10
     O                       h1demand           111
     O                                          188 'TIME'
     O                       rtime              198 '  :  :  '
      *
     O          e            h1hdr          1
     O                                          188 'PAGE'
     O                       page          z    198
      *
      *
     O          e            h1hdr          1
     O                                           17 'Week-ending date:'
     O                       ldwemdy             26 '  /  /  '
      *
      *
     O                                           60 'TF Class group code:'
     O               94                          65 'All'
     O              n94      ldtfcgcd            64
     O              n94      ldtfcgds            82
      *
     O          e            h1hdr          1
     O                                           17 'Year/Week:'
     O                       ldyr          m     23
     O                       ldwk          m     27
      *
     O                                           60 'TF Classification code:'
     O               93                          65 'All'
     O              n93      ldtfclcd            65
     O              n93      ldtfclds            97
      *
     O          e            h1hdr          2
     O                                           60 'Item structure type code:'
     O               95                          65 'All'
     O              n95      ldistycd            65
     O              n95      ldistyds            97
      *
      *-------------------------------------------------------------------------------------
      * Column headings
      *-------------------------------------------------------------------------------------
      *
     O          e            h1hdr       1  1
     O                                            9 'TF '
      *
2484 O                                           89 'STF Std'
  |  O                                          105 'STF'
  |  O                                          121 'STF Produced'
2484 O                                          132 'STF Diff'
      *
      *
     O          e            h1hdr       0  1
     O                                            2 'IS'
     O                                            9 'Cls'
     O                                           13 'TF'
      *
     O                                           89 'Production'
     O                                          105 'Produced'
     O                                          121 'Minus Std'
     O                                          132 'as % of'
      *
     O          e            h1hdr       0  1
     O                                            4 'Type'
     O                                            9 'Grp'
     O                                           14 'Cls'
     O                                           27 'Description'
      *
     O                                           89 'Pounds'
     O                                          105 'Pounds'
     O                                          121 'Prod Pounds'
     O                                          132 'Std Lbs'
      *
     O          e            h1hdr          0
     O                       dash               198
      *
      *-------------------------------------------------------------------------------------
      * Heading line for Item Structure type
      *-------------------------------------------------------------------------------------
      *
     O          e            h2hdr       2  0
     O                       h2desc              34
      *
     O          e            h2hdr       0  0
     O                       h2desc              34
      *
      *-------------------------------------------------------------------------------------
      * Detail line
      *-------------------------------------------------------------------------------------
      *
     O          e            l1dtl       1
     O                       wktfcgcd             9
     O                       svtfclcd            14
     O                       l1desc              74
      *
2484 O                       arxstdplb(x)  k     90
  |  O                       arxpulb  (x)  k    106
  |  O                       arxdifflb(x)  k    122
2484 O                       t1xdiffpc     kb   133
      *
      *
      *-------------------------------------------------------------------------------------
      * Total Line for all Levels
      *-------------------------------------------------------------------------------------
      *
     O          e    89      t1tot          1
      *
     O          e   n89      t1tot       1  1
     O                                           89 '--------------'
     O                                          105 '--------------'
     O                                          121 '--------------'
     O                                          132 '--------'
      *
     O          e            t1tot       0  0
     O                       t1desc              74
2484 O                       arxstdplb(x)  k     90
  |  O                       arxpulb  (x)  k    106
  |  O                       arxdifflb(x)  k    122
2484 O                       t1xdiffpc     k    133
      *
     O          e            t1tot          1
2484 O                       arxstdplb(x)  k     90
  |  O                       arxpulb  (x)  k    106
  |  O                       arxdifflb(x)  k    122
2484 O                       t1xdiffpc     kb   133
      *
      *-------------------------------------------------------------
      * Blank
      *-------------------------------------------------------------
      *
     O          e            blank       1
      *
      *-------------------------------------------------------------
      * No data message line
      *-------------------------------------------------------------
      *
     O          e            nodata      1
     O                                           19 'No data meets your'
     O                                           39 'selection criteria.'
