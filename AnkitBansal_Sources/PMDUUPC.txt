/*********************************************************************/
/*                                                                   */
/*  SYNOPSIS -  BUILD WORK FILE IN QTEMP AND LOGICALS                */
/*              ADD RECORDS TO THE TEMP FILE                         */
/*                                                                   */
/*  SYSTEM    - TRAFFIC SYSTEM                                       */
/*  PROGRAM   - PRINT EXCESSIVE FREIGHT CHARGES REPORT               */
/*              THIS PROGRAM IS SUBMITTED FROM PMDSUPC - A CLP       */
/*  DATE      - 01/31/2007                                           */
/*                                                                   */
/*      PARAMETERS:                                                  */
/*                                                                   */
/*     O    &RETURN     - RETURN CODE                                */
/*     I    &APPLIC     - APPLICATION CODE                           */
/*     I    &COMPNYN    - COMPANY NUMBER (PASSED)                    */
/*     I    &FRDATE     - FROM DATE    (PASSED)                      */
/*     I    &TODATE     - TO DATE      (PASSED)                      */
/*     B    &COPYN      - NO. OF COPIES (PASSED)                     */
/*     I    &DESC       - DESCRIPTION (FOR PROMPT SCREEN)            */
/*     B    &OUTQ       - OUTPUT QUEUE                               */
/*     B    &HOLD       - HOLD STATUS                                */
/*     B    &SAVE       - SAVE STATUS                                */
/*     B    &NQSTAT     - INDICATOR WHETHER MUST RUN IN NIGHT Q      */
/*     B    &NIGHTQ     - NIGHT JOB QUEUE                            */
/*     I    &FRTCODE    - FREIGHT CODE                               */
/*     I    &EMAIL      - EMAIL ADDRESS                              */
/*     I    &EMAILFMT   - EMAIL FORMAT                               */
/*     I    &SLSPRSN    - SALESPERSON CODE                           */
/*     I    &CARRIER    - CARRIER CODE                               */
/*     I    &SHIPTON    - SHIP TO CUSTOMER NUMBER (15.0)             */
/*     I    &WHSE1      - WAREHOUSE 1                               */
/*     I    &WHSE2      - WAREHOUSE 2                               */
/*     I    &WHSE3      - WAREHOUSE 3                               */
/*     I    &WHSE4      - WAREHOUSE 4                               */
/*     I    &WHSE5      - WAREHOUSE 5                               */
/*     I    &WHSE6      - WAREHOUSE 6                               */
/*     I    &WHSE7      - WAREHOUSE 7                               */
/*     I    &WHSE8      - WAREHOUSE 8                               */
/*     I    &WHSE9      - WAREHOUSE 9                               */
/*     I    &WHSE10     - WAREHOUSE 10                              */
/*     I    &SOFLAG     - SELECT/OMIT                               */
/*     I    &RPTTYP     - REPORT TYPE                                */
/*********************************************************************/
/*                        MODIFICATIONS                              */
/*-------------------------------------------------------------------*/
/* LJB 11/12/2007 FT381   ADD SHIP TO CUSTOMER NUMBER AS AN          */
/*     INPUT PARAMETER, CONVERT FROM 15.5 TO 7.0                     */
/* LJB 05/19/2008 FP1225  ADD PARENT CUSTOMER NUMBER AS AN INPUT     */
/*     PARAMETER, CONVERT FROM 15.5 TO 7.0                           */
/* LJB 04/13/2009 E00384                                             */
/*     ADD 10 WAREHOUSE CODES AND SELECT/OMIT FLAG FOR NEW PROGRAM   */
/*     ADD WAREHOUSE CODES AND SELECT OMIT FLAG TO EXISTING          */
/*     PROGRAMS AS WELL. ADD REPORT TYPE,                            */
/*********************************************************************/
 PMDUUPC:    PGM        PARM(&RETURN &DESC &OUTQ &HOLD &SAVE &NQSTAT +
                          &NIGHTQ &COMPNYN &FRDATE &TODATE &COPYN +
                          &CARRIER &SLSPRSN &SHIPTON &PARENTN +
                          &FRTCODE &FRTGRP &WHSE1 &WHSE2 &WHSE3 +
                          &WHSE4 &WHSE5 &WHSE6 &WHSE7 &WHSE8 &WHSE9 +
                          &WHSE10 &SOFLAG &RPTTYP &EMAILFMT &EMAIL)

/* PARMS                                                             */
        DCL    &RETURN     *CHAR   (7)  /* RETURN CODE               */
        DCL    &DESC       *CHAR   (25) /* PRT PGM DESCRIPTION       */
        DCL    &OUTQ       *CHAR   (10) /* OUTPUT QUEUE              */
        DCL    &HOLD       *CHAR   (4)  /* HOLD STATUS               */
        DCL    &SAVE       *CHAR   (4)  /* SAVE STATUS               */
        DCL    &NQSTAT     *CHAR   (01) /* IF MUST RUN IN NIGHT Q    */
        DCL    &NIGHTQ     *CHAR   (10) /* NIGHT JOB QUEUE           */
        DCL    &COMPNYN    *DEC         /* COMPANY NUMBER (PASSED)   */
        DCL    &FRDATE     *DEC         /* FROM DATE (PASSED)        */
        DCL    &TODATE     *DEC         /* TO DATE (PASSED)          */
        DCL    &COPYN      *DEC         /* NO. OF COPIES             */
        DCL    &SHIPTON    *DEC         /* SHIP TO CUSTOMER NUMBER   */
        DCL    &SHPTOCUST  *DEC   (7 0) /* SHIP TO CUSTOMER NUMBER   */
        DCL    &PARENTN    *DEC         /* PARENT  CUSTOMER NUMBER   */
        DCL    &PARENT     *DEC   (7 0) /* PARENT  CUSTOMER NUMBER   */
        DCL    &CARRIER    *CHAR   (3)  /* CARRIER CODE              */
        DCL    &SLSPRSN    *CHAR   (3)  /* SALES PERSON CODE         */
        DCL    &FRTCODE    *CHAR   (6)  /* FREIGHT CHARGE CODE       */
        DCL    &EMAIL      *CHAR   (50) /* EMAIL ADDRESS             */
        DCL    &EMAILFMT   *CHAR   (5)  /* EMAIL FORMAT              */
        DCL    &APPLIC     *CHAR   (6)  /* APPLICATION               */
        DCL    &FRTGRP     *CHAR   (6)  /* FREIGHT GROUP             */
        DCL    VAR(&PRTF)  TYPE(*CHAR) LEN(10) /* PRINTER FILE       */
        DCL    VAR(&TEXT)  TYPE(*CHAR) LEN(50) /* DESCRIPTION        */
        DCL    &WHSE1      *CHAR   (3)  /* WAREHOUSE 1               */
        DCL    &WHSE2      *CHAR   (3)  /* WAREHOUSE 2               */
        DCL    &WHSE3      *CHAR   (3)  /* WAREHOUSE 3               */
        DCL    &WHSE4      *CHAR   (3)  /* WAREHOUSE 4               */
        DCL    &WHSE5      *CHAR   (3)  /* WAREHOUSE 5               */
        DCL    &WHSE6      *CHAR   (3)  /* WAREHOUSE 6               */
        DCL    &WHSE7      *CHAR   (3)  /* WAREHOUSE 7               */
        DCL    &WHSE8      *CHAR   (3)  /* WAREHOUSE 8               */
        DCL    &WHSE9      *CHAR   (3)  /* WAREHOUSE 9               */
        DCL    &WHSE10     *CHAR   (3)  /* WAREHOUSE 10              */
        DCL    &SOFLAG     *CHAR   (1)  /* SELECT/OMIT FLAG          */
        DCL    &RPTTYP     *CHAR   (1)  /* REPORT TYPE (1-4)         */

/* INTERNAL FIELDS - CHAR FIELDS ARE TO FOR SEQUEL VIEW              */
        DCL    &COPY       *DEC    (2 0)        /* Number of copies  */
        DCL    &COMP       *DEC    (3 0)        /* COMPANY NUMBER    */
        DCL    &FDATE      *DEC    (7 0)        /* FROM DATE         */
        DCL    &TDATE      *DEC    (7 0)        /* TO DATE           */
        DCL    &COMPA      *CHAR   (3)          /* COMPANY ALPHA     */
        DCL    &FDATEA     *CHAR   (7)          /* FROM DATE ALPHA   */
        DCL    &TDATEA     *CHAR   (7)          /* TO DATE ALPHA     */

/* ERROR MESSAGE FIELDS                                                       */
        DCL    &MSG        *CHAR   (512)         /* message                   */
        DCL    &MSGID      *CHAR   (7)           /* message id                */
        DCL    &MSGL       *DEC    (5 0)

/*********************************************************************/
/* MONITOR MESSAGES                                                  */
/*********************************************************************/
             MONMSG     MSGID(CPF0000 CPF9999) EXEC(GOTO +
                          CMDLBL(MSGTRAP))

/*********************************************************************/
/* CONVERT 15.5 VARIABLES TO 2.0 & 4.0, CONVERT NUMERIC TO CHARACTER */
/*********************************************************************/
             CHGVAR     VAR(&COMP) VALUE(&COMPNYN)
             CHGVAR     VAR(&COMPA) VALUE(&COMP)
             CHGVAR     VAR(&FDATE) VALUE(&FRDATE)
             CHGVAR     VAR(&FDATEA) VALUE(&FDATE)
             CHGVAR     VAR(&TDATE) VALUE(&TODATE)
             CHGVAR     VAR(&TDATEA) VALUE(&TDATE)
             CHGVAR     VAR(&COPY) VALUE(&COPYN)
             CHGVAR     VAR(&RETURN) VALUE('       ')
             CHGVAR     VAR(&SHPTOCUST) VALUE(&SHIPTON)    /*  FT381 */
             CHGVAR     VAR(&PARENT) VALUE(&PARENTN)       /* FP1225 */

/*-------------------------------------------------------------------*/
/* CREATE A DUPLICATE OF THE WORK AR DETAIL FILE IN QTEMP            */
/* CLEAR, IN CASE THERE ARE MULTIPLE REQUESTS                        */
/*-------------------------------------------------------------------*/
             CRTDUPOBJ  OBJ(PMAICPP) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP) DATA(*NO)
             MONMSG     MSGID(CPF0000)
             CLRPFM     FILE(QTEMP/PMAICPP)
             MONMSG     MSGID(CPF0000)

/*-------------------------------------------------------------------*/
/* CREATE A COPY OF THE LOCIGALS INTO QTEMP, AS WELL                 */
/* CALL PGM TO GET DATA FROM THE SALES HISTORY, LOAD HEADER AND      */
/* LOAD FREIGHT CHARGE FILES AND LOAD THE QTEMP FILE                 */
/*-------------------------------------------------------------------*/
             CRTDUPOBJ  OBJ(PMAICPL0) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)
             MONMSG     MSGID(CPF0000)
             CRTDUPOBJ  OBJ(PMAICPL1) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)
             MONMSG     MSGID(CPF0000)
             CRTDUPOBJ  OBJ(PMAICPL2) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)
             MONMSG     MSGID(CPF0000)
             CRTDUPOBJ  OBJ(PMAICPL3) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)
             MONMSG     MSGID(CPF0000)
             CRTDUPOBJ  OBJ(PMAICPL4) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)                   /* FP1225 */
             MONMSG     MSGID(CPF0000)
             CRTDUPOBJ  OBJ(PMAICPL5) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)                   /* FP1225 */
             MONMSG     MSGID(CPF0000)

/* BUILD THE TEMPORARY FILE WITH DATA BASED ON THE PARMS ENTERED     */
/* ON THE PROMPT SCREEN                                              */
             CALL       PGM(PMDPXFR) PARM(&RETURN &FRTCODE &COMP +
                          &CARRIER &SLSPRSN &FDATE &SHPTOCUST +
                          &PARENT &TDATE &FRTGRP &WHSE1 &WHSE2 +
                          &WHSE3 &WHSE4 &WHSE5 &WHSE6 &WHSE7 &WHSE8 +
                          &WHSE9 &WHSE10 &SOFLAG &RPTTYP)

/********************************************************************/
/* OVERRIDE THE PRINTER FILE. TO EMAIL, THE PRINTER FILE MUST BE    */
/* HOLD = *YES AND SAVE = *YES.                                     */
/********************************************************************/
             IF         COND(&EMAIL *NE '               ') THEN(DO)
             CHGVAR     VAR(&HOLD) VALUE(*YES)
             CHGVAR     VAR(&SAVE) VALUE(*YES)
             ENDDO

/*-------------------------------------------------------------------*/
/* CREATE THE AS400 REPORT                                           */
/* THE REPORT IS BUILT OVER THE WORK FILE SO ALL RECORDS ARE PRINTED */
/*-------------------------------------------------------------------*/
             IF         COND(&RPTTYP *EQ '1') THEN(DO)
             CHGVAR     VAR(&PRTF) VALUE('PMDVPFR' *TCAT '$')
             OVRPRTF    FILE(&PRTF) OUTQ(&OUTQ) COPIES(&COPY) +
                          HOLD(&HOLD) SAVE(&SAVE)
             CALL       PGM(PMDVPFR) PARM(&RETURN &FRTCODE &COMP +
                          &CARRIER &SLSPRSN &FDATE &SHPTOCUST +
                          &PARENT &TDATE &FRTGRP &WHSE1 &WHSE2 +
                          &WHSE3 &WHSE4 &WHSE5 &WHSE6 &WHSE7 &WHSE8 +
                          &WHSE9 &WHSE10 &SOFLAG)
             ENDDO      /* END IF &RPTTYP = '1' */

             IF         COND(&RPTTYP *EQ '2') THEN(DO)
             CHGVAR     VAR(&PRTF) VALUE('PMDZPFR' *TCAT '$')
             OVRPRTF    FILE(&PRTF) OUTQ(&OUTQ) COPIES(&COPY) +
                          HOLD(&HOLD) SAVE(&SAVE)
             CALL       PGM(PMDZPFR) PARM(&RETURN &FRTCODE &COMP +
                          &CARRIER &SLSPRSN &FDATE &SHPTOCUST +
                          &PARENT &TDATE &FRTGRP &WHSE1 &WHSE2 +
                          &WHSE3 &WHSE4 &WHSE5 &WHSE6 &WHSE7 &WHSE8 +
                          &WHSE9 &WHSE10 &SOFLAG)
             ENDDO      /* END IF &RPTTYP = '2' */

             IF         COND(&RPTTYP *EQ '3') THEN(DO)
             CHGVAR     VAR(&PRTF) VALUE('PMPHPFR' *TCAT '$')
             OVRPRTF    FILE(&PRTF) OUTQ(&OUTQ) COPIES(&COPY) +
                          HOLD(&HOLD) SAVE(&SAVE)
             CALL       PGM(PMPHPFR) PARM(&RETURN &FRTCODE &COMP +
                          &CARRIER &SLSPRSN &FDATE &SHPTOCUST +
                          &PARENT &TDATE &FRTGRP &WHSE1 &WHSE2 +
                          &WHSE3 &WHSE4 &WHSE5 &WHSE6 &WHSE7 &WHSE8 +
                          &WHSE9 &WHSE10 &SOFLAG)
             ENDDO      /* END IF &RPTTYP = '3' */

             IF         COND(&RPTTYP *EQ '4') THEN(DO)
             CHGVAR     VAR(&PRTF) VALUE('PNHMPFR' *TCAT '$')
             OVRPRTF    FILE(&PRTF) OUTQ(&OUTQ) COPIES(&COPY) +
                          HOLD(&HOLD) SAVE(&SAVE)
             CALL       PGM(PNHMPFR) PARM(&RETURN &FRTCODE &COMP +
                          &CARRIER &SLSPRSN &FDATE &SHPTOCUST +
                          &TDATE &FRTGRP &WHSE1 &WHSE2 &WHSE3 +
                          &WHSE4 &WHSE5 &WHSE6 &WHSE7 &WHSE8 &WHSE9 +
                          &WHSE10 &SOFLAG)
             ENDDO      /* END IF &RPTTYP = '4' */

/* FOR TESTING ONLY                             +
             CPYF       FROMFILE(QTEMP/PMAICPP) +
                          TOFILE(OMSDEVGEN/PMAICPPSV) +
                          MBROPT(*REPLACE) CRTFILE(*YES) +
             MONMSG     MSGID(CPF0000)                   <<<<<<<<<<<*/

/*-------------------------------------------------------------------*/
/* EMAIL THE REPORT IF REQUESTED                                     */
/*-------------------------------------------------------------------*/
             IF         COND(&EMAIL *NE '               ') THEN(DO)
             CHGVAR     VAR(&TEXT) VALUE('Excessive Freight Charges +
                          Report in' *BCAT &EMAILFMT)

             OVRDBF     FILE(PMAICPP) TOFILE(QTEMP/PMAICPP)

/* IF *XLS IS REQUESTED, EXCEL SPREADSHEET HAS TO BE CREATED         */
             IF         COND(&EMAILFMT *EQ '*XLS') THEN(DO)     +

/*    SPREADSHEET FOR SALESPERSON:                                   */
             IF         COND(&RPTTYP *EQ '1') THEN(CALL PGM(PNHOUPC) +
                          PARM(PMAICPPA &TEXT &EMAIL &EMAILFMT))
/********    IF         COND(&PGM *EQ 'PMDVPFR') THEN(EXECUTE +
                          VIEW(PMAICPPA) PCFMT(*XLS) +
                          REPLACE(*YES) RECIPIENT(&EMAIL) +
                          EMLMSG(&TEXT))                   ***********/

/*    SPREADSHEET FOR CARRIER CODE:                                  */
             IF         COND(&RPTTYP *EQ '2') THEN(CALL PGM(PNHOUPC) +
                          PARM(PMAICPPB &TEXT &EMAIL &EMAILFMT))
/********    IF         COND(&PGM *EQ 'PMDZPFR') THEN(EXECUTE +
                          VIEW(PMAICPPB) PCFMT(*XLS) +
                          REPLACE(*YES) RECIPIENT(&EMAIL) +
                          EMLMSG(&TEXT))     *************************/

/*    SPREADSHEET FOR SHIP TO CUSTOMER NUMBER                        */
             IF         COND(&RPTTYP *EQ '3') THEN(CALL PGM(PNHOUPC) +
                          PARM(PMAICPPC &TEXT &EMAIL &EMAILFMT))
/*********   IF         COND(&PGM *EQ 'PMPHPFR') THEN(EXECUTE +
                          VIEW(PMAICPPC) PCFMT(*XLS) +
                          REPLACE(*YES) RECIPIENT(&EMAIL) +
                          EMLMSG(&TEXT))                **************/

/*    SPREADSHEET FOR WAREHOUSE REPORT                               */
             IF         COND(&RPTTYP *EQ '4') THEN(CALL PGM(PNHOUPC) +
                          PARM(PMAICPPD &TEXT &EMAIL &EMAILFMT))
             ENDDO      /* END: &EMAILFMT *EQ '*XLS' */

/* IF *PDF, *HTML, *RTF, OR *TXT IS REQUESTED                        */
/* CALL PMB8UPC TO SEND THE AS400 SPOOL FILE AS AN ATTACHMENT        */
             ELSE       CMD(CALL PGM(PMB8UPC) PARM(&PRTF &TEXT +
                          &EMAIL &EMAILFMT))
             ENDDO      /* END: &EMAILFMT *NE '*XLS' */

             GOTO       CMDLBL(EXIT)

/*-------------------------USER QUIT--------------------------------*/
 USERQUIT:   CHGVAR     VAR(&RETURN) VALUE('Y2U0005')
             GOTO       CMDLBL(EXIT)

/*----------------------RECEIVE MESSAGES----------------------------*/
 MSGTRAP:    RCVMSG     MSGTYPE(*EXCP) MSGDTA(&MSG) MSGDTALEN(&MSGL) +
                          MSGID(&MSGID)
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(EXIT))

             SNDPGMMSG  MSGID(&MSGID) MSGF(QCPFMSG) MSGDTA(%SST(&MSG +
                          1 &MSGL)) MSGTYPE(*ESCAPE)
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(EXIT))

EXIT:  ENDPGM
