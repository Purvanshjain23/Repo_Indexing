     A*%%TS  SD  20131111  083145  ISLRAMS     REL-V6R1M0  5761-WDS
     A*****************************************************************
     A*
     A*  SYSTEM:        HOG PRODUCTION
     A*  DISPLAY FILE:  HPD574 - SELECT DRIVER
     A*  CREATED BY:    LEANNE FEDOR
     A*  DATE:          01/15/03
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3                  -
     A                                             27 132 *DS4)
     A                                      REF(*LIBL/HSPREF)
     A                                      PRINT
     A                                      CA12(12 'PREVIOUS')
     A                                      CF04(04 'PROMPT')
     A N82                                  ROLLUP(25 'PAGE FORWARD')
     A*---------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP574S1                   SFL
     A*%%TS  SD  20131021  103708  ISLRAMS     REL-V6R1M0  5761-WDS
     A            HDRRN1    R        H      REFFLD(@@RRN)
     A            D1TOCD    R        O  8  2REFFLD(HSRREF/@@TOCD *LIBL/HSPREF)
     A            D1DICD    R        O  8  9REFFLD(@@DICD)
     A            D1DINM    R        O  8 16REFFLD(HSRREF/@@DINM *LIBL/HSPREF)
     A            D1TICD    R        O  8 43REFFLD(HSRREF/@@TICD *LIBL/HSPREF)
     A            D1AIST    R        O  8 52REFFLD(HSRREF/@@AIST *LIBL/HSPREF)
     A            D1DIREF   R        O  8 55REFFLD(HSRREF/@@MTECHREF +
     A                                             *LIBL/HSPREF)
     A*
     A*---------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP574C1                   SFLCTL(HP574S1)
     A*%%TS  SD  20131111  083145  ISLRAMS     REL-V6R1M0  5761-WDS
     A  *DS3                                SFLSIZ(0100)
     A  *DS4                                SFLSIZ(0100)
     A  *DS3                                SFLPAG(0009)
     A  *DS4                                SFLPAG(0009)
     A  *DS3                                WINDOW(HP574EC)
     A  *DS4                                WINDOW(HP574EC)
     A                                      SETOF(51)
     A                                      SETOF(52)
     A                                      SETOF(53)
     A                                      RTNCSRLOC(&RCD &FLD)
     A                                      OVERLAY
     A                                      SFLCSRRRN(&HDRRN)
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A            HDPAG1    R        H      REFFLD(@@PAG)
     A                                      SFLRCDNBR
     A            RCD           10A  H
     A            FLD           10A  H
     A            HDRRN     R        H      REFFLD(@@RRN)
     A            SDPGM     R        O  1  1REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 33'SELECT DRIVER'
     A                                      DSPATR(HI)
     A            C1DESC    R        O  2 24REFFLD(@@DS30)
     A                                      DSPATR(HI)
     A                                  3  2'Position Cursor, Press Enter'
     A                                  4  4'?'
     A                                      DSPATR(HI)
     A                                  4 45'?'
     A                                      DSPATR(HI)
     A N62                              4 52'?'
     A                                      DSPATR(HI)
     A                                  5  2'Trans'
     A                                      DSPATR(HI)
     A                                  5  9'Driver'
     A                                      DSPATR(HI)
     A                                  5 42'Trucker'
     A                                      DSPATR(HI)
     A                                  5 51'A/I'
     A                                      DSPATR(HI)
     A                                  5 60'Driver'
     A                                      DSPATR(HI)
     A                                  6  2'Office'
     A                                      DSPATR(HI)
     A                                  6  9'Code'
     A                                      DSPATR(HI)
     A                                  6 16'Name'
     A                                      DSPATR(HI)
     A                                  6 43'Code'
     A                                      DSPATR(HI)
     A                                  6 51'St'
     A                                      DSPATR(HI)
     A                                  6 55'MTech Reference'
     A                                      DSPATR(HI)
     A            C1TOCD    R        B  7  2REFFLD(HSRREF/@@TOCD *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A  51                                  DSPATR(PC)
     A            C1DICD    R        B  7  9REFFLD(HSRREF/@@DICD *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A            C1DINM    R        B  7 16REFFLD(HSRREF/@@DINM *LIBL/HSPREF)
     A                                      CHGINPDFT(UL LC FE)
     A                                      DSPATR(HI)
     A            C1TICD    R        B  7 43REFFLD(HSRREF/@@TICD *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A  53                                  DSPATR(PC)
     A N62        C1AIST    R        B  7 52REFFLD(HSRREF/@@AIST *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A  52                                  DSPATR(PC)
     A            C1DIREF   R        B  7 55REFFLD(HSRREF/@@MTECHREF *LIBL/HSPR-
     A                                      EF)
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*---------------------------------------------------------------
     A* COMMAND LINE LINE
     A*---------------------------------------------------------------
     A          R HP574K1
     A                                      WINDOW(HP574EC)
     A                                      OVERLAY
     A  43                             17  1'Message program failed. Call IS.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 18  1'F4=Prompt'
     A                                 18 12'F12=Previous'
      *
     A*-------------------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
     A*-------------------------------------------------------------------------
     A          R HP574ES                   SFL SFLMSGRCD(19)
      *
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
     A*
     A*-------------------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*-------------------------------------------------------------------------
     A*
     A          R HP574EC                   SFLCTL(HP574ES)
     A*%%TS  SD  19980327  105628  ISLRAMS     REL-V3R7M0  5716-PW1
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A                                      WINDOW(*DFT 20 75)
     A                                      WDWBORDER((*DSPATR RI) (*CHAR '    -
     A                                          '))
     A            MSGPGM                    SFLPGMQ(10)
     A*
     A*---------------------------------------------------------------
     A* ASSUME FORMAT -- REQUIRED FOR WINDOWS
     A*---------------------------------------------------------------
     A          R ASSUME
     A*%%TS  SD  20011025  160559  ISLRAMS     REL-V4R4M0  5769-PW1
     A                                      ASSUME
     A                                      OVERLAY
     A                                 24 79' '
