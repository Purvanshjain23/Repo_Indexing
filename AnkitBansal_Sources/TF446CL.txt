/***************************************************************** */
/*                                                                 */
/*  SYSTEM:          TRIUMPH FOODS                                 */
/*  PROGRAM NUMBER:  TF446CL                                       */
/*  PROGRAM NAME:    MARGIN ADJUSTMENT RECAP REPORT                */
/*  PROGRAMMER:      LEANNE RAMSEY                                 */
/*  CREATE DATE:     12/12/06                                      */
/*                                                                 */
/*******************************************************************/
/* MODIFICATIONS:                                                  */
/*                                                                 */
/* 08/27/08  LEANNE RAMSEY                                         */
/*           CHANGED "VOLUME" LOGIC FROM "PERIOD" TO "WEEKLY"      */
/*                                                                 */
/* 11/21/08  LeAnne Ramsey                                         */
/*           As part of synchronizing the LDAs between the         */
/*           TFS Margin Adjustment Close and the Meat Costing,     */
/*           the LDA positions were changed.                       */
/*                                                                 */
/* 03/30/09  LeAnne Ramsey                                         */
/*           To handle the user request to break the CoOwned       */
/*           Amount out between SBF and TF on the Margin           */
/*           Adjustment Recap Report, we will now go to file       */
/*           TFL014B to retrieve info. So, added an open query.    */
/*                                                                 */
/* 05/06/09  LEANNE RAMSEY                                         */
/*           CHANGED PRINT LOGIC TO MATCH MEAT COSTING.            */
/* 05/07/24  SANTOSH PATIL - P310149 - FIELD LENGTH IS INCREASED   */
/*           IN PF TFP015. FUNCTION IS RECOMPILED.                 */
/*******************************************************************/

             PGM

             DCL        VAR(&QDATA) TYPE(*CHAR) LEN(70)

             DCL        VAR(&OUTQ) TYPE(*CHAR) LEN(10)
             DCL        VAR(&HOLD) TYPE(*CHAR) LEN(4)
             DCL        VAR(&SAVE) TYPE(*CHAR) LEN(4)
             DCL        VAR(&COPY) TYPE(*DEC)  LEN(1)

             DCL        VAR(&LDFWEDT) TYPE(*DEC) LEN(8)
             DCL        VAR(&LDTWEDT) TYPE(*DEC) LEN(8)

             CHGVAR     VAR(&LDFWEDT) VALUE(%SST(*LDA 85 8))
             CHGVAR     VAR(&LDTWEDT) VALUE(%SST(*LDA 99 8))

/*----------------------------------------------------------------*/
/* SET PRINT PARAMETERS FROM LDA                                  */
/*----------------------------------------------------------------*/

 OUTQ:       CHGVAR     VAR(&OUTQ) VALUE(%SST(*LDA 401 10))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(*JOB))

 HOLD:       CHGVAR     VAR(&HOLD) VALUE(%SST(*LDA 411 4))
             IF         COND(&HOLD *NE '*YES' *AND &HOLD *NE '*NO ') +
                          THEN(CHGVAR VAR(&HOLD) VALUE(*NO))

 SAVE:       CHGVAR     VAR(&SAVE) VALUE(%SST(*LDA 415 4))
             IF         COND(&SAVE *NE '*YES' *AND &SAVE *NE '*NO ') +
                          THEN(CHGVAR VAR(&SAVE) VALUE(*NO))

 COPY:       CHGVAR     VAR(&COPY) VALUE(%SST(*LDA 419 1))
             IF         COND(&COPY = 0) THEN(CHGVAR VAR(&COPY) +
                          VALUE(1))

/*---------------------------------------------------------------------*/
/* CREATE MIX SUMMARY WORKFILE; POPULATE IT WITH WEEK DATA             */
/*---------------------------------------------------------------------*/

             CRTDUPOBJ  OBJ(TFP315) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)
             CRTDUPOBJ  OBJ(TFL315A) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)

             CPYF       FROMFILE(TFP015) TOFILE(QTEMP/TFP315) +
                          MBROPT(*ADD)
             MONMSG     MSGID(CPF2817)

/*------------------------------------------------------------------------*/
/* CREATE VOLUME SUMMARY WORKFILE; POPULATE IT WITH WEEK DATA             */
/*------------------------------------------------------------------------*/

             CRTDUPOBJ  OBJ(TFP318) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)

             CPYF       FROMFILE(TFP018) TOFILE(QTEMP/TFP318) +
                          MBROPT(*ADD)
             MONMSG     MSGID(CPF2817)

/*---------------------------------------------------------------------*/
/* EXTRACT THE DATA FROM THE MIX SUMMARY WORKFILE                      */
/*---------------------------------------------------------------------*/

             CHGVAR     VAR(&QDATA) VALUE(' ')

             CHGVAR     VAR(%SST(&QDATA 1 9)) VALUE('MSEDT *GE')
             CHGVAR     VAR(%SST(&QDATA 11 8)) VALUE(&LDFWEDT)

             CHGVAR     VAR(%SST(&QDATA 20 14)) VALUE('*AND MSEDT *LE')
             CHGVAR     VAR(%SST(&QDATA 35 8)) VALUE(&LDTWEDT)

             CHGVAR     VAR(%SST(&QDATA 44 20)) VALUE('*AND MSITYCD *EQ " "')

/* OVERRIDE MIX SUMMARY FILE                                  */

             OVRDBF     FILE(TFP315)  SHARE(*YES)
             OPNQRYF    FILE((TFP315)) OPTION(*INP) QRYSLT(&QDATA) +
                          KEYFLD((MSISTYCD) (MSTFCLCD) +
                          (MSMIXGRP)) SEQONLY(*YES)

/*---------------------------------------------------------------------*/
/* EXTRACT THE DATA FROM THE VOLUME SUMMARY WORKFILE                   */
/*---------------------------------------------------------------------*/

             CHGVAR     VAR(&QDATA) VALUE(' ')

             CHGVAR     VAR(%SST(&QDATA 1 9)) VALUE('VSEDT *GE')
             CHGVAR     VAR(%SST(&QDATA 11 8)) VALUE(&LDFWEDT)

             CHGVAR     VAR(%SST(&QDATA 20 14)) VALUE('*AND VSEDT *LE')
             CHGVAR     VAR(%SST(&QDATA 35 8)) VALUE(&LDTWEDT)

/* OVERRIDE VOLUME SUMMARY FILE                               */

             OVRDBF     FILE(TFP318)  SHARE(*YES)
             OPNQRYF    FILE((TFP318)) OPTION(*INP) QRYSLT(&QDATA) +
                          KEYFLD((VSISTYCD) (VSTFCLCD)) +
                          SEQONLY(*YES)


/*---------------------------------------------------------------------*/
/* EXTRACT THE COOWNED DATA FROM THE MARGIN ADJUSTMENT DETAIL FILE     */
/*---------------------------------------------------------------------*/

             CHGVAR     VAR(&QDATA) VALUE(' ')

             CHGVAR     VAR(%SST(&QDATA 1 10)) VALUE('ADWEDT *GE')
             CHGVAR     VAR(%SST(&QDATA 12 8)) VALUE(&LDFWEDT)

             CHGVAR     VAR(%SST(&QDATA 21 15)) VALUE('*AND ADWEDT *LE')
             CHGVAR     VAR(%SST(&QDATA 37 8)) VALUE(&LDTWEDT)

             CHGVAR     VAR(%SST(&QDATA 46 22)) VALUE('*AND +
                          ADCOOWNFL *EQ "Y"')

/* OVERRIDE MARGIN ADJUSTMENT TRANSACTION DETAIL FILE         */

             OVRDBF     FILE(TFL014B) SHARE(*YES)
             OPNQRYF    FILE((TFL014B)) OPTION(*INP) QRYSLT(&QDATA) +
                          KEYFLD(*FILE) SEQONLY(*YES)

/*---------------------------------------------------------------------*/
/* BUILD WORKFILES BRINGING THE MIX SUMMARY AND VOLUME SUMMARY DATA    */
/* TOGETHER.                                                           */
/*---------------------------------------------------------------------*/

             CRTDUPOBJ  OBJ(TFP346) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)

             CRTDUPOBJ  OBJ(TFP3460) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)

             CALL       PGM(TF346)

             CLOF       OPNID(TFP315)
             DLTOVR     FILE(TFP315)
             DLTF       FILE(QTEMP/TFL315A)
             DLTF       FILE(QTEMP/TFP315)

             CLOF       OPNID(TFP318)
             DLTOVR     FILE(TFP318)
             DLTF       FILE(QTEMP/TFP318)

             CLOF       OPNID(TFL014B)
             DLTOVR     FILE(TFL014B)

/*---------------------------------------------------------------------*/
/* PRINT THE REPORT                                                    */
/*---------------------------------------------------------------------*/

             OVRPRTF    FILE(PRINT198) OUTQ(&OUTQ) COPIES(&COPY) +
                          HOLD(&HOLD) SAVE(&SAVE)

             CALL       PGM(TF646)

             DLTOVR     FILE(*ALL)

             ENDPGM

