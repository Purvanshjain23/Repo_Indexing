/********************************************************************/
/*                                                                  */
/*  SYNOPSIS - CL PROGRAM FOR FAX/EMAIL BROKER/REG ORDERS THAT ARE  */
/*             EQUAL TO JOB DATE.  THE WORK FILES WF FAX/EMAIL      */
/*             DRIVER AND DETAIL WILL BE BUILT AND THEN AS OF THIS  */
/*             TIME WE WILL ONLY SEND VIA EMAIL.                    */
/*             CURRENTLY THE DATA ELEMENTS ARE ESTABLISHED          */
/*             TO HANDLE FAX OR EMAIL, BUT WE ARE ONLY SEND EMAIL   */
/*             FAX WILL HAVE TO COME ON A LATER PROJECT             */
/*                                                                  */
/*  SYSTEM    - ORDER MANAGEMENT                                    */
/*  DATE      - 02/08/05                                            */
/*                                                                  */
/*      PARAMETERS RECEIVED:                                        */
/*         &RETURN ONLY                                             */
/********************************************************************/
/* SLM   2/08/05  Created                                          */
/* RMC   09/24/07 RECOMPILE FOR DB CHANGE                 */
/* PKD   09/24/08 REPLACED BUILD PROGRAM TO CREATE NON-ACCRUED ITEMS */
/* SLM   07/31/12 REPLACED BUILD PROGRAM TO CREATE NON-COMMISSION    */
/*       ITEMS AS WELL AS COMMISSION ITEMS                           */
/*       NEW DRIVER PROGRAM THAT WILL USE THE CUSTOMER ACCRUAL       */
/*       AND THE SALES HISTORY TO CREATE THE WF FAX/EMAIL BASE       */
/*       ON THE ACTUAL SHIPPING DATE WITH RANGE OF THE               */
/*       CUSTOMER ACCRUAL DATES.   REPLACE THE FOLLOWING STATEMENT   */
/*           CALL       PGM(PMY4XFR) PARM(' ' &ATYPE &IDTE)          */
/*       USING A NEW DRIVER PROCESS                                  */
/*           CALL       PGM(PBEMXFR) PARM(' ' &ATYPE &IDTE)          */
/********************************************************************/
             PGM        PARM(&RETURN &ATYPE &DATE)
             /* INPUT PARMS */
             DCL        VAR(&RETURN) TYPE(*CHAR) LEN(7)
             DCL        VAR(&ATYPE) TYPE(*CHAR) LEN(2)
             DCL        VAR(&DATE) TYPE(*DEC) LEN(15 5)

             /* INTERNAL VARIABLES */
             DCL        VAR(&IDTE) TYPE(*DEC) LEN(7 0)
             DCL        VAR(&IDTEX) TYPE(*CHAR) LEN(7)
             DCL        VAR(&SDTE) TYPE(*CHAR) LEN(10)
             DCL        VAR(&IDTEX) TYPE(*CHAR) LEN(7)
             DCL        VAR(&SUBJECT) TYPE(*CHAR) LEN(50)
             DCL        VAR(&USER) TYPE(*CHAR) LEN(10)
             DCL        VAR(&SEL) TYPE(*CHAR) LEN(1500) /*  FOR +
                          PASSING    */

/*  WF FAX/EMAIL DRIVER FILE WILL BE READ AFTER IT     */
/*  IS BUILT                                           */

             DCLF       FILE(PPARCPL1)

/*  SET UP LIBRARY LIST                                */
/*  WF FAX/EMAIL FILES WILL BE CREATED IN QTEMP        */
/*  AND ESEND IS USED FOR EMAIL                        */

             ADDLIBLE   LIB(ESEND)
             MONMSG CPF0000
             ADDLIBLE LIB(SEQUEL) POSITION(*LAST)
             MONMSG MSGID(CPF2103)

/*  CLEAR WF FAX/EMAIL DETAIL AND DRIVER              */

             CLRPFM     FILE(PPARCPP)
             CLRPFM     FILE(PMEQCPP)
             CLRPFM     FILE(PPASCPP)

/*  CALL TO PROGRAM THAT WILL CREATE THE WF FAX/EMAIL DRIVER&DTL  */
             CHGVAR     VAR(&IDTE) VALUE(&DATE)
             CALL       PGM(PBEMXFR) PARM(' ' &ATYPE &IDTE)

             CALL SUSMMM

 LOOP:       RCVF
             MONMSG     MSGID(CPF0864) EXEC(GOTO CMDLBL(DONE))

             /* IF THE EMAIL ADDRESS IS BLANK READ NEXT RECORD    */
             IF         COND(&ARUGTX *EQ '     ') THEN(DO)
                        GOTO LOOP
             ENDDO

/*  Broker Sequel                                      */
             IF         COND(&ARTBSX *EQ 'B') THEN(DO)
/*  PERFORM SEQUEL ON THE WF FAX/EMAIL DETAIL FILE     */
/*  AND SEND BY EMAIL                                  */

             CHGVAR     VAR(&SEL) VALUE('SELECT ASC4NB +
                          COLHDG("Order#"), CVTDATE(ASAWDT,cymd) +
                          NAME(INVDAT) COLHDG("Invoice" "Date"), +
                          ASEGC7 COLHDG("Ship To Customer#"), +
                          CVTDATE(ASGNDT,cymd) NAME(ASD) +
                          COLHDG("Actual" "Ship" "Date"), ASCONB +
                          COLHDG("Invoice#"), ASBCNA +
                          COLHDG("Customer PO#"), ASBRTX, ASHNNA +
                          COLHDG("City"), ASHGCD COLHDG("Product"), +
                          ASGENA COLHDG("Description"), ASAVQT +
                          COLHDG("Qty Ordered"), ASEXQT COLHDG("Qty +
                          Shipped"), ASBYWG COLHDG("Net Weight"), +
                          ASAKPR COLHDG("Delivered Price"), ASCRCD +
                          COLHDG("U/M"), ASA1VA COLHDG("Amount"), +
                          ASBKPR COLHDG("Commission" "Rate"), +
                          ASAYVA COLHDG("Commission" "Amount") +
                          FROM *LIBL/PPASCPP WHERE ASFRAA="' *TCAT +
                          &ARFRAA *TCAT '" ORDER BY ASEHC7, ASC4NB, +
                          ASHGCD, ASAIC3')
             ENDDO
/*  Regional Sequel                                    */
             IF         COND(&ARTBSX *EQ 'R') THEN(DO)
/*  PERFORM SEQUEL ON THE WF FAX/EMAIL DETAIL FILE     */
/*  AND SEND BY EMAIL                                  */

             CHGVAR     VAR(&SEL) VALUE('SELECT ASC4NB +
                          COLHDG("Order#"), CVTDATE(ASAWDT,cymd) +
                          NAME(INVDAT) COLHDG("Invoice" "Date"), +
                          ASEGC7 COLHDG("Ship To Customer#"), +
                          CVTDATE(ASGNDT,cymd) NAME(ASD) +
                          COLHDG("Actual" "Ship" "Date"), ASCONB +
                          COLHDG("Invoice#"), ASBCNA +
                          COLHDG("Customer PO#"), ASBRTX, ASHNNA +
                          COLHDG("City"),  ASHGCD +
                          COLHDG("Product"), ASGENA +
                          COLHDG("Description"), ASAVQT COLHDG("Qty +
                          Ordered"), ASEXQT COLHDG("Qty Shipped"), +
                          ASBYWG COLHDG("Net Weight"), ASAKPR +
                          COLHDG("Delivered Price"), ASCRCD +
                          COLHDG("U/M"), ASA1VA COLHDG("Amount"), +
                          ASBKPR COLHDG("Commission" "Rate"), +
                          ASAYVA COLHDG("Commission" "Amount"),+
                          ASFWAA COLHDG("Broker" "Code") +
                          FROM *LIBL/PPASCPP WHERE ASFRAA="' *TCAT +
                          &ARFRAA *TCAT '" ORDER BY ASEHC7, ASC4NB, +
                          ASHGCD, ASAIC3')
             ENDDO

             CHGVAR     VAR(&IDTEX) VALUE(&IDTE)
             CVTDAT     DATE(&IDTEX) TOVAR(&SDTE) FROMFMT(*CYMD) +
                          TOFMT(*USA)
             CHGVAR     VAR(&SUBJECT) VALUE(&ARFRAA *BCAT 'Orders +
                          Invoiced' *BCAT &SDTE)

             IF         COND(&RETURN *NE '       ') THEN(DO)
                  CHGVAR VAR(&ARUGTX) VALUE(&RETURN)
             ENDDO

             EXECUTE    SQL(&SEL) MBROPT(*REPLACE) NBRRCDS(*ALL) +
                          PCFMT(*XLS) RECIPIENT(&ARUGTX) +
                          EMLMSG(&SUBJECT) +
                          TEXT(&SUBJECT)

             GOTO       CMDLBL(LOOP)
DONE:

ENDPGM:     ENDPGM
