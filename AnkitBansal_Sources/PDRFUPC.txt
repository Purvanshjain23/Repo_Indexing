/***************************************************************** */
/*                                                                 */
/*  PROGRAM NUMBER:  PDQNUPC                                       */
/*  PROGRAM NAME:    Print ORDER INQURIY FOR SHIPPING              */
/*  PROGRAMMER:      JOHN TREMPER                                  */
/*  CREATE DATE:     06/23/98                                      */
/*                                                                 */
/*  FUNCTION:        This CLP is submitted FROM THE ORDER          */
/*                   INQUIRY FOR SHIPPING DISPLAY FILE AND WILL    */
/*                   RUN AN OPEN QUERY FILE TO MINIMIZE RECORDS.   */
/*                                                                 */
/*                   USER OPTIONS ARE PASSED THROUGH THE PARMS.    */
/*                                                                 */
/*******************************************************************/
/*  Modification History                                           */
/*                                                                 */
/*                                                                 */
/*                                                                 */
/*******************************************************************/
             PGM        PARM(&COMP &ORDER &LOAD &SDATE)
/*-----------------------------------------------------------------*/
/*                      PARAMETER                                  */
/*-----------------------------------------------------------------*/
/* INPUT PARAMETERS                                                */
             DCL        VAR(&COMP)  TYPE(*DEC)  LEN(3 0)
             DCL        VAR(&SDATE) TYPE(*DEC)
             DCL        VAR(&LOAD)  TYPE(*DEC)  LEN(7 0)
             DCL        VAR(&ORDER) TYPE(*DEC)  LEN(7 0)

/*-----------------------------------------------------------------*/
/* PROGRAM VARIABLES                                               */
             DCL        VAR(&SDTE)     TYPE(*DEC)  LEN(7 0)
             DCL        VAR(&QRYCOMP)  TYPE(*CHAR) LEN(3)
             DCL        VAR(&QRYSDATE) TYPE(*CHAR) LEN(7)
             DCL        VAR(&QRYWHSE)  TYPE(*CHAR) LEN(3)
             DCL        VAR(&QRYLOAD)  TYPE(*CHAR) LEN(7)
             DCL        VAR(&QRYORDER) TYPE(*CHAR) LEN(7)
             DCL        VAR(&A)        TYPE(*CHAR) LEN(25)
             DCL        VAR(&B)        TYPE(*CHAR) LEN(25)
             DCL        VAR(&C)        TYPE(*CHAR) LEN(25)
             DCL        VAR(&D)        TYPE(*CHAR) LEN(25)
             DCL        VAR(&E)        TYPE(*CHAR) LEN(200)

/*-----------------------------------------------------------------*/
/* MOVES INPUT TO QUERY FIELDS                                     */
             CHGVAR     VAR(&QRYCOMP)  VALUE(&COMP)
             CHGVAR     VAR(&SDTE)     VALUE(&SDATE)
             CHGVAR     VAR(&QRYSDATE) VALUE(&SDTE)
             CHGVAR     VAR(&QRYLOAD)  VALUE(&LOAD)
             CHGVAR     VAR(&QRYORDER) VALUE(&ORDER)

/*-----------------------------------------------------------------*/
/* OPEN QUERY FILE CONCATS                                         */
             CHGVAR     VAR(&A) VALUE('BEAIC3 *EQ ' *CAT &QRYCOMP)
             CHGVAR     VAR(&B) VALUE('*AND BEAODT *EQ ' +
                                       *CAT &QRYSDATE)
             IF         COND(&QRYLOAD *NE '0000000') THEN(DO)
               CHGVAR   VAR(&C) VALUE('*AND BERMNB *EQ ' +
                                       *CAT &QRYLOAD)
             ENDDO
             IF         COND(&QRYORDER *NE '0000000') THEN(DO)
               CHGVAR   VAR(&D) VALUE('*AND BEC4NB *EQ ' +
                                       *CAT &QRYORDER)
             ENDDO
             CHGVAR     VAR(&E) VALUE(&A *BCAT &B *BCAT &C *BCAT &D)

/*-----------------------------------------------------------------*/
/* OPEN QUERY                                                      */
/*-----------------------------------------------------------------*/
/* PLEASE NOTE:                                                    */
/*   THE 'OVRSCOPE' PARAMETER FOR THE OVRDBF MUST BE SET TO *JOB   */
/*   IN ORDER FOR THIS FUNCTION TO WORK PROPERLY.  AFTER THE       */
/*   PRINT FILE HAS COMPLETED AN EXECUTABLE MESSAGE WILL WILL      */
/*   DELETE THE OVERRIDE.  THE 'OVERSCOPE' ON THE EXECUTABLE       */
/*   MUST BE SET TO *JOB AS WELL.                                  */
/*      JOHN TREMPER, JUNE 24, 1998                                */
/*-----------------------------------------------------------------*/
             OVRDBF     FILE(OPBFCPLY) OVRSCOPE(*JOB) SHARE(*YES)
             OPNQRYF    FILE((OPBFCPLY)) QRYSLT(&E) KEYFLD(*FILE)

/*-----------------------------------------------------------------*/
/*                      PROGRAM END                                */
/*-----------------------------------------------------------------*/
 ENDPGM:

             ENDPGM
