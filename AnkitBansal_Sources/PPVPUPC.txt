/********************************************************************/
/*                                                                  */
/*  PROGRAM  -  PPVPUPC - SUBMITS PPU9UPC                           */
/*                                                                  */
/*  SYNOPSIS -  JOB CONTROL PROMPT FOR PRINTER OVERRIDE INFORMATION */
/*              GENERIC PROMPTING FUNCTION USED BY ANY PROGRAM      */
/*              SENDING COMPANY + PRINTER OVERRIDE PROMPTING        */
/*              ONLY                                                */
/*                                                                  */
/*  SYSTEM    - ACCOUNTS RECEIVABLE SYSTEM                          */
/*  PROGRAM   - PRT PRMT CST/CO TRFINF UP   (CL)  PPVPUPC           */
/*  DATE      - 01/05/06                                            */
/*                                                                  */
/*      PARAMETERS:                                                 */
/*                                                                  */
/*     O    &RETURN     - RETURN CODE                               */
/*     I    &APPLIC     - APPLICATION CODE                          */
/*     I    &PGM        - CL PROGRAM TO SUBMIT                      */
/*     B    &COMPNYN    - COMPANY NUMBER (PASSED)                   */
/*     B    &COPYN      -  NO. OF COPIES (PASSED)                   */
/*     I    &DESC       - PRINT PROGRAM DESCRIPTION (FOR PROMPT     */
/*                         SCREEN)                                  */
/*     B    &OUTQ       -  OUTPUT QUEUE                             */
/*                         IF THIS IS BLANK AT START, PROMPT        */
/*                         PGM IS CALLED; OTHERWISE, THE PRINT CL   */
/*                         IS SUBMITTED WITHOUT PROMPTING           */
/*     B    &HOLD       -  HOLD STATUS                              */
/*     B    &SAVE       -  SAVE STATUS                              */
/*     B    &NQSTAT     -  INDICATOR WHETHER MUST RUN IN NIGHT Q    */
/*     B    &NIGHTQ     -  NIGHT JOB QUEUE                          */
/*                                                                  */
/********************************************************************/

 PPVPUPC:    PGM        PARM(&RETURN &APPLIC &DESC &OUTQ &HOLD &SAVE +
                          &NQSTAT &NIGHTQ &COMPNYN &COPYN &PGM)
/* PARMS                                                                      */
        DCL    &RETURN     *CHAR   (7)           /* Return Code               */
        DCL    &APPLIC     *CHAR   (06)          /* Application Code          */
        DCL    &PGM        *CHAR   (10)          /* CL to Submit              */
        DCL    &COMPNYN    *DEC                  /* Company Number (Passed)   */
        DCL    &DESC       *CHAR   (25)          /* PRT Pgm Description       */
        DCL    &COPYN      *DEC                  /* No. of Copies             */
        DCL    &OUTQ       *CHAR   (10)          /* Output Queue              */
        DCL    &HOLD       *CHAR   (4)           /* Hold Status               */
        DCL    &SAVE       *CHAR   (4)           /* Save Status               */
        DCL    &NQSTAT     *CHAR   (01)          /* If MUST run in Night Q    */
        DCL    &NIGHTQ     *CHAR   (10)          /* Night Job Queue           */
        DCL    &SLSRTE     *CHAR   (3)           /* SALES ROUTE               */

/* INTERNAL FIELDS                                                            */
        DCL    &COPY       *DEC    (2 0)         /* Number of copies          */
        DCL    &COMPANY    *DEC    (3 0)         /* Company Number            */

/* ERROR MESSAGE FIELDS                                                       */
        DCL    &MSG        *CHAR   (512)         /* message                   */
        DCL    &MSGID      *CHAR   (7)           /* message id                */
        DCL    &MSGL       *DEC    (5 0)

/********************************************************************/
/*   INITIALIZE VARIABLES                                           */
/********************************************************************/

             CHGVAR     VAR(&RETURN) VALUE('       ')
             CHGVAR     VAR(&COPY) VALUE(&COPYN)
             CHGVAR     VAR(&COMPANY) VALUE(&COMPNYN)
/********************************************************************/
/*   MONITOR FOR ALL MESSAGES AT PROGRAM LEVEL                      */
/********************************************************************/

             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(MSGTRAP))

             CALL       PGM(PDG0PVR) PARM(&RETURN &DESC &OUTQ &HOLD +
                          &SAVE &COPY &NQSTAT &NIGHTQ &APPLIC)
             CHGVAR     VAR(&COPYN) VALUE(&COPY)
             IF         COND(&RETURN *NE ' ') THEN(GOTO +
                          CMDLBL(USERQUIT))

/********************************************************************/
/*   SUBMIT PRINT CL PROGRAM                                        */
/********************************************************************/

 SUBMIT:     SBMJOB     CMD(CALL PGM(&PGM) PARM(&RETURN &APPLIC +
                          &DESC &OUTQ &HOLD &SAVE &NQSTAT &NIGHTQ +
                          &COMPNYN &COPYN)) JOB(&PGM) +
                          JOBQ(&NIGHTQ)
             GOTO       CMDLBL(END)

/*-------------------------USER QUIT--------------------------------*/
 USERQUIT:   CHGVAR     VAR(&RETURN) VALUE('Y2U0005')
             GOTO       CMDLBL(END)

/*----------------------RECEIVE MESSAGES----------------------------*/

 MSGTRAP:    RCVMSG     MSGTYPE(*EXCP) MSGDTA(&MSG) MSGDTALEN(&MSGL) +
                          MSGID(&MSGID)
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(END))

             SNDPGMMSG  MSGID(&MSGID) MSGF(QCPFMSG) MSGDTA(%SST(&MSG +
                          1 &MSGL)) MSGTYPE(*ESCAPE)
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(END))

 END:        ENDPGM

