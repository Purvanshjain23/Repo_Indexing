     A*%%TS  SD  20050127  094108  ISLRAMS     REL-V5R2M0  5722-WDS
     A*****************************************************************
     A*
     A*  SYSTEM:        HOG PRODUCTION
     A*  DISPLAY FILE:  HPD5051 - SELECT SIRE LINE COMPANY/PRIME LINE
     A*  CREATED BY:    LEANNE FEDOR
     A*  DATE:          01/27/05
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      PRINT
     A                                      CA12(12 'PREVIOUS')
     A N82                                  ROLLUP(25 'PAGE FORWARD')
     A*---------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP5051S1                  SFL
     A*%%TS  SD  20050127  094108  ISLRAMS     REL-V5R2M0  5722-WDS
     A            HDRRN1    R        H      REFFLD(@@RRN)
     A            D1SICOMP  R        O  7  5REFFLD(HSRREF/@@SICOMP *LIBL/HSPREF)
     A            D1PRIME   R        O  7 14REFFLD(HSRREF/@@PRIME *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A            D1SICD    R        O  7 22REFFLD(@@SICD)
     A            D1AIST    R        O  7 33REFFLD(HSRREF/@@AIST *LIBL/HSPREF)
     A            D1SIDS    R        O  7 40REFFLD(HSRREF/@@SIDS *LIBL/HSPREF)
     A*
     A*---------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP5051C1                  SFLCTL(HP5051S1)
     A*%%TS  SD  20050127  094108  ISLRAMS     REL-V5R2M0  5722-WDS
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0008)
     A                                      WINDOW(HP5051EC)
     A                                      OVERLAY
     A                                      SFLCSRRRN(&HDRRN)
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A            HDPAG1    R        H      REFFLD(@@PAG)
     A                                      SFLRCDNBR
     A            HDRRN     R        H      REFFLD(@@RRN)
     A            SDPGM     R        O  1  1REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 18'SELECT SIRE LINE COMPANY/PRIME LIN-
     A                                      E'
     A                                      DSPATR(HI)
     A            C1DESC    R        O  2 20REFFLD(@@DS30)
     A                                      DSPATR(HI)
     A                                  3  2'Position Cursor, Press Enter'
     A                                  4 30'Active/'
     A                                      DSPATR(HI)
     A                                  5  2'Sire Line'
     A                                      DSPATR(HI)
     A                                  5 14'Prime'
     A                                      DSPATR(HI)
     A                                  5 22'Sire'
     A                                      DSPATR(HI)
     A                                  5 30'Inactive'
     A                                      DSPATR(HI)
     A                                  6  2'Company'
     A                                      DSPATR(HI)
     A                                  6 14'Line'
     A                                      DSPATR(HI)
     A                                  6 22'Line'
     A                                      DSPATR(HI)
     A                                  6 31'Status'
     A                                      DSPATR(HI)
     A                                  6 40'Description'
     A                                      DSPATR(HI)
     A*---------------------------------------------------------------
     A* COMMAND LINE LINE
     A*---------------------------------------------------------------
     A          R HP5051K1
     A                                      WINDOW(HP5051EC)
     A                                      OVERLAY
     A  43                             15  1'Message program failed. Call IS.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 16  1'F12=Previous'
      *
     A*-------------------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
     A*-------------------------------------------------------------------------
     A          R HP5051ES                  SFL SFLMSGRCD(17)
      *
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
     A*
     A*-------------------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*-------------------------------------------------------------------------
     A*
     A          R HP5051EC                  SFLCTL(HP5051ES)
     A*%%TS  SD  19980327  105628  ISLRAMS     REL-V3R7M0  5716-PW1
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A                                      WINDOW(*DFT 18 70)
     A                                      WDWBORDER((*DSPATR RI) (*CHAR '    -
     A                                          '))
     A            MSGPGM                    SFLPGMQ(10)
     A*
     A*---------------------------------------------------------------
     A* ASSUME FORMAT -- REQUIRED FOR WINDOWS
     A*---------------------------------------------------------------
     A          R ASSUME
     A*%%TS  SD  20011025  160559  ISLRAMS     REL-V4R4M0  5769-PW1
     A                                      ASSUME
     A                                      OVERLAY
     A                                 24 79' '
