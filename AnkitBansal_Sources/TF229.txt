      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Triumph Foods
      * PROGRAM:     TF229 - Eopfees: Carrier Invoice Close--Automated
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     04/10/06
      *
      * Function:    We initially wrote on-demand programs for Traffic to use to
      *              "freeze/unfreeze" load data for a Period.
      *
      *              We have kept the on-demand programs for I.S. to use.
      *
      *              This new program will be called from the Weekly Revenue Close to
      *              automate the "freeze" when the Revenue Close is for the last week
      *              in a period. This program:
      *                     1) checks to see if it is time to "freeze" and
      *                     2) sets the LDA to match the requirements of the on-demand
      *                        programs---so we don't have 2 sets of "freeze" programs.
      *
      ****************************************************************************************
      * MODIFICATIONS:
      ****************************************************************************************
      * DATE      PROGRAMMER
      *
      * 11/22/06  LeAnne Fedor
      *           Recompile only.
      *           Added more fields to Freight Variance Detail file. Changed field name/text
      *           for "actual freight amount" to "allocated actual freight amount".
      *
      * 07/17/08  LeAnne Ramsey
      *           Recompile only.
      *           In file TFP027-Freight Variance Detail, we:
      *              Replaced: FDAACTFAM  Allocated Actual Freight Amount
      *                  With: FDALFAM    Allocated Load Total Freight Amount
      *
      * 03/10/09  LeAnne Ramsey
      *           Recompile only.
      *           The users want to revert back to Allocated Actual Freight Amount
      *           from Allocated Load Total Freight Amount.
      *           This will reverse the change of 07/17/08 above!
      *           We have decided to keep both of the fields in file TFP027-Freight
      *           Variance Detail:
      *              FDALFAM    Allocated Load Total Freight Amount
      *              FDAACTFAM  Allocated Actual Freight Amount
      /eject
      ****************************************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************************************
      *
     Ftfl027a   if   e           k disk
      *    Freight variance detail
      *
      *
     Ftfp099    if   e           k disk
      *    Function control
      *
      /eject
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      *
      ****************************************************************
      * ARRAYS AND TABLES
      ****************************************************************
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Standard fields
      *
     D wkcedt          s                   like(fncedt)
      *
      *
      * Parms
      *
     D xxfreezefl      s              1
      *
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *---------------------------------------------------------------
      * Local data area
      *---------------------------------------------------------------
      *
     d lda            uds                  dtaara(*lda)
     D  ldcyr                  2      5  0
     D  ldcpe                  6      7  0
      *
     D  ldcbdt                 8     15  0
     D  ldcbmdy               16     21  0
     D  ldcbsyn               22     28  0
      *
     D  ldcedt                29     36  0
     D  ldcemdy               37     42  0
     D  ldcesyn               43     49  0
      *
      *---------------------------------------------------------------
      *  Definition for external data area 'DATFLOAD'
      *---------------------------------------------------------------
     D
     D datfload        ds                  dtaara(datfload)
     D  dafl                   1      1
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      **********************************************************************************
      * Mainline
      **********************************************************************************
      *
      * Retrieve/compare the "Current End Dates" from the Control file for:
      *   1) the Revenue function
      *   2) the EOP function
      *
      *
     C     'REVENUE   '  chain     tfp099                             92
     C                   if        *in92 = *off                                 If rev hit
     C                   z-add     fncedt        wkcedt
      *
     C     'EOPFEES   '  chain     tfp099                             92
     C                   if        *in92 = *off and                             If freeze time
     C                             fncedt = wkcedt
      *
      * Validate that there are not already "frozen" records for
      * this Period.
      *
     C     key01         chain     tfl027a                            92
     C                   if        *in92 = *on                                  If no recs
      *
      * Validate that a manual "freeze" is not running.
      *
     C                   in        datfload
      *
     C                   if        datfload = *blank                            If no manual
     C     *lock         in        datfload
     C                   move      yes           dafl
     C                   out       datfload
      *
      * Set LDA values
     C                   z-add     fncyr         ldcyr
     C                   z-add     fncpe         ldcpe
      *
     C                   z-add     fncbdt        ldcbdt
     C                   z-add     fncbmdy       ldcbmdy
     C                   z-add     fncbsyn       ldcbsyn
      *
     C                   z-add     fncedt        ldcedt
     C                   z-add     fncemdy       ldcemdy
     C                   z-add     fncesyn       ldcesyn
      *
     C                   out       lda
      *
     C                   move      yes           xxfreezefl
     C                   endif                                                  If no manual
     C                   endif                                                  If no recs
      *
     C                   endif                                                  If freeze time
     C                   endif                                                  If rev hit
      *
     C                   seton                                        lr
      *
      /eject
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
     C     *entry        plist
     C                   parm                    xxfreezefl
      *
      * Key lists
      *
     C     key01         klist
     C                   kfld                    fncyr
     C                   kfld                    fncpe
      *
     C                   endsr
      /eject
