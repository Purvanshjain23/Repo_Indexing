
/***************************************************************** */
/*  PROGRAM NUMBER --> POO3UPR                                     */
/*  PROGRAM NAME ----> WEIGHT DISTRIBUTION F/CARC DETAIL, EMAIL */
/*                                                                 */
/*  PROGRAMMER ------> ROSE CENTONZE                               */
/*  DATE ------------> 02/07/02                                    */
/*                                                                 */
/*                                                                 */
/*  PROGRAM PURPOSE -> THIS CL BUILDS THE WORKFILE CONTAINING   */
/*                     WEIGHT RANGES PER KILL DATE/SHIFT/SOURCE.   */
/*                     NEXT, IT UPDATES THE HEAD COUNTS ON THE     */
/*                     CORRESPONDING RECORD FROM THE CARCASS DTL.  */
/*                     THEN THE FILE IS EMAILED TO THE ADDR FROM   */
/*                     THE PROMPT (PKX2PVR)                        */
/***************************************************************** */
/*  MODIFIED:                                                      */
/*                                                                 */
/***************************************************************** */

             PGM        PARM(&RTN &COMPA &FDATEA &TDATEA &CUTOUT &EMAIL)
        DCL    &RTN       *CHAR   (7)
        DCL    &COMP      *DEC    (3 0)           /*  COMPANY #     */
        DCL    &COMPA     *CHAR (3)              /*  FOR PASSING   */
        DCL    &FDATE     *DEC    (7 0)          /*  FROM DATE      */
        DCL    &FDATEA    *CHAR   (7)          /*  FROM DATE      */
        DCL    &TDATE     *DEC    (7 0)          /*  TO  DATE      */
        DCL    &TDATEA    *CHAR   (7)          /*  TO DATE      */
        DCL    &CUTOUT    *CHAR   (6)          /*  CUTOUT WEIGHT RANGE */
        DCL    &EMAIL     *CHAR   (50)
             DCL        VAR(&SEL)  TYPE(*CHAR) LEN(1500)

             CHGVAR    VAR(&COMP) VALUE(&COMPA) /* Change to length +
                          for print program */
             CHGVAR    VAR(&FDATE) VALUE(&FDATEA)
             CHGVAR    VAR(&TDATE) VALUE(&TDATEA) /* Change to length +
                          for print program */

/***************************************************************** */
/*     DUP WORKFILE IN QTEMP                                       */
/***************************************************************** */
             CRTDUPOBJL OBJ(POBVCPP) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)
             CRTDUPOBJL OBJ(POBVCPL0) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)
             CRTDUPOBJL OBJ(POBVCPL1) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)
             CRTDUPOBJL OBJ(POBVCPL2) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)
/***************************************************************** */
/*     CREATE WORKFILE RECORDS PER WEIGHT RANGE, FOR KILL DATE RANGE */
/*     BOTH SHIFTS (1,2) AND BOTH SOURCE TYPES (I/E)               */
/***************************************************************** */
             CALL       PGM(POO1XFR) PARM(' ' &COMP &CUTOUT &FDATE +
                          &TDATE)
/***************************************************************** */
/*     UPDATE WORKFILE RECORD HEAD COUNTS FROM THE CARCASS DETAIL  */
/***************************************************************** */

             CALL       PGM(PKX1XFR) PARM(' ' &COMP &CUTOUT &FDATE +
                          &TDATE)

/***************************************************************** */
/*     EMAIL THE FILE TO EMAIL ADDRESS                             */
/***************************************************************** */
             ADDLIBLE   LIB(SEQUEL)
             MONMSG     MSGID(CPF0000)

             CHGVAR     VAR(&SEL) VALUE('SELECT  BVBNC3, BVGGDT +
                          COLHDG("KILL DATE"), BVMASX +
                          COLHDG("SOURCE"),  BVDMAA +
                          COLHDG("SHIFT"),BVOSNX COLHDG("MIN +
                          CARCASS" "WEIGHT"), BVOTNX COLHDG("MAX CARCASS" +
                          "WEIGHT"), BVVSNB COLHDG("MIN LIVE" +
                          "WEIGHT"), BVVVNB COLHDG("MAX LIVE" +
                          "WEIGHT") ,BVORNX COLHDG("HEAD" +
                          "COUNT")  FROM POBVCPP ORDER BY +
                          BVBNC3,BVGGDT,BVMASX,BVDMAA')

             EXECUTE    SQL(&SEL) MBROPT(*REPLACE) NBRRCDS(*ALL) +
                          KEYFLDCNT(*ALL) ALWNULL(*NO) PCFMT(*XLS) +
                          REPLACE(*YES) TEXT('WEIGHT RANGE +
                          DISTRIBUTION') RECIPIENT(&EMAIL) +
                          EMLMSG('Weight Range Distribution') +
                          OPTIMIZE(*TOTAL) ALWCPY(*YES) MSG(*YES) +
                          UNIQUEKEY(*NONE) IGNDECERR(*NO)

             ENDPGM

