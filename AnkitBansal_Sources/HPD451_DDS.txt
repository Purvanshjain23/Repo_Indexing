     A*%%TS  SD  20000202  154347  ISLRAMS     REL-V4R2M0  5769-PW1
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD451
     A*  TITLE:         SPECIFY PRINT OPTIONS
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  DATE:          04/18/96
     A*
     A*  NOTE:  THIS IS A POP-UP WINDOW. THE WINDOW DEFINITION IS IN
     A*         RECORD FORMAT HP451EC.
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CF04(04 'PROMPT')
     A                                      CF12(12 'PREVIOUS')
     A*
     A*----------------------------------------------------------------
     A* RECORD FORMAT ONE
     A*----------------------------------------------------------------
     A          R HP451R1
     A*%%TS  SD  20000202  154347  ISLRAMS     REL-V4R2M0  5769-PW1
     A                                      WINDOW(HP451EC)
     A                                      TEXT('INPUT SCREEN')
     A                                      SETOF(33)
     A                                      SETOF(34)
     A                                      SETOF(35)
     A                                      SETOF(36)
     A                                      SETOF(54)
     A                                      SETOF(56)
     A                                      OVERLAY
     A                                      RTNCSRLOC(&RCD &FLD)
     A            RCD           10A  H
     A            FLD           10A  H
     A                                  1  8'SPECIFY PRINT OPTIONS'
     A                                      DSPATR(HI)
     A                                  3  1'Number of copies ......'
     A            DFCOPY    R     Y  B  3 27REFFLD(@@COPY)
     A                                      CHECK(FE)
     A                                      EDTCDE(Z)
     A N33                                  DSPATR(HI)
     A  33                                  DSPATR(RI)
     A  33                                  DSPATR(PC)
     A                                  4  1'Hold printed output ...'
     A                                  4 25'?'
     A            DFHOLD    R        B  4 27REFFLD(@@HOLD)
     A N34                                  DSPATR(HI)
     A  34                                  DSPATR(RI)
     A  34
     AO 54                                  DSPATR(PC)
     A                                  5  1'Send output to queue ..'
     A            DFOUTQ    R        B  5 27REFFLD(@@OUTQ)
     A N35                                  DSPATR(HI)
     A  35                                  DSPATR(RI)
     A  35                                  DSPATR(PC)
     A                                  6  1'Run in night jobq .....'
     A                                  6 25'?'
     A            DFNITE    R        B  6 27REFFLD(@@FLAG)
     A N36                                  DSPATR(HI)
     A  36                                  DSPATR(RI)
     A  36
     AO 56                                  DSPATR(PC)
     A*
     A*----------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*----------------------------------------------------------------
     A          R HP451K1
     A                                      TEXT('COMMAND KEYS')
     A                                      WINDOW(HP451EC)
     A                                      OVERLAY
     A  43                              7  2'Message program failed. Call IS.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                  8  1'F4=Prompt'
     A                                  8 12'F12=Previous'
      *
      *----------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *----------------------------------------------------------------
     A          R HP451ES
     A                                      TEXT('Error message subfile')
     A                                      SFL SFLMSGRCD(09)
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
     A*
     A*----------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT WITH WINDOW DEFINITION
     A*----------------------------------------------------------------
     A          R HP451EC
     A                                      TEXT('Error message subfile')
     A                                      SFLCTL(HP451ES)
     A                                      WINDOW(*DFT 10 37)
     A                                      WDWBORDER((*DSPATR RI) (*CHAR '    -
     A                                          '))
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
      *
      *----------------------------------------------------------------
      * ASSUME FORMAT - REQUIRED FOR WINDOWS
      *----------------------------------------------------------------
      *
     A          R ASSUME
     A                                      TEXT('ASSUME FORMAT FOR WINDOWS')
     A                                      ASSUME OVERLAY
     A                                 24 79' '
