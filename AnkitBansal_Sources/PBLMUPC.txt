/*********************************************************************/
/* PROGRAM    : PBLMUPC                                              */
/* TITLE      : CREATE MANIFEST SALES RPT AND EMAIL                  */
/* DATE       : 01/15/2014                                           */
/* Description: CALL THE PRINT MANIFEST SALES REPORT THEN EMAIL      */
/*                TO THE RECIPIENT                                   */
/*                                                                   */
/*  Parameters:                                                      */
/*  Usage   Field       Field Description                            */
/*  -----   ---------   ------------------------------------------   */
/*    I     &CMPNY      COMPANY NUMBER 15.5                          */
/*    I     &ORDER      ORDER NUMBER 15.5                            */
/*    I     &NSUBJECT   EMAIL SUBJECT (LIMIT OF 60 CHAR)             */
/*    I     &MANTYPE    MANIFEST TYPE                                */
/*    I     &EADDRESS   EMAIL ADDRESS TO SEND TO                     */
/*    I     &ESUBJECT   EMAIL SUBJECT (LIMIT OF 60 CHAR)             */
/*    I     &EMESSAGE   MESSAGE TO SEND                              */
/*********************************************************************/
/* Modifications:                                                    */
/*-------------------------------------------------------------------*/
/*   Date     Pgmr     Description                                   */
/* -------- ---------- --------------------------------------------  */
/*                                                                   */
/*********************************************************************/
 PBLMUPC:    PGM        PARM(&RETURN &OUTQ &HOLD &SAVE &CMPNYN +
                          &ORDERN &COPYN &MANTYPE &EADDRESS +
                          &ESUBJECT &EMESSAGE &SPLF)

/* Declare Variables                                                 */
             DCL        VAR(&RETURN)   TYPE(*CHAR) LEN(7)
             DCL        VAR(&SPLF)     TYPE(*CHAR) LEN(10)
             DCL        VAR(&EADDRESS) TYPE(*CHAR) LEN(50)
             DCL        VAR(&ESUBJECT) TYPE(*CHAR) LEN(60)
             DCL        VAR(&EMESSAGE) TYPE(*CHAR) LEN(132)
             DCL        VAR(&CMPNYN)   TYPE(*DEC)
             DCL        VAR(&COMP)     TYPE(*DEC)  LEN(3 0)
             DCL        VAR(&ORDERN)   TYPE(*DEC)
             DCL        VAR(&ORD#)     TYPE(*DEC)  LEN(7 0)
             DCL        VAR(&MANTYPE)  TYPE(*CHAR) LEN(2)
             DCL        VAR(&APPLIC) TYPE(*CHAR) LEN(06)
             DCL        VAR(&OUTQ) TYPE(*CHAR) LEN(10)
             DCL        VAR(&HOLD) TYPE(*CHAR) LEN(04)
             DCL        VAR(&SAVE) TYPE(*CHAR) LEN(04)
             DCL        VAR(&NIGHTQ) TYPE(*CHAR) LEN(10)
             DCL        VAR(&COPYN) TYPE(*DEC)
             DCL        VAR(&COPY) TYPE(*DEC) LEN(2 0)

/* ERROR MESSAGE FIELDS                                                       */
             DCL        VAR(&MSG)   TYPE(*CHAR) LEN(512)
             DCL        VAR(&MSGID) TYPE(*CHAR) LEN(7)
             DCL        VAR(&MSGL)  TYPE(*DEC) LEN(5 0)
             DCL        VAR(&MSG2)  TYPE(*CHAR) LEN(512)

             MONMSG     MSGID(CPF9999 EML9999 CPF0000 EML0000) +
                          EXEC(GOTO CMDLBL(MSGTRAP))

             CHGVAR     VAR(&COMP) VALUE(&CMPNYN)
             CHGVAR     VAR(&ORD#) VALUE(&ORDERN)
             CHGVAR     VAR(&COPY) VALUE(&COPYN)

/*-------------------------------------------------------------------*/
/* CREATE THE REPORT, THEN EMAIL                                     */
/*-------------------------------------------------------------------*/
             OVRPRTF    FILE(OMD0PFR$) OUTQ(&OUTQ) COPIES(&COPY) +
                          HOLD(&HOLD) SAVE(&SAVE)

             OVRPRTF    FILE(OMDZPFR$) OUTQ(&OUTQ) COPIES(&COPY) +
                          HOLD(&HOLD) SAVE(&SAVE)

/* CUSTOMER DETAIL */
             IF         COND(&MANTYPE *EQ 'C' *OR &MANTYPE *EQ +
                          'CD') THEN(DO)
             CALL       PGM(OMDZPFR) PARM(' ' &COMP &ORD# 'CD')

             ESEND/ESNDMAIL RECIPIENT(&EADDRESS) SUBJECT(&ESUBJECT) +
                          MSG(&EMESSAGE) ATTLIST((OMDZPFR$ *PDF *N +
                          OMDZPFR$))
             ENDDO

/* SALES SUMMARY   */
             IF         COND(&MANTYPE *EQ 'SD' *OR &MANTYPE *EQ +
                          'SS') THEN(DO)
             CALL       PGM(OMD0PFR) PARM(' ' &COMP &ORD# &MANTYPE)

             ESEND/ESNDMAIL RECIPIENT(&EADDRESS) SUBJECT(&ESUBJECT) +
                          MSG(&EMESSAGE) ATTLIST((OMD0PFR$ *PDF *N +
                          OMD0PFR$))
             ENDDO


             GOTO       CMDLBL(ENDPGM)

/*----------------------RECEIVE MESSAGES----------------------------*/
 MSGTRAP:    CHGVAR     VAR(&RETURN) VALUE('ESEND_ERR')
             SNDMSG     MSG('ERROR IN ESNDMAIL FOR ' *CAT &EMESSAGE +
                          *BCAT 'in program PBLMUPC') TOUSR(SEABOARD)
             CHGJOB     LOG(4 0 *SECLVL)

 ENDPGM:     ENDPGM
