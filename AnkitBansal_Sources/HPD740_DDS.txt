     A*%%TS  SD  20090707  140132  ISLRAMS     REL-V5R4M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION
     A*  DISPLAY FILE:  HPD740
     A*  TITLE:         VIEW HOG GROUP
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  DATE:          04/06/94
     A*
     A*
     A*  INDICATOR 65 IS ON WHEN GROUP HAS BEEN VOIDED
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A                                      CF05(05 'REFRESH')
     A                                      CA12(12 'PREVIOUS')
     A*
     A*--------------------------------------------------------------*
     A* RECORD FORMAT ONE
     A*--------------------------------------------------------------*
     A          R HP740R1
     A*%%TS  SD  20090707  140132  ISLRAMS     REL-V5R4M0  5722-WDS
     A                                      OVERLAY
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 28'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  2 29'VIEW HOG GROUP'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  4  2'Business office ...........:'
     A            DFFSBO    R        O  4 31REFFLD(HGFSBO)
     A                                      DSPATR(HI)
     A            DFBODS    R        O  4 40REFFLD(BOBODS)
     A                                  5  2'Hog group code and number .:'
     A            DFHGCD    R        O  5 31REFFLD(HGHGCD)
     A                                      DSPATR(HI)
     A            DFHGSN    R        O  5 40REFFLD(HGHGSN)
     A                                      EDTCDE(M)
     A                                  6  2'Group status code .:'
     A            DFGSCD    R        O  6 23REFFLD(HGGSCD)
     A                                      DSPATR(HI)
     A                                  7  2'Farm site .........:'
     A            DFFSCD    R        O  7 23REFFLD(HGFSCD)
     A                                      EDTCDE(M)
     A                                      DSPATR(HI)
     A            DFFSNM    R        O  7 35REFFLD(FSFSNM)
     A                                  8  2'Building and room .:'
     A            DFBLCD    R        O  8 23REFFLD(HGBLCD)
     A                                      DSPATR(HI)
     A            DFRMCD    R        O  8 29REFFLD(HGRMCD)
     A                                      DSPATR(HI)
     A                                  9  2'Production phase ..:'
     A            DFPPCD    R        O  9 23REFFLD(HSRREF/@@PPCD *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A            DFPPDS    R        O  9 35REFFLD(HSRREF/PPPPDS *LIBL/HSPREF)
     A                                 10  2'Production type ...:'
     A            DFPTCD    R        O 10 23REFFLD(HSRREF/@@PTCD *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A            DFPTDS    R        O 10 35REFFLD(PTREC/PTPTDS *LIBL/HSP010)
     A                                 12  2'Group type ........:'
     A            DFGTCD    R        O 12 23REFFLD(@@GTCD)
     A                                      DSPATR(HI)
     A            DFGTDS    R        O 12 26REFFLD(GTGTDS)
     A                                 12 47'Breed stock ...:'
     A            DFBSCD    R        O 12 64REFFLD(HGBSCD)
     A                                      DSPATR(HI)
     A                                 13  2'Stocking density ..:'
     A            DFSDCD    R        O 13 23REFFLD(HSRREF/@@SDCD *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A            DFSDDS    R        O 13 26REFFLD(HSRREF/@@SDDS *LIBL/HSPREF)
     A                                 13 47'Genetic line ..:'
     A            DFGNCD    R        O 13 64REFFLD(HGGNCD)
     A                                      DSPATR(HI)
     A                                 14  2'Stayed/moved code .:'
     A            DFSMCD    R        O 14 23REFFLD(HSRREF/@@SMCD *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A            DFSMDS    R        O 14 26REFFLD(HSRREF/@@SMDS *LIBL/HSPREF)
     A                                 14 47'Sex ...........:'
     A            DFSXCD    R        O 14 64REFFLD(HGSXCD)
     A                                      DSPATR(HI)
     A            DFSXDS    R        O 14 70REFFLD(SXSXDS)
     A                                 15  2'Continuous flow ...:'
     A            DFCFFL    R        O 15 23REFFLD(HSRREF/@@CFFL *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A                                 15 47'Feed group ....:'
     A            DFFGCD    R        O 15 64REFFLD(HSRREF/@@FGCD *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A            DFFGDS    R        O 15 70REFFLD(HSRREF/@@FGDS *LIBL/HSPREF)
     A                                 16  2'Average days old In/Out ...:'
     A            DFADOI    R        O 16 31REFFLD(HGADOI HSPREF)
     A                                      EDTCDE(K)
     A                                      DSPATR(HI)
     A                                 16 36'/'
     A            DFADOO    R        O 16 38REFFLD(HGADOO)
     A                                      EDTCDE(K)
     A                                      DSPATR(HI)
     A                                 17  2'Origin BGF farm site ......:'
     A            DFOBFS    R        O 17 31REFFLD(HGOBFS)
     A                                      EDTCDE(M)
     A                                      DSPATR(HI)
     A            DFOBNM    R        O 17 38REFFLD(FSFSNM)
     A                                 18  2'Origin nursery farm site ..:'
     A            DFONFS    R        O 18 31REFFLD(HGONFS)
     A                                      EDTCDE(M)
     A                                      DSPATR(HI)
     A            DFONNM    R        O 18 38REFFLD(FSFSNM)
     A                                 20  2'Date created'
     A                                 20 18'Date opened'
     A                                 20 33'Date disposed'
     A                                 20 49'Date closed'
     A  65                             21  2'Date voided ...............:'
     A            DFCRDT    R        O 21  3REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  ')
     A                                      DSPATR(HI)
     A            DFOPDT    R        O 21 20REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  ')
     A                                      DSPATR(HI)
     A  65        DFVDDT    R        O 21 32REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  ')
     A                                      DSPATR(HI)
     A            DFDSDT    R        O 21 36REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  ')
     A                                      DSPATR(HI)
     A N65        DFCLDT    R        O 21 51REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  ')
     A                                      DSPATR(HI)
     A*
     A*-------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*--------------------------------------------------------------
     A          R HP740K1
     A*%%TS  SD  19940412  161222  ISLRAMS     REL-V2R2M0  5738-PW1
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A                                 23 11'F5=Refresh'
     A                                 23 23'F12=Previous'
      *
      *--------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *--------------------------------------------------------------
     A          R HP740ES                   SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
      *-------------------------------------------------------------
      * ERROR MESSAGE SUBFILE CONTROL FORMAT
      *--------------------------------------------------------------
     A          R HP740EC                   SFLCTL(HP740ES)
      *
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
