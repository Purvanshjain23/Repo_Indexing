      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      HOG PRODUCTION
      * PROGRAM:     HP609
      * TITLE:       LISTING OF BREED STOCKS
      * PROGRAMMER:  MARY PALMER
      * CREATED:     04/08/94
      *
      * FUNCTION: BATCH PROGRAM TO LIST THE RECORDS IN THE
      *           BREED STOCK MASTER FILE.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      * 04/20/95  LEANNE FEDOR
      *           ADDED USER NAME TO REPORT HEADINGS.
      *
      * 07/31/00  LeAnne Fedor
      *           Removed all reference to 'genetic lines'.
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     FHSP031    IF   E           K DISK
      *    BREED STOCK               (KEY: BSBSCD)
      *
     FQPRINT    O    F  132        PRINTER OFLIND(*INOF)
      *
      /EJECT
      ****************************************************************
      * INPUT SPECS
      ****************************************************************
      *
      *---------------------------------------------------------------
      * STANDARD PROGRAM STATUS DATA STRUCTURE
      *---------------------------------------------------------------
      *    EXTERNALLY DEFINED AS UTPGFR (RECORD FORMAT: PGMDSR)
     D PGMDS         ESDS                  EXTNAME(UTPGFR)
      *
      *---------------------------------------------------------------
      *  NAMED CONSTANTS
      *---------------------------------------------------------------
      *
     D YES             C                   CONST('Y')
     D NO              C                   CONST('N')
      /EJECT
      ****************************************************************
      * MAIN LINE
      ****************************************************************
      *
      * PREPARE TO READ THE BREED STOCK MASTER FILE AND
      * PRINT HEADINGS FOR FIRST TIME THROUGH
      *
     C     *LOVAL        SETLL     HSP031
     C                   EXCEPT    HRHDR
      *
      * READ AND PRINT ALL RECORDS IN THE BREED STOCK MASTER.
      * FOR EACH RECORD READ, RETRIEVE THE GENETIC LINES ASSIGNED TO
      * THAT BREED STOCK FROM THE GENETIC LINE MASTER.
      *
     C     *IN92         DOUEQ     *ON                                          MAIN DO
     C                   READ      HSP031                                 92
     C     *IN92         IFEQ      *OFF                                         IF NOT EOF
      *
     C     *INOF         IFEQ      *ON
     C                   EXCEPT    HRHDR
     C                   ENDIF
      *
      * PRINT BREED STOCK LINE
      *
     C                   EXCEPT    DTBS
     C                   MOVEL     YES           PRTFLG
      *
     C                   ENDIF                                                  IF NOT EOF
     C                   ENDDO                                                  MAIN DO
      *
      *---------------------------------------------------------------
      * EOF PROCESSING
      *---------------------------------------------------------------
      *  IF THE MASTER FILE WAS EMPTY AND NO RECORDS WERE PRINTED,
      *  PRINT THE STANDARD 'NO DATA...' ERROR MESSAGE
      *
     C     PRTFLG        IFEQ      NO
     C                   EXCEPT    NODATA
     C                   ENDIF
      *
     C                   SETON                                        LR
      /EJECT
      *---------------------------------------------------------------
      * *INZSR - INITIALIZATION SUBROUTINE
      *---------------------------------------------------------------
      *
     C     *INZSR        BEGSR
      *
      * SET PRINT FLAG TO DETERMINE IF ANY RECORDS WERE PRINTED
      * RETRIEVE TIME FOR REPORT HEADING
      *
     C                   MOVEL     NO            PRTFLG            1
     C                   MOVE      *ALL'-'       DASH             80
     C                   TIME                    RTIME             6 0
      *
     C                   ENDSR
      /EJECT
      *-------------------------------------------------------------
      * REPORT HEADING LINES
      *-------------------------------------------------------------
      *
     OQPRINT    E            HRHDR          1 01
     O                       SDPGM               10
     O                                           48 'HOG PRODUCTION SYSTEM'
     O                                           70 'DATE'
     O                       UDATE         Y     80
      *
     O          E            HRHDR          1
     O                       SDUSR               10
     O                                           49 'LISTING OF BREED STOCK'
     O                                           70 'TIME'
     O                       RTIME               80 '  :  :  '
      *
     O          E            HRHDR          2
     O                                           70 'PAGE'
     O                       PAGE          Z     80
      *
     O          E            HRHDR          1
     O                                            5 'BREED'
     O                                           19 'BREED STOCK'
     O                                           36 'MIXED'
      *
     O          E            HRHDR          1
     O                                            5 'STOCK'
     O                                           19 'DESCRIPTION'
     O                                           36 'BREED'
      *
      *
     O          E            HRHDR          1
     O                       DASH                80
      *
      *-------------------------------------------------------------
      * BREED STOCK DETAIL LINE
      *-------------------------------------------------------------
      *
     O          E            DTBS        1
     O                       BSBSCD               5
     O                       BSBSDS              28
     O                       BSMBFL              34
      *
      *-------------------------------------------------------------
      * NO DATA MESSAGE LINE
      *-------------------------------------------------------------
      *
     O          E            NODATA      1
     O                                           19 'NO DATA MEETS YOUR'
     O                                           38 'SELECTION CRITERIA'
