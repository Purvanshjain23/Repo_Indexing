     H/TITLE EUP Crt ACCR TRANS RCD    Execute user program
     H DATFMT(*YMD) DATEDIT(*YMD) DEBUG(*YES)
     Y* ADDLIBLE LIB(DFDATA) POSITION(*LAST)
     Z* DFTACTGRP(*NO) BNDDIR(YBNDDIR) DBGVIEW(*SOURCE)
     Z* CVTOPT(*DATETIME) ACTGRP(*CALLER) OPTIMIZE(*BASIC)
      *
     FACCRUALS  uf a e           k disk
      *  JRD Customer Accruals Transaction File
      *
      * Data structures:
     D PGMDS         ESDS                  EXTNAME(Y2PGDSP)
      * Modified Program data structure
     D JBDTTM          DS
      * Job date/time
     D  ##JDT                  1      7  0
     D  ##JCC                  1      1  0
     D  ##JYY                  2      3  0
     D  ##JMM                  4      5  0
     D  ##JDD                  6      7  0
     D  ##JTM                  8     13  0
     D  ##JHH                  8      9  0
     D  ##JNN                 10     11  0
     D  ##JSS                 12     13  0
      *
      * Declared stand-alone variables
     D  P1BKPR         S              9  6
     D  P1AYVA         S             11  2
     D  P1HGCD         S              7  0
     D  P1BKC7         S              7  0
     D  P1BYWG         S             11  2
     D  P1AMDX         S              8  0
     D  P1Y7TX         S              5
     D  P1AVNA         S             30
     D  P1CSAA         S              6
      *
      /EJECT
      *****************************************************************
      * Entry parameters
     C     *ENTRY        PLIST
     C                   PARM                    P1BKPR                         Accrual Rate
     C                   PARM                    P1AYVA                         Extended Amount
     C                   PARM                    P1HGCD                         Item Code
     C                   PARM                    P1BKC7                         Ship To Custome
     C                   PARM                    P1BYWG                         Weight Billed
     C                   PARM                    P1AVNA                         Description Lin
     C                   PARM                    P1AMDX                         SDA Invoice Dat D8#
     C                   PARM                    P1Y7TX                         SDA G/L Sub Led 5 ch
     C                   PARM                    P1CSAA                         Invoice Nbr Char 6
      *****************************************************************
      * Initialize
     C                   EXSR      ZZINIT
      *---------------------------------------------------------------
      * Mainline
      *---------------------------------------------------------------
     C                   eval      cust   = p1bkc7                              Customer Number
     C                   eval      date   = p1amdx                              Date
     C                   eval      prod   = p1hgcd                              Product Code
     C     key01         chain     accrual                            91
     C                   if        *in91 = *off
     C                   exsr      $updacc
     C                   else
     C                   exsr      $crtacc
     C                   endif
      * Exit program
     C                   seton                                        LR
      *
     C/EJECT
      *---------------------------------------------------------------
      * Create ACCRUALS record
      *---------------------------------------------------------------
      *
     C     $crtacc       begsr
      *
      * Populate fields
     C                   eval      rec    = '4'                                 Record ID 3/4/5
     C                   eval      prod   = p1hgcd                              Product Code
     C                   eval      desc   = p1avna                              Description
     C                   eval      pro    = *blanks                             Pro
     C                   eval      weight = p1bywg                              Quantity
     C                   eval      amt    = *zeros                              Amount
     C                   eval      invoic = p1csaa                              Invoice Number
     C                   eval      cust   = p1bkc7                              Customer Number
     C                   eval      terr   = *zeros                              Territory
     C                   eval      date   = p1amdx                              Date
     C                   eval      line   = *zeros                              Line
     C                   eval      delet  = *blanks                             Delete
     C                   eval      vendor = p1y7tx                              A/P Vendor#
     C                   eval      rebc   = *zeros                              Rebate
     C                   eval      accc   = p1bkpr                              Accrual
     C                   eval      frtc   = *zeros                              FRTC
     C                   eval      accrue = *zeros                              Total$
     C                   eval      accamt = p1ayva                              Accrual Amount
      *
      * Create Record
     C                   write     accrual
      *
     C                   endsr
      *---------------------------------------------------------------
      * Update ACCRUALS record
      *---------------------------------------------------------------
      *
     C     $updacc       begsr
      *
      * Accumulate Amount
     C                   eval      weight = weight + p1bywg
     C                   eval      accamt = accamt + p1ayva
      *
      * Update Record
     C                   update    accrual
      *
     C                   endsr
     C/EJECT
     CSR   ZZINIT        BEGSR
      *================================================================
      * Initialisation
      *================================================================
      * Retrieve job attributes
     C                   CALL      'Y2RTJCR'
     C                   PARM                    PGMDS
      * Setup job date/time
     C                   Z-ADD     ##SD7         ##JDT
     C                   TIME                    ##JTM
      *
      * Klist for Accruals
     C     KEY01         klist
     C                   kfld                    cust
     C                   kfld                    date
     C                   kfld                    prod
      *
      *
      *================================================================
     CSR   ZZEXIT        ENDSR
