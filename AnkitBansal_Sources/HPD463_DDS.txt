     A*%%TS  SD  19950125  084436  ISLRAMS     REL-V3R0M5  5763-PW1
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD463
     A*  TITLE:         SPECIFY FEED TICKET EXCEPTION REPORTS
     A*  PROGRAMMER:    MARY PALMER
     A*  DATE:          04/20/94
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A N99                                  CF04(04 'PROMPT')
     A N99 44                               CF08(08 'SUBMIT')
     A N99                                  CF17(17 'PRINT OPTIONS')
     A N99                                  CF20(20 'WRKSPLF')
     A*
     A*--------------------------------------------------------------*
     A* RECORD FORMAT ONE
     A*--------------------------------------------------------------*
     A          R HP463R1
     A*%%TS  SD  19950125  084050  ISLRAMS     REL-V3R0M5  5763-PW1
     A                                      SETOF(31)
     A                                      SETOF(32)
     A                                      SETOF(33)
     A                                      SETOF(51)
     A                                      SETOF(52)
     A                                      RTNCSRLOC(&RCD &FLD)
     A                                      OVERLAY
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 27'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  2 15'SPECIFY FEED TICKET EXCEPTION REPO-
     A                                      RT OPTIONS'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  4  2'Business office ........'
     A                                  4 27'?'
     A            DFFMBO    R        B  4 29REFFLD(BOBOBO)
     A N31                                  DSPATR(HI)
     A  31
     AO 51                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A            DFBODS    R        O  4 40REFFLD(BOBODS)
     A                                  5  2'Cut-off delivery date ..'
     A            DFMDY     R        B  5 29REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A N32                                  DSPATR(HI)
     A  32                                  DSPATR(PC)
     A  32                                  DSPATR(RI)
     A  99                                  DSPATR(PR)
     A                                  6  2'Report option ..........'
     A                                  6 27'?'
     A            DFROCD    R        B  6 29REFFLD(@@FLAG)
     A                                      TEXT('Report OPTION')
     A N33                                  DSPATR(HI)
     A  33                                  DSPATR(RI)
     A  33
     AO 52                                  DSPATR(PC)
     A  99                                  DSPATR(PR)
     A            DFRODS    R        O  6 40REFFLD(@@DS30)
     A                                      TEXT('Report OPTION DESCRIPTION')
     A*
     A*--------------------------------------------------------------*
     A* COMMAND KEY RECORD 1
     A*--------------------------------------------------------------*
     A          R HP463K1
     A*%%TS  SD  19950125  084436  ISLRAMS     REL-V3R0M5  5763-PW1
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A N99                             23 11'F4=Prompt'
     A  44N99                          23 22'F8=Submit'
     A N99                             23 33'F17=Print Options'
     A N99                             23 52'F20=Spool Files'
      *
      *--------------------------------------------------------------*
      * ERROR MESSAGE SUBFILE FORMAT
      *--------------------------------------------------------------*
     A          R HP463ES                   SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
     A*
     A*--------------------------------------------------------------*
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*--------------------------------------------------------------*
     A*
     A          R HP463EC                   SFLCTL(HP463ES)
     A*%%TS  SD  19940415  103325  ISRCENT     REL-V2R2M0  5738-PW1
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
