     A*%%TS  SD  20140306  135116  ISLRAMS     REL-V6R1M0  5761-WDS
     A*****************************************************************
     A*
     A* ENVIRONMENT:  PORK DIVISION
     A* SYSTEM:       AS/400
     A* FILE:         MPD704
     A* DESCRIPTION:  MP2: VIEW INVOICE
     A* PROGRAMMER:   LEANNE FEDOR
     A* DATE:         04/30/02
     A*
     A*----------------------------------------------------------------
     A* MODIFICATIONS LOG:
     A*----------------------------------------------------------------
     A* MODIFIED BY:  BARB GUTIERREZ   3/30/2011
     A*               QUANTITIES AND UNIT COST INCREASED IN SIZE.  THIS
     A*               CAUSED US TO INCREASE THE SCREEN SIZE FROM 24 X 80
     A*               TO 27 X 132.  E001398
     A*
     A* 05/26/21 S17068 Danny Nguyen
     A*          Unit Cost Per Item field length changed from 9.4 to 11.4.
     A*          Shifted 'Unit Cost Per Item' to the right 1 position.
     A*          Shifted 'Applied Amount' to the right 4 positions.
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(27 132 *DS4)
     A                                      REF(*LIBL/MP2REF)
     A                                      CHGINPDFT(FE)
     A                                      PRINT
     A                                      CF03(03 'EXIT')
     A                                      CF05(05 'REFRESH')
     A                                      CF13(13 'FOLD/DROP')
     A*
     A*----------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*----------------------------------------------------------------
     A          R MP704S1                   SFL
     A*%%TS  SD  20110330  113352  ISBGUTI     REL-V6R1M0  5761-WDS
     A  85                                  SFLNXTCHG
     A            HDRRN1         4Y 0H
     A            D1ITEMNO      16A  O 14  2
     A            D1ITEMDS      18A  O 14 20
     A            D1TRQT    R        O 14 41REFFLD(@@TRQT)
     A                                      EDTCDE(M)
17068A**          D1ITEMRT  R        O 14 55REFFLD(@@ITEMRT)
17068A            D1ITEMRT  R        O 14 53REFFLD(@@ITEMRT)
     A                                      EDTCDE(M)
     A            D1INQT    R        O 14 68REFFLD(@@INQT)
     A                                      EDTCDE(M)
     A            D1OVFL    R        O 14 81REFFLD(@@OVFL)
     A            D1APAM    R        O 14 84REFFLD(@@APAM)
     A                                      EDTCDE(M)
     A                                 15  6'Com:'
     A            D1COM     R        O 15 11REFFLD(@@COM)
     A            D1MCU     R        O 15 52REFFLD(@@MCU)
     A            D1OBJ     R        O 15 65REFFLD(@@OBJ)
     A            D1SUB     R        O 15 72REFFLD(MPREC/@@SUB *LIBL/MP2REF)
     A*
     A*---------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*---------------------------------------------------------------
     A*
     A          R MP704C1                   SFLCTL(MP704S1)
     A*%%TS  SD  20140306  135116  ISLRAMS     REL-V6R1M0  5761-WDS
     A                                      SFLSIZ(0100)
     A                                      SFLPAG(0004)
     A                                      SFLCSRRRN(&HDRR1)
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  88                                  SFLFOLD(CF13)
     A  89                                  SFLDROP(CF13)
     A                                      SFLMODE(&FOLD)
     A  82                                  SFLEND
     A                                      OVERLAY
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            FOLD           1A  H
     A            HDRR1          5S 0H
     A            SDPGM         10A  O  1  2DSPATR(HI)
     A                                  1 26'MP2 INTERFACE TO JDE'
     A                                      DSPATR(HI)
     A            SDUSR         10A  O  1 70DSPATR(HI)
     A                                  2 30'VIEW INVOICE'
     A                                      DSPATR(HI)
     A            SDJOB         10A  O  2 70DSPATR(HI)
     A                                  4  2'Vendor number :'
     A            C1JDVNNO  R        O  4 18REFFLD(@@JDVNNO)
     A                                      EDTCDE(M)
     A            C1JDVNNM  R        O  4 28REFFLD(@@JDVNNM)
     A                                  5  2'Purchase order:'
     A            C1PONO    R        O  5 18REFFLD(@@PONO)
     A                                  5 54'Amount to apply .......:'
     A            C1TAAM    R        O  5 82REFFLD(@@TAAM)
     A                                      EDTCDE(K)
     A                                  6  2'Purchase center/Accounting company-
     A                                      :'
     A            C1PCID    R        O  6 38REFFLD(@@PCID)
     A            C1ACONO   R        O  6 43REFFLD(@@ACONO)
     A                                      EDTCDE(Z)
     A                                  6 54'Sales tax .............:'
     A            C1STAM    R        O  6 82REFFLD(@@STAM)
     A                                      EDTCDE(K)
     A                                  7 54'Shipping/handling .....:'
     A            C1SHAM    R        O  7 82REFFLD(@@SHAM)
     A                                      EDTCDE(K)
     A                                  8  2'Invoice nbr :'
     A            C1INNO    R        O  8 16REFFLD(@@INNO)
     A                                  8 54'Total invoice amount ..:'
     A            C1INAM    R        O  8 82REFFLD(@@INAM)
     A                                      EDTCDE(K)
     A                                  9  2'Invoice date:'
     A            C1MDY     R        O  9 16REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  ')
     A*****                             8 73'Applied'
     A                                  9 54'Unapplied amount ......:'
     A            C1UNAM    R        O  9 82REFFLD(@@INAM)
     A                                      EDTCDE(K)
     A*****                             9 73'Dollars'
     A                                 10 54'Total applied dollars .:'
     A            C1APAM    R        O 10 82REFFLD(@@APAM)
     A                                      EDTCDE(K)
     A                                 11  2'Comment:'
     A            C1COM     R        O 11 11REFFLD(@@COM)
     A                                 12 44'Tran'
     A                                      DSPATR(HI)
17068A**                               12 58'Unit'
17068A                                 12 59'Unit'
     A                                      DSPATR(HI)
     A                                 12 72'Inv'
     A                                      DSPATR(HI)
     A                                 12 81'O'
     A                                      DSPATR(HI)
17068A**                               12 85'Applied'
17068A                                 12 89'Applied'
     A                                      DSPATR(HI)
     A                                 13  2'Item'
     A                                      DSPATR(HI)
     A                                 13 20'Item Description'
     A                                      DSPATR(HI)
     A                                 13 45'Qty'
     A                                      DSPATR(HI)
17068A**                               13 58'Cost'
17068A                                 13 59'Cost'
     A                                      DSPATR(HI)
     A                                 13 72'Qty'
     A                                      DSPATR(HI)
     A                                 13 81'V'
     A                                      DSPATR(HI)
17068A**                               13 86'Amount'
17068A                                 13 90'Amount'
     A                                      DSPATR(HI)
     A*
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R MP704K1
     A*%%TS  SD  20020422  105518  ISLRAMS     REL-V5R1M0  5722-WDS
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A                                      DSPATR(HI)
     A                                 23  2'F3=Exit'
     A                                 23 11'F5=Refresh'
     A                                 23 46'F13=Fold/Unfold Comment'
     A*
     A*---------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE FORMAT
     A*---------------------------------------------------------------
     A          R MP704ES                   SFL SFLMSGRCD(24)
     A*
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
     A*
     A*---------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*---------------------------------------------------------------
     A          R MP704EC                   SFLCTL(MP704ES)
     A*
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
