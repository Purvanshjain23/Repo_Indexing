/***************************************************************** */
/*  PROGRAM NUMBER --> HP244CL                                     */
/*  PROGRAM NAME ----> FOS Download: Files for Hog Group           */
/*  PROGRAMMER ------> Brad Baden                                  */
/*  DATE ------------> 11/11/2016                                  */
/*                                                                 */
/*  FUNCTION --------> Thia program builds a file for the FOS      */
/*                     Download for Hog Groups.  The file will     */
/*                     contain BGF Source Group and Nursery        */
/*                     Source Group for a Destination Hog Group.   */
/*                                                                 */
/***************************************************************** */

             PGM

             DCL        VAR(&QDATA) TYPE(*CHAR) LEN(298)

/*********************************************************************/
/* LOAD *LDA WITH VALUES FOR HP311                                   */
/*********************************************************************/

/* LOAD HOG GROUP CODE WITH ZEROES TO SIGNIFY ALL GROUPS             */

             CHGDTAARA  DTAARA(*LDA (53 7)) VALUE('0000000')

/* LOAD REPORT TYPE AND MIXED GROUPS                                 */

             CHGDTAARA  DTAARA(*LDA (174 1)) VALUE(D)  /* Detail */
             CHGDTAARA  DTAARA(*LDA (182 1)) VALUE(S)  /*Mixed Group */


/*********************************************************************/
/* SETUP QUERY FOR HOG GROUPS                                        */
/*********************************************************************/

/* ALWAYS OMIT VOIDED GROUPS                                         */

             CHGVAR     VAR(%SST(&QDATA 1 15)) VALUE('HGGSCD *EQ "OP"')

/* ALWAYS OMIT BGF GROUPS                                            */

             CHGVAR     VAR(%SST(&QDATA 17 23)) VALUE('*AND HGPPCD +
                          *NE "BGF  "')


/*-------------------------------------------------------------------*/
/* RUN QUERY OVER THE HOG GROUP FILE                                 */
/*-------------------------------------------------------------------*/

             OVRDBF     FILE(HSP034) SHARE(*YES)
             OPNQRYF    FILE((HSP034)) QRYSLT(&QDATA) KEYFLD(*FILE) +
                          SEQONLY(*YES)

/*-------------------------------------------------------------------*/
/* CREATE THE WORKFILES                                              */
/*-------------------------------------------------------------------*/
             CLRPFM     FILE(HSP244)

             CRTDUPOBJL OBJ(HSP315) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)
             MONMSG     MSGID(CPF0000) EXEC(CLRPFM FILE(QTEMP/HSP315))

             CRTDUPOBJL OBJ(HSP317) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)
             MONMSG     MSGID(CPF0000) EXEC(CLRPFM FILE(QTEMP/HSP317))

             CRTDUPOBJL OBJ(HSP318) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)
             MONMSG     MSGID(CPF0000) EXEC(CLRPFM FILE(QTEMP/HSP318))
             CRTDUPOBJL OBJ(HSL318A) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)
             MONMSG     MSGID(CPF0000)


/*-------------------------------------------------------------------*/
/* CALL THE PROGRAM TO BUILD THE WORKFILES                           */
/*-------------------------------------------------------------------*/

             CALL       PGM(HP311)

/*-------------------------------------------------------------------*/
/* CALL THE PROGRAM TO BUILD THE HSP244 FILE                         */
/*-------------------------------------------------------------------*/

             CALL       PGM(HP244)

             CLOF       OPNID(HSP034)
             DLTOVR     FILE(*ALL)

             ENDPGM
