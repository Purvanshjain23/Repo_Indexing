      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Hog Production
      * PROGRAM:     TF303 - Cash: Workfile: Cash Distribution Balance Listing
      * PROGRAMMER:  LeAnne Ramsey
      * CREATED:     09/03/09
      *
      * FUNCTION:    Build a workfile of Cash Distribution Balance data summarizing
      *              the Exempt/Not Exempt records together for each WeekEnding Date.
      *
      **********************************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 09/08/09  Alice Brownfield
      *           Changed field names in TFP011-Cash Distribution Balance and, subsequently,
      *           in workfile TFP303. So, we had to change this code to use the new field names.
      *           No logic was changed. This was for Enhancement:
      *           E00493-change to allocate AR Adjustments at gross vs net; i.e. give some
      *           to Sales Expense then the NET to TF & SBF.
      /eject
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Ftfp011    if   e           k disk
      *    Cash distribution balance (records selected with open query)
      *
      *
     Ftfp303    o    e           k disk
      *    Workfile: Cash distribution balance summary
      *
      /eject
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      *
      ****************************************************************
      * ARRAYS AND TABLES
      ****************************************************************
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Control fields
      *
     D first           s              1    inz('Y')
     D svwedt          s                   like(cbwedt)
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /eject
      ****************************************************************************************
      * Mainline
      ****************************************************************************************
      *
      * Read all the Cash Distribution Balance records selected by the open query.
      * Write a Workfile record for each WeekEnding Date.
      *
     C     *loval        setll     tfp011
      *
     C                   dou       *in90 = *on                                  Main do
     C                   read      tfp011                                 90
     C                   if        *in90 = *off                                 If not EOF
      *
     C                   select
     C                   when      first = yes
     C                   move      no            first
     C                   z-add     cbwedt        svwedt
      *
     C                   when      cbwedt <> svwedt
     C                   exsr      $wrt303
     C                   z-add     cbwedt        svwedt
     C                   endsl
      *
      * Detail processing
      *
      *        Aggregate
     C                   add       cbagpram      w1agpram
     C                   add       cbagclam      w1agclam
     C                   add       cbaaragam     w1aaragam
     C                   add       cbasxam       w1asxam
      *        Seaboard
     C                   add       cbsnpram      w1snpram
     C                   add       cbsnclam      w1snclam
     C                   add       cbsaranam     w1saranam
      *        Triumph
     C                   add       cbtnpram      w1tnpram
     C                   add       cbtnclam      w1tnclam
     C                   add       cbtaranam     w1taranam
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Main do
      * EOF processing
      *
     C                   if        first = no
     C                   exsr      $wrt303
     C                   endif
      *
     C                   seton                                        lr
      /eject
      *----------------------------------------------------------------
      * Write the workfile record
      *----------------------------------------------------------------
      *
     C     $wrt303       begsr
      *
     C                   z-add     svwedt        w1wedt
     C                   write     w1rec
     C                   clear                   w1rec
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
     C                   endsr
      *
