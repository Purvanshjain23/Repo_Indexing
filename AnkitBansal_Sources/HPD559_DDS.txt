     A*%%TS  SD  20031230  091524  ISLRAMS     REL-V5R2M0  5722-WDS
     A*****************************************************************
     A*
     A*  SYSTEM:        HOG PRODUCTION
     A*  DISPLAY FILE:  HPD559 - SELECT REPORT CODE
     A*  CREATED BY:    LEANNE FEDOR
     A*  DATE:          06/23/03
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3                  -
     A                                             27 132 *DS4)
     A                                      REF(*LIBL/HSPREF)
     A                                      PRINT
     A                                      CA12(12 'PREVIOUS')
     A                                      CF04(04 'PROMPT')
     A N82                                  ROLLUP(25 'PAGE FORWARD')
     A*---------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP559S1                   SFL
     A*%%TS  SD  20030623  171800  ISLRAMS     REL-V5R2M0  5722-WDS
     A            HDRRN1    R        H      REFFLD(@@RRN)
     A            D1CACD    R        O  9  2REFFLD(@@CACD)
     A            D1RPCD    R        O  9 13REFFLD(@@RPCD)
     A            D1RSDS    R        O  9 23REFFLD(RPRSDS)
     A            D1RLDS    R        O  9 38REFFLD(RPRLDS)
     A*
     A*---------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP559C1                   SFLCTL(HP559S1)
     A*%%TS  SD  20031230  091524  ISLRAMS     REL-V5R2M0  5722-WDS
     A  *DS3                                SFLSIZ(0100)
     A  *DS4                                SFLSIZ(0100)
     A  *DS3                                SFLPAG(0008)
     A  *DS4                                SFLPAG(0008)
     A  *DS3                                WINDOW(HP559EC)
     A  *DS4                                WINDOW(HP559EC)
     A                                      SETOF(51)
     A                                      RTNCSRLOC(&RCD &FLD)
     A                                      OVERLAY
     A                                      SFLCSRRRN(&HDRRN)
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A            HDPAG1    R        H      REFFLD(@@PAG)
     A                                      SFLRCDNBR
     A            RCD           10A  H
     A            FLD           10A  H
     A            HDRRN     R        H      REFFLD(@@RRN)
     A            SDPGM     R        O  1  1REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 21'SELECT REPORT CODE'
     A                                      DSPATR(HI)
     A  98                              2  2'(Only Report Codes that are not Us-
     A                                      er-Controlled are displayed.)'
     A                                      DSPATR(HI)
     A  97                              2  3'(Only Report Codes that are User-C-
     A                                      ontrolled are displayed.)'
     A                                      DSPATR(HI)
     A                                  4  2'Position Cursor, Press Enter'
     A                                  5  4'?'
     A                                      DSPATR(HI)
     A                                  6  2'Report'
     A                                      DSPATR(HI)
     A                                  6 13'Report'
     A                                      DSPATR(HI)
     A                                  6 23'Report Short'
     A                                      DSPATR(HI)
     A                                  6 38'Report Long'
     A                                      DSPATR(HI)
     A                                  7  2'Category'
     A                                      DSPATR(HI)
     A                                  7 13'Code'
     A                                      DSPATR(HI)
     A                                  7 23'Description'
     A                                      DSPATR(HI)
     A                                  7 38'Description'
     A                                      DSPATR(HI)
     A            C1CACD    R        B  8  2REFFLD(HSRREF/@@CACD *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A  51                                  DSPATR(PC)
     A            C1RPCD    R        B  8 13REFFLD(HSRREF/@@RPCD *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A            C1RSDS    R        B  8 23REFFLD(HSRREF/RPRSDS *LIBL/HSPREF)
     A                                      CHGINPDFT(UL LC FE)
     A                                      DSPATR(HI)
     A            C1RLDS    R        B  8 38REFFLD(HSRREF/RPRLDS *LIBL/HSPREF)
     A                                      CHGINPDFT(UL LC FE)
     A                                      DSPATR(HI)
     A*---------------------------------------------------------------
     A* COMMAND LINE LINE
     A*---------------------------------------------------------------
     A          R HP559K1
     A                                      WINDOW(HP559EC)
     A                                      OVERLAY
     A  43                             17  1'Message program failed. Call IS.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 18  1'F4=Prompt'
     A                                 18 12'F12=Previous'
      *
     A*-------------------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
     A*-------------------------------------------------------------------------
     A          R HP559ES                   SFL SFLMSGRCD(19)
      *
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
     A*
     A*-------------------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*-------------------------------------------------------------------------
     A*
     A          R HP559EC                   SFLCTL(HP559ES)
     A*%%TS  SD  19980327  105628  ISLRAMS     REL-V3R7M0  5716-PW1
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A                                      WINDOW(*DFT 20 65)
     A                                      WDWBORDER((*DSPATR RI) (*CHAR '    -
     A                                          '))
     A            MSGPGM                    SFLPGMQ(10)
     A*
     A*---------------------------------------------------------------
     A* ASSUME FORMAT -- REQUIRED FOR WINDOWS
     A*---------------------------------------------------------------
     A          R ASSUME
     A*%%TS  SD  20011025  160559  ISLRAMS     REL-V4R4M0  5769-PW1
     A                                      ASSUME
     A                                      OVERLAY
     A                                 24 79' '
