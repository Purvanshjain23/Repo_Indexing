      *         *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      HOG PRODUCTION
      * PROGRAM:     HP352
      * TITLE:       CHANGE PRODUCTION TYPE FOR FARM SITE
      * PROGRAMMER:  LEANNE FEDOR
      * CREATED:     03/21/96
      *
      *  FUNCTION:   THIS BATCH PROGRAM IS SUBMITTED FROM HP452
      *              USING QCMDEXC.  IT ALLOWS THE USER TO GLOBALLY
      *              CHANGE THE PRODUCTION TYPE ASSOCIATED WITH A
      *              FARM SITE THROUGHOUT THE ENTIRE DATABASE.
      *
      *              THE USER'S SUBMISSION CRITERIA ARE PASSED IN THE
      *              LDA.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 06/03/97  LEANNE FEDOR
      *           RECOMPILE ONLY. NEW FIELDS 'PURGE FLAG' AND 'DATE OF
      *           FIRST PURGE' ADDED TO HOG GROUP FILE.
      *
      * 06/25/97  LEANNE FEDOR
      *           RECOMPILE ONLY. NEW FIELD 'COST THROUGH SYSTEM FLAG'
      *           ADDED TO FEED TICKET HEADER.
      *
      * 12/09/97  LEANNE FEDOR
      *           RECOMPILE ONLY. EXPENSE TICKET NUMBER FIELD RENAMED
      *           FROM 'XTNO' TO 'XTSN'.
      *
      * 03/09/98  LEANNE FEDOR
      *           RECOMPILE ONLY. FEED POUNDS AND FEED AMOUNT FIELDS
      *           INCREASED IN SIZE PER NATHAN MALONE.
      *
      * 04/13/98  LeAnne Fedor
      *           Recompile only. New field 'state' added to farm site file.
      *
      * 01/11/99  LeAnne Fedor
      *           Recompile only. New field 'scheduled kill date' added
      *           to sales movement header file.
      *
      * 10/17/00  LeAnne Fedor
      *           Recompile only. New field 'load type' added to the sales
      *           movement head file.
      *
      * 01/30/01  LeAnne Fedor
      *           Production type was removed from the feed ticket files.
      *           So, removed all production type logic from program.
      *
      * 06/05/01  LeAnne Fedor
      *           Recompile only. New field 'multisite' added to Farm Site file.
      *
      * 06/28/01  LeAnne Fedor
      *           Recompile only.
      *           Three fields (manager/supervisor/multisite) renamed in Farm Site file.
      *           Manager and supervisor fields removed from Hog Group file.
      *
      * 07/18/01  LeAnne Fedor
      *           Recompile only.
      *           New field 'feed group' added to Hog Group file.
      *           Expense ticket amount expanded from 7,2 to 9,2.
      *
      * 09/04/01  LeAnne Fedor
      *           Recompile only. New 'square feet' fields added to Farm Site file.
      *
      * 12/04/02  LeAnne Fedor
      *           Removed Production Type from the Killed/Dead file and the Inventory
      *           Adjustment File. So, removed the files from this program.
      *
      * 12/12/02  LeAnne Fedor
      *           Removed the Expense Ticket file from this program. The file still has
      *           'production type' in it. But, we have changed our use/logic of this
      *           field...we no longer need to process the file in this program!
      *           Please refer to the reference file for a detailed explanation of
      *           the logic change for the Expense Ticket file.
      *
      * 12/18/02  LeAnne Fedor
      *           Removed Production Type from the Sales Movement Header file.
      *           So, removed the file from this program.
      *
      * 07/06/09  LeAnne Ramsey
      *           Recompile only. Added new field 'Continuous Flow Flag' to Hog Group file.
      *
      * 10/15/13  LeAnne Ramsey (E2831)
      *           Recompile only. Added field 'MTech Reference'.
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     FHSP018    UF   E           K DISK
      *    FARM SITE
      *
      *
     FHSL034A   UF   E           K DISK
      *    HOG GROUP
      *
      *
     FHSL074C   UF   E           K DISK
      *    TRANSFER MOVEMENT HEADER
      *
      *
     FHSL074D   UF   E           K DISK    RENAME(MHREC:MHRECD)
      *    TRANSFER MOVEMENT HEADER
      *
      /EJECT
      ****************************************************************
      * INPUT SPECS
      ****************************************************************
      *
      *---------------------------------------------------------------
      *  LDA -  WITH THE REQUESTED PRINT SELECTIONS
      *---------------------------------------------------------------
      *
     D                UDS
     D  LDPTCD                 1      5
     D  LDFSCD                 6     10  0
      *
      *---------------------------------------------------------------
      * STANDARD PROGRAM STATUS DATA STRUCTURE
      *---------------------------------------------------------------
      *    EXTERNALLY DEFINED AS UTPGFR (RECORD FORMAT: PGMDSR)
     D PGMDS         ESDS                  EXTNAME(UTPGFR)
      *
      *---------------------------------------------------------------
      *  NAMED CONSTANTS
      *---------------------------------------------------------------
      *
     D YES             C                   CONST('Y')
     D NO              C                   CONST('N')
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * MAIN LINE
      ****************************************************************
      *
      * PROCESS THE FARM SELECTED BY THE USER
      *
      * FARM SITE
      *
     C     LDFSCD        CHAIN     HSP018                             92
     C     *IN92         IFEQ      *OFF
     C                   MOVEL     LDPTCD        FSPTCD
     C                   UPDATE    FSREC
     C                   ENDIF
      *
      * HOG GROUP
      *
     C     LDFSCD        SETLL     HSL034A
     C     *IN90         DOUEQ     *ON
     C     LDFSCD        READE     HSL034A                                90
     C     *IN90         IFEQ      *OFF
     C                   MOVEL     LDPTCD        HGPTCD
     C                   UPDATE    HGREC
     C                   ENDIF
     C                   ENDDO
      *
      * TRANSFER MOVEMENTS OUT OF THE FARM
      *
     C     LDFSCD        SETLL     HSL074C
     C     *IN90         DOUEQ     *ON
     C     LDFSCD        READE     HSL074C                                90
     C     *IN90         IFEQ      *OFF
     C                   MOVEL     LDPTCD        MHORPT
     C                   UPDATE    MHREC
     C                   ENDIF
     C                   ENDDO
      *
      * TRANSFER MOVEMENTS INTO THE FARM
      *
     C     LDFSCD        SETLL     HSL074D
     C     *IN90         DOUEQ     *ON
     C     LDFSCD        READE     HSL074D                                90
     C     *IN90         IFEQ      *OFF
     C                   MOVEL     LDPTCD        MHDNPT
     C                   UPDATE    MHRECD
     C                   ENDIF
     C                   ENDDO
      *
      *
      *--------------------------------------------------------------
      * EOF PROCESSING
      *--------------------------------------------------------------
      *
     C                   SETON                                        LR
      /EJECT
