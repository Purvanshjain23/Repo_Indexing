      *****************  RPG PROGRAM HEADING  ************************
     h option(*SRCSTMT:*NODEBUGIO)
      *
      *
      * SYSTEM:      Hog Production System
      * PROGRAM:     HP3022
      * TITLE:       Workfile--BGF Weekly Wean Projections Listing (Multiple)
      * PROGRAMMER:  LeAnne Ramsey
      * CREATED:     01/20/12
      *
      * FUNCTION:  Batch program to build a workfile.
      *            This is submitted from HP4021-Work With BGF Weekly Wean
      *            Projections.
      *
      *            In January 2012, Sami Wilson requested EMail/DWL functionality
      *            for this listing. To accomplish this, I had to go to this workfile
      *            approach. So, while this workfile/logic is new, the Report/Listing
      *            is not new.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 10/15/13  LeAnne Ramsey (E2831)
      *           Recompile only. Added field 'MTech Reference'.
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fhsl086a   if   e           k disk
      *    BGF Weekly Wean Projections (records selected by open query)
      *
      *
     Fhsp018    if   e           k disk
      *   Farm Site
      *
      *
     Fhsp3022   o    e           k disk
      *  Workfile: Listing of BGF Weekly Wean Projections
      *
      /EJECT
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Standard fields
      *
      * Control fields
      *
     D procfl          s              1    inz('Y')
      *
      *
      * Save fields
      *
     D svfscd          s                   like(fsfscd)
     D svrpwedt        s                   like(pwrpwedt)
      *
      *
      * Work fields for date manipulation
      *
     D wkmdy           s               D   datfmt(*mdy)
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *---------------------------------------------------------------
      * Local data area.
      *---------------------------------------------------------------
      *
     Dlda             uds                  dtaara(*lda)
      *
     D  ldfscd                 2      6  0
     D  ldfsnm                 7     31
      *
     D  ldcell                40     41  0
      *
     D  ldfrpcdyr             42     45  0
     D  ldfrpcdwk             46     47  0
     D  ldfrpwedt             48     55  0
      *
     D  ldtrpcdyr             56     59  0
     D  ldtrpcdwk             60     61  0
     D  ldtrpwedt             62     69  0
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * MAINLINE
      ****************************************************************
      *
      * Prepare to read the file.
      *
     C     *loval        setll     hsl086a
      *
      * Read all records in the file that were selected by the open query.
      *
     C                   dou       *in90 = *on                                  Main do
     C                   read      hsl086a                                90
     C                   if        *in90 = *off                                 If not EOF
      *
      * Default the 'process' flag to YES.
      *
     C                   move      yes           procfl
      *
      * If the user selected a single cell check that his farm is in that
      * cell before continuing.
      *
     C                   if        ldcell <> 0                                  If 1 cell
     C     pwfscd        chain     hsp018                             92
     C                   if        *in92 = *on or fscell <> ldcell
     C                   move      no            procfl
     C                   endif
     C                   endif                                                  If 1 cell
      *
      * Continue processing.
      *
     C                   if        procfl = yes                                 If continue
      *
     C                   select
     C                   when      pwfscd <> svfscd
     C                   z-add     pwfscd        svfscd
     C                   z-add     pwrpwedt      svrpwedt
     C                   exsr      $week1
      *
     C                   when      pwrpwedt <> svrpwedt
     C                   z-add     pwrpwedt      svrpwedt
     C                   exsr      $week1
      *
     C                   other
     C                   exsr      $week2
     C                   exsr      $wrt3022
     C                   endsl
      *
     C                   endif                                                  If process
     C                   endif                                                  If not EOF
     C                   enddo                                                  Main do
      *
      * EOF processing
      *
     C                   seton                                        lr
      /EJECT
      *---------------------------------------------------------------
      * Populate the fields for Projection Week 1
      *---------------------------------------------------------------
      *
     C     $week1        begsr
      *
     C                   z-add     pwfscd        w1fscd
      *
      * Retrieve Cell
      *
     C     pwfscd        chain     hsp018                             92
     C                   if        *in92 = *on
     C                   z-add     0             w1cell
     C                   else
     C                   z-add     fscell        w1cell
     C                   endif
      *
      * Flip Report Week-ending date
      *
     C     *iso          test(d)                 pwrpwedt               92
     C                   if        *in92 = *on                                  If bad date
     C                   z-add     0             w1rpwedt
     C                   z-add     0             w1rpwemdy
     C                   else
     C                   z-add     pwrpwedt      w1rpwedt
     C     *iso          move      pwrpwedt      wkmdy
     C                   movel     wkmdy         w1rpwemdy
     C                   endif                                                  If bad date
      *
     C                   z-add     pwrpcdwk      w1rpcdwk
     C                   z-add     pwrpcdyr      w1rpcdyr
      *
      * Flip Week 1 Week-ending date
      *
     C     *iso          test(d)                 pwpwwedt               92
     C                   if        *in92 = *on                                  If bad date
     C                   z-add     0             w1pwwedt1
     C                   z-add     0             w1pwwemdy1
     C                   else
     C                   z-add     pwpwwedt      w1pwwedt1
     C     *iso          move      pwpwwedt      wkmdy
     C                   movel     wkmdy         w1pwwemdy1
     C                   endif                                                  If bad date
      *
     C                   z-add     pwpwcdwk      w1pwcdwk1
     C                   z-add     pwpwcdyr      w1pwcdyr1
      *
      * Sunday thru Saturday weaned piglet head
      *
     C                   z-add     pwsupwhd      w1supwhd
     C                   z-add     pwmopwhd      w1mopwhd
     C                   z-add     pwtupwhd      w1tupwhd
     C                   z-add     pwwepwhd      w1wepwhd
     C                   z-add     pwthpwhd      w1thpwhd
     C                   z-add     pwfrpwhd      w1frpwhd
     C                   z-add     pwsapwhd      w1sapwhd
      *
      * Total piglets weaned
     C                   z-add     pwtopwhd      w1topwhd1
      * Weaned sows
     C                   z-add     pwpwsohd      w1pwsohd1
      * Sow culls
     C                   z-add     pwsocuhd      w1socuhd1
      * Boar culls
     C                   z-add     pwbocuhd      w1bocuhd1
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Populate the fields for Projection Week 2
      *---------------------------------------------------------------
      *
     C     $week2        begsr
      *
      * Flip Week2 Week-ending date
      *
     C     *iso          test(d)                 pwpwwedt               92
     C                   if        *in92 = *on                                  If bad date
     C                   z-add     0             w1pwwedt2
     C                   z-add     0             w1pwwemdy2
     C                   else
     C                   z-add     pwpwwedt      w1pwwedt2
     C     *iso          move      pwpwwedt      wkmdy
     C                   movel     wkmdy         w1pwwemdy2
     C                   endif                                                  If bad date
      *
     C                   z-add     pwpwcdwk      w1pwcdwk2
     C                   z-add     pwpwcdyr      w1pwcdyr2
      *
      * Populate head fields
      *     Total weaned piglet head
      *     Weaned sow head
      *
     C                   z-add     pwtopwhd      w1topwhd2
     C                   z-add     pwpwsohd      w1pwsohd2
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Write a workfile record
      *---------------------------------------------------------------
      *
     C     $wrt3022      begsr
      *
     C                   write     w1rec
     C                   clear                   w1rec
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
     C                   endsr
      /EJECT
