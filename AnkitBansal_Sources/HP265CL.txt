/***************************************************************** */
/*                                                                 */
/*  SYSTEM:          HOG PRODUCTION SYSTEM                         */
/*  PROGRAM NUMBER:  HP265CL                                       */
/*  PROGRAM NAME:    SAFEDATA: UPLOAD BGF DAILY PRODUCTION         */
/*  PROGRAMMER:      LEANNE FEDOR                                  */
/*  CREATE DATE:     01/15/02                                      */
/*                                                                 */
/*  FUNCTION:        THIS CL IS CALLED FROM AND FTP CL WHICH IS    */
/*                   SUBMITTED FROM THE JOB SCHEDULER.             */
/*                                                                 */
/*  THE FUNCTION:                                                  */
/*  1) WRITES THE RECEIVING FILE TO AN EDITING FILE                */
/*  2) PRINTS AN EDIT LISTING OVER THE EDITING FILE                */
/*     AND WRITES RECORDS WITHOUT ERRORS TO HPS DATABASE           */
/*                                                                 */
/*******************************************************************/
/* MODIFIED:                                                       */
/*  1/24/2020 JBB WI314 - Live Reporting IBMi                      */
/*                Add logic to override printer file.  If user     */
/*                is a programmer, hold report and do not put      */
/*                report on the Guymon printer.  Also, retrieve    */
/*                system date and CHGJOB with system date.         */
/*                                                                 */
/*******************************************************************/

             PGM

             DCL        VAR(&HOLD) TYPE(*CHAR) LEN(10)
             DCL        VAR(&COPYS) TYPE(*DEC) LEN(2)
             DCL        VAR(&GRPPRFL)  TYPE(*CHAR) LEN(10)
             DCL        VAR(&SYSDATE)  TYPE(*CHAR) LEN(6)

/*********************************************************************/
/*  Retrieve the user's Group Profile.  Set printer overrides based  */
/*  on the group profile.                                            */
/*********************************************************************/

             RTVUSRPRF  GRPPRF(&GRPPRFL)

/*-------------------------------------------------------------------*/
/*  WRITE THE BGF DAILY PRODUCTION RECEIVING TO THE EDITING FILE     */
/*-------------------------------------------------------------------*/

             CALL       PGM(HP265)

/*------------------------------------------------------------------*/
/*  SET UP PRINT FILE OVERRIDES AND OVERRIDE FILE                    */
/*------------------------------------------------------------------*/

             SELECT
             WHEN       COND(&GRPPRFL = 'SBDPGMR ') THEN(DO)
             CHGVAR     VAR(&HOLD) VALUE(*YES)
             CHGVAR     VAR(&COPYS) VALUE(1)
             OVRPRTF    FILE(QPRINT) COPIES(&COPYS) HOLD(&HOLD) +
                          SHARE(*YES)
             ENDDO

             OTHERWISE  CMD(DO)

 HOLD:       CHGVAR     VAR(&HOLD) VALUE(*YES)
 COPYS:      CHGVAR     VAR(&COPYS) VALUE(2)

 OVRPRTF:    OVRPRTF    FILE(QPRINT) OUTQ(GUYPRT03) COPIES(&COPYS) +
                          HOLD(&HOLD) SHARE(*YES)
             ENDDO

             ENDSELECT

/*-------------------------------------------------------------------*/
/*  RETRIEVE QSYS DATE                                               */
/*  This was done because the date the "listener" program started is */
/*  the date that was being used as the Create Date on the HSP082    */
/*  file.  Using the CHGJOB command changes the job date to the      */
/*  system date.                                                     */
/*-------------------------------------------------------------------*/
             RTVSYSVAL  SYSVAL(QDATE) RTNVAR(&SYSDATE)
             CHGJOB     DATE(&SYSDATE)

/*-------------------------------------------------------------------*/
/*  CALL PROGRAM TO EDIT/WRITE TO DATABASE                           */
/*-------------------------------------------------------------------*/

             CALL       PGM(HP266)
             DLTOVR     FILE(*ALL)

/*-------------------------------------------------------------------*/
/*  REORG THE EDITING FILE                                           */
/*-------------------------------------------------------------------*/
             RGZPFM     *LIBL/HSP266
             MONMSG     MSGID(CPF0000)

             ENDPGM
