      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Hog Production System
      * PROGRAM:     HP8003 - Roll-up Room Square Feed for Farm Total Square Feet
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     02/22/02
      *
      * Function:    This function sums the 'square feet' for every active room on the
      *              incoming farm and returns the 'total square feet' of buildings on
      *              the farm.
      *
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 01/16/06  LeAnne Fedor
      *           Recompile only.
      *           We have removed "Bin Set" from HSP020-Building/Rooms and put it in our new
      *           HSP113-Rooms/Bin Sets file.
      /eject
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fhsp020    if   e           k disk
      *    Building rooms
      *
      /eject
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      *
      * Parm fields
      *
     D xxfscd          s                   like(brfscd)
     D xxfssqft        s              7p 0
      *
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * Mainline
      ****************************************************************
      *
      * Read all active rooms for this farm site accumulating the 'square feet'
      * for the farm.
      *
     C     xxfscd        setll     hsp020
      *
     C                   dou       *in90 = *on                                  Do rooms
     C     xxfscd        reade     hsp020                                 90
     C                   if        *in90 = *off and brrmst = 'A'                If active
     C                   add       brrmsqft      xxfssqft
     C                   endif                                                  If active
     C                   enddo                                                  Do daily
      *
     C                   seton                                        lr
      /eject
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
     C     *entry        plist
     C                   parm                    xxfscd
     C                   parm                    xxfssqft
      *
     C                   endsr
      /eject
