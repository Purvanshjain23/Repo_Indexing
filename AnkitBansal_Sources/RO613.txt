      *****************  RPG PROGRAM HEADING  ************************
     h option(*SRCSTMT:*NODEBUGIO)
      ****************************************************************
      *
      * ENVIRONMENT: Pork Division
      * SYSTEM:      Resource Optimization
      * PROGRAM:     RO613 - Meat Cost Report
      * PROGRAMMER:  LeAnne Ramsey
      * CREATED:     01/22/07
      *
      * FUNCTION:    Print records from the files:
      *                1) Component Item Meat Cost
      *                2) Item Meat Cost
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 02/13/07  LeAnne Ramsey
      *           Changed to print listing single-spaced.
      *
      * 04/11/07  LeAnne Ramsey
      *           Changed logic from "sales" values to "produced" values.
      *
      * 11/08/07  LeAnne Ramsey
      *           Recompile only. Sales Yield % was added to the Item Meat Cost file.
      *
      * 06/06/08  LeAnne Ramsey
      *           Recompile only. New fields (Ingredient and Other Cost) added to
      *           multiple files.
      *
      * 11/21/08  LeAnne Ramsey
      *           As part of synchronizing the LDAs between the TFS Margin Adjustment Close
      *           and the Meat Costing, we changed the LDA positions.
      *
      * 03/02/09  LeAnne Ramsey
      *           If the "yielded meat cost" is not equal to the "std meat cost", we will
      *           plop an '*' after the "yielded meat cost".
      *
      * 06/17/09  LeAnne Ramsey
      *           Recompile only. Three fields were added to ROP103-Item Meat Cost:
      *               TF Classification Code, TF Class Group Code, Item Structure Type Code
      /eject
      ****************************************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************************************
      *
     Fpoaxcpl1  if   e           k disk
      *  Item standard cost
      *
      *
     frop102    if   e           k disk
      *  Component item meat cost
      *
      *
     frol102d   if   e           k disk    rename(cmrec:cmrecd)
      *  Component item meat cost
      *
      *
     Frol103b   if   e           k disk
      *  Item meat cost (records selected by open query)
      *
      *
     Fprint198  o    f  198        printer oflind(*inof)
      *
      /eject
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Standard fields
      *
     d rtime           s              6  0
     D dash            s            188    inz(*all'-')
      *
      *
      * Control fields
      *
     D first           s              1    inz('Y')
     D svpgcd          s                   like(impgcd)
     D svplcd          s                   like(implcd)
      *
      *
      * Workfields for date manipulation
      *
     D wkisodate       s               d   datfmt(*iso)
     D wksyndt         s              7  0
      *
      * Workfields
      *
     D wkmco           s                   like(imymco)
      *
      *
      * Print fields
      *
     d h1pfds          s             11
     d h1dsds          s              7
      *
     d l1puam          s             11  2
     d l1sypc          s                   like(cmsypc)
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *
      *---------------------------------------------------------------
      *  Local data area
      *---------------------------------------------------------------
      *
     d lda            uds                  dtaara(*lda)
     D  ldpfcd                 1      1
      *
     D  ldwedt                29     36  0
     D  ldwemdy               44     49  0
      *
     D  ldcono                82     84  0
     D  lddsfl               135    135
      *
     D  lditcd               222    228  0
     D  lditds               229    258
     D  ldpgcd               259    264
     D  ldpgds               265    294
     D  ldplcd               299    304
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * Mainline
      ****************************************************************
      *
      * Print headings
     C                   exsr      $h1hdr

      * Read all Item Meat Cost records selected by the open query.
      *
     C     *loval        setll     rol103b
      *
     C                   dou       *in90 = *on                                  Main do
     C                   read      rol103b                                90
     C                   if        *in90 = *off                                 If not EOF
      *
      * Print a "heading" line for each unique Primal Group/Production Line
      *
     C                   move      no            first
     C                   if        impgcd <> svpgcd or
     C                             implcd <> svplcd
     C                   move      impgcd        svpgcd
     C                   move      implcd        svplcd
     C                   exsr      $h2hdr
     C                   endif
      *
      * Print this Item Meat Cost record
      *
     C                   exsr      $l1dtl
      *
      * If the user selected the Detail version, print the companion
      * Component Meat Cost records.
      *
     C                   if        lddsfl = 'D'
     C                   exsr      $detail
     C                   endif
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Main do
      *
      * EOF processing
      *
     C                   if        first = yes
     C                   except    nodata
     C                   endif
      *
     C                   seton                                        lr
      /eject
      *---------------------------------------------------------------
      * Print Heading for Primal Group/Production LIne
      *---------------------------------------------------------------
      *
     C     $h2hdr        begsr
      *
     C                   if        *inof = *on
     C                   exsr      $h1hdr
     C                   endif
      *
     C                   except    h2hdr
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Print the Summary detail line
      *---------------------------------------------------------------
      *
     C     $l1dtl        begsr
      *
      * Place the 15,6 Net Product Revenue Amount into an 11,2 print field.
      *
     C                   z-add(h)  impuam        l1puam
      *
      * Retrieve Item Sales Yield Percent
      *
     C     key02         chain     rop102                             92
     C                   if        *in92 = *off
     C                   z-add     cmsypc        l1sypc
     C                   endif
      *
      * Retrieve Std Meat Cost
      * (If the Yielded Meat Cost is not equal to the Std Meat Cost, plop an '*'
      *  after the Yielded Meat Cost. The only time they should be different is
      *  when there is an override in effect (see file ROP016).
      *
     C     *iso          move      ldwedt        wkisodate
     C     *cymd         move      wkisodate     wksyndt
      *
     C                   setoff                                       81
     C     key03         chain     poaxcpl1                           92
     C                   if        *in92 = *off
     C                   eval(h)   wkmco = (axfrpr/100)
     C                   if        wkmco <> imymco
     C                   seton                                        81
     C                   endif
     C                   endif
      *
      * Overflow
     C                   if        *inof = *on
     C                   exsr      $h1hdr
     C                   exsr      $h2hdr
     C                   endif
      *
     C                   except    l1dtl
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Print the Component Item records for the Item
      *---------------------------------------------------------------
      *
     C     $detail       begsr
      *
     C                   except    blank
      *
     C     key01         setll     rol102d
      *
     C                   dou       *in91 = *on                                  Do loop
     C     key01         reade     rol102d                                91
     C                   if        *in91 = *off                                 If not EOF
      *
     C                   if        cmbomty = 'P' and                            If itself
     C                             cmitcd = cmciitcd
     C                   else
      *
     C                   if        *inof = *on
     C                   exsr      $h1hdr
     C                   exsr      $h2hdr
     C                   endif
      *
     C                   setoff                                       8283
     C                   setoff                                       8485
      *
     C                   select
     C                   when      cmbomty = 'P'
     C                   seton                                        84
     C                   if        cmsypc = 0
     C                   seton                                        82
     C                   endif
      *
     C                   when      cmbomty = 'C'
     C                   seton                                        85
     C                   if        cmcspc = 0
     C                   seton                                        83
     C                   endif
     C                   endsl
      *
     C                   except    l2dtl
     C                   endif                                                  If itself
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Do loop
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Print report headings
      *---------------------------------------------------------------
      *
     C     $h1hdr        begsr
      *
     C                   except    h1hdr
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Key lists
      *
     C     key01         klist
     C                   kfld                    ldcono
     C                   kfld                    ldwedt
     C                   kfld                    impgcd
     C                   kfld                    implcd
     C                   kfld                    imitcd
      *
     C     key02         klist
     C                   kfld                    ldcono
     C                   kfld                    ldwedt
     C                   kfld                    imitcd
     C                   kfld                    imitcd
      *
     C     key03         klist
     C                   kfld                    ldcono
     C                   kfld                    imitcd
     C                   kfld                    wksyndt
      *
      *
     C                   time                    rtime
      *
      * Set up report headings.
      *
      * This report is generated on-demand and also from the submitted
      * Weekly Meat Costing function.
      *
     C                   select
     C                   when      ldpfcd = 'D'
     C                   eval      h1pfds = ' On-Demand '
      *
     C                   when      ldpfcd = 'F'
     C                   eval      h1pfds = '   Final   '
      *
     C                   when      ldpfcd = 'P'
     C                   eval      h1pfds = 'Preliminary'
     C                   endsl
      *
      * Detail/Summary
      *
     C                   select
     C                   when      lddsfl = 'D'
     C                   eval      h1dsds = 'Detail'
     C                   seton                                        86
      *
     C                   when      lddsfl = 'S'
     C                   eval      h1dsds = 'Summary'
     C                   endsl
      *
      * Primal Group Code
      *
     C                   if        ldpgcd = *blank
     C                   seton                                        87
     C                   endif
      *
      * Production Line Code
      *
     C                   if        ldplcd = *blank
     C                   seton                                        89
     C                   endif
      * Item
     C                   if        lditcd = 0
     C                   seton                                        88
     C                   endif
      *
     C                   endsr
      /eject
      *-------------------------------------------------------------
      * Report lines
      *-------------------------------------------------------------
      *
     Oprint198  e            h1hdr          1 01
     O                       sdpgm               10
     O                                          100 'Resource Optimization '
     O                                          106 'System'
     O                                          178 'DATE'
     O                       udate         y    188
      *
      *
     O          e            h1hdr          1
     O                       sdusr               10
     O                                          100 'Meat Cost Report'
     O                                          178 'TIME'
     O                       rtime              188 '  :  :  '
      *
     O          e            h1hdr          1
     O                       h1pfds              97
     O                                          178 'PAGE'
     O                       page          z    188
      *
      *
     O          e            h1hdr          1
     O                                           21 'Company:'
     O                       ldcono        z     26
     O                       h1dsds              95
      *
     O          e            h1hdr          1
     O                                           21 'Week-ending date:'
     O                       ldwemdy             31 '  /  /  '
      *
     O          e            h1hdr          1
     O                                           21 'Primal group code:'
     O              n87      ldpgcd              29
     O              n87      ldpgds              63
     O               87                          26 'All'
      *
      *
     O          e            h1hdr          1
     O                                           21 'Production line code:'
     O              n89      ldplcd              29
     O               89                          26 'All'
      *
     O          e            h1hdr          2
     O                                           21 'Item code:'
     O              n88      lditcd        z     30
     O              n88      lditds              63
     O               88                          26 'All'
      *
      *
      *-------------------------------------------------------------
      * Column headings
      *-------------------------------------------------------------
     O          e            h1hdr          1
     O                                            6 'Primal'
     O                                           18 'Production'
     O               86                          36 'Comp'
     O                                          102 'Sales'
     O                                          165 'Produced'
      *
     O          e            h1hdr          1
     O                                            5 'Group'
     O                                           15 'Line'
     O                                           26 'Item'
     O               86                          36 'Item'
     O               86                          42 'BOM'
     O                                           52 'Yielded'
     O                                           66 'Base'
     O                                           78 'Credit'
     O               86                          92 'Meat Cost'
     O                                          102 'Yield'
     O               86                         114 'Consumed'
     O                                          128 'Produced'
     O                                          148 'Produced'
     O                                          165 'Value'
      *
     O          e            h1hdr          1
     O                                            4 'Code'
     O                                           15 'Code'
     O                                           26 'Code'
     O               86                          36 'Code'
     O               86                          42 'Type'
     O                                           53 'Meat Cost'
     O                                           66 'Price'
     O                                           78 'Value'
     O               86                          92 'Group Code'
     O                                          102 'Percent'
     O               86                         114 'Percent'
     O                                          128 'Price/LB'
     O                                          148 'Pounds'
     O                                          165 'Amount'
     O                                          189 'Base Item Description'
      *
     O          e            h1hdr          0
     O                       dash               188
      *
      *-------------------------------------------------------------
      * Heading line for Primal Group/Production Line
      *-------------------------------------------------------------
      *
     O          e            h2hdr       2  0
     O                       svpgcd               6
     O                       svplcd              17
      *
     O          e            h2hdr       0  0
     O                       svpgcd               6
     O                       svplcd              17
      *
      *-------------------------------------------------------------
      * Detail line one  (items)
      *-------------------------------------------------------------
      *
     O          e            l1dtl       1  0
     O                       imitcd        m     27
     O                       imymco        k     54
     O               81                          55 '*'
     O                       imbspr        k     67
     O                       imcrvl        k     79
     O                       l1sypc         b   104 '  0.    -%'
     O                       impupr        k    129
     O                       impulb        k    149
     O                       l1puam        k    166
     O                       imbids             198
      *
      *-------------------------------------------------------------
      * Detail line two  (component items)
      *-------------------------------------------------------------
      *
     O          e            l2dtl       0  1
     O                       cmciitcd      m     37
     O                       cmbomty             41
     O                       cmcibspr      k     67
     O                       cmcicrvl      k     79
     O                       cmcgcd              89
     O               84n82   cmsypc             104 '  0.    -%'
     O               85n83   cmcspc             116 '  0.    -%'
      /eject
      *-------------------------------------------------------------
      * Blank line
      *-------------------------------------------------------------
      *
     O          e            blank       1
      /eject
      *-------------------------------------------------------------
      * No data line
      *-------------------------------------------------------------
      *
     O          e            nodata      2
     O                                           19 'No data meets your '
     O                                           38 'selection criteria.'
      /eject
