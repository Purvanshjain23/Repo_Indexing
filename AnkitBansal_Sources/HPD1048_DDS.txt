     A*%%TS  SD  20021127  093244  ISLRAMS     REL-V5R1M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION
     A*  DISPLAY FILE:  HPD1048
     A*  TITLE:         REVISE REPORTED WEEKLY ACTIVITY
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  DATE:          09/25/02
     A*
     A*  INDICATOR 60 IS ON WHEN IN REVISE MODE
     A*  INDICATOR 61 IS ON WHEN IN CREATE MODE
     A*  INDICATOR 62 IS ON WHEN IN DELETE MODE
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A  61N90                               CF04(04 'PROMPT')
     A N90                                  CF05(05 'REFRESH')
     A N90                                  CF07(07 'DEATHS')
     A N90                                  CF09(09 'ACCEPT')
     A N61N90                               CF11(11 'DELETE')
     A*
     A*-----------------------------------------------------------------
     A* RECORD FORMAT ONE
     A*-----------------------------------------------------------------
     A          R HP1048R1
     A*%%TS  SD  20021127  093244  ISLRAMS     REL-V5R1M0  5722-WDS
     A                                      SETOF(30)
     A                                      SETOF(31)
     A                                      SETOF(32)
     A                                      SETOF(33)
     A                                      SETOF(34)
     A                                      SETOF(35)
     A                                      SETOF(36)
     A                                      SETOF(37)
     A                                      SETOF(38)
     A                                      SETOF(39)
     A                                      SETOF(50)
     A                                      SETOF(52)
     A                                      SETOF(58)
     A                                      SETOF(59)
     A                                      RTNCSRLOC(&RCD &FLD)
     A                                      OVERLAY
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 29'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A            D1MODE         6A  O  2  2DSPATR(HI)
     A                                  2 24'REVISE REPORTED WEEKLY ACTIVITY'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  4  2'Week-ending date ..'
     A            D1WEMDY   R        B  4 22REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A  31                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A N31 61                               DSPATR(HI)
     A  61                                  DSPATR(UL)
     A  60
     AO 62                                  DSPATR(PR)
     A                                  4 39'Calendar year/week:'
     A            D1CDYR    R        O  4 59REFFLD(HSRREF/@@CDYR *LIBL/HSPREF)
     A                                      EDTCDE(M)
     A            D1CDWK    R        O  4 65REFFLD(HSRREF/@@CDWK *LIBL/HSPREF)
     A                                      EDTCDE(M)
     A                                  6 10'ENTER:'
     A                                      DSPATR(HI)
     A                                  6 18'Farm site ..'
     A  61                              6 31'?'
     A            D1FSCD    R        B  6 33REFFLD(HSRREF/@@FSCD *LIBL/HSPREF)
     A                                      EDTCDE(M)
     A  30
     AO 50                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A N30 61                               DSPATR(HI)
     A  61                                  DSPATR(UL)
     A  60
     AO 62                                  DSPATR(PR)
     A                                  7 18'Building ...'
     A  61                              7 31'?'
     A            D1BLCD    R        B  7 33REFFLD(HSRREF/@@BLCD *LIBL/HSPREF)
     A  38
     AO 58                                  DSPATR(PC)
     A  38                                  DSPATR(RI)
     A N38 61                               DSPATR(HI)
     A  61                                  DSPATR(UL)
     A  60
     AO 62                                  DSPATR(PR)
     A                                  8 18'Room .......'
     A  61                              8 31'?'
     A            D1RMCD    R        B  8 33REFFLD(HSRREF/@@RMCD *LIBL/HSPREF)
     A  39
     AO 59                                  DSPATR(PC)
     A  39                                  DSPATR(RI)
     A N39 61                               DSPATR(HI)
     A  61                                  DSPATR(UL)
     A  60
     AO 62                                  DSPATR(PR)
     A                                  9 10'--OR--'
     A                                      DSPATR(HI)
     A                                 10 18'Hog group ..'
     A  61                             10 31'?'
     A            D1HGCD    R        B 10 33REFFLD(HSRREF/@@HGCD *LIBL/HSPREF)
     A  32
     AO 52                                  DSPATR(PC)
     A  32                                  DSPATR(RI)
     A N32 61                               DSPATR(HI)
     A  61                                  DSPATR(UL)
     A  60
     AO 62                                  DSPATR(PR)
     A                                 12  2'Beginning inventory head .....'
     A            D1BGINHD  R        B 12 33REFFLD(HSRREF/@@BGINHD *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A  33                                  DSPATR(PC)
     A  33                                  DSPATR(RI)
     A N62N33                               DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(PR)
     A                                 13  2'Transferred in head ..........'
     A            D1TIHD    R        B 13 33REFFLD(HSRREF/@@TIHD *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A  34                                  DSPATR(PC)
     A  34                                  DSPATR(RI)
     A N62N34                               DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(PR)
     A                                 15  2'Transferred out head .........'
     A            D1TOHD    R        B 15 33REFFLD(HSRREF/@@TOHD *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A  35                                  DSPATR(PC)
     A  35                                  DSPATR(RI)
     A N62N35                               DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(PR)
     A                                 16  2'Cull sale head ...............'
     A            D1CUHD    R        B 16 33REFFLD(HSRREF/@@CUHD *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A  36                                  DSPATR(PC)
     A  36                                  DSPATR(RI)
     A N62N36                               DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(PR)
     A                                 17  2'Market/feeder pig sale head ..'
     A            D1MKHD    R        B 17 33REFFLD(HSRREF/@@MKHD *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A  37                                  DSPATR(PC)
     A  37                                  DSPATR(RI)
     A N62N37                               DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(PR)
     A                                 19  2'Dead head ....................'
     A            D1KDHD    R        O 19 33REFFLD(HSRREF/@@ENINHD *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A                                 20  2'Ending inventory head ........'
     A            D1ENINHD  R        O 20 33REFFLD(HSRREF/@@ENINHD *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A*-----------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*-----------------------------------------------------------------
     A          R HP1048K1
     A*%%TS  SD  20021105  131156  ISLRAMS     REL-V5R1M0  5722-WDS
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A  61N90                          23 11'F4=Prompt'
     A N90                             23 22'F5=Refresh'
     A N90                             23 34'F7=Maintain deaths'
     A N90                             23 54'F9=Accept'
     A N90N61                          23 65'F11=Delete'
     A*
      *------------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *-----------------------------------------------------------------
     A          R HP1048ES                  SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
      *------------------------------------------------------------------
      * ERROR MESSAGE SUBFILE CONTROL FORMAT
      *-----------------------------------------------------------------
     A          R HP1048EC                  SFLCTL(HP1048ES)
      *
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
