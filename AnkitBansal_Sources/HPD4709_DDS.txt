     A*%%TS  SD  20050323  135849  ISLRAMS     REL-V5R2M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD4709
     A*  TITLE:         SPECIFY OPTIONS FOR ORDER REQUESTS ON HOLD REPORT
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  DATE:          07/21/04
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A                                      CF04(04 'PROMPT')
     A                                      CF08(08 'SUBMIT')
     A                                      CF17(17 'PRINT OPTIONS')
     A                                      CF20(20 'WRKSPLF')
     A*
     A*--------------------------------------------------------------*
     A* RECORD FORMAT ONE
     A*--------------------------------------------------------------*
     A          R HP4709R1
     A*%%TS  SD  20050323  135849  ISLRAMS     REL-V5R2M0  5722-WDS
     A                                      SETOF(30)
     A                                      SETOF(31)
     A                                      SETOF(33)
     A                                      SETOF(34)
     A                                      SETOF(35)
     A                                      SETOF(36)
     A                                      SETOF(37)
     A                                      SETOF(38)
     A                                      SETOF(39)
     A                                      SETOF(50)
     A                                      SETOF(51)
     A                                      SETOF(53)
     A                                      SETOF(54)
     A                                      SETOF(55)
     A                                      SETOF(56)
     A                                      SETOF(59)
     A                                      OVERLAY
     A                                      RTNCSRLOC(&RCD &FLD)
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 27'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  2 14'SPECIFY OPTIONS FOR ORDER REQUESTS-
     A                                       ON HOLD REPORT'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  4  2'Version .............'
     A                                  4 24'?'
     A            D1VER     R        B  4 26REFFLD(HSRREF/@@AIST *LIBL/HSPREF)
     A N33                                  DSPATR(HI)
     A  33
     AO 53                                  DSPATR(PC)
     A  33                                  DSPATR(RI)
     A            D1VERDS       32A  O  4 35
     A                                  5  2'E-mail report .......'
     A                                  5 24'?'
     A            D1EMAIL   R        B  5 26REFFLD(@@FLAG)
     A N30                                  DSPATR(HI)
     A  30
     AO 50                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A                                  7  2'Production phase ....'
     A                                  7 24'?'
     A            D1PPCD    R        B  7 26REFFLD(PPPPCD)
     A N35                                  DSPATR(HI)
     A  35
     AO 55                                  DSPATR(PC)
     A  35                                  DSPATR(RI)
     A            D1PPDS    R        O  7 35REFFLD(PPPPDS)
     A                                  7 66'(Blank for all)'
     A                                  8  2'Cell manager ........'
     A                                  8 24'?'
     A            D1CMGCD   R        B  8 26REFFLD(HSRREF/@@MGCD *LIBL/HSPREF)
     A N36                                  DSPATR(HI)
     A  56
     AO 36                                  DSPATR(PC)
     A  36                                  DSPATR(RI)
     A                                      DSPATR(UL)
     A            D1CMGNM   R        O  8 35REFFLD(HSRREF/MGMGNM *LIBL/HSPREF)
     A                                  8 66'(Blank for all)'
     A*
     A                                  9  2'Production manager ..'
     A                                  9 24'?'
     A            D1PMGCD   R        B  9 26REFFLD(HSRREF/@@MGCD *LIBL/HSPREF)
     A N31                                  DSPATR(HI)
     A  51
     AO 31                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A                                      DSPATR(UL)
     A            D1PMGNM   R        O  9 35REFFLD(HSRREF/MGMGNM *LIBL/HSPREF)
     A                                  9 66'(Blank for all)'
     A*
     A                                 10  2'Cell ................'
     A            D1CELL    R        B 10 26REFFLD(@@CELL)
     A                                      EDTCDE(M)
     A N37                                  DSPATR(HI)
     A  37                                  DSPATR(PC)
     A  37                                  DSPATR(RI)
     A                                 10 66'(Blank for all)'
     A                                 11  2'Farm site ...........'
     A                                 11 24'?'
     A            D1FSCD    R        B 11 26REFFLD(FSFSCD)
     A                                      EDTCDE(M)
     A N38                                  DSPATR(HI)
     A  38
     AO 58                                  DSPATR(PC)
     A  38                                  DSPATR(RI)
     A            D1FSNM    R        O 11 35REFFLD(FSFSNM)
     A                                 11 66'(Blank for all)'
     A                                 12  2'Work order number ...'
     A                                 12 24'?'
     A            D1WO      R        B 12 26REFFLD(HSRREF/@@WO *LIBL/HSPREF)
     A N39                                  DSPATR(HI)
     A  39
     AO 59                                  DSPATR(PC)
     A  39                                  DSPATR(RI)
     A                                 12 66'(Blank for all)'
     A                                 14  2'When you E-mail a report, it will -
     A                                      always page-break on Manager; (Cell-
     A                                       Manager'
     A                                      DSPATR(HI)
     A                                      COLOR(PNK)
     A                                 15 11'for Version 1; Production Manager -
     A                                      for Version 2.)'
     A                                      COLOR(PNK)
     A                                 16  2'Version 3 does not allow page brea-
     A                                      ks on Manager.'
     A                                      DSPATR(HI)
     A                                      COLOR(PNK)
     A                                 17  2'When you select Version 1 or Versi-
     A                                      on 2 and you are not E-mailing, pag-
     A                                      e breaks'
     A                                      COLOR(PNK)
     A                                 18 11'are optional:'
     A                                      COLOR(PNK)
     A                                 18 26'Page break on manager ..'
     A                                 18 51'?'
     A            D1PBFL    R        B 18 53REFFLD(@@FLAG)
     A N34                                  DSPATR(HI)
     A  34
     AO 54                                  DSPATR(PC)
     A  34                                  DSPATR(RI)
     A*
     A*--------------------------------------------------------------*
     A* COMMAND KEY RECORD 1
     A*--------------------------------------------------------------*
     A          R HP4709K1
     A*%%TS  SD  19941028  131416  ISLRAMS     REL-V3R0M5  5763-PW1
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A                                 23 11'F4=Prompt'
     A                                 23 22'F8=Submit'
     A                                 23 33'F17=Print Options'
     A                                 23 52'F20=Spool Files'
      *
      *--------------------------------------------------------------*
      * ERROR MESSAGE SUBFILE FORMAT
      *--------------------------------------------------------------*
     A          R HP4709ES                  SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
     A*
     A*--------------------------------------------------------------*
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*--------------------------------------------------------------*
     A*
     A          R HP4709EC                  SFLCTL(HP4709ES)
     A*%%TS  SD  19940415  103325  ISRCENT     REL-V2R2M0  5738-PW1
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
