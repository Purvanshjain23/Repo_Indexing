      *****************  RPG PROGRAM HEADING  ************************
     h option(*SRCSTMT:*NODEBUGIO)
      *
      *
      * SYSTEM:      Hog Production System
      * PROGRAM:     HP3102
      * PROJECT:     E13022
      * TITLE:       Update Processed flag to 'Y'
      * PROGRAMMER:  Brad Baden
      * CREATED:      5/04/2018
      *
      * FUNCTION:  Batch program to update records in HSP196
      *            This is called from HP4082CL - Move Edited Scheduled Transfers into HPS.
      *            This program is called after program HP3101 creates Delivery Sched
      *            Detail INT records for HPE.  IT reads all of the HSP196 records where
      *            the Processed flag is 'N' and changes the value to 'Y' so that the
      *            records are not picked up on a future execution of the HPS to HPE upload.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * xx/xx/xx  xxxxxxxxxxxxx (Exxxxx)
      *           xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx

      ****************************************************************
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fhsp196    uf   e             disk
      *    Scheduled Load Planning File (records selected by open query)
      *
      /EJECT
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Standard fields
      *
      *
      * Work fields
      *
     D wkisodate       s               d   datfmt(*iso)
     D wkupdt          s                   like(lpupdt)
     D wkuptm          s                   like(lpuptm)
     D wkupusr         s                   like(lpupusr)
      *
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * MAINLINE
      ****************************************************************
      *
      * Prepare to read the file.
      *
     C     *start        setll     hsp196
      *
      * Read all records in the file that were selected by the open query.
      *
     C                   dou       *in90 = *on                                  Main do
     C                   read      hsp196                                 90
     C                   if        *in90 = *off                                 if not EOF
      *
      ** If the value of the Processed flag is No
     C                   if        lprcfl = no                                  if lprcfl = no
      *
      ** Change the value of the Processed flag to Yes and set the audit stamp
     C                   eval      lprcfl = yes
     C                   eval      lpupdt  = wkupdt
     C                   eval      lpuptm   = wkuptm
     C                   eval      lpupusr  = wkupusr
      *
      ** Update the current HSP196 record
     C                   update    lprec
      *
     C                   endif                                                  fi lprcfl = no
      *
     C                   endif                                                  fi not EOF
     C                   enddo                                                  Main do
      *
     C                   eval      *inlr = *on
      *
      /EJECT
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      *
      * Set up the date/time/user for time-stamping the HSP196 record
      *
     C     *mdy          move      udate         wkisodate
     C                   move      wkisodate     wkupdt
      *
     C                   time                    wkuptm
     C                   move      sdusr         wkupusr
      *
     C                   endsr
