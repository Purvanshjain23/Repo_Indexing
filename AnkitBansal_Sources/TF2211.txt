      *****************  RPG PROGRAM HEADING  ************************
     h option(*SRCSTMT:*NODEBUGIO)
      ****************************************************************
      *
      * SYSTEM:      Triumph Foods
      * PROGRAM:     TF2211 -Datamart MARGIN: Build Weekly Margins
      * PROGRAMMER:  LeAnne Ramsey
      * CREATED:     02/06/08
      *
      * Function:    This program builds one record for each Week/Item.
      *              Only Finished Goods data is included.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 06/02/08  LeAnne Ramsey
      *           Per Alice, default N=No into Mix, Volume, Raw Material flags.
      *
      * 09/11/08  LeAnne Ramsey
      *           Recompile only. Export/Domestic Flag was added to TFP010-
      *           Weekly Product Revenue Detail file.
      *
      * 06/02/10  LeAnne Ramsey
      *           Added a field to TFP8014:
      *              ADWEDTMDCY-Week Ending Date in date format mm/dd/ccyy
      *
      * 06/15/11  LeAnne Ramsey (E1588)
      *           Added 2 fields to TFP8014:
      *              ADEXLB-Exception Lbs
      *              ADLESSEXLB-Production Lbs less Exception Lbs
      *
      * 02/21/12  LeAnne Ramsey (E1950)
      *           Added 3 fields to TFP8014 for Brad Hamilton:
      *              ADPIECEFL-Piece Flag
      *              ADPIECES-Number of Pieces
      *              ADISCQTY-ISC Sales Production Quantity
      *
      *           NOTE: These fields don't have ANYTHING to do with TFS...but Brad H.
      *           wanted them in this cube because it already had Produced Lbs.
      *
      * 09/05/12  LeAnne Ramsey (E2243)
      *           Recompile only.
      *           Per Damon G. we must now write more/special records to TFP014 for the
      *           Items that receive lbs/dollars from Skirt Meat Items!! So, I had to add
      *           a new field to TFP014:
      *                         ADSMSFL-Skirt Meat Split Flag
      *
      * 11/15/12  LeAnne Ramsey (E2337)
      *           Recompile only.
      *           Rose had to increase the price/cwt field sizes in PPBTCPP-Inventory
      *           Value file and PPBSCPP-Inventory Stock Closing.
      *
      * 11/14/17  Danny Nguyen  (R12011A-Weekly Product Revenue)
      *           Recompile only. Added 25 STF fields to TFP010 file.
      *
      * 02/08/22  Danny Nguyen   - DO2484 - WI479 STF Variance Reporting
      *           Recompile only due to TFP014 DBF change.
      *           Recompile only. Added 2 STF fields to TFP010 file.
      *           PRXYPC   - STF STD YIELD %
      *           PRXPMPPC - STF PUMP %
      *
      * 10/10/22  R Centonze   W105621 - Increase price fields to handle > 999.99
      *                        CHANGE @@ANPRPR TO SIZE 10.6  --> pranprpr tfp010
      *         COPY @@SPU1MG TO  @@SPU1MG10 SIZE 10.6  -> FIELD TFP014.ADSPU1MG
      *         COPY @@TPU1MG TO  @@TPU1MG10 SIZE 10.6  -> FIELD TFP014.ADTPU1MG
      *         COPY @@SSL1MG TO  @@SSL1MG10 SIZE 10.6  -> FIELD TFP014.ADSSL1MG
      *         COPY @@SSL2MG TO  @@SSL2MG10 SIZE 10.6  -> FIELD TFP014.ADSSL2MG
      *         COPY @@TSL1MG TO  @@TSL1MG10 SIZE 10.6  -> FIELD TFP014.ADTSL1MG
      *         COPY @@TSL2MG TO  @@TSL2MG10 SIZE 10.6  -> FIELD TFP014.ADTSL2MG
      * 05/07/24  Santosh Patil P310149 - Field length is increased
      *           in TFP014. Function is recompiled only.
      *
      /eject
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fombyrel1  if   e           k disk
      *  Item default
      *
      *
     Fppbscpl3  if   e           k disk
      *    Inventory stock closing
      *
      *
     Ftfp010    if   e           k disk
      *  Weekly product revenue detail file
      *
      *
     Ftfl014e   if   e           k disk    rename(adrec:adrece) prefix(p1)
      *  Product margin adjustment transaction detail
      *  (LF selects only Finished Goods)
      *
      *
     Ftfl019c   if   e           k disk
      *  Product exceptions
      *
      *
     Ftfp8014   o    e           k disk
      *  Datamart TFS: Weekly margins
      *
      /eject
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Control fields
      *
     D first           s              1    inz('Y')
      *
     D svwedt          s                   like(ad80wedt)
     D svprcd          s                   like(adprcd)
      *
      *
      * Workfields for date manipulation
      *
     D wkisodate       s               D   datfmt(*iso)
      *
      *
      * Workfields
      *
     D wkcono          s                   like(adcono)
      *
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *---------------------------------------------------------------
      * Definition for external data area DATFMARG
      *---------------------------------------------------------------
     D
     Ddatfmarg        uds
     D  da099dt               30     37  0
     D  dadmdt                80     87  0
     D  dasbmfl              100    100
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************************************
      * Mainline
      ****************************************************************************************
      *
      * The TFS Adjustment Detail records have both Seaboard and Triumph data in a single
      * record. For the Datamart file, we must write separate records for the 2 companies.
      * Since we will be processing very few records for each week, we will make 2 passes
      * thru the file (instead of defining/using workfields for accumulating).
      *
     C                   z-add     360           wkcono
     C                   move      yes           first
     C                   exsr      $summarize
      *
     C                   z-add     960           wkcono
     C                   move      yes           first
     C                   exsr      $summarize
      *
     C                   seton                                        lr
      /eject
      *-----------------------------------------------------------------------------------------
      * Process the Product Margin Adjustment Transaction Detail records
      *-----------------------------------------------------------------------------------------
      *
     C     $summarize    begsr
      *
     C     dadmdt        setgt     tfl014e
      *
     C                   dou       *in90 = *on or p1adwedt > da099dt            Do summary
     C                   read      tfl014e                                90
     C                   if        *in90 = *off and                             If not EOF
     C                             p1adwedt <= da099dt
      * Control break
     C                   select
     C                   when      first = yes
     C                   move      no            first
     C                   exsr      $directmap
     C                   exsr      $inventory
      *
     C                   if        p1adpefl = yes
     C                   exsr      $exception
     C                   endif
      *
     C                   when      p1adwedt <> svwedt or
     C                             p1adprcd <> svprcd
     C                   exsr      $wrt8014
     C                   exsr      $directmap
     C                   exsr      $inventory
      *
     C                   if        p1adpefl = yes
     C                   exsr      $exception
     C                   endif
     C                   endsl
      *
      * Detail processing
     C                   exsr      $detail
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Do summary
      *
      * EOF processing
      *
     C                   if        first = no
     C                   exsr      $wrt8014
     C                   endif
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Detail processing to accumulate values
      *---------------------------------------------------------------
      *
     C     $detail       begsr
      *
     C                   select
     C                   when      wkcono = 360
     C                   add       p1adspulb     adpulb
     C                   add       p1adspuam     adpuam
     C                   add       p1adspuslb    adpuslb
     C                   add       p1adspumam    adpumam
     C                   add       p1adspulam    adpulam
     C                   add       p1adspukam    adpukam
     C                   add       p1adspuiam    adpuiam
     C                   add       p1adspuoam    adpuoam
     C                   add       p1adspupam    adpupam
     C                   add       p1adstrpam    adstrpam
      *
     C                   when      wkcono = 960
     C                   add       p1adtpulb     adpulb
     C                   add       p1adtpuam     adpuam
     C                   add       p1adtpuslb    adpuslb
     C                   add       p1adtpumam    adpumam
     C                   add       p1adtpulam    adpulam
     C                   add       p1adtpukam    adpukam
     C                   add       p1adtpuiam    adpuiam
     C                   add       p1adtpuoam    adpuoam
     C                   add       p1adtpupam    adpupam
     C                   endsl
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Write a Datamart "Weekly Margins" record
      *---------------------------------------------------------------
      *
     C     $wrt8014      begsr
      *
      * If the Mix, Volume, Raw Material flags are blank, just plop a N=No into
      * them so that the Datamart Cube will have something in the fields.
      *
     C                   if        admixfl = *blank
     C                   move      no            admixfl
     C                   endif
      *
     C                   if        advolfl = *blank
     C                   move      no            advolfl
     C                   endif
      *
     C                   if        adrmfl = *blank
     C                   move      no            adrmfl
     C                   endif
      *
      * Subtract Exception Pounds from the Produced Lbs
      *
     C     adpulb        sub       adexlb        adlessexlb
      *
      * Retrieve the "Piece Count" info that Brad Hamilton requested (E1950)
      *
     C                   exsr      $piece
      *
      * Write record
     C                   write     adrec
     C                   clear                   adrec
      *
     C                   endsr
      /eject
      *--------------------------------------------------------------------------------------
      * Set up Datamart fields and save control fields
      *--------------------------------------------------------------------------------------
      *
     C     $directmap    begsr
      *
      * Save control fields
     C                   z-add     p1adwedt      svwedt
     C                   z-add     p1adprcd      svprcd
      *
      * Set up Datamart fields that are a direct map from the Margin Adjustment Detail file
      *
     C                   z-add     wkcono        adcono
     C                   z-add     p1adprcd      adprcd
     C                   move      p1aditycd     aditycd
     C                   move      p1adtfcgcd    adtfcgcd
     C                   move      p1adtfclcd    adtfclcd
     C                   move      p1adistycd    adistycd
     C                   z-add     p1adisgrcd    adisgrcd
     C                   z-add     p1adisclcd    adisclcd
     C                   move      p1admixgrp    admixgrp
      *
     C                   move      p1adcoownfl   adcoownfl
     C                   move      p1adprdcmp    adprdcmp
     C                   z-add     p1adtownpc    adtownpc
      *
     C                   move      p1admixfl     admixfl
     C                   move      p1advolfl     advolfl
     C                   move      p1adrmfl      adrmfl
      *
     C                   move      p1adglsub     adglsub
     C                   z-add     p1adypc       adypc
     C                   z-add     p1adpumppc    adpumppc
      *
     C                   z-add     p1adyr        adyr
     C                   z-add     p1adwk        adwk
     C                   z-add     p1adwbdt      ad80wbdt
     C                   z-add     p1adwedt      ad80wedt
      *
     C                   z-add     p1adpe        adpe
     C                   z-add     p1adpbdt      ad80pbdt
     C                   z-add     p1adpedt      ad80pedt
      *
      * Get dates into the "date" format.
      *
     C     *iso          move      ad80wbdt      wkisodate
     C                   move      wkisodate     adwbdt
      *
     C     *iso          move      ad80wedt      wkisodate
     C                   move      wkisodate     adwedt
     C                   move      adwedt        adwedtmdcy
      *
     C     *iso          move      ad80pbdt      wkisodate
     C                   move      wkisodate     adpbdt
      *
     C     *iso          move      ad80pedt      wkisodate
     C                   move      wkisodate     adpedt
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Retrieve Inventory for a Week/Product
      *---------------------------------------------------------------
      *
     C     $inventory    begsr
      *
      * Retrieve Beginning/Ending Inventory values from the Weekly Revenue Detail
      * for this Week/Item. Only process records where Product Exempt Flag is NO.
      *
     C     key01         setll     tfp010
      *
     C                   dou       *in91 = *on                                  Do loop
     C     key01         reade     tfp010                                 91
     C                   if        *in91 = *off and prprexfl = no               If not EOF
      *
     C                   select
     C                   when      wkcono = 360
     C                   add       prsbilb       adbilb
     C                   add       prseilb       adeilb
     C                   add       prsbiam       adbiam
     C                   add       prseiam       adeiam
      *
     C                   when      wkcono = 960
     C                   add       prtbilb       adbilb
     C                   add       prteilb       adeilb
     C                   add       prtbiam       adbiam
     C                   add       prteiam       adeiam
     C                   endsl
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Do loop
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Retrieve Exception Pounds for a Week/Product
      *---------------------------------------------------------------
      *
     C     $exception    begsr
      *
     C     key01         setll     tfl019c
      *
     C                   dou       *in91 = *on                                  Do loop
     C     key01         reade     tfl019c                                91
     C                   if        *in91 = *off                                 If not EOF
      *
     C                   select
     C                   when      wkcono = 360
     C                   add       pesexpulb     adexlb
      *
     C                   when      wkcono = 960
     C                   add       petexpulb     adexlb
     C                   endsl
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Do loop
      *
     C                   endsr
      /eject
      *-----------------------------------------------------------------------------------------
      * Retrieve the "Piece Count" info that Brad Hamilton requested in Feb 2012 (E1950)
      *-----------------------------------------------------------------------------------------
      *
     C     $piece        begsr
      *
      * First, retrieve the Item Default info.
      * Only process Items that are 1) Piece Count Items and 2) in a Box.
      *
     C                   move      no            adpiecefl
      *
     C     adprcd        chain     ombyrel1                           92
     C                   if        *in92 = *off and                             If Piece/Box
     C                             bypcct = Yes and
     C                             byhhcd = 'BX'
      *
     C                   move      yes           adpiecefl
      *
      * Accumulate the ISC Sales Production Qty for this Item/Week
      * (Compare the Company before populating....we didn't have a LF with
      *  Company as part of the key...so, read and check.)
      *
     C     key02         setll     ppbscpl3
      *
     C                   dou       *in91 = *on                                  Do ISC qty
     C     key02         reade     ppbscpl3                               91
     C                   if        *in91 = *off and                             If not EOF
     C                             bsb2c3 = wkcono
     C                   add       bsoiqt        adiscqty
     C                   endif                                                  If not EOF
     C                   enddo                                                  Do ISC qty
      *
      * Calc the Number of Pieces as:
      *    Bieces per Bag * Bags per Unit * Quantity
      *
     C                   eval(h)   adpieces = bybdnb * bybjnb * adiscqty
     C                   endif                                                  If Piece/Box
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Initialization
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Key lists
      *
     C     key01         klist
     C                   kfld                    adprcd
     C                   kfld                    ad80wedt
      *
     C     key02         klist
     C                   kfld                    ad80wedt
     C                   kfld                    adprcd
      *
     C                   endsr
      /eject
