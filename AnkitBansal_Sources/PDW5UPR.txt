/********************************************************************/
/*                                                                  */
/*  SYNOPSIS -  JOB CONTROL PROMPT FOR PRINTER OVERRIDE INFORMATION */
/*              GENERIC PROMPTING FUNCTION. CAUSE PRINTER OVERRIDE  */
/*              PROMPTING AND PASSES COMPANY, FROM DATE, THROUGH    */
/*              DATE, DOLLARS, POUNDS, RPT1, RPT2, RPT3.            */
/*                                                                  */
/*  SYSTEM    - ORDER MANAGEMENT SYSTEM                             */
/*  PROGRAM   - PRINT REPORTS                                       */
/*  DATE      - 08/23/95                                            */
/*  MODIFIED  - 12/02/2005 LJB                                      */
/*              07/03/2007 JRB DLY029 ADDED TYPE GROUP AND CLASS    */
/*                                                                  */
/*  PDW5UPR   - PMT PASS RANKING FLDS  UP                           */
/*                                                                  */
/*  PARAMETERS:                                                     */
/*                                                                  */
/* USE  FIELD      DESCRIPTION                   INIT VALUE         */
/*                                                                  */
/*  O   &RETURN  - RETURN CODE                   BLANK              */
/*  I   &APPLIC  - APPLICATION CODE              O/M                */
/*  I   &CLP     - CL PROGRAM TO SUBMIT          OMNNPCLP           */
/*  I   &PGM     - RPG PROGRAM TO SUBMIT                            */
/*                  (THIS PROGRAM DOESN'T EXIST ANYMORE)            */
/*  I   &DESC    - PRINT PROGRAM DESCRIPTION     PRINT RANKING REPT */
/*                  (FOR PROMPT SCREEN)                             */
/*  B   &OUTQ    - OUTPUT QUEUE                  PROMPTED VALUE     */
/*                  IF THIS IS BLANK AT START,                      */
/*                  PROMPT PGM IS CALLED; ELSE                      */
/*                  THE PRINT CL IS SUBMITTED                       */
/*                  WITHOUT PROMPTING                               */
/*  B   &HOLD    - HOLD STATUS                   PROMPTED VALUE     */
/*  B   &SAVE    - SAVE STATUS                   PROMPTED VALUE     */
/*  B   &NQSTAT  - INDICATES WHETHER MUST RUN    PROMPTED VALUE     */
/*                  IN NIGHT QUEUE                                  */
/*  B   &NIGHTQ  - NIGHT JOB QUEUE               PROMPTED VALUE     */
/*  B   &COMPNYN - COMPANY NUMBER (PASSED)       PROMPTED VALUE     */
/*  B   &FRMDATN - FROM DATE      (PASSED)       PROMPTED VALUE     */
/*  B   &THRDATN - THRU DATE      (PASSED)       PROMPTED VALUE     */
/*  B   &DOLLARN - DOLLARS        (PASSED)       SALES EXCEED DOLS  */
/*  B   &POUNDSN - POUNDS         (PASSED)       SALES EXCEED PNDS  */
/*  B   &MEMOFLG - CM/DM FLAG     (PASSED)       A                  */
/*  B   &RPT1    - REPORT SEL 1   (PASSED)       N                  */
/*  B   &RPT2    - REPORT SEL 2   (PASSED)       N                  */
/*  B   &RPT3    - REPORT SEL 3   (PASSED)       N                  */
/*  B   &CSTSEQ  - CUSTOMER SEQ   (PASSED)       PROMPTED VALUE     */
/*  B   &COPYN   - NO. OF COPIES  (PASSED)       1                  */
/*  B   &TYPE    - ITEM STRUCTURE TYPE                              */
/*  B   &GROUP   - ITEM STRUCTURE GROUP                             */
/*  B   &CLASS   - ITEM STRUCTURE CLASS                             */
/*                                                                  */
/********************************************************************/
 PDW5UPR:    PGM        PARM(&RETURN &APPLIC &CLP &PGM &DESC &OUTQ +
                          &HOLD &SAVE &NQSTAT &NIGHTQ &COMPNYN +
                          &FRMDATN &THRDATN &DOLLARN &POUNDSN +
                          &COPYN &MEMOFLG &RPT1 &RPT2 &RPT3 +
                          &CSTSEQ &TYPE &GROUP &CLASS)
/* PARMS                                                                      */
        DCL    &RETURN     *CHAR   (7)           /* Return Code = ' '         */
        DCL    &APPLIC     *CHAR   (06)          /* Application Code = O/M    */
        DCL    &CLP        *CHAR   (10)          /* CL to Submit = OMNNPCLP   */
        DCL    &PGM        *CHAR   (10)          /* RPG TO SUBMIT = OMXTPFR   */
        DCL    &DESC       *CHAR   (25)          /* PRT Pgm Description       */
        DCL    &COPYN      *DEC                  /* No. of Copies = 1         */
        DCL    &OUTQ       *CHAR   (10)          /* Output Queue              */
        DCL    &HOLD       *CHAR   (4)           /* Hold Status               */
        DCL    &SAVE       *CHAR   (4)           /* Save Status               */
        DCL    &NQSTAT     *CHAR   (01)          /* If MUST run in Night Q=NO */
        DCL    &NIGHTQ     *CHAR   (10)          /* Night Job Queue           */
        DCL    &COMPNYN    *DEC                  /* Company Number (Passed)   */
        DCL    &FRMDATN    *DEC                  /* FROM DATE      (Passed)   */
        DCL    &THRDATN    *DEC                  /* THROUGH DATE   (Passed)   */
        DCL    &DOLLARN    *DEC                  /* DOLLARS        (Passed)   */
        DCL    &POUNDSN    *DEC                  /* POUNDS         (Passed)   */
        DCL    &MEMOFLG    *CHAR   (1)           /* CM/DM FLAG                */
        DCL    &RPT1       *CHAR   (1)           /* REPORT SEL 1              */
        DCL    &RPT2       *CHAR   (1)           /* REPORT SEL 2              */
        DCL    &RPT3       *CHAR   (1)           /* REPORT SEL 3              */
        DCL    &CSTSEQ     *CHAR   (1)           /* CUSTOMER SEQUENCE         */
             DCL        VAR(&TYPE) TYPE(*DEC) LEN(3 0) /* ITEM +
                          STRUCTURE TYPE */
             DCL        VAR(&GROUP) TYPE(*DEC) LEN(3 0) /* ITEM +
                          STRUCTURE GROUP      */
             DCL        VAR(&CLASS) TYPE(*DEC) LEN(3 0) /* ITEM +
                          STRUCTURE CLASS      */

/* INTERNAL FIELDS                                                            */
        DCL    &COPY       *DEC    (2 0)         /* Number of copies          */

/* ERROR MESSAGE FIELDS                                                       */
        DCL    &MSG        *CHAR   (512)         /* message                   */
        DCL    &MSGID      *CHAR   (7)           /* message id                */
        DCL    &MSGL       *DEC    (5 0)

/********************************************************************/
/*   INITIALIZE VARIABLES                                           */
/********************************************************************/
             CHGVAR     VAR(&RETURN) VALUE('       ')
             CHGVAR     VAR(&COPY) VALUE(&COPYN)

/********************************************************************/
/*   MONITOR FOR ALL MESSAGES AT PROGRAM LEVEL                      */
/********************************************************************/
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(MSGTRAP))

/********************************************************************/
/*   CALL PROMPT PROGRAM                                            */
/********************************************************************/
/*           IF         COND(&OUTQ *NE '   ') THEN(GOTO CMDLBL(SUBMIT))*/

             CALL       PGM(PDG0PVR) PARM(&RETURN &DESC &OUTQ &HOLD +
                          &SAVE &COPY &NQSTAT &NIGHTQ &APPLIC)
             CHGVAR     VAR(&COPYN) VALUE(&COPY)
             IF         COND(&RETURN *NE ' ') THEN(GOTO +
                          CMDLBL(USERQUIT))

/********************************************************************/
/*   SUBMIT PRINT CL PROGRAM                                        */
/*     &CLP IS PASSED IN HARD CODED AS OMNNPCLP                     */
/********************************************************************/
 SUBMIT:     SBMJOB     CMD(CALL PGM(&CLP) PARM(' ' &PGM &COMPNYN +
                          &FRMDATN &THRDATN &DOLLARN &POUNDSN +
                          &MEMOFLG &RPT1 &RPT2 &RPT3 &CSTSEQ &OUTQ +
                          &HOLD &SAVE &COPYN &NIGHTQ &TYPE &GROUP +
                          &CLASS)) JOB(&PGM) JOBQ(&NIGHTQ)

             GOTO       CMDLBL(END)

/*-------------------------USER QUIT--------------------------------*/
 USERQUIT:   CHGVAR     VAR(&RETURN) VALUE('Y2U0005')
             GOTO       CMDLBL(END)

/*----------------------RECEIVE MESSAGES----------------------------*/
 MSGTRAP:    RCVMSG     MSGTYPE(*EXCP) MSGDTA(&MSG) MSGDTALEN(&MSGL) +
                          MSGID(&MSGID)
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(END))

             SNDPGMMSG  MSGID(&MSGID) MSGF(QCPFMSG) MSGDTA(%SST(&MSG +
                          1 &MSGL)) MSGTYPE(*ESCAPE)
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(END))

 END:        ENDPGM
