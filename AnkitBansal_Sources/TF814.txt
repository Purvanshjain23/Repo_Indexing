      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Triumph Foods
      * PROGRAM:     TF814 - Calculate Weekly Yields
      * PROGRAMMER:  LeAnne Ramsey
      * CREATED:     02/20/09
      *
      * Function:    This function uses incoming data to calc/return Weekly Yield values.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      * 11/09/17  Danny Nguyen    - R12011-Kill Cut Data for STF
      *           File TFP050 was changed. Recompile only. No logic change.
      *
      /eject
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Ftfl050a   if   e           k disk
      *    Kill/Cut data
      *
      /eject
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      *---------------------------------------------------------------
      * CONSTANTS
      *---------------------------------------------------------------
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      *---------------------------------------------------------------
      * STANDALONE FIELDS
      *---------------------------------------------------------------
      *
      * Workfields
      *
     D wkpc            s             15  9
      *
      *
      * Parm fields
      *
     D xxwedt          s              8  0
     D xxistycd        s              3  0
     D xxisgrcd        s              3  0
     D xxtfclcd        s              3
     D xxtfcgcd        s              2
     D xxspulb         s             11  2
     D xxspuslb        s             11  2
     D xxtpulb         s             11  2
     D xxtpuslb        s             11  2
      *
     D xxspc           s              7  4
     D xxskllb         s              7  4
     D xxsctlb         s              7  4
     D xxtpc           s              7  4
     D xxtkllb         s              7  4
     D xxtctlb         s              7  4
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * Mainline
      ****************************************************************
      *
      * Retrieve Kill/Cut record for the WeekEnding Date
      *
     C     xxwedt        chain     tfl050a                            92
     C                   if        *in92 = *off                                 If hit
      *
     C                   select
     C                   when      xxtfcgcd = 'OT'
      *
      * Item Structures: 700-Misc and 720-Trim, calc:
      *    1) Produced Pounds per Head Cut
      *    2) Yield % (calc: Produced Pounds / Cold Carcass Pounds)
      *
     C                   when      xxistycd = 700 or xxistycd = 720
      *              SBD
     C                   if        kcscthd <> 0
     C                   eval(h)   xxsctlb = xxspulb / kcscthd
     C                   endif
      *
     C                   if        kcscclb <> 0
     C                   eval      wkpc = (xxspulb / kcscclb) * 100
     C                   if        wkpc <= 999
     C                   z-add     wkpc          xxspc
     C                   endif
     C                   endif
      *
      *              TF
     C                   if        kctcthd <> 0
     C                   eval(h)   xxtctlb = xxtpulb / kctcthd
     C                   endif
      *
     C                   if        kctcclb <> 0
     C                   eval      wkpc = (xxtpulb / kctcclb) * 100
     C                   if        wkpc <= 999
     C                   z-add     wkpc          xxtpc
     C                   endif
     C                   endif
      *
      * Item Structures: 740-Kill and 750-Render, calc:
      *    1) Produced Pounds per Head Killed
      *    2) Yield % (calc: Produced Pounds / Hot Carcass Pounds)
      *
     C                   when      xxistycd = 740 or xxistycd = 750
      *              SBD
     C                   if        kcsklhd <> 0
     C                   eval(h)   xxskllb = xxspulb / kcsklhd
     C                   endif
      *
     C                   if        kcshclb <> 0
     C                   eval      wkpc = (xxspulb / kcshclb) * 100
     C                   if        wkpc <= 999
     C                   z-add     wkpc          xxspc
     C                   endif
     C                   endif
      *              TF
     C                   if        kctklhd <> 0
     C                   eval(h)   xxtkllb = xxtpulb / kctklhd
     C                   endif
      *
     C                   if        kcthclb <> 0
     C                   eval      wkpc = (xxtpulb / kcthclb) * 100
     C                   if        wkpc <= 999
     C                   z-add     wkpc          xxtpc
     C                   endif
     C                   endif
      *
      * For Non-ByProducts and Cushion ByProducts, calc:
      *   1) Yield Percent (calc: Start Weight / Cold Carcass Pounds)
      *
     C                   when      xxtfcgcd <> 'BP' or
     C                             (xxtfcgcd = 'BP' and xxtfclcd = 'BP5'
     C                              and xxistycd = 600 and xxisgrcd = 630)
      *              SBD
     C                   if        kcscclb <> 0
     C                   eval      wkpc = (xxspuslb / kcscclb) * 100
     C                   if        wkpc <= 999
     C                   z-add     wkpc          xxspc
     C                   endif
     C                   endif
      *              TF
     C                   if        kctcclb <> 0
     C                   eval      wkpc = (xxtpuslb / kctcclb) * 100
     C                   if        wkpc <= 999
     C                   z-add     wkpc          xxtpc
     C                   endif
     C                   endif
     C                   endsl
      *
     C                   endif                                                  If hit
      *
     C                   seton                                        lr
      /eject
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
     C     *entry        plist
     C                   parm                    xxwedt
     C                   parm                    xxistycd
     C                   parm                    xxisgrcd
     C                   parm                    xxtfclcd
     C                   parm                    xxtfcgcd
     C                   parm                    xxspulb
     C                   parm                    xxspuslb
     C                   parm                    xxtpulb
     C                   parm                    xxtpuslb
      *
     C                   parm                    xxspc
     C                   parm                    xxsctlb
     C                   parm                    xxskllb
     C                   parm                    xxtpc
     C                   parm                    xxtctlb
     C                   parm                    xxtkllb
      *
     C                   endsr
      /eject
