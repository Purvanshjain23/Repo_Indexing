PGM  (&FLDVNM &OBJATR &ATRMNC &RETRY &PRFMNC)
/*T: YALCVNM Allocate field name. */
/*Z: CRTCLPGM LOG(*NO) */
/*H: SYSTEM     : SYNON/2 ROUTINE                                      */
/*H: PROGRAMMER : P.WILSON                                             */
/*H: COPYRIGHT (C) 1986 SYNON.                                         */
/*H: SYNOPSIS: Generates a DDS field name. */
/*W: SYNON/2 FIELD NAMES MUST BE UNIQUE WITHIN A MODEL */

/*M: 06/04/88  PV Add preferred mnemonic parameter and processing  */
/*M:              &MNCCDE -> &ATRMNC     &MNEMONIC -> &AUTMNC      */
/*                Attribute mnemonic     Automatically allocated   */
/*W: Preferred mnemonic only passed if YALCVNM is *MNC and */
/*W: a mnemonic exists for the object */

DCL &FLDVNM *CHAR 10 /*O: Allocated field name. */
                     /*   Blank = give up auto allocate */
DCL &OBJATR *CHAR  3 /*I: Synon/2 field attribute. */
DCL &ATRMNC *CHAR  3 /*I: Field attribute mnemonic. */
DCL &RETRY  *DEC   3 /*I: Retry count, 1-on first entry */
                     /*   incremented by 1 for each retry.*/
DCL &AUTMNC *CHAR 2  /* Automatically allocated mnemonic */
DCL &PRFMNC *CHAR 6 /* Preferred mnemonic 1 + 2 + 3 char mnemonics */

/*H: 1. If preferred mnemonic try only once */
/*      Otherwise allow 5 tries             */
            IF (   (      (&RETRY *GT 1) +
                     *AND (&PRFMNC *NE ' ') ) +
               *OR (      (&RETRY *GT 10) +
                     *AND (&PRFMNC *EQ ' ') ) ) DO
               CHGVAR &FLDVNM ' '
               RETURN
            ENDDO

/*H: 2. If preferred mnemonic specified use it */
/*      Otherwise call program to allocate mnemonic */
/*      from current series for base field attribute type */
            IF (&PRFMNC *EQ ' ') DO
               IF (%SST(&OBJATR 1 2) = %SST(&ATRMNC 1 2)) DO
                  CALL YALCVNMR1R (&OBJATR &AUTMNC)
               ENDDO
               ELSE DO
               /* If mnemonic code not based on field type */
               /* then start new allocation range */
                  CALL YALCVNMR1R (&ATRMNC &AUTMNC)
               ENDDO
            ENDDO

/*H: 3. Construct field name: Field mnemonic + attribute mnemonic */
            IF (&PRFMNC *EQ ' ') CHGVAR &FLDVNM (&AUTMNC |< +
                                                 %SST(&ATRMNC 1 2))
            ELSE                 CHGVAR &FLDVNM (%SST(&PRFMNC 2 2) |< +
                                                 %SST(&ATRMNC 1 2))
ENDPGM
