      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      HPS
      * PROGRAM:     HP293 - Retrieve Farm Site Data using Farm Site
      * PROGRAMMER:  LeAnne Ramsey
      * CREATED:     03/04/13
      *
      * FUNCTION:    This program retrieves Farm Site data using incoming Farm Site Code
      *              from Synon.  Susan Mason requested this in prep for the Phase 1 MTech
      *              Installation. It returns the same info as HP249...it just retrieves
      *              the Farm Site record directly from HSP018 using the incoming Farm
      *              Site Code.
      *
      *************************************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 05/09/13  LeAnne Ramsey (E2489)
      *           Susan Mason requested that 3 additional parms be added to this program:
      *             Farm County
      *             Cell Manager Code
      *             Cell Manager Name
      *
      * 05/23/13  LeAnne Ramsey (E2489)
      *           Susan Mason requested that I pass back '       ' in xxreturn
      *           when I do not get a hit on Farm.
      *
      * 10/15/13  LeAnne Ramsey (E2831)
      *           Recompile only. Added field 'MTech Reference'.
      *
      * 10/22/13  LeAnne Ramsey (E2831)
      *           We will now pass the MTech References back out
      *           to the calling Synon program.
      *
      * 02/13/14  LeAnne Ramsey (E2831)
      *           We just found out that MTech does not/will not have Cell Manager.
      *           John Lalumondier is only populating Production Manager in the
      *           HSP018 file. So, I have changed all "cmgcd" to be "pmgcd".
      *
      * 03/03/14  LeAnne Ramsey (E2831)
      *           Susan Mason says to take out my 02/13/14 change above.She wants
      *           this program to continue to send back Cell Manager to her programs.
      *           So, I have changed all "pmgcd" back to "cmgcd".
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS:
      ****************************************************************
      *
     Fhsp018    if   e           k disk
      *  Farm site
      *
      *
     Fhsp016    if   e           k disk
      *  Farm site
      *
      /eject
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      *
      ****************************************************************
      * ARRAYS AND TABLES
      ****************************************************************
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Parm fields
      *
     D xxreturn        s              7
     D xxfscd          s                   like(fsfscd)
     D xxfsnm          s                   like(fsfsnm)
     D xxsacd          s                   like(fssacd)
     D xxcrcd          s                   like(fscrcd)
     D xxcnty          s                   like(fscnty)
     D xxcmgcd         s                   like(fscmgcd)
     D xxmgnm          s                   like(mgmgnm)
     D xxfsref         s                   like(fsmtechref)
     D xxmgref         s                   like(mgmtechref)
      *
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * MAINLINE
      ****************************************************************
      *
      * Retrieve farm info
      *
     C     xxfscd        chain     hsp018                             92
     C                   if        *in92 = *off                                 If farm hit
     C                   z-add     fsfscd        xxfscd
     C                   movel     fsfsnm        xxfsnm
     C                   movel     fscrcd        xxcrcd
     C                   movel     fssacd        xxsacd
     C                   movel     fscnty        xxcnty
     C                   movel     fscmgcd       xxcmgcd
     C                   movel     fsmtechref    xxfsref
     C                   else
     C                   eval      xxreturn = 'ERROR'
     C                   endif                                                  If farm hit
      *
      * Retrieve Cell Manager Name
      *
     C     xxcmgcd       chain     hsp016                             92
     C                   if        *in92 = *off                                 If hit
     C                   movel     mgmgnm        xxmgnm
     C                   movel     mgmtechref    xxmgref
     C                   endif                                                  If hit
      *
     C                   seton                                        lr
      /eject
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
     C     *entry        plist
     C                   parm                    xxreturn
     C                   parm                    xxfsnm
     C                   parm                    xxfscd
     C                   parm                    xxcrcd
     C                   parm                    xxsacd
     C                   parm                    xxcnty
     C                   parm                    xxcmgcd
     C                   parm                    xxmgnm
     C                   parm                    xxfsref
     C                   parm                    xxmgref
      *
     C                   endsr
      /eject
