     A*%%TS  SD  20030120  112952  ISLRAMS     REL-V5R1M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION
     A*  DISPLAY FILE:  HPD145
     A*  TITLE:         MAINTAIN SCHEDULED SALE MOVEMENT
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  DATE:          06/08/95
     A*
     A*----------------------------------------------------------------
     A*  INDICATORS:
     A*
     A*  60  REVISE MODE
     A*  61  CREATE MODE
     A*  90  THE SALE THE USER HAS SELECTED FROM THE WORK WITH HAS
     A*      BEEN DELETED BY ANOTHER USER SINCE SELECTED
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A N90                                  CF04(04 'PROMPT')
     A N90                                  CF05(05 'REFRESH')
     A N90                                  CF09(09 'ACCEPT')
     A  60N90                               CF11(11 'DELETE')
     A                                      CA12(12 'PREVIOUS')
     A*---------------------------------------------------------------
     A* SUBFILE FORMAT 1
     A*---------------------------------------------------------------
     A*
     A          R HP145S1                   SFL
     A*%%TS  SD  20020717  113226  ISLRAMS     REL-V5R1M0  5722-WDS
     A  85                                  SFLNXTCHG
     A            HDRRN1         4Y 0H
     A            HDHGSN    R        H      REFFLD(HGHGSN)
     A            DFBLCD    R        B 18  2REFFLD(@@BLCD)
     A  35
     AO 55                                  DSPATR(PC)
     A  35                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A  42N35N90                            DSPATR(HI)
     A  42N35N90                            DSPATR(UL)
     A            DFRMCD    R        B 18  9REFFLD(@@RMCD)
     A  36
     AO 56                                  DSPATR(PC)
     A  36                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A  42N36N90                            DSPATR(HI)
     A  42N36N90                            DSPATR(UL)
     A            DFHGCD    R        B 18 17REFFLD(HGHGCD)
     A  44
     AO 54                                  DSPATR(PC)
     A  44                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A  42N44N90                            DSPATR(HI)
     A  42N44N90                            DSPATR(UL)
     A            DFSCHD    R        B 18 29REFFLD(SGSCHD)
     A                                      EDTCDE(K)
     A  74                                  DSPATR(PC)
     A  74                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A  42N74N90                            DSPATR(HI)
     A  42N74N90                            DSPATR(UL)
     A            DFGSCD    R        O 18 41REFFLD(HGGSCD)
     A            DFPTCD    R        O 18 51REFFLD(HSRREF/@@PTCD *LIBL/HSPREF)
     A            DFPPCD    R        O 18 59REFFLD(HSRREF/@@PPCD *LIBL/HSPREF)
     A*
     A*----------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT  1
     A*----------------------------------------------------------------
     A*
     A          R HP145C1                   SFLCTL(HP145S1)
     A*%%TS  SD  20030120  112952  ISLRAMS     REL-V5R1M0  5722-WDS
     A                                      SFLSIZ(0100)
     A                                      SFLPAG(0004)
     A N90                                  ROLLUP(25 'PAGE FORWARD')
     A                                      SFLCSRRRN(&HDRR1)
     A                                      OVERLAY
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A                                      SETOF(30)
     A                                      SETOF(31)
     A                                      SETOF(32)
     A                                      SETOF(33)
     A                                      SETOF(35)
     A                                      SETOF(36)
     A                                      SETOF(44)
     A                                      SETOF(47)
     A                                      SETOF(50)
     A                                      SETOF(51)
     A                                      SETOF(52)
     A                                      SETOF(53)
     A                                      SETOF(54)
     A                                      SETOF(55)
     A                                      SETOF(56)
     A                                      SETOF(57)
     A                                      SETOF(74)
     A                                      SETOF(75)
     A                                      SETOF(76)
     A                                      SETOF(79)
     A                                      RTNCSRLOC(&RCD &FLD)
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            HDRR1          5S 0H
     A            HDTPCD    R        H      REFFLD(SHTPCD)
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A            DFMODE    R        O  1 14REFFLD(@@MODE)
     A                                      DSPATR(HI)
     A                                  1 30'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A            C1VERSION     18A  O  2  2DSPATR(HI)
     A                                  2 25'MAINTAIN SCHEDULED SALE MOVEMENT'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  4  2'Movement number ......:'
     A            C1MVSN    R        O  4 29REFFLD(@@MVSN)
     A                                      EDTCDE(Z)
     A                                  5  2'Sale type .............'
     A N90                              5 27'?'
     A            C1STCD    R        B  5 29REFFLD(STSTCD)
     A  47
     AO 57                                  DSPATR(PC)
     A  47                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A N47N90 42                            DSPATR(HI)
     A N47N90 42                            DSPATR(UL)
     A                                  6  2'Load type .............'
     A N90                              6 27'?'
     A            C1LTCD    R        B  6 29REFFLD(HSRREF/@@LTCD *LIBL/HSPREF)
     A  30
     AO 50                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A N30N90 42                            DSPATR(HI)
     A N30N90 42                            DSPATR(UL)
     A            C1LTDS    R        O  6 40REFFLD(HSRREF/@@LTDS *LIBL/HSPREF)
     A                                  7  2'Customer number .......'
     A N90                              7 27'?'
     A            C1CVNO    R        B  7 29REFFLD(SHCVNO)
     A                                      EDTCDE(M)
     A  32
     AO 52                                  DSPATR(PC)
     A  32                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A  42N32N90                            DSPATR(HI)
     A  42N32N90                            DSPATR(UL)
     A            C1CNAM    R        O  7 40REFFLD(@@DS30)
     A                                  8  2'Scheduled ship date ...'
     A            C1MDY     R     S  B  8 29REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  ')
     A  76                                  DSPATR(PC)
     A  76                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A  42N76N90                            DSPATR(HI)
     A  42N76N90                            DSPATR(UL)
     A                                  9  2'Scheduled kill date ...'
     A            C1SCKLMDY R     S  B  9 29REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  ')
     A  75                                  DSPATR(PC)
     A  75                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A  42N75N90                            DSPATR(HI)
     A  42N75N90                            DSPATR(UL)
     A                                 10  2'Farm ..................'
     A N90                             10 27'?'
     A            C1FSCD    R        B 10 29REFFLD(FSFSCD)
     A  31
     AO 51                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A  42N31N90                            DSPATR(HI)
     A  42N31N90                            DSPATR(UL)
     A            C1FSNM    R        O 10 40REFFLD(FSFSNM)
     A                                 11  2'Trucker ...............'
     A N90                             11 27'?'
     A            C1TICD    R        B 11 29REFFLD(SHTICD)
     A  33
     AO 53                                  DSPATR(PC)
     A  33                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A  42N33N90                            DSPATR(HI)
     A  42N33N90                            DSPATR(UL)
     A            C1TINM    R        O 11 40REFFLD(TITINM)
     A                                 12  2'Total scheduled head ..'
     A            C1SCHD    R        B 12 29REFFLD(SGSCHD)
     A                                      EDTCDE(K)
     A  79                                  DSPATR(PC)
     A  79                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A N79N90 42                            DSPATR(HI)
     A N79N90 42                            DSPATR(UL)
     A                                 14  4'?'
     A N90                             14 10'?'
     A                                 14 19'?'
     A                                 15 26'Scheduled'
     A                                 15 39'Group'
     A                                 15 50'--Production--'
     A                                 16  2'Bldg'
     A                                 16  9'Room'
     A                                 16 17'Group'
     A                                 16 31'Head'
     A                                 16 39'Status'
     A                                 16 51'Type'
     A                                 16 59'Phase'
     A*----------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*----------------------------------------------------------------
     A          R HP145K1
     A*%%TS  SD  19971010  104506  ISLRAMS     REL-V3R6M0  5716-PW1
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A N90                             23 11'F4=Prompt'
     A N90                             23 22'F5=Refresh'
     A N90                             23 34'F9=Accept'
     A  60N61N90                       23 45'F11=Delete'
     A                                 23 57'F12=Previous'
      *----------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *----------------------------------------------------------------
     A          R HP145ES                   SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
     A*----------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*----------------------------------------------------------------
     A*
     A          R HP145EC                   SFLCTL(HP145ES)
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
