/***************************************************************** */
/*                                                                 */
/*  PROGRAM NUMBER:  PNG5UPC                                       */
/*  PROGRAM NAME:    DELETE MEMBERS FROM BALCKBOX FILE             */
/*  PROGRAMMER:      LARA BUSER                                    */
/*  CREATE DATE:     04/02/2009                                    */
/*                                                                 */
/*                   DSPFD OF BALCKBOX TO AN OUTFILE IN QTEMP      */
/*                   REMOVE ALL MEMBERS WITH A CREATE DATE         */
/*                   LESS THAN OR EQUAL TO THE PURGE DATE          */
/*  NOTE:            DSPFDMBR IS A WORK FILE THAT MUST BE CREATED  */
/*                   PRIOR TO COMPILING THIS PROGRAM               */
/*******************************************************************/
/* MODIFICATIONS:                                                  */
/* XXX 99/99/9999 XX9999                                           */
/*******************************************************************/
             PGM        PARM(&RTNCODE &PURGEDATE &PCGCODE &PCGLIB +
                          &PCAPPLIC &FILENAME &PCFLGL &PCFTAPE +
                          &PCFIELD &DATEFMT)

             DCL        VAR(&RTNCODE) TYPE(*CHAR) LEN(7)
             DCL        VAR(&PURGEDATE) TYPE(*DEC) LEN(7 0)
             DCL        VAR(&PCGCODE) TYPE(*CHAR) LEN(3)
             DCL        VAR(&PCGLIB) TYPE(*CHAR) LEN(10) /* PRG FROM */
             DCL        VAR(&PCAPPLIC) TYPE(*CHAR) LEN(6)
             DCL        VAR(&FILENAME) TYPE(*CHAR) LEN(10)
             DCL        VAR(&PCFLGL) TYPE(*CHAR) LEN(10)
             DCL        VAR(&PCFTAPE) TYPE(*CHAR) LEN(10)
             DCL        VAR(&PCFIELD) TYPE(*CHAR) LEN(6)
             DCL        VAR(&DATEFMT) TYPE(*CHAR) LEN(10)
             DCL        VAR(&PURGEDATEA) TYPE(*CHAR) LEN(7)
             DCL        VAR(&CYMD) TYPE(*CHAR) LEN(7)

             DCLF       FILE(DSPFDMBR)

             DSPFD      FILE(&PCGLIB/BALCKBOX) TYPE(*MBRLIST) +
                          OUTPUT(*OUTFILE) OUTFILE(QTEMP/DSPFDMBR)
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(DONE))
             OVRDBF     FILE(DSPFDMBR) TOFILE(QTEMP/DSPFDMBR)

/* CONVERT PURGE DATE TO ALPHA                                      */
             CHGVAR     VAR(&PURGEDATEA) VALUE(&PURGEDATE)

 LOOP:       RCVF
             MONMSG     MSGID(CPF0864) EXEC(GOTO CMDLBL(DONE))

/* CREATE DATE IS 2 FIELDS IN THE FILE-CONCATONATE TO CYMD FORMAT   */
             CHGVAR     VAR(&CYMD) VALUE(&MLCCEN *TCAT &MLCDAT)
             IF         COND(&CYMD *LE &PURGEDATEA) THEN(DO)
             RMVM       FILE(&PCGLIB/&MLFILE) MBR(&MLNAME)
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(LOOP))
             ENDDO

             GOTO       CMDLBL(LOOP)

 DONE:       ENDPGM

