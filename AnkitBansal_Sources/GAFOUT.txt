/********************************************************************/
/*                                                                  */
/*  SYNOPSIS - DOWNLOAD GEORGIA FREEZER ORDER HEADER AND ORDER      */
/*             DETIAL FILE TO GAF SERVER.                           */
/*             THIS PROGRAM IS CALLED FROM THE AS/400 JOB SCHEDULER */
/*             EVERY DAY AT 2:00PM.                                 */
/*                                                                  */
/*                                                                  */
/*  COMPANY   - SEABOARD COPRORATION                                */
/*  USER NAME - MARK ARENSBERG                                      */
/*  DATE      -  7/08/99                                            */
/********************************************************************/
/*                                                                  */
/*  MODIFIED  - ADDED STOP/DROP NUMBER TO THE END OF THE GAF ORDER  */
/*              HEADER FILE.  THIS INCREASE THE RECORD LENGTH BY    */
/*              5 CHARACTERS.                                       */
/*  USER NAME - JOHN TREMPER                                        */
/*  DATE      - 10/30/99                                            */
/*                                                                  */
/********************************************************************/
/*                                                                  */
/*      PARAMETERS RECEIVED:                                        */
/*                                                                  */
/********************************************************************/

 GAFOUT:     PGM

             DCL  &COMP *DEC 3 0                   /*  COMP NO.     */
             DCL  &BTCHNO *DEC 6 0                  /*  BATCH NO.    */
             DCL  &BTCHNOA *CHAR 6                  /*  BTCH NO. ALPH*/
             DCL  &PRFXOH *CHAR 2 VALUE(OH)         /*  PC FILE PRFX */
             DCL  &PRFXOD *CHAR 2 VALUE(OD)         /*  PC FILE PRFX */
             DCL  &FILENMOH  *CHAR   37             /* PC ORD HEADER */
             DCL  &FILENMOD  *CHAR   32             /* PC ORD DETAIL */

/*********************************************************************/
/*   SET LIBRARY LIST                                                */
/*********************************************************************/

             CALL       PGM(SETPOU)

/*********************************************************************/
/*   LOCK ORDER HEADER FILE SO NO DATA IS ADDED WHILE FILE IS FTP'D  */
/*********************************************************************/

             CALL       PGM(POAVXFR) PARM('' 'L')

/*********************************************************************/
/*   SETUP FILE NAME WITH PASSED IN DATES OR UDATE                   */
/*********************************************************************/
             CHGVAR     VAR(&COMP) VALUE(260)

             CALL       PGM(POBUXFR) PARM('' &COMP &BTCHNO)

             CHGVAR     VAR(&BTCHNOA) VALUE(&BTCHNO)

             CHGVAR     VAR(&FILENMOH) VALUE(&PRFXOH *CAT &BTCHNOA)
             CHGVAR     VAR(&FILENMOD) VALUE(&PRFXOD *CAT &BTCHNOA)

/*********************************************************************/
/*   MOVE FILES TO AS/400 OFFICE FOLDER TO BE DOWNLOADED             */
/*********************************************************************/

             ADDLIBLE LIB(SEQUEL)
             MONMSG MSGID(CPF2103)    /* already exist in list */

             EXECUTE VIEW(PONKCPP) TOFLR(POULTRY) TODOC(PONKCPP) +
                          PCFMT(*SDF) REPLACE(*YES)

             EXECUTE VIEW(PONLCPP) TOFLR(POULTRY) TODOC(PONLCPP) +
                          PCFMT(*SDF) REPLACE(*YES)

             RMVLIBLE   LIB(SEQUEL)

/*********************************************************************/
/*   COPY DOWNLOAD FILE TO PC                                        */
/*********************************************************************/

/*   SEND ORDER HEADER FILE TO PC  AND CLEAR FILE ON AS400           */
             CALL       PGM(FTPGAFDOC) PARM('PONKCPP' &FILENMOH)

             CLRPFM     PONKCPP

/*   SEND ORDER DETAIL FILE TO PC                                    */
             CALL       PGM(FTPGAFDOC) PARM('PONLCPP' &FILENMOD)

             CLRPFM     PONLCPP

/*********************************************************************/
/* UNLOCK ORDER HEADER FILE ONCE PROCESS HAS COMPLETED               */
/*********************************************************************/

             CALL       PGM(POAVXFR) PARM('' 'U')

 END:        ENDPGM
