     h option(*SRCSTMT:*NODEBUGIO)
      ****************************************************************
      *
      * SYSTEM:      Triumph Foods
      * PROGRAM:     TF809
      * TITLE:       Retrieve Item Structure Values
      * PROGRAMMER:  LeAnne Ramsey
      * CREATED:     03/26/07
      *
      * FUNCTION:  This function retrieves the values for:
      *                1) Inventory Flag
      *                2) Mix Flag
      *                3) Volume Flag
      *                4) Meat Cost Group Code
      *                5) TF Percent Owned
      *            that are stored in the file PPAOREP-TF Margin Adjustment Group Detail
      *            at the level of:
      *                      TF classification code
      *                      Item structure type code
      *                      Item structure group code
      *                      Item structure class code
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 04/16/07  LeAnne Ramsey
      *           Added:    Meat Cost Group Code
      *           Changed:  Capability Flag          to  Inventory Flag
      *                     Exclude from Mix Flag    to  Mix Flag
      *                     Exclude from Volume Flag to  Volume Flag
      *
      * 05/14/07  LeAnne Ramsey
      *           Added:    TF Percent Owned
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fppaorel1  if   e           k disk
      *   TF Margin Adjustment Group Detail
      *
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      *-----------------------------------------------------------------
      * CONSTANTS
      *-----------------------------------------------------------------
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      *-----------------------------------------------------------------
      * STANDALONE FIELDS
      *-----------------------------------------------------------------
      *
      * Parms
      *
     D xxtfclcd        s              3
     D xxistycd        s              3  0
     D xxisgrcd        s              3  0
     D xxisclcd        s              3  0
     D xxinvfl         s              1
     D xxmixfl         s              1
     D xxvolfl         s              1
     D xxcgcd          s              4
     D xxtownpc        s              7  4
     D xxprdcmp        s              3
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * Mainline
      ****************************************************************
      *
      * Retrieve the record for the incoming values:
      *
     C     key01         chain     ppaorel1                           92
     C                   if        *in92 = *on                                  If no hit
     C                   move      no            xxinvfl
     C                   move      no            xxmixfl
     C                   move      no            xxvolfl
     C                   move      *blank        xxcgcd
     C                   z-add     0             xxtownpc
     C                   move      *blank        xxprdcmp
     C                   else
      *
     C                   move      aoz8sx        xxinvfl
     C                   move      aoswsx        xxmixfl
     C                   move      aosxsx        xxvolfl
     C                   move      aohfaa        xxcgcd
     C                   z-add     aoggpc        xxtownpc
      *
      *          Set Producing Company value.
      *
     C                   select
     C                   when      aod3tx = 'S'
     C                   eval      xxprdcmp = 'SBF'
      *
     C                   when      aod3tx = 'T'
     C                   eval      xxprdcmp = 'TF '
     C                   other
      *
     C                   move      *blank        xxprdcmp
     C                   endsl
     C                   endif                                                  If hit
      *
     C                   seton                                        lr
      /EJECT
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Parm lists
      *
     C     *entry        plist
     C                   parm                    xxtfclcd
     C                   parm                    xxistycd
     C                   parm                    xxisgrcd
     C                   parm                    xxisclcd
     C                   parm                    xxinvfl
     C                   parm                    xxmixfl
     C                   parm                    xxvolfl
     C                   parm                    xxcgcd
     C                   parm                    xxtownpc
     C                   parm                    xxprdcmp
      *
      * Key lists
      *
     C     key01         klist
     C                   kfld                    xxtfclcd
     C                   kfld                    xxistycd
     C                   kfld                    xxisgrcd
     C                   kfld                    xxisclcd
      *
     C                   endsr
      /EJECT
