     A*%%TS  SD  20040329  100533  ISLEANNER   REL-V5R2M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD1705
     A*  TITLE:         COPY FARM LINKS
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  CREATED:       02/05/04
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A                                      CF04(04 'PROMPT')
     A                                      CF09(09 'COPY')
     A*
     A*---------------------------------------------------------------
     A* RECORD FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP1705R1
     A*%%TS  SD  20040329  100533  ISLEANNER   REL-V5R2M0  5722-WDS
     A                                      SETOF(31)
     A                                      SETOF(51)
     A                                      RTNCSRLOC(&RCD &FLD)
     A                                      OVERLAY
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 25'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USR)
     A                                      DSPATR(HI)
     A                                  2 28'COPY FARM LINKS'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@JOB)
     A                                      DSPATR(HI)
     A                                  4  2'This function copies all Farm Link-
     A                                      s that exist for the FROM Template'
     A                                  5  2'to the TO Template that you specif-
     A                                      y below.'
     A                                  7  2'Any/all Farm Links that already ex-
     A                                      ist for the TO Template will be'
     A                                  8  2'replaced with the FROM Template li-
     A                                      nks.'
     A                                 11  2'Copy from template:'
     A            D1FBTCD   R        O 11 24REFFLD(@@BTCD)
     A            D1FBTDS   R        O 11 32REFFLD(@@BTDS)
     A                                 12  2'Copy to template ..'
     A                                 12 22'?'
     A            D1TBTCD   R        B 12 24REFFLD(@@BTCD)
     A N31                                  DSPATR(HI)
     A  31
     AO 51                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A            D1TBTDS   R        O 12 32REFFLD(@@BTDS)
     A*
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R HP1705K1
     A*%%TS  SD  20040205  142304  ISLRAMS     REL-V5R2M0  5722-WDS
     A  43                             21  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A                                 23 11'F4=Prompt'
     A                                 23 22'F9=Copy'
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *---------------------------------------------------------------
     A          R HP1705ES                  SFL SFLMSGRCD(24)
      *
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
     A*---------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*---------------------------------------------------------------
     A*
     A          R HP1705EC                  SFLCTL(HP1705ES)
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
