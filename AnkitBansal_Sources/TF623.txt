      *****************  RPG PROGRAM HEADING  ************************
     h option(*SRCSTMT:*NODEBUGIO)
      ****************************************************************
      *
      * SYSTEM:      Triumph Foods
      * PROGRAM:     TF623
      * TITLE:       Margin: Byproduct Mix Adjustment Recap Report
      * PROGRAMMER:  LeAnne Ramsey
      * CREATED:     04/23/07
      *
      * FUNCTION:  Batch program to print the records from the TF Byproduct Mix
      *            Recap file for a selected date range.
      *
      *            This report runs over files created in the Resource
      *            Optimization Meat Cost Calculation.
      *
      *            This 'report' is generated:
      *               1) On-demand
      *               2) From the Margin Adjustment Close function
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 08/07/07  LeAnne Ramsey
      *           Recompile only. Fields added to Recap file.
      *
      * 02/13/08  LeAnne Ramsey
      *           This report can now be generated with a From/To Date range.
      *
      * 12/03/08  LeAnne Ramsey
      *           We will now generate this report automatically from the Margin Adjustment
      *           Closed. So, we changed the LDA to match the Margin Close function.
      *
      * 04/02/09  LeAnne Ramsey
      *           Tom Dye has requested that the "CoOwned Transfer Cost Amount" be
      *           broken out into 2 columns: SBD and TF. We do not hold the CoOwned Amount
      *           broken out by Producing Company in the Recap file. So, we added logic
      *           to retrieve the values from the ByProduct Detail file.
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Frol122c   if   e           k disk
      *  ByProduct detail (records selected with Open Query)
      *
      *
     Frol123a   if   e           k disk
      *  TF Byproduct mix recap (records selected with Open Query)
      *
      *
     Fprint198  o    f  198        printer oflind(*inof)
      *
      /EJECT
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Standard fields
      *
     d rtime           s              6  0
      *
      *
      * Control fields
      *
     D first           s              1    inz('Y')
     D svtfclcd        s                   like(yrtfclcd)
      *
      *
      * Workfields for date manipulation
      *
     D wkisodate       s               D   datfmt(*iso)
      *
      *
      * Print fields
      *
     D dash            s            198    inz(*all'-')
      *
     D h1demand        s             35
     d h1fwemdy        s              6  0
     d h1twemdy        s              6  0
      *
     D l1tfclds        s                   like(yrtfclds)
     D l1smixam        s                   like(yrsmixam)
     D l1scoam         s                   like(yrstrpam)
     D l1tcoam         s                   like(yrstrpam)
     D l1sduam         s                   like(yrsduam)
      *
     D t1smixam        s                   like(yrsmixam)
     D t1scoam         s                   like(yrstrpam)
     D t1tcoam         s                   like(yrstrpam)
     D t1sduam         s                   like(yrsduam)
      *
      ****************************************************************
      * ARRAYS AND TABLES
      ****************************************************************
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *
      *---------------------------------------------------------------
      * Local data area.
      *---------------------------------------------------------------
      *
     Dlda             uds                  dtaara(*lda)
     D  ldpfcd                 1      1
      *
     D  ldfwedt               85     92  0
     D  ldtwedt               99    106  0
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ***************************************************************************************
      * Main Line
      ***************************************************************************************
      *
     C                   exsr      $h1hdr
      *
      * Read all records from the TF Byproduct Mix Recap file selected by the
      * Open Query.
      *
     C     *loval        setll     rol123a
      *
     C                   dou       *in90 = *on                                  Main do
     C                   read      rol123a                                90
     C                   if        *in90 = *off                                 If not EOF
      * Break logic
     C                   select
     C                   when      first = yes
     C                   move      no            first
     C                   move      yrtfclcd      svtfclcd
     C                   move      yrtfclds      l1tfclds
      *
     C                   when      yrtfclcd <> svtfclcd
     C                   exsr      $l1dtl
     C                   move      yrtfclcd      svtfclcd
     C                   move      yrtfclds      l1tfclds
     C                   endsl
      * Detail
     C                   add       yrsmixam      l1smixam
     C                   add       yrsduam       l1sduam
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Main do
      *
      * Print a heading and the standard 'no data' message if you had
      * no data.
     C                   if        first = yes                                  If nothing
     C                   except    nodata
     C                   else
      *
     C                   exsr      $l1dtl
      *
     C                   if        *inof = *on
     C                   exsr      $h1hdr
     C                   endif
     C                   exsr      $t1tot
     C                   endif                                                  If nothing
      *
     C                   seton                                        lr
      /EJECT
      *---------------------------------------------------------------
      * Print detail line
      *---------------------------------------------------------------
      *
     C     $l1dtl        begsr
      *
      * Retrieve SBF/TF CoOwned Amounts for this TF Classification
      *
     C                   exsr      $coown
      *
      * Accumulate values for Report Total
      *
     C                   add       l1smixam      t1smixam
     C                   add       l1scoam       t1scoam
     C                   add       l1tcoam       t1tcoam
     C                   add       l1sduam       t1sduam
      *
      * Print headings if necessary
      *
     C                   if        *inof = *on
     C                   exsr      $h1hdr
     C                   endif
      *
     C                   except    l1dtl
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------------------------------
      * Retriev/sum CoOwned Values by Producing Company
      *---------------------------------------------------------------------------------------
      *
      * An open query has aleady selected the appropriate ByProduct Detail records:
      *   1) in the date range
      *   2) Item Type = WP
      *   3) Standalone = NO
      *   4) TF CoOwned % <> 0
      *
      * So, retrieve/sum values for this:
      *     TF Classification Code
      *
     C     $coown        begsr
      *
     C     svtfclcd      setll     rol122c
      *
     C                   dou       *in93 = *on                                  Do co-own
     C     svtfclcd      reade     rol122c                                93
     C                   if        *in93 = *off                                 If not EOF
      *
     C                   select
     C                   when      ydprdcmp = 'SBF'
     C                   add       ydstrpam      l1scoam
      *
     C                   when      ydprdcmp = 'TF '
     C                   add       ydstrpam      l1tcoam
     C                   endsl
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Do co-own
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Print Report Total
      *---------------------------------------------------------------
      *
     C     $t1tot        begsr
      *
     C                   if        *inof = *on
     C                   exsr      $h1hdr
     C                   endif
      *
     C                   except    t1tot
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Print report headings
      *---------------------------------------------------------------
      *
     C     $h1hdr        begsr
      *
     C                   except    h1hdr
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Retrieve time for report heading.
      *
     C                   time                    rtime
      *
      * This report is generated on-demand and also from the
      * Margin Adjustment Close function.
      *
     C                   select
     C                   when      ldpfcd = 'D'
     C                   eval      %subst(h1demand:13:9) = 'On-Demand'
      *
     C                   when      ldpfcd = 'F'
     C                   eval      %subst(h1demand:4:29) =
     C                             'Final Margin Adjustment Close'
      *
     C                   when      ldpfcd = 'P'
     C                   eval      %subst(h1demand:1:35) =
     C                             'Preliminary Margin Adjustment Close'
     C                   endsl
      *
      *
      * Flip From/To Week-ending Dates into MMDDYY format.
      *
     C     *iso          move      ldfwedt       wkisodate
     C     *mdy          move      wkisodate     h1fwemdy
      *
     C     *iso          move      ldtwedt       wkisodate
     C     *mdy          move      wkisodate     h1twemdy
      *
     C                   endsr
      /eject
      *-------------------------------------------------------------
      * Report lines
      *-------------------------------------------------------------
      *
     Oprint198  e            h1hdr          1 01
     O                       sdpgm               10
     O                                          103 'Byproduct Mix Adjustment'
     O                                          116 ' Recap Report'
     O                                          188 'DATE'
     O                       udate         y    198
      *
      *
     O          e            h1hdr          1
     O                       sdusr               10
     O                       h1demand           113
     O                                          188 'TIME'
     O                       rtime              198 '  :  :  '
      *
     O          e            h1hdr          1
     O                                          188 'PAGE'
     O                       page          z    198
      *
      *
     O          e            h1hdr          1
     O                                           22 'From week-ending date:'
     O                       h1fwemdy            32 '  /  /  '
      *
     O          e            h1hdr          2
     O                                           22 'To week-ending date:'
     O                       h1twemdy            32 '  /  /  '
      *
      *
      *-------------------------------------------------------------
      * Column headings
      *-------------------------------------------------------------
      *
     O          e            h1hdr       0  1
     O                                           45 'Seaboard'
     O                                           73 'CoOwned Transfer Cost '
     O                                           79 'Amount'
     O                                           96 'Total'
      *
     O          e            h1hdr          1
     O                                           45 'Mix'
     O                                           56 '---- '
     O                                           79 'Producing Company ----'
     O                                           96 'Amount'
      *
     O          e            h1hdr          1
     O                                           18 'TF Classification '
     O                                           29 'Description'
     O                                           45 'Amount'
     O                                           60 'SBF'
     O                                           77 'TF'
     O                                           96 'Due'
      *
     O          e            h1hdr          1
     O                       dash               198
      *
      *-------------------------------------------------------------
      * Detail line
      *-------------------------------------------------------------
      *
     O          e            l1dtl       1
     O                       l1tfclds            30
     O                       l1smixam      kb    46
     O                       l1scoam       kb    63
     O                       l1tcoam       kb    80
     O                       l1sduam       kb    97
      *
      *-------------------------------------------------------------
      * Total line for Report
      *-------------------------------------------------------------
      *
     O          e            t1tot       1  1
     O                                           45 '--------------'
     O                                           62 '--------------'
     O                                           79 '--------------'
     O                                           96 '--------------'
     O          e            t1tot
     O                                           30 'TOTAL:'
     O                       t1smixam      k     46
     O                       t1scoam       k     63
     O                       t1tcoam       k     80
     O                       t1sduam       k     97
      *
      *-------------------------------------------------------------
      * No data message line
      *-------------------------------------------------------------
      *
     O          e            nodata      1
     O                                           19 'No data meets your'
     O                                           39 'selection criteria.'
      /eject
