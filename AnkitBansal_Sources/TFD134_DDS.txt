     A*%%TS  SD  20050317  104626  ISLRAMS     REL-V5R2M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        TRIUMPH FOODS
     A*  DISPLAY FILE:  TFD134
     A*  TITLE:         ASSIGN/UNASSIGN CHARGES TO AN INVOICE
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  DATE:          03/10/05
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/TFPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A                                      CF04(04 'PROMPT')
     A                                      CF05(05 'REFRESH')
     A                                      CF10(10 'ASSIGN')
     A*
     A*
     A*---------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*---------------------------------------------------------------
     A          R TF134S1                   SFL
     A*%%TS  SD  20050317  104626  ISLRAMS     REL-V5R2M0  5722-WDS
     A  85                                  SFLNXTCHG
     A            HDRRN1         4Y 0H
     A            D1OPT          2A  B 11  2CHGINPDFT(UL)
     A  30                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A N30                                  DSPATR(HI)
     A  90                                  DSPATR(PR)
     A  90                                  DSPATR(ND)
     A N90                                  DSPATR(UL)
     A            D1INFL    R        O 11  5REFFLD(TFREF/@@INFL *LIBL/TFPREF)
     A            D1SMCD    R        O 11  8REFFLD(TFREF/@@SMCD *LIBL/TFPREF)
     A            D1CCCD    R        O 11 11REFFLD(TFREF/@@CCCD *LIBL/TFPREF)
     A            D1ICDTMDY R        O 11 22REFFLD(TFREF/@@DT06 *LIBL/TFPREF)
     A                                      EDTWRD('  /  /  ')
     A            D1ICSN    R        O 11 31REFFLD(@@ICSN)
     A                                      EDTCDE(Z)
     A            D1CCDS    R        O 11 39REFFLD(@@CCDS)
     A            D1ICAM    R        O 11 65REFFLD(TFREF/@@ICAM *LIBL/TFPREF)
     A                                      EDTCDE(K)
     A*
     A*---------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*---------------------------------------------------------------
     A          R TF134C1                   SFLCTL(TF134S1)
     A*%%TS  SD  20050317  104626  ISLRAMS     REL-V5R2M0  5722-WDS
     A                                      SFLSIZ(0050)
     A                                      SFLPAG(0011)
     A N82                                  ROLLUP(25 'PAGE FORWARD')
     A                                      SETOF(30)
     A                                      SETOF(51)
     A                                      SETOF(52)
     A                                      SETOF(53)
     A                                      RTNCSRLOC(&RCD &FLD)
     A                                      OVERLAY
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 18'ASSIGN/UNASSIGN CHARGES TO AN INVO-
     A                                      ICE '
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USR)
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@JOB)
     A                                      DSPATR(HI)
     A                                  3  2'Invoice number :'
     A                                      DSPATR(HI)
     A            C1INSN    R        O  3 19REFFLD(TFREF/@@INSN *LIBL/TFPREF)
     A                                      EDTCDE(Z)
     A                                  3 29'Frequency:'
     A                                      DSPATR(HI)
     A            C1CFQDS   R        O  3 40REFFLD(TFREF/@@CFQDS *LIBL/TFPREF)
     A                                  3 52'Amounts:'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  4  2'Invoice date ..:'
     A                                      DSPATR(HI)
     A            C1INDTMDY R        O  4 19REFFLD(TFREF/@@DT06 *LIBL/TFPREF)
     A                                      EDTWRD('  /  / 0')
     A                                  4 29'Status ..:'
     A                                      DSPATR(HI)
     A            C1STDS    R        O  4 40REFFLD(TFREF/@@STDS *LIBL/TFPREF)
     A                                  4 52'Invoice:'
     A                                      DSPATR(HI)
     A            C1INAM    R        O  4 61REFFLD(TFREF/@@INAM *LIBL/TFPREF)
     A                                      EDTCDE(K)
     A                                  5 55'Memo:'
     A                                      DSPATR(HI)
     A            C1MEMOAM  R        O  5 61REFFLD(TFREF/@@MEMOAM *LIBL/TFPREF)
     A                                      EDTCDE(K)
     A N90                              6  2'4=Remove Charge from the Invoice'
     A                                      COLOR(PNK)
     A                                  7  6'?'
     A                                      DSPATR(HI)
     A                                  7  9'?'
     A                                      DSPATR(HI)
     A                                  7 14'?'
     A                                      DSPATR(HI)
     A                                  8  5'In'
     A                                      DSPATR(HI)
     A                                  8  8'Sy'
     A                                      DSPATR(HI)
     A                                  8 11'Charge'
     A                                      DSPATR(HI)
     A                                  8 22'-Invoice Charge-'
     A                                      DSPATR(HI)
     A                                  9  2'Op'
     A                                      DSPATR(HI)
     A                                  9  5'Iv'
     A                                      DSPATR(HI)
     A                                  9  8'Md'
     A                                      DSPATR(HI)
     A                                  9 11'Code'
     A                                      DSPATR(HI)
     A                                  9 24'Date'
     A                                      DSPATR(HI)
     A                                  9 32'Number'
     A                                      DSPATR(HI)
     A                                  9 39'Charge Code Description'
     A                                      DSPATR(HI)
     A                                  9 66'Charge Amount'
     A                                      DSPATR(HI)
     A            C1INFL    R        B 10  5REFFLD(TFREF/@@INFL *LIBL/TFPREF)
     A                                      DSPATR(HI)
     A  53                                  DSPATR(PC)
     A            C1SMCD    R        B 10  8REFFLD(TFREF/@@SMCD *LIBL/TFPREF)
     A  52                                  DSPATR(PC)
     A                                      DSPATR(HI)
     A            C1CCCD    R        B 10 11REFFLD(TFREF/@@CCCD *LIBL/TFPREF)
     A  51                                  DSPATR(PC)
     A                                      DSPATR(HI)
     A            C1ICDTMDY R        B 10 22REFFLD(TFREF/@@DT06 *LIBL/TFPREF)
     A                                      EDTWRD('  /  / 0')
     A                                      DSPATR(HI)
     A            C1ICSN    R        B 10 31REFFLD(@@ICSN)
     A                                      EDTCDE(Z)
     A                                      DSPATR(HI)
     A            C1CCDS    R        B 10 39REFFLD(TFREF/@@CCDS *LIBL/TFPREF)
     A                                      CHECK(LC)
     A                                      DSPATR(HI)
     A*
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R TF134K1
     A*%%TS  SD  20050310  163527  ISLRAMS     REL-V5R2M0  5722-WDS
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A                                 23 11'F4=Prompt'
     A                                 23 22'F5=Refresh'
     A                                 23 34'F10=Assign Charges to the Invo-
     A                                      ice'
     A                                      COLOR(PNK)
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *---------------------------------------------------------------
     A          R TF134ES                   SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE CONTROL FORMAT
      *---------------------------------------------------------------
     A          R TF134EC                   SFLCTL(TF134ES)
      *
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
