     A*%%TS  SD  20140306  103823  ISLRAMS     REL-V6R1M0  5761-WDS
     A*****************************************************************
     A* ENVIRONMENT:  PORK DIVISION
     A* SYSTEM:       AS/400
     A* FILE:         MPD406
     A* DESCRIPTION:  MP2: WORK WITH PURCHASE ORDER RECEIPTS
     A* PROGRAMMER:   LEANNE FEDOR
     A* DATE:         04/03/02
     A*
     A* MODIFIED BY:  BARB GUTIERREZ 03/30/2011
     A*               QTY INCREASED.  THIS CAUSED US TO INCREASE THE DSPSIZ
     A*               FROM 24 X 80 TO 27 X 132.
     A*               E001398.
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(27 132 *DS4)
     A                                      REF(*LIBL/MP2REF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A                                      CF05(05 'REFRESH')
     A*
     A*
     A*---------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*---------------------------------------------------------------
     A          R MP406S1                   SFL
     A*%%TS  SD  20110330  114814  ISBGUTI     REL-V6R1M0  5761-WDS
     A  85                                  SFLNXTCHG
     A            HDRRN1         4Y 0H
     A            HDPOSN    R        H      REFFLD(@@POSN)
     A            D1OPT     R        B 12  2REFFLD(@@OPT)
     A                                      CHGINPDFT(UL)
     A  30                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A N30                                  DSPATR(HI)
     A            D1RCNO    R        O 12  6REFFLD(@@RCNO)
     A                                      EDTCDE(4)
     A            D1SERNO   R        O 12 16REFFLD(@@SERNO)
     A                                      EDTCDE(Z)
     A            D1TTCD    R        O 12 26REFFLD(@@TTCD)
     A            D1TRQT    R        O 12 61REFFLD(@@TRQT)
     A                                      EDTCDE(M)
     A            D1BALQT   R        O 12 75REFFLD(@@TRQT)
     A                                      EDTCDE(M)
     A            D1POFL    R        O 12 88REFFLD(@@POFL)
     A            D1MCU     R        O 12 30REFFLD(@@MCU)
     A            D1OBJ     R        O 12 43REFFLD(@@OBJ)
     A            D1SUB     R        O 12 50REFFLD(MPREC/@@SUB *LIBL/MP2REF)
     A                                 13  5'Item:'
     A                                      DSPATR(HI)
     A            D1ITEMNO  R        O 13 11REFFLD(@@ITEMNO)
     A            D1ITEMDS      40A  O 13 37
     A*
     A*---------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*---------------------------------------------------------------
     A          R MP406C1                   SFLCTL(MP406S1)
     A*%%TS  SD  20140306  103823  ISLRAMS     REL-V6R1M0  5761-WDS
     A                                      SFLSIZ(0050)
     A                                      SFLPAG(0005)
     A N82                                  ROLLUP(25 'PAGE FORWARD')
     A                                      SETOF(30)
     A                                      OVERLAY
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            SDPGM         10A  O  1  2DSPATR(HI)
     A                                  1 26'MP2 INTERFACE TO JDE'
     A                                      DSPATR(HI)
     A            SDUSR         10A  O  1 70DSPATR(HI)
     A                                  2 21'WORK WITH PURCHASE ORDER RECEIPTS'
     A                                      DSPATR(HI)
     A            SDJOB         10A  O  2 70DSPATR(HI)
     A                                  4  2'JDE vendor ...:'
     A                                      DSPATR(HI)
     A            C1JDVNNO  R        O  4 18REFFLD(@@JDVNNO)
     A                                      EDTCDE(M)
     A                                  4 41'Purchasing center:'
     A                                      DSPATR(HI)
     A            C1PCID    R        O  4 60REFFLD(@@PCID)
     A                                  5  2'Purchase order:'
     A                                      DSPATR(HI)
     A            C1PONO    R        O  5 18REFFLD(@@PONO)
     A                                  5 40'Accounting company:'
     A                                      DSPATR(HI)
     A            C1ACONO   R        O  5 60REFFLD(@@ACONO)
     A                                      EDTCDE(M)
     A                                  7  2'5=View invoices'
     A                                  8 86'Posted'
     A                                      DSPATR(HI)
     A                                  9  8'Receipt'
     A                                      DSPATR(HI)
     A                                  9 19'Serial'
     A                                      DSPATR(HI)
     A                                  9 26'Tr'
     A                                      DSPATR(HI)
     A                                  9 32'Cost'
     A                                      DSPATR(HI)
     A                                  9 42'Account'
     A                                      DSPATR(HI)
     A                                  9 65'Trans'
     A                                      DSPATR(HI)
     A                                  9 77'Balance'
     A                                      DSPATR(HI)
     A                                  9 88'To'
     A                                      DSPATR(HI)
     A                                 10  2'Opt'
     A                                      DSPATR(HI)
     A                                 10  9'Number'
     A                                      DSPATR(HI)
     A                                 10 19'Number'
     A                                      DSPATR(HI)
     A                                 10 26'Cd'
     A                                      DSPATR(HI)
     A                                 10 32'Center'
     A                                      DSPATR(HI)
     A                                 10 42'Object'
     A                                      DSPATR(HI)
     A                                 10 50'Sub Code'
     A                                      DSPATR(HI)
     A                                 10 63'Quantity'
     A                                      DSPATR(HI)
     A                                 10 76'Quantity'
     A                                      DSPATR(HI)
     A                                 10 88'JDE'
     A                                      DSPATR(HI)
     A            C1RCNO    R        B 11  6REFFLD(@@RCNO)
     A                                      DSPATR(HI)
     A                                      EDTCDE(M)
     A            C1TTCD    R        B 11 26REFFLD(@@TTCD)
     A                                      DSPATR(HI)
     A            C1POFL    R        B 11 88REFFLD(@@POFL)
     A                                      DSPATR(HI)
     A*
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R MP406K1
     A*%%TS  SD  20011024  165925  ISLRAMS     REL-V4R4M0  5769-PW1
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A                                 23 11'F5=Refresh'
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *---------------------------------------------------------------
     A          R MP406ES                   SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE CONTROL FORMAT
      *---------------------------------------------------------------
     A          R MP406EC                   SFLCTL(MP406ES)
      *
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
