     A*%%TS  SD  20060118  130955  ISLRAMS     REL-V5R3M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD154
     A*  TITLE:         ASSIGN BIN SETS TO ROOMS
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  CREATED:       07/10/01
     A*
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A N90                                  CF04(04 'PROMPT')
     A N90                                  CF05(05 'REFRESH')
     A N90                                  CF09(09 'ACCEPT')
     A                                      CA12(12 'PREVIOUS')
     A*
     A*-------------------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*-------------------------------------------------------------------------
     A          R HP154S1                   SFL
     A*%%TS  SD  20060113  092747  ISLRAMS     REL-V5R3M0  5722-WDS
     A  85                                  SFLNXTCHG
     A            HDRRN1    R        H      REFFLD(@@RRN)
     A            D1BLCD    R        O 10  5REFFLD(HSRREF/@@BLCD *LIBL/HSPREF)
     A            D1RMCD    R        O 10 14REFFLD(HSRREF/@@RMCD *LIBL/HSPREF)
     A            D1BICD1   R        B 10 24REFFLD(HSRREF/@@BICD *LIBL/HSPREF)
     A  31
     AO 51                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A N31                                  DSPATR(HI)
     A            D1BICD2   R        B 10 32REFFLD(HSRREF/@@BICD *LIBL/HSPREF)
     A  32
     AO 52                                  DSPATR(PC)
     A  32                                  DSPATR(RI)
     A N32                                  DSPATR(HI)
     A            D1RMST    R        O 10 44REFFLD(BRREC/BRRMST *LIBL/HSP020)
     A*
     A*-------------------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*-------------------------------------------------------------------------
     A          R HP154C1                   SFLCTL(HP154S1)
     A*%%TS  SD  20060118  130955  ISLRAMS     REL-V5R3M0  5722-WDS
     A                                      SFLSIZ(0050)
     A                                      SFLPAG(0011)
     A                                      SFLCSRRRN(&HDRR1)
     A                                      SETOF(31)
     A                                      SETOF(32)
     A                                      SETOF(50)
     A                                      SETOF(51)
     A                                      SETOF(52)
     A                                      RTNCSRLOC(&RCD &FLD)
     A                                      OVERLAY
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A            HDPAG1    R        H      REFFLD(@@PAG)
     A                                      SFLRCDNBR
     A            HDRR1     R        H      REFFLD(@@RRN)
     A            RCD       R        H      REFFLD(@@RCD)
     A            FLD       R        H      REFFLD(@@FLD)
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 28'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USR)
     A                                      DSPATR(HI)
     A            C1MODE    R        O  2  2REFFLD(@@MODE)
     A                                      DSPATR(HI)
     A                                  2 27'ASSIGN BIN SETS TO ROOMS'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@JOB)
     A                                      DSPATR(HI)
     A                                  4  3'Farm site ..:'
     A            C1FSCD    R        O  4 18REFFLD(FSFSCD)
     A                                      EDTCDE(M)
     A            C1FSNM    R        O  4 26REFFLD(FSFSNM)
     A                                  6 26'?'
     A                                      DSPATR(HI)
     A                                  6 34'?'
     A                                      DSPATR(HI)
     A                                  6 44'?'
     A                                      DSPATR(HI)
     A                                  7  3'Building'
     A                                      DSPATR(HI)
     A                                  7 14'Room'
     A                                      DSPATR(HI)
     A                                  7 23'-- Bin Sets --'
     A                                      DSPATR(HI)
     A                                  7 42'Room'
     A                                      DSPATR(HI)
     A                                  8  5'Code'
     A                                      DSPATR(HI)
     A                                  8 14'Code'
     A                                      DSPATR(HI)
     A                                  8 24'Code'
     A                                      DSPATR(HI)
     A                                  8 32'Code'
     A                                      DSPATR(HI)
     A                                  8 41'Status'
     A                                      DSPATR(HI)
     A            C1RMST    R        B  9 44REFFLD(BRREC/BRRMST *LIBL/HSP020)
     A  50                                  DSPATR(PC)
     A                                      DSPATR(HI)
     A*
     A*-------------------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*-------------------------------------------------------------------------
     A          R HP154K1
     A*%%TS  SD  20010710  143207  ISLRAMS     REL-V4R4M0  5769-PW1
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A N90                             23 11'F4=Prompt'
     A N90                             23 22'F5=Refresh'
     A N90                             23 34'F9=Accept'
     A                                 23 45'F12=Previous'
      *
     A*-------------------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
     A*-------------------------------------------------------------------------
     A          R HP154ES                   SFL SFLMSGRCD(24)
      *
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
     A*-------------------------------------------------------------------------
      * ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*-------------------------------------------------------------------------
     A          R HP154EC                   SFLCTL(HP154ES)
      *
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
