/*********************************************************************/
/* PROGRAM    : PMT7UPC                                             */
/* TITLE      : EXECUTE THE ESNDMAIL CMD TO EMAIL COA - HPB ONLY    */
/* DATE       : 04/17/2008                                           */
/* Description: EXECUTE THE ESNDNMAIL COMMAND, ADD AND REMOVE THE    */
/*                ESEND LIBRARY, MONITOR FOR ERRORS                  */
/*                                                                   */
/*  Parameters:                                                      */
/*  Usage   Field       Field Description                            */
/*  -----   ---------   ------------------------------------------   */
/*    I     &EADDRESS   EMAIL ADDRESS TO SEND TO                     */
/*    I     &ESUBJECT   EMAIL SUBJECT                                */
/*    I     &EMESSAGE   MESSAGE TO SEND                              */
/*********************************************************************/
/* Modifications:                                                    */
/*-------------------------------------------------------------------*/
/*   Date     Pgmr     Description                                   */
/* -------- ---------- --------------------------------------------  */
/* 12/08/08  RMC       ADDED SEND ADDENDUM PAGE PARM                */
/* 11/13/09  RMC       PASS ULSD CO TO CL AND PRTF                */
/* 10/26/18  RMC       NEW COA STJ OVERLAY -  LOGO CHG            */
/* 08/09/22  DN        W104274 - ADDED COMPANY 375 TO EMAIL COA      */
/*                     REPORT PUWXPFR. PASSED IN FUEL CODE AS INPUT  */
/*                     PARM &FUELCD.                                 */
/*********************************************************************/

 PMT7UPC:    PGM        PARM(&RETURN &EADDRESS &ESUBJECT &EMESSAGE +
                    &COMPN &BATCHN &SNDADD &ULSDCO &FUELCD)
/*-------------------------------------------------------------------*/
/* Declare Variables                                                 */
/*-------------------------------------------------------------------*/
             DCL        VAR(&RETURN)   TYPE(*CHAR) LEN(7)
             DCL        VAR(&EADDRESS) TYPE(*CHAR) LEN(50)
             DCL        VAR(&ESUBJECT) TYPE(*CHAR) LEN(60)
             DCL        VAR(&EMESSAGE) TYPE(*CHAR) LEN(256)
             DCL        VAR(&SNDADD) TYPE(*CHAR) LEN(1)
             DCL        VAR(&ULSDCO) TYPE(*CHAR) LEN(4)
             DCL        VAR(&COMPN) TYPE(*DEC)
             DCL        VAR(&BATCHN) TYPE(*DEC)
             DCL        VAR(&FUELCD) TYPE(*CHAR) LEN(6)

             DCL        VAR(&COMP) TYPE(*DEC) LEN(3 0)
             DCL        VAR(&BATCH) TYPE(*DEC) LEN(5 0)

/* ERROR MESSAGE FIELDS                                                       */
             DCL        VAR(&MSG)   TYPE(*CHAR) LEN(512)
             DCL        VAR(&MSGID) TYPE(*CHAR) LEN(7)
             DCL        VAR(&MSGL)  TYPE(*DEC) LEN(5 0)
             DCL        VAR(&MSG2)  TYPE(*CHAR) LEN(512)

             CHGVAR     VAR(&COMP) VALUE(&COMPN)
             CHGVAR     VAR(&BATCH) VALUE(&BATCHN)

/*-------------------------------------------------------------------*/
/* EXECUTE OVERRIDE PRINTFILES                                       */
/*-------------------------------------------------------------------*/
/* W104274 - ADDED FUEL CODE CHECK TO PROCESS CO 375. USE EXISTING +
             OVERLAY */
/* W104274   IF         COND(&COMP *EQ 361) THEN(DO)            */
             IF         COND((&COMP *EQ 361) *OR (&COMP *EQ 375 *AND +
                          &FUELCD *NE 'R99.9' *AND &FUELCD *NE +
                          'NAP')) THEN(DO)
             OVRPRTF    FILE(PMT9PFR$)  PAGESIZE(88 +
                          136) LPI(8) CPI(18) OVRFLW(88) +
                          FRONTOVL(COAOVL .2 0) USRDTA('COA')
             CALL       PGM(PMT9PFR) PARM('' &COMP &ULSDCO &BATCH &SNDADD)
 COA:        ESNDMAIL   RECIPIENT(&EADDRESS) SUBJECT(&ESUBJECT) +
                          MSG(&EMESSAGE) MSGTYPE(*DFT) ATTLIST((* +
                          *PDF *N PMT9PFR$ * *ALL *ALL))
             MONMSG     MSGID(EML0999) EXEC(GOTO CMDLBL(MSGTRAP))
                          ENDDO
  IF COND(&COMP *EQ 373 ) THEN(DO)
             OVRPRTF    FILE(PUJ8PFR$) DEV(ISPRT02) DEVTYPE(*AFPDS) +
                          PAGESIZE(88 136) LPI(8) CPI(15) +
                          OVRFLW(88) FONT(254 7) FRONTOVL(COASTJ .4 +
                          0) USRDTA('COA')
             CALL       PGM(PUJ8PFR) PARM('' &COMP &ULSDCO &BATCH &SNDADD)
 COA373:     ESNDMAIL   RECIPIENT(&EADDRESS) SUBJECT(&ESUBJECT) +
                          MSG(&EMESSAGE) MSGTYPE(*DFT) ATTLIST((* +
                          *PDF *N PUJ8PFR$ * *ALL *ALL))
             MONMSG     MSGID(EML0999) EXEC(GOTO CMDLBL(MSGTRAP))
                          ENDDO
/* W104274 - ADDED COMPANY 375 (SE KANSAS, LLC - HUGOTON) BY CHECKING +
             FUEL CODE */
             IF         COND((&FUELCD *EQ 'R99.9') *OR (&FUELCD *EQ +
                          'NAP')) THEN(DO)
             OVRPRTF    FILE(PUWXPFR$) PAGESIZE(88 136) LPI(8) +
                          CPI(18) OVRFLW(88) FRONTOVL(COAHUG .15 +
                          .25) USRDTA('COA')
             CALL       PGM(PUWXPFR) PARM('' &COMP &ULSDCO &BATCH)
 COA375:     ESNDMAIL   RECIPIENT(&EADDRESS) SUBJECT(&ESUBJECT) +
                          MSG(&EMESSAGE) MSGTYPE(*DFT) ATTLIST((* +
                          *PDF *N PUWXPFR$ * *ALL *ALL))
             MONMSG     MSGID(EML0999) EXEC(GOTO CMDLBL(MSGTRAP))
                          ENDDO

/*-------------------------------------------------------------------*/
/*-------------------------------------------------------------------*/
/* EXECUTE THE COMMAND AND MONITOR FOR ERRORS                        */
/*-------------------------------------------------------------------*/
             ADDLIBLE   LIB(ESEND)
             MONMSG     MSGID(CPF2103 CPF9999)

 DELETE:     DLTSPLF    FILE(*SELECT) SELECT(*CURRENT *ALL *ALL COA)

             GOTO       CMDLBL(ENDPGM)

/*----------------------RECEIVE MESSAGES----------------------------*/

 MSGTRAP:    RCVMSG     MSGTYPE(*EXCP) MSGDTA(&MSG) MSGDTALEN(&MSGL) +
                          MSGID(&MSGID)
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(ENDPGM))

             SNDPGMMSG  MSGID(&MSGID) MSGF(QCPFMSG) MSGDTA(%SST(&MSG +
                          1 &MSGL)) MSGTYPE(*ESCAPE)
             MONMSG     MSGID(CPF0000)

 ENDPGM:     RMVLIBLE   LIB(ESEND)
             MONMSG     MSGID(CPF0000)

             ENDPGM
