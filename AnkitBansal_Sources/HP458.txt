      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Hog Production System
      * PROGRAM:    HP458 - Reset values in the Sales Header HSP084
      *                     and Sales Detail HSP085 after an Edit run
      *                     that ended because of errors.
      *
      * PROGRAMMER:  BRAD BADEN
      * CREATED:      3/07/2018
      *
      * FUNCTION: This program is called from HP458CL.  It reads all
      *           of the Tattoo Header records that are in an HPE Push
      *           Status of 'R' for a company.  The associated HSP084
      *           and HSP085 records are reset to their values before
      *           the Edit was originally run.  In HSP084, the Movement
      *           Status is reset to 'SC' and the Shipped Date and
      *           Received Date are set to zero.  In HSP085, the Shipped
      *           Head and Shipped Pounds are set to zero.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER       PROJECT
      *
      * 11/03/20  ISE              H16853
      *           Recompile only. Increased the Buy Order field length from 5 to 7.
      *
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fpka1cpq2  if   e           k disk
      *  Kill Data for Business Office
      *
      *
     Fhsp084    uf   e           k disk
      *  Sales Movement Header
      *
      *
     Fhsp085    uf   e           k disk
      *  Sales Movement Detail
      *
      *
      /EJECT
      ****************************************************************
      * Definition Specifications
      ****************************************************************
      *---------------------------------------------------------------
      * PROCEDURE PROTOTYPE
      *---------------------------------------------------------------
     D ResetHPS        PR                  EXTPGM('HP458')
     D   pCompany#                    3
     D   pKillDate                    7
      *
     D ResetHPS        PI
     D   pCompany#                    3
     D   pKillDate                    7
      *
      *---------------------------------------------------------------
      * STANDALONE FIELDS
      *---------------------------------------------------------------
     D   company       S              3  0
     D   killdate      S              7  0
      /EJECT
      ****************************************************************
      * MAINLINE
      ****************************************************************
      *
      * Process all Tattoo Header records for the Company Number where
      * the HPE Push Status = 'R' for Ready (Determined by select
      * criteria on the access path pka1cpq2)
      *
     C                   eval      company = %dec(pCompany#:3:0)
     C                   eval      killdate = %dec(pKillDate:7:0)
      *
     C     company       setll     pka1cpq2
     C                   if        %found                                       if setll %found
      *
     C                   dou       %eof(pka1cpq2)                               dou %eof pka1cpq2
     C     company       reade     pka1cpq2
     C                   if        not %eof(pka1cpq2)                           if not %eof pka1cpq2
      *
     C                   if        a1b0dt <= killdate and                       if a1b0dt <= pKillDa
     C                             a1bsdt <= killdate
      *
      ** Retrieve the associated Sales Header record
      *
     C     a1oxnb        chain     hsp084
     C                   if        %found(hsp084)                               if %found hsp084
     C                   eval      shmscd = 'SC'
     C                   eval      shshdt = 0
     C                   eval      shrcdt = 0
      *
     C                   update    shrec
     C                   endif                                                  fi %found hsp084
      *
      ** Retrieve the associated Sales Detail record
      *
     C     a1oxnb        setll     hsp085
     C                   if        %found(hsp085)                               if %found hsp085
      *
     C                   dou       %eof(hsp085)                                 dou %eof hsp085
     C     a1oxnb        reade     hsp085
     C                   if        not %eof(hsp085)                             if not %eof hsp085
     C                   eval      sgshhd = 0
     C                   eval      sgshlb = 0
      *
     C                   update    sgrec
     C                   endif                                                  fi not %eof hsp085
     C                   enddo                                                  uod %eof hsp085
     C                   endif                                                  fi %found hsp085
      *
     C                   endif                                                  fi a1b0dt <= pKillDa
     C                   endif                                                  fi not %eof pka1cpq2
     C                   enddo                                                  uod %eof pka1cpq2
     C                   endif                                                  fi setll %found
      *
     C                   eval      *inlr = *on
