      *****************  RPG PROGRAM HEADING  ************************
      *
      * ENVIRONMENT:  PORK DIVISION
      * SYSTEM:       AS/400
      * PROGRAM:      MP651
      * DESCRIPTION:  MP2: List of Overridden Items
      * PROGRAMMER:   LeAnne Fedor
      * DATE:         04/30/02
      *
      * FUNCTION:  Batch program to list the items on invoices that have YES for
      *            the 'Override Calculated Amount' flag.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 10/20/06  LeAnne Ramsey
      *           This program has been running since 2002 for 'posted' invoices.
      *           We now also use it to print 'unposted' invoices during an 'edit'
      *           run of the 'post invoice' function. The program has been changed
      *           to accomdate this.
      *
      * 03/12/08  LeAnne Ramsey
      *           We gave all functionality to High Plains Biodiesel. So,
      *           we now run over two different library lists. We added a Data Area
      *           holding Company Number. We will now print Company Number/Name at
      *           the top of the Report.
      *           Also replaced QPRINT with PRINT198.
      *
      * 03/30/11  Barb Gutierrez
      *           Invoiced qty (7.0) and Unit cost per item (7.2) have increased to
      *           9.2 and 9.4 respectively.  Had to adjust printing of the report to
      *           accomodate the changes.   E001398
      *
      * 05/25/21  Danny Nguyen   (S17068)
      *           DBFC on MPP351 file. W1ITEMRT field length changed from 9.4 to 11.4.
      *           Applied Amount & Difference Amount field length changed from 9.2 to 11.2.
      *           Shifted Unit Cost Per Item & everything to the right to align properly.
      *
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fcaabrel1  if   e           k disk
      *    Company
      *
      *
     Fmpp351    if   e           k disk
      *    MP2: Workfile
      *
      *
     Fprint198  o    f  198        printer oflind(*inof)
      *
      /EJECT
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Standard fields
      *
     D prtfl           s              1    inz('N')
     D dash            s            198    inz(*all'-')
     d rtime           s              6  0
      *
      *
      * Print fields
      *
     d h1conm          s                   like(abadtx)
      *
     d l1jdvnnm        s             20
     d l1itemno        s             10
      ** S17068-Shortened 'Invoice Detail Comment' Value.
17068d** l1com           s             35
17068d l1com           s             29
      *
      *
      * Control fields
      *
     d svjdvnno        s                   like(w1jdvnno)
     D first           s              1    inz('Y')
      *
      *
      * Parm fields
      *
     d xxflag          s              1
     d xxdata          s              1
     d xxbatno         s              8  0
     d xxfmdy          s              6  0
     d xxtmdy          s              6  0
     d xxjdvnno        s                   like(w1jdvnno)
     d xxjdvnnm        s             40
      *
      *
      ****************************************************************
      * DATA AREA
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Local data area.
      *---------------------------------------------------------------
      *
     Dlda             uds                  dtaara(*lda)
     D  ldacono               86     88  0
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * MAIN
      ****************************************************************
      *
      * Prepare to read the file and print headings the first
      * time through.
      *
     C     *loval        setll     mpp351
     C                   except    h1hdr
      *
      *
      * Read and print all records in the file.
      *
     C                   dou       *in90 = *on                                  Main do
     C                   read      mpp351                                 90
     C                   if        *in90 = *off                                 If not EOF
      *
     C                   if        first = yes
     C                   move      no            first
     C                   z-add     w1jdvnno      svjdvnno
     C                   endif
      *
     C                   if        *inof = *on
     C                   except    h1hdr
     C                   endif
      *
      * Print detail line
      *
     C                   if        w1jdvnno <> svjdvnno
     C                   except    blank
     C                   z-add     w1jdvnno      svjdvnno
     C                   endif
      *
     C                   movel     w1jdvnnm      l1jdvnnm
     C                   movel     w1itemno      l1itemno
     C                   movel     w1com         l1com
     C                   except    l1dtl
     C                   movel     yes           prtfl
     C                   endif                                                  If not EOF
     C                   enddo                                                  Main do
      *
      *---------------------------------------------------------------
      * EOF processing
      *---------------------------------------------------------------
      * If the file was empty, print the standard 'no data...'
      * error message
      *
     C                   if        prtfl = no
     C                   except    nodata
     C                   move      no            xxdata
     C                   else
     C                   move      yes           xxdata
     C                   endif
      *
     C                   seton                                        lr
      /EJECT
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Parm lists
     C     *entry        plist
     C                   parm                    xxflag
     C                   parm                    xxdata
     C                   parm                    xxbatno
     C                   parm                    xxfmdy
     C                   parm                    xxtmdy
     C                   parm                    xxjdvnno
     C                   parm                    xxjdvnnm
      *
      *
      * Set up a line of underscores and retrieve time for report heading.
      *
     C                   time                    rtime
      *
      *
      * Retrieve Company Name
      *
     C     ldacono       chain     caabrel1                           92
     C                   if        *in92 = *off
     C                   eval      h1conm = abadtx
     C                   endif
      *
      *  Setup the user selections in the headings.
      *
     C                   select
     C                   when      xxflag = 'B'
     C                   seton                                        87
     C                   when      xxflag = 'E'
     C                   seton                                        89
     C                   endsl
      *
      * All vendors
     C                   if        xxjdvnno = 0
     C                   seton                                        88
     C                   endif
      *
     C                   endsr
      /EJECT
      *-------------------------------------------------------------
      * Report heading lines
      *-------------------------------------------------------------
      *
     Oprint198  e            h1hdr          1 01
     O                       sdpgm               10
     O                                           97 'MP2 INTERFACE TO JDE'
     O                                          188 'DATE'
     O                       udate         y    198
      *
     O          e            h1hdr          1
     O                       sdusr               10
     O                                           99 'LIST OF OVERRIDDEN ITEMS'
     O                                          188 'TIME'
     O                       rtime              198 '  :  :  '
      *
     O          e            h1hdr          1
     O                                          188 'PAGE'
     O                       page          z    198
      *
     O          e    89      h1hdr          2
     O                                           99 'Edit Run of Post Function'
      *
     O          e            h1hdr          1
     O                                           19 'Accounting company:'
     O                       ldacono       z     29
     O                       h1conm              61
      *
     O          e   n89 87   h1hdr          1
     O                                           19 'JDE batch number:'
     O                       xxbatno       z     29
      *
     O          e   n89n87   h1hdr          1
     O                                           19 'From batch date:'
     O                       xxfmdy              29 '  /  /  '
      *
     O          e   n89n87   h1hdr          1
     O                                           19 'To batch date:'
     O                       xxtmdy              29 '  /  /  '
      *
      *
     O          e   n89n87   h1hdr          2
     O                                           19 'JDE vendor:'
     O              N88      xxjdvnno      z     29
     O               88                          24 'ALL'
     O              N88      xxjdvnnm            71
      *
      *
     O          e            h1hdr          1
     O                                            8 'JDE'
      *
     O          e            h1hdr          1
     O                                            8 'VENDOR'
     O                                           40 'PURCHASE'
     O                                           63 'OBJECT'
     O                                          118 'INVOICED'
17068O**                                        130 'UNIT COST'
  |  O                                          135 'UNIT COST'
  |  O**                                        142 'APPLIED'
  |  O                                          151 'APPLIED'
  |  O**                                        155 'DIFFERENCE'
17068O                                          167 'DIFFERENCE'
      *
     O          e            h1hdr          1
     O                                            8 'NUMBER'
     O                                           25 'JDE VENDOR NAME'
     O                                           37 'ORDER'
     O                                           54 'COST CENTER'
     O                                           64 'ACCOUNT'
     O                                           72 'SUBSID'
     O                                           87 'ITEM NUMBER'
     O                                          103 'INVOICE NUMBER'
     O                                          118 'QUANTITY'
17068O**                                        130 'PER ITEM'
  |  O                                          135 'PER ITEM'
  |  O**                                        142 'AMOUNT'
  |  O                                          151 'AMOUNT'
  |  O**                                        155 'AMOUNT'
  |  O                                          167 'AMOUNT'
  |  O**                                        180 'INVOICE DETAIL COMMENT'
17068O                                          191 'INVOICE DETAIL COMMENT'
      *
     O          e            h1hdr          1
     O                       dash               198
      *
      *-------------------------------------------------------------
      * Detail line
      *-------------------------------------------------------------
      *
     O          e            l1dtl       1
     O                       w1jdvnno      z      8
     O                       l1jdvnnm            30
     O                       w1pono              40
     O                       w1mcu               54
     O                       w1obj               63
     O                       w1sub               74
     O                       l1itemno            86
     O                       w1inno             109
     O                       w1inqt        k    120
17068O**                     w1itemrt      k    133
  |  O                       w1itemrt      k    136
  |  O**                     w1apam        k    146
  |  O                       w1apam        k    152
  |  O**                     w1diffam      k    158
17068O                       w1diffam      k    168
     O                       l1com              198
      *
      *
      *-------------------------------------------------------------
      * Blank line
      *-------------------------------------------------------------
      *
     O          e            blank       1
      *
      *-------------------------------------------------------------
      * No data message line
      *-------------------------------------------------------------
      *
     O          e            nodata      1
     O                                           19 'NO DATA MEETS YOUR'
     O                                           38 'SELECTION CRITERIA'
