      *****************  RPG PROGRAM HEADING  ************************
      * SYSTEM:      HOG PRODUCTION
      * PROGRAM:     HP684
      * TITLE:       SALES MOVEMENT/CHECK REPORT
      * PROGRAMMER:  LEANNE FEDOR
      * CREATED:     09/08/94
      *
      *  FUNCTION:   THIS PROGRAM PRINTS THE SALES MOVEMENT/CHECK
      *              REPORT.
      *
      *              THE SELECTIONS ARE ENTERED THROUGH HP484 AND AN
      *              OPNQRY IS USED IN HP484CL TO SELECT RECORDS.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 09/27/94  LEANNE FEDOR
      *           RECOMPILE ONLY. REMOVED THE FOLLOWING UNUSED
      *           DATABASE FIELDS FROM THE CHECK FILES:
      *             HSP063-CHECK HEADER        CHCHBO CHHRDT
      *             HSP064-CHECK DETAIL        CDCHBO CDCSAM
      *             HSP065-CHECK ADJUSTMENTS   CJCHBO
      *
      * 12/13/94  MARY PALMER
      *             FIX CUSTOMER NAME PRINITNG TO GROUP JUST SUMMARIZED
      *
      * 04/24/95  LEANNE FEDOR
      *           CHANGED FARM SITE CODE FROM ALPHA TO NUMERIC.
      *
      * 07/03/95  LEANNE FEDOR
      *           CHANGED FILES FOR MOVEMENT REWRITE.
      *
      * 08/31/95  LEANNE FEDOR
      *           BUSINESS OFFICE IS NOW AN OPTIONAL SELECTION.
      *
      * 09/01/95  LEANNE FEDOR
      *           ADDED TOTALS BY FARM FOR EACH PREMIUM/DISCOUNT AND
      *           DEDUCTIONS/ADD-ON.
      *
      * 09/26/95  LEANNE FEDOR
      *           ADDED TOTALS FOR EACH PREMIUM/DISCOUNT AND
      *           DEDUCTION/ADD-ON FOR THE BREAKS ON SALES TYPE AND
      *           PRODUCTION PHASE.
      *
      * 10/11/95  LEANNE FEDOR
      *           CHANGED WORKFILE LOGIC IN HP384. THE WORKFILE NOW
      *           HOLDS A RECORD FOR EACH CHECK DETAIL RECORD TO BE
      *           PRINTED.
      *
      * 11/01/95  LEANNE FEDOR
      *           RECOMPILE ONLY. NEW FIELD 'GROUP TYPE' ADDED TO
      *           HOG GROUP FILE.
      *
      * 04/29/96  LEANNE FEDOR
      *           PER DAVE O'SULLIVAN, ADDED BREED STOCK COLUMN.
      *
      * 05/07/96  LEANNE FEDOR
      *           DAVE O'SULLIVAN REQUESTED THAT 'BREED STOCK' BE
      *           A SELECTION FOR THE REPORT. LOGIC WAS ADDED TO PLACE
      *           THIS SELECTION IN THE REPORT HEADING.
      *
      * 05/13/96  LEANNE FEDOR
      *           MADE CHANGES TO FACILITATE NEW JDE RELEASE CONVERSION
      *           CHANGED SEARCH TYPE TO 3A; CALL NEW GENERIC
      *           PROGRAM HPJDEV TO VALIDATE INSTEAD OF HPJDEA SINCE
      *           NO VENDOR ADDRESS IS REQUIRED.
      *
      * 06/03/97  LEANNE FEDOR
      *           RECOMPILE ONLY. NEW FIELDS 'PURGE FLAG' AND 'DATE OF
      *           FIRST PURGE' ADDED TO HOG GROUP FILE.
      *
      * 03/04/98  LeAnne Fedor
      *           The following fields were added to the database for
      *           the sales movement detail and check detail files:
      *                     condemned head and pounds
      *                     yard dead head and pounds
      *           The logic was changed to subtract condemned and yard
      *           dead values out of the live values (head and pounds)
      *           and add them into the DOA values. The headings were
      *           changed accordingly.
      *
      * 06/18/98  LeAnne Fedor
      *           Per Tom Krause:
      *             1) live column will now be 'live'
      *             2) DOA column will now be DOA only
      *             3) add new yard dead and condemened column
      *
      * 06/28/01  LeAnne Fedor
      *           Recompile only.
      *           Manager and supervisor fields removed from Hog Group file.
      *
      * 07/06/09  LeAnne Ramsey
      *           Recompile only. Added new field 'Continuous Flow Flag' to Hog Group file.
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     FHSP011    IF   E           K DISK
      *   PRODUCTION PHASE
      *
      *
     FHSL034D   IF   E           K DISK
      *    HOG GROUP
      *
      *
     FHSL064C   IF   E           K DISK
      *    CHECK DETAIL        (records selected in OPNQRY)
      *
      *
     FHSL065C   IF   E           K DISK
      *    CHECK ADJUSTMENTS
      *
      *
     FHSP066    IF   E           K DISK
      *   SALE TYPE CODES
      *
      *
     FHSP384    IF   E           K DISK
      *  WORKFILE
      *
      *
     FQPRINT    O    F  198        PRINTER OFLIND(*INOF)
      *
      /EJECT
      ****************************************************************
      * Definition Specifications
      ****************************************************************
      *
      *---------------------------------------------------------------
      *  DEDUCTION/ADD-ON CODES AND AMOUNTS FROM CHECK DETAIL
      *  PREMIUM/DISCOUNTS CODES AND AMOUNTS FROM CHECK DETAIL
      *---------------------------------------------------------------
      *
     D DD              S              5    DIM(20)                              DED/ADD AMOUNT
     D DA              S              7  2 DIM(20)
     D PD              S              5    DIM(20)                              PRM/DIS AMOUNT
     D PA              S              7  2 DIM(20)
      *
      *---------------------------------------------------------------
      *  TOTAL DEDUCTIONS/ADD-ONS PER FARM SITE.
      *  TOTAL PREMIUMS/DISCOUNTS PER FARM SITE.
      *---------------------------------------------------------------
      *
     D FDD             S              5    DIM(20)                              DED/ADD AMOUNT
     D FDA             S              9  2 DIM(20)
     D FPD             S              5    DIM(20)                              PRM/DIS AMOUNT
     D FPA             S              9  2 DIM(20)
      *
      *---------------------------------------------------------------
      *  TOTAL DEDUCTIONS/ADD-ONS PER SALES TYPE
      *  TOTAL PREMIUMS/DISCOUNTS PER SALES TYPE.
      *---------------------------------------------------------------
      *
     D SDD             S              5    DIM(20)                              DED/ADD AMOUNT
     D SDA             S              9  2 DIM(20)
     D SPD             S              5    DIM(20)                              PRM/DIS AMOUNT
     D SPA             S              9  2 DIM(20)
      *
      *---------------------------------------------------------------
      *  TOTAL DEDUCTIONS/ADD-ONS PER PHASE.
      *  TOTAL PREMIUMS/DISCOUNTS PER PHASE.
      *---------------------------------------------------------------
      *
     D PDD             S              5    DIM(20)                              DED/ADD AMOUNT
     D PDA             S              9  2 DIM(20)
     D PPD             S              5    DIM(20)                              PRM/DIS AMOUNT
     D PPA             S              9  2 DIM(20)
      *
      *---------------------------------------------------------------
      *  STANDARD MESSAGE ARRAYS (REQUIRED IN BATCH FOR JDE CUSTOMER)
      *---------------------------------------------------------------
     D MGW             S              1    DIM(50)                              MSG WORK PARMS
      *
      *---------------------------------------------------------------
      * STANDARD ERROR MESSAGE HANDLING DATA STRUCTURES
      *---------------------------------------------------------------
      * THE FOLLOWING ERROR MESSAGE DATA STRUCTURES ARE REQUIRED IN
      * THIS BATCH PROGRAM BECAUSE THEY ARE USED AS STANDARD INPUT
      * PARMS TO THE GENERIC PROGRAM THAT RETRIEVES THE JDE CUSTOMER
      * NAME.
      *
      * FOR ERROR MESSAGE HANDLING, A PACKED INDEX (E) IS REQUIRED.
      * FOR PROGRAM READABILITY, DEFINE A CORRESPONDING ERROR COUNT
      * FIELD CALLED 'ERROR'
      *
     D                 DS                  INZ
     D  ERROR                  1      2P 0
     D  E                      1      2P 0
      *
     D MGI             DS           140    INZ
     D  MGID                   1    140
     D                                     DIM(20)                              MSG ID ARRAY
      *
     D MGD             DS          1000    INZ
     D  MGDT                   1   1000
     D                                     DIM(20)                              MSG PARMS
      *
      *---------------------------------------------------------------
      *  DATE FORMATS
      *---------------------------------------------------------------
      *
      * DATE IN MMDDYY FORMAT
      *
     D                 DS
     D  WKMDY                  1      6  0
     D  WKMM                   1      2  0
     D  WKDD                   3      4  0
     D  WKYY                   5      6  0
      *
      * DATE IN CCYYMMDD FORMAT
      *
     D                 DS
     D  WKCYMD                 1      8  0
     D  WKCEN                  1      2  0
     D  WKYER                  3      4  0
     D  WKMON                  5      6  0
     D  WKDAY                  7      8  0
      *
      *----------------------------------------------------------------
      * LDA - REPORT SELECTIONS
      *----------------------------------------------------------------
      *
     D                UDS
     D  LDFSBO                 1      5
     D  LDBODS                 6     35
     D  LDFYMD                36     43  0
     D  LDFMDY                44     49  0
     D  LDTYMD                50     57  0
     D  LDTMDY                58     63  0
      *
     D  LDPTCD                64     68
     D  LDPTDS                69     78
     D  LDPPCD                79     83
     D  LDPPDS                84    113
      *
     D  LDSTCD               114    118
     D  LDSTDS               119    138
      *
     D  LDFSCD               149    153  0
     D  LDFSNM               154    178
      *
     D  LDCVNO               179    186  0
     D  LDCNAM               187    216
     D  LDHGSN               217    223  0
     D  LDHGCD               224    230
     D  LDBSCD               231    235
     D  LDBSDS               236    255
      *
      *---------------------------------------------------------------
      * STANDARD PROGRAM STATUS DATA STRUCTURE
      *---------------------------------------------------------------
      *    EXTERNALLY DEFINED AS UTPGFR (RECORD FORMAT: PGMDSR)
     D PGMDS         ESDS                  EXTNAME(UTPGFR)
      *
      *
      *---------------------------------------------------------------
      * NAMED CONSTANTS
      *---------------------------------------------------------------
     D YES             C                   CONST('Y')
     D NO              C                   CONST('N')
     D CARC            C                   CONST('CARCASS PAYMENT')
     D LIVE            C                   CONST('LIVE PAYMENT')
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * MAIN LINE
      ****************************************************************
      *
      * PROCESS EACH CHECK DETAIL RECORD IN THE WORKFILE
      *
     C     *IN90         DOUEQ     *ON                                          MAIN DO
     C                   READ      HSP384                                 90
     C     *IN90         IFEQ      *OFF                                         IF NOT EOF
      *
      * IF THIS IS THE FIRST TIME,
      *    RETRIEVE PRODUCTION PHASE DESCRIPTION
      *    RETRIEVE SALE TYPE DESCRIPTION
      *    PRINT REPORT HEADINGS
      *
     C     FIRST         IFEQ      YES                                          IF FIRST
     C                   EXSR      $PPDS
     C                   EXSR      $STDS
     C                   EXSR      $HEAD
     C                   MOVEL     NO            FIRST
     C                   ENDIF                                                  IF FIRST
      *
      *--------------------------------------------------------------
      * PERFORM CONTROL BREAK LOGIC
      *--------------------------------------------------------------
      *
      * IF BREAK IN PRODUCTION PHASE,
      *    PRINT FARM TOTAL LINE
      *    PRINT SALE TYPE TOTAL LINE
      *    PRINT PRODUCTION PHASE TOTAL LINE
      *    RETRIEVE DESCRIPTION OF NEXT PHASE
      *    RETRIEVE DESCRIPTION OF NEXT SALES TYPE
      *    PAGE BREAK AND PRINT REPORT HEADINGS.
      *
     C                   SELECT
     C     WFPPCD        WHENNE    SVPPCD                                       WH PHASE
     C                   EXSR      $TFTOT
     C                   EXSR      $TSTOT
     C                   EXSR      $TPTOT
      *
     C                   EXSR      $PPDS
     C                   EXSR      $STDS
     C                   EXSR      $HEAD
      *
      * IF BREAK IN SALES TYPE CODE OR PAYMENT TYPE
      *    PRINT FARM TOTAL LINE
      *    PRINT SALES TYPE TOTAL LINE
      *    RETRIEVE DESCRIPTION OF NEXT SALES TYPE
      *    PRINT SALES TYPE HEADING
      *
     C     WFSTCD        WHENNE    SVSTCD                                       WH TYPE
     C     WFTPCD        ORNE      SVTPCD
      *
     C                   EXSR      $TFTOT
     C                   EXSR      $TSTOT
      *
     C                   EXSR      $STDS
      *
     C     *INOF         IFEQ      *ON
     C                   EXSR      $HEAD
     C                   ELSE
     C                   EXCEPT    HDSALE
     C                   ENDIF
      *
      * IF BREAK IN FARM SITE
      *    PRINT FARM TOTAL LINE
      *    SET INDICATORS TO CONTROL PRINTING OF FIELDS ON
      *          THE DETAIL LINE
      *    SAVE FARM SITE CODE
      *
     C     WFFSCD        WHENNE    SVFSCD                                       WH FARM
     C                   EXSR      $TFTOT
     C                   SETON                                        1011
     C                   Z-ADD     WFFSCD        SVFSCD
     C                   ENDSL
      *
      *--------------------------------------------------------------
      * PERFORM DETAIL LINE LOGIC
      *--------------------------------------------------------------
      *
      * IF RECEIVED DATE HAS CHANGED FROM THE PREVIOUS RECORD,
      * THEN FLIP RECEIVED DATE FROM CCYYMMDD TO MMDDYY FOR
      * PRINTING. AND SET THE INDICATOR THAT CONTROLS PRINTING
      *
     C     WFRCDT        IFNE      WKCYMD
     C                   Z-ADD     WFRCDT        WKCYMD
     C                   EXSR      $TOMDY
     C                   Z-ADD     WKMDY         DTRCDT
     C                   SETON                                        11
     C                   ENDIF
      *
      * RETRIEVE CUSTOMER NAME
      *
     C                   EXSR      $CUST
      *
      * RETRIEVE CHECK ADJUSTMENTS AND PRINT DETAIL LINE
      *
     C                   EXSR      $CHECK
     C                   ENDIF                                                  IF NOT EOF
     C                   ENDDO                                                  MAIN DO
      *
      *----------------------------------------------------------------
      * EOF PROCESSING
      *----------------------------------------------------------------
      *
      *  IF NO DETAIL RECORDS MATCHED THE SELECTION CRITERIA,
      *         PRINT THE STANDARD 'NO DATA...' ERROR MESSAGE
      *  OTHERWISE, PRINT THE FOLLOWING TOTAL LINES
      *          FARM TOTAL
      *          SALES TYPE TOTAL
      *          PHASE TOTAL
      *
      *
     C     FIRST         IFEQ      YES                                          IF NO DATA
     C                   EXCEPT    HRHDR
     C                   EXCEPT    NODATA
     C                   ELSE
     C                   EXSR      $TFTOT
     C                   EXSR      $TSTOT
     C                   EXSR      $TPTOT
     C                   ENDIF                                                  IF NO DATA
      *
     C                   SETON                                        LR
      /EJECT
      *----------------------------------------------------------------
      * $CHECK - RETRIEVE CHECK ADJUSTMENTS AND PRINT DETAIL LINE
      *----------------------------------------------------------------
      *
     C     $CHECK        BEGSR
      *
     C     K2L064        CHAIN     HSL064C                            92
     C     *IN92         IFEQ      *OFF
      *
      *  LOAD DEDUCTION/ADD-ONS AND PREMIUM/DISCOUNTS INTO ARRAYS
      *  FOR PRINTING ON DETAIL LINE
      *
     C                   EXSR      $TOTDA
      *
      * CALCULATE NET AMOUNT
      *
     C     CDGRAM        ADD       DTDAAM        DTNTAM
      *
      * Sum condemned and yard dead head and pounds
      *
     C     cdcdhd        add       cdydhd        dtcdydhd
     C                   ADD       dtcdydhd      tpcdydhd
     C                   ADD       dtcdydhd      tscdydhd
     C                   ADD       dtcdydhd      tfcdydhd
      *
     C     cdcdlb        add       cdydlb        dtcdydlb
     C                   ADD       dtcdydlb      tpcdydlb
     C                   ADD       dtcdydlb      tscdydlb
     C                   ADD       dtcdydlb      tfcdydlb
      *
      *
      * Accumulate DOA head and pounds
      *
     C                   ADD       cddohd        tpdohd
     C                   ADD       cddohd        tsdohd
     C                   ADD       cddohd        tfdohd
      *
     C                   ADD       cddolb        tpdolb
     C                   ADD       cddolb        tsdolb
     C                   ADD       cddolb        tfdolb
      *
      * Accumulate live head and pounds
      *
     C                   ADD       cdlvhd        tplvhd
     C                   ADD       cdlvhd        tslvhd
     C                   ADD       cdlvhd        tflvhd
      *
     C                   ADD       cdlvlb        tplvlb
     C                   ADD       cdlvlb        tslvlb
     C                   ADD       cdlvlb        tflvlb
      *
      *  ADD CHECK VALUES TO PRODUCTION PHASE TOTALS
      *
     C                   ADD       CDCRLB        TPCRLB
     C                   ADD       CDBAAM        TPBAAM
     C                   ADD       CDGRAM        TPGRAM
     C                   ADD       DTNTAM        TPNTAM
     C                   ADD       DTDAAM        TPDAAM
     C                   ADD       DTPDAM        TPPDAM
      *
      *  ADD CHECK VALUES TO SALES TYPE TOTALS
      *
     C                   ADD       CDCRLB        TSCRLB
     C                   ADD       CDBAAM        TSBAAM
     C                   ADD       CDGRAM        TSGRAM
     C                   ADD       DTNTAM        TSNTAM
     C                   ADD       DTDAAM        TSDAAM
     C                   ADD       DTPDAM        TSPDAM
      *
      *  ADD CHECK VALUES TO FARM TOTALS
      *
     C                   ADD       CDCRLB        TFCRLB
     C                   ADD       CDBAAM        TFBAAM
     C                   ADD       CDGRAM        TFGRAM
     C                   ADD       DTNTAM        TFNTAM
     C                   ADD       DTDAAM        TFDAAM
     C                   ADD       DTPDAM        TFPDAM
      *
      * RETRIEVE HOG GROUP DATA
      *
     C     CDHGSN        CHAIN     HSL034D                            92
     C     *IN92         IFEQ      *OFF
     C                   MOVEL     HGHGCD        DTHGCD
     C                   MOVEL     HGBSCD        DTBSCD
     C                   ENDIF
      *
      * PRINT DETAIL LINE AND SET OFF INDICATORS THAT CONTROL
      * PRINTING OF DETAIL LINE FIELDS
      *
     C     *INOF         IFEQ      *ON
     C                   EXSR      $HEAD
     C                   ENDIF
     C                   EXCEPT    DTLINE
     C                   SETOFF                                       1011
      *
      * READ/PRINT CHECK ADJUSTMENT ARRAYS
      *
     C                   EXSR      $PRTDA
     C                   ENDIF                                                  IF FOUND
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $TOTDA - BUILD ARRAY OF DEDUCTION/ADDS, PREMIUM/DISCOUNTS
      *----------------------------------------------------------------
      *
     C     $TOTDA        BEGSR
      *
     C                   MOVE      *BLANK        DD
     C                   Z-ADD     0             DA
     C                   Z-ADD     0             F
      *
     C                   MOVE      *BLANK        PD
     C                   Z-ADD     0             PA
     C                   Z-ADD     0             Q
     C                   SETOFF                                       70
      *
     C     K1L065        SETLL     HSL065C
     C                   SETOFF                                       70
      *
     C     *IN70         DOWEQ     *OFF                                         DO LOOP
     C     K1L065        READE     HSL065C                                70
     C     *IN70         IFEQ      *OFF                                         IF HIT
      *
      *----------------------------------------------------
      * LOAD DETAIL TIME ARRAYS
      *----------------------------------------------------
      * LOAD DEDUCTIONS/ADDITIONS IN DETAIL ARRAY (DA)
      *
     C     CJCJFL        IFEQ      'D'                                          IF DEDUCTION
     C                   Z-ADD     1             D
     C     CJCJCD        LOOKUP    DD(D)                                  50
     C     *IN50         IFEQ      *OFF                                         IF DA FOUND
     C                   ADD       1             F
     C                   MOVEL     CJCJCD        DD(F)
     C                   Z-ADD     CJCJAM        DA(F)
     C                   ELSE
     C                   ADD       CJCJAM        DA(D)
     C                   ENDIF                                                  IF DA FOUND
     C                   ELSE
      *
      * LOAD PREMIUM/DISCOUNTS INTO ARRAY
      *
     C                   Z-ADD     1             P
     C     CJCJCD        LOOKUP    PD(P)                                  50
     C     *IN50         IFEQ      *OFF                                         IF PD FOUND
     C                   ADD       1             Q
     C                   MOVEL     CJCJCD        PD(Q)
     C                   Z-ADD     CJCJAM        PA(Q)
     C                   ELSE
     C                   ADD       CJCJAM        PA(P)
     C                   ENDIF                                                  IF PD FOUND
     C                   ENDIF                                                  IF DEDUCTION
      *
      *----------------------------------------------------------------
      * LOAD TOTAL ARRAYS FOR FARM, SALES TYPE, PHASE
      *---------------------------------------------------------------
      *
     C                   EXSR      $TFARR
     C                   EXSR      $TSARR
     C                   EXSR      $TPARR
      *
     C                   ENDIF                                                  IF HIT
     C                   ENDDO                                                  DO LOOP
      *
     C                   XFOOT     DA            DTDAAM
     C                   XFOOT     PA            DTPDAM
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $CUST - RETRIEVE CUSTOMER/VENDOR NAME FROM JD EDWARDS SYSTEM
      *----------------------------------------------------------------
      *
     C     $CUST         BEGSR
      *
     C                   Z-ADD     WFCVNO        INAJD
     C                   MOVEL(P)  'C'           INAT1
     C                   EXSR      $JDEV
     C     JDEFL         IFEQ      YES
     C                   MOVEL(P)  'UNKNOWN'     DTCNAM
     C                   ELSE
     C                   MOVEL     OUALPH        DTCNAM
     C                   ENDIF
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $PPDS - RETRIEVE PRODUCTION PHASE DESCRIPTION
      *----------------------------------------------------------------
      *
     C     $PPDS         BEGSR
      *
     C     WFPPCD        CHAIN     HSP011                             92
     C     *IN92         IFEQ      *OFF
     C                   MOVEL     PPPPDS        HPPPDS
     C                   ELSE
     C                   MOVEL     'UNKNOWN'     HPPPDS
     C                   ENDIF
      *
     C                   MOVEL     WFPPCD        SVPPCD
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $STDS - RETRIEVE SALES TYPE CODE DESCRIPTION
      *----------------------------------------------------------------
      *
     C     $STDS         BEGSR
      *
     C     WFSTCD        CHAIN     HSP066                             92
     C     *IN92         IFEQ      *OFF
     C                   MOVEL     STSTDS        HDSTDS
     C                   ELSE
     C                   MOVEL     'UNKNOWN'     HDSTDS
     C                   ENDIF
      *
      * TYPE OF PAYMENT
     C                   SELECT
     C     WFSTCD        WHENEQ    'CULLS'
     C     WFSTCD        OREQ      'FPS  '
     C                   MOVE      *BLANK        HDTPDS
      *
     C     WFTPCD        WHENEQ    'C'
     C                   MOVEL(P)  CARC          HDTPDS
      *
     C     WFTPCD        WHENEQ    'L'
     C                   MOVEL(P)  LIVE          HDTPDS
     C                   ENDSL
      *
     C                   MOVEL     WFSTCD        SVSTCD
     C                   MOVEL     WFTPCD        SVTPCD
     C                   Z-ADD     WFFSCD        SVFSCD
      *
     C                   SETON                                        1011
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $HEAD - PRINT REPORT HEADINGS
      *----------------------------------------------------------------
      *
     C     $HEAD         BEGSR
      *
     C                   SETON                                        1011
     C                   EXCEPT    HRHDR
     C                   EXCEPT    HPHDR
     C                   EXCEPT    HDSALE
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $PRTDA -  PRINT ARRAYS OF DEDUCTION/ADDS, PREMIUMS/DISCOUNTS
      *----------------------------------------------------------------
      *
     C     $PRTDA        BEGSR
      *
      * START AT ELEMENT 2 SINCE ELEMENT 1 WAS PRINTED IN DTLINE.
      *
     C                   Z-ADD     1             D
      *
     C     2             DO        20            P
     C                   ADD       1             D
     C     PA(P)         IFNE      0
     C     DA(D)         ORNE      0
     C                   EXCEPT    DTLIN2
     C                   ENDIF
     C                   ENDDO
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $TFARR - LOAD FARM TOTAL ARRAYS
      *----------------------------------------------------------------
      *
      * ARRAYS FOR DEDUCTIONS/ADD-ONS AND PREMIUMS/DISCOUNTS
      *
     C     $TFARR        BEGSR
      *
     C     CJCJFL        IFEQ      'D'                                          IF FARM DED
     C                   Z-ADD     1             D
     C     CJCJCD        LOOKUP    FDD(D)                                 50
     C     *IN50         IFEQ      *OFF                                         IF FARM DA
     C                   ADD       1             A
     C                   MOVEL     CJCJCD        FDD(A)
     C                   Z-ADD     CJCJAM        FDA(A)
     C                   ELSE
     C                   ADD       CJCJAM        FDA(D)
     C                   ENDIF                                                  IF FARM DA
     C                   ELSE
      *
      * LOAD PREMIUM/DISCOUNTS INTO ARRAY
      *
     C                   Z-ADD     1             P
     C     CJCJCD        LOOKUP    FPD(P)                                 50
     C     *IN50         IFEQ      *OFF                                         IF FARM PD
     C                   ADD       1             B
     C                   MOVEL     CJCJCD        FPD(B)
     C                   Z-ADD     CJCJAM        FPA(B)
     C                   ELSE
     C                   ADD       CJCJAM        FPA(P)
     C                   ENDIF                                                  IF FARM PD
     C                   ENDIF                                                  IF FARM DED
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $TFTOT - PRINT FARM TOTALS
      *----------------------------------------------------------------
      *
     C     $TFTOT        BEGSR
      *
     C     *INOF         IFEQ      *ON
     C                   EXSR      $HEAD
     C                   ENDIF
     C                   EXCEPT    TFTOT
      *
      *  PRINT THE SUBTOTALS FOR PREMIUM/DISCOUNTS AND
      *  DEDUCTIONS/ADD-ONS FOR THE FARM.
      *
     C                   MOVEL     NO            PRTFL
     C                   Z-ADD     0             D
      *
     C     1             DO        20            P
     C                   ADD       1             D
     C     FPA(P)        IFNE      0
     C     FDA(D)        ORNE      0
      *
     C     *INOF         IFEQ      *ON
     C                   EXSR      $HEAD
     C                   ENDIF
     C                   EXCEPT    TFTOT2
      *
     C                   MOVEL     YES           PRTFL
     C                   ENDIF
     C                   ENDDO
      *
      * PRINT A BLANK LINE IF THERE WERE SUBTOTALS
      *
     C     PRTFL         IFEQ      YES
     C                   EXCEPT    BLANK
     C                   ENDIF
      *
      *  CLEAR THE FARM ARRAYS AND INDEXES
      *
     C                   MOVE      *BLANK        FDD
     C                   Z-ADD     0             FDA
     C                   MOVE      *BLANK        FPD
     C                   Z-ADD     0             FPA
     C                   Z-ADD     0             A
     C                   Z-ADD     0             B
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $TSARR - LOAD ARRAY FOR SALES TYPE TOTALS
      *----------------------------------------------------------------
      *
      * ARRAYS FOR DEDUCTIONS/ADD-ONS AND PREMIUMS/DISCOUNTS
      *
     C     $TSARR        BEGSR
      *
     C     CJCJFL        IFEQ      'D'                                          IF DEDUCT
     C                   Z-ADD     1             D
     C     CJCJCD        LOOKUP    SDD(D)                                 50
     C     *IN50         IFEQ      *OFF                                         IF NOT FOUND
     C                   ADD       1             M
     C                   MOVEL     CJCJCD        SDD(M)
     C                   Z-ADD     CJCJAM        SDA(M)
     C                   ELSE
     C                   ADD       CJCJAM        SDA(D)
     C                   ENDIF                                                  IF NOT FOUND
     C                   ELSE
      *
      * LOAD PREMIUM/DISCOUNTS INTO ARRAY
      *
     C                   Z-ADD     1             P
     C     CJCJCD        LOOKUP    SPD(P)                                 50
     C     *IN50         IFEQ      *OFF                                         IF PREM/DISC
     C                   ADD       1             N
     C                   MOVEL     CJCJCD        SPD(N)
     C                   Z-ADD     CJCJAM        SPA(N)
     C                   ELSE
     C                   ADD       CJCJAM        SPA(P)
     C                   ENDIF                                                  IF PREM/DISC
     C                   ENDIF                                                  IF DEDUCTION
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $TSTOT - PRINT SALES TYPE TOTALS
      *----------------------------------------------------------------
      *
     C     $TSTOT        BEGSR
      *
     C     *INOF         IFEQ      *ON
     C                   EXSR      $HEAD
     C                   ENDIF
     C                   EXCEPT    TSTOT
      *
      *  PRINT THE SUBTOTALS FOR PREMIUM/DISCOUNTS AND
      *  DEDUCTIONS/ADD-ONS FOR THE SALES TYPE.
      *
     C                   MOVEL     NO            PRTFL
     C                   Z-ADD     0             D
      *
     C     1             DO        20            P
     C                   ADD       1             D
     C     SPA(P)        IFNE      0
     C     SDA(D)        ORNE      0
      *
     C     *INOF         IFEQ      *ON
     C                   EXSR      $HEAD
     C                   ENDIF
     C                   EXCEPT    TSTOT2
      *
     C                   MOVEL     YES           PRTFL
     C                   ENDIF
     C                   ENDDO
      *
      * PRINT A BLANK LINE IF THERE WERE SUBTOTALS
      *
     C     PRTFL         IFEQ      YES
     C                   EXCEPT    BLANK
     C                   ENDIF
      *
      *  CLEAR THE SALES TYPE ARRAYS AND INDEXES
      *
     C                   MOVE      *BLANK        SDD
     C                   Z-ADD     0             SDA
     C                   MOVE      *BLANK        SPD
     C                   Z-ADD     0             SPA
     C                   Z-ADD     0             M
     C                   Z-ADD     0             N
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $TPARR - LOAD ARRAY FOR PHASE TOTALS
      *----------------------------------------------------------------
      *
      * ARRAYS FOR DEDUCTIONS/ADD-ONS AND PREMIUMS/DISCOUNTS
      *
     C     $TPARR        BEGSR
      *
     C     CJCJFL        IFEQ      'D'                                          IF DEDUCTION
     C                   Z-ADD     1             D
     C     CJCJCD        LOOKUP    PDD(D)                                 50
     C     *IN50         IFEQ      *OFF                                         IF NOT FOUND
     C                   ADD       1             G
     C                   MOVEL     CJCJCD        PDD(G)
     C                   Z-ADD     CJCJAM        PDA(G)
     C                   ELSE
     C                   ADD       CJCJAM        PDA(D)
     C                   ENDIF                                                  IF NOT FOUND
     C                   ELSE
      *
      * LOAD PREMIUM/DISCOUNTS INTO ARRAY
      *
     C                   Z-ADD     1             P
     C     CJCJCD        LOOKUP    PPD(P)                                 50
     C     *IN50         IFEQ      *OFF                                         IF PREM/DISC
     C                   ADD       1             H
     C                   MOVEL     CJCJCD        PPD(H)
     C                   Z-ADD     CJCJAM        PPA(H)
     C                   ELSE
     C                   ADD       CJCJAM        PPA(P)
     C                   ENDIF                                                  IF PREM/DISC
     C                   ENDIF                                                  IF DEDUCTION
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $TPTOT - PRINT PHASE TOTALS
      *----------------------------------------------------------------
      *
     C     $TPTOT        BEGSR
      *
     C     *INOF         IFEQ      *ON
     C                   EXCEPT    HRHDR
     C                   EXCEPT    HPHDR
     C                   EXCEPT    BLANK
     C                   ENDIF
     C                   EXCEPT    TPTOT
      *
      *  PRINT THE SUBTOTALS FOR PREMIUM/DISCOUNTS AND
      *  DEDUCTIONS/ADD-ONS FOR THE PHASE.
      *
     C                   MOVEL     NO            PRTFL
     C                   Z-ADD     0             D
      *
     C     1             DO        20            P
     C                   ADD       1             D
     C     PPA(P)        IFNE      0
     C     PDA(D)        ORNE      0
      *
     C     *INOF         IFEQ      *ON
     C                   EXCEPT    HRHDR
     C                   EXCEPT    HPHDR
     C                   EXCEPT    BLANK
     C                   ENDIF
     C                   EXCEPT    TPTOT2
      *
     C                   MOVEL     YES           PRTFL
     C                   ENDIF
     C                   ENDDO
      *
      *  CLEAR THE PHASE ARRAYS AND INDEXES
      *
     C                   MOVE      *BLANK        PDD
     C                   Z-ADD     0             PDA
     C                   MOVE      *BLANK        PPD
     C                   Z-ADD     0             PPA
     C                   Z-ADD     0             G
     C                   Z-ADD     0             H
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $TOMDY - FLIP DATE FROM YYMMDD TO MMDDYY
      *----------------------------------------------------------------
      *
     C     $TOMDY        BEGSR
      *
     C                   Z-ADD     WKMON         WKMM
     C                   Z-ADD     WKDAY         WKDD
     C                   Z-ADD     WKYER         WKYY
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $JDEV - SUBROUTINE TO CALL THE EXTERNAL PROGRAM TO VALIDATE JDE
      *         ADDRESS NUMBERS.
      *----------------------------------------------------------------
      *
     C     $JDEV         BEGSR
      *
     C                   MOVEL     NO            JDEFL                          JDE ERRORS
      *
     C                   CALL      'HPJDEV'
     C                   PARM                    MGI                            MSG ID'S
     C                   PARM                    MGD                            MSG DATA FLD
     C                   PARM                    ERROR                          # ERR MSGS
     C                   PARM                    INAJD                          JDE ADDR #
     C                   PARM                    INAT1                          JDE ADDR TYP
     C                   PARM                    JDEFL                          ERROR FLAG
     C                   PARM      *BLANK        OUALPH                         JDE NAME
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * *INZSR - INITIALIZATION SUBROUTINE
      *---------------------------------------------------------------
      *
     C     *INZSR        BEGSR
      *
      * KEY FOR FILE HSL065 -CHECK ADJUSTMENTS
      *
     C     K1L065        KLIST
     C                   KFLD                    CDCHDT
     C                   KFLD                    CDCVNO
     C                   KFLD                    CDCHNO
     C                   KFLD                    CDLNNO
      *
     C     K2L064        KLIST
     C                   KFLD                    WFMVSN
     C                   KFLD                    WFCVNO
     C                   KFLD                    WFCHNO
     C                   KFLD                    WFCHDT
     C                   KFLD                    WFLNNO
      *
      * INDEX FIELDS FOR ARRAYS
      *
     C                   Z-ADD     0             P                 2 0
     C                   Z-ADD     0             D                 2 0
      *
     C                   Z-ADD     0             F                 2 0
     C                   Z-ADD     0             Q                 2 0
      *
     C                   Z-ADD     0             A                 2 0
     C                   Z-ADD     0             B                 2 0
      *
     C                   Z-ADD     0             M                 2 0
     C                   Z-ADD     0             N                 2 0
      *
     C                   Z-ADD     0             G                 2 0
     C                   Z-ADD     0             H                 2 0
      *
      * J.D. EDWARDS PARMS
      *
     C                   MOVE      *BLANK        JDEFL             1            JDE ERR FLAG
     C                   MOVE      *BLANK        INAT1             3            JDE ADDR TYP
     C     *LIKE         DEFINE    CJCVNO        INAJD                          JDE NUMBER
     C                   MOVE      *BLANK        OUALPH           40            JDE NAME
      *
      * SAVE FIELDS FOR LEVEL BREAK COMPARISONS
      *
     C     *LIKE         DEFINE    WFPPCD        SVPPCD
     C     *LIKE         DEFINE    WFSTCD        SVSTCD
     C     *LIKE         DEFINE    WFFSCD        SVFSCD
     C     *LIKE         DEFINE    WFTPCD        SVTPCD
      *
      * FLAG TO CONTROL PRINTING OF FARM SITE TOTAL LINES
      *
     C                   MOVEL     NO            PRTFL             1
      *
      * PRINT FIELDS FOR THE DETAIL LINE
      *
     C     *LIKE         DEFINE    cdcdhd        dtcdydhd
     C     *LIKE         DEFINE    cdcdlb        dtcdydlb
     C     *LIKE         DEFINE    LDFMDY        DTRCDT
     C                   MOVE      *BLANK        DTCNAM            8
     C     *LIKE         DEFINE    CDGRAM        DTDAAM
     C     *LIKE         DEFINE    CDGRAM        DTPDAM
     C     *LIKE         DEFINE    CDGRAM        DTNTAM          + 2
     C     *LIKE         DEFINE    HGHGCD        DTHGCD
     C     *LIKE         DEFINE    HGBSCD        DTBSCD
      *
      * PRINT FIELD FOR HEADINGS
      *
     C     *LIKE         DEFINE    PPPPDS        HPPPDS
     C     *LIKE         DEFINE    STSTDS        HDSTDS
     C                   MOVE      *BLANK        HDTPDS           15
      *
      * PRINT FIELDS FOR THE PRODUCTION PHASE TOTAL LINE
      *
     C                   Z-ADD     0             tpdohd            9 0
     C                   Z-ADD     0             tplvhd            9 0
     C                   Z-ADD     0             tpcdydhd          9 0
      *
     C                   Z-ADD     0             tpdolb           11 0
     C                   Z-ADD     0             tplvlb           11 0
     C                   Z-ADD     0             tpcdydlb         11 0
      *
     C                   Z-ADD     0             TPCRLB           11 0
     C                   Z-ADD     0             TPBAAM           11 2
     C                   Z-ADD     0             TPGRAM           11 2
     C                   Z-ADD     0             TPNTAM           11 2
     C                   Z-ADD     0             TPDAAM           11 2
     C                   Z-ADD     0             TPPDAM           11 2
      *
      * PRINT FIELDS FOR THE SALES TYPE TOTAL LINE
      *
     C     *LIKE         DEFINE    tpdohd        tsdohd
     C     *LIKE         DEFINE    tplvhd        tslvhd
     C     *LIKE         DEFINE    tpcdydhd      tscdydhd
      *
     C     *LIKE         DEFINE    tpdolb        tsdolb
     C     *LIKE         DEFINE    tplvlb        tslvlb
     C     *LIKE         DEFINE    tpcdydlb      tscdydlb
      *
     C     *LIKE         DEFINE    TPCRLB        TSCRLB
     C     *LIKE         DEFINE    TPBAAM        TSBAAM
     C     *LIKE         DEFINE    TPGRAM        TSGRAM
     C     *LIKE         DEFINE    TPNTAM        TSNTAM
     C     *LIKE         DEFINE    TPDAAM        TSDAAM
     C     *LIKE         DEFINE    TPPDAM        TSPDAM
      *
      * PRINT FIELDS FOR THE FARM TOTAL LINE
      *
     C     *LIKE         DEFINE    tpdohd        tfdohd
     C     *LIKE         DEFINE    tplvhd        tflvhd
     C     *LIKE         DEFINE    tpcdydhd      tfcdydhd
      *
     C     *LIKE         DEFINE    tpdolb        tfdolb
     C     *LIKE         DEFINE    tplvlb        tflvlb
     C     *LIKE         DEFINE    tpcdydlb      tfcdydlb
      *
     C     *LIKE         DEFINE    TPCRLB        TFCRLB
     C     *LIKE         DEFINE    TPBAAM        TFBAAM
     C     *LIKE         DEFINE    TPGRAM        TFGRAM
     C     *LIKE         DEFINE    TPNTAM        TFNTAM
     C     *LIKE         DEFINE    TPDAAM        TFDAAM
     C     *LIKE         DEFINE    TPPDAM        TFPDAM
      *
      *  SETUP THE USER SELECTIONS IN THE REPORT HEADINGS.
      *
     C     LDFSBO        IFEQ      *BLANK
     C                   MOVEL     'ALL'         LDFSBO
     C                   ENDIF
      *
     C     LDPPCD        IFEQ      *BLANK
     C                   MOVEL     'ALL'         LDPPCD
     C                   ENDIF
      *
     C     LDPTCD        IFEQ      *BLANK
     C                   MOVEL     'ALL'         LDPTCD
     C                   ENDIF
      *
     C     LDSTCD        IFEQ      *BLANK
     C                   MOVEL     'ALL'         LDSTCD
     C                   ENDIF
      *
     C     LDFSCD        IFEQ      0
     C                   SETON                                        89
     C                   ENDIF
      *
     C     LDBSCD        IFEQ      *BLANK
     C                   MOVEL     'ALL'         LDBSCD
     C                   ENDIF
      *
     C     LDHGCD        IFEQ      *BLANK
     C                   MOVEL     'ALL'         LDHGCD
     C                   ENDIF
      *
     C     LDCVNO        IFEQ      0
     C                   SETON                                        95
     C                   ENDIF
      *
      * SET UP A FIRST TIME FLAG
      * SET UP A DASHED LINE FOR UNDERLINING THE HEADING
      * RETRIEVE TIME FOR REPORT HEADING
      * INITIALIZE DATE FIELD
      *
     C                   MOVE      YES           FIRST             1
     C                   MOVEL     *ALL'-'       DASH            198
     C                   TIME                    RTIME             6 0
     C                   Z-ADD     0             WKCYMD
      *
     C                   ENDSR
      /EJECT
      *-------------------------------------------------------------
      * REPORT HEADINGS
      *-------------------------------------------------------------
     OQPRINT    E            HRHDR          1 01
     O                       SDPGM               10
     O                                          111 'HOG PRODUCTION SYSTEM'
     O                                          188 'DATE'
     O                       UDATE         Y    198
      *
     O          E            HRHDR          1
     O                       SDUSR               10
     O                                          107 'SALES MOVEMENT/CHECK'
     O                                          114 'REPORT'
     O                                          188 'TIME'
     O                       RTIME              198 '  :  :  '
      *
     O          E            HRHDR          1
     O                                          188 'PAGE'
     O                       PAGE          Z    198
      *
     O          E            HRHDR          1
     O                                           18 'BUSINESS OFFICE:'
     O                       LDFSBO              25
     O                       LDBODS            +  5
      *
     O          E            HRHDR          1
     O                                           18 'FROM RECEIVE DATE:'
     O                       LDFMDY        Y   +  2
      *
     O          E            HRHDR          1
     O                                           18 'TO RECEIVE DATE:'
     O                       LDTMDY        Y   +  2
      *
     O          E            HRHDR          1
     O                                           18 'PRODUCTION TYPE:'
     O                       LDPTCD              25
     O                       LDPTDS            +  5
      *
     O          E            HRHDR          1
     O                                           18 'PRODUCTION PHASE:'
     O                       LDPPCD              25
     O                       LDPPDS            +  5
      *
     O          E            HRHDR          1
     O                                           18 'SALE TYPE CODE:'
     O                       LDSTCD              25
     O                       LDSTDS            +  5
      *
     O          E            HRHDR          1
     O                                           18 'FARM SITE:'
     O              N89      LDFSCD        M     26
     O              N89      LDFSNM              55
     O               89                          23 'ALL'
      *
     O          E            HRHDR          1
     O                                           18 'BREED STOCK:'
     O                       LDBSCD              25
     O                       LDBSDS            +  5
      *
     O          E            HRHDR          1
     O                                           18 'HOG GROUP:'
     O                       LDHGCD              27
      *
     O          E            HRHDR          2
     O                                           18 'CUSTOMER:'
     O               95                          23 'ALL'
     O              N95      LDCVNO        Z     28
     O                       LDCNAM            +  2
      *
      *-------------------------------------------------------------
      * COLUMN HEADINGS
      *-------------------------------------------------------------
      *
     O          E            HRHDR          1
     O                                            5 'FARM'
     O                                           15 'RECEIVE'
     O                                           24 'MOVEMENT'
     O                                           26 'P'
     O                                           43 'CHECK'
     O                                           65 'BREED'
      *
     O                                           80 '--- LIVE ---'
     O                                           96 '--- DOA ----'
     O                                          112 '- Y/D & CD -'
      *
     O                                          121 'CARCASS'
     O                                          135 'BASE'
     O                                          166 'GROSS'
     O                                          196 'NET'
     O          E            HRHDR          1
     O                                            5 'SITE'
     O                                           13 'DATE'
     O                                           24 'NUMBER'
     O                                           26 'T'
     O                                           35 'CUSTOMER'
     O                                           43 'NUMBER'
     O                                           50 'TATTOO'
     O                                           57 'GROUP'
     O                                           65 'STOCK'
      *
     O                                           72 'HEAD'
     O                                           80 'POUNDS'
     O                                           88 'HEAD'
     O                                           96 'POUNDS'
     O                                          104 'HEAD'
     O                                          112 'POUNDS'
      *
     O                                          121 'POUNDS'
     O                                          135 'AMOUNT'
     O                                          153 'PREMIUM/DISCOUNT'
     O                                          166 'AMOUNT'
     O                                          184 'DEDUCTION/ADD-ON'
     O                                          197 'AMOUNT'
     O          E            HRHDR          1
     O                       DASH               198
      *
      *-------------------------------------------------------------
      * PRODUCTION PHASE HEADING
      *-------------------------------------------------------------
      *
     O          E            HPHDR          1
     O                                           84 '*****'
     O                                          101 'PRODUCTION PHASE'
     O                                          107 '*****'
     O                       HPPPDS            +  3
      *
      *-------------------------------------------------------------
      * SALE TYPE HEADING
      *-------------------------------------------------------------
      *
     O          E            HDSALE         2
     O                                            3 '***'
     O                                         +  1 'S A L E   T Y P E'
     O                                         +  1 '***'
     O                       SVSTCD            +  2
     O                       HDSTDS            +  2
     O                       HDTPDS            +  2
      *
      *
      *-------------------------------------------------------------
      * DETAIL LINE
      *-------------------------------------------------------------
      *
     O          E            DTLINE         1
     O               10      WFFSCD        MB     6
     O               11      DTRCDT        Y     15
     O                       WFMVSN        Z     24
     O                       WFTPCD              26
     O                       DTCNAM              35
     O                       CDCHNO        Z     43
     O                       CDTANO        Z     50
     O                       DTHGCD         B    59
     O                       DTBSCD         B    65
      *
     O                       cdlvhd        K     73
     O                       cdlvlb        K     81
     O                       cddohd        K     89
     O                       cddolb        K     97
     O                       dtcdydhd      kb   105
     O                       dtcdydlb      kb   113
      *
     O                       CDCRLB        K    122
     O                       CDBAAM        K    136
     O                       PD(1)              142
     O                       PA(1)         K    153
     O                       CDGRAM        K    167
     O                       DD(1)              173
     O                       DA(1)         K    184
     O                       DTNTAM        KB   198
      *
     O          E            DTLIN2         1
     O                       PD(P)              142
     O                       PA(P)         K    153
     O                       DD(D)              173
     O                       DA(D)         K    184
      *
      *----------------------------------------------------------------
      * TOTAL FOR FARM   (TOTALS ARE STAGGERED ON 2 LINES)
      *----------------------------------------------------------------
      *
     O          E            TFTOT       1  0
     O                                           54 'FARM SITE'
     O                       SVFSCD        M     61
     O                                           67 'TOTAL'
     O                       tflvhd        KB    73
     O                       tfdohd        KB    89
     O                       tfcdydhd      KB   105
     O                       TFCRLB        KB   122
     O                       TFGRAM        KB   167
     O                       TFNTAM        KB   198
      *
     O          E            TFTOT       1  2
     O                       tflvlb        KB    81
     O                       tfdolb        KB    97
     O                       tfcdydlb      KB   113
     O                       TFBAAM        KB   136
     O                       TFPDAM        KB   153
     O                       TFDAAM        KB   184
      *
     O          E            TFTOT2         1
     O                       FPD(P)             140
     O                       FPA(P)        K    153
     O                       FDD(D)             171
     O                       FDA(D)        K    184
      *
      *----------------------------------------------------------------
      * TOTAL FOR SALES TYPE (TOTALS ARE STAGGERED ON 2 LINES)
      *----------------------------------------------------------------
      *
     O          E            TSTOT       0  0
     O                                           38 'SALE TYPE'
     O                       SVSTCD              44
     O                       HDTPDS              60
     O                                           67 'TOTAL'
     O                       tslvhd        KB    73
     O                       tsdohd        KB    89
     O                       tscdydhd      KB   105
     O                       TSCRLB        KB   122
     O                       TSGRAM        KB   167
     O                       TSNTAM        KB   198
      *
     O          E            TSTOT       1  2
     O                       tslvlb        KB    81
     O                       tsdolb        KB    97
     O                       tscdydlb      KB   113
     O                       TSBAAM        KB   136
     O                       TSPDAM        KB   153
     O                       TSDAAM        KB   184
      *
     O          E            TSTOT2         1
     O                       SPD(P)             140
     O                       SPA(P)        K    153
     O                       SDD(D)             171
     O                       SDA(D)        K    184
      *
      *----------------------------------------------------------------
      * TOTAL FOR PRODUCTION PHASE   (TOTALS ARE STAGGERED ON 2 LINES)
      *----------------------------------------------------------------
      *
     O          E            TPTOT       0  0
     O                                           54 'PRODUCTION PHASE'
     O                       SVPPCD              60
     O                                           67 'TOTAL'
     O                       tplvhd        KB    73
     O                       tpdohd        KB    89
     O                       tpcdydhd      KB   105
     O                       TPCRLB        KB   122
     O                       TPGRAM        KB   167
     O                       TPNTAM        KB   198
      *
     O          E            TPTOT       1  2
     O                       tplvlb        KB    81
     O                       tpdolb        KB    97
     O                       tpcdydlb      KB   113
     O                       TPBAAM        KB   136
     O                       TPPDAM        KB   153
     O                       TPDAAM        KB   184
      *
     O          E            TPTOT2         1
     O                       PPD(P)             140
     O                       PPA(P)        K    153
     O                       PDD(D)             171
     O                       PDA(D)        K    184
      *
      *----------------------------------------------------------------
      * BLANK LINE
      *----------------------------------------------------------------
      *
     O          E            BLANK          1
      *
      *-------------------------------------------------------------
      * NO DATA MESSAGE LINE
      *-------------------------------------------------------------
      *
     O          E            NODATA      2
     O                                           19 'NO DATA MEETS YOUR'
     O                                           38 'SELECTION CRITERIA'
