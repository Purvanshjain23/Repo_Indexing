     A*%%TS  SD  20021114  101354  ISLRAMS     REL-V5R1M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD7051
     A*  TITLE:         VIEW GROUPS FOR TATTOO/KILL DATE
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  CREATED:       11/13/02
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A N90                                  CF05(05 'REFRESH')
     A N82N90                               ROLLUP(25 'PAGE FORWARD')
     A*
     A*---------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP7051S1                  SFL
     A*%%TS  SD  20021114  092204  ISLRAMS     REL-V5R1M0  5722-WDS
     A  85                                  SFLNXTCHG
     A            HDRRN1         4Y 0H
     A            D1OPT          2A  B 11  2CHGINPDFT(UL)
     A  90                                  DSPATR(PR)
     A  90                                  DSPATR(ND)
     A  30                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A N30N90                               DSPATR(HI)
     A N30N90                               DSPATR(UL)
     A            D1HGCD    R        O 11  8REFFLD(@@HGCD)
     A            D1FSCD    R        O 11 18REFFLD(@@FSCD)
     A                                      EDTCDE(Z)
     A            D1PPCD    R        O 11 27REFFLD(@@PPCD)
     A            D1PTCD    R        O 11 36REFFLD(@@PTCD)
     A            D1GTCD    R        O 11 47REFFLD(@@GTCD)
     A            D1SXCD    R        O 11 54REFFLD(@@SXCD)
     A*
     A*---------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP7051C1                  SFLCTL(HP7051S1)
     A*%%TS  SD  20021114  101354  ISLRAMS     REL-V5R1M0  5722-WDS
     A                                      SFLSIZ(0100)
     A                                      SFLPAG(0011)
     A                                      OVERLAY
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 25'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  2 19'VIEW GROUPS FOR TATTOO/KILL DATE'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  4  2'Tattoo .....:'
     A            C1TANO    R        O  4 17REFFLD(HSRREF/CDTANO *LIBL/HSPREF)
     A                                      EDTCDE(Z)
     A                                  5  2'Kill date ..:'
     A            C1KMDY    R        O  5 17REFFLD(HSRREF/@@DT06 *LIBL/HSPREF)
     A                                      EDTWRD('  /  /  ')
     A                                  7  2'5=View sire line/sow source data'
     A                                  9  8'Hog'
     A                                      DSPATR(HI)
     A                                  9 19'Farm'
     A                                      DSPATR(HI)
     A                                  9 27'--Production--'
     A                                      DSPATR(HI)
     A                                  9 45'Group'
     A                                      DSPATR(HI)
     A                                 10  2'Opt'
     A                                      DSPATR(HI)
     A                                 10  8'Group'
     A                                      DSPATR(HI)
     A                                 10 19'Site'
     A                                      DSPATR(HI)
     A                                 10 27'Phase'
     A                                      DSPATR(HI)
     A                                 10 36'Type'
     A                                      DSPATR(HI)
     A                                 10 45'Type'
     A                                      DSPATR(HI)
     A                                 10 53'Sex'
     A                                      DSPATR(HI)
     A*
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R HP7051K1
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A                                      DSPATR(HI)
     A                                 23  2'F3=Exit'
     A N90                             23 11'F5=Refresh'
     A*
     A*---------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE FORMAT
     A*---------------------------------------------------------------
     A          R HP7051ES                  SFL SFLMSGRCD(24)
     A*
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
     A*
     A*---------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*---------------------------------------------------------------
     A          R HP7051EC                  SFLCTL(HP7051ES)
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
