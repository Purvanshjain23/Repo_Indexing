      *****************  RPG PROGRAM HEADING  ************************
      * SYSTEM:      HOG PRODUCTION
      * PROGRAM:     HP670
      * TITLE:       PRINT SALES MOVEMENT BILL OF LADING
      * PROGRAMMER:  LEANNE FEDOR
      * CREATED:     02/06/96
      *
      *  FUNCTION:   THIS PROGRAM PRINTS A BOL FOR A SCHEDULED SALES MOVEMENT.
      *
      *
      *              As of January 2003, we have two separate print functions in HPS.
      *              At this time, Synon/WEB functions were installed to create
      *              Market Sales for Seaboard and interface them into HPS. These movements
      *              that originated in SYNON/WEB will, typically, be printed thru a
      *              different HPS program since they 1) have a different layout and
      *              2) are driven by the SYNON files. But, we have left the capability
      *              to print them thru this function (when the version is not Restricted)
      *              ...they will 1) print here from the HPS files...not the
      *              SYNON files and 2) will print on the layout in this program.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 10/17/00  LeAnne Fedor
      *           Added logic to print new 'load type code' 1 space after
      *           the movement number.
      *
      * 06/05/01  LeAnne Fedor
      *           Recompile only. New field 'multisite' added to Farm Site file.
      *
      * 06/28/01  LeAnne Fedor
      *           Recompile only.
      *           Three fields (manager/supervisor/multisite) renamed in Farm Site file.
      *           Manager and supervisor fields removed from Hog Group file.
      *
      * 09/04/01  LeAnne Fedor
      *           Recompile only. New 'square feet' fields added to Farm Site file.
      *
      * 03/26/02  LeAnne Fedor
      *           'Cell' is now a selection variable. So, added code to check for 'cell'
      *           associated with farm on a movement.
      *
      * 01/15/03  LeAnne Fedor
      *           Removed 'sex' and 'genetic line' from Sales Movement Detail file.
      *           So, changed code to retreive 'sex' for the group from the Hog Group
      *           file.
      *
      * 01/21/03  LeAnne Fedor
      *           If the version is R=restricted, we will NOT print MRKTS movements for
      *           Seaboard
      *
      * 07/06/09  LeAnne Ramsey
      *           Recompile only. Added new field 'Continuous Flow Flag' to Hog Group file.
      *
      * 10/15/13  LeAnne Ramsey (E2831)
      *           Recompile only. Added field 'MTech Reference'.
      *
      * 01/21/21  ISE            TSN593
      *           Replaced *loval with *start
      *
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     FHSP018    IF   E           K DISK
      *    FARM SITE
      *
     FHSL034D   IF   E           K DISK
      *    HOG GROUP
      *
     FHSP046    IF   E           K DISK
      *    TRUCKER
      *
     FHSP066    IF   E           K DISK
      *    SALES TYPE
      *
     FHSP084    UF   E           K DISK
      * SALES MOVEMENT HEADER  (records selected/keyed in OPNQRY)
      *
      *
     FHSP085    IF   E           K DISK
      * SALES MOVEMENT DETAIL
      *
      *
     FQPRINT    O    F  198        PRINTER OFLIND(*INOF)
      *
      /EJECT
      ****************************************************************
      * Definition specs
      ****************************************************************
      *
      * Workfields for date manipulation
      *
     D wkudate         s                   like(shscdt)
     D wkcymdiso       s               D   datfmt(*iso)
      *
      *---------------------------------------------------------------
      * STANDARD ERROR MESSAGE HANDLING DATA STRUCTURES
      *---------------------------------------------------------------
      *
      * FOR ERROR MESSAGE HANDLING, A PACKED INDEX (E) IS REQUIRED.
      * FOR PROGRAM READABILITY, DEFINE A CORRESPONDING ERROR COUNT
      * FIELD CALLED 'ERROR'
      *
     D                 DS                  INZ
     D  ERROR                  1      2P 0
     D  E                      1      2P 0
      *
      * THIS DATA STRUCTURE SUPPLIES THE NAME OF THE MESSAGE FILE TO
      * THE MESSAGE HANDLING CL PROGRAM.  THE FIELD NAME MSGFIL MUST BE
      * CONSTANT. THE VALUE IN QUOTES IS THE NAME OF THE SPECIFIC
      * MESSAGE FILE CONTAINING THE USER DEFINED MESSAGES.
      *
     D                 DS                  INZ
     D  MSGFIL                 1     10    INZ('HSMSGF    ')
      *
      * THE FOLLOWING 3 DATA STRUCTURES ARE USED TO SPEED MESSAGE
      * HANDLING SINCE IT IS FASTER TO CLEAR DATA STRUCTURES THAN
      * ARRAYS.  EACH IS ASSOCIATED WITH A STANDARD MESSAGE ARRAY.
      *
     D MGI             DS           140    INZ
     D  MGID                   1    140
     D                                     DIM(20)                              MSG ID ARRAY
      *
     D MGD             DS          1000    INZ
     D  MGDT                   1   1000
     D                                     DIM(20)                              MSG PARMS
      *
     D MGWK            DS            50    INZ
     D  MGW                    1     50
     D                                     DIM(50)                              MSG WORK PARMS
      *
      *---------------------------------------------------------------
      *  LDA
      *---------------------------------------------------------------
      *
     Dlda             uds                  dtaara(*lda)
     D  ldcell                32     33  0
     D  ldversion             59     59
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *---------------------------------------------------------------
      * Standard database file information data structure
      *---------------------------------------------------------------
      *    externally defined as UTDBGR (record format: FDBCKD)
     D dbfeed        e ds                  extname(utdbfr)
      *
      *
      *
      *---------------------------------------------------------------
      *  DATE FORMATS
      *---------------------------------------------------------------
      *
      * DATE IN MMDDYY FORMAT
      *
     D                 DS
     D  WKMDY                  1      6  0
     D  WKMM                   1      2  0
     D  WKDD                   3      4  0
     D  WKYY                   5      6  0
      *
      * DATE IN CCYYMMDD FORMAT
      *
     D                 DS
     D  WKCYMD                 1      8  0
     D  WKCEN                  1      2  0
     D  WKYER                  3      4  0
     D  WKMON                  5      6  0
     D  WKDAY                  7      8  0
      *
      *---------------------------------------------------------------
      *  NAMED CONSTANTS
      *---------------------------------------------------------------
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * MAIN LINE
      ****************************************************************
      *
593  C*    *LOVAL        SETLL     HSP084
593  C     *START        SETLL     HSP084
      *
      * READ/PROCESS ALL SALES MOVEMENTS SELECTED BY THE OPNQRY
      *
     C     *IN90         DOUEQ     *ON                                          MAIN DO
     C                   READ      HSP084                                 90
     C     *IN90         IFEQ      *OFF                                         IF NOT EOF
     C                   move      yes           procfl
      *
      *
      * Retrieve 'cell' associated with farm and compare to user's selected
      * 'cell'.
      *
     C                   if        ldcell <> 0
     C     shfscd        chain     hsp018                             92
     C                   select
     C                   when      *in92 = *on or ldcell <> fscell
     C                   move      no            procfl
     C                   endsl
     C                   endif
      *
      * If this is a restricted run
      *
     C                   if        ldversion = 'R' and
     C                             shstcd = 'MRKTS' and shcvno = 360516
     C                   move      no            procfl
     C                   endif
      *
      * If continuing
     C                   if        procfl = yes                                 If process
      *
     C                   Z-ADD     SHMVSN        HRMVSN
     C                   move      shltcd        hrltcd
     C                   Z-ADD     SHCVNO        HRCVNO
      *
      * RETRIEVE CUSTOMER NAME
      *
     C                   Z-ADD     SHCVNO        INAJD
     C                   MOVEL(P)  'C'           INAT1
     C                   EXSR      $JDEV
      *
     C     JDEFL         IFEQ      YES                                           IF BAD CUST
     C                   MOVEL(P)  'UNKNOWN'     HRCNAM
     C                   ELSE
     C                   MOVEL     OUALPH        HRCNAM
     C                   ENDIF                                                   IF BAD CUST
      *
      * RETRIEVE SALES TYPE DESCRIPTION
      *
     C     SHSTCD        CHAIN     HSP066                             92
     C     *IN92         IFEQ      *OFF
     C                   MOVEL     STSTDS        HRSTDS
     C                   ELSE
     C                   MOVEL(P)  'UNKNOWN'     HRSTDS
     C                   ENDIF
      *
      * FLIP SCHEDULED SHIP DATE FROM CCYYMMDD TO MMDDYY
      *
     C                   Z-ADD     SHSCDT        WKCYMD
     C                   EXSR      $TOMDY
     C                   Z-ADD     WKMDY         HRSMDY
      *
      * Flip/print scheduled kill date if this sales movement is
      * a 1) market sale for 2) Seaboard.
      *
     C                   if        shcvno = 360516 and
     C                             shstcd = 'MRKTS'
     C                   seton                                        75
     C                   z-add     shsckldt      wkcymd
     C                   exsr      $tomdy
     C                   z-add     wkmdy         hrscklmdy
     C                   else
     C                   setoff                                       75
     C                   endif
      *
      *
      * RETRIEVE FIRST 15 CHARACTERS OF TRUCKER NAME
      *
     C     SHTICD        CHAIN     HSP046                             92
     C     *IN92         IFEQ      *OFF
     C                   MOVEL     TITINM        HRTINM
     C                   ELSE
     C                   MOVEL(P)  'UNKNOWN'     HRTINM
     C                   ENDIF
      *
     C                   EXCEPT    HRHDR
      *
      * RETRIEVE FARM NAME (YOU WILL ONLY PRINT FIRST 6 CHARACTERS)
      *
     C                   Z-ADD     SHFSCD        DTFSCD
     C     SHFSCD        CHAIN     HSP018                             92
     C     *IN92         IFEQ      *OFF                                         IF FARM HIT
     C                   MOVEL     FSFSNM        DTFSNM
     C                   ENDIF                                                  IF FARM HIT
      *
      * RETRIEVE/PRINT MOVEMENT DETAIL
      *
     C                   EXSR      $DTL
      *
      * PRINT TOTAL SCHEDULED HEAD
      *
     C                   EXCEPT    TRTOT
      *
     C                   ADD       1             SHPTCT
     C                   Z-ADD     WKUDATE       SHUPDT
     C                   TIME                    SHUPTM
     C                   MOVEL     SDPGM         SHPGM
      *
     C                   UPDATE    SHREC
      *
     C                   endif                                                  If process
     C                   ENDIF                                                  IF NOT EOF
     C                   ENDDO                                                  MAIN DO
      *
     C                   SETON                                        LR
      *
      /EJECT
      *---------------------------------------------------------------
      * $TOMDY - FLIP DATE FROM CCYYMMDD TO MMDDYY
      *---------------------------------------------------------------
      *
     C     $TOMDY        BEGSR
      *
     C                   Z-ADD     WKMON         WKMM
     C                   Z-ADD     WKDAY         WKDD
     C                   Z-ADD     WKYER         WKYY
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * $DTL - RETRIEVE DATA AND PRINT DETAIL LINE FOR BOL
      *---------------------------------------------------------------
      *
     C     $DTL          BEGSR
      *
     C     SHMVSN        SETLL     HSP085
      *
     C     *IN91         DOUEQ     *ON                                          DO DETAIL
     C     SHMVSN        READE     HSP085                                 91
     C     *IN91         IFEQ      *OFF                                         IF NOT EOF
      *
     C                   Z-ADD     SGSCHD        DTSCHD
     C                   ADD       SGSCHD        TRSCHD
      *
      * RETRIEVE BREED STOCK, BUILDING, ROOM
      *   NOTES: 1) PRINT FIRST LETTER OF BREED STOCK IN
      *             GENETIC LINE COLUMN
      *          2) PRINT 'BUILDING' FOR 'SITE' ON FIRST LINE
      *          2) PRINT 'ROOM' FOR 'BARN' ON SECOND LINE
      *
     C     SGHGSN        CHAIN     HSL034D                            92
     C     *IN92         IFEQ      *OFF                                         IF GROUP OK
     C                   MOVEL     HGHGCD        DTHGCD
     C                   MOVEL     HGBSCD        DTBSCD
     C                   MOVEL     HGBLCD        DTBLCD
     C                   MOVEL     HGRMCD        DTRMCD
     C                   MOVEL     HGSXCD        DTSXCD
     C                   ENDIF                                                  IF GROUP OK
      *
     C                   EXCEPT    DTLINE
      *
     C                   ENDIF                                                  IF NOT EOF
     C                   ENDDO                                                  DO DETAIL
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $JDEV - SUBROUTINE TO CALL THE EXTERNAL PROGRAM TO VALIDATE JDE
      *         ADDRESS NUMBERS.
      *----------------------------------------------------------------
      *
     C     $JDEV         BEGSR
      *
     C                   MOVEL     NO            JDEFL                          JDE ERRORS
      *
     C                   CALL      'HPJDEV'
     C                   PARM                    MGI                            MSG ID'S
     C                   PARM                    MGD                            MSG DATA FLD
     C                   PARM                    ERROR                          # ERR MSGS
     C                   PARM                    INAJD                          JDE NUMBER
     C                   PARM                    INAT1                          JDE ADDR TYP
     C                   PARM                    JDEFL                          ERROR FLAG
     C                   PARM      *BLANK        OUALPH                         JDE NAME
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * *INZSR - INITIALIZATION SUBROUTINE
      *---------------------------------------------------------------
      *
     C     *INZSR        BEGSR
      *
      * DEFINE PRINT FIELDS FOR HEADING
      *
     C     *LIKE         DEFINE    SHLTCD        HRLTCD
     C     *LIKE         DEFINE    SHMVSN        HRMVSN
     C                   MOVE      *BLANK        HRSTDS           15
     C                   MOVE      *BLANK        HRTINM           15
     C                   Z-ADD     0             HRSMDY            6 0
     C                   z-add     0             hrscklmdy         6 0
     C     *LIKE         DEFINE    SHCVNO        HRCVNO
     C                   MOVE      *BLANK        HRCNAM           15
      *
      * DEFINE PRINT FIELDS FOR DETAIL LINES
      *
     C     *LIKE         DEFINE    SGSCHD        DTSCHD
     C     *LIKE         DEFINE    SHFSCD        DTFSCD
     C                   MOVE      *BLANK        DTFSNM            6
     C                   MOVE      *BLANK        DTBSCD            1
     C     *LIKE         DEFINE    HGSXCD        DTSXCD
     C     *LIKE         DEFINE    HGHGCD        DTHGCD
     C     *LIKE         DEFINE    HGBLCD        DTBLCD
     C     *LIKE         DEFINE    HGRMCD        DTRMCD
      *
      * DEFINE PRINT FIELDS FOR DETAIL LINES
      *
     C     *LIKE         DEFINE    SGSCHD        TRSCHD
      *
      * JDE FIELDS
      *
     C                   MOVE      *BLANK        JDEFL             1            JDE ERR FLAG
     C                   MOVE      *BLANK        INAT1             3            JDE ADDR TYP
     C     *LIKE         DEFINE    SHCVNO        INAJD                          JDE NUMBER
     C                   MOVE      *BLANK        OUALPH           40            JDE NAME
      *
      *
      * Control fields
      *
     C                   MOVE      *BLANK        procfl            1
      *
      * Flip system date to CCYYMMDD for writing movement event records.
      *
     C     *mdy          move      udate         wkcymdiso
     C                   move      wkcymdiso     wkudate
      *
     C                   ENDSR
      /EJECT
      *-------------------------------------------------------------
      * OUTPUT SPECIFICATIONS
      *-------------------------------------------------------------
      *
     OQPRINT    E            HRHDR             8
     O                       HRMVSN        ZB    23
     O                       HRLTCD         B    26
     O                       HRSTDS         B    45
     O                                           51 'CUST:'
     O                       HRCVNO        ZB    60
     O                       HRCNAM         B    76
      *
     O          E            HRHDR            10
     O                       HRSMDY        YB    23
      *
     O          E            HRHDR            14
     O                       HRTINM         B    30
      *
     O          E    75      HRHDR            20
     O                                           51 'Scheduled Kill Date:'
     O                       HRSCKLMDY     YB    61
      *
     O          E            HRHDR          0 28
      *
      *---------------------------------------------------------------
      * DETAIL SECTION OF BILL OF LADING
      *---------------------------------------------------------------
      *
     O          E            DTLINE      1
     O                       DTFSCD        Z     18
     O                       DTFSNM              26
     O                       DTHGCD         B    37
     O                       DTBLCD         B    45
     O                       DTBSCD         B    49
     O                       DTSXCD         B    52
     O                       DTSCHD        KB    61
      *
     O          E            DTLINE      1
     O                       DTRMCD         B    46
      *
      *---------------------------------------------------------------
      * TOTAL LINE
      *---------------------------------------------------------------
      *
     O          E            TRTOT            51
     O                       TRSCHD        KB    61
      /EJECT
