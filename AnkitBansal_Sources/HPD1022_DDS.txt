     A*%%TS  SD  20070920  083230  ISLRAMS     REL-V5R4M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD1022
     A*  TITLE:         REVISE NUR/FIN TARGET TABLE
     A*  PROGRAMMER:    SHARON ZURN
     A*  DATE:          01/04/07
     A*
     A*  INDICATOR 60 IS ON WHEN IN REVISE MODE
     A*  INDICATOR 61 IS ON WHEN IN CREATE MODE
     A*  INDICATOR 62 IS ON WHEN IN DELETE MODE
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CF03(03 'EXIT')
     A  60                                  CF05(05 'REFRESH')
     A  60                                  CF11(11 'DELETE')
     A*
     A*-----------------------------------------------------------------
     A* RECORD FORMAT ONE
     A*-----------------------------------------------------------------
     A          R HP1022R1
     A*%%TS  SD  20070920  083230  ISLRAMS     REL-V5R4M0  5722-WDS
     A                                      SETOF(31)
     A                                      SETOF(32)
     A                                      SETOF(33)
     A                                      OVERLAY
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 27'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USR)
     A                                      DSPATR(HI)
     A            D1MODE    R        O  2  2REFFLD(@@MODE)
     A                                      DSPATR(HI)
     A                                  2 24'REVISE NUR/FIN TARGET TABLE'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@JOB)
     A                                      DSPATR(HI)
     A                                  4  2'Origin BGF cell ..'
     A            D1OBCELL  R        B  4 23REFFLD(HSRREF/@@OBCELL *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A N60N62N31                            DSPATR(HI)
     A  31                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A  60
     AO 62                                  DSPATR(PR)
     A N60N62                               DSPATR(UL)
     A                                  5  2'Origin BGF pod ...'
     A            D1OBPOD   R        B  5 22REFFLD(HSRREF/@@OBPOD *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A N60N62N32                            DSPATR(HI)
     A  32                                  DSPATR(PC)
     A  32                                  DSPATR(RI)
     A  60
     AO 62                                  DSPATR(PR)
     A N60N62                               DSPATR(UL)
     A*
     A                                  6  2'Description ......'
     A            D1NTDS    R        B  6 22REFFLD(HSRREF/@@NTDS *LIBL/HSPREF)
     A N62N33                               DSPATR(HI)
     A  33                                  DSPATR(PC)
     A  33                                  DSPATR(RI)
     A  62                                  DSPATR(PR)
     A N62                                  DSPATR(UL)
     A*-----------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*-----------------------------------------------------------------
     A          R HP1022K1
     A*%%TS  SD  20070104  125603  ISSZURN     REL-V5R3M0  5722-WDS
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A  60                             23 11'F5=Refresh'
     A  60                             23 23'F11=Delete'
      *
      *------------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *-----------------------------------------------------------------
     A          R HP1022ES                  SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
      *------------------------------------------------------------------
      * ERROR MESSAGE SUBFILE CONTROL FORMAT
      *-----------------------------------------------------------------
     A          R HP1022EC                  SFLCTL(HP1022ES)
      *
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
