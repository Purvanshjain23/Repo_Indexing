/***************************************************************** */
/*                                                                 */
/*  PROGRAM NUMBER:  HP462CL                                       */
/*  PROGRAM NAME:    COST FEED TICKETS                             */
/*  PROGRAMMER:      ROSE MARTIN                                   */
/*  CREATE DATE:     08/09/94                                      */
/*                                                                 */
/*  FUNCTION:        THIS CL IS SUBMITTED WITH QCMDEXC FROM        */
/*                   HP462-COST FEED TICKETS.                      */
/*                   USER OPTIONS ARE PASSED THROUGH THE LDA,      */
/*                   INCLUDING #COPIES, HOLD, & OUTQ.              */
/*                                                                 */
/*******************************************************************/
/* MODIFICATIONS                                                   */
/*                                                                 */
/* 09/12/95  LEANNE FEDOR                                          */
/*           RENAMED CALLED PROGRAM TO BE HP362 INSTEAD OF HP262.  */
/*                                                                 */
/* 06/19/97  LEANNE FEDOR                                          */
/*           CHANGED OUT QUEUE LOGIC.                              */
/*******************************************************************/

             PGM

             DCL        VAR(&LDCYMD) TYPE(*DEC) LEN(8)
             DCL        VAR(&LDEFFL) TYPE(*CHAR) LEN(1)
             DCL        VAR(&LDFMBO) TYPE(*CHAR) LEN(5)

             DCL        VAR(&HOLD) TYPE(*CHAR) LEN(10)
             DCL        VAR(&COPYS) TYPE(*DEC) LEN(2)
             DCL        VAR(&OUTQ) TYPE(*CHAR) LEN(10)

             CHGVAR     VAR(&LDFMBO) VALUE(%SST(*LDA 1 5))
             CHGVAR     VAR(&LDCYMD) VALUE(%SST(*LDA 6 8))
             CHGVAR     VAR(&LDEFFL) VALUE(%SST(*LDA 14 1))

/*******************************************************************/
/* SET PARAMETERS FROM LDA                         */
/*******************************************************************/

 HOLD:       IF         COND(%SST(*LDA 411 1) = 'Y') THEN(CHGVAR +
                          VAR(&HOLD) VALUE(*YES))
             ELSE       CMD(CHGVAR VAR(&HOLD) VALUE(*NO))

 COPYS:      CHGVAR     VAR(&COPYS) VALUE(%SST(*LDA 412 1))
             IF         COND(&COPYS = 0) THEN(CHGVAR VAR(&COPYS) +
                          VALUE(1))

 OUTQ:       CHGVAR     VAR(&OUTQ) VALUE(%SST(*LDA 413 10))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(%SST(*LDA 401 10)))
             IF COND(&OUTQ = '          ') THEN(CHGVAR VAR(&OUTQ) +
                          VALUE(*JOB))

 OVRPRTF:    OVRPRTF    FILE(QPRINT) OUTQ(&OUTQ) COPIES(&COPYS) +
                          HOLD(&HOLD) SHARE(*YES)


/*******************************************************************/
/*  CALL PROGRAM TO EDIT/PRINT FEED TICKET COSTINGS */
/*******************************************************************/

             CALL       PGM(HP362)

             DLTOVR     FILE(QPRINT)

             ENDPGM

