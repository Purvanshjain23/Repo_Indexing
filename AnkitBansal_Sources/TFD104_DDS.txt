     A*%%TS  SD  20080530  161605  ISLRAMS     REL-V5R4M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        TRIUMPH FOODS
     A*  DISPLAY FILE:  TFD104
     A*  TITLE:         CREATE CASH DISTRIBUTION BALANCE ADJUSTMENT
     A*  PROGRAMMER:    LEANNE RAMSEY
     A*  CREATED:       11/02/06
     A*
     A* WE ONLY 'CREATE' CASH DISTRIBUTION BALANCE ADJUSTMENT RECORDS.
     A* THERE IS NO REVISE/DELETE CAPABILITY/FUNCTION.
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/TFPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A                                      CF04(04 'PROMPT')
     A                                      CF09(09 'ACCEPT')
     A                                      CA12(12 'PREVIOUS')
     A*
     A*--------------------------------------------------------------
     A* RECORD FORMAT ONE
     A*--------------------------------------------------------------
     A          R TF104R1
     A*%%TS  SD  20080530  161605  ISLRAMS     REL-V5R4M0  5722-WDS
     A                                      SETOF(30)
     A                                      SETOF(31)
     A                                      SETOF(32)
     A                                      SETOF(33)
     A                                      SETOF(35)
     A                                      SETOF(55)
     A                                      RTNCSRLOC(&RCD &FLD)
     A                                      OVERLAY
     A            RCD       R        H      REFFLD(@@RCD)
     A            FLD       R        H      REFFLD(@@FLD)
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 18'CREATE CASH DISTRIBUTION BALANCE A-
     A                                      DJUSTMENT'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USR)
     A                                      DSPATR(HI)
     A            D1MODE         6A  O  2  2DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USR)
     A                                      DSPATR(HI)
     A                                  3 43'Adjustment date:'
     A                                      COLOR(BLU)
     A            D1AJMDY   R        O  3 60REFFLD(TFREF/@@DT06 *LIBL/TFPREF)
     A                                      EDTWRD('  /  /  -')
     A                                  5 43'Week-ending date'
     A                                      DSPATR(UL)
     A                                      COLOR(BLU)
     A                                  5 61'Year'
     A                                      DSPATR(UL)
     A                                      COLOR(BLU)
     A                                  5 67'Week'
     A                                      DSPATR(UL)
     A                                      COLOR(BLU)
     A                                  6  2'Revenue week-ending date ..'
     A            D1MDY     R        B  6 32REFFLD(TFREF/@@DT06 *LIBL/TFPREF)
     A                                      EDTWRD('  /  /  -')
     A  30                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A N30                                  DSPATR(HI)
     A            D1WEMDY   R        O  6 47REFFLD(TFREF/@@DT06 *LIBL/TFPREF)
     A                                      EDTWRD('  /  /  ')
     A            D1YR      R        O  6 61REFFLD(TFREF/@@YR *LIBL/TFPREF)
     A                                      EDTCDE(Z)
     A            D1WK      R        O  6 68REFFLD(TFREF/@@WK *LIBL/TFPREF)
     A                                      EDTCDE(Z)
     A                                  7  2'Record exempt flag ........'
     A                                  7 30'?'
     A            D1RCEXFL  R        B  7 32REFFLD(@@RCEXFL)
     A  35
     AO 55                                  DSPATR(PC)
     A  35                                  DSPATR(RI)
     A N35                                  DSPATR(HI)
     A                                  9  2'Adjustment reason ......'
     A            D1AJRSN   R        B  9 29REFFLD(@@AJRSN)
     A                                      CHECK(LC)
     A  31                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A N31                                  DSPATR(HI)
     A                                 11 36'Seaboard'
     A                                      DSPATR(UL)
     A                                      COLOR(BLU)
     A                                 11 54'Triumph'
     A                                      DSPATR(UL)
     A                                      COLOR(BLU)
     A                                 11 69'Aggregate'
     A                                      DSPATR(UL)
     A                                      COLOR(BLU)
     A                                 12  2'Current balance amounts:'
     A            D1SBALAM  R        O 12 30REFFLD(TFREF/@@SBALAM *LIBL/TFPREF)
     A                                      EDTCDE(K)
     A            D1TBALAM  R        O 12 47REFFLD(TFREF/@@TBALAM *LIBL/TFPREF)
     A                                      EDTCDE(K)
     A            D1ABALAM  R        O 12 64REFFLD(TFREF/@@ABALAM *LIBL/TFPREF)
     A                                      EDTCDE(K)
     A                                 13  5'Adjustment amounts ..'
     A            D1SCDBAAM R        B 13 30REFFLD(TFREF/@@SCDBAAM *LIBL/TFPREF)
     A                                      EDTCDE(K)
     A  32                                  DSPATR(PC)
     A  32                                  DSPATR(RI)
     A N32                                  DSPATR(HI)
     A            D1TCDBAAM R        B 13 47REFFLD(TFREF/@@TCDBAAM *LIBL/TFPREF)
     A                                      EDTCDE(K)
     A  33                                  DSPATR(PC)
     A  33                                  DSPATR(RI)
     A N33                                  DSPATR(HI)
     A            D1ACDBAAM R        O 13 64REFFLD(TFREF/@@ABALAM *LIBL/TFPREF)
     A                                      EDTCDE(K)
     A                                 14  6'New balance amounts:'
     A                                      COLOR(PNK)
     A            D1SNEWAM  R        O 14 30REFFLD(TFREF/@@SBALAM *LIBL/TFPREF)
     A                                      EDTCDE(K)
     A                                      COLOR(PNK)
     A            D1TNEWAM  R        O 14 47REFFLD(TFREF/@@TBALAM *LIBL/TFPREF)
     A                                      EDTCDE(K)
     A                                      COLOR(PNK)
     A            D1ANEWAM  R        O 14 64REFFLD(TFREF/@@ABALAM *LIBL/TFPREF)
     A                                      EDTCDE(K)
     A                                      COLOR(PNK)
     A*--------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*--------------------------------------------------------------
     A          R TF104K1
     A*%%TS  SD  20061106  133516  ISLRAMS     REL-V5R3M0  5722-WDS
     A  43                             22  2'Message program failed.   Call MIS-
     A                                       user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A                                 23 11'F4=Prompt'
     A                                 23 22'F9=Accept'
     A                                 23 33'F12=Previous'
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *---------------------------------------------------------------
     A          R TF104ES                   SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE CONTROL FORMAT
      *---------------------------------------------------------------
     A          R TF104EC                   SFLCTL(TF104ES)
      *
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
