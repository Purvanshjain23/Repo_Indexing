/***************************************************************** */
/*                                                                 */
/*  PROGRAM NUMBER --> HP464CL                                     */
/*  PROGRAM NAME ----> SPECIFY FEED TICKET DETAIL REPORT OPTIONS   */
/*  PROGRAMMER ------> LEANNE FEDOR                                */
/*  DATE ------------> 06/19/97                                    */
/*                                                                 */
/*  PROGRAM PURPOSE -> THIS CL CALLS THE PROGRAMS TO PRINT FEED    */
/*                     TICKET DETAIL REPORTS.                      */
/*                                                                 */
/*                     USER SELECTIONS ARE PASSED THRU THE LDA.    */
/*******************************************************************/
/*  MODIFICATIONS:                                                 */
/*                                                                 */
/* 03/10/98  LEANNE FEDOR                                          */
/*           INCREASED PRINT LINE FROM 132 TO 198 TO ACCOMMODATE   */
/*           INCREASED SIZE OF FEED POUNDS AND FEED DOLLARS ON     */
/*           DETAIL REPORT BY VENDOR INVOICE NUMBER.               */
/*                                                                 */
/* 01/08/99  LEANNE FEDOR                                          */
/*           FIXED BUG WHERE MISSING APOSTROPHE IN QUERY SELECT    */
/*           CAUSED ABEND.                                         */
/*                                                                 */
/* 01/31/01  LEANNE FEDOR                                          */
/*           REMOVED ALL REFERENCE TO PRODUCTION TYPE. IT IS NO    */
/*           LONGER A SELECTOR FOR THIS REPORT.                    */
/*                                                                 */
/* 06/12/01  LEANNE FEDOR                                          */
/*           REMOVED ALL REFERENCE TO 'REPORT SEQUENCE'. ONLY ONE  */
/*           REPORT IS AVAILABLE NOW.  NO ONE WAS USING THE 'VENDOR*/
/*           INVOICE REPORT'. SO, WE GOT RID OF IT.                */
/*                                                                 */
/* 11/09/12  LEANNE RAMSEY (E2321)                                 */
/*           ADDED OPTION TO EMAIL XLS INSTEAD OF PRINTING REPORT. */
/*           REQUESTED BY KIM HUMBARD. DUE TO THE NUMBER OF        */
/*           COLUMNS, WE LIMIT THE ROWS EXTRACTED TO 26,700.       */
/*           OTHERWISE, IT GOES INTO LALA LAND.                    */
/*******************************************************************/

             PGM

/* DEFINE THE VARIABLE REQUIRED IN THE COMMAND TO OVERRIDE THE     */
/* PRINT FILE TO A LASER PRINTER                                   */

             DCL        VAR(&CMD) TYPE(*CHAR) LEN(512)

             DCL        VAR(&QDATA) TYPE(*CHAR) LEN(210)
             DCL        VAR(&RCDCNT) TYPE(*DEC) LEN(10 0)

             DCL        VAR(&LDFMBO) TYPE(*CHAR) LEN(5)
             DCL        VAR(&LDFYMD) TYPE(*DEC) LEN(8)
             DCL        VAR(&LDTYMD) TYPE(*DEC) LEN(8)
             DCL        VAR(&LDPPCD) TYPE(*CHAR) LEN(5)
             DCL        VAR(&LDFSCD) TYPE(*DEC) LEN(5)
             DCL        VAR(&LDRNCD) TYPE(*CHAR) LEN(5)
             DCL        VAR(&LDHGSN) TYPE(*DEC) LEN(7)
             DCL        VAR(&LDTRCD) TYPE(*CHAR) LEN(1)
             DCL        VAR(&LDRPFL) TYPE(*CHAR) LEN(1)
             DCL        VAR(&LDEMAIL) TYPE(*CHAR) LEN(50)
             DCL        VAR(&LDSEL)   TYPE(*CHAR) LEN(130)

             DCL        VAR(&HOLD) TYPE(*CHAR) LEN(10)
             DCL        VAR(&COPYS) TYPE(*DEC) LEN(2)
             DCL        VAR(&OUTQ) TYPE(*CHAR) LEN(10)

             CHGVAR     VAR(&LDFMBO) VALUE(%SST(*LDA 1 5))
             CHGVAR     VAR(&LDFYMD) VALUE(%SST(*LDA 6 8))
             CHGVAR     VAR(&LDTYMD) VALUE(%SST(*LDA 14 8))
             CHGVAR     VAR(&LDPPCD) VALUE(%SST(*LDA 27 5))
             CHGVAR     VAR(&LDFSCD) VALUE(%SST(*LDA 32 5))
             CHGVAR     VAR(&LDRNCD) VALUE(%SST(*LDA 37 5))
             CHGVAR     VAR(&LDHGSN) VALUE(%SST(*LDA 42 7))
             CHGVAR     VAR(&LDTRCD) VALUE(%SST(*LDA 49 1))
             CHGVAR     VAR(&LDRPFL) VALUE(%SST(*LDA 80 1))
             CHGVAR     VAR(&LDEMAIL) VALUE(%SST(*LDA 278 50))
             CHGVAR     VAR(&LDSEL)  VALUE(%SST(*LDA 451 130))

/*------------------------------------------------------------------*/
/*  CREATE WORKFILE                                                 */
/*------------------------------------------------------------------*/

             CRTDUPOBJL OBJ(HSP338) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)

/*------------------------------------------------------------------*/
/* SET UP THE OPEN QUERY                                            */
/*------------------------------------------------------------------*/

             CHGVAR     VAR(&QDATA) VALUE(' ')

/*  ALWAYS SELECT FEED TICKET DETAIL FOR DATE RANGE SPECIFIED    */

             CHGVAR     VAR(%SST(&QDATA 1 10)) VALUE('FDTKDT *GE')
             CHGVAR     VAR(%SST(&QDATA 12 8)) VALUE(&LDFYMD)
             CHGVAR     VAR(%SST(&QDATA 21 15)) VALUE('*AND FDTKDT +
                          *LE')
             CHGVAR     VAR(%SST(&QDATA 37 8)) VALUE(&LDTYMD)

/*  IF BUSINESS OFFICE IS NOT BLANK                              */

             IF         COND(&LDFMBO *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 46 17)) VALUE('*AND FDFMBO +
                          *EQ "')
             CHGVAR     VAR(%SST(&QDATA 63 5)) VALUE(&LDFMBO)
             CHGVAR     VAR(%SST(&QDATA 68 1)) VALUE('"')
             ENDDO


/*  IF FARM SITE IS NOT A BLANK                                  */

             IF         COND(&LDFSCD *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 118 15)) VALUE('*AND FDFSCD +
                          *EQ')
             CHGVAR     VAR(%SST(&QDATA 134 5)) VALUE(&LDFSCD)
             ENDDO

/*  IF RATION CODE IS NOT A BLANK                                */

             IF         COND(&LDRNCD *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 142 17)) VALUE('*AND FDRNCD +
                          *EQ "')
             CHGVAR     VAR(%SST(&QDATA 159 5)) VALUE(&LDRNCD)
             CHGVAR     VAR(%SST(&QDATA 164 1)) VALUE('"')
             ENDDO

/*  IF HOG GROUP IS NOT A BLANK,                                  */

             IF         COND(&LDHGSN *NE 0000000) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 166 15)) VALUE('*AND FDHGSN +
                          *EQ')
             CHGVAR     VAR(%SST(&QDATA 182 7)) VALUE(&LDHGSN)
             ENDDO

/*  IF TRANSACTION CODE IS NOT BLANK                              */

             IF         COND(&LDTRCD *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 190 17)) VALUE('*AND FDTRCD +
                          *EQ "')
             CHGVAR     VAR(%SST(&QDATA 207 1)) VALUE(&LDTRCD)
             CHGVAR     VAR(%SST(&QDATA 208 1)) VALUE('"')
             ENDDO


/*------------------------------------------------------------------*/
/*  RUN THE QUERY OVER THE FEED TICKET DETAIL FILE. THEN,           */
/*  CREATE A WORKFILE OF THE RECORDS SELECTED BY THE OPEN QUERY.    */
/*------------------------------------------------------------------*/

             OVRDBF     FILE(HSP038) SHARE(*YES)
             OPNQRYF    FILE((HSP038)) QRYSLT(&QDATA) +
                          KEYFLD((FDFSCD) (FDRNCD) +
                          (FDADCD) (FDRTNO) (FDTKNO) (FDTRCD)) +
                          SEQONLY(*YES)

             CALL       PGM(HP338)
             CLOF       OPNID(HSP038)
             DLTOVR     FILE(HSP038)


/*---------------------------------------------------------------------*/
/* IF EMAILING AN XLS, CREATE THE DOWNLOAD FILE                        */
/*---------------------------------------------------------------------*/

             IF         COND(&LDEMAIL *NE ' ') THEN(DO)
             CRTDUPOBJ  OBJ(HSP338) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP) NEWOBJ(HSP338DWL) DATA(*NO)
             ENDDO

/*---------------------------------------------------------------------*/
/* OVERRIDE THE KEYS ON THE WORKFILE....DEPENDING ON THE USER'S REPORT */
/*---------------------------------------------------------------------*/

/*  BY PRODUCTION PHASE                                             */

             IF         COND(&LDRPFL *EQ 'P') THEN(DO)
             OVRDBF     FILE(HSP338) SHARE(*YES)
             OPNQRYF    FILE((HSP338)) KEYFLD((WDPPCD) (WDFSCD) +
                          (WDRNCD) (WDADCD) (WDRTNO) (WDTKNO) +
                          (WDTRCD)) SEQONLY(*YES)
             IF         COND(&LDEMAIL *NE ' ') THEN(DO)
             CPYFRMQRYF FROMOPNID(HSP338) TOFILE(QTEMP/HSP338DWL) +
                          MBROPT(*REPLACE)
             ENDDO
             ENDDO


/*  BY PRODUCTION MANAGER                                           */

             IF         COND(&LDRPFL *EQ 'M') THEN(DO)
             OVRDBF     FILE(HSP338) SHARE(*YES)
             OPNQRYF    FILE((HSP338)) KEYFLD((WDPMGCD) (WDFSCD) +
                          (WDRNCD) (WDADCD) (WDRTNO) (WDTKNO) +
                          (WDTRCD)) SEQONLY(*YES)
             IF         COND(&LDEMAIL *NE ' ') THEN(DO)
             CPYFRMQRYF FROMOPNID(HSP338) TOFILE(QTEMP/HSP338DWL) +
                          MBROPT(*REPLACE)
             ENDDO
             ENDDO

/********************************************************************/
/* WHEN PRINTING THE REPORT                                         */
/********************************************************************/

/* THIS REPORT IS CONDENSED PRINT OF 15CPI ON 198 COLUMNS OF PRINT  */
/* FORCE IT TO PRINT OUT OF DRAWER 2 ON LANDSCAPE                   */


             IF         COND(&LDEMAIL *EQ ' ') THEN(DO)

 HOLD:       IF         COND(%SST(*LDA 411 1) = 'Y') THEN(CHGVAR +
                          VAR(&HOLD) VALUE('*YES      '))
             ELSE       CMD(CHGVAR VAR(&HOLD) VALUE('*NO       '))

 COPYS:      CHGVAR     VAR(&COPYS) VALUE(%SST(*LDA 412 1))
             IF         COND(&COPYS = 0) THEN(CHGVAR VAR(&COPYS) +
                          VALUE(1))

 OUTQ:       CHGVAR     VAR(&OUTQ) VALUE(%SST(*LDA 413 10))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(%SST(*LDA 401 10)))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE('*JOB      '))

             SBDPRTOVR  CMDVAR(&CMD) FILE(QPRINT) OUTQ(&OUTQ) +
                          WIDTH(198) LANDSCAPE(*YES) DRAWER(2) +
                          COPIES(&COPYS) HOLD(&HOLD)
             CALL       PGM(QCMDEXC) PARM(&CMD 512)
             CALL       PGM(HP665)

             CLOF       OPNID(HSP338)

             ENDDO

/********************************************************************/
/* WHEN EMAILING AN XLS                                             */
/********************************************************************/

             IF         COND(&LDEMAIL *NE ' ') THEN(DO)

/* Retrieve the number of records in the download file                  */

             RTVMBRD    FILE(HSP338DWL) NBRCURRCD(&RCDCNT)

             IF         COND(&RCDCNT *EQ 0) THEN(DO)
             ESNDMAIL   RECIPIENT(&LDEMAIL) SUBJECT('Feed Ticket +
                          Detail Report') MSG('''Your selections on +
                          HP464-Specify Feed Ticket Detail Report +
                          Options did not extract any data for the +
                          spreadsheet. Please change your +
                          selections and submit your request +
                          again.')
             MONMSG     MSGID(CPF0000)
             ENDDO

             IF         COND(&RCDCNT *GE 26700) THEN(DO)
             ESNDMAIL   RECIPIENT(&LDEMAIL) SUBJECT('Feed Ticket +
                          Detail Report') MSG('Your selections on +
                          HP464-Specify Feed Ticket Detail Report +
                          Options extracted over 26,700 records-- +
                          which is too many records for the +
                          spreadsheet email. Change your selections +
                          to extract fewer records and submit your +
                          request again.')
             MONMSG     MSGID(CPF0000)
             ENDDO

             IF         COND(&RCDCNT *GT 0 *AND &RCDCNT *LT 26700) THEN(DO)
             EXECUTE    SQL('SELECT * FROM HSP338DWL') NBRRCDS(*ALL) +
                          PCFMT(*XLS) RECIPIENT(&LDEMAIL) +
                          EMLMSG(&LDSEL) +
                          TEXT('Feed Ticket Detail Report')
             MONMSG     MSGID(CPF0000)
             ENDDO
             ENDDO

             DLTOVR     FILE(*ALL)

             ENDPGM

