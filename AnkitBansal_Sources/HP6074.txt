      *****************  RPG PROGRAM HEADING  ************************
      * SYSTEM:      Hog Production
      * PROGRAM:     HP6074
      * TITLE:       Commuter Herd Movement Log
      * PROGRAMMER:  LeAnne Ramsey
      * CREATED:     12/21/11
      *
      *  FUNCTION:   This program prints a report of Transfers with
      *              Dates in the user's selected range
      *
      *              This program is submitted by HP4074CL which is called
      *              from HP4074 thru QCMDEXC.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER    (Task)
      * 10/19/16  LARA BUSER    (E7606)
      *    Date Type Selector flag is passed to this program using *LDA
      *    Records are selected using OPNQRYF in the CLP which called this
      *    program
      *      "S" = selected by Scheduled Date
      *      "R" = selected by Received Date
      *    HSL3074A logical file added to read Commuter Workfile by
      *      scheduled ship date
      *
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
     Fhsp3074   if   e           k disk
      *   Workfile, record format = wfrec
      *
     Fhsl3074a  if   e           k disk    rename(wfrec:wfreca)
      *   Workfile, record format renamed to wfreca
      *
     Fprint80   o    f   80        printer oflind(*inof)
      *
      /EJECT
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Standard fields
      *
     d rtime           s              6  0
      *
      *
      * Control fields
      *
     D first           s              1    inz('Y')
      *
      *
      * Workfields for date manipulation
      *
     D wkisodate       s               D   datfmt(*iso)
      *
      *
      * Print fields
      *
     D dash            s             80    inz(*all'-')
     D l1dtmdy         s              6  0
     D h1aslds         s             47
     D h1range         s             32
      *
      *
      ****************************************************************
      * ARRAYS AND TABLES
      ****************************************************************
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *---------------------------------------------------------------
      * Local data area.
      *---------------------------------------------------------------
      *
     Dlda             uds                  dtaara(*lda)
     D  ldfsbo                 1      5
     D  ldfymd                 6     13  0
     D  ldtymd                14     21  0
     D  ldaslfl               51     51
      *
     D  ldbods                56     85
     D  ldfmdy               208    213  0
     D  ldtmdy               214    219  0
     D  ldaslds              226    255
     D  lddtsel              260    260
      *
      /EJECT
      ****************************************************************
      * Mainline
      ****************************************************************
      *
     C                   if        lddtsel = 'R'                                If OK date
     C                   exsr      $rcvdt
     C                   endif
      *
     C                   if        lddtsel = 'S'                                If OK date
     C                   exsr      $schdt
     C                   endif
      *
     C                   seton                                        lr
      *
      *----------------------------------------------------------------
      * Read work file for request by Received Date
      *----------------------------------------------------------------
     C     $rcvdt        begsr
      *
     C     *loval        setll     hsp3074
      *
     C                   read      hsp3074                                90
     C                   dow       *in90 = *off                                 Main do
      *
     C                   if        first = yes
     C                   move      no            first
     C                   exsr      $h1hdr
     C                   endif
      *
      * Flip Received Date to MMDDYY format
      *
     C     *iso          test(d)                 wfrcdt                 92
     C                   if        *in92 = *off                                 If OK date
     C                   move      wfrcdt        wkisodate
     C     *mdy          move      wkisodate     l1dtmdy
     C                   endif                                                  If OK date
      *
      * Print detail
     C                   if        *inof = *on
     C                   exsr      $h1hdr
     C                   endif
      *
     C                   except    l1line
     C*                                                                         If not eof
     C                   read      hsp3074                                90
     C                   enddo                                                  Main do
      *
      * EOF
     C                   if        first = yes
     C                   exsr      $h1hdr
     C                   except    nodata
     C                   endif
      *
     C                   endsr
      /EJECT
      *
      *----------------------------------------------------------------
      * Read work file for request by Scheduled Date
      *----------------------------------------------------------------
     C     $schdt        begsr
      *
     C     *loval        setll     hsl3074a
      *
     C                   read      hsl3074a                               91
     C                   dow       *in91 = *off                                 Main do
      *
     C                   if        first = yes
     C                   move      no            first
     C                   exsr      $h1hdr
     C                   endif
      *
      * Flip Scheduled Date to MMDDYY format
      *
     C     *iso          test(d)                 wfscdt                 92
     C                   if        *in92 = *off                                 If OK date
     C                   move      wfscdt        wkisodate
     C     *mdy          move      wkisodate     l1dtmdy
     C                   endif                                                  If OK date
      *
      * Print detail
     C                   if        *inof = *on
     C                   exsr      $h1hdr
     C                   endif
      *
     C                   except    l1line
     C*                                                                         If not eof
     C                   read      hsl3074a                               91
     C                   enddo                                                  Main do
      *
      * EOF with no scheduled ship date records
     C                   if        first = yes
     C                   exsr      $h1hdr
     C                   except    nodata
     C                   endif
      *
     C                   endsr
      /EJECT
      *----------------------------------------------------------------
      * Print headings
      *----------------------------------------------------------------
      *
     C     $h1hdr        begsr
      *
     C                   except    h1hdr
      *
     C                   endsr
      /EJECT
      *----------------------------------------------------------------
      * Initialization subroutine
      *----------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Retrieve time for report heading.
      *
     C                   time                    rtime
      *
      * Set up report headings
      *
     C                   if        ldfsbo = *blank
     C                   movel(p)  'ALL'         ldfsbo
     C                   endif
      *
      * Transfers across a State line
      *
     C                   select
     C                   when      ldaslfl = 'I'
     C                   eval      h1aslds = 'Transfers across a State Line +
     C                                        are Included'
     C                   when      ldaslfl = 'E'
     C                   eval      h1aslds = 'Transfers across a State Line +
     C                                        are Excluded'
     C                   when      ldaslfl = 'O'
     C                   eval      h1aslds = 'Only Transfers across a State +
     C                                        Line are printed'
     C                   endsl
      *
      * Date range is either for Scheduled Date or Received Date
      *
     C                   select
     C                   when      lddtsel = 'S'
     C                   eval      *in38 = *on
     C                   eval      h1range = 'Date Range is for Scheduled Date'
      *
     C                   when      lddtsel = 'R'
     C                   eval      *in39 = *on
     C                   eval      h1range = 'Date Range is for Received Date '
     C                   endsl
      *
     C                   endsr
      /EJECT
      *-------------------------------------------------------------
      * Heading lines
      *-------------------------------------------------------------
      *
     Oprint80   e            h1hdr          1 01
     O                       sdpgm               10
     O                                           48 'HOG PRODUCTION SYSTEM'
     O                                           70 'DATE'
     O                       udate         Y     80
      *
     O          e            h1hdr          1
     O                       sdusr               10
     O                                           51 'COMMUTER HERD MOVEMENT LOG'
     O                                           70 'TIME'
     O                       rtime               80 '  :  :  '
      *
     O          e            h1hdr          1
     O                                           70 'PAGE'
     O                       page          Z     80
      *
     O          e            h1hdr          1
     O                                           22 'Business Office ......'
     O                       ldfsbo              29
     O                       ldbods              64
      *
     O          E            h1hdr          1
     O                       h1range             32
      *
     O          E            h1hdr          1
     O                                           22 'From Date .........'
     O                       ldfmdy        Y     32
      *
     O          E            h1hdr          1
     O                                           22 'To Date ...........'
     O                       ldtmdy        Y     32
      *
     O          E            h1hdr          2
     O                                           22 'Across a State Line ..'
     O                       h1aslds             71
      *
      *-------------------------------------------------------------
      * Column headings
      *-------------------------------------------------------------
      *
     O          e            h1hdr          1
     O                                           37 '-------- ORIGIN ---------'
     O                                           65 '------ DESTINATION ------'
      *
     O          e            h1hdr          1
     O               38                           9 'SCHEDULED'
     O               39                           8 'RECEIVED'
     O                                           17 'FARM'
     O                                           24 'FARM'
     O                                           30 'BLDG'
     O                                           37 'ROOM'
      *
     O                                           45 'FARM'
     O                                           52 'FARM'
     O                                           58 'BLDG'
     O                                           65 'ROOM'
     O                                           75 'QUALITY'
     O          e            h1hdr          1
     O                                            6 'DATE'
     O                                           17 'STATE'
     O                                           24 'SITE'
     O                                           30 'CODE'
     O                                           37 'CODE'
      *
     O                                           45 'STATE'
     O                                           52 'SITE'
     O                                           58 'CODE'
     O                                           65 'CODE'
     O                                           75 'HEAD'
      *
     O          e            h1hdr          1
     O                       dash                80
      *
      *-------------------------------------------------------------
      * Detail Line
      *-------------------------------------------------------------
      *
     O          e            l1line         1
     O                       l1dtmdy              8 '  /  /  '
      * Origin
     O                       wforsacd            17
     O                       wforfs        z     24
     O                       wforbl              31
     O                       wforrm              38
      *
     O                       wfdnsacd            45
     O                       wfdnfs        z     52
     O                       wfdnbl              59
     O                       wfdnrm              66
      *
     O                       wfqlhd        k     76
      *
      *-------------------------------------------------------------
      * No data message line
      *-------------------------------------------------------------
     O          e            nodata         1
     O                                           19 'No data meets your'
     O                                           38 'selection criteria'
