      *****************  RPG PROGRAM HEADING  ************************
     h option(*SRCSTMT:*NODEBUGIO)
      ****************************************************************
      *
      * ENVIRONMENT: Pork Division
      * SYSTEM:      Resource Optimization
      * PROGRAM:     RO614 - Ham Group Yield Breakdown Report
      * PROGRAMMER:  LeAnne Ramsey
      * CREATED:     01/04/08
      *
      * FUNCTION:    Print records from the files:
      *                1) Item Meat Cost
      *                2) Ham Group Matrix Summary
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 11/20/08  LeAnne Ramsey
      *           As a prep for synchronizing the TFS and Meat Cost LDAs, we
      *           removed the unused LDA fields.
      *
      * 11/21/08  LeAnne Ramsey
      *           As part of synchronizing the LDAs between the TFS Margin Adjustment Close
      *           and the Meat Costing, we changed the LDA positions.
      *
      * 06/17/09  LeAnne Ramsey
      *           Recompile only. Three fields were added to ROP103-Item Meat Cost:
      *               TF Classification Code, TF Class Group Code, Item Structure Type Code
      /eject
      ****************************************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************************************
      *
     Fombyrel1  if   e           k disk
      *  Item default
      *
      *
     Frol103b   if   e           k disk
      *  Item meat cost (records selected by open query)
      *
      *
     frop106    if   e           k disk
      *  Ham group matrix summary
      *
      *
     Fprint198  o    f  198        printer oflind(*inof)
      *
      /eject
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Standard fields
      *
     d rtime           s              6  0
     D dash            s            188    inz(*all'-')
      *
      *
      * Control fields
      *
     D first           s              1    inz('Y')
     D svpgcd          s                   like(impgcd)
     D svplcd          s                   like(implcd)
      *
      *
      * Print fields
      *
     d h1pfds          s             11
      *
     d l2itds          s                   like(byavna)
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *---------------------------------------------------------------
      *  Local data area
      *---------------------------------------------------------------
      *
     d lda            uds                  dtaara(*lda)
     D  ldpfcd                 1      1
      *
     D  ldwedt                29     36  0
     D  ldwemdy               44     49  0
      *
     D  ldcono                82     84  0
      *
     D  lditcd               222    228  0
     D  lditds               229    258
     D  ldpgcd               259    264
     D  ldpgds               265    294
     D  ldplcd               299    304
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * Mainline
      ****************************************************************
      *
      * Print headings
     C                   exsr      $h1hdr

      * Read all Item Meat Cost records selected by the open query.
      * Only process the record if the Item has records in the Ham Group
      * Matrix Summary file.
      *
     C     *loval        setll     rol103b
      *
     C                   dou       *in90 = *on                                  Main do
     C                   read      rol103b                                90
     C                   if        *in90 = *off                                 If not EOF
      *
      * Continue only when this Item as record(s) in the Ham Group
      * Matrix Summary file.
      *
     C     key01         chain     rop106                             92
     C                   if        *in92 = *off                                 If breakdown
      *
      * Print a "heading" line for each unique Primal Group/Production Line
      *
     C                   move      no            first
     C                   if        impgcd <> svpgcd or
     C                             implcd <> svplcd
     C                   move      impgcd        svpgcd
     C                   move      implcd        svplcd
     C                   exsr      $h2hdr
     C                   endif
      *
      * Print the Item Meat Cost record
      *
     C                   exsr      $l1dtl
      *
      * Print the Ham Group Yield Breakdown record(s) for the Item
      *
     C                   exsr      $breakdown
      *
     C                   endif                                                  If breakdown
     C                   endif                                                  If not EOF
     C                   enddo                                                  Main do
      *
      * EOF processing
     C                   if        first = yes
     C                   except    nodata
     C                   endif
      *
     C                   seton                                        lr
      /eject
      *---------------------------------------------------------------
      * Print Heading for Primal Group/Production Line
      *---------------------------------------------------------------
      *
     C     $h2hdr        begsr
      *
     C                   if        *inof = *on
     C                   exsr      $h1hdr
     C                   endif
      *
     C                   except    h2hdr
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Print the Item detail line
      *---------------------------------------------------------------
      *
     C     $l1dtl        begsr
      *
     C                   if        *inof = *on
     C                   exsr      $h1hdr
     C                   exsr      $h2hdr
     C                   endif
      *
     C                   except    l1dtl
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Print the Ham Group Yield Breakdown records for the Item
      *---------------------------------------------------------------
      *
     C     $breakdown    begsr
      *
     C                   except    blank
      *
     C     key01         setll     rop106
      *
     C                   dou       *in91 = *on                                  Do loop
     C     key01         reade     rop106                                 91
     C                   if        *in91 = *off                                 If not EOF
      *
     C                   if        *inof = *on
     C                   exsr      $h1hdr
     C                   exsr      $h2hdr
     C                   endif
      *
      * Retrieve Component Item Description
      *
     C     msciitcd      chain     ombyrel1                           92
     C                   if        *in92 = *off
     C                   move      byavna        l2itds
     C                   endif
      *
     C                   except    l2dtl
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Do loop
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Print report headings
      *---------------------------------------------------------------
      *
     C     $h1hdr        begsr
      *
     C                   except    h1hdr
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Key lists
      *
     C     key01         klist
     C                   kfld                    ldcono
     C                   kfld                    ldwedt
     C                   kfld                    imitcd
      *
     C                   time                    rtime
      *
      * Set up report headings.
      *
      * This report is generated on-demand and also from the submitted
      * Weekly Meat Costing function.
      *
     C                   select
     C                   when      ldpfcd = 'D'
     C                   eval      h1pfds = ' On-Demand '
      *
     C                   when      ldpfcd = 'F'
     C                   eval      h1pfds = '   Final   '
      *
     C                   when      ldpfcd = 'P'
     C                   eval      h1pfds = 'Preliminary'
     C                   endsl
      *
      *
      * Primal Group Code
      *
     C                   if        ldpgcd = *blank
     C                   seton                                        87
     C                   endif
      *
      * Production Line Code
      *
     C                   if        ldplcd = *blank
     C                   seton                                        89
     C                   endif
      * Item
     C                   if        lditcd = 0
     C                   seton                                        88
     C                   endif
      *
     C                   endsr
      /eject
      *-------------------------------------------------------------
      * Report lines
      *-------------------------------------------------------------
      *
     Oprint198  e            h1hdr          1 01
     O                       sdpgm               10
     O                                          100 'Resource Optimization '
     O                                          106 'System'
     O                                          178 'DATE'
     O                       udate         y    188
      *
      *
     O          e            h1hdr          1
     O                       sdusr               10
     O                                          104 'Ham Group Yield Breakdown'
     O                                          178 'TIME'
     O                       rtime              188 '  :  :  '
      *
     O          e            h1hdr          1
     O                       h1pfds              97
     O                                          178 'PAGE'
     O                       page          z    188
      *
      *
     O          e            h1hdr          1
     O                                           21 'Company:'
     O                       ldcono        z     26
      *
     O          e            h1hdr          1
     O                                           21 'Week-ending date:'
     O                       ldwemdy             31 '  /  /  '
      *
     O          e            h1hdr          1
     O                                           21 'Primal group code:'
     O              n87      ldpgcd              29
     O              n87      ldpgds              63
     O               87                          26 'All'
      *
      *
     O          e            h1hdr          1
     O                                           21 'Production line code:'
     O              n89      ldplcd              29
     O               89                          26 'All'
      *
     O          e            h1hdr          2
     O                                           21 'Item code:'
     O              n88      lditcd        z     30
     O              n88      lditds              63
     O               88                          26 'All'
      *
      *
      *-------------------------------------------------------------
      * Column headings
      *-------------------------------------------------------------
     O          e            h1hdr          1
     O                                            6 'Primal'
     O                                           18 'Production'
     O                                           36 'Comp'
     O                                           63 'Sales'
      *
     O          e            h1hdr          1
     O                                            5 'Group'
     O                                           15 'Line'
     O                                           26 'Item'
     O                                           36 'Item'
     O                                           50 'Meat Cost'
     O                                           63 'Yield'
      *
     O          e            h1hdr          1
     O                                            4 'Code'
     O                                           15 'Code'
     O                                           26 'Code'
     O                                           36 'Code'
     O                                           51 'Group Code'
     O                                           63 'Percent'
     O                                           85 'Item Description'
      *
     O          e            h1hdr          0
     O                       dash               188
      *
      *-------------------------------------------------------------
      * Heading line for Primal Group/Production Line
      *-------------------------------------------------------------
      *
     O          e            h2hdr       2  0
     O                       svpgcd               6
     O                       svplcd              17
      *
     O          e            h2hdr       0  0
     O                       svpgcd               6
     O                       svplcd              17
      *
      *-------------------------------------------------------------
      * Detail line one  (items)
      *-------------------------------------------------------------
      *
     O          e            l1dtl       1  0
     O                       imitcd        m     27
      *
      *-------------------------------------------------------------
      * Detail line two  (breakdown)
      *-------------------------------------------------------------
      *
     O          e            l2dtl       0  1
     O                       msciitcd      m     37
     O                       mscicgcd            47
     O                       mscisypc            65 '  0.    -%'
     O                       l2itds         b    99
      /eject
      *-------------------------------------------------------------
      * Blank line
      *-------------------------------------------------------------
      *
     O          e            blank       1
      /eject
      *-------------------------------------------------------------
      * No data line
      *-------------------------------------------------------------
      *
     O          e            nodata      2
     O                                           19 'No data meets your '
     O                                           38 'selection criteria.'
      /eject
