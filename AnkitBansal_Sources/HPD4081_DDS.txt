     A*%%TS  SD  20170913  161204  ISJBADE     REL-V7R1M0  5770-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD4081
     A*  TITLE:         SPECIFY-MOVE RECEIVED SCHEDULED DATA TO EDITING
     A*  PROGRAMMER:    BRAD BADEN
     A*  PROJECT:       E010812
     A*  DATE:          09/13/2017
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A                                      CF08(08 'SUBMIT')
     A                                      CF20(20 'WRKSPLF')
     A*
     A*--------------------------------------------------------------*
     A* RECORD FORMAT ONE
     A*--------------------------------------------------------------*
     A          R HP4081R1
     A*%%TS  SD  20170913  161204  ISJBADE     REL-V7R1M0  5770-WDS
     A                                      OVERLAY
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 27'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  2  9'SPECIFY-MOVE RECEIVED SCHEDULED MO-
     A                                      VEMENTS DATA'
     A                                      DSPATR(HI)
     A                                  2 56'TO EDITING'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  5  2'This function will move the upload-
     A                                      ed data from the Receiving file to-
     A                                       the'
     A                                  6  2'Editing file. During this step, t-
     A                                      he Farm Site/Building/Room will be'
     A                                  7  2'populated using our upload rules.-
     A                                       After this step completes, contin-
     A                                      ue'
     A                                  8  2'with the Menu Option to Edit (Main-
     A                                      tain) the Uploaded Scheduled Move-'
     A                                  9  2'ments. (Just FYI, the Receiving fi-
     A                                      le will be empty at the end of this'
     A                                 10  2'move.)'
     A                                 12  2'Business Office.....'
     A                                      COLOR(BLU)
     A            SDBOBO         5A    12 23
     A                                      DSPATR(HI)
     A            SDBODS        30A    12 30
     A                                      COLOR(BLU)
     A                                 14  2'Take F8-Submit to submit the move -
     A                                      from Receiving to Editing.'
     A                                      DSPATR(HI)
     A*--------------------------------------------------------------*
     A* COMMAND KEY RECORD 1
     A*--------------------------------------------------------------*
     A          R HP4081K1
     A*%%TS  SD  20170913  161204  ISJBADE     REL-V7R1M0  5770-WDS
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A                                 23 11'F8=Submit'
     A                                 23 22'F20=Spool Files'
      *
      *--------------------------------------------------------------*
      * ERROR MESSAGE SUBFILE FORMAT
      *--------------------------------------------------------------*
     A          R HP4081ES                  SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
     A*
     A*--------------------------------------------------------------*
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*--------------------------------------------------------------*
     A*
     A          R HP4081EC                  SFLCTL(HP4081ES)
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
