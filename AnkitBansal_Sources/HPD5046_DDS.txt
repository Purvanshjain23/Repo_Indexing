     A*%%TS  SD  20040726  142837  ISLRAMS     REL-V5R2M0  5722-WDS
     A*****************************************************************
     A*
     A*  SYSTEM:        HOG PRODUCTION
     A*  DISPLAY FILE:  HPD5046 - SELECT WORK ORDER WITH STATUS OPTIONS
     A*  CREATED BY:    LEANNE FEDOR
     A*  DATE:          07/26/04
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      PRINT
     A                                      CA12(12 'PREVIOUS')
     A                                      CF04(04 'PROMPT')
     A N82                                  ROLLUP(25 'PAGE FORWARD')
     A*---------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP5046S1                  SFL
     A*%%TS  SD  20040726  142837  ISLRAMS     REL-V5R2M0  5722-WDS
     A            HDRRN1    R        H      REFFLD(@@RRN)
     A            D1WO      R        O  8  1REFFLD(HSRREF/@@WO *LIBL/HSPREF)
     A            D1RQSCD   R        O  8 15REFFLD(@@RQSCD)
     A            D1RQMDY   R        O  8 22REFFLD(HSRREF/@@DT06 *LIBL/HSPREF)
     A                                      EDTWRD('  /  /  ')
     A            D1FSCD    R        O  8 34REFFLD(@@FSCD)
     A                                      EDTCDE(M)
     A            D1CELL    R        O  8 43REFFLD(HSRREF/@@CELL *LIBL/HSPREF)
     A                                      EDTCDE(M)
     A            D1FSNM    R        O  8 48REFFLD(FSFSNM)
     A*
     A*---------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP5046C1                  SFLCTL(HP5046S1)
     A*%%TS  SD  20040726  142837  ISLRAMS     REL-V5R2M0  5722-WDS
     A                                      SFLSIZ(0100)
     A                                      SFLPAG(0009)
     A                                      WINDOW(HP5046EC)
     A                                      SETOF(53)
     A                                      SETOF(54)
     A                                      RTNCSRLOC(&RCD &FLD)
     A                                      OVERLAY
     A                                      SFLCSRRRN(&HDRRN)
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A            HDPAG1    R        H      REFFLD(@@PAG)
     A                                      SFLRCDNBR
     A            RCD           10A  H
     A            FLD           10A  H
     A            HDRRN     R        H      REFFLD(@@RRN)
     A            SDPGM     R        O  1  1REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 31'SELECT WORK ORDER'
     A                                      DSPATR(HI)
     A            C1DESC        22A  O  2 31DSPATR(HI)
     A                                  3  1'Position Cursor, Press Enter'
     A N62                              4 16'?'
     A                                      DSPATR(HI)
     A                                  4 37'?'
     A                                      DSPATR(HI)
     A                                  5  1'Work Order'
     A                                      DSPATR(HI)
     A                                  5 13'Request'
     A                                      DSPATR(HI)
     A                                  5 23'Request'
     A                                      DSPATR(HI)
     A                                  5 35'Farm'
     A                                      DSPATR(HI)
     A                                  6  1'Number'
     A                                      DSPATR(HI)
     A                                  6 13'Status'
     A                                      DSPATR(HI)
     A                                  6 25'Date'
     A                                      DSPATR(HI)
     A                                  6 35'Site'
     A                                      DSPATR(HI)
     A                                  6 42'Cell'
     A                                      DSPATR(HI)
     A                                  6 48'Farm Name'
     A                                      DSPATR(HI)
     A            C1WO      R        B  7  1REFFLD(HSRREF/@@WO *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A N62        C1RQSCD   R        B  7 15REFFLD(HSRREF/@@RQSCD *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A  53                                  DSPATR(PC)
     A            C1RQMDY   R        B  7 22REFFLD(HSRREF/@@DT06 *LIBL/HSPREF)
     A                                      EDTWRD('  /  /  ')
     A                                      DSPATR(HI)
     A            C1FSCD    R        B  7 34REFFLD(@@FSCD)
     A                                      DSPATR(HI)
     A  54                                  DSPATR(PC)
     A            C1CELL    R        B  7 43REFFLD(HSRREF/@@CELL *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A                                      EDTCDE(M)
     A            C1FSNM    R        B  7 48REFFLD(FSFSNM)
     A                                      CHGINPDFT(UL LC FE)
     A                                      DSPATR(HI)
     A*
     A*---------------------------------------------------------------
     A* COMMAND LINE LINE
     A*---------------------------------------------------------------
     A          R HP5046K1
     A                                      WINDOW(HP5046EC)
     A                                      OVERLAY
     A  43                             17  1'Message program failed. Call IS.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 18  1'F12=Previous'
     A                                 18 15'F4=Prompt'
      *
     A*-------------------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
     A*-------------------------------------------------------------------------
     A          R HP5046ES                  SFL SFLMSGRCD(19)
      *
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
     A*
     A*-------------------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*-------------------------------------------------------------------------
     A*
     A          R HP5046EC                  SFLCTL(HP5046ES)
     A*%%TS  SD  19980327  105628  ISLRAMS     REL-V3R7M0  5716-PW1
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A                                      WINDOW(*DFT 20 75)
     A                                      WDWBORDER((*DSPATR RI) (*CHAR '    -
     A                                          '))
     A            MSGPGM                    SFLPGMQ(10)
     A*
     A*---------------------------------------------------------------
     A* ASSUME FORMAT -- REQUIRED FOR WINDOWS
     A*---------------------------------------------------------------
     A          R ASSUME
     A*%%TS  SD  20011025  160559  ISLRAMS     REL-V4R4M0  5769-PW1
     A                                      ASSUME
     A                                      OVERLAY
     A                                 24 79' '
