/***************************************************************** */
/*                                                                 */
/*  PROGRAM NUMBER:  PKQ9UPC                                       */
/*  PROGRAM NAME:    PRINT REST TIME EVALUATION REPORT             */
/*  PROGRAMMER:      ROSE CENTONZE                                 */
/*  CREATE DATE:     05/06/99                                      */
/*                                                                 */
/*  FUNCTION:        THIS CL IS SUBMITTED WITH EXECUTE MESSAGE     */
/*                   FROM PROMPT FOR PRINT.                        */
/*                   USER OPTIONS ARE PASSED THROUGH THE PARMS.    */
/*                                                                 */
/*******************************************************************/
/*  Modification History                                           */
/*  RMC 1/22/02  ADD FTP CAPABILITY FOR OUTPUT FILE.               */
/*               SEQUEL STMT MAXES OUT AT 2000 CHARACTERS, SO SEND */
/*               PKEMCPP IN 2 PARTS: SIRE LINES 1-25, THEN 26-50   */
/*  RMC 2/21/02  REMOVE SIRE LINE FIELDS FROM FILE/FTP             */
/*  PKD 2/26/02  ADDED TO KILL DATE SELECTION                      */
/*  PKD 2/26/02  ADDED PARAMETER CALLED &PRCTYPE (PROCESS TYPE)    */
/*               THAT INDICATES IF PRINTING OR DOWNLOAD SHOULD     */
/*               BE PERFORMED.  "B" WILL BE SENT IF NEITHER       */
/*               THE PRINT OR THE DOWNLOAD SHOULD BE PERFORMED.   */
/*  RMC 3/26/02  ADDED TO SEQUEL OUTPUT : LOAD TIME DIFF, TRAVEL  */
/*               TIME DIFF, AND LOAD TYPE.                        */
/*  SLM 9/09/10  REMOVE HARD CODED ST1 CHG SQL TO IGNORE          */
/*               STRESS PENS                                      */
/*******************************************************************/
             PGM        PARM(&RTN &PDEV &HOLD &SAVE &XCOPIES &ACO +
                          &ADFROM &SRTYP &PEN &KSHIFT &ATATTO &ADTO +
                          &DOCUM &PRCTYPE &OMS)

/* PRINT OPTION PARMS                                             */
             DCL        VAR(&RTN)     TYPE(*CHAR) LEN(7)
             DCL        VAR(&PDEV)    TYPE(*CHAR) LEN(10)
             DCL        VAR(&HOLD)    TYPE(*CHAR) LEN(4)
             DCL        VAR(&SAVE)    TYPE(*CHAR) LEN(4)
             DCL        VAR(&XCOPIES) TYPE(*CHAR) LEN(15)

/* SYSTEM NAME PARM                                              */
             DCL        VAR(&OMS)     TYPE(*CHAR) LEN(33)
             DCL        VAR(&LPP)     TYPE(*CHAR) LEN(40)

/* FUNCTION SPECIFIC PARMS                                        */
             DCL        VAR(&ACO)     TYPE(*CHAR) LEN(3)
             DCL        VAR(&ADFROM)  TYPE(*CHAR) LEN(7)
             DCL        VAR(&ADTO)    TYPE(*CHAR) LEN(7)
             DCL        VAR(&SRTYP)   TYPE(*CHAR) LEN(1)
             DCL        VAR(&ATATTO)  TYPE(*CHAR) LEN(5)
             DCL        VAR(&PEN)     TYPE(*CHAR) LEN(3)
             DCL        VAR(&KSHIFT)  TYPE(*CHAR) LEN(1)
             DCL        VAR(&PRCTYPE)  TYPE(*CHAR) LEN(1) /* PROCESS TYPE */

/* NUMERIC VARIABLES                                             */
             DCL        VAR(&COPIES)  TYPE(*DEC)  LEN(2 0)
             DCL        VAR(&CO)      TYPE(*DEC)  LEN(3 0)
             DCL        VAR(&DFROM)   TYPE(*DEC)  LEN(7 0)
             DCL        VAR(&DTO)     TYPE(*DEC)  LEN(7 0)
             DCL        VAR(&TATTO)   TYPE(*DEC)  LEN(5 0)

/* SBDPRTOVR VARIABLE                                          */
             DCL        VAR(&CMD)     TYPE(*CHAR) LEN(512)

/* FTP VARIABLES                                               */
        DCL    &DOCUMENT  *CHAR   (10)
        DCL    &DOCUM     *CHAR    (06)           /*  FOR PASSING    */
        DCL    &PCFILE    *CHAR   (32)
        DCL    &SEL       *CHAR    (2000)         /*  FOR PASSING    */

/* CHANGE ALPHA PARMS TO NUMERIC VARIABLES                       */
             CHGVAR     VAR(&COPIES) VALUE(&XCOPIES)
             CHGVAR     VAR(&LPP) VALUE(&OMS)
             CHGVAR     VAR(&CO) VALUE(&ACO)
             CHGVAR     VAR(&DFROM) VALUE(&ADFROM)
             CHGVAR     VAR(&DTO) VALUE(&ADTO)
             CHGVAR     VAR(&TATTO) VALUE(&ATATTO)
/****************************************************************/
/*  DUP W001 PSE WORKFILE IN QTEMP                    */
/****************************************************************/
/*  GOTO SKIP   */
             CRTDUPOBJL  OBJ(PKEMCPP) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP) DATA(*NO)
             CRTDUPOBJL  OBJ(PKEMCPL0) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP) DATA(*NO)
             CRTDUPOBJL  OBJ(PKEMCPL1) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP) DATA(*NO)
             CRTDUPOBJL  OBJ(PKEMCPL2) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP) DATA(*NO)
             CRTDUPOBJL  OBJ(PKEMCPL3) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP) DATA(*NO)
 SKIP:
/****************************************************************/
/*  BUILD W001 PSE WORKFILE                                     */
/****************************************************************/
             CALL       PGM(PKRCXFR) PARM(' ' &CO &TATTO &DFROM &PEN +
                          &SRTYP &KSHIFT &DTO)
/****************************************************************/
/*  PRINT PSE                                                  */
/****************************************************************/

             IF         COND(&PRCTYPE *EQ 'P') THEN(DO)

/* CREATE PRINTER FILE OVERRIDE CODE                            */
             SBDPRTOVR  CMDVAR(&CMD) FILE(PKRBPFR$) OUTQ(&PDEV) +
                          COPIES(&COPIES) HOLD(&HOLD) SAVE(&SAVE)
             CALL       PGM(QCMDEXC) PARM(&CMD 512)

/* PRINT PGM                                                   */
             CALL       PGM(PKRBPFR) PARM(' ' &CO &TATTO &DFROM &PEN +
                          &SRTYP &KSHIFT &OMS)
 ENDPRINT:   ENDDO

FTP:
 /*------------------------------------------------*/
   /* RUN SQL TO PUSH document to /DOWNLOAD.PORK    */
 /*------------------------------------------------*/
             IF         COND(&PRCTYPE *EQ 'D') THEN(DO)

             ADDLIBLE LIB(SEQUEL) POSITION(*LAST)
             MONMSG MSGID(CPF2103)

   /*-----------------------------------------------------*/
   /* EXECUTE SEQUEL TO SELECT THE WORKF FILE AND FTP IT -*/
   /*-----------------------------------------------------*/

FILE:
             CHGVAR     VAR(&SEL) VALUE('SELECT EMB0DT COLHDG("KILL +
                          DATE"), EMCVNB COLHDG("TATTOO"), EMLIST +
                          COLHDG("KILL" "SHIFT"), EMCMCD +
                          COLHDG("PEN"), EMC2NB COLHDG("SEQ#"), +
                          EMKAST COLHDG("SRC") , EMC0NB +
                          COLHDG("PEN" "HEAD"), EMWSHD +
                          COLHDG("SHIFT" "HELD"), EMI5NB +
                          COLHDG("YARD" "DEAD"), EMB6DT +
                          COLHDG("SCALE" "WGT DT"), EMANTM +
                          COLHDG("SCALE" "WGT TM"), EMBXDT +
                          COLHDG("DRIVE" "DT"), EMAJTM +
                          COLHDG("DRIVE" "TM"), EMBBTM COLHDG("WAIT +
                          TM" "DIFF"), EMF7NB COLHDG("AVG LIVE" +
                          "WGT"), EMFWA1 COLHDG("FARM SITE"), EMCXNB +
                          COLHDG("DOA"), EMOXNB COLHDG("MOVEMENT"), +
                          EMJUCD COLHDG("GROUP"), +
                          EMBCTM COLHDG("LOAD TM" "DIFF"), +
                          EMBDTM COLHDG("TRAVEL TM" "DIFF"), +
                          EMJVCD COLHDG("LOAD TYPE") +
 FROM PKEMCPP WHERE EMTSST<>"S"  +
   ORDER BY EMB0DT,EMLIST,EMCMCD,EMCVNB,EMC2NB')

             EXECUTE    SQL(&SEL) MBROPT(*REPLACE) NBRRCDS(*ALL) +
                          KEYFLDCNT(*ALL) ALWNULL(*NO) +
                          TOFLR('GUY.IS') TODOC(&DOCUM) +
                          PCFMT(*XLS) REPLACE(*YES) +
                          TEXT('REST TIME RECEIVING') +
                          OPTIMIZE(*TOTAL) ALWCPY(*YES) MSG(*YES) +
                          UNIQUEKEY(*NONE) IGNDECERR(*NO) +
                          DTSTYLE(*FIELD '/' *FIELD ':')
 /*  PRIOR 9/9/2010 BY SLM                 */
 /*  FROM PKEMCPP WHERE EMCMCD<>"ST1"  +   */
   /*------------------------------------*/
   /* PUSH document to /DOWNLOAD.PORK    */
   /*------------------------------------*/
             CHGVAR VAR(&DOCUMENT) VALUE(&DOCUM)
             CHGVAR VAR(&PCFILE)   VALUE(&DOCUM *TCAT '.XLS')
             CALL PGM(FTPPRKSRVR) PARM(&DOCUMENT &PCFILE)
   /*------------------------------------*/
   ENDDO
             ENDPGM
