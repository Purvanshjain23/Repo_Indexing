     A*%%TS  SD  20050314  170829  ISLRAMS     REL-V5R2M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        TRIUMPH FOODS
     A*  DISPLAY FILE:  TFD4320
     A*  TITLE:         SPECIFY INVOICE CHARGES LISTING OPTIONS
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  DATE:          03/14/05
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/TFPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CF03(03 'EXIT')
     A                                      CF04(04 'PROMPT')
     A                                      CF08(08 'SUBMIT')
     A*
     A*----------------------------------------------------------------
     A* RECORD FORMAT ONE
     A*----------------------------------------------------------------
     A          R TF4320R1
     A*%%TS  SD  20050314  170829  ISLRAMS     REL-V5R2M0  5722-WDS
     A                                      WINDOW(TF4320EC)
     A                                      TEXT('INPUT SCREEN')
     A                                      SETOF(30)
     A                                      SETOF(31)
     A                                      SETOF(32)
     A                                      SETOF(33)
     A                                      SETOF(34)
     A                                      SETOF(35)
     A                                      SETOF(36)
     A                                      SETOF(37)
     A                                      SETOF(38)
     A                                      SETOF(39)
     A                                      SETOF(40)
     A                                      SETOF(50)
     A                                      SETOF(51)
     A                                      SETOF(53)
     A                                      SETOF(54)
     A                                      SETOF(55)
     A                                      SETOF(56)
     A                                      SETOF(57)
     A                                      SETOF(58)
     A                                      SETOF(59)
     A                                      OVERLAY
     A                                      RTNCSRLOC(&RCD &FLD)
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  1REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 14'SPECIFY INVOICE CHARGES LISTING OP-
     A                                      TIONS'
     A                                      DSPATR(HI)
     A                                  3  1'Include posted charges ...'
     A                                  3 28'?'
     A            D1YNFL    R        B  3 30REFFLD(@@FLAG)
     A  30
     AO 50                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A N30                                  DSPATR(HI)
     A                                  4  1'From invoice charge date .'
     A            D1FMDY    R        B  4 30REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A  32                                  DSPATR(PC)
     A  32                                  DSPATR(RI)
     A N32                                  DSPATR(HI)
     A                                  4 43'(Blank for all)'
     A                                  5  1'To invoice charge date ...'
     A            D1TMDY    R        B  5 30REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A  40                                  DSPATR(PC)
     A  40                                  DSPATR(RI)
     A N40                                  DSPATR(HI)
     A                                  5 43'(Blank for all)'
     A                                  6  1'Charge status code .......'
     A                                  6 28'?'
     A            D1INSTCD  R        B  6 30REFFLD(@@INSTCD)
     A  35
     AO 55                                  DSPATR(PC)
     A  35                                  DSPATR(RI)
     A N35                                  DSPATR(HI)
     A                                  6 43'(Blank for all)'
     A                                  7  1'Charge frequency code ....'
     A                                  7 28'?'
     A            D1CFQCD   R        B  7 30REFFLD(TFREF/@@CFQCD *LIBL/TFPREF)
     A  37
     AO 57                                  DSPATR(PC)
     A  37                                  DSPATR(RI)
     A N37                                  DSPATR(HI)
     A                                  7 43'(Blank for all)'
     A                                  8  1'User-controlled flag .....'
     A                                  8 28'?'
     A            D1UCFL    R        B  8 30REFFLD(@@UCFL)
     A  36
     AO 56                                  DSPATR(PC)
     A  36                                  DSPATR(RI)
     A N36                                  DSPATR(HI)
     A                                  8 43'(Blank for all)'
     A                                  9  1'System module code .......'
     A                                  9 28'?'
     A            D1SMCD    R        B  9 30REFFLD(TFREF/@@SMCD *LIBL/TFPREF)
     A  38
     AO 58                                  DSPATR(PC)
     A  38                                  DSPATR(RI)
     A N38                                  DSPATR(HI)
     A                                  9 43'(Blank for all)'
     A                                 10  1'Include in invoice total .'
     A                                 10 28'?'
     A            D1INFL    R        B 10 30REFFLD(TFREF/@@INFL *LIBL/TFPREF)
     A  39
     AO 59                                  DSPATR(PC)
     A  39                                  DSPATR(RI)
     A N39                                  DSPATR(HI)
     A                                 10 43'(Blank for all)'
     A                                 11  1'Charge code ..............'
     A                                 11 28'?'
     A            D1CCCD    R        B 11 30REFFLD(TFREF/@@CCCD *LIBL/TFPREF)
     A  33
     AO 53                                  DSPATR(PC)
     A  33                                  DSPATR(RI)
     A N33                                  DSPATR(HI)
     A                                 11 43'(Blank for all)'
     A                                 12  1'Invoice number ...........'
     A                                 12 28'?'
     A            D1INSN    R        B 12 30REFFLD(@@INSN)
     A                                      EDTCDE(M)
     A  34
     AO 54                                  DSPATR(PC)
     A  34                                  DSPATR(RI)
     A N34                                  DSPATR(HI)
     A                                 12 43'(Blank for all)'
     A                                 13  1'Invoice charge number ....'
     A                                 13 28'?'
     A            D1ICSN    R        B 13 30REFFLD(@@ICSN)
     A                                      EDTCDE(M)
     A  31
     AO 51                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A N31                                  DSPATR(HI)
     A                                 13 43'(Blank for all)'
     A*
     A*----------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*----------------------------------------------------------------
     A          R TF4320K1
     A*%%TS  SD  20000605  133618  ISLRAMS     REL-V4R2M0  5769-PW1
     A                                      WINDOW(TF4320EC)
     A                                      TEXT('COMMAND KEYS')
     A                                      OVERLAY
     A  43                             14  2'Message program failed. Call IS.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 15  2'F3=Exit'
     A                                 15 11'F4=Prompt'
     A                                 15 22'F8=Submit'
      *
      *----------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *----------------------------------------------------------------
     A          R TF4320ES
     A                                      TEXT('Error message subfile')
     A                                      SFL SFLMSGRCD(16)
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
     A*
     A*----------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT WITH WINDOW DEFINITION
     A*----------------------------------------------------------------
     A          R TF4320EC                  SFLCTL(TF4320ES)
     A*%%TS  SD  20000628  103649  ISLRAMS     REL-V4R2M0  5769-PW1
     A                                      TEXT('Error message subfile')
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A                                      WINDOW(*DFT 17 76)
     A                                      WDWBORDER((*DSPATR RI) (*CHAR '    -
     A                                          '))
     A            MSGPGM                    SFLPGMQ(10)
     A*
     A*----------------------------------------------------------------
     A* ASSUME FORMAT - REQUIRED FOR WINDOWS
     A*----------------------------------------------------------------
     A*
     A          R ASSUME
     A*%%TS  SD  20000627  095625  ISLRAMS     REL-V4R2M0  5769-PW1
     A                                      TEXT('ASSUME FORMAT FOR WINDOWS')
     A                                      ASSUME
     A                                      OVERLAY
     A                                 24 79' '
