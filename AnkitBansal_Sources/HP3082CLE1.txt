/***************************************************************** */
/*                                                                 */
/*  PROGRAM NUMBER:  HP3082CLE1 - Copied from HP3082CL             */
/*  PROGRAM NAME:    GL ACCOUNT/HPS EXPENSE CODE EXCEPTION REPORT  */
/*  PROGRAMMER:      BRAD BADEN                                    */
/*  CREATE DATE:      5/28/2020                                    */
/*                                                                 */
/*  FUNCTION:        THIS CL IS CALLED FROM HP3082S.  THIS IS THE  */
/*                   JDE ENTERPRISE 1 VERSION TO PRINT THE         */
/*                   EXCEPTION REPORT.                             */
/*                                                                 */
/*                   OPTIONS ARE PASSED THROUGH THE LDA.           */
/*                                                                 */
/*                   This is the JDE Enterprise 1 version of       */
/*                   HP3082CL.                                     */
/*                                                                 */
/*******************************************************************/
/*  MODIFIED:                                                      */
/*                                                                 */
/*                                                                 */
/*                                                                 */
/*                                                                 */
/*******************************************************************/

             PGM        PARM(&PPCOCD)

             DCL        VAR(&PPCOCD) TYPE(*CHAR) LEN(3)            /* E5290 */

/* DEFINE THE VARIABLE REQUIRED IN THE COMMAND TO OVERRIDE THE     */
/* PRINT FILE TO A LASER PRINTER                                   */

             DCL        VAR(&CMD) TYPE(*CHAR) LEN(512)
             DCL        VAR(&QDATA) TYPE(*CHAR) LEN(167)

             DCL        VAR(&JDECO) TYPE(*CHAR) LEN(5)
             DCL        VAR(&HOLD) TYPE(*CHAR) LEN(4)
             DCL        VAR(&SAVE) TYPE(*CHAR) LEN(4)
             DCL        VAR(&COPYS) TYPE(*DEC) LEN(2)
             DCL        VAR(&OUTQ) TYPE(*CHAR) LEN(10)

             DCL        VAR(&CC) TYPE(*DEC) LEN(2)
             DCL        VAR(&YY) TYPE(*DEC) LEN(2)

             CHGVAR     VAR(&CC) VALUE(%SST(*LDA 20 2))
             CHGVAR     VAR(&YY) VALUE(%SST(*LDA 22 2))


/*-------------------------------------------------------------------*/
/*  SET UP AND RUN OPEN QUERY OVER GL EXPENSE FILE                   */
/*-------------------------------------------------------------------*/
/* SELECTIONS:                                                       */
/*     COMPANY EQUAL TO 350   --> use parm company e5290             */
/*     2-DIGIT CENTURY OF CURRENTLY OPEN HPS ACCOUNTING PERIOD       */
/*     2-DIGIT YEAR OF CURRENTLY OPEN HPS ACCOUNTING PERIOD          */
/*     LEDGER TYPE OF AA                                             */
/*     OBJECT ACCOUNT IN THE RANGE OF 5999 THRU 9999                 */
/*     EXCEPT OMIT 'OFFSET' OBJECT ACCOUNTS 9630 AND 9642            */

             CHGVAR     VAR(&JDECO) VALUE('00' *CAT &PPCOCD)   /* E5290  */
             CHGVAR     VAR(&QDATA) VALUE(' ')

             CHGVAR     VAR(%SST(&QDATA 1 8)) VALUE('GBCO +
                          *EQ')
/*           CHGVAR     VAR(%SST(&QDATA 10 7)) VALUE('"00350"')   original */
             CHGVAR     VAR(%SST(&QDATA 10 7)) VALUE(&JDECO)    /* E5290 */

             CHGVAR     VAR(%SST(&QDATA 18 15)) VALUE('*AND GBCTRY +
                          *EQ')
             CHGVAR     VAR(%SST(&QDATA 34 2)) VALUE(&CC)

             CHGVAR     VAR(%SST(&QDATA 37 13)) VALUE('*AND GBFY +
                          *EQ')
             CHGVAR     VAR(%SST(&QDATA 51 2)) VALUE(&YY)

             CHGVAR     VAR(%SST(&QDATA 54 13)) VALUE('*AND GBLT +
                          *EQ')
             CHGVAR     VAR(%SST(&QDATA 68 4)) VALUE('"AA"')

             CHGVAR     VAR(%SST(&QDATA 73 23)) VALUE('*AND GBOBJ +
                          *GT "5999  "')

             CHGVAR     VAR(%SST(&QDATA 97 23)) VALUE('*AND GBOBJ +
                          *LE "9999  "')

             CHGVAR     VAR(%SST(&QDATA 121 23)) VALUE('*AND GBOBJ +
                          *NE "9630  "')

             CHGVAR     VAR(%SST(&QDATA 145 23)) VALUE('*AND GBOBJ +
                          *NE "9642  "')


/*           OVRDBF     FILE(F0902CB) SHARE(*YES)                    */
/*           OPNQRYF    FILE((F0902CB)) QRYSLT(&QDATA) KEYFLD(*FILE) +
                          SEQONLY(*YES)                              */

             OVRDBF     FILE(E10902CB) SHARE(*YES)
             OPNQRYF    FILE((E10902CB)) QRYSLT(&QDATA) KEYFLD(*FILE) +
                          SEQONLY(*YES)

/*-------------------------------------------------------------------*/
/* SET UP PRINT OVERRIDES                                            */
/*-------------------------------------------------------------------*/

 HOLD:       IF         COND(%SST(*LDA 411 1) = 'Y') THEN(CHGVAR +
                          VAR(&HOLD) VALUE(*YES))
             ELSE       CMD(CHGVAR VAR(&HOLD) VALUE(*NO))

 COPYS:      CHGVAR     VAR(&COPYS) VALUE(%SST(*LDA 412 1))
             IF         COND(&COPYS = 0) THEN(CHGVAR VAR(&COPYS) +
                          VALUE(1))

OUTQ:        CHGVAR     VAR(&OUTQ) VALUE(%SST(*LDA 413 10))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(%SST(*LDA 401 10)))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(*JOB))


/* AS OF MARCH 2004, USERS ASKED US TO ALWAYS PUT OUTPUT ON SAVE/HOLD */
/* FOR THE EOP RUNS                                                   */

             CHGVAR     VAR(&SAVE) VALUE(*YES)
             CHGVAR     VAR(&HOLD) VALUE(*YES)


/*------------------------------------------------------------------*/
/* OVERRIDE PRINT FILE                                              */
/*------------------------------------------------------------------*/

/* USE THE CUSTOMIZED SEABOARD COMMAND TO OVERRIDE THE PRINT FILE.  */
/* THIS COMMAND ALLOWS THE REPORT TO BE GENERATED ON A STANDARD LINE*/
/* PRINTER OR A LASER PRINTER. TO USE THIS OVERRIDE YOU MUST DECLARE*/
/* A COMMAND VARIABLE AND FOLLOW THE OVERRIDE WITH A CALL TO        */
/* QCMDEXC.                                                         */

             SBDPRTOVR  CMDVAR(&CMD) FILE(QPRINT) OUTQ(&OUTQ) +
                          WIDTH(132) LANDSCAPE(*PRTF) DRAWER(*PRTF) +
                          COPIES(&COPYS) HOLD(&HOLD) SAVE(&SAVE)

             CALL       PGM(QCMDEXC) PARM(&CMD 512)


/*-------------------------------------------------------------------*/
/* PRINT THE REPORT                                                  */
/*-------------------------------------------------------------------*/

/*           CLOF       OPNID(F0902CB)                               */
/*           CALL       PGM(HP6082)                                  */

             CALL       PGM(HP6082E1)
             CLOF       OPNID(E10902CB)

             DLTOVR     FILE(*ALL)

             ENDPGM

