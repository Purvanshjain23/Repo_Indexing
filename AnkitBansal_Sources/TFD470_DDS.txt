     A*%%TS  SD  20090915  162134  ISLRAMS     REL-V5R4M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        TRIUMPH FOODS
     A*  DISPLAY FILE:  TFD470
     A*  TITLE:         SPECIFY LISTING OF OMS CREDIT/DEBIT MEMOS
     A*  PROGRAMMER:    LEANNE RAMSEY
     A*  CREATED:       08/21/09
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/TFPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A                                      CF04(04 'PROMPT')
     A                                      CF08(08 'SUBMIT')
     A                                      CF17(17 'PRINT OPTIONS')
     A                                      CF20(20 'WRKSPLF')
     A*
     A*---------------------------------------------------------------
     A* RECORD FORMAT ONE
     A*---------------------------------------------------------------
     A          R TF470R1
     A*%%TS  SD  20090915  162134  ISLRAMS     REL-V5R4M0  5722-WDS
     A                                      SETOF(30)
     A                                      SETOF(31)
     A                                      SETOF(32)
     A                                      SETOF(33)
     A                                      SETOF(34)
     A                                      SETOF(35)
     A                                      SETOF(36)
     A                                      SETOF(37)
     A                                      SETOF(38)
     A                                      SETOF(39)
     A                                      SETOF(56)
     A                                      SETOF(58)
     A                                      SETOF(59)
     A                                      OVERLAY
     A                                      RTNCSRLOC(&RCD &FLD)
     A            RCD       R        H      REFFLD(@@RCD)
     A            FLD       R        H      REFFLD(@@FLD)
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 15'SPECIFY OPTIONS FOR LISTING OF OMS-
     A                                       CREDIT/DEBIT MEMOS'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USR)
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@JOB)
     A                                      DSPATR(HI)
     A                                  4  2'Report sort sequence .....'
     A            D1RPT     R        B  4 31REFFLD(TFREF/@@FLAG *LIBL/TFPREF)
     A  37                                  DSPATR(PC)
     A  37                                  DSPATR(RI)
     A N37                                  DSPATR(HI)
     A                                  4 43'(C=Claim, P=Product)'
     A                                  6  2'From date ................'
     A            D1FMDY    R        B  6 31REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A  30                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A N30                                  DSPATR(HI)
     A                                  6 65'(Blank for all)'
     A                                  7  2'To date ..................'
     A            D1TMDY    R        B  7 31REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A  31                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A N31                                  DSPATR(HI)
     A                                  7 65'(Blank for all)'
     A                                  8  2'Data source code .........'
     A                                  8 29'?'
     A            D1DSCD    R        B  8 31REFFLD(TFREF/@@DSCD *LIBL/TFPREF)
     A  36
     AO 56                                  DSPATR(PC)
     A  36                                  DSPATR(RI)
     A N36                                  DSPATR(HI)
     A            D1DSDS    R        O  8 43REFFLD(TFREF/@@DSDS *LIBL/TFPREF)
     A                                  8 65'(Blank for all)'
     A                                 10  2'Company number ...........'
     A            D1CONO    R        B 10 31REFFLD(@@CONO)
     A                                      EDTCDE(M)
     A  32                                  DSPATR(PC)
     A  32                                  DSPATR(RI)
     A N32                                  DSPATR(HI)
     A                                 10 65'(Blank for all)'
     A                                 11  2'Claim number .............'
     A            D1CLNO    R        B 11 31REFFLD(@@CLNO)
     A                                      EDTCDE(M)
     A  33                                  DSPATR(PC)
     A  33                                  DSPATR(RI)
     A N33                                  DSPATR(HI)
     A                                 11 65'(Blank for all)'
     A                                 12  2'Memo type ................'
     A            D1MMTY    R        B 12 31REFFLD(@@MMTY)
     A                                      DSPATR(HI)
     A                                 12 65'(Blank for all)'
     A                                 13  2'Credit/debit memo number .'
     A            D1MMNO    R        B 13 31REFFLD(@@MMNO)
     A                                      EDTCDE(M)
     A  35                                  DSPATR(PC)
     A  35                                  DSPATR(RI)
     A N35                                  DSPATR(HI)
     A                                 13 65'(Blank for all)'
     A                                 14  2'Claim order number .......'
     A            D1CLORNO  R        B 14 31REFFLD(TFREF/@@CLORNO *LIBL/TFPREF)
     A                                      EDTCDE(M)
     A  34                                  DSPATR(PC)
     A  34                                  DSPATR(RI)
     A N34                                  DSPATR(HI)
     A                                 14 65'(Blank for all)'
     A                                 16  2'Product code .............'
     A                                 16 29'?'
     A            D1PRCD    R        B 16 31REFFLD(TFREF/@@PRCD *LIBL/TFPREF)
     A                                      EDTCDE(M)
     A  38
     AO 58                                  DSPATR(PC)
     A  38                                  DSPATR(RI)
     A N38                                  DSPATR(HI)
     A            D1PRDS        20A  O 16 43
     A                                 16 65'(Blank for all)'
     A                                 18  2'A/R customer number ......'
     A                                 18 29'?'
     A            D1CUNO    R        B 18 31REFFLD(@BBREE7/BBANC7 *LIBL/CABBREP)
     A                                      EDTCDE(M)
     A  39
     AO 59                                  DSPATR(PC)
     A  39                                  DSPATR(RI)
     A N39                                  DSPATR(HI)
     A            D1CUNM        20A  O 18 43
     A                                 18 65'(Blank for all)'
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R TF470K1
     A*%%TS  SD  20040928  155440  ISLRAMS     REL-V5R2M0  5722-WDS
     A  43                             21  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A                                 23 11'F4=Prompt'
     A                                 23 22'F8=Submit'
     A                                 23 33'F17=Print Options'
     A                                 23 52'F20=Spool Files'
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *---------------------------------------------------------------
     A          R TF470ES                   SFL SFLMSGRCD(24)
      *
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
     A*---------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*---------------------------------------------------------------
     A*
     A          R TF470EC                   SFLCTL(TF470ES)
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
