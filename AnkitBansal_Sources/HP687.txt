      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      HOG PRODUCTION
      * PROGRAM:     HP687
      * TITLE:       LISTING OF PIG DAYS BY FARM SITE
      * PROGRAMMER:  MARY PALMER
      * CREATED:     8/30/94
      *
      * FUNCTION: THIS PROGRAM IS CALLED FROM HP487.    THE USER'S
      *           SELECTIONS ARE PASSED IN THE LDA.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 12/12/01  LeAnne Fedor
      *           Changed to use a workfile that already has pig days in it.
      *
      * 04/22/10  LeAnne Ramsey
      *           Changed to use the standard IBM print file PRINT80 instead of QPRINT.
      *
      * 10/16/13  LeAnne Ramsey (E2831)
      *           Recompile only. Added field 'MTech Reference'.
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     FHSP018    IF   E           K disk
      *    FARM SITE
      *
     Fhsp309    if   e           k disk
      *    Workfile: Pig days
      *
      *
     Fprint80   O    f  080        printer oflind(*inof)
      *
      /EJECT
      ****************************************************************
      * INPUT SPECS
      ****************************************************************
      *
      *---------------------------------------------------------------
      *  LOCAL DATA AREA
      *---------------------------------------------------------------
      *
     D                UDS
     D  LDFSBO                 1      5
     D  LDFDTE                 6     13  0
     D  LDTDTE                14     21  0
     D  LDFSCD                22     26  0
     D  LDFSNM                27     51
     D  LDBODS                52     81
     D  LDFMDY                82     87  0
     D  LDTMDY                88     93  0
     D  LDPPCD                94     98
     D  LDPPDS                99    128
      *
      *
      *---------------------------------------------------------------
      * STANDARD PROGRAM STATUS DATA STRUCTURE
      *---------------------------------------------------------------
      *    EXTERNALLY DEFINED AS UTPGFR (RECORD FORMAT: PGMDSR)
     D PGMDS         ESDS                  EXTNAME(UTPGFR)
      *
      *----------------------------------------------------------------
      * DATE FORMATS
      *----------------------------------------------------------------
      * DATE IN MMDDYY FORMAT
      *
     D                 DS
     D  WKMDY                  1      6  0
     D  WKMM                   1      2  0
     D  WKDD                   3      4  0
     D  WKYY                   5      6  0
      *
      * DATE IN CCYYMMDD FORMAT
      *
     D                 DS
     D  WKCYMD                 1      8  0
     D  WKCEN                  1      2  0
     D  WKYER                  3      4  0
     D  WKMON                  5      6  0
     D  WKDAY                  7      8  0
      *
      *---------------------------------------------------------------
      *  NAMED CONSTANTS
      *---------------------------------------------------------------
      *
     D YES             C                   CONST('Y')
     D NO              C                   CONST('N')
     D UNKNWN          C                   CONST('UNKNOWN')
     D ALL             C                   CONST('ALL')
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * MAIN LINE
      ****************************************************************
      *
      *
     C     *IN92         DOUEQ     *ON                                          MAIN DO
     C                   READ      HSP309                                 92
     C     *IN92         IFEQ      *OFF                                         IF NOT EOF
      *
     C     FIRST         IFEQ      YES
     C                   EXSR      $FSBRK
     C                   MOVEL     NO            FIRST
     C                   ENDIF
      *
      * IF BREAK ON FARM SITE
      *
     C     WPFSCD        IFNE      SVFSCD                                       IF NEW FARM
     C                   EXCEPT    TFTOT
     C                   EXSR      $FSBRK
     C                   ENDIF                                                  IF NEW FARM
      *
      *
      * MOVE PIG DAYS TO DETAIL LINE AND ACCUMULATE TOTALS
      *
     C                   Z-ADD     WPPIGDAY      DTOPDY
     C                   ADD       WPPIGDAY      TFOPDY
     C                   ADD       WPPIGDAY      TROPDY
      * FLIP OPEN DATE
      *
     C                   Z-ADD     WPOPDT        WKCYMD
     C                   EXSR      $TOMDY
     C                   Z-ADD     WKMDY         DTOPDT
      *
      * FLIP DISPOSED DATE
      *
     C                   Z-ADD     WPDSDT        WKCYMD
     C                   EXSR      $TOMDY
     C                   Z-ADD     WKMDY         DTDSDT
      *
      * PRINT DETAIL LINE
      *
     C     *INOF         IFEQ      *ON
     C                   EXCEPT    HRHDR
     C                   EXCEPT    HFHDR
     C                   ENDIF
     C                   EXCEPT    DTLINE
      *
     C                   ENDIF                                                  IF NOT EOF
     C                   ENDDO                                                  MAIN DO
      *
      *---------------------------------------------------------------
      * EOF PROCESSING
      *---------------------------------------------------------------
      * PRINT STANDARD 'NO DATA...' ERROR MESSAGE IF NO DATA MET THE
      * USER'S SELECTION CRITERIA
      *
     C     FIRST         IFEQ      YES                                          IF NOTHING
     C                   EXCEPT    NODATA
     C                   ELSE
      *
      * PRINT TOTALS
      *
     C     *INOF         IFEQ      *ON
     C                   EXCEPT    HRHDR
     C                   EXCEPT    HFHDR
     C                   ENDIF
     C                   EXCEPT    TFTOT
      *
     C     *INOF         IFEQ      *ON
     C                   EXCEPT    HRHDR
     C                   EXCEPT    HFHDR
     C                   ENDIF
     C                   EXCEPT    TRTOT
     C                   ENDIF                                                  IF NOTHING
      *
      * CALL UTILITY PROGRAM TO RECLAIM RESOURCES--CLOSE FILES
      * AND CLEANUP AFTER THE GENERIC PROGRAMS THAT DO NOT SETON
      * THE LAST RECORD INDICATOR.
      *
     C                   CALL      'UTRCRSC'
     C                   SETON                                        LR
      /EJECT
      *---------------------------------------------------------------
      * BREAK ON FARM SITE
      *---------------------------------------------------------------
      *
     C     $FSBRK        BEGSR
      *
     C     WPFSCD        CHAIN     HSP018                             93
     C     *IN93         IFEQ      *OFF                                         IF HIT
     C                   MOVEL     FSFSNM        HFFSNM
     C                   ELSE
     C                   MOVEL(P)  UNKNWN        HFFSNM
     C                   ENDIF                                                  IF HIT
      *
     C                   Z-ADD     WPFSCD        SVFSCD
      *
     C     *INOF         IFEQ      *ON
     C                   EXCEPT    HRHDR
     C                   ENDIF
     C                   EXCEPT    HFHDR
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      *  FLIP DATE TO MMDDYY FORMAT
      *---------------------------------------------------------------
      *
     C     $TOMDY        BEGSR
      *
     C                   Z-ADD     WKMON         WKMM
     C                   Z-ADD     WKDAY         WKDD
     C                   Z-ADD     WKYER         WKYY
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * INITIALIZATION SUBROUTINE
      *---------------------------------------------------------------
      *
     C     *INZSR        BEGSR
      *
      * DEFINE PRINT FIELDS FOR REPORT HEADING AND REPORT TOTAL LINES
      *
     C                   Z-ADD     0             TROPDY           15 0
      *
      * DEFINE PRINT FIELDS FOR DETAIL LINE
      *
     C     *LIKE         DEFINE    WPPIGDAY      DTOPDY
     C                   Z-ADD     0             DTOPDT            6 0
     C                   Z-ADD     0             DTDSDT            6 0
      *
      * DEFINE PRINT FIELDS FOR FARM HEADING AND FARM TOTAL LINES
      *
     C     *LIKE         DEFINE    FSFSNM        HFFSNM
     C                   Z-ADD     0             TFOPDY           15 0
      *
      *--------------------------------------------------------------
      * SET UP REPORT HEADINGS
      *--------------------------------------------------------------
      *
      * ALL BUSINESS OFFICES
      *
     C     LDFSBO        IFEQ      *BLANK
     C                   MOVEL     'ALL'         LDFSBO
     C                   ENDIF
      * ALL FARMS
     C     LDFSCD        IFEQ      0
     C                   SETON                                        89
     C                   ENDIF
      *
      * ALL PRODUCTION PHASES
      *
     C     LDPPCD        IFEQ      *BLANK
     C                   MOVEL     'ALL  '       LDPPCD
     C                   ENDIF
      *
      * DEFINE SAVE FIELDS FOR CONTROL BREAKS
      *
     C     *LIKE         DEFINE    FSFSCD        SVFSCD
      *
      * SET UP FIRST TIME FLAG
      * SET UP DASHED LINE FOR UNDERLINING THE HEADINGS
      * RETRIEVE TIME FOR REPORT HEADING
      *
     C                   MOVEL     YES           FIRST             1
     C                   MOVE      *ALL'-'       DASH             80
     C                   TIME                    RTIME             6 0
      *
      * PRINT REPORT HEADING FOR FIRST TIME
      *
     C                   EXCEPT    HRHDR
      *
     C                   ENDSR
      /EJECT
      *-------------------------------------------------------------
      * REPORT HEADING LINES
      *-------------------------------------------------------------
      *
     Oprint80   E            HRHDR          1 01
     O                       SDPGM               10
     O                                           50 'HOG PRODUCTION SYSTEM'
     O                                           70 'DATE'
     O                       UDATE         Y     80
      *
     O          E            HRHDR          1
     O                       SDUSR               10
     O                                           45 'LISTING OF PIG DAYS B'
     O                                           56 'Y FARM SITE'
     O                                           70 'TIME'
     O                       RTIME               80 '  :  :  '
      *
     O          E            HRHDR          1
     O                                           70 'PAGE'
     O                       PAGE          Z     75
      *
     O          E            HRHDR          1
     O                                           16 'BUSINESS OFFICE '
     O                                           22 '.....'
     O                       LDFSBO              29
     O                       LDBODS              63
      *
     O          E            HRHDR          1
     O                                           13 'FROM DATE ...'
     O                                           22 '.........'
     O                       LDFMDY        Y     32
      *
     O          E            HRHDR          1
     O                                           13 'TO DATE .....'
     O                                           22 '.........'
     O                       LDTMDY        Y     32
      *
      *
     O          E            HRHDR          1
     O                                           16 'PRODUCTION PHASE'
     O                                           22 '.....'
     O                       LDPPCD              29
     O                       LDPPDS              63
      *
     O          E            HRHDR          2
     O                                           22 'FARM SITE ............'
     O              N89      LDFSCD        M     30
     O               89                          27 'ALL'
     O              N89      LDFSNM              58
      *
     O          E            HRHDR          1
     O                                           10 'HOG GROUP'
     O                                           20 'STATUS'
     O                                           36 'DATE OPENED'
     O                                           53 'DATE DISPOSED'
     O                                           75 'PIG DAYS'
      *
     O          E            HRHDR          0
     O                       DASH                80
      *
      *-------------------------------------------------------------
      * FARM HEADING LINE
      *-------------------------------------------------------------
      *
     O          E            HFHDR       1  1
     O                       SVFSCD        M      6
     O                       HFFSNM              32
      *
      *-------------------------------------------------------------
      * DETAIL LINE
      *-------------------------------------------------------------
      *
     O          E            DTLINE      1
     O                       WPHGCD               9
     O                       WPGSCD              18
     O                       DTOPDT         B    34 '  /  /  -'
     O                       DTDSDT         B    50 '  /  /  -'
     O                       DTOPDY        KB    75
      *
      *-------------------------------------------------------------
      * FARM SITE TOTAL
      *-------------------------------------------------------------
     O          E            TFTOT       2  1
     O                       SVFSCD        M     43
     O                                           53 'SITE TOTAL'
     O                       TFOPDY        KB    75
      *
      *-------------------------------------------------------------
      * REPORT TOTAL LINE
      *-------------------------------------------------------------
     O          E            TRTOT       1  2
     O                                           53 'REPORT TOTAL'
     O                       TROPDY        K     75
      *
      *-------------------------------------------------------------
      * NO DATA MESSAGE LINE
      *-------------------------------------------------------------
      *
     O          E            NODATA      1
     O                                           19 'NO DATA MEETS YOUR'
     O                                           38 'SELECTION CRITERIA'
