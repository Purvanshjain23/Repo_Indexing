/***************************************************************** */
/*                                                                 */
/*  SYSTEM:          TRIUMPH FOODS                                 */
/*  PROGRAM NUMBER:  TF496CL                                       */
/*  PROGRAM NAME:    WORK WITH BASE MARKETING FEES                 */
/*  PROGRAMMER:      LEANNE FEDOR                                  */
/*  CREATE DATE:     12/16/04                                      */
/*                                                                 */
/*  PRINTS LISTING(S) FROM WORK WITH.                              */
/*                                                                 */
/*******************************************************************/
/* MODIFICATIONS:                                                  */
/*                                                                 */
/* 05/06/09  LEANNE RAMSEY                                         */
/*           CHANGED PRINT LOGIC TO MATCH MEAT COSTING.            */
/*******************************************************************/

             PGM

             DCL        VAR(&QDATA) TYPE(*CHAR) LEN(20)

             DCL        VAR(&OUTQ) TYPE(*CHAR) LEN(10)
             DCL        VAR(&HOLD) TYPE(*CHAR) LEN(4)
             DCL        VAR(&SAVE) TYPE(*CHAR) LEN(4)
             DCL        VAR(&COPY) TYPE(*DEC)  LEN(1)

             DCL        VAR(&LDYRNO) TYPE(*DEC) LEN(3)

             CHGVAR     VAR(&LDYRNO) VALUE(%SST(*LDA 1 3))

/*----------------------------------------------------------------*/
/* SET PARAMETERS FROM LDA FOR LISTINGS                           */
/*----------------------------------------------------------------*/

 OUTQ:       CHGVAR     VAR(&OUTQ) VALUE(%SST(*LDA 401 10))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(*JOB))

 HOLD:       CHGVAR     VAR(&HOLD) VALUE(%SST(*LDA 411 4))
             IF         COND(&HOLD *NE '*YES' *AND &HOLD *NE '*NO ') +
                          THEN(CHGVAR VAR(&HOLD) VALUE(*NO))

 SAVE:       CHGVAR     VAR(&SAVE) VALUE(%SST(*LDA 415 4))
             IF         COND(&SAVE *NE '*YES' *AND &SAVE *NE '*NO ') +
                          THEN(CHGVAR VAR(&SAVE) VALUE(*NO))

 COPY:       CHGVAR     VAR(&COPY) VALUE(%SST(*LDA 419 1))
             IF         COND(&COPY = 0) THEN(CHGVAR VAR(&COPY) +
                          VALUE(1))

/*---------------------------------------------------------------------*/
/* SET UP PRINT OVERRIDES                                              */
/*---------------------------------------------------------------------*/

             OVRPRTF    FILE(PRINT132) OUTQ(&OUTQ) COPIES(&COPY) +
                          HOLD(&HOLD) SAVE(&SAVE)

/*---------------------------------------------------------------------*/
/* PRINTING ALL RECORDS                                                */
/*---------------------------------------------------------------------*/

             IF         COND(&LDYRNO *EQ 0) THEN(DO)
             CALL       PGM(TF696)
             ENDDO

/*---------------------------------------------------------------------*/
/* PRINTING A SINGLE RECORD                                            */
/*---------------------------------------------------------------------*/
             IF         COND(&LDYRNO *NE 0) THEN(DO)

             CHGVAR     VAR(&QDATA) VALUE(' ')
             CHGVAR     VAR(%SST(&QDATA 1 10)) VALUE('BMYRNO *EQ')
             CHGVAR     VAR(%SST(&QDATA 12 3)) VALUE(&LDYRNO)

             OVRDBF     FILE(TFP003) SHARE(*YES)
             OPNQRYF    FILE((TFP003)) OPTION(*INP) QRYSLT(&QDATA) +
                          KEYFLD(*FILE) SEQONLY(*YES)
             CALL       PGM(TF696)
             CLOF       OPNID(TFP003)
             ENDDO

             DLTOVR     FILE(*ALL)

             ENDPGM

