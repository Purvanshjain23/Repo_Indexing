     A*%%TS  SD  20140527  130540  ISLRAMS     REL-V6R1M0  5761-WDS
     A*****************************************************************
     A*
     A*  SYSTEM:        TRIUMPH FOODS
     A*  DISPLAY FILE:  TFD404
     A*  TITLE:         WORK WITH CASH DISTRIBUTION BALANCE ADJUSTMENTS
     A*  PROGRAMMER:    LEANNE RAMSEY
     A*  CREATED:       10/30/06
     A*
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/TFPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A                                      CF04(04 'PROMPT')
     A                                      CF05(05 'REFRESH')
     A                                      CF06(06 'CREATE')
     A                                      CF07(07 'LISTING')
     A                                      CF13(13 'FOLD/DROP')
     A                                      CF20(20 'WRKSPLF')
     A*
     A*
     A*---------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*---------------------------------------------------------------
     A          R TF404S1                   SFL
     A*%%TS  SD  20080602  125306  ISLRAMS     REL-V5R4M0  5722-WDS
     A  85                                  SFLNXTCHG
     A            HDRRN1         4Y 0H
     A            D1WEMDY   R        O  8  4REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  ')
     A            D1AJMDY   R        O  8 16REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  ')
     A            D1RCEXFL  R        O  8 29REFFLD(TFREF/@@RCEXFL *LIBL/TFPREF)
     A            D1SCDBAAM R        O  8 33REFFLD(TFREF/@@SCDBAAM)
     A                                      EDTCDE(K)
     A            D1TCDBAAM R        O  8 50REFFLD(TFREF/@@TCDBAAM)
     A                                      EDTCDE(K)
     A            D1AJSN    R        O  8 69REFFLD(TFREF/@@AJSN *LIBL/TFPREF)
     A                                      EDTCDE(M)
     A                                  9 21'Reason:'
     A                                      COLOR(PNK)
     A            D1AJRSN   R        O  9 29REFFLD(@@AJRSN)
     A                                      COLOR(PNK)
     A*
     A*---------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*---------------------------------------------------------------
     A          R TF404C1                   SFLCTL(TF404S1)
     A*%%TS  SD  20080602  113601  ISLRAMS     REL-V5R4M0  5722-WDS
     A                                      SFLSIZ(0050)
     A                                      SFLPAG(0006)
     A N82                                  ROLLUP(25 'PAGE FORWARD')
     A                                      SETOF(50)
     A                                      RTNCSRLOC(&RCD &FLD)
     A                                      OVERLAY
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A  78                                  SFLFOLD(CF13)
     A  79                                  SFLDROP(CF13)
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            FOLD           1A  H
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 17'WORK WITH CASH DISTRIBUTION BALANC-
     A                                      E ADJUSTMENTS'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USR)
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@JOB)
     A                                      DSPATR(HI)
     A                                  3 29'?'
     A                                      DSPATR(HI)
     A                                  4  4'Revenue'
     A                                      DSPATR(HI)
     A                                  4 27'Record'
     A                                      DSPATR(HI)
     A                                  4 39'Seaboard'
     A                                      DSPATR(HI)
     A                                  4 57'Triumph'
     A                                      DSPATR(HI)
     A                                  4 69'System-'
     A                                      DSPATR(HI)
     A                                  5  2'Week-Ending'
     A                                      DSPATR(HI)
     A                                  5 15'Adjustment'
     A                                      DSPATR(HI)
     A                                  5 27'Exempt'
     A                                      DSPATR(HI)
     A                                  5 37'Adjustment'
     A                                      DSPATR(HI)
     A                                  5 54'Adjustment'
     A                                      DSPATR(HI)
     A                                  5 68'Assigned'
     A                                      DSPATR(HI)
     A                                  6  6'Date'
     A                                      DSPATR(HI)
     A                                  6 18'Date'
     A                                      DSPATR(HI)
     A                                  6 28'Flag'
     A                                      DSPATR(HI)
     A                                  6 41'Amount'
     A                                      DSPATR(HI)
     A                                  6 58'Amount'
     A                                      DSPATR(HI)
     A                                  6 70'Number'
     A                                      DSPATR(HI)
     A            C1WEMDY   R        B  7  4REFFLD(TFREF/@@DT06 *LIBL/TFPREF)
     A                                      EDTWRD('  /  /  ')
     A                                      DSPATR(HI)
     A            C1AJMDY   R        B  7 16REFFLD(TFREF/@@DT06 *LIBL/TFPREF)
     A                                      EDTWRD('  /  /  ')
     A                                      DSPATR(HI)
     A            C1RCEXFL  R        B  7 29REFFLD(TFREF/@@RCEXFL *LIBL/TFPREF)
     A                                      DSPATR(HI)
     A  50                                  DSPATR(PC)
     A            C1AJSN    R        B  7 69REFFLD(TFREF/@@AJSN *LIBL/TFPREF)
     A                                      EDTCDE(M)
     A                                      DSPATR(HI)
     A*
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R TF404K1
     A*%%TS  SD  20061031  140529  ISLRAMS     REL-V5R3M0  5722-WDS
     A  43                             21  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 22  2'F3=Exit'
     A                                 22 11'F4=Prompt'
     A                                 22 22'F5=Refresh'
     A                                 22 34'F6=Create'
     A                                 22 45'F7=Listing'
     A                                 22 57'F13=Fold/Unfold'
     A                                 23 57'F20=Spool Files'
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *---------------------------------------------------------------
     A          R TF404ES                   SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE CONTROL FORMAT
      *---------------------------------------------------------------
     A          R TF404EC                   SFLCTL(TF404ES)
      *
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
