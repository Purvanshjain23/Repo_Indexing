      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Hog Production System
      * PROGRAM:     HP6013
      * TITLE:       Listing of Load Crews
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     12/23/02
      *
      * FUNCTION:  Batch program to list the records in the file.
      *            This listing is submitted from HP4013-Work With Load Crews
      *
      *            The listing 'selections' will be the positioner/selector values
      *            in the control record at the time the user submitted the listing.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 10/15/13  LeAnne Ramsey (E2831)
      *           Recompile only. Added field 'MTech Reference'.
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fhsp108    if   e           k disk
      *    Load crews
      *
      *
     Fqprint    o    f   80        printer oflind(*inof)
      *
      /EJECT
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Standard fields
      *
     D prtfl           s              1    inz('N')
     D dash            s             80
     d rtime           s              6  0
      *
      * Workfields
      *
     D wkshift         s              1
      *
      *
      * Print fields
      *
     D h1crew          s             75
     D h1status        s             75
     D h1office        s             75
     D h1shift         s             75
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *---------------------------------------------------------------
      * Local data area.
      *---------------------------------------------------------------
      *
     Dlda             uds                  dtaara(*lda)
     D  ldcmcd                 1      5
     D  ldaist                 6      6
     D  ldtocd                 7     11
     D  ldshift               12     12  0
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * MAINLINE
      ****************************************************************
      *
      * Prepare to read the file and print headings the first
      * time through.
      *
     C     ldcmcd        setll     hsp108
     C                   except    h1hdr
      *
      * Read all records in the file; process those that match LDA values.
      *
     C                   dou       *in90 = *on                                  Main do
     C                   read      hsp108                                 90
     C                   if        *in90 = *off and                             If not EOF
     C                             (ldaist = ' ' or ldaist = lcaist) and
     C                             (ldtocd = ' ' or ldtocd = lctocd) and
     C                             (ldshift = 0 or ldshift = lcshift)
      *
      * Print detail line
      *
     C                   if        *inof = *on
     C                   except    h1hdr
     C                   endif
      *
     C                   except    l1dtl
     C                   movel     yes           prtfl
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Main do
      *
      *---------------------------------------------------------------
      * EOF processing
      *---------------------------------------------------------------
      * If the file was empty, print the standard 'no data...'
      * error message
      *
     C                   if        prtfl = no
     C                   except    nodata
     C                   endif
      *
     C                   seton                                        lr
      /EJECT
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Set up a line of underscores and retrieve time for report heading.
      *
     C                   movel     *all'-'       dash
     C                   time                    rtime
      *
      * Set up headings for user selections:
      *
     C                   if        ldcmcd = *blank
     C                   eval      h1crew = 'Listing begins with the ' +
     C                                        'first load crew'
     C                   else
     C                   eval      h1crew = 'Listing begins with load ' +
     C                                        'crew ' +
     C                                        ldcmcd
     C                   endif
      *
      * Active/inactive status
      *
     C                   select
     C                   when      ldaist = *blank
     C                   eval      h1status = 'Listing includes both Active ' +
     C                                        'and Inactive load crews'
     C                   when      ldaist = 'A'
     C                   eval      h1status = 'Listing includes only Active ' +
     C                                        'load crews'
     C                   when      ldaist = 'I'
     C                   eval      h1status = 'Listing includes only ' +
     C                                        'Inactive load crews'
     C                   endsl
      *
      *
      * Transportation office
      *
     C                   select
     C                   when      ldtocd = *blank
     C                   eval      h1office = 'Listing includes load crews' +
     C                                        ' from all Transportation Offices'
     C                   other
     C                   eval      h1office = 'Listing only includes load ' +
     C                                        'crews from Transportation ' +
     C                                        'Office ' +
     C                                        ldtocd
     C                   endsl
      *
      *
      * Shift
     C                   select
     C                   when      ldshift = 0
     C                   eval      h1shift = 'Listing includes all shifts'
     C                   other
     C                   move      ldshift       wkshift
     C                   eval      h1shift = 'Listing only includes shift ' +
     C                                        wkshift
     C                   endsl
      *
     C                   endsr
      /EJECT
      *-------------------------------------------------------------
      * Report heading lines
      *-------------------------------------------------------------
      *
     Oqprint    e            h1hdr          1 01
     O                       sdpgm               10
     O                                           52 'HOG PRODUCTION SYSTEM'
     O                                           70 'DATE'
     O                       udate         y     80
      *
     O          e            h1hdr          1
     O                       sdusr               10
     O                                           52 'LISTING OF LOAD CREWS'
     O                                           70 'TIME'
     O                       rtime               80 '  :  :  '
      *
     O          e            h1hdr          2
     O                                           70 'PAGE'
     O                       page          z     80
      *
      *
     O          e            h1hdr          1
     O                       h1crew              75
      *
     O          e            h1hdr          1
     O                       h1status            75
      *
     O          e            h1hdr          1
     O                       h1office            75
      *
     O          e            h1hdr          2
     O                       h1shift             75
      *
      *
     O          e            h1hdr          1
     O                                            4 'LOAD'
     O                                           45 'ACTIVE/'
     O                                           65 'TRANSPORTATION'
      *
     O          e            h1hdr          1
     O                                            4 'CREW'
     O                                           45 'INACTIVE'
     O                                           61 'OFFICE'
     O                                           80 'DEFAULT'
     O          e            h1hdr          1
     O                                            4 'CODE'
     O                                           21 'DESCRIPTION'
     O                                           45 'STATUS'
     O                                           60 'CODE'
     O                                           79 'SHIFT'
      *
     O          e            h1hdr          1
     O                       dash                80
      *
      *-------------------------------------------------------------
      * Detail line
      *-------------------------------------------------------------
      *
     O          e            l1dtl       1
     O                       lclccd               5
     O                       lclcds              35
     O                       lcaist              42
     O                       lctocd              61
     O                       lcshift       z     76
      *
      *-------------------------------------------------------------
      * No data message line
      *-------------------------------------------------------------
      *
     O          e            nodata      1
     O                                           19 'NO DATA MEETS YOUR'
     O                                           38 'SELECTION CRITERIA'
