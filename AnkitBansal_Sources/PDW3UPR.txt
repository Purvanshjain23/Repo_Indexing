/********************************************************************/
/*                                                                  */
/*  SYNOPSIS -  JOB CONTROL PROMPT FOR POSTING OR CANCELING A       */
/*              PHYSICAL COUNT.                                     */
/*              PASSES COMPANY, WAREHOUSE, CYCLE CODE  AND DATE.    */
/*                                                                  */
/*  SYSTEM    - INVENTORY MANAGEMENT                                */
/*  DATE      - 08/18/95                                            */
/*                                                                  */
/*      PARAMETERS:                                                 */
/*                                                                  */
/*     O    &RETURN     - RETURN CODE                               */
/*     I    &APPLIC     - APPLICATION CODE                          */
/*     I    &PGM        - CL PROGRAM TO PROCESS                     */
/*     B    &COMPNYN    - COMPANY NUMBER (PASSED)                   */
/*     B    &WHSE       - WAREHOUSE      (PASSED)                   */
/*     B    &CYCLE      - CYCLE          (PASSED)                   */
/*     B    &DATE       - DATE           (PASSED)                   */
/*     I    &JOBTYPE    - 0=BATCH, 1=INTERACTIVE                    */
/*                                                                  */
/********************************************************************/
/*  REVISIONS                                                       */
/*  E3191 JJH 07/17/14 ADD JOB TYPE PARM TO PROCESS INTERACTIVE OR  */
/*                     BATCH.                                       */
/*                     SEND EMAIL TO DISTRIBUTION LIST FOR COMPANY  */
/*                     AND WAREHOUSE PROCESSED.                     */
/********************************************************************/
 PDW3UPR:    PGM        PARM(&RETURN &APPLIC &PGM &COMPNYN &DATE +
                             &WHSE &CYCLE &JOBTYPE)
/* PARMS                                                                      */
        DCL    &RETURN     *CHAR   (7)           /* Return Code               */
        DCL    &APPLIC     *CHAR   (06)          /* Application Code          */
        DCL    &PGM        *CHAR   (10)          /* CL to Submit              */
        DCL    &COMPNYN    *DEC                  /* Company Number (Passed)   */
        DCL    &DATE       *DEC                  /* TRANS DATE                */
        DCL    &WHSE       *CHAR   (3)           /* WAREHOUSE                 */
        DCL    &CYCLE      *CHAR   (3)           /* CYCLE                     */
        DCL    &JOBTYPE    *CHAR   (1)           /* JOB TYPE                  */

/* ERROR MESSAGE FIELDS                                                       */
        DCL    &MSG        *CHAR   (512)         /* message                   */
        DCL    &MSGID      *CHAR   (7)           /* message id                */
        DCL    &MSGL       *DEC    (5 0)

/********************************************************************/
/*   INITIALIZE VARIABLES                                           */
/********************************************************************/

             CHGVAR     VAR(&RETURN) VALUE('       ')
/********************************************************************/
/*   MONITOR FOR ALL MESSAGES AT PROGRAM LEVEL                      */
/********************************************************************/

             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(MSGTRAP))

/********************************************************************/
/*   SUBMIT OR CALL CLP PROGRAM                                     */
/********************************************************************/
/* E3191 JJH 07/17/14 - RUN JOB IN BATCH OR INTERACTIVE             */
             IF         COND(&JOBTYPE *EQ '0') THEN(DO)
             SBMJOB     CMD(CALL PGM(&PGM) PARM(&COMPNYN &DATE +
                          &WHSE &CYCLE)) JOB(&PGM)
             GOTO       CMDLBL(END)
             ENDDO

             IF         COND(&JOBTYPE *EQ '1') THEN(DO)
             CALL       PGM(&PGM) PARM(&COMPNYN &DATE &WHSE &CYCLE)
             GOTO       CMDLBL(END)
             ENDDO
/*----------------------RECEIVE MESSAGES----------------------------*/

 MSGTRAP:    RCVMSG     MSGTYPE(*EXCP) MSGDTA(&MSG) MSGDTALEN(&MSGL) +
                          MSGID(&MSGID)
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(END))

             SNDPGMMSG  MSGID(&MSGID) MSGF(QCPFMSG) MSGDTA(%SST(&MSG +
                          1 &MSGL)) MSGTYPE(*ESCAPE)
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(END))

 END:        ENDPGM

