     A*%%TS  SD  20090910  152239  ISLRAMS     REL-V5R4M0  5722-WDS
     A*****************************************************************
     A*
     A*  SYSTEM:        TF FEES & PAYMENTS
     A*  DISPLAY FILE:  TFD533 - SELECT A/R CUSTOMER
     A*  CREATED BY:    LEANNE RAMSEY
     A*  DATE:          08/27/09
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/TFPREF)
     A                                      PRINT
     A                                      CA12(12 'PREVIOUS')
     A N82                                  ROLLUP(25 'PAGE FORWARD')
     A*---------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*---------------------------------------------------------------
     A          R TF533S1                   SFL
     A*%%TS  SD  20090827  140425  ISLRAMS     REL-V5R4M0  5722-WDS
     A            HDRRN1    R        H      REFFLD(@@RRN)
     A            D1CUNO    R        O  8  2REFFLD(@BBREE7/BBANC7 *LIBL/CABBREP)
     A                                      EDTCDE(M)
     A            D1CUNM    R        O  8 11REFFLD(@BBREE7/BBAYTX *LIBL/CABBREP)
     A            D1STAT    R        O  8 44REFFLD(@BBREE7/BBVSST *LIBL/CABBREP)
     A            D1CITY    R        O  8 50REFFLD(@BBREE7/BBAJNA *LIBL/CABBREP)
     A            D1STATE   R        O  8 72REFFLD(@BBREE7/BBBGCD *LIBL/CABBREP)
     A*
     A*---------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*---------------------------------------------------------------
     A          R TF533C1                   SFLCTL(TF533S1)
     A*%%TS  SD  20090910  152239  ISLRAMS     REL-V5R4M0  5722-WDS
     A                                      SFLSIZ(0100)
     A                                      SFLPAG(0009)
     A                                      WINDOW(TF533EC)
     A                                      OVERLAY
     A                                      SFLCSRRRN(&HDRRN)
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A            HDPAG1    R        H      REFFLD(@@PAG)
     A                                      SFLRCDNBR
     A            HDRRN     R        H      REFFLD(@@RRN)
     A            SDPGM     R        O  1  1REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 26'SELECT A/R CUSTOMER'
     A                                      DSPATR(HI)
     A                                  3  2'Position Cursor, Press Enter'
     A                                  5  2'Customer'
     A                                      DSPATR(HI)
     A                                  6  2'Number'
     A                                      DSPATR(HI)
     A                                  6 11'Name'
     A                                      DSPATR(HI)
     A                                  6 42'Status'
     A                                      DSPATR(HI)
     A                                  6 50'City'
     A                                      DSPATR(HI)
     A                                  6 70'State'
     A                                      DSPATR(HI)
     A            C1CUNO    R        B  7  2REFFLD(@BBREE7/BBBKC7 *LIBL/CABBREP)
     A                                      DSPATR(HI)
     A                                      EDTCDE(M)
     A            C1CUNM    R        B  7 11REFFLD(@BBREE7/BBAYTX *LIBL/CABBREP)
     A                                      CHGINPDFT(UL LC FE)
     A                                      DSPATR(HI)
     A            C1STAT    R    1A  B  7 44REFFLD(@BBREE7/BBVSST *LIBL/CABBREP)
     A                                      DSPATR(HI)
     A*---------------------------------------------------------------
     A* COMMAND LINE LINE
     A*---------------------------------------------------------------
     A          R TF533K1
     A*%%TS  SD  20070122  101312  ISLRAMS     REL-V5R3M0  5722-WDS
     A                                      WINDOW(TF533EC)
     A                                      OVERLAY
     A  43                             17  1'Message program failed. Call IS.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 18  2'F12=Previous'
      *
     A*-------------------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
     A*-------------------------------------------------------------------------
     A          R TF533ES                   SFL SFLMSGRCD(19)
      *
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
     A*
     A*-------------------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*-------------------------------------------------------------------------
     A*
     A          R TF533EC                   SFLCTL(TF533ES)
     A*%%TS  SD  19980327  105628  ISLRAMS     REL-V3R7M0  5716-PW1
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A                                      WINDOW(*DFT 20 75)
     A                                      WDWBORDER((*DSPATR RI) (*CHAR '    -
     A                                          '))
     A            MSGPGM                    SFLPGMQ(10)
     A*
     A*---------------------------------------------------------------
     A* ASSUME FORMAT -- REQUIRED FOR WINDOWS
     A*---------------------------------------------------------------
     A          R ASSUME
     A*%%TS  SD  20011025  160559  ISLRAMS     REL-V4R4M0  5769-PW1
     A                                      ASSUME
     A                                      OVERLAY
     A                                 24 79' '
