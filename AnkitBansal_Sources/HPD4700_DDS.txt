     A*%%TS  SD  20040309  083349  ISLRAMS     REL-V5R2M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD4700
     A*  TITLE:         WORK WITH BUDGET TEMPLATES
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  DATE:          01/30/04
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A                                      CF04(04 'PROMPT')
     A                                      CF05(05 'REFRESH')
     A                                      CF06(06 'CREATE')
     A                                      CF07(07 'LIST TEMPLATES')
     A                                      CF09(09 'LIST UNLINKED FARMS')
     A                                      CF10(10 'VIEW ITEMS/TEMPLATES')
     A                                      CF14(14 'W/W BUDGET ITEMS')
     A                                      CF15(15 'VIEW FARM LINKS')
     A                                      CF16(16 'W/W/ FARM BUDGETS')
     A                                      CF17(17 'PRINT OPTIONS')
     A                                      CF20(20 'WRKSPLF')
     A                                      CF24(24 'VIEW UNLINKED FARMS')
     A*
     A*-------------------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*-------------------------------------------------------------------------
     A          R HP4700S1                  SFL
     A*%%TS  SD  20040209  145640  ISLRAMS     REL-V5R2M0  5722-WDS
     A  85                                  SFLNXTCHG
     A            HDRRN1    R        H      REFFLD(@@RRN)
     A            D1OPT     R        B 14  2REFFLD(@@OPT)
     A                                      CHGINPDFT(UL)
     A  30                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A N30                                  DSPATR(HI)
     A            D1BTCD    R        O 14  6REFFLD(@@BTCD)
     A            D1BTDS    R        O 14 14REFFLD(@@BTDS)
     A            D1BACD    R        O 14 40REFFLD(HSRREF/@@BACD *LIBL/HSPREF)
     A            D1CSCD    R        O 14 42REFFLD(HSRREF/@@CSCD *LIBL/HSPREF)
     A            D1BTWK    R        O 14 44REFFLD(HSRREF/@@BTWK *LIBL/HSPREF)
     A                                      EDTCDE(4)
     A            D1PPCD    R        O 14 51REFFLD(HSRREF/@@PPCD *LIBL/HSPREF)
     A            D1TFCD    R        O 14 57REFFLD(HSRREF/@@TFCD *LIBL/HSPREF)
     A            D1CELL    R        O 14 64REFFLD(HSRREF/@@CELL *LIBL/HSPREF)
     A                                      EDTCDE(4)
     A            D1SIZE    R        O 14 68REFFLD(HSRREF/@@SIZE *LIBL/HSPREF)
     A            D1AIST    R        O 14 77REFFLD(@@AIST)
     A*
     A*-------------------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*-------------------------------------------------------------------------
     A          R HP4700C1                  SFLCTL(HP4700S1)
     A*%%TS  SD  20040309  083349  ISLRAMS     REL-V5R2M0  5722-WDS
     A                                      SFLSIZ(0050)
     A                                      SFLPAG(0005)
     A N82                                  ROLLUP(25 'PAGE FORWARD')
     A                                      SETOF(30)
     A                                      SETOF(51)
     A                                      SETOF(52)
     A                                      SETOF(53)
     A                                      SETOF(54)
     A                                      SETOF(55)
     A                                      RTNCSRLOC(&RCD &FLD)
     A                                      OVERLAY
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A            HDPAG1    R        H      REFFLD(@@PAG)
     A                                      SFLRCDNBR
     A            RCD       R        H      REFFLD(@@RCD)
     A            FLD       R        H      REFFLD(@@FLD)
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 28'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USR)
     A                                      DSPATR(HI)
     A                                  2 26'WORK WITH BUDGET TEMPLATES'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@JOB)
     A                                      DSPATR(HI)
     A                                  4  2'2=Revise'
     A                                  4 14'7=Delete Items w/zero Qty'
     A                                  4 43'13=Remove Links to Farms w/no Budg-
     A                                      ets'
     A                                  5  2'4=Delete'
     A                                  5 14'8=Delete all Items'
     A                                  5 43'14=Remove Links to All Farms'
     A                                  6  2'6=Print'
     A                                  6 14'9=Maintain Items'
     A                                  6 43'15=Maintain Farm Links'
     A                                  7 13'10=View Items'
     A                                  7 43'16=View Farm Links'
     A                                  8 13'12=Copy Items'
     A                                  8 43'17=Copy Farm Links'
     A                                  9  2'24=Print Template/Farm Budget Vari-
     A                                      ance'
     A                                 10 40'?'
     A                                      DSPATR(HI)
     A                                 10 42'?'
     A                                      DSPATR(HI)
     A                                 10 53'?'
     A                                      DSPATR(HI)
     A                                 10 59'?'
     A                                      DSPATR(HI)
     A                                 10 77'?'
     A                                      DSPATR(HI)
     A                                 11  5'Template'
     A                                      DSPATR(HI)
     A                                 11 40'A'
     A                                      DSPATR(HI)
     A                                 11 42'C'
     A                                      DSPATR(HI)
     A                                 11 51'Prod'
     A                                      DSPATR(HI)
     A                                 11 57'Type'
     A                                      DSPATR(HI)
     A                                 11 75'Act/'
     A                                      DSPATR(HI)
     A                                 12  2'Op'
     A                                      DSPATR(HI)
     A                                 12  6'Code'
     A                                      DSPATR(HI)
     A                                 12 14'Description'
     A                                      DSPATR(HI)
     A                                 12 40'V'
     A                                      DSPATR(HI)
     A                                 12 42'S'
     A                                      DSPATR(HI)
     A                                 12 44'Weeks'
     A                                      DSPATR(HI)
     A                                 12 51'Phase'
     A                                      DSPATR(HI)
     A                                 12 57'Farm'
     A                                      DSPATR(HI)
     A                                 12 63'Cell'
     A                                      DSPATR(HI)
     A                                 12 68'Size'
     A                                      DSPATR(HI)
     A                                 12 75'Inact'
     A                                      DSPATR(HI)
     A            C1BTCD    R        B 13  6REFFLD(@@BTCD)
     A                                      DSPATR(HI)
     A            C1BTDS    R        B 13 14REFFLD(HSRREF/@@BTDS *LIBL/HSPREF)
     A                                      CHGINPDFT(UL LC FE)
     A                                      DSPATR(HI)
     A            C1BACD    R        B 13 40REFFLD(HSRREF/@@BACD *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A  51                                  DSPATR(PC)
     A            C1CSCD    R        B 13 42REFFLD(HSRREF/@@CSCD *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A  52                                  DSPATR(PC)
     A            C1BTWK    R        B 13 44REFFLD(HSRREF/@@BTWK *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A                                      EDTCDE(Z)
     A            C1PPCD    R        B 13 51REFFLD(HSRREF/@@PPCD *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A  53                                  DSPATR(PC)
     A            C1TFCD    R        B 13 57REFFLD(HSRREF/@@TFCD *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A  54                                  DSPATR(PC)
     A            C1CELL    R        B 13 64REFFLD(HSRREF/@@CELL *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A                                      EDTCDE(Z)
     A            C1SIZE    R        B 13 68REFFLD(HSRREF/@@SIZE *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A            C1AIST    R        B 13 77REFFLD(@@AIST)
     A                                      DSPATR(HI)
     A  55                                  DSPATR(PC)
     A*
     A*-------------------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*-------------------------------------------------------------------------
     A          R HP4700K1
     A*%%TS  SD  20040224  152618  ISLRAMS     REL-V5R2M0  5722-WDS
     A  43                             19  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 20  2'F3=Exit'
     A                                 20 13'F4=Prompt'
     A                                 20 24'F10=View All Items/Templates'
     A                                 20 58'F7=List Templates'
     A                                 21  2'F5=Refresh'
     A                                 21 13'F6=Create'
     A                                 21 24'F15=View All Farm/Template Links'
     A                                 21 58'F9=List Unlinked Farms'
     A                                 22  2'F14=W/W Budget Items'
     A                                 22 24'F24=View Unlinked Farms'
     A                                 22 58'F17-Print Options'
     A                                 23  2'F16=W/W Farm Budgets'
     A                                 23 58'F20=Spool Files'
      *
     A*-------------------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
     A*-------------------------------------------------------------------------
     A          R HP4700ES                  SFL SFLMSGRCD(24)
      *
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
     A*-------------------------------------------------------------------------
      * ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*-------------------------------------------------------------------------
     A          R HP4700EC                  SFLCTL(HP4700ES)
      *
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
