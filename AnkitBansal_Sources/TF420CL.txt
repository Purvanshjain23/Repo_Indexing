/***************************************************************** */
/*                                                                 */
/*  SYSTEM:          TRIUMPH FOODS                                 */
/*  PROGRAM NUMBER:  TF420CL                                       */
/*  PROGRAM NAME:    EOP FEES CLOSE                                */
/*  PROGRAMMER:      LEANNE FEDOR                                  */
/*  CREATE DATE:     04/13/05                                      */
/*                                                                 */
/*  FUNCTION:        EOP FEES CLOSE                                */
/*                   1.  CALCULATE FREIGHT VARIANCE                */
/*                                                                 */
/*******************************************************************/
/* MODIFIED:                                                       */
/*                                                                 */
/* 05/21/07  LEANNE RAMSEY                                         */
/*           ADDED A NEW DATA AREA (DATSTPRD) TO CONTROL E-MAILS.  */
/*                                                                 */
/* 05/29/08  LEANNE RAMSEY                                         */
/*           OVERRODE KEY LIST FOR TFP027 IN TF223.                */
/*                                                                     */
/* 05/07/09  LEANNE RAMSEY                                             */
/*           CHANGED PRINT LOGIC TO MATCH MEAT COSTING.                */
/*******************************************************************/

             PGM

             DCL        VAR(&QDATA) TYPE(*CHAR) LEN(65)

             DCL        VAR(&USER) TYPE(*CHAR) LEN(10)
             DCL        VAR(&JOBNAME) TYPE(*CHAR) LEN(10)
             DCL        VAR(&JOBNBR) TYPE(*CHAR) LEN(6)
             DCL        VAR(&EMAIL)  TYPE(*CHAR) LEN(38)
             DCL        VAR(&ERROR) TYPE(*CHAR) LEN(1)

             DCL        VAR(&OUTQ) TYPE(*CHAR) LEN(10)
             DCL        VAR(&HOLD) TYPE(*CHAR) LEN(4)
             DCL        VAR(&SAVE) TYPE(*CHAR) LEN(4)
             DCL        VAR(&COPY) TYPE(*DEC)  LEN(1)

             DCL        VAR(&LDPFCD)  TYPE(*CHAR) LEN(1)
             DCL        VAR(&LDYR) TYPE(*DEC) LEN(4)
             DCL        VAR(&LDPE) TYPE(*DEC) LEN(2)
             DCL        VAR(&LDBEGDT)  TYPE(*DEC) LEN(8)
             DCL        VAR(&LDBEGSYN) TYPE(*DEC) LEN(7)
             DCL        VAR(&LDENDDT)  TYPE(*DEC) LEN(8)
             DCL        VAR(&LDENDSYN) TYPE(*DEC) LEN(7)
             DCL        VAR(&TSTPRDFL) TYPE(*CHAR) LEN(1)

             CHGVAR     VAR(&ERROR) VALUE('N')
             CHGVAR     VAR(&LDPFCD) VALUE(%SST(*LDA 1 1))
             CHGVAR     VAR(&LDYR) VALUE(%SST(*LDA 2 4))
             CHGVAR     VAR(&LDPE) VALUE(%SST(*LDA 6 2))
             CHGVAR     VAR(&LDBEGDT) VALUE(%SST(*LDA 8 8))
             CHGVAR     VAR(&LDBEGSYN) VALUE(%SST(*LDA 22 7))
             CHGVAR     VAR(&LDENDDT) VALUE(%SST(*LDA 29 8))
             CHGVAR     VAR(&LDENDSYN) VALUE(%SST(*LDA 43 7))

/*----------------------------------------------------------------*/
/* For sending Emails:                                            */
/* Retrieve Data Area that lets us know whether we are in Test    */
/* or Production (Always default to the "TEST" Distribution List.)*/
/*----------------------------------------------------------------*/

             CHGVAR     VAR(&EMAIL) VALUE('TF Test')
             RTVDTAARA  DTAARA(DATSTPRD (1 1)) RTNVAR(&TSTPRDFL)

/*----------------------------------------------------------------*/
/* Set Print Parms                                                */
/*----------------------------------------------------------------*/

 OUTQ:       CHGVAR     VAR(&OUTQ) VALUE(%SST(*LDA 401 10))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(*JOB))

 HOLD:       CHGVAR     VAR(&HOLD) VALUE(%SST(*LDA 411 4))
             IF         COND(&HOLD *NE '*YES' *AND &HOLD *NE '*NO ') +
                          THEN(CHGVAR VAR(&HOLD) VALUE(*NO))

 COPY:       CHGVAR     VAR(&COPY) VALUE(%SST(*LDA 419 1))
             IF         COND(&COPY = 0) THEN(CHGVAR VAR(&COPY) +
                          VALUE(1))

 SAVE:       CHGVAR     VAR(&SAVE) VALUE(%SST(*LDA 415 4))
             IF         COND(&SAVE *NE '*YES' *AND &SAVE *NE '*NO ') +
                          THEN(CHGVAR VAR(&SAVE) VALUE(*NO))

/*---------------------------------------------------------------------*/
/* Always delete any/all existing records for the Year/Period          */
/*---------------------------------------------------------------------*/

             CALL       PGM(TF220)

/*---------------------------------------------------------------------*/
/* Run the edit program with Error Listing                             */
/*---------------------------------------------------------------------*/

             OVRPRTF    FILE(PRINT80) OUTQ(&OUTQ) COPIES(&COPY) +
                          HOLD(&HOLD) SAVE(&SAVE)

             CALL       PGM(TF222) PARM(&ERROR &LDPFCD)
             DLTOVR     FILE(*ALL)


/****************************************************************************/
/* If there are no errors                                                   */
/****************************************************************************/

             IF         COND(&ERROR *EQ 'N') THEN(DO)        /* No Error Do */

/* Clear workfile                                                    */

             CLRPFM     FILE(TFP317) /* Product Level */


/****************************************************************************/
/* FREIGHT VARIANCE                                                         */
/****************************************************************************/

/*-----------------------------------------------------------------*/
/* "Unpaid Loads" workfile/listing                                 */
/*-----------------------------------------------------------------*/

             OVRPRTF    FILE(PRINT132) OUTQ(&OUTQ) COPIES(&COPY) +
                          HOLD(&HOLD) SAVE(&SAVE)

/* Select "unpaid" records for the Year/Period                     */

             CHGVAR     VAR(&QDATA) VALUE(' ')
             CHGVAR     VAR(%SST(&QDATA 1 8))   VALUE('FDYR *EQ')
             CHGVAR     VAR(%SST(&QDATA 10 4))  VALUE(&LDYR)
             CHGVAR     VAR(%SST(&QDATA 15 13)) VALUE('*AND FDPE *EQ')
             CHGVAR     VAR(%SST(&QDATA 29 2))  VALUE(&LDPE)
             CHGVAR     VAR(%SST(&QDATA 32 19)) VALUE('*AND FDPUFL +
                          *EQ "U"')

             OVRDBF     FILE(TFP027) SHARE(*YES)
             OPNQRYF    FILE((TFP027)) QRYSLT(&QDATA) +
                          KEYFLD((FDCONO) (FDASDT) (FDCACD) +
                          (FDLOAD) (FDPRCD) (FDPEDT)) SEQONLY(*YES)

             CALL       PGM(TF223)

             CLOF       OPNID(TFP027)
             DLTOVR     FILE(TFP027)

/*-----------------------------------------------------------------*/
/* "Paid Loads" workfile/listing                                   */
/*-----------------------------------------------------------------*/

/* Select "paid" records for the Year/Period                     */

             CHGVAR     VAR(&QDATA) VALUE(' ')
             CHGVAR     VAR(%SST(&QDATA 1 8))   VALUE('FDYR *EQ')
             CHGVAR     VAR(%SST(&QDATA 10 4))  VALUE(&LDYR)
             CHGVAR     VAR(%SST(&QDATA 15 13)) VALUE('*AND FDPE *EQ')
             CHGVAR     VAR(%SST(&QDATA 29 2))  VALUE(&LDPE)
             CHGVAR     VAR(%SST(&QDATA 32 19)) VALUE('*AND FDPUFL +
                          *EQ "P"')

             OVRDBF     FILE(TFP027)  SHARE(*YES)
             OPNQRYF    FILE((TFP027)) QRYSLT(&QDATA) KEYFLD(*FILE) +
                          SEQONLY(*YES)

             CALL       PGM(TF226)

             CLOF       OPNID(TFP027)
             DLTOVR     FILE(TFP027)
             DLTOVR     FILE(PRINT132)

/*-----------------------------------------------------------------*/
/* Create Freight Variance records                                 */
/*-----------------------------------------------------------------*/

             CALL       PGM(TF225)

/*-----------------------------------------------------------------*/
/* Print Freight Variance records                                  */
/*-----------------------------------------------------------------*/

             OVRPRTF    FILE(PRINT198) OUTQ(&OUTQ) COPIES(&COPY) +
                          HOLD(&HOLD) SAVE(&SAVE)
             CALL       PGM(TF621)


             DLTOVR     FILE(PRINT198)



/****************************************************************************/
/* If it is a Final run:                                                    */
/*  1) create Invoice Charge for Freight Variance                           */
/*  2) e-mail reports                                                       */
/****************************************************************************/

             IF         COND(&LDPFCD *EQ 'F') THEN(DO)
             CALL       PGM(TF224)

             RTVJOBA    JOB(&JOBNAME) USER(&USER) NBR(&JOBNBR)

             IF         COND(&TSTPRDFL *EQ 'P') THEN(CHGVAR +
                          VAR(&EMAIL) VALUE('TF EOP Fgt Variance'))

             ESNDMAIL   RECIPIENT(&EMAIL) SUBJECT('Final: EOP +
                          Freight Variance') MSG('Distribution +
                          List: TF EOP Fgt Variance') ATTLIST((* +
                          *PDF *N *ALL &JOBNBR/&USER/&JOBNAME *ALL +
                          *ALL))
             MONMSG     MSGID(CPF0000)
             ENDDO

             ENDDO                                           /* No Error Do */

/****************************************************************************/
/* Always update the Function Control File                                  */
/****************************************************************************/

             CALL       PGM(TF221) PARM(&LDPFCD)

             ENDPGM

