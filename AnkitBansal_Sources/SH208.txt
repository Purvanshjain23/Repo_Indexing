      *****************  RPG PROGRAM HEADING  ************************
     h option(*SRCSTMT:*NODEBUGIO)
      ****************************************************************
      *
      * ENVIRONMENT: Pork Division
      * SYSTEM:      Datamart--Sales
      * PROGRAM:     SH207 - Datamart Sales: Build Weekly Quotas
      * PROGRAMMER:  LeAnne Ramsey
      * CREATED:     01/17/08
      *
      * FUNCTION: This program builds Weekly Quotas data from the
      *           OMS-Order Management System "Sales Market Weekly Quotas" file.
      *
      *           For each run of the Datamart build, we will delete/rewrite
      *           records for the system "year".
      *
      ****************************************************************
      * Date      Programmer
      *
      * 10/21/08  LeAnne Ramsey
      *           Recompile only. New field "Commission Schedule" added to PMDDREP.
      *
      * 01/16/09  LeAnne Ramsey
      *           Recompile only. Field size of Product Type Code was changedfrom 6A to 16A.
      *
      * 05/24/10  LeAnne Ramsey
      *           Added a field to SHP208-Datamart: Sales Market Weekly Quota
      *           1) WQWEDTMDCY-Week Ending Date in date format mm/dd/ccyy
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fpmddrel1  if   e           k disk
      *  Sales market
      *
      *
     Fpmderel2  if   e           k disk
      *  Sales market weekly quotas
      *
      *
     Fpmdfrel1  if   e           k disk
      *  Sales product types
      *
      *
     Fshl208a   uf   e           k disk    rename(wqrec:wqreca)
      *  Datamart: Sales market weekly quotas
      *
      *
     Fshp208    o    e           k disk
      *  Datamart: Sales market weekly quotas
      *
      /EJECT
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      *---------------------------------------------------------------
      *  Named Constants
      *---------------------------------------------------------------
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      *---------------------------------------------------------------
      * Standalone fields
      *---------------------------------------------------------------
      *
      * Parms
      *
     D xxcorg          s                   like(wqcorg)
      *
      *
      * Workfields for date manipulation
      *
     D wknulldt        s                   like(wqwedt)   inz(D'0001-01-01')
     D wkyear          s              4  0
      *
      ****************************************************************
      * Data Structures
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * Mainline
      ****************************************************************
      *
      * Read/process records from the OMS Sales Market Weekly Quotas file
      * where "accounting year" is equal or greater than the system year.
      *
     C     wkyear        setll     pmderel2
      *
     C                   dou       *in90 = *on                                  Main do loop
     C                   read      pmderel2                               90
     C                   if        *in90 = *off                                 If not EOF
      *
     C                   clear                   wqrec
      *
     C                   move      xxcorg        wqcorg
     C                   move      deisaa        wqmkcd
     C                   move      deiwaa        wqptcd
     C                   move      devsst        wqstatfl
      *
     C                   z-add     deannb        wqacyr
     C                   z-add     dej8ny        wqacqr
     C                   z-add     deetnb        wqacpe
     C                   z-add     deeinb        wqacwk
      *
     C                   z-add     den8wg        wqquowt
      *
      * Retrieve Product Type Description from Sales Product Type file.
      *
     C     wqptcd        chain     pmdfrel1                           92
     C                   if        *in92 = *off
     C                   move      dfr7na        wqptds
     C                   endif
      *
      * Retrieve Market Code Description from Sales Market file.
      *
     C     wqmkcd        chain     pmddrel1                           92
     C                   if        *in92 = *off
     C                   move      ddr3na        wqmkds
     C                   endif
      *
      * Format Week-ending date
      *
     C     *cymd         test(d)                 dea2dt                 92
     C                   if        *in92 = *off
     C     *cymd         move      dea2dt        wqwedt
     C                   move      wqwedt        wqwedtmdcy
     C     *iso          move      wqwedt        wq80wedt
     C                   else
     C                   move      wknulldt      wqwedt
     C                   move      wqwedt        wqwedtmdcy
     C                   z-add     0             wq80wedt
     C                   endif
      *
     C                   write     wqrec
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Main do loop
      *
     C                   seton                                        lr
      /eject
      *---------------------------------------------------------------
      *  Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
     C     *entry        plist
     C                   parm                    xxcorg
      *
      * Key lists
      *
     C     key01         klist
     C                   kfld                    xxcorg
     C                   kfld                    wkyear
      *
      * Delete all Datamart records for this Organization for the "system" year
      * or beyond.
     C                   move      *year         wkyear
      *
     C     key01         setll     shl208a
      *
     C                   dou       *in91 = *on                                  Do delete
     C     xxcorg        reade     shl208a                                91
     C                   if        *in91 = *off and
     C                             wqacyr >= wkyear
     C                   delete    wqreca
     C                   endif
     C                   enddo                                                  Do delete
      *
     C                   endsr
      /EJECT
