/*********************************************************************/
/*     Program: PDX2UPR                                              */
/*       Title: Convert to lotus and FTP (Open Claims)               */
/*        Date: 12/21/98                                             */
/* Description: - Convert download file to Lotus using SEQUEL.       */
/*              - FTP to Novell Network.                             */
/*                                                                   */
/*  Parameters:                                                      */
/*  Usage   Field       Field Description                            */
/*  -----   ---------   ------------------------------------------   */
/*                                                                   */
/*********************************************************************/
/* Modifications:                                                    */
/*                                                                   */
/*   Date     Pgmr     Description                                   */
/* -------- ---------- --------------------------------------------  */
/* 5/5/00    RMC       THE WRONG FIELD WAS USED FOR THE STATUS. IT   */
/*                     SHOULD BE NAPPTX.                             */
/*                                                                   */
/* 06/30/00 P DROGE    CHANGED TO CREATE EXCEL FORMAT FILE INSTEAD   */
/*                     OF LOTUS.                                     */
/*                                                                   */
/* 03/09/09 S MASON    E00336 ALLOW EMAIL OF DOWNLOAD FILES          */
/*                     THIS WILL BE EITHER DOWNLOAD OR EMAIL NOT     */
/*                     BOTH                                          */
/*                                                                   */
/*                                                                   */
/*********************************************************************/
             PGM        PARM(&RPTTYP &EMAIL)

   /*-------------------*/
   /* Declare Variables */
   /*-------------------*/

             /* Internal Fields */
             DCL  VAR(&ALREADY)  TYPE(*CHAR) LEN(1)
             DCL  VAR(&DOCUMENT) TYPE(*CHAR) LEN(10)
             DCL  VAR(&PCFILE)   TYPE(*CHAR) LEN(32)
             DCL  VAR(&USRCLS)   TYPE(*CHAR) LEN(10)
             DCL  VAR(&PCFMT)    TYPE(*CHAR) LEN(10) VALUE(*XLS)
             DCL  VAR(&PCEXT)    TYPE(*CHAR) LEN(3) VALUE(XLS)
             DCL  VAR(&EMAIL)    TYPE(*CHAR) LEN(50)
             DCL  VAR(&RPTTYP)   TYPE(*CHAR) LEN(1)
             DCL  VAR(&TEXT)     TYPE(*CHAR) LEN(80)


   /*----------------------------*/
   /* Add SEQUEL Library to List */
   /*----------------------------*/
             CHGVAR VAR(&ALREADY) VALUE('N')
             ADDLIBLE LIB(SEQUEL) POSITION(*LAST)
             MONMSG MSGID(CPF2103) EXEC(DO)       /* already in libl */
                CHGVAR VAR(&ALREADY) VALUE('Y')
             ENDDO

             IF         COND(&EMAIL *NE '       ') THEN(GOTO +
                          CMDLBL(EMAIL))

   /*--------------------------------------------*/
   /* Convert "A/R Claim Download Wrkf" to Lotus */
   /*--------------------------------------------*/
             EXECUTE SQL('SELECT +
                           nainnx COLHDG("Claim" "#"), +
                           natstx COLHDG("Reason" "Code"), +
                           naiqnx COLHDG("Orig" "Ord #"), +
                           naionx COLHDG("Load" "Id"), +
                           naipnx COLHDG("Invoice" "#"), +
                           natvtx COLHDG("Sales" "Person"), +
                           natttx COLHDG("Customer" "Name"), +
                           natwtx COLHDG("Explanation" "for Claim"), +
                           nab8vl COLHDG("AMT" "CM" "DM()"), +
                           naDQVL COLHDG("RESALE" "AMT"), +
                           naDRVL COLHDG("NET" "LOSS"), +
                           natytx COLHDG("Date" "Logd"), +
                           naPPTx COLHDG("Status" "A/P"), +
                           natutx COLHDG("Date" "Sent" "Plant"), +
                           nachsx COLHDG("CM" "or" "DM") +
                           nabic3 COLHDG("Company"), +
                           NAAJCD COLHDG("Whs"), +
                           NACJSX COLHDG("Clm" "Sts") +
                          FROM pdnacpp') +
                     TOFLR(GUY.IS) +
                     TODOC(OPENCLM) +
                     PCFMT(&PCFMT) +
                     REPLACE(*YES)

   /*---------------------------------*/
   /* Remove SEQUEL Library from List */
   /*---------------------------------*/
             IF COND(&ALREADY *EQ 'N') THEN(DO)
                RMVLIBLE   LIB(SEQUEL)
             ENDDO

   /*------------------------------------*/
   /* FTP documents to EDIPC/PORK/CLAIMS */
   /*------------------------------------*/
             CHGVAR VAR(&DOCUMENT) VALUE('OPENCLM')
             CHGVAR VAR(&PCFILE)   VALUE('OPENCLM.' *CAT &PCEXT)
             CALL PGM(FTPCLAIMS) PARM(&DOCUMENT &PCFILE)

             GOTO       CMDLBL(END)

EMAIL:
/*********************************************************************/
/********************  SEND EMAIL QUERY   ****************************/
/*********************************************************************/

             CHGVAR     VAR(&TEXT) VALUE('PDNACPP WF Open A/R Claim +
                          data')

             IF         COND(&EMAIL *NE '       ') THEN(DO)
             EXECUTE      VIEW(PDNACPPOP) PCFMT(*XLS) REPLACE(*YES) +
                          RECIPIENT(&EMAIL) EMLMSG(&TEXT)

             ENDDO
END:
             ENDPGM
