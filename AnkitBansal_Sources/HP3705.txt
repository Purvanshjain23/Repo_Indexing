      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Hog Production System
      * PROGRAM:     HP3705-Remove Budget Item from Templates
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     02/09/04
      *
      * Function:    Depending on the incoming parm, this function:
      *                1) deletes all records from the Budget Template Detail file for
      *                   a specified Budget Item, or
      *                2) deletes only the records that have zero quantities for the
      *                   specified Budget Item
      *
      *              This program is called from HP4701-Work With Budget Items
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      /eject
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fhsl186b   uf   e           k disk
      *    Budget template detail
      *
      /eject
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      *----------------------------------------------------------------
      * CONSTANTS
      *----------------------------------------------------------------
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      *----------------------------------------------------------------
      * STANDALONE FIELDS
      *----------------------------------------------------------------
      *
      * Standard fields
      *
     D rtncd           s              2
      *
      *
      * Parms
      *
     D xxbgit          s                   like(tdbgit)
     D xxonly0         s              1
      *
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * Mainline
      ****************************************************************
      *
      * Process each Budget template detail record for the incoming Budget
      * Template code.
      *
     C     xxbgit        setll     hsl186b
      *
     C                   dou       *in90 = *on                                  Main do
     C     xxbgit        reade     hsl186b                                90
     C                   if        *in90 = *off                                 If not EOF
      *
     C                   if        (xxonly0 = yes and tdbtqt = 0) or
     C                             xxonly0 = no
     C                   delete    tdrec
     C                   endif
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Main do
      *
     C                   seton                                        lr
      /eject
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
     C     *entry        plist
     C                   parm                    xxbgit
     C                   parm                    xxonly0
     C                   parm                    rtncd
      *
     C                   endsr
      /eject
