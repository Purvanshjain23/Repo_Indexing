      *****************  RPG PROGRAM HEADING  ************************
      * SYSTEM:      Triumph Foods
      * PROGRAM:     TF3052
      * TITLE:       Cash: Workfile: Cash Distribution Report
      * PROGRAMMER:  LeAnne Ramsey
      * CREATED:     05/06/08
      *
      *  FUNCTION:   This program builds 1 summary/header workfile.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      /EJECT
      ****************************************************************
      * File specifications
      ****************************************************************
      *
     Ftfl012a   if   e           k disk
      *    Cash distribution header
      *    (records selected by open query)
      *
      *
     Ftfp013    if   e           k disk
      *    Cash distribution detail
      *
      *
     Ftfp3052   o    e           k disk
      *    Workfile: Cash distribution report--header
      *
      /EJECT
      ****************************************************************
      * Definition specifications
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Named Constants
      *---------------------------------------------------------------
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      *
      *---------------------------------------------------------------------
      * Standalone fields
      *---------------------------------------------------------------------
      *
      * Workfields for date manipulation
      *
     D wkisodate       s               D   datfmt(*iso)
      *
      ****************************************************************
      * Data structures
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *---------------------------------------------------------------
      * Local data area.
      *---------------------------------------------------------------
      *
     Dlda             uds                  dtaara(*lda)
      *
     D  ldfddt                 7     14  0
     D  ldfmdy                15     20  0
      *
     D  ldtddt                21     28  0
     D  ldtmdy                29     34  0
      *
     D  lddsfl                35     35
     D  lddsds                36     45
      *
     D  ldincfl               46     46
     D  ldincds               47     76
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      *-------------------------------------------------------------------------------------
      * MAINLINE
      *-------------------------------------------------------------------------------------
      *
      * Process all Cash Distributon Header records selected by the Open Query.
      *
     C                   dou       *in90 = *on                                  Main do
     C                   read      tfl012a                                90
     C                   if        *in90 = *off                                 If not EOF
      *
      * Retrieve/sum Detail records for this batch.
      *
     C     chbtsn        setll     tfp013
     C                   dou       *in91 = *on                                  Do detail
     C     chbtsn        reade     tfp013                                 91
     C                   if        *in91 = *off                                 If not EOF
     C                   add       cdasxcdam     w1asxcdam
     C                   add       cdscdam       w1scdam
     C                   add       cdtcdam       w1tcdam
     C                   endif                                                  If not EOF
     C                   enddo                                                  Do detail
      *
      * Total Cash Distributed Amount
      *
     C                   eval      w1cdam = w1asxcdam + w1scdam + w1tcdam
      * Variance
     C                   eval      w1varam = chcpam - w1cdam
      *
      * Write a record depending on the users 'batches to include' selections.
      *
     C                   if        ldincfl = 'A' or
     C                             (ldincfl = 'N' and w1varam = 0) or
     C                             (ldincfl = 'V' and w1varam <> 0)
      *
     C                   z-add     chbtsn        w1btsn
     C                   z-add     chcpam        w1cpam
      *
      * Flip Daily Date to MMDDYY format
      *
     C                   z-add     chddt         w1ddt
      *
     C     *iso          test(d)                 chddt                  92
     C                   if        *in92 = *off                                 If OK daily
     C                   move      chddt         wkisodate
     C     *mdy          move      wkisodate     w1dmdy
     C                   endif                                                  If OK daily
      *
     C                   write     w1rec
     C                   endif
      *
     C                   clear                   w1rec
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Main do
      *
      * EOF processing
      *
     C                   seton                                        lr
      /eject
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
     C                   endsr
      /eject
