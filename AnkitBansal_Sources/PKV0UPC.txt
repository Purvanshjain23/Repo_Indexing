/***************************************************************** */
/*                                                                 */
/*  PROGRAM NUMBER:  PKV0UPC                                       */
/*  PROGRAM NAME:    PRINT INTERNAL TRUCKSCALE LOG                 */
/*  PROGRAMMER:      PURVA DROGE                                   */
/*  CREATE DATE:     07/26/00                                      */
/*                                                                 */
/*  FUNCTION:        THIS CL IS SUBMITTED FROM THE PROMPT          */
/*                   TO PRINT THE INTERNAL TRUCKSCALE LOG.         */
/*                                                                 */
/*******************************************************************/
/*  Modification History                                           */
/*                                                                 */
/*   Date     Pgmr     Description                                 */
/* -------- ---------- --------------------------------------------*/
/* 11/04/20   ISE      RECOMPILE ONLY. INCREASED BUY ORDER NUMBER  */
/*                     FIELD LENGTH 5 TO 7.                        */
/*******************************************************************/
             PGM   PARM(&CO &PROD &SDTE &TDTE &SRC &EMAIL &PDEV +
                          &HOLD &SAVE &XCOPIES)

/*******************************************************************/
/*  VARIABLE DEFINITION                                            */
/*******************************************************************/

/* INPUT PARMS                                                    */
             DCL        VAR(&CO) TYPE(*DEC) LEN(3 0)
             DCL        VAR(&PROD) TYPE(*CHAR) LEN(6)
             DCL        VAR(&SDTE) TYPE(*DEC) LEN(7 0)
             DCL        VAR(&SRC) TYPE(*CHAR) LEN(1)
             DCL        VAR(&TDTE) TYPE(*DEC) LEN(7 0)
             DCL        VAR(&EMAIL) TYPE(*CHAR) LEN(50)

/* PRINT OPTION PARMS                                             */
             DCL        VAR(&PDEV) TYPE(*CHAR) LEN(10)
             DCL        VAR(&HOLD) TYPE(*CHAR) LEN(4)
             DCL        VAR(&SAVE) TYPE(*CHAR) LEN(4)
             DCL        VAR(&XCOPIES) TYPE(*CHAR) LEN(15)
             DCL        VAR(&COPIES) TYPE(*DEC) LEN(2 0)

/* PROGRAM VARIABLES */
             DCL        VAR(&DWL) TYPE(*CHAR) LEN(1) VALUE(N)
             DCL        VAR(&SEL) TYPE(*CHAR) LEN(1000)

/* SYSTEM NAME PARM                                               */

/* SBDPRTOVR VARIABLE                                          */
             DCL        VAR(&CMD) TYPE(*CHAR) LEN(512)

/*******************************************************************/
/*  INITIALIZATION                                                 */
/*******************************************************************/
/* CHANGE ALPHA PARMS TO NUMERIC VARIABLES                       */
             CHGVAR     VAR(&COPIES) VALUE(&XCOPIES)

/* CLEAR DOWNLOAD FILE                                           */
             CLRPFM PKFGCPP

/*******************************************************************/
/*  MAINLINE                                                       */
/*******************************************************************/
/* CREATE PRINTER FILE OVERRIDE CODE                            */
             SBDPRTOVR  CMDVAR(&CMD) FILE(PKVYPFR$) OUTQ(&PDEV) +
                          COPIES(&COPIES) HOLD(&HOLD) SAVE(&SAVE)
             CALL       PGM(QCMDEXC) PARM(&CMD 512)

/* PRINT TRUCK LOG REPORT                                      */
            IF COND(&EMAIL *NE '  ') THEN(DO)
             CHGVAR     VAR(&DWL) VALUE(Y)
            ENDDO

             CALL       PGM(PKVYPFR) PARM(' ' &CO &PROD &SDTE &TDTE +
                          &DWL &SRC)
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(PGMERR))

/* DATE(CVTDATE(A1B0DT,CYMD)) COLHDG("KILL" "DATE") NAME(CDATE) */

            IF COND(&DWL *EQ 'Y') THEN(DO)
             CHGVAR     VAR(&SEL) VALUE('SELECT +
                FGHONB COLHDG("CO" "NUM"), +
                FGBNNB COLHDG("BUY" "ORDER"), +
                FGBONB COLHDG("LOAD" "NUM"), +
                DATE(CVTDATE(FGEYDT,CYMD)) COLHDG("SCALE" "DATE") NAME(SDATE), +
                FGOXNB COLHDG("MOVEMENT"), +
                FGTHHD COLHDG("FARM" "SITE"), +
                FGJ8CD COLHDG("DRIVER"), +
                FGCWNB COLHDG("HEAD" "RECVD"), +
                FGCXNB COLHDG("DOA"), +
                FGHHA1 COLHDG("RESALE"), +
                FGFCNB COLHDG("YARD" "DEAD"), +
                FGHIA1 COLHDG("GROSS" "WEIGHT"), +
                FGBGTM COLHDG("TIME" "IN"), +
                FGHJA1 COLHDG("TARE" "WEIGHT"), +
                FGBHTM COLHDG("TIME" "OUT"), +
                FGBITM COLHDG("ACTUAL" "UNLOAD"), +
                FGBJTM COLHDG("WAIT" "TIME") FROM PKFGCPP')

             EXECUTE    SQL(&SEL) PCFMT(*XLS) REPLACE(*YES) +
                          TEXT('Truck Scale Log') RECIPIENT(&EMAIL) +
                          EMLMSG('Truck Scale Log')
            ENDDO

             GOTO       CMDLBL(ENDPGM)

/*******************************************************************/
/*  ERROR HANDLING                                                 */
/*******************************************************************/
PGMERR:
             CHGJOB     LOG(4 00 *SECLVL)
             SNDPGMMSG  MSG('Program Error')
             GOTO       CMDLBL(ENDPGM)

/*******************************************************************/
/*  END PROGRAM                                                    */
/*******************************************************************/
ENDPGM:
             DLTOVR *ALL

             ENDPGM
