      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Triumph Foods
      * PROGRAM:     TF433
      * TITLE:       Work with Invoices
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     03/09/05
      *
      *
      * FUNCTION: This Work With displays the records in the Invoice Header file.
      *
      *           Restrictors:
      *                Include posted invoices flag
      *
      *           Positioners:
      *                Invoice date
      *
      *           Selectors:
      *                Invoice number
      *                Invoice status code
      *                Charge frequency code
      *                Due date
      *                Posted date
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 05/07/09  LeAnne Ramsey
      *           Changed print logic to match Meat Costing.
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Ftfd433    cf   e             workstn sfile(tf433s1:hdrrn1)
     F                                     infds(iofeed)
      *
      *
     Ftfl020b   if   e           k disk    infds(dbfeed)
      *    Invoice header
      *
      /eject
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D create          c                   'CREATE'
     D revise          c                   'REVISE'
     D delete          c                   'DELETE'
     D yes             c                   'Y'
     D no              c                   'N'
     D set1            c                   'SET1  '
     D edit1           c                   'EDIT1 '
     D scrn1           c                   'SCRN1 '
     D exit            c                   'EXIT  '
     D update          c                   'UPDATE'
      *
      *
      ****************************************************************
      * ARRAYS AND TABLES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Compile time array for SBMJOB used in QCMDEXC
      *---------------------------------------------------------------
     D cmd             s              1    dim(80) ctdata perrcd(80)
      *
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Standard fields
      *
     D mode            s              6
     D pgm             s             10
     D msgfl           s             10
     D maxmsg          s              2  0  inz(20)
     D routne          s              6
     D maxrrn          s              4  0
     D reset           s              1    inz('N')
     D norecs          s              1    inz('N')
     D count           s              2  0
     D wkflow          s                   like(idflow)
     D rtncd           s              2    inz('00')
     D svrrn1          s                   like(hdrrn1)
      *
      *
      *  Parms for use with QCMDEXC.  'LENGTH' is the length of the
      *  command that will be issued with QCMDEXC.  QCMDEXC requires
      *  the field containing length to be defined as 15 with 5 decimal
      *  positions.  The field 'CMDSBM' will hold the values of the
      *  submit job command when passed as a parm on the call to
      *  QCMDEXC.
      *
     D length          s             15  5 inz(80)
     D cmdsbm          s             80
      *
      *
      * Workfields required for subfile paging--see initialization
      * subroutine.
      *
     D wkplin          s              2  0
     D wkrlin          s              2  0
      *
      *
      * Save fields for restrictors/positioners/selectors
      *
     D svynfl          s                   like(c1ynfl)
     D svinsn          s                   like(c1insn)
     D svindtmdy       s                   like(c1indtmdy)
     D svindt          s                   like(ihindt)
     D svdudtmdy       s                   like(c1dudtmdy)
     D svpodtmdy       s                   like(c1podtmdy)
     D svinstcd        s                   like(c1instcd)
     D svcfqcd         s                   like(c1cfqcd)
      *
      *
      * Workfields for date manipulation
      *
     D wkisodate       s               D   datfmt(*iso)
      *
      *
      * Parms
      *
     D xxfmdy          s                   like(d1indtmdy)
     D xxtmdy          s                   like(d1indtmdy)
     D xxdmdy          s                   like(d1indtmdy)
     D xxpmdy          s                   like(d1indtmdy)
      *
     D xxfcymd         s                   like(ihindt)
     D xxtcymd         s                   like(ihindt)
     D xxdcymd         s                   like(ihindt)
     D xxpcymd         s                   like(ihindt)
      *
     D xxynfl          s                   like(c1ynfl)
      *
     D xxinsn          s                   like(c1insn)
     D xxinstcd        s                   like(c1instcd)
     D xxinstds        s             10
      *
     D xxcfqcd         s                   like(c1cfqcd)
     D xxcfqds         s             10
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * standard error message handling data structures
      *---------------------------------------------------------------
      *
      * For error message handling, a packed index (3) is required.
      * For program readability, define a corresponding error count
      * field called 'error'
      *
     D                 ds                  inz
     D  error                         2p 0
     D   e                            2p 0 overlay(error)
      *
      * This data structure supplies the name of the message file to
      * the message handling CL program.  The field name MSGFIL must be
      * constant. The value in quotes is the name of the specific
      * message file containing the user defined messages.
      *
     D                 ds                  inz
     D  msgfil                       10    inz('HSMSGF    ')
      *
      *---------------------------------------------------------------
      *  Standard message data structures
      *---------------------------------------------------------------
      *
      * The following 3 data structures are used to speed message
      * handling since it is faster to clear data structures than
      * arrays.  Each is associated with a standard message array.
      *
     D mgi             ds                  inz
     D  mgid                          7    dim(20)
      *
     D mgd             ds                  inz
     D  mgdt                         50    dim(20)
      *
     D mgwk            ds                  inz
     D  mgw                           1    dim(50)
      *
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *---------------------------------------------------------------
      * Standard workstation information data structure
      *---------------------------------------------------------------
      *    externally defined as UTWSFR (record format: UTIDFRR)
     D iofeed        e ds                  extname(utwsfr)
      *
      *
      *---------------------------------------------------------------
      * Standard database file information data structure
      *---------------------------------------------------------------
      *    externally defined as UTDBGR (record format: FDBCKD)
     D dbfeed        e ds                  extname(utdbfr)
      *
      *
      *---------------------------------------------------------------
      * Standard subfile option conversion
      *---------------------------------------------------------------
     D                 ds                  inz
     D d1opt                          2
     D   wkopt1                       1    overlay(d1opt)
     D   wkopt2                       1    overlay(d1opt:2)
      *
      *
      *---------------------------------------------------------------
      * Data areas
      *---------------------------------------------------------------
      *
      * Local data area.
      *
     Dlda             uds                  dtaara(*lda)
     D  ldtask                 1      1
     D  ldinsn                 2      8  0
     D  ldynfl                 9      9
     D  ldfmdy                10     15  0
     D  ldfcymd               16     23  0
     D  ldtmdy                24     29  0
     D  ldtcymd               30     37  0
     D  ldinstcd              38     38
     D  ldcfqcd               39     39
     D  ldpmdy                40     45  0
     D  ldpcymd               46     53  0
     D  lddmdy                54     59  0
     D  lddcymd               60     67  0
      *
     D  ldoutq               401    410
     D  ldhold               411    414
     D  ldsave               415    418
     D  ldcopy               419    419  0
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * MAINLINE
      ****************************************************************
      *
     C                   eval      routne = set1
      *
     C     *inlr         doweq     *off                                         Main do loop
     C     routne        caseq     set1          $set1
     C     routne        caseq     scrn1         $scrn1
     C     routne        caseq     edit1         $edit1
     C     routne        caseq     exit          $exit
     C                   endcs
     C                   enddo                                                  Main do loop
      /EJECT
      *----------------------------------------------------------------
      * Set environment for screen 1
      *----------------------------------------------------------------
      *
     C     $set1         begsr
      *
      * Clear and fill the subfile
      *
     C                   exsr      $clr1
     C                   exsr      $frsh1
      *
      * Set routine to display screen
      *
     C                   eval      routne = scrn1
      *
     C                   endsr
      /EJECT
      *----------------------------------------------------------------
      * Perform operator I/O for screen 1
      *----------------------------------------------------------------
      *
     C     $scrn1        begsr
      *
      * Write the error messages from the error arrays to the
      * error message subfile
      *
     C                   exsr      $wrmsg
      *
      * Write screen 1 to crt
      *
     C                   write     tf433k1
     C                   write     tf433ec
     C                   exfmt     tf433c1
      *
      * Save the value of the lowest subfile rrn on the screen for
      * later repositioning of the subfile
      *
     C                   eval      wkflow = idflow
      *
      * Clear the messages from the message data structures and
      * program message queue
      *
     C                   exsr      $clmsg
      *
      * Get user's entry and set next routine
      *
     C                   select
     C                   when      *in03 = *on                                  F3-EXIT
     C                   eval      routne = exit
      *
     C                   when      *in04 = *on                                  F4-prompt
     C                   exsr      $f4s1
      *
     C                   if        fld = 'C1YNFL' and
     C                             c1ynfl <> svynfl
     C                   exsr      $ynfl
     C                   endif
      *
     C                   if        error = 0
     C                   exsr      $chksv
     C                   else
     C                   eval      routne = scrn1
     C                   endif
      *
     C                   when      *in05 = *on and                              F5-refresh
     C                             (c1ynfl = yes or c1ynfl = no)
     C                   eval      reset = yes
     C                   eval      routne = set1
      *
     C                   when      *in06 = *on                                  F6-create
     C                   exsr      $creat
     C                   eval      hdpag1 = wkflow
     C                   eval      reset = yes
     C                   eval      routne = set1
      *
     C                   when      *in07 = *on                                  F7-listing
     C                   exsr      $listopts
     C                   z-add     wkflow        hdpag1
     C                   eval      routne = scrn1
      *
     C                   when      *in17 = *on                                  F17-print options
     C                   call      'RO890'
     C                   eval      routne = scrn1
      *
     C                   when      *in20 = *on                                  F20-wrksplf
     C                   call      'QEZOUTPT'                                     API
     C                   eval      hdpag1 = wkflow
     C                   eval      routne = scrn1
      *
      *
      * Reposition the subfile if the user has changed any of the
      * restrictors or positioners or selectors in the control record.
      *
     C                   when      c1ynfl <> svynfl or                          Positioners,etc
     C                             c1insn <> svinsn or
     C                             c1indtmdy <> svindtmdy or
     C                             c1dudtmdy <> svdudtmdy or
     C                             c1podtmdy <> svpodtmdy or
     C                             c1instcd <> svinstcd or
     C                             c1cfqcd <> svcfqcd
      *
     C                   if        c1ynfl <> svynfl                                              nge
     C                   exsr      $ynfl
     C                   endif                                                                   nge
      *
     C                   if        error = 0
     C                   exsr      $chksv
     C                   else
     C                   eval      routne = scrn1
     C                   endif
      *
     C                   when      *in25 = *on                                  F25-rollup
     C                   exsr      $load1
     C                   other
      *
      * User has pressed enter
      *
     C                   if        norecs = yes                                 If empty sfl
     C                   eval      routne = set1
     C                   else
     C                   eval      routne = edit1
     C                   endif                                                  If empty sfl
     C                   endsl
      *
      * Bring in the LDA to pick up any printer changes the user may
      * have made from called programs
      *
     C                   in        lda
      *
     C                   endsr
      /EJECT
      *----------------------------------------------------------------
      * Save the control record positioners/selectors
      *----------------------------------------------------------------
      *
     C     $chksv        begsr
      *
     C                   eval      reset = no
     C                   eval      routne = set1
      *
     C                   move      c1ynfl        svynfl
     C                   z-add     c1insn        svinsn
     C                   z-add     c1indtmdy     svindtmdy
     C                   z-add     c1dudtmdy     svdudtmdy
     C                   z-add     c1podtmdy     svpodtmdy
     C                   move      c1instcd      svinstcd
      *
      * Save Invoice Date in CCYYMMDD format for positioning in the database file.
      *
     C                   z-add     0             svindt
     C                   if        svindtmdy <> 0                               If date
     C     *mdy          test(d)                 c1indtmdy              92
     C                   if        *in92 = *off                                 If OK date
     C     *mdy          move      c1indtmdy     wkisodate
     C     *iso          move      wkisodate     svindt
     C                   endif                                                  If OK date
     C                   endif                                                  If date
      *
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Validate 'include posted chargse' flag
      *---------------------------------------------------------------
      *
     C     $ynfl         begsr
      *
     C                   if        c1ynfl <> yes and
     C                             c1ynfl <> no
     C                   seton                                        31
     C                   if        error < maxmsg
     C                   add       1             error
     C                   eval      mgid(e) = 'HS09106'
     C                   endif
     C                   endif
      *
     C                   endsr
      /EJECT
      *----------------------------------------------------------------
      * Edit input fields for screen 1
      *----------------------------------------------------------------
      *
     C     $edit1        begsr
      *
      * First, edit all the selections entered by the user in the
      * subfile option field to see that valid selections were input
      *
     C                   exsr      $edop1
      *
      * If there are no errors in the subfile option selectors entered
      * by the user, then execute the subroutine to reread and process
      * the records selected by the user.  After completing the
      * subroutine, set the routine to start over.
      *
     C                   if        error = 0                                    If no error
     C                   exsr      $proc1
      *
     C                   if        rtncd = '00'
     C                   eval      routne = set1
     C                   else
     C                   eval      hdpag1 = wkflow
     C                   eval      routne = scrn1
     C                   endif
      *
      * else there were errors in the subfile options entered,
      * so set to redisplay screen with errors to user
      *
     C                   else
     C                   eval      routne = scrn1
     C                   endif                                                  If no error
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      *  Logic for F7-Listing
      *---------------------------------------------------------------
      *
     C     $listopts     begsr
      *
      * Call the pop-up window to allow user to enter listing options.
      * (Pass in the restrictors/selectors from this W/W)
      *
     C                   call      'TF4330'
     C                   parm      c1ynfl        xxynfl
     C                   parm      c1indtmdy     xxfmdy
     C                   parm      0             xxfcymd
     C                   parm      c1indtmdy     xxtmdy
     C                   parm      0             xxtcymd
     C                   parm      c1instcd      xxinstcd
     C                   parm      c1cfqcd       xxcfqcd
     C                   parm      c1dudtmdy     xxdmdy
     C                   parm      0             xxdcymd
     C                   parm      c1podtmdy     xxpmdy
     C                   parm      0             xxpcymd
     C                   parm      c1insn        xxinsn
     C                   parm      *blank        rtncd
      *
      * If the user took the function key in the pop-up window to submit
      * the listing.
      *
     C                   if        rtncd = '08'
     C                   exsr      $list
     C                   endif
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Listing logic
      *---------------------------------------------------------------
      *
     C     $list         begsr
      *
     C                   exsr      $initlda
      *
     C                   select
     C                   when      d1opt = ' 6'
     C                   move      'P'           ldtask
     C                   z-add     d1insn        ldinsn
      *
     C                   when      d1opt = '15'
     C                   move      'F'           ldtask
     C                   z-add     d1insn        ldinsn
      *
     C                   when      d1opt = '18'
     C                   move      'R'           ldtask
     C                   z-add     d1insn        ldinsn
     C                   other
      *
     C                   move      'L'           ldtask
     C                   move      xxynfl        ldynfl
     C                   z-add     xxfmdy        ldfmdy
     C                   z-add     xxfcymd       ldfcymd
     C                   z-add     xxtmdy        ldtmdy
     C                   z-add     xxtcymd       ldtcymd
     C                   z-add     xxdmdy        lddmdy
     C                   z-add     xxdcymd       lddcymd
     C                   z-add     xxpmdy        ldpmdy
     C                   z-add     xxpcymd       ldpcymd
     C                   move      xxinstcd      ldinstcd
     C                   move      xxcfqcd       ldcfqcd
     C                   z-add     xxinsn        ldinsn
     C                   endsl
      *
     C                   out       lda
      *
      * Move the 80-character compile time array 'CMD' to an
      * 80-character field for passing as a parm with QCMDEXC.
      *
     C                   movea     cmd           cmdsbm
     C                   call      'QCMDEXC'
     C                   parm                    cmdsbm
     C                   parm                    length
      *
      * Issue the standard request submission message
      *
     C                   if        error < maxmsg
     C                   add       1             error
     C                   eval      mgid(e) = 'HS09003'
     C                   endif
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Clear subfile for screen 1
      *---------------------------------------------------------------
      *
     C     $clr1         begsr
      *
     C                   eval      svrrn1 = 0
      *
     C                   clear                   tf433s1
     C                   seton                                        83        sflclr
     C                   write     tf433c1
     C                   setoff                                       818283
      *                                                    81=sfldsp
      *                                                    82=sflend
      *                                                    83=sflclr
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Refresh/fill the subfile for screen 1
      *---------------------------------------------------------------
      *
     C     $frsh1        begsr
      *
      * Set position in file
      *
     C                   if        c1indtmdy = 0
     C     *hival        setll     tfl020b
     C                   else
     C     svindt        setll     tfl020b
     C                   endif
      *
      *
      * The field 'reset' is a flag field to indicate whether you are
      * attempting to reposition the user close to where he was in the
      * subfile.
      * The lowest subfile RRN on the screen from the DSPF feedback
      * area (IDFLOW) was retrieved earlier and put in WKFLOW.
      * Add to WKFLOW the number of lines on your subfile page or the
      * number less 1 to determine a subfile RRN to reposition to.
      *
     C                   if        reset = yes
     C                   eval      maxrrn = wkflow + wkrlin
     C                   else
      *
      * Don't try to reposition the user, just start the subfile over
      *
     C                   eval      maxrrn = wkplin
     C                   endif
      *
      * Load the subfile until you have the required number of records.
      * The number will depend on whether uou are repositioning the
      * subfile or not. Stop if you get to the end of the subfile.
      *
     C                   dou       svrrn1 >= maxrrn or *in82 = *on
     C                   exsr      $load1
     C                   enddo
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Load subfile for screen 1
      *---------------------------------------------------------------
      *
     C     $load1        begsr
      *
     C                   eval      hdrrn1 = svrrn1
      *
      * Set the count of records on the subfile page to zero.
      *
     C                   z-add     0             count
      *
      * Fill the subfile until the end of the database file or
      * you have filled all the lines on the subfile page.
      *
     C                   setoff                                       82
     C                   dou       *in82 = *on or count >= wkplin               Do fill
      *
     C                   read      tfl020b                                82
     C                   if        *in82 = *off                                 If read
      *
     C                   if        c1ynfl = yes or                              If process
     C                             (c1ynfl = no and ihinstcd <> 'P')
      *
     C                   z-add     ihinsn        d1insn
     C                   move      ihinstcd      d1instcd
     C                   move      ihcfqcd       d1cfqcd
      *
      *
      * Flip Invoice Date from CCYYMMDD to MMDDYY for display
      *
     C                   if        ihindt <> 0
     C     *iso          test(d)                 ihindt                 92
     C                   if        *in92 = *off                                 If OK date
     C                   move      ihindt        wkisodate
     C     *mdy          move      wkisodate     d1indtmdy
     C                   endif                                                  If OK date
     C                   endif
      *
      *
      * Flip Due Date from CCYYMMDD to MMDDYY for display
      *
     C                   if        ihdudt <> 0
     C     *iso          test(d)                 ihdudt                 92
     C                   if        *in92 = *off                                 If OK due
     C                   move      ihdudt        wkisodate
     C     *mdy          move      wkisodate     d1dudtmdy
     C                   endif                                                  If OK due
     C                   endif
      *
      *
      * Flip Invoice Posted Date from CCYYMMDD to MMDDYY for display
      *
     C                   if        ihpodt = 0
     C                   z-add     0             d1podtmdy
     C                   else
     C     *iso          test(d)                 ihpodt                 92
     C                   if        *in92 = *off                                 If OK posted
     C                   move      ihpodt        wkisodate
     C     *mdy          move      wkisodate     d1podtmdy
     C                   endif                                                  If OK posted
     C                   endif
      *
      *
      * Only include records that match the user's selectors
      *
     C                   if        c1insn <> 0 and                              If invoice nbr
     C                             c1insn <> d1insn
     C                   else
      *
     C                   if        c1instcd <> *blank and                       If status
     C                             c1instcd <> d1instcd
     C                   else
      *
     C                   if        c1cfqcd <> *blank and                        If frequency
     C                             c1cfqcd <> d1cfqcd
     C                   else
      *
     C                   if        c1dudtmdy <> 0 and                           If due date
     C                             c1dudtmdy <> d1dudtmdy
     C                   else
      *
     C                   if        c1podtmdy <> 0 and                           If posted date
     C                             c1podtmdy <> d1podtmdy
     C                   else
      *
     C                   add       1             count
     C                   add       1             hdrrn1
     C                   move      *blank        d1opt
     C                   write     tf433s1
     C                   seton                                        81
      *
      * For positioning, place the subfile relative record number into
      * the sflrcdnbr field (hdpag1)
      *
     C                   eval      hdpag1 = hdrrn1
      *
     C                   endif                                                  If posted date
     C                   endif                                                  If due date
     C                   endif                                                  If frequency
     C                   endif                                                  If status
     C                   endif                                                  If invoice nbr
     C                   endif                                                  If process
     C                   endif                                                  If read
     C                   enddo                                                  Do fill
      *
      * If no records were written to the subfile, then issue
      * the standard message 'No data to Display'.
      *
     C                   if        hdrrn1 = 0                                   If no recs
     C                   eval      norecs = yes
      *
     C                   if        error < maxmsg
     C                   add       1             error
     C                   eval      mgid(e) = 'HS09002'
     C                   endif
      *
     C                   else
     C                   eval      norecs = no
     C                   endif                                                  If no recs
      *
     C                   eval      svrrn1 = hdrrn1
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Call the create program when the user takes F6
      *---------------------------------------------------------------
      *
     C     $creat        begsr
      *
     C                   call      'TF133'
     C                   parm      create        mode
     C                   parm      0             d1insn
     C                   parm      '00'          rtncd
      *
     C                   endsr
      /EJECT
      *--------------------------------------------------------------------------------------
      * Read/edit changed subfile records for valid subfile options on screen 1
      *--------------------------------------------------------------------------------------
      *
     C     $edop1        begsr
      *
     C                   eval      error = 0
      *
      * Read all changed subfile records, editing for valid options
      *
     C                   setoff                                       80
     C                   dou       *in80 = *on                                  Do readc
     C                   readc     tf433s1                                80
     C                   if        *in80 = *off                                 If rec read
      *
      * Set off error indicator for subfile option field
      *
     C                   setoff                                       30
      *
      * Convert option user entered to standard format. For example,
      * a ' 1', '01', and '1 ' must be standardized to ' 1'.
      *
     C                   if        wkopt2 = *blank                              If 2 = blank
     C                   eval      wkopt2 = wkopt1
     C                   eval      wkopt1 = *blank
     C                   endif                                                  If 2 = blank
      *
     C                   if        wkopt1 = *zero                               If 1 is zero
     C                   eval      wkopt1 = *blank
     C                   endif                                                  If 1 is zero
      *
      * Test for valid subfile option entry on all changed records
      *
     C                   if        d1opt <> ' 2' and                            If bad opt
     C                             d1opt <> ' 4' and
     C                             d1opt <> ' 5' and
     C                             d1opt <> ' 6' and
     C                             d1opt <> ' 7' and
     C                             d1opt <> '15' and
     C                             d1opt <> '18' and
     C                             d1opt <> '  '
     C                   seton                                        30
     C                   if        error < maxmsg
     C                   add       1             error
     C                   eval      mgid(e) = 'HS09006'
     C                   exsr      $err1
     C                   endif
     C                   endif                                                  If bad opt
      *
      *
      * Make additional checks
      *
     C                   select
      * Revising
     C                   when      d1opt = ' 2'
     C                   exsr      $opt2
      * Deleting
     C                   when      d1opt = ' 4'
     C                   exsr      $opt4
      * Print preview
     C                   when      d1opt = ' 6'
     C                   exsr      $opt6
      *
      * Assign/Unassign Charges
     C                   when      d1opt = ' 7'
     C                   exsr      $opt7
      * Post/print
     C                   when      d1opt = '15'
     C                   exsr      $opt15
      * Reprint
     C                   when      d1opt = '18'
     C                   exsr      $opt18
     C                   endsl
      *
      * Rewrite the subfile record with the modified data tag on so it
      * will be reread and processed.
      *
     C                   if        error = 0 and d1opt = *blank
     C                   setoff                                       85
     C                   else
     C                   seton                                        85
     C                   endif
      *
     C                   update    tf433s1
     C                   setoff                                       8530
     C                   endif                                                  If rec read
     C                   enddo                                                  Do readc
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------------------------------
      * Edits for Option 2=Revise
      *---------------------------------------------------------------------------------------
      *
      * You cannot revise an Invoice when:
      *   1) the status is Posted
      *
     C     $opt2         begsr
      *
      * Invoice has a status of Posted
      *
     C                   if        d1instcd = 'P'                               If posted
     C                   seton                                        30
     C                   if        error < maxmsg
     C                   add       1             error
     C                   eval      mgid(e) = 'HS08034'
     C                   exsr      $err1
     C                   endif
     C                   endif                                                  If posted
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------------------------------
      * Edits for Option 4=Delete
      *---------------------------------------------------------------------------------------
      *
      * You cannot delete an Invoice when:
      *   1) the status is Posted
      *
     C     $opt4         begsr
      *
      * Invoice has a status of Posted
      *
     C                   if        d1instcd = 'P'                               If posted
     C                   seton                                        30
     C                   if        error < maxmsg
     C                   add       1             error
     C                   eval      mgid(e) = 'HS08033'
     C                   exsr      $err1
     C                   endif
     C                   endif                                                  If posted
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------------------------------
      * Edits for Option 6=Print Preview
      *---------------------------------------------------------------------------------------
      *
      * You can only print a preview when:
      *   1) the status is Assigned
      *
     C     $opt6         begsr
      *
      * Invoice does not have a status of Assigned
      *
     C                   if        d1instcd <> 'A'                              If not assigned
     C                   seton                                        30
     C                   if        error < maxmsg
     C                   add       1             error
     C                   eval      mgid(e) = 'HS08035'
     C                   exsr      $err1
     C                   endif
     C                   endif                                                  If not assigned
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------------------------------
      * Edits for Option 7=Assign/Unassign Charges
      *---------------------------------------------------------------------------------------
      *
      * This option is only valid when the Invoice Status is not Posted.
      *
     C     $opt7         begsr
      *
      * Invoice has a status of Posted
      *
     C                   if        d1instcd = 'P'                               If posted
     C                   seton                                        30
     C                   if        error < maxmsg
     C                   add       1             error
     C                   eval      mgid(e) = 'HS08038'
     C                   exsr      $err1
     C                   endif
     C                   endif                                                  If posted
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------------------------------
      * Edits for Option 15=Post/Print
      *---------------------------------------------------------------------------------------
      *
      * You can only post/print when:
      *   1) the status is Assigned
      *
     C     $opt15        begsr
      *
      * Invoice does not have a status of Assigned
      *
     C                   if        d1instcd <> 'A'                              If not assigned
     C                   seton                                        30
     C                   if        error < maxmsg
     C                   add       1             error
     C                   eval      mgid(e) = 'HS08036'
     C                   exsr      $err1
     C                   endif
     C                   endif                                                  If not assigned
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------------------------------
      * Edits for Option 18=Reprint
      *---------------------------------------------------------------------------------------
      *
      * You can only Reprint an Invoice when:
      *   1) the status is Posted
      *
     C     $opt18        begsr
      *
      * Invoice does not have a status of Posted
      *
     C                   if        d1instcd <> 'P'                              If not assigned
     C                   seton                                        30
     C                   if        error < maxmsg
     C                   add       1             error
     C                   eval      mgid(e) = 'HS08037'
     C                   exsr      $err1
     C                   endif
     C                   endif                                                  If not assigned
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------------------------------
      * Read subfile records and process user selections for screen 1
      *---------------------------------------------------------------------------------------
      *
     C     $proc1        begsr
      *
     C                   z-add     0             error
     C                   move      '00'          rtncd
      *
      * Read all the changed subfile records processing the subfile
      * selection
      *
     C                   setoff                                       80
     C                   dou       *in80 = *on                                  Do process
     C                   readc     tf433s1                                80
     C                   if        *in80 = *off                                 If rec read
     C                   if        rtncd = '00'                                 If enter
      *
      * Option: Revise
     C                   select
     C                   when      d1opt = ' 2'
     C                   eval      reset = yes
     C                   call      'TF133'
     C                   parm      revise        mode
     C                   parm                    d1insn
     C                   parm                    rtncd
      *
      * Option: Delete
     C                   when      d1opt = ' 4'
     C                   eval      reset = yes
     C                   call      'TF133'
     C                   parm      delete        mode
     C                   parm                    d1insn
     C                   parm                    rtncd
      *
      * Option: View
     C                   when      d1opt = ' 5'
     C                   eval      reset = yes
     C                   call      'TF733'
     C                   parm                    d1insn
     C                   parm                    rtncd
      *
      * Option: Print Invoice Preview
      *
     C                   when      d1opt = ' 6'
     C                   eval      reset = yes
     C                   exsr      $list
      *
      * Option: Assign/Unassign Invoice Charges
      *
     C                   when      d1opt = ' 7'
     C                   eval      reset = yes
     C                   call      'TF134'
     C                   parm                    d1insn
     C                   parm                    rtncd
      *
      * Option: Post/print Invoice
      *
     C                   when      d1opt = '15'
     C                   eval      reset = yes
     C                   exsr      $list
      *
      * Option: Reprint Invoice
      *
     C                   when      d1opt = '18'
     C                   eval      reset = yes
     C                   exsr      $list
     C                   endsl
      *
      * Upon returning from the called program,
      * clear the subfile option selected by the user and rewrite
      * the subfile record.
      *
     C                   eval      d1opt = *blank
     C                   setoff                                       85
     C                   else
     C                   seton                                        85
     C                   endif                                                  If enter
      *
     C                   update    tf433s1
     C                   endif                                                  If rec read
      *
     C                   enddo                                                  Do process
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * F4 prompt on screen 1
      *---------------------------------------------------------------
      *
     C     $f4s1         begsr
      *
      * Include posted invoices
      *
     C                   select
     C                   when      fld = 'C1YNFL'
     C                   call      'HP522'
     C                   parm                    xxynfl
     C                   seton                                        51
     C                   if        xxynfl <> *blank
     C                   movel     xxynfl        c1ynfl
     C                   endif
      *
      * Invoice status code
      *
     C                   when      fld = 'C1INSTCD'
     C                   call      'TF512'
     C                   parm      *blank        xxinstcd
     C                   parm      *blank        xxinstds
     C                   seton                                        52
     C                   if        xxinstcd <> *blank
     C                   move      xxinstcd      c1instcd
     C                   endif
      *
      * Charge frequency code
      *
     C                   when      fld = 'C1CFQCD'
     C                   call      'TF508'
     C                   parm      *blank        xxcfqcd
     C                   parm      *blank        xxcfqds
     C                   seton                                        53
     C                   if        xxcfqcd <> *blank
     C                   movel     xxcfqcd       c1cfqcd
     C                   endif
     C                   other
      *
      * F4 not valid for this field...issue message
      *
     C                   if        error < maxmsg
     C                   add       1             error                                        013
     C                   movel     'HS09011'     mgid(e)
     C                   endif
      *
     C                   endsl
      *
     C                   endsr
      /EJECT
      *-----------------------------------------------------------------------------------
      * Set the subfile to redisplay at the first page that has errors on screen 1
      *-----------------------------------------------------------------------------------
      *
     C     $err1         begsr
      *
     C                   if        error = 1
     C                   eval      hdpag1 = hdrrn1
     C                   endif
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Seton last record and end job
      *---------------------------------------------------------------
      *
     C     $exit         begsr
     C                   seton                                        lr
     C                   endsr
      *
      *---------------------------------------------------------------
      * Write error messages
      *---------------------------------------------------------------
      *
     C     $wrmsg        begsr
      *
     C                   call      'UT80045J'                           43
     C                   parm                    mgid
     C                   parm                    mgdt
     C                   parm                    error
     C                   parm      sdpgm         pgm
     C                   parm      msgfil        msgfl
      *
      * If call to UT80045J failed, a message hardcoded in the DSPF
      * command line format will be set on. So, redisplay screen.
      *
     C                   if        *in43 = *on
     C                   eval      routne = set1
     C                   endif
      *
     C                   endsr
      *
      *----------------------------------------------------------------
      * Clear messages
      *----------------------------------------------------------------
      *
     C     $clmsg        begsr
      *
     C                   call      'UT80045J'                           43
     C                   parm      *blank        mgi
     C                   parm      *blank        mgd
     C                   parm      *zero         error
     C                   parm      sdpgm         pgm
     C                   parm      msgfil        msgfl
      *
      * If call to UT80045J failed, a message hardcoded in the DSPF
      * command line format will be set on; so, redisplay screen.
      *
     C                   if        *in43 = *on
     C                   eval      routne = set1
     C                   endif
      *
     C                   endsr
      /EJECT
      *----------------------------------------------------------------
      * Initialize LDA
      *----------------------------------------------------------------
      *
     C     $initlda      begsr
      *
     C                   move      *blank        ldtask
     C                   move      *blank        ldynfl
     C                   z-add     0             ldinsn
     C                   z-add     0             ldfmdy
     C                   z-add     0             ldfcymd
     C                   z-add     0             ldtmdy
     C                   z-add     0             ldtcymd
     C                   z-add     0             lddmdy
     C                   z-add     0             lddcymd
     C                   z-add     0             ldpmdy
     C                   z-add     0             ldpcymd
     C                   move      *blank        ldinstcd
     C                   move      *blank        ldcfqcd
      *
     C                   endsr
      /EJECT
      *----------------------------------------------------------------
      * Initialization subroutine
      *----------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Parm lists
      *   None
      *
      * Key lists
      *   None
      *
      * Retrieve/initialize "printer" fields in the LDA
      *
     C                   call      'RO891CL'
     C                   in        lda
      *
      * Initialize LDA fields
     C                   exsr      $initlda
     C                   out       lda
      *
      * Default to 'no' the value for control field 'include posted invoices'
      *
     C                   move      no            c1ynfl
     C                   move      no            svynfl
      *
      * To support F5-Refresh, logic requires the number of lines on
      * the subfile page and also the number of lines less 1.  By
      * setting these values up as constant work fields, the processing
      * logic can be standardized between programs.
      *
     C                   z-add     10            wkplin
     C     wkplin        sub       1             wkrlin
      *
      * The following standard code must be included to make the
      * standard error message handling program function properly.
      * This code sets and clears the program message queue.
      *
     C                   movel     '*'           msgpgm
     C                   exsr      $clmsg
      *
     C                   endsr
      /EJECT
      *****************************************************************
      * COMPILE TIME ARRAY 'CMD' USED IN QCMDEXC
      *****************************************************************
** CMD table 'CMD' submits listing when F7 selected
SBMJOB CMD(CALL PGM(TF433CL )) JOB(INVOICE  ) JOBQ(*JOBD)
