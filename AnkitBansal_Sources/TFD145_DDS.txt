     A*%%TS  SD  20061010  111500  ISLRAMS     REL-V5R3M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        TRIUMPH FOODS
     A*  DISPLAY FILE:  TFD145
     A*  TITLE:         REVISE PRODUCT SPLIT
     A*  PROGRAMMER:    LEANNE RAMSEY
     A*  CREATED:       10/10/06
     A*
     A*
     A*  INDICATOR 60 IS ON WHEN IN REVISE MODE
     A*  INDICATOR 61 IS ON WHEN IN CREATE MODE
     A*  INDICATOR 62 IS ON WHEN IN DELETE MODE
     A*  INDICATOR 63 IS ON WHEN WE ARE PROTECTING EVERY FIELD EXCEPT STATUS
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/TFPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A N62                                  CF04(04 'PROMPT')
     A  60N62                               CF05(05 'REFRESH')
     A  60N62N63                            CF11(11 'DELETE')
     A*
     A*---------------------------------------------------------------
     A* RECORD FORMAT ONE
     A*---------------------------------------------------------------
     A          R TF145R1
     A*%%TS  SD  20061010  111500  ISLRAMS     REL-V5R3M0  5722-WDS
     A                                      SETOF(30)
     A                                      SETOF(31)
     A                                      SETOF(32)
     A                                      SETOF(33)
     A                                      SETOF(34)
     A                                      SETOF(35)
     A                                      SETOF(36)
     A                                      SETOF(51)
     A                                      SETOF(53)
     A                                      SETOF(54)
     A                                      SETOF(55)
     A                                      SETOF(56)
     A                                      OVERLAY
     A                                      RTNCSRLOC(&RCD &FLD)
     A            RCD       R        H      REFFLD(@@RCD)
     A            FLD       R        H      REFFLD(@@FLD)
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 25'REVISE PRODUCT SPLIT'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USR)
     A                                      DSPATR(HI)
     A            D1MODE         6A  O  2  2DSPATR(HI)
     A                                  2 25'FOR MARGIN ADJUSTMENT'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@JOB)
     A                                      DSPATR(HI)
     A                                  4 33'Week-ending'
     A                                      DSPATR(UL)
     A                                      COLOR(BLU)
     A                                  5  2'Effective date ..'
     A            D1EFMDY   R     Y  B  5 22REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A  30                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A  61N30                               DSPATR(HI)
     A  61                                  DSPATR(UL)
     A N61                                  DSPATR(PR)
     A            D1WEMDY   R     Y  O  5 34REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A                                      COLOR(BLU)
     A                                  6  2'Product code ....'
     A  61                              6 20'?'
     A            D1PRCD    R        B  6 22REFFLD(TFREF/@@PRCD *LIBL/TFPREF)
     A                                      EDTCDE(M)
     A  31
     AO 51                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A  61N31                               DSPATR(HI)
     A  61                                  DSPATR(UL)
     A N61                                  DSPATR(PR)
     A            D1PRDS    R        O  6 33REFFLD(TFREF/@@PRDS *LIBL/TFPREF)
     A                                  8  2'Split percent ...'
     A            D1SPLTPC  R        B  8 22REFFLD(TFREF/@@SPLTPC *LIBL/TFPREF)
     A                                      EDTCDE(K)
     A  32                                  DSPATR(PC)
     A  32                                  DSPATR(RI)
     A N62N63N32                            DSPATR(HI)
     A N62N63                               DSPATR(UL)
     A  62
     AO 63                                  DSPATR(PR)
     A                                 10  2'Item structure type code ..'
     A N62N63                          10 30'?'
     A            D1ISTYCD  R        B 10 32REFFLD(TFREF/@@ISTYCD *LIBL/TFPREF)
     A                                      EDTCDE(M)
     A  33
     AO 53                                  DSPATR(PC)
     A  33                                  DSPATR(RI)
     A N62N63N33                            DSPATR(HI)
     A N62N63                               DSPATR(UL)
     A  62
     AO 63                                  DSPATR(PR)
     A            D1ISTYDS  R        O 10 38REFFLD(TFREF/@@ISTYDS *LIBL/TFPREF)
     A                                 11  2'TF class group code .......'
     A N62N63                          11 30'?'
     A            D1TFCGCD  R        B 11 32REFFLD(TFREF/@@TFCGCD *LIBL/TFPREF)
     A  34
     AO 54                                  DSPATR(PC)
     A  34                                  DSPATR(RI)
     A N62N63N34                            DSPATR(HI)
     A N62N63                               DSPATR(UL)
     A  62
     AO 63                                  DSPATR(PR)
     A            D1TFCGDS  R        O 11 38REFFLD(TFREF/@@TFCGDS *LIBL/TFPREF)
     A                                 12  2'TF classification code ....'
     A N62N63                          12 30'?'
     A            D1TFCLCD  R        B 12 32REFFLD(TFREF/@@TFCLCD *LIBL/TFPREF)
     A  35
     AO 55                                  DSPATR(PC)
     A  35                                  DSPATR(RI)
     A N62N63N35                            DSPATR(HI)
     A N62N63                               DSPATR(UL)
     A  62
     AO 63                                  DSPATR(PR)
     A            D1TFCLDS  R        O 12 38REFFLD(TFREF/@@TFCLDS *LIBL/TFPREF)
     A                                 13  2'Active/inactive status ....'
     A N62                             13 30'?'
     A            D1AIST    R        B 13 32REFFLD(TFREF/@@AIST *LIBL/TFPREF)
     A  36
     AO 56                                  DSPATR(PC)
     A  36                                  DSPATR(RI)
     A N62N36                               DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(PR)
     A*
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R TF145K1
     A  43                             22  2'Message program failed.   Call MIS-
     A                                       user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A N62                             23 11'F4=Prompt'
     A N62N61                          23 22'F5=Refresh'
     A N62N61N63                       23 34'F11=Delete'
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *---------------------------------------------------------------
     A          R TF145ES                   SFL SFLMSGRCD(24)
      *
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
     A*---------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*---------------------------------------------------------------
     A*
     A          R TF145EC                   SFLCTL(TF145ES)
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
