      ****************************************************************
      *
      * SYSTEM:      HOG PRODUCTION
      * PROGRAM:     HP656
      * TITLE:       LISTING OF RATION CALORIES
      * PROGRAMMER:  LEANNE FEDOR
      * CREATED:     12/07/94
      *
      * FUNCTION: BATCH PROGRAM SUBMITTED FROM SPECIFY RATION CALORIES
      *           TO MAINTAIN (HP456). THE CALORIES WILL BE PRINTED
      *           FOR A SPECIFIC EFFECTIVE DATE.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 06/18/97  LEANNE FEDOR
      *           REPLACED ENTRY PARM LOGIC WITH LDA VALUES.
      *
      * 05/31/07  LeAnne Ramsey
      *           Some users are having printing problems. So, we are switching
      *           from QPRINT to PRINT132 for the printer file and replacing
      *           John Erikson's customized printer override command in the CL with
      *           the standard override command.
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     FHSL073A   IF   E           K DISK
      *    RATION CALORIES           (KEY: CPCEDT (DESCEND) CPRNCD)
      *
     FHSP071    IF   E           K DISK
      *    RATION                    (KEY: RNRNCD)
      *
     Fprint132  O    F  132        PRINTER OFLIND(*INOF)
      *
      /EJECT
      ****************************************************************
      * INPUT SPECS
      ****************************************************************
      *
      *---------------------------------------------------------------
      * STANDARD PROGRAM STATUS DATA STRUCTURE
      *---------------------------------------------------------------
      *    EXTERNALLY DEFINED AS UTPGFR (RECORD FORMAT: PGMDSR)
     D PGMDS         ESDS                  EXTNAME(UTPGFR)
      *
      *
      *---------------------------------------------------------------
      *  NAMED CONSTANTS
      *---------------------------------------------------------------
      *
     D YES             C                   CONST('Y')
     D NO              C                   CONST('N')
      *
      *----------------------------------------------------------------
      * LDA - REPORT SELECTIONS
      *----------------------------------------------------------------
      *
     D                UDS
     D  LDEFDT                 1      8  0
     D  LDMDY                  9     14  0
      /EJECT
      ****************************************************************
      * MAIN LINE
      ****************************************************************
      *
      * PRINT HEADINGS FOR FIRST PAGE AND THEN
      * READ/PROCESS ALL CALORIE RECORDS FOR SELECTED DATE.
      *
     C     LDEFDT        SETLL     HSL073A
     C                   EXCEPT    HDRPT
      *
     C     *IN92         DOUEQ     *ON                                          MAIN DO
     C     LDEFDT        READE     HSL073A                                92
     C     *IN92         IFEQ      *OFF                                         IF NOT EOF
      *
      * RETRIEVE RATION DESCRIPTION.
      *
     C     CPRNCD        CHAIN     HSP071                             93
     C     *IN93         IFEQ      *OFF
     C                   MOVEL     RNRNDS        WKRNDS
     C                   ELSE
     C                   MOVEL(P)  'UNKNOWN'     WKRNDS
     C                   ENDIF
      *
      * TEST FOR OVERFLOW AND PRINT HEADINGS IF ON
      *
     C     *INOF         IFEQ      *ON
     C                   EXCEPT    HDRPT
     C                   ENDIF
      *
      * PRINT DETAIL LINE
      *
     C                   EXCEPT    DTL
     C                   MOVEL     YES           PRTFLG
      *
     C                   ENDIF                                                  IF NOT EOF
     C                   ENDDO                                                  MAIN DO
      *
      * PRINT STANDARD 'NO DATA...' ERROR MESSAGE IF NO DATA MET THE
      * USER'S SELECTION CRITERIA
      *
     C     PRTFLG        IFEQ      NO
     C                   EXCEPT    NODATA
     C                   ENDIF
      *
     C                   SETON                                        LR
      /EJECT
      *---------------------------------------------------------------
      * *INZSR - INITIALIZATION SUBROUTINE
      *---------------------------------------------------------------
      *
     C     *INZSR        BEGSR
      *
      * PRINT FIELD
     C     *LIKE         DEFINE    RNRNDS        WKRNDS
      *
      * SET PRINT FLAG TO DETERMINE IF ANY RECORDS WERE PRINTED
      * SET UP DASHED LINE THAT APPEARS UNDER HEADINGS
      * RETRIEVE TIME FOR REPORT HEADING
      *
     C                   MOVE      NO            PRTFLG            1
     C                   MOVE      *ALL'-'       DASH             80
     C                   TIME                    RTIME             6 0
      *
     C                   ENDSR
      /EJECT
      *-------------------------------------------------------------
      * REPORT HEADING LINES
      *-------------------------------------------------------------
      *
     Oprint132  E            HDRPT          1 01
     O                       SDPGM               10
     O                                           49 'HOG PRODUCTION SYSTEM'
     O                                           70 'DATE'
     O                       UDATE         Y     80
      *
     O          E            HDRPT          1
     O                       SDUSR               10
     O                                           42 'LISTING OF RATION'
     O                                           51 'CALORIES'
     O                                           70 'TIME'
     O                       RTIME               80 '  :  :  '
      *
     O          E            HDRPT          1
     O                                           70 'PAGE'
     O                       PAGE          Z     75
      *
     O          E            HDRPT          2
     O                                           15 'EFFECTIVE DATE:'
     O                       LDMDY         Y     25
      *
     O          E            HDRPT          1
     O                                            8 'RATION'
     O                                           25 'DESCRIPTION'
     O                                           60 'CALORIES PER POUND'
      *
     O          E            HDRPT          1
     O                       DASH                80
      *
      *-------------------------------------------------------------
      *  DETAIL LINE
      *-------------------------------------------------------------
      *
     O          E            DTL         1
     O                       CPRNCD               7
     O                       WKRNDS              34
     O                       CPCLRT        K     55
      *
      *-------------------------------------------------------------
      * NO DATA MESSAGE LINE
      *-------------------------------------------------------------
      *
     O          E            NODATA      1
     O                                           19 'NO DATA MEETS YOUR'
     O                                           38 'SELECTION CRITERIA'
