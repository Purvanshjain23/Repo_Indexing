     A*%%TS  SD  20090807  131720  ISLRAMS     REL-V5R4M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD450
     A*  TITLE:         WORK WITH FARM SITE
     A*  PROGRAMMER:    ROSE CENTONZE
     A*  DATE:          03/08/94
     A*
     A*****************************************************************
     A* MODIFICATIONS:
     A* 06/18/97  LEANNE FEDOR
     A*           ADDED A FUNCTION KEY FOR PRINT OPTIONS.
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A N99                                  CF04(04 'PROMPT')
     A N99                                  CF05(05 'REFRESH')
     A N99 84
     AON99 87                               CF06(06 'CREATE')
     A N99                                  CF07(07 'LISTING')
     A N99                                  CF13(13 'FOLD/DROP')
     A N99                                  CF14(14 'VIEW CC')
     A N99                                  CF15(15 'VIEW FARMS')
     A N99                                  CF17(17 'PRINT OPTIONS')
     A N99                                  CF18(18 'LIST DEATHS')
     A N99                                  CF19(19 'LIST BINS')
     A N99                                  CF20(20 'WRKSPLF')
     A*
     A*
     A*---------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP450S1                   SFL
     A*%%TS  SD  20030109  153854  ISLRAMS     REL-V5R1M0  5722-WDS
     A  85                                  SFLNXTCHG
     A            HDRRN1         4Y 0H
     A            DFOPT          2A  B 13  2CHGINPDFT(UL)
     A  30                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A N30                                  DSPATR(HI)
     A  99                                  DSPATR(PR)
     A            DFFSCD    R        O 13  6REFFLD(FSFSCD)
     A                                      EDTCDE(M)
     A            DFCNFSCD  R        O 13 13REFFLD(HSRREF/@@CNFSCD *LIBL/HSPREF)
     A                                      EDTCDE(M)
     A            DFTFCD    R        O 13 21REFFLD(HSRREF/@@TFCD *LIBL/HSPREF)
     A            DFGTCD    R        O 13 30REFFLD(HSRREF/@@GTCD *LIBL/HSPREF)
     A            DFPOD     R        O 13 34REFFLD(HSRREF/@@POD *LIBL/HSPREF)
     A                                      EDTCDE(Z)
     A            DFCELL    R        O 13 40REFFLD(HSRREF/@@CELL *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A            DFPTCD    R        O 13 45REFFLD(FSPTCD)
     A            DFPPCD    R        O 13 51REFFLD(FSPPCD)
     A            DFCMGCD   R        O 13 58REFFLD(FSREC/FSCMGCD *LIBL/HSP018)
     A            DFPMGCD   R        O 13 64REFFLD(FSREC/FSPMGCD *LIBL/HSP018)
     A            DFMMGCD   R        O 13 70REFFLD(FSREC/FSMMGCD *LIBL/HSP018)
     A            DFAIST    R        O 13 77REFFLD(HSRREF/@@AIST *LIBL/HSPREF)
     A            DFFSNM    R        O 14  6REFFLD(FSFSNM)
     A*
     A*---------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP450C1                   SFLCTL(HP450S1)
     A*%%TS  SD  20030109  153854  ISLRAMS     REL-V5R1M0  5722-WDS
     A                                      SFLSIZ(0050)
     A                                      SFLPAG(0004)
     A N82N99                               ROLLUP(25 'PAGE FORWARD')
     A                                      SETOF(31)
     A                                      SETOF(50)
     A                                      SETOF(51)
     A                                      SETOF(52)
     A                                      SETOF(53)
     A                                      SETOF(54)
     A                                      SETOF(55)
     A                                      SETOF(56)
     A                                      SETOF(57)
     A                                      SETOF(58)
     A                                      SETOF(59)
     A                                      SETOF(77)
     A                                      RTNCSRLOC(&RCD &FLD)
     A                                      OVERLAY
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A  78                                  SFLFOLD(CF13)
     A  79                                  SFLDROP(CF13)
     A                                      SFLMODE(&FOLD)
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            FOLD           1A  H
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 26'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A            C1VERSION R        O  2  2REFFLD(HSRREF/@@DS20 *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A                                  2 26'WORK WITH FARM SITES'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  4  2'Business office .. ?'
     A            C1FSBO    R        B  4 23REFFLD(FSFSBO)
     A  31
     AO 51                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A N31                                  DSPATR(HI)
     A  99                                  DSPATR(PR)
     A            C1BODS    R        O  4 30REFFLD(BOBODS)
     A                                  6  2'5=View'
     A                                  6 12'6=Print'
     A                                  6 22'10=View Bins'
     A                                  6 40'8=View Buildings'
     A                                  6 62'12=View Deaths'
     A  84
     AO 87
     AO 88                              7  2'2=Revise'
     A  84
     AO 87                              7 12'4=Delete'
     A  84
     AO 89                              7 23'9=Maintain Bins'
     A  84
     AO 88                              7 40'7=Maintain Buildings'
     A  84
     AO 87                              7 62'11=Maintain Deaths'
     A  84
     AO 88                              8  2'3=Capacity/Managers'
     A  84
     AO 89                              8 22'13=Assign Bin Sets to Rooms'
     A                                  9 15'?'
     A                                      DSPATR(HI)
     A                                  9 22'?'
     A                                      DSPATR(HI)
     A                                  9 30'?'
     A                                      DSPATR(HI)
     A                                  9 47'?'
     A                                      DSPATR(HI)
     A                                  9 53'?'
     A                                      DSPATR(HI)
     A                                  9 59'?'
     A                                      DSPATR(HI)
     A                                  9 65'?'
     A                                      DSPATR(HI)
     A                                  9 72'?'
     A                                      DSPATR(HI)
     A                                  9 78'?'
     A                                      DSPATR(HI)
     A                                 10 13'Consol'
     A                                      DSPATR(HI)
     A                                 10 21'Farm'
     A                                      DSPATR(HI)
     A                                 10 28'Group'
     A                                      DSPATR(HI)
     A                                 10 45'Production'
     A                                      DSPATR(HI)
     A                                 10 58'--- Managers ----'
     A                                      DSPATR(HI)
     A                                 10 77'A/I'
     A                                      DSPATR(HI)
     A                                 11  2'Opt'
     A                                      DSPATR(HI)
     A                                 11  7'Farm'
     A                                      DSPATR(HI)
     A                                 11 14'Farm'
     A                                      DSPATR(HI)
     A                                 11 21'Type'
     A                                      DSPATR(HI)
     A                                 11 28'Type'
     A                                      DSPATR(HI)
     A                                 11 34'Pod'
     A                                      DSPATR(HI)
     A                                 11 39'Cell'
     A                                      DSPATR(HI)
     A                                 11 45'Type'
     A                                      DSPATR(HI)
     A                                 11 51'Phase'
     A                                      DSPATR(HI)
     A                                 11 58'Cell'
     A                                      DSPATR(HI)
     A                                 11 64'Prod'
     A                                      DSPATR(HI)
     A                                 11 70'Multi'
     A                                      DSPATR(HI)
     A                                 11 77'St'
     A                                      DSPATR(HI)
     A            C1FSCD    R        B 12  6REFFLD(FSFSCD)
     A                                      EDTCDE(M)
     A                                      DSPATR(HI)
     A  52
     AON31N51                               DSPATR(PC)
     A  99                                  DSPATR(PR)
     A            C1CNFSCD  R        B 12 13REFFLD(HSRREF/@@CNFSCD *LIBL/HSPREF)
     A                                      EDTCDE(M)
     A                                      DSPATR(HI)
     A  58                                  DSPATR(PC)
     A  99                                  DSPATR(PR)
     A            C1TFCD    R        B 12 21REFFLD(HSRREF/@@TFCD *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A  59                                  DSPATR(PC)
     A  99                                  DSPATR(PR)
     A            C1GTCD    R        B 12 30REFFLD(HSRREF/@@GTCD *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A  50                                  DSPATR(PC)
     A  99                                  DSPATR(PR)
     A            C1POD     R        B 12 34REFFLD(HSRREF/@@POD *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A  99                                  DSPATR(PR)
     A            C1CELL    R        B 12 40REFFLD(HSRREF/@@CELL *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A                                      EDTCDE(K)
     A  99                                  DSPATR(PR)
     A            C1PTCD    R        B 12 45REFFLD(FSPTCD)
     A                                      DSPATR(HI)
     A  53                                  DSPATR(PC)
     A  99                                  DSPATR(PR)
     A            C1PPCD    R        B 12 51REFFLD(FSPPCD)
     A                                      DSPATR(HI)
     A  54                                  DSPATR(PC)
     A  99                                  DSPATR(PR)
     A            C1CMGCD   R        B 12 58REFFLD(FSREC/FSCMGCD *LIBL/HSP018)
     A                                      DSPATR(HI)
     A  56                                  DSPATR(PC)
     A  99                                  DSPATR(PR)
     A            C1PMGCD   R        B 12 64REFFLD(FSREC/FSPMGCD *LIBL/HSP018)
     A                                      DSPATR(HI)
     A  55                                  DSPATR(PC)
     A  99                                  DSPATR(PR)
     A            C1MMGCD   R        B 12 70REFFLD(FSREC/FSMMGCD *LIBL/HSP018)
     A                                      DSPATR(HI)
     A  57                                  DSPATR(PC)
     A  99                                  DSPATR(PR)
     A            C1AIST    R        B 12 77REFFLD(HSRREF/@@AIST *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A  77                                  DSPATR(PC)
     A  99                                  DSPATR(PR)
     A*
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R HP450K1
     A*%%TS  SD  20090807  131720  ISLRAMS     REL-V5R4M0  5722-WDS
     A  43                             21  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 22  2'F3=Exit'
     A N99                             22 11'F4=Prompt'
     A N99                             22 21'F5=Refresh'
     A N99 87
     AON99 84                          22 32'F6=Create'
     A N99                             22 42'F7=Listing'
     A                                 22 54'F13=Fld/Unfld'
     A N99                             22 69'F14=View CC'
     A N99                             23  2'F15=View Farm-No CC'
     A N99                             23 23'F17=Print Opt'
     A N99                             23 38'F18=Death Rpts'
     A N99                             23 53'F19=Farm/Bin List'
     A N99                             23 71'F20=Spool'
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *---------------------------------------------------------------
     A          R HP450ES                   SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE CONTROL FORMAT
      *---------------------------------------------------------------
     A          R HP450EC                   SFLCTL(HP450ES)
      *
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
