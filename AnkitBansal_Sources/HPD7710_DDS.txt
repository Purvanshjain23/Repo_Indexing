     A*%%TS  SD  20041013  142443  ISLRAMS     REL-V5R2M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD7710
     A*  TITLE:         VIEW ITEMS
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  DATE:          04/13/04
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A                                      CF04(04 'PROMPT')
     A                                      CF05(05 'REFRESH')
     A                                      CF20(20 'WRKSPLF')
     A                                      CF21(21 'PRINT')
     A*
     A*
     A*---------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP7710S1                  SFL
     A*%%TS  SD  20041013  142443  ISLRAMS     REL-V5R2M0  5722-WDS
     A  85                                  SFLNXTCHG
     A            HDRRN1         4Y 0H
     A            D1ITCD    R        O 10  2REFFLD(HSRREF/@@ITCD *LIBL/HSPREF)
     A            D1BGIT    R        O 10 30REFFLD(HSRREF/@@BGIT *LIBL/HSPREF)
     A            D1ITTYCD  R        O 10 59REFFLD(@@ITTYCD)
     A            D1AIST    R        O 10 67REFFLD(HSRREF/@@AIST *LIBL/HSPREF)
     A            D1BGFL    R        O 10 72REFFLD(HSRREF/@@BGFL *LIBL/HSPREF)
     A            D1CRITFL  R        O 10 77REFFLD(HSRREF/@@CRITFL *LIBL/HSPREF)
     A            D1ITDS    R        O 11  7REFFLD(@@ITDS)
     A                                      COLOR(PNK)
     A                                 11 58'CPU Amt:'
     A                                      DSPATR(HI)
     A            D1CPUAM   R        O 11 67REFFLD(HSRREF/@@CPUAM *LIBL/HSPREF)
     A                                      COLOR(PNK)
     A                                      EDTCDE(K)
     A*
     A*
     A*---------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP7710C1                  SFLCTL(HP7710S1)
     A*%%TS  SD  20040728  154801  ISLRAMS     REL-V5R2M0  5722-WDS
     A                                      SFLSIZ(0050)
     A                                      SFLPAG(0006)
     A N82                                  ROLLUP(25 'PAGE FORWARD')
     A                                      SETOF(51)
     A                                      SETOF(52)
     A                                      SETOF(53)
     A                                      SETOF(54)
     A                                      SETOF(55)
     A                                      RTNCSRLOC(&RCD &FLD)
     A                                      OVERLAY
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 26'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  2 32'VIEW ITEMS'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  4 61'?'
     A                                      DSPATR(HI)
     A                                  4 67'?'
     A                                      DSPATR(HI)
     A                                  4 72'?'
     A                                      DSPATR(HI)
     A                                  4 77'?'
     A                                      DSPATR(HI)
     A                                  5 36'?'
     A                                      DSPATR(HI)
     A                                  5 59'Item'
     A                                      DSPATR(HI)
     A                                  6 59'Type'
     A                                      DSPATR(HI)
     A                                  6 66'AI'
     A                                      DSPATR(HI)
     A                                  6 70'Bdgt'
     A                                      DSPATR(HI)
     A                                  6 76'Crit'
     A                                      DSPATR(HI)
     A                                  7  2'Item Number'
     A                                      DSPATR(HI)
     A                                  7 30'Budget Item'
     A                                      DSPATR(HI)
     A                                  7 59'Code'
     A                                      DSPATR(HI)
     A                                  7 66'St'
     A                                      DSPATR(HI)
     A                                  7 70'Flag'
     A                                      DSPATR(HI)
     A                                  7 76'Flag'
     A                                      DSPATR(HI)
     A            C1ITCD    R        B  8  2REFFLD(HSRREF/@@ITCD *LIBL/HSPREF)
     A                                      CHGINPDFT(UL LC FE)
     A                                      DSPATR(HI)
     A            C1BGIT    R        B  8 30REFFLD(HSRREF/@@BGIT *LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      DSPATR(HI)
     A  51                                  DSPATR(PC)
     A            C1ITTYCD  R        B  8 59REFFLD(@@ITTYCD)
     A                                      DSPATR(HI)
     A  52                                  DSPATR(PC)
     A            C1AIST    R        B  8 67REFFLD(HSRREF/@@AIST *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A  53                                  DSPATR(PC)
     A            C1BGFL    R        B  8 72REFFLD(HSRREF/@@BGFL *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A  54                                  DSPATR(PC)
     A            C1CRITFL  R        B  8 77REFFLD(HSRREF/@@CRITFL *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A  55                                  DSPATR(PC)
     A                                  9  2'Desc'
     A                                      DSPATR(HI)
     A            C1ITDS    R        B  9  7REFFLD(HSRREF/@@ITDS *LIBL/HSPREF)
     A                                      CHGINPDFT(UL LC FE)
     A                                      DSPATR(HI)
     A*
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R HP7710K1
     A*%%TS  SD  20040719  170618  ISLRAMS     REL-V5R2M0  5722-WDS
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A                                 23 11'F4=Prompt'
     A                                 23 22'F5=Refresh'
     A                                 23 34'F20=Spool files'
     A                                 23 51'F21=Print'
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *---------------------------------------------------------------
     A          R HP7710ES                  SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE CONTROL FORMAT
      *---------------------------------------------------------------
     A          R HP7710EC                  SFLCTL(HP7710ES)
      *
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
