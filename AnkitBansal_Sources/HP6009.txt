      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Hog Production System
      * PROGRAM:     HP6009
      * TITLE:       Listing of Sire Lines
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     10/25/01
      *
      * FUNCTION:  Batch program to list the records in the file.
      *            This listing is submitted from HP4009-Work With Sire Lines
      *
      *            A pop-up window is called from HP4009 to allow user to enter
      *            listing options. THe W/W selectors are passed into this pop-up
      *            window.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 01/31/05  LeAnne Fedor
      *           New fields (Prime Line, Sire Line Company) were added to the
      *           Sire Line file---and to this listing. Also, the listing can now
      *           be generated in 4 different sort sequences.
      *
      * 10/14/10  LeAnne Ramsey   (E1079)
      *           Expanded the LDA 'Sire Line Company' fields from 1-character to 2-characters.
      *           So, adjusted position in LDA.
      *
      * 01/21/21  ISE              TSN593
      *           Replaced *loval with *start
      *
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fhsp098    if   e           k disk
      *    Sire lines  (records selected/keyed in open query)
      *
      *
     Fqprint    o    f  132        printer oflind(*inof)
      *
      /EJECT
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Standard fields
      *
     D dash            s            132
     d rtime           s              6  0
      *
      *
      * Control fields
      *
     D first           s              1    inz('Y')
     D svsicomp        s                   like(sisicomp)
     D svgg0           s                   like(sigg0)
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *---------------------------------------------------------------
      * Local data area.
      *---------------------------------------------------------------
      *
     Dlda             uds                  dtaara(*lda)
     D  ldlistcd               1      1
     D  ldlistds               2     36
     D  ldsicd                37     41
      *
     D  ldprime               43     45  0
     D  ldaist                46     46
     D  ldgg0                 47     51
     D  ldgg1                 52     56
     D  ldgg2                 57     61
      *
     D  ldsicomp              62     63
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * MAINLINE
      ****************************************************************
      *
      * Prepare to read the file and print headings the first time through.
      *
593  C*    *loval        setll     hsp098
593  C     *start        setll     hsp098
     C                   except    h1hdr
      *
      * Read all records in the file selected by the open query.
      *
     C                   dou       *in90 = *on                                  Main do
     C                   read      hsp098                                 90
     C                   if        *in90 = *off                                 If not EOF
      *
      * First time
     C                   if        first = yes
     C                   move      no            first
     C                   move      sisicomp      svsicomp
     C                   move      sigg0         svgg0
     C                   endif
      *
      * Print a blank line when:
      *   1) Company changes on the listing by Sire Line or Company
      *   2) Cognos Grouping changes on the listing by Cognos Grouping
      *
     C                   if        ((ldlistcd = 'S' or ldlistcd = 'C') and
     C                             sisicomp <> svsicomp) or
     C                             ldlistcd = 'R' and sigg0 <> svgg0
     C                   except    blank
     C                   endif
      *
      * Print detail line
     C                   if        *inof = *on
     C                   except    h1hdr
     C                   endif
      *
     C                   except    l1dtl
     C                   move      sisicomp      svsicomp
     C                   move      sigg0         svgg0
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Main do
      *
      *---------------------------------------------------------------
      * EOF processing
      *---------------------------------------------------------------
      * If the file was empty, print the standard 'no data...' message.
      *
     C                   if        first = yes
     C                   except    nodata
     C                   endif
      *
     C                   seton                                        lr
      /EJECT
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Set up a line of underscores and retrieve time for report heading.
      *
     C                   movel     *all'-'       dash
     C                   time                    rtime
      *
      * Set up indicators to control printing in the heading.
      *
      * User selected all Sire Lines
      *
     C                   if        ldsicd = *blank
     C                   seton                                        97
     C                   endif
      *
      * User selected all Sire Line Companies
      *
     C                   if        ldsicomp = *blank
     C                   seton                                        93
     C                   endif
      *
      * User selected all Prime Lines
      *
     C                   if        ldprime = 0
     C                   seton                                        99
     C                   endif
      *
      * User selected both Active and Inactive
      *
     C                   if        ldaist = *blank
     C                   seton                                        98
     C                   endif
      *
      * User selected all Cognos Report Groupings
      *
     C                   if        ldgg0 = *blank
     C                   seton                                        96
     C                   endif
      *
      * User selected all Alternate Report Grouping 1 values
      *
     C                   if        ldgg1 = *blank
     C                   seton                                        95
     C                   endif
      *
      * User selected all Alternate Report Grouping 2 values
      *
     C                   if        ldgg2 = *blank
     C                   seton                                        94
     C                   endif
      *
     C                   endsr
      /EJECT
      *-------------------------------------------------------------
      * Report heading lines
      *-------------------------------------------------------------
      *
     Oqprint    e            h1hdr          1 01
     O                       sdpgm               10
     O                                           82 'HOG PRODUCTION SYSTEM'
     O                                          122 'DATE'
     O                       udate         y    132
      *
     O          e            h1hdr          1
     O                       sdusr               10
     O                                           82 'LISTING OF SIRE LINES'
     O                                          122 'TIME'
     O                       rtime              132 '  :  :  '
      *
     O          e            h1hdr          2
     O                                          122 'PAGE'
     O                       page          z    132
      *
      *
     O          e            h1hdr          1
     O                                           25 'Sort sequence ...........'
     O                                           28 '..:'
     O                       ldlistcd            31
     O                       ldlistds            72
      *
     O          e            h1hdr          1
     O                                           25 'Sire line ...............'
     O                                           28 '..:'
     O              n97      ldsicd              35
     O               97                          33 'All'
      *
      *
     O          e            h1hdr          1
     O                                           25 'Sire line company .......'
     O                                           28 '..:'
     O              n93      ldsicomp            32
     O               93                          33 'All'
      *
      *
     O          e            h1hdr          1
     O                                           25 'Prime line ..............'
     O                                           28 '..:'
     O              n99      ldprime       z     33
     O               99                          33 'All'
      *
      *
     O          e            h1hdr          1
     O                                           25 'Active/inactive status ..'
     O                                           28 '..:'
     O              n98      ldaist              31
     O               98                          33 'All'
      *
      *
     O          e            h1hdr          1
     O                                           25 'Cognos report grouping ..'
     O                                           28 '..:'
     O              n96      ldgg0               35
     O               96                          33 'All'
      *
      *
     O          e            h1hdr          1
     O                                           25 'Alternate report grouping'
     O                                           28 ' 1:'
     O              n95      ldgg1               35
     O               95                          33 'All'
      *
      *
     O          e            h1hdr          2
     O                                           25 'Alternate report grouping'
     O                                           28 ' 2:'
     O              n94      ldgg2               35
     O               94                          33 'All'
      *
      *
     O          e            h1hdr          1
     O                                           13 'SIRE'
     O                                           31 'COGNOS'
     O                                           45 'ADG GENETIC'
     O                                           57 'MORTALITY'
     O                                           69 'ALTERNATE'
     O                                           84 'ALTERNATE'
     O                                           97 'ACTIVE/'
      *
     O          e            h1hdr          1
     O                                            4 'SIRE'
     O                                           13 'LINE'
     O                                           21 'PRIME'
     O                                           31 'REPORT'
     O                                           45 'ADJUSTMENT'
     O                                           54 'RATE'
     O                                           68 'REPORT'
     O                                           83 'REPORT'
     O                                           97 'INACTIVE'
     O          e            h1hdr          1
     O                                            4 'LINE'
     O                                           14 'COMPANY'
     O                                           21 'LINE'
     O                                           32 'GROUPING'
     O                                           43 'POUNDS'
     O                                           56 'GEN ADJ'
     O                                           70 'GROUPING 1'
     O                                           85 'GROUPING 2'
     O                                           96 'STATUS'
     O                                          113 'DESCRIPTION'
      *
     O          e            h1hdr          1
     O                       dash               132
      *
      *-------------------------------------------------------------
      * Detail line
      *-------------------------------------------------------------
      *
     O          e            l1dtl       1
     O                       sisicd               5
     O                       sisicomp            12
     O                       siprime       z     20
     O                       sigg0               30
     O                       siaadglb      k     44
     O                       simgart       k     56
     O                       sigg1               67
     O                       sigg2               82
     O                       siaist              94
     O                       sisids             132
      *
      *-------------------------------------------------------------
      * Blank line
      *-------------------------------------------------------------
      *
     O          e            blank       1
      *
      *-------------------------------------------------------------
      * No data message line
      *-------------------------------------------------------------
      *
     O          e            nodata      1
     O                                           19 'NO DATA MEETS YOUR'
     O                                           38 'SELECTION CRITERIA'
