     A*%%TS  SD  20090407  092554  ISLRAMS     REL-V5R4M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD157
     A*  TITLE:         MAINTAIN GROUP DEATHS
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  CREATED:       05/31/95
     A*
     A*  INDICATOR 60 IS ON WHEN IN REVISE MODE
     A*  INDICATOR 61 IS ON WHEN IN CREATE MODE
     A*
     A*  INDICATOR 63 IS ON WHEN THE SUBFILE FIELDS ARE NON-DISPLAY
     A*               BECAUSE THE RECORD WAS SUCCESSFULLY DELETED
     A*               (THE HIDDEN SUBFILE FIELD HDNDFL = YES)
     A*
     A*  INDICATOR 64 IS ON WHEN A SUBFILE RECORD FIELDS MUST BE PROTECTED.
     A*               (THE HIDDEN SUBFILE FIELD HDPFL = YES). FIELDS ARE
     A*               PROTECTED IF:
     A*               1) THE MOVEMENT IS POSTED
     A*               2) THE GROUP IS DISPOSED, CLOSED OR VOIDED
     A*               3) THE SUBFILE RECORD WAS SUCCESSFULLY CREATED
     A*
     A*  INDICATOR 90 IS ON WHEN THE FARM SITE THE USER SELECTED
     A*               FROM THE WORK WITH HAS BEEN DELETED BY ANOTHER
     A*               USER
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A N90                                  CF04(04 'PROMPT')
     A N90                                  CF05(05 'REFRESH')
     A N90                                  CF09(09 'ACCEPT')
     A N90                                  CF10(10 'CHANGE MODES')
     A                                      CA12(12 'PREVIOUS')
     A*
     A*--------------------------------------------------------------*
     A* SUBFILE FORMAT ONE
     A*--------------------------------------------------------------*
     A          R HP157S1                   SFL
     A*%%TS  SD  19991101  140620  ISLRAMS     REL-V4R2M0  5769-PW1
     A  85                                  SFLNXTCHG
     A            HDRRN1         4Y 0H
     A            HDHGSN    R        H      REFFLD(KDHGSN)
     A            HDKDDT    R        H      REFFLD(KDKDDT)
     A            HDPFL     R        H      REFFLD(@@FLAG)
     A            HDNDFL    R        H      REFFLD(@@FLAG)
     A            DFOPT          2A  B 11  2CHGINPDFT(UL)
     A  61
     AO 64
     AO 90                                  DSPATR(PR)
     A  61
     AO 64
     AO 90                                  DSPATR(ND)
     A  30                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A N30N61N64
     A N90                                  DSPATR(HI)
     A  60N30N64
     A N90                                  DSPATR(UL)
     A            DFMDY     R        B 11  6REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A  61
     AO 64
     AO 90                                  DSPATR(PR)
     A  61
     AO 63                                  DSPATR(ND)
     A  31                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A N31N64N90                            DSPATR(HI)
     A N31N64N90                            DSPATR(UL)
     A            DFBLCD    R        B 11 17REFFLD(HSRREF/@@BLCD *LIBL/HSPREF)
     A  64
     AO 90                                  DSPATR(PR)
     A  63                                  DSPATR(ND)
     A  38
     AO 58                                  DSPATR(PC)
     A  38                                  DSPATR(RI)
     A N38N64N90                            DSPATR(HI)
     A N38N64N90                            DSPATR(UL)
     A            DFRMCD    R        B 11 24REFFLD(HSRREF/@@RMCD *LIBL/HSPREF)
     A  64
     AO 90                                  DSPATR(PR)
     A  63                                  DSPATR(ND)
     A  39
     AO 59                                  DSPATR(PC)
     A  39                                  DSPATR(RI)
     A N39N64N90                            DSPATR(HI)
     A N39N64N90                            DSPATR(UL)
     A            DFHGCD    R        B 11 31REFFLD(HGHGCD)
     A  64
     AO 90                                  DSPATR(PR)
     A  63                                  DSPATR(ND)
     A  33
     AO 53                                  DSPATR(PC)
     A  33                                  DSPATR(RI)
     A N33N64N90                            DSPATR(HI)
     A N33N64N90                            DSPATR(UL)
     A            DFKDHD    R        B 11 40REFFLD(KDKDHD)
     A                                      EDTCDE(M)
     A  64
     AO 90                                  DSPATR(PR)
     A  63                                  DSPATR(ND)
     A  34                                  DSPATR(PC)
     A  34                                  DSPATR(RI)
     A N34N64N90                            DSPATR(HI)
     A N34N64N90                            DSPATR(UL)
     A            DFKDLB    R        B 11 48REFFLD(KDKDLB)
     A                                      EDTCDE(M)
     A  64
     AO 90                                  DSPATR(PR)
     A  63                                  DSPATR(ND)
     A  35                                  DSPATR(PC)
     A  35                                  DSPATR(RI)
     A N35N64N90                            DSPATR(HI)
     A N35N64N90                            DSPATR(UL)
     A            DFDRCD    R        B 11 56REFFLD(KDDRCD)
     A  64
     AO 90                                  DSPATR(PR)
     A  63                                  DSPATR(ND)
     A  37
     AO 57                                  DSPATR(PC)
     A  37                                  DSPATR(RI)
     A N37N64N90                            DSPATR(HI)
     A N37N64N90                            DSPATR(UL)
     A            DFDSFL    R        B 11 65REFFLD(KDDSFL)
     A  64
     AO 90                                  DSPATR(PR)
     A  63                                  DSPATR(ND)
     A  36
     AO 56                                  DSPATR(PC)
     A  36                                  DSPATR(RI)
     A N36N64N90                            DSPATR(HI)
     A N36N64N90                            DSPATR(UL)
     A            DFMSCD    R        O 11 70REFFLD(KDMSCD)
     A  63                                  DSPATR(ND)
     A            DFMVSN    R     Y  O 11 73REFFLD(KDMVSN)
     A                                      EDTCDE(M)
     A  63                                  DSPATR(ND)
     A*
     A*--------------------------------------------------------------*
     A* SUBFILE CONTROL FORMAT ONE
     A*--------------------------------------------------------------*
     A*
     A          R HP157C1                   SFLCTL(HP157S1)
     A*%%TS  SD  20090407  092554  ISLRAMS     REL-V5R4M0  5722-WDS
     A                                      SFLSIZ(0100)
     A                                      SFLPAG(0010)
     A                                      SETOF(30)
     A                                      SETOF(32)
     A                                      SETOF(31)
     A                                      SETOF(33)
     A                                      SETOF(34)
     A                                      SETOF(35)
     A                                      SETOF(36)
     A                                      SETOF(37)
     A                                      SETOF(38)
     A                                      SETOF(39)
     A                                      SETOF(49)
     A                                      SETOF(50)
     A                                      SETOF(51)
     A                                      SETOF(52)
     A                                      SETOF(53)
     A                                      SETOF(54)
     A                                      SETOF(54)
     A                                      SETOF(55)
     A                                      SETOF(57)
     A                                      SETOF(58)
     A                                      SETOF(59)
     A                                      SETOF(63)
     A N82N90                               ROLLUP(25 'PAGE FORWARD')
     A                                      SFLCSRRRN(&HDRR1)
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND(*MORE)
     A                                      OVERLAY
     A                                      RTNCSRLOC(&RCD &FLD)
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            HDRR1          5S 0H
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 25'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A            DFMODE         6A  O  2  2DSPATR(HI)
     A                                  2 25'MAINTAIN GROUP DEATHS'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  4  2'Farm site .....:'
     A            C1FSCD    R        O  4 20REFFLD(FSFSCD)
     A                                      EDTCDE(M)
     A            C1FSNM    R        O  4 30REFFLD(FSFSNM)
     A  61                              5  2'Date of death ..'
     A  61        C1KDDTMDY R        B  5 20REFFLD(HSRREF/@@DT06 *LIBL/HSPREF)
     A                                      EDTWRD('  /  /  ')
     A  90                                  DSPATR(PR)
     A  32                                  DSPATR(PC)
     A  32                                  DSPATR(RI)
     A N32N90                               DSPATR(HI)
     A N32N90                               DSPATR(UL)
     A N61N90                           6  2'4=Delete'
     A N90                              7 19'?'
     A N90                              7 26'?'
     A N90                              7 33'?'
     A N90                              7 58'?'
     A N90                              7 65'?'
     A N61N90                           7 70'?'
     A N61                              8  7'Date of'
     A                                  8 62'Destroy'
     A N61                              9  2'Opt'
     A N61                              9  8'Death'
     A                                  9 17'Bldg'
     A                                  9 24'Room'
     A                                  9 31'Group'
     A                                  9 41'Head'
     A                                  9 47'Pounds'
     A                                  9 55'Reason'
     A                                  9 64'Y/N'
     A                                  9 70'St'
     A                                  9 73'Movement'
     A N90        C1MDY     R        B 10  6REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A  61                                  DSPATR(ND)
     A  61                                  DSPATR(PR)
     A N90        C1BLCD    R        B 10 17REFFLD(HSRREF/@@BLCD *LIBL/HSPREF)
     A  55                                  DSPATR(PC)
     A                                      DSPATR(HI)
     A  61                                  DSPATR(ND)
     A  61                                  DSPATR(PR)
     A N90        C1RMCD    R        B 10 24REFFLD(HSRREF/@@RMCD *LIBL/HSPREF)
     A  49                                  DSPATR(PC)
     A                                      DSPATR(HI)
     A  61                                  DSPATR(ND)
     A  61                                  DSPATR(PR)
     A N90        C1HGCD    R        B 10 31REFFLD(HGHGCD)
     A  50                                  DSPATR(PC)
     A                                      DSPATR(HI)
     A  61                                  DSPATR(ND)
     A  61                                  DSPATR(PR)
     A N90        C1DRCD    R        B 10 56REFFLD(DRDRCD)
     A  52                                  DSPATR(PC)
     A                                      DSPATR(HI)
     A  61                                  DSPATR(ND)
     A  61                                  DSPATR(PR)
     A N90        C1DSFL    R        B 10 65REFFLD(KDDSFL)
     A  51                                  DSPATR(PC)
     A                                      DSPATR(HI)
     A  61                                  DSPATR(ND)
     A  61                                  DSPATR(PR)
     A N90        C1MSCD    R        B 10 70REFFLD(KDMSCD)
     A                                      DSPATR(HI)
     A  61                                  DSPATR(ND)
     A  61                                  DSPATR(PR)
     A  54                                  DSPATR(PC)
     A N90        C1MVSN    R        B 10 73REFFLD(KDMVSN)
     A                                      DSPATR(HI)
     A  61                                  DSPATR(ND)
     A  61                                  DSPATR(PR)
     A*
     A*--------------------------------------------------------------*
     A* COMMAND KEY RECORD 1
     A*--------------------------------------------------------------*
     A          R HP157K1
     A*%%TS  SD  20010125  155711  ISLRAMS     REL-V4R4M0  5769-PW1
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A                                      DSPATR(HI)
     A                                 23  2'F3=Exit'
     A N90                             23 11'F4=Prompt'
     A N90                             23 22'F5=Refresh'
     A N90                             23 34'F9=Accept'
     A  60N90                          23 45'F10=Go to Create'
     A  61N90                          23 45'F10=Go to Revise'
     A                                 23 63'F12=Previous'
     A*
     A*--------------------------------------------------------------*
     A* ERROR MESSAGE SUBFILE FORMAT
     A*--------------------------------------------------------------*
     A          R HP157ES                   SFL SFLMSGRCD(24)
     A*
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
     A*
     A*--------------------------------------------------------------*
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*--------------------------------------------------------------*
     A          R HP157EC                   SFLCTL(HP157ES)
     A*
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
