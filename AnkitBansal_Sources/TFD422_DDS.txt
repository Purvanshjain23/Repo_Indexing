     A*%%TS  SD  20080902  153308  ISLRAMS     REL-V5R4M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        TRIUMPH FOODS
     A*  DISPLAY FILE:  TFD422
     A*  TITLE:         SPECIFY BYPRODUCT ADJUSTMENT REPORTS
     A*  PROGRAMMER:    LEANNE RAMSEY
     A*  CREATED:       05/01/07
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/TFPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A                                      CF04(04 'PROMPT')
     A                                      CF08(08 'SUBMIT')
     A                                      CF17(17 'PRINT OPTIONS')
     A                                      CF20(20 'WRKSPLF')
     A*
     A*---------------------------------------------------------------
     A* RECORD FORMAT ONE
     A*---------------------------------------------------------------
     A          R TF422R1
     A*%%TS  SD  20080902  153308  ISLRAMS     REL-V5R4M0  5722-WDS
     A                                      SETOF(30)
     A                                      SETOF(31)
     A                                      SETOF(33)
     A                                      SETOF(51)
     A                                      SETOF(53)
     A                                      OVERLAY
     A                                      RTNCSRLOC(&RCD &FLD)
     A            RCD       R        H      REFFLD(@@RCD)
     A            FLD       R        H      REFFLD(@@FLD)
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 16'SPECIFY OPTIONS FOR BYPRODUCT ADJU-
     A                                      STMENT REPORTS'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USR)
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@JOB)
     A                                      DSPATR(HI)
     A                                  4 27'Week-ending date'
     A                                      DSPATR(UL)
     A                                      COLOR(BLU)
     A                                  4 48'Year'
     A                                      DSPATR(UL)
     A                                      COLOR(BLU)
     A                                  4 54'Week'
     A                                      DSPATR(UL)
     A                                      COLOR(BLU)
     A                                  4 60'Period'
     A                                      DSPATR(UL)
     A                                      COLOR(BLU)
     A                                  5  2'Date .....'
     A            D1MDY     R        B  5 15REFFLD(TFREF/@@DT06 *LIBL/TFPREF)
     A                                      EDTWRD('  /  /  -')
     A  30                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A N30                                  DSPATR(HI)
     A            D1WEMDY   R        O  5 31REFFLD(TFREF/@@DT06 *LIBL/TFPREF)
     A                                      EDTWRD('  /  /  ')
     A            D1YR      R        O  5 48REFFLD(TFREF/@@YR *LIBL/TFPREF)
     A                                      EDTCDE(Z)
     A            D1WK      R        O  5 55REFFLD(TFREF/@@WK *LIBL/TFPREF)
     A                                      EDTCDE(Z)
     A            D1PE      R        O  5 62REFFLD(TFREF/@@PE *LIBL/TFPREF)
     A                                      EDTCDE(Z)
     A*
     A                                  7  2'Reports ..'
     A                                  7 13'?'
     A            D1RPT     R        B  7 15REFFLD(TFREF/@@FLAG *LIBL/TFPREF)
     A  31
     AO 51                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A N31                                  DSPATR(HI)
     A            D1RPTDS       20A  O  7 27
     A                                 10  2'TF classification code ..'
     A                                 10 28'?'
     A            D1TFCLCD  R        B 10 30REFFLD(TFREF/@@TFCLCD *LIBL/TFPREF)
     A N33                                  DSPATR(HI)
     A  33
     AO 53                                  DSPATR(PC)
     A  33                                  DSPATR(RI)
     A            D1TFCLDS  R        O 10 35REFFLD(TFREF/@@TFCLDS *LIBL/TFPREF)
     A                                 10 69'(Blank=All)'
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R TF422K1
     A*%%TS  SD  20040928  155440  ISLRAMS     REL-V5R2M0  5722-WDS
     A  43                             21  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A                                 23 11'F4=Prompt'
     A                                 23 22'F8=Submit'
     A                                 23 33'F17=Print Options'
     A                                 23 52'F20=Spool Files'
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *---------------------------------------------------------------
     A          R TF422ES                   SFL SFLMSGRCD(24)
      *
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
     A*---------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*---------------------------------------------------------------
     A*
     A          R TF422EC                   SFLCTL(TF422ES)
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
