      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      HOG PRODUCTION
      * PROGRAM:     HP237 - Write Feed Receiving Records to Editing Files
      * PROGRAMMER:  LeAnne Ramsey
      * CREATED:     04/29/97
      *
      * FUNCTION: This program reads the Uploaded Receiving file and writes Header/Detail
      *           records to the Editing files.
      *
      *           This program is submitted by HP437CL which is called from HP437 through
      *           QCMDEXC.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 01/30/01  LeAnne Fedor
      *           Recompile only.  Production type was removed from feed files.
      *
      * 03/28/01  LeAnne Fedor
      *           Per John Zimmerman, change 'bin' logic to move the first 3
      *           positions instead of the first 2 positions.
      *
      * 05/02/01  LeAnne Fedor
      *           Recompile only. Grossman ticket number expanded from
      *           6 to 7 positions.
      *
      * 05/07/01  LeAnne Fedor
      *           Per John Zimmerman, he will now flag some records with a value
      *           of '1' in the processing flag field. They should be included on
      *           listing of staging data but then dropped from any further
      *           processing. This will help the users monitor the Grossman system...
      *           but we are not going to move these records into HPS.
      *
      * 05/21/01  LeAnne Fedor
      *           Per John Zimmerman, change 'bin' logic to move the first 5
      *           positions instead of the first 3 positions. They are renumbering bins
      *           in HPS to include the farm number. (ie: bin 1 on farm 661 would be
      *           66101 in HPS.)
      *
      * 12/11/01  LeAnne Fedor
      *           Users will now be uploading data from Grossman and from another system
      *           called Beta Raven.  Each record from Beta Raven should be defaulted
      *           to cost thru HPS. So, John Zimmerman will identify these records by
      *           populating position 79 of the upload record with:
      *            1 = Beta Raven, so default the cost thru system flag to YES
      *            *blank = Grossman, so default the cost thru system flag to NO
      *
      * 01/07/02  LeAnne Fedor
      *           Assign a ticket number when the uploaded value is not numeric.
      *
      * 07/16/02  LeAnne Fedor
      *           Recompile only. Production Phase removed from Grossman Feed Ticket
      *           header file.
      *
      * 08/12/02  LeAnne Fedor
      *           John Zimmerman is going to send us 'ration' and 'additive' in the
      *           6-character field that originally held 'item'.
      *
      * 08/22/07  LeAnne Ramsey
      *           Removed the logic for Grossman; we no longer have Grossman tickets.
      *           (We were only printing Grossman tickets on a listing; not uploading them
      *           into HPS. (See the 05/07/01 change above.)
      *           So, on the mainline read I changed the code from:
      *                      *IN90         IFEQ      *OFF
      *                      GSFLAG        ANDNE     '1'
      *           To:
      *                      *IN90         IFEQ      *OFF
      *
      * 09/23/09  LeAnne Ramsey
      *           Debbie Deen/John Davison needed all the Farm 30 uploaded feed to go to
      *           Farm 278 and, ultimately, to be allocated to all the Farm 278 groups.
      *           She created a shared Bin on Farm 278 and tied it to the active Rooms.
      *           I created a new Farm Feed Ticket Override file/function that will allow
      *           her to set up any "override" that she wants. This program will access this
      *           new "override" file and make the "overrides" as it writes to the "editing"
      *           files. (Note: The Staging and Historical Staging files will hold the
      *           uploaded values....not the override values.)
      *           To determine what overrides were in affect we are keeping a History Log file
      *           everytime the user creates/revises/deletes an override. This History Log file
      *           is HSP9131.
      *
      * 10/26/09  LeAnne Ramsey
      *           Rewrote to incorporate David Weaver's enhancements from his Grain Accounting
      *           Project (where he rewrote the pc side of things.)
      *           We no longer have/need the "staging" file. So, this program now reads/processes
      *           the "receiving" file.
      *
      * 10/30/19  Brad Baden     E15736 - Upload Toll Mill Receipts
      *           Populate UDINAM field in HSP238 with U1INGR value
      *           in file HSP235.  Also update field UHCSFL in file
      *           HSP237 with value from HSL089A.  If record not
      *           found, set UHCSFL to "N".
      *
      * 03/26/20  Danny Nguyen - P405 - Farm Number Increase
      *           Recompile only. Increased Bin Code (@@BNCD) in HSPREF file from 5A to 6A.
      *
      * 05/10/22 Eric L SDN736 Recompiled ticket nbr increase (TKNO & RTNO 7.0 TO 9.0)
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fhsl089a   if   e           k disk
      *  Feed Mill Alias
      *
      *
     Fhsp131    if   e           k disk
      *  Farm feed ticket overrides
      *
      *
     Fhsp235    if   e           k disk
      *  Uploaded feed tickets: Receiving file
      *
      *
     Fhsp237    o    e             disk
      *  Uploaded feed tickets: Editing file--Header
      *
      *
     Fhsp238    o    e             disk
      *  Uploaded feed tickets: Editing file--Detail
      *
      /EJECT
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      *---------------------------------------------------------------
      * Standalone fields
      *---------------------------------------------------------------
      *
      * Control fields
      *
     D first           s              1    inz('Y')
     D newtkno         s              1    inz('Y')
     D svtkno          s                   like(u1tkno)
      *
      * Workfields
      *
     D wkbncd          s                   like(ffbncd)
     D wklnno          s                   like(udlnno)
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *---------------------------------------------------------------
      *  Data areas
      *---------------------------------------------------------------
      *
      * Definition for external data area 'DAFDSN' for assigning the next ticket number
      *
     Ddafdsn          uds                  dtaara
     D  datkno                 1      9  0
     D  dabegtkno             11     19  0
     D  daendtkno             21     29  0
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * MAINLINE
      ****************************************************************
      *
      * Read each record in the Upload Receiving file
      *
     C     *loval        setll     hsp235
      *
     C                   dou       *in90 = *on                                  Main do
     C                   read      hsp235                                 90
     C                   if        *in90 = *off                                 If not EOF
      *
      * Write a new Ticket Header record for each new Ticket.
      *
     C                   if        first = yes or u1tkno <> svtkno
     C                   exsr      $wrt237
     C                   move      no            first
     C                   endif
      *
      * Write a Detail record for each record read.
      *
     C                   exsr      $wrt238
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Main do
      *
     C                   seton                                        lr
      /EJECT
      *---------------------------------------------------------------
      * Wriate a Feed Ticket Header record to the Editing file
      *---------------------------------------------------------------
      *
     C     $wrt237       begsr
      *
     C                   clear                   uhrec
     C                   move      *blank        wkbncd
      *
      * If the Ticket Number is not numeric, assign a ticket number.
      *
     C                   testn                   u1tkno               92
     C                   if        *in92 = *off                                 If not numeric
     C                   exsr      $tkno
     C                   else
     C                   move      u1tkno        uhtkno
     C                   endif                                                  If not numeric
      *
      * Ticket Date
     C                   z-add     u1tkdt        uhtkdt
      * Mapping Mill Nbr
     C                   z-add     u1mmno        uhmmno
      * Invoice Number
     C                   movel(p)  u1inv         uhvino
      * Farm site
     C                   z-add     u1fscd        uhfscd
      *
      *    Check for an Override on this Farm
      *
     C     uhfscd        chain     hsp131                             92
     C                   if        *in92 = *off
     C                   z-add     fftfscd       uhfscd
     C                   move      ffbncd        wkbncd
     C                   endif
      *
      * Default YES to:
      *   Produced Flag
      *   Cost thru System Flag
      *
     C                   move      yes           uhprfl
     C                   move      yes           uhcsfl
      *
      * 10/30/19  Brad Baden     E15736 - Upload Toll Mill Receipts
      * Retrieve Cost Through System Flag from HSL089A
      *   by Mapping Mill Number
      *
     C     u1mmno        chain     hsl089a
     C                   if        %found(hsl089a)
     C                   move      macsfl        uhcsfl
     C                   else
     C                   move      no            uhcsfl
     C                   end
      *
      * Write header
     C                   write     uhrec
      *
     C                   z-add     0             wklnno
     C                   move      u1tkno        svtkno
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Write a Feed Ticket Detail record
      *---------------------------------------------------------------
      *
     C     $wrt238       begsr
      *
     C                   clear                   udrec
      *
      * Ticket and Line number
     C                   z-add     uhtkno        udtkno
     C                   add       1             wklnno
     C                   z-add     wklnno        udlnno
      *
      *
      * Move either a) the override bin or b) uploaded bin to Bin Code.
      *
     C                   if        wkbncd <> *blank
     C                   move      wkbncd        udbncd
     C                   else
     C                   move      u1bncd        udbncd
     C                   endif
      *
      * Ration/additive
     C                   movel(p)  u1rncd        udrncd
     C                   movel(p)  u1adcd        udadcd
      * 10/30/19  Brad Baden     E15736 - Upload Toll Mill Receipts
      *
      * Ingredient Cost
     C                   z-add     u1ingr        udinam
      *
      * Pounds
      * (Leave Pounds at zero if the uploaded value exceeds 9,999,999 pounds..the HPS max.
      *
     C                   if        u1lbs <= 9999999
     C                   z-add     u1lbs         udfdlb
     C                   endif
      *
     C                   write     udrec
      *
     C                   endsr
      /EJECT
      *----------------------------------------------------------------
      * Retrieve a system-assigned ticket number
      *----------------------------------------------------------------
      *
     C     $tkno         begsr
      *
     C     *lock         in        dafdsn
     C                   add       1             datkno
     C                   z-add     datkno        uhtkno
     C                   out       dafdsn
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Initialization
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
     C                   endsr
      /EJECT
