     A*%%TS  SD  20040224  111548  ISLRAMS     REL-V5R2M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD1711
     A*  TITLE:         CLOSE FARM BUDGET
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  CREATED:       02/24/04
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A                                      CF09(09 'COPY')
     A*
     A*---------------------------------------------------------------
     A* RECORD FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP1711R1
     A*%%TS  SD  20040224  111548  ISLRAMS     REL-V5R2M0  5722-WDS
     A                                      OVERLAY
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 25'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USR)
     A                                      DSPATR(HI)
     A                                  2 27'CLOSE FARM BUDGET'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@JOB)
     A                                      DSPATR(HI)
     A                                  4  2'Farm site .........:'
     A            D1FSCD    R        O  4 24REFFLD(HSRREF/@@FSCD *LIBL/HSPREF)
     A                                      EDTCDE(M)
     A            D1FSNM    R        O  4 31REFFLD(HSRREF/FSFSNM *LIBL/HSPREF)
     A                                  5  2'Budget template ...:'
     A            D1BTCD    R        O  5 23REFFLD(HSRREF/@@BTCD *LIBL/HSPREF)
     A            D1BTDS    R        O  5 31REFFLD(HSRREF/@@BTDS *LIBL/HSPREF)
     A                                  6  2'Budget availability:'
     A            D1BACD    R        O  6 23REFFLD(HSRREF/@@BACD *LIBL/HSPREF)
     A            D1BADS    R        O  6 31REFFLD(HSRREF/@@BADS *LIBL/HSPREF)
     A                                  7  2'Creation schedule .:'
     A            D1CSCD    R        O  7 23REFFLD(HSRREF/@@CSCD *LIBL/HSPREF)
     A            D1CSDS    R        O  7 31REFFLD(HSRREF/@@CSDS *LIBL/HSPREF)
     A                                  8  2'Weeks .............:'
     A            D1BTWK    R        O  8 23REFFLD(HSRREF/@@BTWK *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A                                 10  2'Budget number:'
     A            D1FBSN    R        O 10 17REFFLD(HSRREF/@@FBSN *LIBL/HSPREF)
     A                                      EDTCDE(Z)
     A                                 11  2'Budget status:'
     A            D1FBSDS   R        O 11 17REFFLD(HSRREF/@@FBSDS *LIBL/HSPREF)
     A                                 12  2'From date ...:'
     A            D1FMDY    R        O 12 17REFFLD(HSRREF/@@DT06 *LIBL/HSPREF)
     A                                      EDTWRD('  /  /  ')
     A                                 13  2'To date .....:'
     A            D1TMDY    R        O 13 17REFFLD(HSRREF/@@DT06 *LIBL/HSPREF)
     A                                      EDTWRD('  /  /  ')
     A                                 15  2'This function sets the status of a-
     A                                      n Active Farm Budget to Closed.'
     A                                      DSPATR(HI)
     A                                 16  2'Once you Close a Farm Budget, you -
     A                                      cannot reactivate or use it on any'
     A                                      DSPATR(HI)
     A                                 17  2'future requisitions.'
     A                                      DSPATR(HI)
     A                                 19  2'If you are sure that you wish to C-
     A                                      lose this Budget, press F9=Close.'
     A                                      DSPATR(HI)
     A*
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R HP1711K1
     A*%%TS  SD  20040205  142304  ISLRAMS     REL-V5R2M0  5722-WDS
     A  43                             21  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A                                 23 22'F9=Close'
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *---------------------------------------------------------------
     A          R HP1711ES                  SFL SFLMSGRCD(24)
      *
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
     A*---------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*---------------------------------------------------------------
     A*
     A          R HP1711EC                  SFLCTL(HP1711ES)
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
