/***************************************************************** */
/*  PROGRAM NUMBER --> HP447CL                                     */
/*  PROGRAM NAME ----> CLOSED GROUP ANALYSIS REPORT                */
/*  PROGRAMMER ------> LEANNE FEDOR                                */
/*  DATE ------------> 06/30/97                                    */
/*                                                                 */
/*  PROGRAM PURPOSE -> THIS CL USES OPEN QUERY AND WORKFILES       */
/*                     TO PRODUCE THE REPORT.                      */
/*                     VALUES ARE PASSED THRU THE LDA FROM         */
/*                     SPECIFY HP447.                              */
/*                                                                 */
/***************************************************************** */
/* MODIFIED  PROGRAMMER                                            */
/*                                                                 */
/*******************************************************************/

             PGM

/* DEFINE THE VARIABLE REQUIRED IN THE COMMAND TO OVERRIDE THE     */
/* PRINT FILE TO A LASER PRINTER                                   */

             DCL        VAR(&CMD) TYPE(*CHAR) LEN(512)

             DCL        VAR(&QDATA)  TYPE(*CHAR) LEN(179)
             DCL        VAR(&LDFSBO) TYPE(*CHAR) LEN(5)
             DCL        VAR(&LDFYMD) TYPE(*DEC)  LEN(8)
             DCL        VAR(&LDTYMD) TYPE(*DEC)  LEN(8)
             DCL        VAR(&LDPTCD) TYPE(*CHAR) LEN(5)
             DCL        VAR(&LDPPCD) TYPE(*CHAR) LEN(5)
             DCL        VAR(&LDFSCD) TYPE(*DEC) LEN(5)
             DCL        VAR(&LDGTCD) TYPE(*CHAR) LEN(1)

             DCL        VAR(&HOLD) TYPE(*CHAR) LEN(10)
             DCL        VAR(&COPYS) TYPE(*DEC) LEN(2)
             DCL        VAR(&OUTQ) TYPE(*CHAR) LEN(10)

             CHGVAR     VAR(&LDFSBO) VALUE(%SST(*LDA 1 5))
             CHGVAR     VAR(&LDFYMD) VALUE(%SST(*LDA 6 8))
             CHGVAR     VAR(&LDTYMD) VALUE(%SST(*LDA 14 8))
             CHGVAR     VAR(&LDPTCD) VALUE(%SST(*LDA 22 5))
             CHGVAR     VAR(&LDPPCD) VALUE(%SST(*LDA 27 5))
             CHGVAR     VAR(&LDFSCD) VALUE(%SST(*LDA 32 5))
             CHGVAR     VAR(&LDGTCD) VALUE(%SST(*LDA 61 1))


/* ALWAYS SELECT ONLY CLOSED HOG GROUPS                           */

             CHGVAR     VAR(%SST(&QDATA 1 12)) VALUE('HGGSCD *EQ "')
             CHGVAR     VAR(%SST(&QDATA 13 2)) VALUE('CL')
             CHGVAR     VAR(%SST(&QDATA 15 1)) VALUE('"')

/* ALWAYS SELECT ONLY GROUPS WITH A CLOSE DATE IN THE SELECTED RANGE */

             CHGVAR     VAR(%SST(&QDATA 17 15)) VALUE('*AND HGCLDT +
                          *GE')
             CHGVAR     VAR(%SST(&QDATA 33 8)) VALUE(&LDFYMD)
             CHGVAR     VAR(%SST(&QDATA 42 15)) VALUE('*AND HGCLDT +
                          *LE')
             CHGVAR     VAR(%SST(&QDATA 58 8)) VALUE(&LDTYMD)

/* IF USER SELECTED A PRODUCTION PHASE, ADD SELECTION            */

             IF         COND(&LDPPCD *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 67 17)) VALUE('*AND HGPPCD +
                          *EQ "')
             CHGVAR     VAR(%SST(&QDATA 84 5)) VALUE(&LDPPCD)
             CHGVAR     VAR(%SST(&QDATA 89 1)) VALUE('"')
             ENDDO


/*  IF USER SELECTED A BUSINESS OFFICE, ADD SELECTION            */

             IF         COND(&LDFSBO *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 91 17)) VALUE('*AND HGFSBO +
                          *EQ "')
             CHGVAR     VAR(%SST(&QDATA 108 5)) VALUE(&LDFSBO)
             CHGVAR     VAR(%SST(&QDATA 113 1)) VALUE('"')
             ENDDO

/*  IF USER SELECTED A PRODUCTION TYPE, ADD SELECTION          */

             IF         COND(&LDPTCD *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 115 17)) VALUE('*AND HGPTCD +
                          *EQ "')
             CHGVAR     VAR(%SST(&QDATA 132 5)) VALUE(&LDPTCD)
             CHGVAR     VAR(%SST(&QDATA 137 1)) VALUE('"')
             ENDDO


/*  IF USER SELECTED A FARM, ADD FARM SELECTION                  */

             IF         COND(&LDFSCD *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 139 15)) VALUE('*AND HGFSCD +
                          *EQ')
             CHGVAR     VAR(%SST(&QDATA 155  5)) VALUE(&LDFSCD)
             ENDDO

/*  IF USER SELECTED A GROUP TYPE, ADD A GROUP TYPE SELECTION         */

             IF         COND(&LDGTCD *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 161 17)) VALUE('*AND HGGTCD +
                          *EQ "')
             CHGVAR     VAR(%SST(&QDATA 178 1)) VALUE(&LDGTCD)
             CHGVAR     VAR(%SST(&QDATA 179 1)) VALUE('"')
             ENDDO


/*------------------------------------------------------------------*/
/* RUN QUERY OVER THE HOG GROUP FILE                                 */
/*------------------------------------------------------------------*/

             OVRDBF     FILE(HSP034) SHARE(*YES)
             OPNQRYF    FILE((HSP034)) QRYSLT(&QDATA) KEYFLD(HGPPCD) +
                          SEQONLY(*YES)

/*------------------------------------------------------------------*/
/*  CREATE  WORKFILES                                               */
/*------------------------------------------------------------------*/

             CRTDUPOBJL OBJ(HSP332) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)

             CRTDUPOBJL OBJ(HSP333) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)

             CRTDUPOBJL OBJ(HSL333A) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)

             CRTDUPOBJL OBJ(HSP334) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)

             CRTDUPOBJL OBJ(HSP335) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)


/*------------------------------------------------------------------*/
/* BUILD DATA IN GROUP WORKFILES                                    */
/*------------------------------------------------------------------*/

             CALL       PGM(HP346)

/*------------------------------------------------------------------*/
/* BUILD DATA IN PHASE WORKFILES                                    */
/*------------------------------------------------------------------*/

             CALL       PGM(HP347)

/*------------------------------------------------------------------*/
/* SETUP THE REPORT PRINT PARAMETERS                                */
/*------------------------------------------------------------------*/

 HOLD:       IF         COND(%SST(*LDA 411 1) = 'Y') THEN(CHGVAR +
                          VAR(&HOLD) VALUE(*YES))
             ELSE       CMD(CHGVAR VAR(&HOLD) VALUE(*NO))

 COPYS:      CHGVAR     VAR(&COPYS) VALUE(%SST(*LDA 412 1))
             IF         COND(&COPYS = 0) THEN(CHGVAR VAR(&COPYS) +
                          VALUE(1))

 OUTQ:       CHGVAR     VAR(&OUTQ) VALUE(%SST(*LDA 413 10))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(%SST(*LDA 401 10)))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(*JOB))

/*------------------------------------------------------------------*/
/* OVERRIDE PRINT FILE                                              */
/*------------------------------------------------------------------*/

/* USE THE CUSTOMIZED SEABOARD COMMAND TO OVERRIDE THE PRINT FILE.  */
/* THIS COMMAND ALLOWS THE REPORT TO BE GENERATED ON A STANDARD LINE*/
/* PRINTER OR A LASER PRINTER. TO USE THIS OVERRIDE YOU MUST DECLARE*/
/* A COMMAND VARIABLE AND FOLLOW THE OVERRIDE WITH A CALL TO        */
/* QCMDEXC.                                                         */

/* THIS REPORT IS CONDENSED PRINT OF 15CPI ON 198 COLUMNS OF PRINT  */
/* FORCE IT TO PRINT OUT OF DRAWER 2 ON LANDSCAPE                   */

             SBDPRTOVR  CMDVAR(&CMD) FILE(QPRINT) OUTQ(&OUTQ) +
                          WIDTH(198) LANDSCAPE(*YES) DRAWER(2) +
                          COPIES(&COPYS) HOLD(&HOLD)

             CALL       PGM(QCMDEXC) PARM(&CMD 512)


/*------------------------------------------------------------------*/
/*  RUN REPORT                                                      */
/*------------------------------------------------------------------*/

             CALL PGM(HP647)


EXIT:        CLOF       OPNID(HSP034)
             DLTOVR     FILE(*ALL)
             ENDPGM

