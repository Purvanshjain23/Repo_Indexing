     A*%%TS  SD  20070601  092436  ISLRAMS     REL-V5R4M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        TRIUMPH FOODS
     A*  DISPLAY FILE:  TFD409
     A*  TITLE:         SPECIFY OPTIONS FOR REVENUE REPORT BY GL SUB
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  CREATED:       02/09/06
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/TFPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A                                      CF04(04 'PROMPT')
     A                                      CF08(08 'SUBMIT')
     A                                      CF17(17 'PRINT OPTIONS')
     A                                      CF20(20 'WRKSPLF')
     A*
     A*---------------------------------------------------------------
     A* RECORD FORMAT ONE
     A*---------------------------------------------------------------
     A          R TF409R1
     A*%%TS  SD  20070601  092436  ISLRAMS     REL-V5R4M0  5722-WDS
     A                                      SETOF(35)
     A                                      SETOF(36)
     A                                      SETOF(37)
     A                                      SETOF(38)
     A                                      SETOF(39)
     A                                      SETOF(55)
     A                                      SETOF(58)
     A                                      SETOF(59)
     A                                      OVERLAY
     A                                      RTNCSRLOC(&RCD &FLD)
     A            RCD       R        H      REFFLD(@@RCD)
     A            FLD       R        H      REFFLD(@@FLD)
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 17'SPECIFY OPTIONS FOR REVENUE REPORT-
     A                                       BY GL SUB'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USR)
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@JOB)
     A                                      DSPATR(HI)
     A                                  4  2'From date ..'
     A            D1FMDY    R     Y  B  4 16REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A N37                                  DSPATR(HI)
     A  37                                  DSPATR(PC)
     A  37                                  DSPATR(RI)
     A                                  4 27'Week-ending:'
     A            D1FWEMDY  R     Y  O  4 40REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A                                  5  2'To date ....'
     A            D1TMDY    R     Y  B  5 16REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A N36                                  DSPATR(HI)
     A  36                                  DSPATR(PC)
     A  36                                  DSPATR(RI)
     A                                  5 27'Week-ending:'
     A            D1TWEMDY  R     Y  O  5 40REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A                                  7  2'Detail/summary/both ..............-
     A                                      ...'
     A                                  7 40'?'
     A            D1DSFL    R        B  7 42REFFLD(@@FLAG)
     A N35                                  DSPATR(HI)
     A  35
     AO 55                                  DSPATR(PC)
     A  35                                  DSPATR(RI)
     A            D1DSDS        23A  O  7 44
     A                                  8  2'CoOwned Only/Not CoOwned Only/Both-
     A                                       ..'
     A                                  8 40'?'
     A            D1COFL    R        B  8 42REFFLD(TFREF/@@IECD *LIBL/TFPREF)
     A N39                                  DSPATR(HI)
     A  39
     AO 59                                  DSPATR(PC)
     A  39                                  DSPATR(RI)
     A            D1CODS        30A  O  8 44
     A                                 10  2'Include records where adjustment d-
     A                                      ollars are zero ..'
     A                                 10 55'?'
     A            D1ZEROFL  R        B 10 57REFFLD(@@FLAG)
     A N38                                  DSPATR(HI)
     A  38
     AO 58                                  DSPATR(PC)
     A  38                                  DSPATR(RI)
     A*
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R TF409K1
     A*%%TS  SD  20040928  155440  ISLRAMS     REL-V5R2M0  5722-WDS
     A  43                             21  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A                                 23 11'F4=Prompt'
     A                                 23 22'F8=Submit'
     A                                 23 33'F17=Print Options'
     A                                 23 52'F20=Spool Files'
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *---------------------------------------------------------------
     A          R TF409ES                   SFL SFLMSGRCD(24)
      *
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
     A*---------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*---------------------------------------------------------------
     A*
     A          R TF409EC                   SFLCTL(TF409ES)
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
