     A*%%TS  SD  20180227  102112  ISJBADE     REL-V7R1M0  5770-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD445
     A*  TITLE:         WORK WITH SCHEDULED SALE MOVEMENTS
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  DATE:          09/21/95
     A*
     A*****************************************************************
     A* INDICATORS
     A* 99  USER IS NOT AUTHORIZED TO FUNCTION
     A*****************************************************************
     A* RMC 2/18/2016 E5190  ADD BUSINESS OFFICE RESTRICTOR
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A N99                                  CF04(04 'PROMPT')
     A N99                                  CF05(05 'REFRESH')
     A N99                                  CF06(06 'CREATE')
     A N99                                  CF17(17 'BOL')
     A N99                                  CF20(20 'WRKSPLF')
     A*
     A*----------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*----------------------------------------------------------------
     A          R HP445S1                   SFL
     A*%%TS  SD  20030213  073224  ISLRAMS     REL-V5R1M0  5722-WDS
     A  85                                  SFLNXTCHG
     A            HDRRN1         4Y 0H
     A            DFOPT          2A  B 12  2CHGINPDFT(UL)
     A  30                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A N30                                  DSPATR(HI)
     A  99                                  DSPATR(PR)
     A  99                                  DSPATR(ND)
     A            DFMVSN    R        O 12  5REFFLD(@@MVSN)
     A                                      EDTCDE(4)
     A            DFSMDY    R     Y  O 12 13REFFLD(@@DT06)
     A                                      EDTCDE(Y)
     A            DFKMDY    R     Y  O 12 22REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  ')
     A            DFHGCD    R        O 12 31REFFLD(@@HGCD)
     A            DFSCHD    R     Y  O 12 39REFFLD(SGSCHD)
     A                                      EDTCDE(Z)
     A            DFFSCD    R        O 12 45REFFLD(@@FSCD)
     A                                      EDTCDE(Z)
     A            DFBLCD    R        O 12 51REFFLD(@@BLCD)
     A            DFPPCD    R        O 12 57REFFLD(@@PPCD)
     A            DFSTCD    R        O 12 63REFFLD(@@STCD)
     A            DFLTCD    R        O 12 69REFFLD(HSRREF/@@LTCD *LIBL/HSPREF)
     A            DFCVNO    R        O 12 72REFFLD(@@AJD)
     A                                      EDTCDE(Z)
     A*
     A*----------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*----------------------------------------------------------------
     A          R HP445C1                   SFLCTL(HP445S1)
     A*%%TS  SD  20030213  075029  ISLRAMS     REL-V5R1M0  5722-WDS
     A                                      SFLSIZ(0050)
     A                                      SFLPAG(0010)
     A N82N99                               ROLLUP(25 'PAGE FORWARD')
     A                                      SETOF(31)
     A                                      SETOF(52)
     A                                      SETOF(53)
     A                                      SETOF(54)
     A                                      SETOF(55)
     A                                      SETOF(56)
     A                                      SETOF(57)
     A                                      RTNCSRLOC(&RCD &FLD)
     A                                      OVERLAY
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 31'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A            C1VERSION     18A  O  2  2DSPATR(HI)
     A                                  2 24'WORK WITH SCHEDULED SALE MOVEMENTS'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  4  2'Business office .. ?'
     A            C1FSBO    R        B  4 23REFFLD(HGFSBO)
     A  31
     AO 51                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A N31                                  DSPATR(HI)
     A  99                                  DSPATR(PR)
     A            C1BODS    R        O  4 30REFFLD(BOBODS)
     A N99                              6  2'2=Revise'
     A N99                              6 12'4=Delete'
     A N99                              6 22'5=View'
     A                                  8 15'Sched'
     A                                      DSPATR(HI)
     A                                  8 24'Sched'
     A                                      DSPATR(HI)
     A N99                              8 34'?'
     A                                      DSPATR(HI)
     A N99                              8 47'?'
     A                                      DSPATR(HI)
     A N99                              8 59'?'
     A                                      DSPATR(HI)
     A N99                              8 65'?'
     A                                      DSPATR(HI)
     A                                  8 69'?'
     A                                      DSPATR(HI)
     A N99                              8 76'?'
     A                                      DSPATR(HI)
     A                                  9 15'Ship'
     A                                      DSPATR(HI)
     A                                  9 24'Kill'
     A                                      DSPATR(HI)
     A                                  9 39'Sched'
     A                                      DSPATR(HI)
     A                                  9 63'Sale'
     A                                      DSPATR(HI)
     A                                  9 68'Load'
     A                                      DSPATR(HI)
     A                                  9 75'Cust'
     A                                      DSPATR(HI)
     A                                 10  2'Op'
     A                                      DSPATR(HI)
     A                                 10  5'Movemnt'
     A                                      DSPATR(HI)
     A                                 10 15'Date'
     A                                      DSPATR(HI)
     A                                 10 24'Date'
     A                                      DSPATR(HI)
     A                                 10 31'Group'
     A                                      DSPATR(HI)
     A                                 10 40'Head'
     A                                      DSPATR(HI)
     A                                 10 46'Farm'
     A                                      DSPATR(HI)
     A                                 10 51'Bldg'
     A                                      DSPATR(HI)
     A                                 10 57'Phase'
     A                                      DSPATR(HI)
     A                                 10 63'Type'
     A                                      DSPATR(HI)
     A                                 10 68'Type'
     A                                      DSPATR(HI)
     A                                 10 74'Number'
     A                                      DSPATR(HI)
     A            C1MVSN    R        B 11  5REFFLD(@@MVSN)
     A                                      EDTCDE(4)
     A                                      DSPATR(HI)
     A  99                                  DSPATR(PR)
     A  99                                  DSPATR(ND)
     A            C1SMDY    R     Y  B 11 13REFFLD(@@DT06)
     A                                      DSPATR(HI)
     A                                      EDTWRD('  /  /  ')
     A  99                                  DSPATR(PR)
     A  99                                  DSPATR(ND)
     A            C1KMDY    R     Y  B 11 22REFFLD(@@DT06)
     A                                      DSPATR(HI)
     A                                      EDTWRD('  /  /  ')
     A  99                                  DSPATR(PR)
     A  99                                  DSPATR(ND)
     A            C1HGCD    R        B 11 31REFFLD(@@HGCD)
     A                                      DSPATR(HI)
     A  52                                  DSPATR(PC)
     A  99                                  DSPATR(PR)
     A  99                                  DSPATR(ND)
     A            C1FSCD    R        B 11 45REFFLD(@@FSCD)
     A                                      EDTCDE(Z)
     A                                      DSPATR(HI)
     A  53                                  DSPATR(PC)
     A  99                                  DSPATR(PR)
     A  99                                  DSPATR(ND)
     A            C1BLCD    R        B 11 51REFFLD(@@BLCD)
     A                                      DSPATR(HI)
     A  99                                  DSPATR(PR)
     A  99                                  DSPATR(ND)
     A            C1PPCD    R        B 11 57REFFLD(@@PPCD)
     A                                      DSPATR(HI)
     A  54                                  DSPATR(PC)
     A  99                                  DSPATR(PR)
     A  99                                  DSPATR(ND)
     A            C1STCD    R        B 11 63REFFLD(@@STCD)
     A                                      DSPATR(HI)
     A  56                                  DSPATR(PC)
     A  99                                  DSPATR(PR)
     A  99                                  DSPATR(ND)
     A            C1LTCD    R        B 11 69REFFLD(HSRREF/@@LTCD *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A  57                                  DSPATR(PC)
     A  99                                  DSPATR(PR)
     A  99                                  DSPATR(ND)
     A            C1CVNO    R        B 11 72REFFLD(@@AJD)
     A                                      EDTCDE(Z)
     A                                      DSPATR(HI)
     A  55                                  DSPATR(PC)
     A  99                                  DSPATR(PR)
     A  99                                  DSPATR(ND)
     A*----------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*----------------------------------------------------------------
     A          R HP445K1
     A*%%TS  SD  19960206  095214  ISLRAMS     REL-V3R1M0  5763-PW1
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A                                 23 11'F4=Prompt'
     A                                 23 22'F5=Refresh'
     A                                 23 34'F6=Create'
P408 A**                               23 45'F17=Print BOL'
  |  A                                 23 45'F17=Prt/Email BOL'
  |  A**                               23 60'F20=Spool Files'
P408 A                                 23 64'F20=Spool Files'
      *----------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *----------------------------------------------------------------
     A          R HP445ES                   SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
      *----------------------------------------------------------------
      * ERROR MESSAGE SUBFILE CONTROL FORMAT
      *----------------------------------------------------------------
     A          R HP445EC                   SFLCTL(HP445ES)
      *
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
