      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      HPS
      * PROGRAM:     HP291 - Retrieve Manager Data using Farm Site
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     03/03/03
      *
      * FUNCTION:    This program retrieves Manager data using incoming Farm Site number
      *              from Synon.
      *
      *
      *************************************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 10/15/13  LeAnne Ramsey (E2831)
      *           Recompile only. Added field 'MTech Reference'.
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS:
      ****************************************************************
      *
      *
     Fhsp016    if   e           k disk
      *  Manager
      *
      *
     Fhsp018    if   e           k disk
      *  Farm site
      *
      *
      /eject
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      *
      ****************************************************************
      * ARRAYS AND TABLES
      ****************************************************************
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Work fields
      *
     D wkmgcd          s                   like(mgmgcd)
     D wkmgnm          s                   like(mgmgnm)
     D wkarea          s              3
     D wkprefix        s              3
     D wknumber        s              4
     D wkphone         s             12
      *
      *
      * Parm fields
      *
     D xxreturn        s              7
     D xxfscd          s                   like(fsfscd)
     D xxmmgcd         s                   like(fsmmgcd)
     D xxmmgnm         s                   like(mgmgnm)
     D xxmphone        s             12
     D xxpmgcd         s                   like(fspmgcd)
     D xxpmgnm         s                   like(mgmgnm)
     D xxpphone        s             12
      *
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * MAINLINE
      ****************************************************************
      *
      * Retrieve Managers from Farm Site file
      *
     C     xxfscd        chain     hsp018                             92
     C                   if        *in92 = *off                                 If farm hit
      *
      * Multisite manager
     C                   if        fsmmgcd <> *blank
     C                   move      fsmmgcd       wkmgcd
     C                   exsr      $mgcd
     C                   move      wkmgcd        xxmmgcd
     C                   move      wkmgnm        xxmmgnm
     C                   move      wkphone       xxmphone
     C                   endif
      *
      * Production manager
      *
     C                   if        fspmgcd <> *blank
     C                   move      fspmgcd       wkmgcd
     C                   exsr      $mgcd
     C                   move      wkmgcd        xxpmgcd
     C                   move      wkmgnm        xxpmgnm
     C                   move      wkphone       xxpphone
     C                   endif
      *
     C                   endif                                                  If farm hit
      *
     C                   seton                                        lr
      /eject
      *---------------------------------------------------------------
      * Retrieve Manager data
      *---------------------------------------------------------------
      *
     C     $mgcd         begsr
      *
     C                   move      *blank        wkmgnm
     C                   move      *blank        wkphone
      *
     C     wkmgcd        chain     hsp016                             92
     C                   if        *in92 = *off
     C                   move      mgmgnm        wkmgnm
      *
     C                   if        mgtlacno <> 0 or mgtlphno <> 0
     C                   exsr      $phone
     C                   endif
      *
     C                   endif
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Format Phone number
      *---------------------------------------------------------------
      *
     C     $phone        begsr
      *
     C                   move(p)   mgtlacno      wkarea
     C                   movel(p)  mgtlphno      wkprefix
     C                   move(p)   mgtlphno      wknumber
      *
     C                   eval      wkphone = wkarea + '-' + wkprefix + '-'
     C                                       + wknumber
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
     C     *entry        plist
     C                   parm                    xxreturn
     C                   parm                    xxfscd
     C                   parm                    xxmmgcd
     C                   parm                    xxmmgnm
     C                   parm                    xxmphone
     C                   parm                    xxpmgcd
     C                   parm                    xxpmgnm
     C                   parm                    xxpphone
      *
      * I expect the SYNON program that called this retrieval to have cleared the return parms.
      * But, just in case it did not, clear them here.
      *
     C                   move      *blank        xxreturn
     C                   move      *blank        xxmmgcd
     C                   move      *blank        xxmmgnm
     C                   move      *blank        xxmphone
     C                   move      *blank        xxpmgcd
     C                   move      *blank        xxpmgnm
     C                   move      *blank        xxpphone
      *
     C                   endsr
      /eject
