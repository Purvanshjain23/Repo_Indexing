      *
      * ENVIRONMENT: PORK
      * SYSTEM:      ACCOUNT RECEIVABLE SYSTEM
      * PROGRAM:     ARCUSTBAL
      * TITLE:       FOREIGN CORPORATE TAX SAVINGS
      *              CALCULATE CUSTOMER'S OUTSTANDING BALANCE
      * PROGRAMMER:  LEANNE FEDOR
      * CREATED:     10/24/97
      *
      *  FUNCTION:   THIS PROGRAM CALCULATES A CUSTOMER'S BALANCE DUE
      *              AS OF A SPECIFIED DATE.  THIS LOGIC IS CUSTOMIZED
      *              FOR THE FOREIGN CORPORATE TAX SAVINGS INFO.
      *
      *              THE LOGIC WILL CALCULATE THE CUSTOMER'S BALANCE
      *              FOR A SINGLE COMPANY IF THE INCOMING PARM FOR COMPANY
      *              IS POPULATED. IF THE INCOMING COMPANY IS ZERO, THE
      *              LOGIC CALCULATES THE TOTAL BALANCE FOR THE INCOMING
      *              CUSTOMER FOR ALL COMPANIES HE IS ASSOCIATED WITH.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      * 07/31/98  BRUCE LAWSON
      *           RECOMPILE ONLY. OMS FILE CHANGE
      * 08/04/05  BARB GUTIERREZ
      *           RECOMPILE ONLY DUE TO DATABASE FILE CHANGES.
      * 08/02/16  BRAD BADEN    PROJECT: E5262
      *           DO NOT PROCESS A/R HEADER INVOICE RECORDS WITH A
      *           SUFFIX = 'RB'  (REBILL).  NOTE: THIS PROGRAM HAS
      *           NEVER BEEN CALLED IN THE PRODUCTIN ENVIRONMENT.
      /EJECT
      *******************************************************************
      * FILE SPECIFICTIONS
      *******************************************************************
      *
      *
     FARBDCPL6IF  E           K        DISK
      *  A/R Header                Co/Customer AR
      *
     FARBECPL1IF  E           K        DISK
      *  A/R DETAIL
      *              Co/Invoice number/Invoice suffix/Sequence number
      *
     FCAEOREL3IF  E           K        DISK
      *   Company/Customer
      *
      *
      *******************************************************************
      * EXTENSION SPECIFICATIONS
      *******************************************************************
      *
      *******************************************************************
      * INPUT SPECS
      *******************************************************************
      *
      *
      *---------------------------------------------------------------
      *  NAMED CONSTANTS
      *---------------------------------------------------------------
      *
     I              'Y'                   C         YES
     I              'N'                   C         NO
      *
      *
      *---------------------------------------------------------------
      *  DATA STRUCTURES
      *---------------------------------------------------------------
      *
      *---------------------------------------------------------------
      * STANDARD PROGRAM STATUS DATA STRUCTURE
      *---------------------------------------------------------------
      *    EXTERNALLY DEFINED AS UTPGFR (RECORD FORMAT: PGMDSR)
     IPGMDS     ESDSUTPGFR
      *
      *
     I                                        1   1 DUMMYD
      *  THE PRECEDING DUMMY DATA STRUCTURE IS ONLY TO KEEP THE I-SPEC
      *  INFO FROM RUNNING OVER INTO THE 'CALC SPECS' ON THE COMPILE
      *  LISTING.  IT SERVES NO OTHER PURPOSE AND CAN BE DELETED
      *  AT ANY TIME!!!
      /EJECT
      *******************************************************************
      * MAINLINE PROCESSING
      *******************************************************************
      *
      * POSITION IN THE COMPANY/CUSTOMER FILE WITH THE A/R CUSTOMER VALUE
      * PASSED IN AS A PARM.
      *
     C           XXANC7    SETLLCAEOREL3
      *
      *
     C           *IN90     DOUEQ*ON                        MAIN DO LOOP
     C           XXANC7    READECAEOREL3                 90
     C           *IN90     IFEQ *OFF                       IF NOT EOF
      *
     C           XXAIC3    IFEQ 0                          IF PROCESS
     C           XXAIC3    OREQ EOAIC3
     C                     EXSR $BAL
     C                     ENDIF                           IF PROCESS
      *
     C                     ENDIF                           IF NOT EOF
     C                     ENDDO                           MAIN DO LOOP
      *
      *
      * EOF PROCESSING
      *
     C                     SETON                     LR
      /EJECT
      *-----------------------------------------------------------------
      * $BAL - DETERMINE THE BALANCE FOR THIS CUSTOMER AS OF THE CUTOFF
      *        DATE.
      *-----------------------------------------------------------------
      *
     C           $BAL      BEGSR
      *
      *
      * POSITION IN A/R HEADER FILE ON COMPANY AND CUSTOMER.
      *
     C           KEY1      SETLLARBDCPL6
      *
      *
      * PROCESS EACH A/R HEADER RECORD FOR THIS COMPANY/CUSTOMER THAT:
      *   1) DOES NOT HAVE A PURGE STATUS OF 'H'ISTORY
      *   2) DOES NOT HAVE A SUFFIX OF 'CB' (CHARGEBACK) OR 'RB' (REBILL)
      *   3) HAS A TRANSACTION DATE ON OR BEFORE THE CUTOFF DATE
      *
      * 08/02/16 JBB E5262 - ALSO EXCLUDE 'RB' SUFFIXES (REBILL)
      *
     C           *IN91     DOUEQ*ON                        DO BAL LOOP
     C           KEY1      READEARBDCPL6                 91
     C           *IN91     IFEQ *OFF                       IF A/R HEADER
     C           BDE3ST    ANDNE'H'
     C           BDCEST    ANDNE'CB'
     C           BDCEST    ANDNE'RB'
     C           BDB4DT    ANDLEXXDT
      *
      *
      *
      * CALCULATE THE BALANCE FOR THIS A/R HEADER RECORD
      * AS OF THE CUTOFF DATE. BECAUSE THERE IS NO TIE FROM
      * THE A/R HEADER TO THE A/R DETAIL FILE FOR CREDIT
      * MEMEOS AND 'ON ACCOUNT' RECORDS, WE HAD TO
      * CREATE SEPARATE LOGIC TO CALCULATE THE REMAINING BALANCE
      * DUE.
      *
     C                     SELEC
     C           BDCEST    WHEQ 'IN'
     C           BDCEST    OREQ 'DM'
     C                     Z-ADDBDBSVA    WKDUE
     C                     EXSR $DUEIN
      *
     C           BDCEST    WHEQ 'CM'
     C           BDCEST    OREQ 'OA'
     C                     EXSR $DUECM
     C                     ENDSL
      *
      * ADD THE VALUE YOU HAVE CALCULATED TO THE PARM FIELD
      * THAT WILL BE RETURNED TO THE CALLING PROGRAM.
      *
     C                     ADD  WKDUE     XXBAL
      *
     C                     ENDIF                           IF A/R HEADER
     C                     ENDDO                           DO BAL LOOP
      *
      *
     C                     ENDSR
      /EJECT
      *--------------------------------------------------------------------
      * $DUEIN - CALCULATE AMOUNT DUE FOR A/R HEADER RECORD AS OF CUTOFF
      *          DATE (INVOICES AND DEBIT MEMOS ONLY)
      *--------------------------------------------------------------------
      *
      * CALCULATE THE REMAINING BALANCE FOR THIS A/R HEADER RECORD
      * AS OF THE CUTOFF DATE BY PROCESSING ALL THE A/R DETAIL
      * RECORDS.
      *
     C           $DUEIN    BEGSR
      *
      *
      * POSITION IN THE A/R DETAIL FILE USING THE VALUES FROM THE
      * A/R HEADER RECORD:
      *                    COMPANY NUMBER
      *                    INVOICE NUMBER
      *                    INVOICE SUFFIX
      *
      *
     C           KEY2      SETLLARBECPL1
      *
      *
      * PROCESS EACH A/R DETAIL RECORD FOR THE A/R HEADER RECORD THAT:
      *   1) DOES NOT HAVE AN ADJUSTMENT TYPE CODE OF ARM OR ARC
      *   2) HAS A G/L POST DATE ON OR BEFORE THE CUTOFF DATE
      *
     C           *IN93     DOUEQ*ON                        DO DUE LOOP
     C           KEY2      READEARBECPL1                 93
     C           *IN93     IFEQ *OFF                       IF A/R DTL
     C           BFUHCD    ANDNE'ARM'
     C           BFUHCD    ANDNE'ARC'
     C           BFHYDT    ANDLEXXDT
     C           WKDUE     SUB  BFBSVA    WKDUE
     C                     ENDIF                           IF A/R DTL
     C                     ENDDO                           DO DUE LOOP
      *
     C                     ENDSR
      /EJECT
      *--------------------------------------------------------------------
      * $DUECM - CALCULATE AMOUNT DUE FOR A/R HEADER RECORD AS OF CUTOFF
      *          DATE  (CREDIT MEMOS AND 'ON ACCOUNT' RECORDS ONLY)
      *--------------------------------------------------------------------
      *
      * NOW, CALCULATE THE REMAINING BALANCE FOR THIS A/R
      * HEADER RECORD AS OF THE CUTOFF DATE. BECAUSE THERE
      * IS NO TIE FROM THE A/R HEADER TO THE A/R DETAIL FILE
      * FOR CREDIT MEMOS AND 'ON ACCOUNT' RECORDS, WE USE VALUES
      * FROM THE A/R HEADER RECORD ITSELF FOR THE REMAINING BALANCE
      * DUE.
      *
      * IF THE 'PAID IN FULL DATE' IS AFTER THE USER'S CUTOFF DATE
      *   SET THE BALANCE DUE EQUAL TO THE 'TRANSACTION AMOUNT'
      * IF THE 'PAID IN FULL DATE' IS ON OR BEFORE THE USER'S CUTOFF DATE
      *   SET THE BALANCE DUE EQUAL TO THE 'REMAINING BALANCE DUE' VALUE
      *
     C           $DUECM    BEGSR
      *
     C           BDH4DT    IFGT XXDT
     C                     Z-SUBBDBSVA    WKDUE
     C                     ELSE
     C                     Z-SUBBDBGVA    WKDUE
     C                     ENDIF
      *
     C                     ENDSR
      /EJECT
      *---------------------------------------------------------------
      * *INZSR - INITIALIZATION SUBROUTINE
      *---------------------------------------------------------------
      *
     C           *INZSR    BEGSR
      *
      * PARMS
      *
     C           *ENTRY    PLIST
     C                     PARM           XXAIC3           COMPANY NUMBER
     C                     PARM           XXDT    70       CUTOFF DATE CYYMMDD
     C                     PARM           XXANC7           A/R CUSTOMER NUMBER
     C                     PARM           XXBAL  112       A/R CUSTOMER BALANCE
      *
     C           *LIKE     DEFN EOAIC3    XXAIC3
     C           *LIKE     DEFN EOBKC7    XXANC7
      *
      * KEY LISTS
      *
     C           KEY1      KLIST
     C                     KFLD           EOAIC3           COMPANY NUMBER
     C                     KFLD           XXANC7           A/R CUSTOMER NUMBER
      *
      *
     C           KEY2      KLIST
     C                     KFLD           BDAIC3           COMPANY NUMBER
     C                     KFLD           BDCONB           INVOICE NUMBER
     C                     KFLD           BDCEST           INVOICE SUFFIX
      *
      * WORKFIELDS
      *
     C                     Z-ADD0         WKDUE  152
      *
      *
     C                     ENDSR
      /EJECT
