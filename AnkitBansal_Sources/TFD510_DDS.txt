     A*%%TS  SD  20050303  132214  ISLRAMS     REL-V5R2M0  5722-WDS
     A*****************************************************************
     A*
     A*  SYSTEM:        TRIUMPH FOODS
     A*  DISPLAY FILE:  TFD510 SELECT GL CODE (RESTRICTED TO A SINGLE COMPANY)
     A*  CREATED BY:    LEANNE FEDOR
     A*  DATE:          03/03/05
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/TFPREF)
     A                                      PRINT
     A                                      CF12(12 'PREVIOUS')
     A N82                                  ROLLUP(25 'PAGE FORWARD')
     A*---------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*---------------------------------------------------------------
     A          R TF510S1                   SFL
     A*%%TS  SD  20050303  105840  ISLRAMS     REL-V5R2M0  5722-WDS
     A            HDRRN1    R        H      REFFLD(@@RRN)
     A            D1GLCD    R        O  9  1REFFLD(@@GLDBCD)
     A            D1GLCDDS  R        O  9 12REFFLD(@@GLDBCDDS)
     A*
     A*---------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*---------------------------------------------------------------
     A          R TF510C1                   SFLCTL(TF510S1)
     A*%%TS  SD  20050303  132214  ISLRAMS     REL-V5R2M0  5722-WDS
     A                                      SFLSIZ(0100)
     A                                      SFLPAG(0009)
     A                                      WINDOW(TF510EC)
     A                                      OVERLAY
     A                                      SFLCSRRRN(&HDRRN)
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A            HDPAG1    R        H      REFFLD(@@PAG)
     A                                      SFLRCDNBR
     A            HDRRN     R        H      REFFLD(@@RRN)
     A            SDPGM     R        O  1  1REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 14'SELECT GL CODE'
     A                                      DSPATR(HI)
     A                                  3  1'Company:'
     A            C1CONO    R        O  3 10REFFLD(TFREF/@@CONO *LIBL/TFPREF)
     A                                      EDTCDE(Z)
     A                                      COLOR(PNK)
     A            C1CONM    R        O  3 15REFFLD(TFREF/@@CONM *LIBL/TFPREF)
     A                                      COLOR(PNK)
     A                                  5  1'Position Cursor, Press Enter'
     A                                      DSPATR(HI)
     A                                  7  1'Code'
     A                                      DSPATR(HI)
     A                                  7 12'Description'
     A                                      DSPATR(HI)
     A            C1GLCD    R        B  8  1REFFLD(@@GLDBCD)
     A                                      DSPATR(HI)
     A            C1GLCDDS  R        B  8 12REFFLD(@@GLDBCDDS)
     A                                      CHGINPDFT(UL LC FE)
     A                                      DSPATR(HI)
     A*---------------------------------------------------------------
     A* COMMAND LINE LINE
     A*---------------------------------------------------------------
     A          R TF510K1
     A                                      WINDOW(TF510EC)
     A                                      OVERLAY
     A  43                             18  1'Message program failed. Call IS.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 19  1'F12=Previous'
      *
     A*-------------------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
     A*-------------------------------------------------------------------------
     A          R TF510ES                   SFL SFLMSGRCD(19)
      *
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
     A*
     A*-------------------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*-------------------------------------------------------------------------
     A*
     A          R TF510EC                   SFLCTL(TF510ES)
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A                                      WINDOW(*DFT 20 45)
     A                                      WDWBORDER((*DSPATR RI) (*CHAR '    -
     A                                          '))
     A            MSGPGM                    SFLPGMQ(10)
     A*
     A*---------------------------------------------------------------
     A* ASSUME FORMAT -- REQUIRED FOR WINDOWS
     A*---------------------------------------------------------------
     A          R ASSUME
     A                                      ASSUME
     A                                      OVERLAY
     A                                 24 79' '
