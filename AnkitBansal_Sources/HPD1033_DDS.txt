     A*%%TS  SD  20081117  111433  ISLRAMS     REL-V5R4M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD1033
     A*  TITLE:         CHANGE STATUS OF HPS-TO-JDE UPLOAD CONTROL RECORD
     A*  PROGRAMMER:    LEANNE RAMSEY
     A*  DATE:          11/10/08
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A                                      CF09(09 'ACCEPT')
     A                                      CF20(20 'WRKSPLF')
     A*--------------------------------------------------------------*
     A* RECORD FORMAT ONE
     A*--------------------------------------------------------------*
     A          R HP1033R1
     A*%%TS  SD  20081117  111433  ISLRAMS     REL-V5R4M0  5722-WDS
     A                                      OVERLAY
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 27'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  2 14'CHANGE STATUS OF HPS-TO-JDE UPLOAD-
     A                                       CONTROL RECORD'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  5  2'You are changing the status of:'
     A            D1HUCD    R        O  5 34REFFLD(HSRREF/@@HUCD *LIBL/HSPREF)
     A            D1HUDS    R        O  5 38REFFLD(HSRREF/@@HUDS *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A                                  6 28'From:'
     A            D1FRUPLSCDR        O  6 34REFFLD(HSRREF/@@UPLSCD *LIBL/HSPREF)
     A            D1FRUPLSDSR        O  6 38REFFLD(HSRREF/@@UPLSDS *LIBL/HSPREF)
     A                                  7 30'To:'
     A            D1TOUPLSCDR        O  7 34REFFLD(HSRREF/@@UPLSCD *LIBL/HSPREF)
     A            D1TOUPLSDSR        O  7 38REFFLD(HSRREF/@@UPLSDS *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A*
     A* CHANGING STATUS TO RERUN
     A*
     A  87                             10  2'When you want to reupload HPS data-
     A                                       to JDE, take these steps:'
     A                                      COLOR(BLU)
     A                                      DSPATR(UL)
     A  87                             11  2'1) Void/Delete the JDE batch that -
     A                                      was already uploaded.'
     A                                      COLOR(BLU)
     A  87                             12  5'If you do not Void/Delete the batc-
     A                                      h, you will have DUPLICATE data in -
     A                                      JDE.'
     A                                      COLOR(PNK)
     A  87                             13  2'2) Use this function to change the-
     A                                       Control Record status to Rerun.'
     A                                      COLOR(BLU)
     A  87                             14  2'3) ReUpload the HPS data thru the -
     A                                      normal HPS-to-JDE Upload function.'
     A                                      COLOR(BLU)
     A*
     A* CHANGING STATUS TO UPLOADED
     A*
     A  88                             15  2'The only time that you would want -
     A                                      to change a Control Record status b-
     A                                      ack to'
     A                                      COLOR(BLU)
     A  88                             16  2'Uploaded to JDE'
     A                                      DSPATR(HI)
     A  88                             16 18'is when you have set the status to-
     A                                       Rerun and do not truly'
     A                                      COLOR(BLU)
     A  88                             17  2'wish to Delete/Void the JDE Batch -
     A                                      and reupload.'
     A                                      COLOR(BLU)
      *--------------------------------------------------------------*
      * COMMAND KEY RECORD 1
      *--------------------------------------------------------------*
     A          R HP1033K1
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A                                 23 11'F9=Accept'
     A                                 23 22'F20=Spool Files'
      *
      *--------------------------------------------------------------*
      * ERROR MESSAGE SUBFILE FORMAT
      *--------------------------------------------------------------*
     A          R HP1033ES                  SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
      *--------------------------------------------------------------*
      * ERROR MESSAGE SUBFILE CONTROL FORMAT
      *--------------------------------------------------------------*
      *
     A          R HP1033EC                  SFLCTL(HP1033ES)
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
