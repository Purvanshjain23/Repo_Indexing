     A*%%TS  SD  20091014  132429  ISLRAMS     REL-V5R4M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        TRIUMPH FOODS
     A*  DISPLAY FILE:  TFD105
     A*  TITLE:         REVISE CASH DISTRIBUTION HEADER
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  DATE:          05/16/05
     A*
     A*  INDICATOR 60 IS ON WHEN IN REVISE MODE
     A*  INDICATOR 61 IS ON WHEN IN CREATE MODE
     A*  INDICATOR 62 IS ON WHEN IN DELETE MODE
     A*  INDICATOR 63 IS ON WHEN THE STATUS OF THE HEADER IS "ASSIGNED"
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/TFPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CF03(03 'EXIT')
     A  60N62                               CF05(05 'REFRESH')
     A N62                                  CF09(09 'ACCEPT')
     A  60N62N63                            CF11(11 'DELETE')
     A                                      CA12(12 'PREVIOUS')
     A*
     A*--------------------------------------------------------------
     A* RECORD FORMAT ONE
     A*--------------------------------------------------------------
     A          R TF105R1
     A*%%TS  SD  20091014  132429  ISLRAMS     REL-V5R4M0  5722-WDS
     A                                      SETOF(30)
     A                                      SETOF(31)
     A                                      SETOF(32)
     A                                      SETOF(33)
     A                                      SETOF(34)
     A                                      SETOF(36)
     A                                      SETOF(37)
     A                                      SETOF(38)
     A                                      SETOF(39)
     A                                      OVERLAY
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 24'REVISE CASH DISTRIBUTION HEADER'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USR)
     A                                      DSPATR(HI)
     A            D1MODE         6A  O  2  2DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USR)
     A                                      DSPATR(HI)
     A                                  3  2'Daily date ..'
     A            D1MDY     R        B  3 16REFFLD(TFREF/@@DT06 *LIBL/TFPREF)
     A                                      EDTWRD('  /  /  -')
     A  30                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A N62N63N30                            DSPATR(HI)
     A N62N63                               DSPATR(UL)
     A  62
     AO 63                                  DSPATR(PR)
     A                                  3 30'Batch number:'
     A                                      COLOR(PNK)
     A            D1BTSN    R        O  3 44REFFLD(TFREF/@@BTSN *LIBL/TFPREF)
     A                                      EDTCDE(Z)
     A                                  3 52'Status:'
     A                                      COLOR(PNK)
     A            D1STDS    R        O  3 60REFFLD(TFREF/@@STDS *LIBL/TFPREF)
     A                                  5  2'Deposit-Prior day total cash colle-
     A                                      cted .:'
     A                                      COLOR(PNK)
     A            D1DPDCCAM R        O  5 44REFFLD(TFREF/@@DPDCCAM *LIBL/TFPREF)
     A                                      EDTCDE(K)
     A                                  6  2'Deposit-Current day total cash col-
     A                                      lected:'
     A                                      COLOR(PNK)
     A            D1DCDCCAM R        O  6 44REFFLD(TFREF/@@DCDCCAM *LIBL/TFPREF)
     A                                      EDTCDE(K)
     A                                  7  2'Bank report-Prior day total cash c-
     A                                      ollected .............'
     A            D1BPDCCAM R        B  7 60REFFLD(TFREF/@@BPDCCAM *LIBL/TFPREF)
     A                                      EDTCDE(K)
     A  31                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A N62N31                               DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(PR)
     A                                  8  2'Bank report-Current day total cash-
     A                                       collected ...........'
     A            D1BCDCCAM R        B  8 60REFFLD(TFREF/@@BCDCCAM *LIBL/TFPREF)
     A                                      EDTCDE(K)
     A  32                                  DSPATR(PC)
     A  32                                  DSPATR(RI)
     A N62N32                               DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(PR)
     A                                 10  2'Bank report-Opening available amou-
     A                                      nt ...................'
     A            D1BOAAM   R        B 10 60REFFLD(TFREF/@@BOAAM *LIBL/TFPREF)
     A                                      EDTCDE(K)
     A  33                                  DSPATR(PC)
     A  33                                  DSPATR(RI)
     A N62N33                               DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(PR)
     A                                 10 78'+'
     A                                 11  2'Bank report-Current day immediate -
     A                                      availability amount ..'
     A            D1BCDIAAM R        B 11 60REFFLD(TFREF/@@BCDIAAM *LIBL/TFPREF)
     A                                      EDTCDE(K)
     A  34                                  DSPATR(PC)
     A  34                                  DSPATR(RI)
     A N62N34                               DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(PR)
     A                                 11 78'+'
     A                                 12  2'Unapplied wire transfer pre-paymen-
     A                                      ts ...................'
     A            D1WTPPAM  R        B 12 60REFFLD(TFREF/@@WTPPAM *LIBL/TFPREF)
     A                                      EDTCDE(K)
     A  36                                  DSPATR(PC)
     A  36                                  DSPATR(RI)
     A N62N36                               DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(PR)
     A                                 12 78'-'
     A                                 13  2'Required balance in account ......-
     A                                      ......................'
     A            D1RBAM    R        B 13 60REFFLD(TFREF/@@RBAM *LIBL/TFPREF)
     A                                      EDTCDE(K)
     A  37                                  DSPATR(PC)
     A  37                                  DSPATR(RI)
     A N62N37                               DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(PR)
     A                                 13 78'-'
     A                                 14 38'Cash payment amount:'
     A                                      COLOR(PNK)
     A            D1CPAM    R        O 14 60REFFLD(TFREF/@@CPAM *LIBL/TFPREF)
     A                                      EDTCDE(K)
     A  39                                  DSPATR(RI)
     A  39                                  DSPATR(PC)
     A                                 14 78'='
     A                                 16  2'TF distribution adjustment amount -
     A                                      .......'
     A            D1TDAAM   R        B 16 44REFFLD(TFREF/@@TDAAM *LIBL/TFPREF)
     A                                      EDTCDE(K)
     A N62                                  DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(PR)
     A                                 17  2'TF distribution adjustment comment-
     A                                       ......'
     A            D1TDACOM  R        B 17 44REFFLD(TFREF/@@TDACOM *LIBL/TFPREF)
     A                                      CHECK(LC)
     A  38                                  DSPATR(PC)
     A  38                                  DSPATR(RI)
     A N62N38                               DSPATR(HI)
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(PR)
     A                                 18  2'Sales expense cash distributed amo-
     A                                      unt ..:'
     A                                      COLOR(PNK)
     A            D1ASXCDAM R        O 18 44REFFLD(TFREF/@@ASXCDAM *LIBL/TFPREF)
     A                                      EDTCDE(K)
     A                                 19  2'SBF cash distributed amount ......-
     A                                      .......'
     A                                      COLOR(PNK)
     A            D1SCDAM   R        O 19 44REFFLD(TFREF/@@SCDAM *LIBL/TFPREF)
     A                                      EDTCDE(K)
     A                                 20  2'TF cash distributed amount .......-
     A                                      ......:'
     A                                      COLOR(PNK)
     A            D1TCDAM   R        O 20 44REFFLD(TFREF/@@TCDAM *LIBL/TFPREF)
     A                                      EDTCDE(K)
     A                                 21  2'TF total payment amount ..........-
     A                                      ......:'
     A                                      COLOR(PNK)
     A            D1TTPAM   R        O 21 44REFFLD(TFREF/@@TTPAM *LIBL/TFPREF)
     A                                      EDTCDE(K)
     A*--------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*--------------------------------------------------------------
     A          R TF105K1
     A*%%TS  SD  20050608  120008  ISLRAMS     REL-V5R3M0  5722-WDS
     A  43                             22  2'Message program failed.   Call MIS-
     A                                       user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A N62N61                          23 11'F5=Refresh'
     A N62                             23 23'F9=Accept'
     A N61N62N63                       23 34'F11=Delete'
     A                                 23 46'F12=Previous'
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *---------------------------------------------------------------
     A          R TF105ES                   SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE CONTROL FORMAT
      *---------------------------------------------------------------
     A          R TF105EC                   SFLCTL(TF105ES)
      *
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
