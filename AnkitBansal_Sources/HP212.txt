      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      HPS Data Mart
      * PROGRAM:     HP212 - Build DataMart Nursery/Grow Finish Farms from HPS
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     06/02/00
      *
      * FUNCTION:    This file in the Data Mart will be re-created with
      *              each load of the Data Mart.
      *
      *              Only Nursery and Grow Finish farms are included.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 06/05/01  LeAnne Fedor
      *           Added 'multisite' to Datamart Farm Site file.
      *
      * 06/26/01  LeAnne Fedor
      *           Three fields (manager/supervisor/multisite) renamed in HPS Farm Site file.
      *           Manager and supervisor fields removed from HPS Hog Group file.
      *
      * 09/04/01  LeAnne Fedor
      *           Recompile only. New 'square feet' fields added to Farm Site file.
      *
      * 01/06/03  LeAnne Fedor
      *           Added 'group type' to Datamart Farm Site file.
      *
      * 09/29/03  Barb Gutierrez
      *           We're making all datamart farm masters identical. Therefore we needed to bring
      *           in pod number, pod desc, breed stock code, hog capacity and we added a production
      *           phase sequence number so we could sort production phase in cognos according to the
      *           phase instead of alphabetical. (bgf/nur/gf versus bgf/gf/nur)
      *
      * 10/27/03  LeAnne Fedor
      *           Added 'County'.
      *
      * 11/22/04  LeAnne Fedor
      *           Added logic to calculate Hog Capacity for Farm.
      *
      * 12/27/04  LeAnne Fedor
      *           Added logic to calculate Farm Size for Farm.
      *
      * 10/15/13  LeAnne Ramsey (E2831)
      *           Recompile only. Added field 'MTech Reference'.
      *
      * 10/15/13  LeAnne Ramsey (E2831)
      *           Recompile only. Added field 'MTech Reference'.
      /eject
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fhsp016    if   e           k disk
      *  Manager
      *
      *
     Fhsp018    if   e           k disk
      *  Farm site
      *
      *
     Fhppf018   o    e             disk    rename(fsrec:fsrec1) prefix(p1)
      *
      /eject
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      *
      ****************************************************************
      * ARRAYS AND TABLES
      ****************************************************************
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Control fields
      *
     D process         s              1
     D start1          s              1  0
      *
      * Work fields
      *
     D wkalphfscd      s              5
     D wkalphcell      s              2
      *
      *
      * Parm fields
      *
     D xxsize          s                   like(p1fssize)
     D xxcphd          s                   like(p1fscphd)
     D xxppcd          s                   like(fsppcd)
     D xxfscd          s                   like(fsfscd)
     D xxfssqft        s              7  0
      *
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * MAINLINE
      ****************************************************************
      *
      * Process farm records in the Hog Production System (omit BGF farms)
      *
     C                   dou       *in90 = *on                                  Main do
     C                   read      hsp018                                 90
     C                   if        *in90 = *off and                             If not EOF
     C                             (fsppcd = 'GF   ' or fsppcd = 'NUR  ')
      *
      * Populate fields that map directly file-to-file
      *
     C                   z-add     fsfscd        p1fsfscd
     C                   move      fscocd        p1fscocd
     C                   move      fsdvcd        p1fsdvcd
     C                   move      fsrgcd        p1fsrgcd
     C                   move      fsfsnm        p1fsfsnm
     C                   move      fsfsbo        p1fsfsbo
     C                   move      fsfmbo        p1fsfmbo
     C                   move      fsdzcd        p1fsdzcd
     C                   move      fsppcd        p1fsppcd
     C                   move      fsptcd        p1fsptcd
     C                   move      fsgtcd        p1fsgtcd
     C                   move      fscrcd        p1fscrcd
     C                   z-add     fscell        p1fscell
     C                   move      fspmgcd       p1fsmgcd
     C                   move      fscmgcd       p1fssvcd
     C                   move      fsmmgcd       p1fsmlcd
     C                   move      fscjd         p1fscjd
     C                   z-add     fspajd        p1fspajd
     C                   z-add     fsfajd        p1fsfajd
     C                   move      fscnty        p1fscnty
     C                   move      fssacd        p1fssacd
     C                   z-add     fspod         p1fspod
      *
      *
      *
      * If you have a Target Square Feet Per Pig value, calculate Hog Capacity:
      *   1) Retrieve total square feet on the farm
      *   2) Calc hog capacity as:
      *       total square feet on the farm / target square feet per pig
      *   3) If you have a Hog Capacity value,
      *      calc Farm Size based on Production Phase/Hog Capacity
      *
     C                   if        fshdsqft <> 0                                If target
      *
     C                   call      'HP8003'
     C                   parm      fsfscd        xxfscd
     C                   parm      0             xxfssqft
      *
     C     xxfssqft      div(h)    fshdsqft      p1fscphd
      *
     C                   if        p1fscphd <> 0
     C                   call      'HP8018'
     C                   parm      fsppcd        xxppcd
     C                   parm      p1fscphd      xxcphd
     C     p1fssize      parm      *blank        xxsize
     C                   endif
      *
     C                   endif                                                  If target
      *
      *
      * Determine which sequence number to use for the production phase.  This will control
      * how the phase is sorted within cognos.
      *
     C                   select
     C                   when      fsppcd = 'NUR'
     C                   z-add     2             p1fsppcdsq
     C                   when      fsppcd = 'GF '
     C                   z-add     3             p1fsppcdsq
     C                   endsl
      *
      * Retrieve 'production manager name' and concatenate with code
      * into a single name field.
      *
     C     fspmgcd       chain     hsp016                             92
     C                   if        *in92 = *off                                 If found
     C                   eval      p1fsmgnm = %trim(fspmgcd) + ' ' +
     C                             %trim(mgmgnm)
     C                   endif                                                  If found
      *
      * Retrieve 'cell manager name' and concatenate with code
      * into a single name field.
      *
     C     fscmgcd       chain     hsp016                             92
     C                   if        *in92 = *off                                 If found
     C                   eval      p1fssvnm = %trim(fscmgcd) + ' ' +
     C                             %trim(mgmgnm)
     C                   endif                                                  If found
      *
      * Retrieve 'multisite manager name' and concatenate with code
      * into a single name field.
      *
     C     fsmmgcd       chain     hsp016                             92
     C                   if        *in92 = *off                                 If found
     C                   eval      p1fsmlnm = %trim(fsmmgcd) + ' ' +
     C                             %trim(mgmgnm)
     C                   endif                                                  If found
      *
      *
      * Concatenate the word 'farm' and the farm site number into
      * a single 'description' field.
      *
     C                   movel     fsfscd        wkalphfscd
      *
     C                   move      yes           process
     C                   z-add     1             start1
      *
     C                   dow       process = yes and start1 <= 5                Do farm loop
     C                   if        %subst(wkalphfscd: start1: 1) = '0'          If replace
     C                   eval      %subst(wkalphfscd: start1: 1) = *blank
     C                   else
     C                   move      no            process
     C                   endif                                                  If replace
     C                   add       1             start1
     C                   enddo                                                  Do farm loop
      *
     C                   eval      p1fsfsds = 'FARM' + ' ' + %trim(wkalphfscd)
      *
      *
      * Concatenate the word 'cell' and the cell number into
      * a single 'description' field.
      *
     C                   movel     fscell        wkalphcell
      *
     C                   move      yes           process
     C                   z-add     1             start1
      *
     C                   dow       process = yes and start1 <= 2                Do cell loop
     C                   if        %subst(wkalphcell: start1: 1) = '0'          If replace
     C                   eval      %subst(wkalphcell: start1: 1) = *blank
     C                   else
     C                   move      no            process
     C                   endif                                                  If replace
     C                   add       1             start1
     C                   enddo                                                  Do cell loop
      *
     C                   eval      p1fscellds = 'CELL' + ' ' + %trim(wkalphcell)
      *
      *
      * Write record
      *
     C                   write     fsrec1
     C                   clear                   fsrec1
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Main do
      *
     C                   seton                                        lr
      /eject
