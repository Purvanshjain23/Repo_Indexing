/********************************************************************/
/*                                                                  */
/*  SYNOPSIS -  JOB CONTROL PROMPT FOR PRINTER OVERRIDE INFORMATION */
/*              GENERIC PROMPTING FUNCTION USED TO POST INVOICES    */
/*                                                                  */
/*  SYSTEM    - ORDER MANAGEMENT SYSTEM                             */
/*  PROGRAM   - POST INVOICES                                       */
/*  DATE      - 08/01/95                                            */
/*                                                                  */
/*      PARAMETERS:                                                 */
/*                                                                  */
/*     O    &RETURN     - RETURN CODE                               */
/*     I    &PGM        - CL PROGRAM TO SUBMIT                      */
/*     I    &APPLIC     - APPLICATION CODE                          */
/*     B    &OUTQ       - OUTPUT QUEUE                              */
/*     B    &HOLD       - HOLD STATUS                               */
/*     B    &SAVE       -  SAVE STATUS                              */
/*     B    &NQSTAT     -  INDICATOR WHETHER MUST RUN IN NIGHT Q    */
/*     B    &NIGHTQ     -  NIGHT JOB QUEUE                          */
/*     I    &COMPNYN    -  COMPANY NUMBER (PASSED)                  */
/*     I    &INVOICN    -  INVOICE NUMBER (PASSED)                  */
/*     B    &COPYN      -  NO. OF COPIES  (PASSED)                  */
/*                                                                  */
/********************************************************************/
/*  SLM 11/04/05  PASS NEW PARM OF PROCESS CUSTOMER TYPE            */
/*  LJB 07/06/06  CHANGE TEST FOR PRINTER PARMS PROMPT TO CHECK     */
/*                FOR BLANK OUTQ PARM INSTEAD OF ZERO INVOICE#      */
 /* P16169 RMC  12/01/2020  SUBMIT FOR INVOICE <>0 TOO             */
/********************************************************************/
 PDIPSUPR:   PGM        PARM(&RETURN &PGM &APPLIC &OUTQ &HOLD &SAVE +
                          &NQSTAT &NIGHTQ &COMPNYN &INVOICN &COPYN +
                          &PRCUSTT)
/* PARMS                                                                      */
        DCL    &RETURN     *CHAR   (7)           /* Return Code               */
        DCL    &APPLIC     *CHAR   (06)          /* Application Code          */
        DCL    &PGM        *CHAR   (10)          /* CL to Submit              */
        DCL    &COPYN      *DEC                  /* No. of Copies             */
        DCL    &OUTQ       *CHAR   (10)          /* Output Queue              */
        DCL    &HOLD       *CHAR   (4)           /* Hold Status               */
        DCL    &SAVE       *CHAR   (4)           /* Save Status               */
        DCL    &NQSTAT     *CHAR   (01)          /* If MUST run in Night Q    */
        DCL    &NIGHTQ     *CHAR   (10)          /* Night Job Queue           */
        DCL    &DESC       *CHAR   (25)          /* PRT Pgm Description       */
        DCL    &COMPNYN    *DEC                  /* Company Number (Passed)   */
        DCL    &INVOICN    *DEC                  /* Invoice Number (Passed)   */
        DCL    &COPYN      *DEC                  /* No. of Copies  (Passed)   */
        DCL    &PRCUSTT    *CHAR   (02)          /* PROCESS CUST TYPE (PASSED)*/

/* INTERNAL FIELDS                                                            */
        DCL    &COPY       *DEC    (2 0)         /* Number of copies          */
        DCL    &ZERO       *DEC          VALUE(0.0)

/* ERROR MESSAGE FIELDS                                                       */
        DCL    &MSG        *CHAR   (512)         /* message                   */
        DCL    &MSGID      *CHAR   (7)           /* message id                */
        DCL    &MSGL       *DEC    (5 0)

/********************************************************************/
/*   INITIALIZE VARIABLES                                           */
/********************************************************************/
             CHGVAR     VAR(&RETURN) VALUE('       ')
             CHGVAR     VAR(&COPY) VALUE(&COPYN)

/********************************************************************/
/*   MONITOR FOR ALL MESSAGES AT PROGRAM LEVEL                      */
/********************************************************************/
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(MSGTRAP))

/********************************************************************/
/*   CALL PROMPT PROGRAM                                            */
/********************************************************************/
/****        IF (&INVOICN *EQ &ZERO) THEN(DO)                    ****/
             IF (&OUTQ *EQ '          ') THEN(DO)
             CHGVAR     VAR(&NQSTAT) VALUE('N')
             CALL       PGM(PDG0PVR) PARM(&RETURN &DESC &OUTQ &HOLD +
                          &SAVE &COPY &NQSTAT &NIGHTQ &APPLIC)
             IF         COND(&RETURN *NE ' ') THEN(GOTO +
                          CMDLBL(USERQUIT))
             ENDDO
             CHGVAR     VAR(&COPYN) VALUE(&COPY)

/********************************************************************/
/*   SUBMIT PRINT CL PROGRAM                                        */
/********************************************************************/
 SUBMIT:     IF (&INVOICN *EQ &ZERO) THEN(DO)
             SBMJOB     CMD(CALL PGM(&PGM) PARM(&COMPNYN &INVOICN +
                          &OUTQ &HOLD &SAVE &COPYN &NQSTAT &NIGHTQ +
                          &PRCUSTT)) JOB(INVPOST)
             ENDDO
             ELSE DO
     /*      CALL       PGM(&PGM) PARM(&COMPNYN &INVOICN &OUTQ &HOLD +
                          &SAVE &COPYN &NQSTAT &NIGHTQ &PRCUSTT)       */
 /* P16169 RMC  12/01/2020  SUBMIT FOR BOTH                            */
             SBMJOB     CMD(CALL PGM(&PGM) PARM(&COMPNYN &INVOICN +
                          &OUTQ &HOLD &SAVE &COPYN &NQSTAT &NIGHTQ +
                          &PRCUSTT)) JOB(INVPOST)
             ENDDO

             GOTO       CMDLBL(END)

/*-------------------------USER QUIT--------------------------------*/
 USERQUIT:   CHGVAR     VAR(&RETURN) VALUE('Y2U0005')
             GOTO       CMDLBL(END)

/*----------------------RECEIVE MESSAGES----------------------------*/
 MSGTRAP:    RCVMSG     MSGTYPE(*EXCP) MSGDTA(&MSG) MSGDTALEN(&MSGL) +
                          MSGID(&MSGID)
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(END))

             SNDPGMMSG  MSGID(&MSGID) MSGF(QCPFMSG) MSGDTA(%SST(&MSG +
                          1 &MSGL)) MSGTYPE(*ESCAPE)
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(END))

 END:        ENDPGM

