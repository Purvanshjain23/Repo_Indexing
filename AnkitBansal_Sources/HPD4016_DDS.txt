     A*%%TS  SD  20120229  141724  ISLRAMS     REL-V6R1M0  5761-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD4016
     A*  TITLE:         WORK WITH GROUP OVERRIDES
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  DATE:          08/26/04
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A                                      CF04(04 'PROMPT')
     A                                      CF05(05 'REFRESH')
     A                                      CF06(06 'CREATE')
     A                                      CF07(07 'LISTING')
     A                                      CF17(17 'PRINT OPTIONS')
     A                                      CF20(20 'WRKSPLF')
     A*
     A*
     A*---------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP4016S1                  SFL
     A*%%TS  SD  20120229  141724  ISLRAMS     REL-V6R1M0  5761-WDS
     A  85                                  SFLNXTCHG
     A            HDRRN1         4Y 0H
     A            HDHGSN    R        H      REFFLD(@@HGSN)
     A            D1OPT          2A  B 10  2CHGINPDFT(UL)
     A  30                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A N30                                  DSPATR(HI)
     A            D1HGCD    R        O 10  7REFFLD(HSRREF/@@HGCD *LIBL/HSPREF)
     A            D1FSCD    R        O 10 17REFFLD(HSRREF/@@FSCD *LIBL/HSPREF)
     A                                      EDTCDE(Z)
     A            D1GSCD    R        O 10 26REFFLD(HSRREF/@@GSCD *LIBL/HSPREF)
     A            D1PPCD    R        O 10 33REFFLD(HSRREF/@@PPCD *LIBL/HSPREF)
     A            D1SICD    R        O 10 41REFFLD(HSRREF/@@SICD *LIBL/HSPREF)
     A            D1OBFS    R        O 10 51REFFLD(HSRREF/@@OBFS *LIBL/HSPREF)
     A                                      EDTCDE(Z)
     A            D1ONFS    R        O 10 67REFFLD(HSRREF/@@ONFS *LIBL/HSPREF)
     A                                      EDTCDE(Z)
     A*
     A*---------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP4016C1                  SFLCTL(HP4016S1)
     A*%%TS  SD  20120229  141724  ISLRAMS     REL-V6R1M0  5761-WDS
     A                                      SFLSIZ(0050)
     A                                      SFLPAG(0010)
     A N82                                  ROLLUP(25 'PAGE FORWARD')
     A                                      SETOF(50)
     A                                      SETOF(51)
     A                                      SETOF(52)
     A                                      SETOF(53)
     A                                      SETOF(54)
     A                                      SETOF(55)
     A                                      SETOF(56)
     A                                      RTNCSRLOC(&RCD &FLD)
     A                                      OVERLAY
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 26'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  2 24'WORK WITH GROUP OVERRIDES'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  4  2'2=Revise'
     A                                  4 12'4=Delete'
     A                                  4 22'6=Print'
     A                                  6 10'?'
     A                                      DSPATR(HI)
     A                                  6 19'?'
     A                                      DSPATR(HI)
     A                                  6 26'?'
     A                                      DSPATR(HI)
     A                                  6 35'?'
     A                                      DSPATR(HI)
     A                                  6 43'?'
     A                                      DSPATR(HI)
     A                                  6 53'?'
     A                                      DSPATR(HI)
     A                                  6 69'?'
     A                                      DSPATR(HI)
     A                                  7  7'Hog'
     A                                      DSPATR(HI)
     A                                  7 17'Farm'
     A                                      DSPATR(HI)
     A                                  7 24'Group'
     A                                      DSPATR(HI)
     A                                  7 33'Prod'
     A                                      DSPATR(HI)
     A                                  7 41'Sire'
     A                                      DSPATR(HI)
     A                                  7 49'Origin BGF'
     A                                      DSPATR(HI)
     A                                  7 62'Origin Nursery'
     A                                      DSPATR(HI)
     A                                  8  2'Opt'
     A                                      DSPATR(HI)
     A                                  8  7'Group'
     A                                      DSPATR(HI)
     A                                  8 17'Site'
     A                                      DSPATR(HI)
     A                                  8 24'Status'
     A                                      DSPATR(HI)
     A                                  8 33'Phase'
     A                                      DSPATR(HI)
     A                                  8 41'Line'
     A                                      DSPATR(HI)
     A                                  8 49'Farm Site'
     A                                      DSPATR(HI)
     A                                  8 65'Farm Site'
     A                                      DSPATR(HI)
     A            C1HGCD    R        B  9  7REFFLD(HSRREF/@@HGCD *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A  50                                  DSPATR(PC)
     A            C1FSCD    R        B  9 17REFFLD(HSRREF/@@FSCD *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A  55                                  DSPATR(PC)
     A                                      EDTCDE(Z)
     A            C1GSCD    R        B  9 26REFFLD(HSRREF/@@GSCD *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A  56                                  DSPATR(PC)
     A            C1PPCD    R        B  9 33REFFLD(HSRREF/@@PPCD *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A  51                                  DSPATR(PC)
     A            C1SICD    R        B  9 41REFFLD(HSRREF/@@SICD *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A  52                                  DSPATR(PC)
     A            C1OBFS    R        B  9 51REFFLD(HSRREF/@@OBFS *LIBL/HSPREF)
     A                                      EDTCDE(Z)
     A                                      DSPATR(HI)
     A  53                                  DSPATR(PC)
     A            C1ONFS    R        B  9 67REFFLD(HSRREF/@@ONFS *LIBL/HSPREF)
     A                                      EDTCDE(Z)
     A                                      DSPATR(HI)
     A  54                                  DSPATR(PC)
     A*
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R HP4016K1
     A*%%TS  SD  20040830  084508  ISLRAMS     REL-V5R2M0  5722-WDS
     A  43                             21  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 22  2'F3=Exit'
     A                                 22 11'F4=Prompt'
     A                                 22 22'F5=Refresh'
     A                                 22 34'F6=Create'
     A                                 22 45'F7=Listing'
     A                                 22 57'F17=Print Options'
     A                                 23 57'F20=Spool Files'
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *---------------------------------------------------------------
     A          R HP4016ES                  SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE CONTROL FORMAT
      *---------------------------------------------------------------
     A          R HP4016EC                  SFLCTL(HP4016ES)
      *
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
