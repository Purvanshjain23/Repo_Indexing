      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Triumph Foods
      * PROGRAM:     TF226 - Eopfees: Freight Variance--Paid Loads with Listing
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     11/01/05
      *
      * Function:    This program processes Paid Loads.
      *
      *
      ****************************************************************************************
      * MODIFICATIONS:
      ****************************************************************************************
      * DATE      PROGRAMMER
      *
      * 03/20/06  LeAnne Fedor
      *           Changed logic to read our new file of loads that are "frozen" for a Period.
      *
      * 03/29/06  LeAnne Fedor
      *           Added new column: Prior Period Paid Freight
      *
      * 04/07/06  LeAnne Fedor
      *           Added "difference amount" to the Freight Variance Detail file. Changed the
      *           logic to use the new file field instead of calculating the value.
      *
      * 11/22/06  LeAnne Fedor
      *           Added more fields to Freight Variance Detail file. Changed field name/text
      *           for "actual freight amount" to "allocated actual freight amount".
      *
      * 05/14/07  LeAnne Ramsey
      *           Added parms to the call to TF813:
      *               Co-owned Flag
      *               Producing Company
      *
      * 08/13/07  LeAnne Ramsey
      *           Added parm on the call to TF813:
      *               Item Type Code
      *
      * 07/17/08  LeAnne Ramsey
      *           In file TFP027-Freight Variance Detail, we:
      *              Replaced: FDAACTFAM  Allocated Actual Freight Amount
      *                  With: FDALFAM    Allocated Load Total Freight Amount
      *           So, in this program, we:
      *             1) changed the field name
      *             2) changed the column heading
      *
      * 09/11/08  LeAnne Ramsey
      *           Added parm to the call to TF813:
      *               Export/Domestic Flag
      *
      * 03/10/09  LeAnne Ramsey
      *           The users want to revert back to Allocated Actual Freight Amount
      *           from Allocated Load Total Freight Amount.
      *           This will reverse the change of 07/17/08 above!
      *           We have decided to keep two sets of fields in TFP027-Freight
      *           Variance Detail:
      *               ALLOCATED LOAD TOT FRT AM       FDALFAM
      *               LOAD TOTAL DIFFERENCE AMT       FDLDIFFAM
      *               LOAD TOTAL PRIOR DIFF AMT       FDLPDIFFAM
      *               ALLOCATED ACTUAL FRT AMT        FDAAFAM
      *               ACTUAL DIFFERENCE AMT           FDADIFFAM
      *               ACTUAL PRIOR DIFF AMT           FDAPDIFFAM
      *           We have changed this program to now use/display:
      *             1) Allocated Actual Freight Amount
      *
      * 01/21/21  ISE             TSN593
      *           Replaced *loval with *start
      *
      /eject
      ****************************************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************************************
      *
     Ftfp027    if   e           k disk
      *    Freight variance--detail (records selected by open query)
      *
      *
     Ftfp001    if   e           k disk
      *    Preliminary/Final
      *
      *
     Ftfp317    uf a e           k disk
      *    Workfile: Freight Variance--Product Level
      *
      *
     Fprint132  o    f  132        printer oflind(*inof)
      *
      /eject
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      *
      ****************************************************************
      * ARRAYS AND TABLES
      ****************************************************************
      *
     D araam           s             11  2 dim(3)
     D arabsam         s             11  2 dim(3)
     D ardiffam        s             11  2 dim(3)
     D arpdiffam       s             11  2 dim(3)
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Standard fields
      *
     D dash117         s            117    inz(*all'-')
     d rtime           s              6  0


      * Control fields
      *
     D first           s              1    inz('Y')
     D svcono          s                   like(fdcono)
     D svload          s                   like(fdload)
      *
      *
      * Work fields for date manipulation
      *
     D wkisodate       s               D   datfmt(*iso)
      *
      *
      * Parms
      *
     D xxprcd          s                   like(w1prcd)
     D xxistycd        s                   like(w1istycd)
     D xxisgrcd        s                   like(w1isgrcd)
     D xxisclcd        s                   like(w1isclcd)
     D xxtfclcd        s                   like(w1tfclcd)
     D xxtfcgcd        s                   like(w1tfcgcd)
     D xxglsub         s              3
     D xxitycd         s              3
     D xxcoownfl       s              1
     D xxprdcmp        s              3
     D xxedfl          s              1
      *
      *
      * Array index
      *
     d x               s              1  0
      *
      *
      * Print fields
      *
     d h1pfds          s                   like(pfpfds)
      *
     d l1cacd          s                   like(fdcacd)
     d l1asdtmdy       s              6  0
      *
     d t1desc          s             14
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *---------------------------------------------------------------
      * Local data area
      *---------------------------------------------------------------
      *
     d lda            uds                  dtaara(*lda)
     D  ldpfcd                 1      1
     D  ldcyr                  2      5  0
     D  ldcpe                  6      7  0
      *
     D  ldcbdt                 8     15  0
     D  ldcbmdy               16     21  0
     D  ldcbsyn               22     28  0
      *
     D  ldcedt                29     36  0
     D  ldcemdy               37     42  0
     D  ldcesyn               43     49  0
      *
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      **********************************************************************************
      * Mainline
      **********************************************************************************
      *
      * Print headings.
     C                   exsr      $h1hdr
      *
      * By the time you get to this program, you are processing loads that:
      *          1) Have a Date in the Period being processed
      *          2) Have a Carrier Payment Status of P=Paid
      *          3) Are Prepaid (have a 'P' in the first position of Shipping Method)
      *
593  C*    *loval        setll     tfp027
593  C     *start        setll     tfp027
      *
     C                   dou       *in90 = *on                                  Main do
     C                   read      tfp027                                 90
     C                   if        *in90 = *off                                 If not EOF
      *
     C                   select
     C                   when      first = yes
     C                   move      no            first
     C                   z-add     fdcono        svcono
     C                   exsr      $load
      *
     C                   when      svcono <> fdcono
     C                   exsr      $loadtot
     C                   exsr      $comptot
      *
     C                   when      svload <> fdload
     C                   exsr      $loadtot
     C                   endsl
      *
      * Accumulate freight into arrays for printing load/company/report totals.
      * Note: As of March 2009, we are using "Actual" instead of "Load Total" values.
      *   1) Absorbed
      *   2) Allocated
      *   3) Difference
      *   4) Prior difference
      *
     C                   add       fdabsfam      arabsam
     C                   add       fdaafam       araam
     C                   add       fdadiffam     ardiffam
     C                   add       fdapdiffam    arpdiffam
      *
      * Write/Update the Freight Amounts by Product on the Workfile record
      *
     C                   exsr      $upd317
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Main do
      *
      *-------------------------
      * EOF processing
      *-------------------------
      *
     C                   select
     C                   when      first = yes
     C                   except    nodata
     C                   other
      *
      * Print totals for load/company/report
      *
     C                   exsr      $loadtot
     C                   exsr      $comptot
     C                   exsr      $rpttot
     C                   endsl
      *
     C                   seton                                        lr
      /eject
      *----------------------------------------------------------------------------------
      * Set up print fields for the load
      *----------------------------------------------------------------------------------
      *
     C     $load         begsr
      *
      * Get Actual Ship Date into MMDDYY format for printing
      *
     C     *iso          test(d)                 fdasdt                 92
     C                   if        *in92 = *off                                 If OK date
     C                   move      fdasdt        wkisodate
     C     *mdy          move      wkisodate     l1asdtmdy
     C                   endif                                                  If OK date
      *
      * Carrier code
     C                   move      fdcacd        l1cacd
      *
      * Save control field
     C                   z-add     fdload        svload
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Update/write a Workfile record at the Product Level
      *---------------------------------------------------------------
      *
     C     $upd317       begsr
      *
      * If you already have a record for this product in the workfile, update it.
      *
     C     fdprcd        chain     tfp317                             92
     C                   if        *in92 = *off                                 If hit
     C                   add       fdabsfam      w1pabsam
     C                   add       fdaafam       w1paam
     C                   add       fdadiffam     w1pdiffam
     C                   update    w1rec
     C                   else
      *
      * Set up amounts
     C                   z-add     fdabsfam      w1pabsam
     C                   z-add     fdaafam       w1paam
     C                   z-add     fdadiffam     w1pdiffam
     C                   z-add     0             w1uabsam
     C                   z-add     0             w1uaam
     C                   z-add     0             w1udiffam
      *
      * Retrieve the Item Structure for this Product.
      *
     C                   z-add     fdprcd        w1prcd
      *
     C                   call      'TF813'
     C                   parm      w1prcd        xxprcd
     C     w1istycd      parm      0             xxistycd
     C     w1isgrcd      parm      0             xxisgrcd
     C     w1isclcd      parm      0             xxisclcd
     C     w1tfclcd      parm      *blank        xxtfclcd
     C     w1tfcgcd      parm      *blank        xxtfcgcd
     C                   parm      *blank        xxglsub
     C                   parm      *blank        xxitycd
     C                   parm      *blank        xxcoownfl
     C                   parm      *blank        xxprdcmp
     C                   parm      *blank        xxedfl
      *
     C                   write     w1rec
     C                   endif                                                  If hit
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Print report headings for Load Section
      *---------------------------------------------------------------
      *
     C     $h1hdr        begsr
      *
     C                   except    h1hdr
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Print Load Total line
      *---------------------------------------------------------------
      *
     C     $loadtot      begsr
      *
     C                   if        *inof = *on
     C                   exsr      $h1hdr
     C                   endif
      *
     C                   z-add     1             x
      *
     C                   except    l1dtl
     C                   exsr      $clear
     C                   exsr      $load
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Print Total Line
      *---------------------------------------------------------------
      *
     C     $t1tot        begsr
      *
     C                   if        *inof = *on
     C                   exsr      $h1hdr
     C                   endif
      *
     C                   except    t1tot
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Clear array entries
      *---------------------------------------------------------------
      *
     C     $clear        begsr
      *
     C                   z-add     0             arabsam(x)
     C                   z-add     0             araam(x)
     C                   z-add     0             ardiffam(x)
     C                   z-add     0             arpdiffam(x)
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Print Company Total
      *---------------------------------------------------------------
      *
     C     $comptot      begsr
      *
     C                   z-add     2             x
     C                   eval      t1desc = 'Company Total:'
      *
     C                   exsr      $t1tot
      *
      * Clear Company totals from arrays
      *
     C                   exsr      $clear
      *
     C                   z-add     fdcono        svcono
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Print Report Total
      *---------------------------------------------------------------
      *
     C     $rpttot       begsr
      *
     C                   z-add     3             x
      *
     C                   except    blank
     C                   eval      t1desc = ' Report Total:'
     C                   seton                                        88
     C                   exsr      $t1tot
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      *
      * Set up heading info.
      *
     C                   time                    rtime
      *
      *
      * Retrieve 'preliminary/final' description
      *
     C     ldpfcd        chain     tfp001                             92
     C                   if        *in92 = *off
     C                   move      pfpfds        h1pfds
     C                   endif
      *
     C                   endsr
      /eject
      *-------------------------------------------------------------
      * Report heading lines for Section 1: Loads
      *-------------------------------------------------------------
      *
     Oprint132  e            h1hdr          1 01
     O                       sdpgm               10
     O                                           63 'VERIFICATION FOR FREIGHT'
     O                                           72 ' VARIANCE'
     O                                          107 'DATE'
     O                       udate         y    117
      *
     O          e            h1hdr          1
     O                       sdusr               10
     O                       h1pfds              61
     O                                          107 'TIME'
     O                       rtime              117 '  :  :  '
      *
      *
     O          e            h1hdr          2
     O                                           61 'Paid Loads'
     O                                          107 'PAGE'
     O                       page          z    117
      *
      *
     O          e            h1hdr          1
     O                                           18 'Year/Period .....:'
     O                       ldcyr         z     24
     O                       ldcpe         z     28
      *
     O          e            h1hdr          1
     O                                           18 'Period begin date:'
     O                       ldcbmdy             28 '  /  /  '
      *
     O          e            h1hdr          2
     O                                           18 'Period end date .:'
     O                       ldcemdy             28 '  /  /  '
      *
      *
      *-------------------------------------------------------------
      * Column headings for Section 1: Loads
      *-------------------------------------------------------------
      *
     O          e            h1hdr       0  1
     O                                            7 'Company'
     O                                           17 'Load'
     O                                           27 'Carrier'
     O                                           41 'Actual'
      *
     O                                           60 'Absorbed'
     O                                           79 'Actual'
     O                                           97 'Prior'
     O                                          116 'Difference'
      *
     O          e            h1hdr       0  1
     O                                            6 'Number'
     O                                           16 'ID'
     O                                           25 'Code'
     O                                           41 'Ship Date'
      *
     O                                           60 'Freight'
     O                                           79 'Freight'
     O                                           97 'Diff Amount'
     O                                          116 'Amount'
      *
     O          e            h1hdr          0
     O                       dash117            117
      *
      *
      *-------------------------------------------------------------
      * Detail line for Section 1: Load
      *-------------------------------------------------------------
      *
     O          e            l1dtl       1
     O                       svcono        z      5
     O                       svload        z     15
     O                       l1cacd              22
     O                       l1asdtmdy           41 '  /  /  '
     O                       arabsam(x)    k     61
     O                       araam(x)      k     80
     O                       arpdiffam(x)  k     98
     O                       ardiffam(x)   k    117
      *
      *-------------------------------------------------------------
      * Total line for Section 1: Loads
      *-------------------------------------------------------------
      *
     O          e   n88      t1tot       1  1
     O                                           60 '--------------'
     O                                           79 '--------------'
     O                                           97 '--------------'
     O                                          116 '--------------'
      *
     O          e            t1tot       0  1
     O                       t1desc              43
     O                       arabsam(x)    k     61
     O                       araam(x)      k     80
     O                       arpdiffam(x)  k     98
     O                       ardiffam(x)   k    117
      *
      *-------------------------------------------------------------
      * Blank line
      *-------------------------------------------------------------
      *
     O          e            blank       1
      *
      *-------------------------------------------------------------
      * No data message line
      *-------------------------------------------------------------
      *
     O          e            nodata      1
     O                                           19 'There are no paid '
     O                                           40 'loads for the period.'
      /eject
