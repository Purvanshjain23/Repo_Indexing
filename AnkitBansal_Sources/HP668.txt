      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      HOG PRODUCTION
      * PROGRAM:     HP668 - UNALLOCATED FEED TICKETS REPORT
      * PROGRAMMER:  LEANNE FEDOR
      * CREATED:     06/05/94
      *
      * FUNCTION: THIS PROGRAM PRINTS A REPORT OF EACH FEED
      *           TICKET THAT COULD NOT BE ALLOCATED TO GROUPS.
      *
      *           THIS PROGRAM IS SUBMITTED BY HP468CL WHICH IS CALLED
      *           FROM HP468 THROUGH QCMDEXC.
      *
      *           THE DATA FOR THE REPORT COMES FROM THE WORKFILE
      *           CREATED IN HP301-BUILD WORKFILES.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 01/30/01  LeAnne Fedor
      *           Recompile only.  Production type was removed from feed files.
      *
      * 07/16/02  LeAnne Fedor
      *           Recompile only.  Production Phase was removed from feed ticket header.
      *
      * 08/15/06  LeAnne Fedor
      *           Konner Everhard requested that we change this report to print as an
      *           Exception Only report because there was WAY too many pages to print
      *           and/or review. He only wants to print the tickets that cannot be
      *           allocated.
      *
      * 04/22/08  LeAnne Ramsey
      *           Changed to the IBM print file PRINT132 and
      *           cleaned up code.
      *
      * 07/29/09  LeAnne Ramsey
      *           Since this is not an exception report, the "no data meets your selection"
      *           message was confusing. So, I changed it to make more sense.
      *
      * 03/26/20  Danny Nguyen - P405 - Farm Number Increase
      *           Recompile only. Increased Bin Code (@@BNCD) in HSPREF file from 5A to 6A.
      *
      * 05/10/22 Eric L SDN736 Recompiled ticket nbr increase (TKNO & RTNO 7.0 TO 9.0)
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fhsl301a   if   e           k disk
      *  Workfile for feed ticket allocation (ticket)
      *
      *
     Fprint132  o    f  132        printer oflind(*inof)
      *
      /EJECT
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * Constants
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      ****************************************************************
      * Standalone fields
      ****************************************************************
      *
      * Standard fields
      *
     d dash            s            132    inz(*all'-')
     d rtime           s              6  0
      *
      *
      * Control fields
      *
     D first           s              1    inz('Y')
     D svtkno          s                   like(wttkno)
      *
      *
      * Workfields for date manipulation
      *
     D wkisodate       s               D   datfmt(*iso)
     D wkmdy           s               D   datfmt(*mdy)
      *
      *
      * Print fields
      *
     d h1desc          s              5
     d hrepmdy         s              6  0
      *
     d l1tkmdy         s              6  0
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *----------------------------------------------------------------
      * LDA
      *----------------------------------------------------------------
     D                uds
     D  ldcymd                 1     08  0
     D  ldeffl                 9     09
     D  ldfmbo                10     14
     D  ldbods                23     52
     D  ldcmdy                53     58  0
      *
      *---------------------------------------------------------------
      * Definition for external data area 'DAAPER'
      *---------------------------------------------------------------
      *
     D dsdaaper       uds                  dtaara(daaper)
     D  daaper                 1     24
     D  daccyy                 1      4
     D  daper                  5      6
     D  dabpdt                 7     14  0
     D  daepdt                15     22  0
     D  dapgfl                23     23
     D  dappfl                24     24
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      *
      /EJECT
      **************************************************************************************
      * Mainline
      **************************************************************************************
      *
      * Read each feed ticket record in the workfile. If it is "not allocated"
      * then print it.
      *
     C                   dou       *in90 = *on                                  Main do
     C                   read      hsl301a                                90
     C                   if        *in90 = *off and wtalfl = 'T'                If not alloc
      *
      * First time
     C                   if        first = yes
     C                   except    h1hdr
     C                   move      no            first
     C                   endif
      *
      * Flip Ticket Date from CCYYMMDD to MMDDYY
      *
     C     *iso          test(d)                 wttkdt                 92
     C                   if        *in92 = *off                                 If OK date
     C                   move      wttkdt        wkisodate
     C     *mdy          move      wkisodate     l1tkmdy
     C                   endif                                                  If OK date
      *
      * Print Feed Ticket Info
      *
     C                   if        *inof = *on
     C                   except    h1hdr
     C                   endif
      *
      * Print a blank line between "tickets"
      *
     C                   if        svtkno <> wttkno
     C                   except    blank
     C                   z-add     wttkno        svtkno
     C                   endif
      *
     C                   except    l1dtl
      *
     C                   endif                                                  If not alloc
     C                   enddo                                                  Main do
      *
      * EOF Processing
     C                   if        first = yes
     C                   except    h1hdr
     C                   except    nodata
     C                   endif
      *
     C                   seton                                        lr
      /EJECT
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Setup Report headings
      *
     C                   time                    rtime
      *
     C                   if        ldeffl = 'E'
     C                   movel(p)  'EDIT'        h1desc
     C                   else
     C                   movel(p)  'FINAL'       h1desc
     C                   endif
      *
      * Convert the EOP date from CCYYMMDD TO MMDDYY for the heading
      *
     C                   eval      wkmdy = %date(daepdt:*iso)
     C                   move      wkmdy         hrepmdy
      *
     C                   endsr
      /EJECT
      *-------------------------------------------------------------
      * Report headings lines
      *-------------------------------------------------------------
      *
     Oprint132  E            h1hdr          1 01
     O                                            5 'HP668'
     O                                           75 'HOG PRODUCTION SYSTEM'
     O                                          122 'DATE'
     O                       udate         Y    132
      *
     O          e            h1hdr          1
     O                       sdusr               10
     O                                           61 'UNALLOCATED'
     O                                           80 'FEED TICKET REPORT'
     O                                          122 'TIME'
     O                       rtime              132 '  :  :  '
      *
     O          e            h1hdr          1
     O                       h1desc              68
     O                                          100 'EOP DATE:'
     O                       hrepmdy            110 '  /  /  '
     O                                          122 'PAGE'
     O                       page          Z    132
      *
     O          e            h1hdr          1
     O                                           16 'CUTOFF DATE:'
     O                       ldcmdy        Y     26
      *
     O          e            h1hdr          1
     O                                           16 'BUSINESS OFFICE:'
     O                       ldfmbo              26
     O                       ldbods              58
      *
      *-------------------------------------------------------------
      * Column headings
      *-------------------------------------------------------------
      *
     O          e            h1hdr       1  1
     O                                           14 'TRAN'
     O                                           94 'INGREDIENT'
     O                                          114 'MANUFACTURING'
     O                                          131 'MEDICATION'
      *
     O          e            h1hdr          1
     O                                            9 'TICKET'
     O                                           14 'CODE'
     O                                           20 'DATE'
     O                                           28 'FARM'
     O                                           35 'BIN'
     O                                           45 'RATION'
     O                                           55 'ADDITIVE'
     O                                           77 'POUNDS'
     O                                           94 'AMOUNT'
     O                                          114 'AMOUNT'
     O                                          131 'AMOUNT'
      *
     O          e            h1hdr          1
     O                       dash               132
      *
      *-------------------------------------------------------------
      * Feed Ticket Detail Line
      *-------------------------------------------------------------
      *
     O          E            l1dtl       0  1
     O                       wttkno        m     10
     O                       wttrcd              13
     O                       l1tkmdy             22 '  /  /  '
     O                       wtfscd        m     29
     O                       wtbncd              37
     O                       wtrncd              44
     O                       wtadcd              52
      *
     O                       wtfdlb        k     78
     O                       wtinam        k     95
     O                       wtmfam        k    115
     O                       wtmdam        k    132
      *
      *-------------------------------------------------------------
      * Blank line
      *-------------------------------------------------------------
      *
     O          e            blank       1
      *
      *-------------------------------------------------------------
      * No data message line
      *-------------------------------------------------------------
      *
     O          e            nodata      2
     O                                           21 'All tickets for your '
     O                                           47 'selected Date/Office have '
     O                                           62 'been allocated.'
