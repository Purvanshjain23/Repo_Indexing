/***************************************************************** */
/*                                                                 */
/*  PROGRAM NUMBER --> HP687CL                                     */
/*  PROGRAM NAME ----> LISTING OF PIG DAYS BY FARM SITE            */
/*                                                                 */
/*  PROGRAM PURPOSE -> THIS CL CALLS THE PROGRAM TO PRINT A PIG    */
/*                     DAYS BY FARM SITE REPORT.  VALUES ARE PASSED*/
/*                     THRU THE LDA FROM SPECIFY HP487.            */
/*                                                                 */
/*  PROGRAMMER ------> MARY PALMER               09/26/94          */
/*                                                                 */
/*******************************************************************/
/* MODIFIED:                                                       */
/* 12/12/01  LEANNE FEDOR                                          */
/*           CHANGED LOGIC TO BUILD A WORKFILE SO WE COULD USE IT  */
/*           OTHER PLACES.                                         */
/*                                                                 */
/* 04/22/10  LEANNE RAMSEY                                         */
/*           REPLACED QPRINT WITH PRINT80; SO, REMOVED THE SEABOARD*/
/*           CUSTOMIZED PRINT OVERRIDE COMMAND.                    */
/*******************************************************************/

             PGM

             DCL        VAR(&QDATA) TYPE(*CHAR) LEN(155)
             DCL        VAR(&LDFSBO) TYPE(*CHAR) LEN(5)
             DCL        VAR(&LDFYMD) TYPE(*DEC) LEN(8)
             DCL        VAR(&LDTYMD) TYPE(*DEC) LEN(8)
             DCL        VAR(&LDFSCD) TYPE(*DEC) LEN(5)
             DCL        VAR(&LDPPCD) TYPE(*CHAR) LEN(5)

             DCL        VAR(&HOLD) TYPE(*CHAR) LEN(10)
             DCL        VAR(&COPYS) TYPE(*DEC) LEN(2)
             DCL        VAR(&OUTQ) TYPE(*CHAR) LEN(10)

             CHGVAR     VAR(&LDFSBO) VALUE(%SST(*LDA 1 5))
             CHGVAR     VAR(&LDFYMD) VALUE(%SST(*LDA 6 8))
             CHGVAR     VAR(&LDTYMD) VALUE(%SST(*LDA 14 8))
             CHGVAR     VAR(&LDFSCD) VALUE(%SST(*LDA 22 5))
             CHGVAR     VAR(&LDPPCD) VALUE(%SST(*LDA 94 5))

/*------------------------------------------------------------------*/
/* CREATE A WORKFILE                                                 */
/*------------------------------------------------------------------*/

             CRTDUPOBJL OBJ(HSP309) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)

/*------------------------------------------------------------------*/
/*  CREATE SELECT STATEMENT OF GROUPS                              */
/*------------------------------------------------------------------*/

/*  ALWAYS OMIT HOG GROUPS WITH A STATUS OF VOID                 */

             CHGVAR     VAR(%SST(&QDATA 1 15)) VALUE('HGGSCD +
                          *NE "VD"')

/*  ALWAYS OMIT GROUPS THAT OPENED AFTER THE USER'S 'TO' DATE       */

             CHGVAR     VAR(%SST(&QDATA 17 15)) VALUE('*AND HGOPDT +
                          *LE')
             CHGVAR     VAR(%SST(&QDATA 33 8)) VALUE(&LDTYMD)

/*  ALWAYS OMIT DISPOSED GROUPS WITH A DISPOSE DATE BEFORE OR ON     */
/*  THE USER'S 'FROM' DATE                                           */

             CHGVAR     VAR(%SST(&QDATA 42 18)) VALUE('*AND (HGDSDT +
                          *EQ 0')
             CHGVAR     VAR(%SST(&QDATA 61 14)) VALUE('*OR HGDSDT +
                          *GT')
             CHGVAR     VAR(%SST(&QDATA 76 8)) VALUE(&LDFYMD)
             CHGVAR     VAR(%SST(&QDATA 84 1)) VALUE(')')

/*  IF BUSINESS OFFICE                                           */

             IF         COND(&LDFSBO *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 86 17)) VALUE('*AND HGFSBO +
                          *EQ "')
             CHGVAR     VAR(%SST(&QDATA 103 5)) VALUE(&LDFSBO)
             CHGVAR     VAR(%SST(&QDATA 108 1)) VALUE('"')
             ENDDO

/*  IF FARM SITE                                                 */

             IF         COND(&LDFSCD *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 110 15)) VALUE('*AND HGFSCD +
                          *EQ')
             CHGVAR     VAR(%SST(&QDATA 126 5)) VALUE(&LDFSCD)
             ENDDO

/*  IF PRODUCTION PHASE                                            */

             IF         COND(&LDPPCD *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 132 17)) VALUE('*AND HGPPCD +
                          *EQ "')
             CHGVAR     VAR(%SST(&QDATA 149 5)) VALUE(&LDPPCD)
             CHGVAR     VAR(%SST(&QDATA 154 1)) VALUE('"')
             ENDDO

/*------------------------------------------------------------------*/
/*  RUN QUERY AND CALL PROGRAM TO BUILD WORKFILE                    */
/*------------------------------------------------------------------*/

             OVRDBF     FILE(HSL034E) SHARE(*YES)
             OPNQRYF    FILE((HSL034E)) QRYSLT(&QDATA) KEYFLD(*FILE)

             CALL       PGM(HP309) PARM(&LDFYMD &LDTYMD)

             CLOF       OPNID(HSL034E)
             DLTOVR     FILE(HSL034E)


/*------------------------------------------------------------------*/
/* SETUP PRINT PARAMETERS                                           */
/*------------------------------------------------------------------*/

 HOLD:       IF         COND(%SST(*LDA 411 1) = 'Y') THEN(CHGVAR +
                          VAR(&HOLD) VALUE('*YES      '))
             ELSE       CMD(CHGVAR VAR(&HOLD) VALUE('*NO       '))

COPYS:       CHGVAR     VAR(&COPYS) VALUE(%SST(*LDA 412 1))
             IF         COND(&COPYS = 0) THEN(CHGVAR VAR(&COPYS) +
                          VALUE(1))

OUTQ:        CHGVAR     VAR(&OUTQ) VALUE(%SST(*LDA 413 10))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(%SST(*LDA 401 10)))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE('*JOB      '))

/*------------------------------------------------------------------*/
/* OVERRIDE PRINT FILE AND PRINT REPORT                             */
/*------------------------------------------------------------------*/

             OVRPRTF    FILE(PRINT80) OUTQ(&OUTQ) COPIES(&COPYS) +
                          HOLD(&HOLD)

             CALL       PGM(HP687)

             DLTOVR     FILE(*ALL)

             ENDPGM

