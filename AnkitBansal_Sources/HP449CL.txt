/***************************************************************** */
/*                                                                 */
/*  PROGRAM NUMBER:  HP449CL                                       */
/*  PROGRAM NAME:    SPECIFY POST OF EXPENSE EDIT FILE TO HPS      */
/*  PROGRAMMER:      LEANNE FEDOR                                  */
/*  CREATE DATE:     01/05/98                                      */
/*                                                                 */
/*  FUNCTION:        THIS CL IS SUBMITTED WITH QCMDEXC FROM        */
/*                   HP449.                                        */
/*                   USER OPTIONS ARE PASSED THROUGH THE LDA.      */
/*                                                                 */
/*******************************************************************/
/* MODIFIED:                                                       */
/* 07/11/01  RANDY ROLF KEPT LOCKING THE EDITING FILE BY KEEPING   */
/*           THE WORK WITH SCREEN UP. THIS CAUSED THE CLEAR OF THE */
/*           FILE TO FAIL. SO, I HAVE ADDED A HARD ALLOCATION OF   */
/*           BOTH FILES THAT WILL BE CLEARED.                      */
/*******************************************************************/

             PGM

/* DEFINE THE VARIABLE REQUIRED IN THE COMMAND TO OVERRIDE THE     */
/* PRINT FILE TO A LASER PRINTER                                   */

             DCL        VAR(&CMD) TYPE(*CHAR) LEN(512)
             DCL        VAR(&USER) TYPE(*CHAR) LEN(10)
             DCL        VAR(&HOLD) TYPE(*CHAR) LEN(10)
             DCL        VAR(&COPYS) TYPE(*DEC) LEN(2)
             DCL        VAR(&OUTQ) TYPE(*CHAR) LEN(10)

             DCL        VAR(&ALLOCERR) TYPE(*CHAR) LEN(1)
             DCL        VAR(&ERFL) TYPE(*CHAR) LEN(1)
             DCL        VAR(&ISCD) TYPE(*CHAR) LEN(1)
             DCL        VAR(&COCD) TYPE(*CHAR) LEN(3)
             DCL        VAR(&CCYY) TYPE(*DEC) LEN(4)
             DCL        VAR(&PER)  TYPE(*DEC) LEN(2)


/* THE USERS CANNOT HAVE THE INTERFACE RECEIVING OR EDITING FILES   */
/* OPEN WHILE THIS CLP IS RUNNING. SO, ALLOCATE THE FILES.          */

             ALCOBJ     OBJ((*LIBL/HSP230 *FILE *EXCL))
             MONMSG     MSGID(CPF0000) EXEC(DO)
             RTVJOBA    USER(&USER)
             SNDMSG     MSG('***** Your post of the Expense +
                          Interface data into the HPS files could +
                          not run because the Interface Receiving +
                          File was in use.  Everyone must be out of +
                          the Expense Interface Functions for this +
                          posting job to run successfully.  So, +
                          make sure no one is using the other +
                          Expense Interface functions and then +
                          resubmit your post. ****') TOUSR(&USER)
             CHGVAR     VAR(&ALLOCERR) VALUE('Y')
             GOTO       EXIT
                        ENDDO


             ALCOBJ     OBJ((*LIBL/HSP231 *FILE *EXCL))
             MONMSG     MSGID(CPF0000) EXEC(DO)
             RTVJOBA    USER(&USER)
             SNDMSG     MSG('***** Your post of the Expense +
                          Interface data into the HPS files could +
                          not run because the Edit File for the GL +
                          Expense Interface was in use.  Everyone +
                          must be out of the Expense Interface +
                          Functions for this posting job to run +
                          successfully.  So, make sure no one is +
                          using the other Expense Interface +
                          functions and then resubmit your post. +
                          ****') TOUSR(&USER)
             CHGVAR     VAR(&ALLOCERR) VALUE('Y')
             GOTO       EXIT
                        ENDDO


/* SET PRINT PARAMETERS FROM LDA                         */

 HOLD:       IF         COND(%SST(*LDA 411 1) = 'Y') THEN(CHGVAR +
                          VAR(&HOLD) VALUE(*YES))
             ELSE       CMD(CHGVAR VAR(&HOLD) VALUE(*NO))

 COPYS:      CHGVAR     VAR(&COPYS) VALUE(%SST(*LDA 412 1))
             IF         COND(&COPYS = 0) THEN(CHGVAR VAR(&COPYS) +
                          VALUE(1))

 OUTQ:       CHGVAR     VAR(&OUTQ) VALUE(%SST(*LDA 413 10))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(%SST(*LDA 401 10)))
             IF COND(&OUTQ = '          ') THEN(CHGVAR VAR(&OUTQ) +
                          VALUE(*JOB))

/*------------------------------------------------------------------*/
/* OVERRIDE PRINT FILE                                              */
/*------------------------------------------------------------------*/

/* USE THE CUSTOMIZED SEABOARD COMMAND TO OVERRIDE THE PRINT FILE.  */
/* THIS COMMAND ALLOWS THE REPORT TO BE GENERATED ON A STANDARD LINE*/
/* PRINTER OR A LASER PRINTER. TO USE THIS OVERRIDE YOU MUST DECLARE*/
/* A COMMAND VARIABLE AND FOLLOW THE OVERRIDE WITH A CALL TO        */
/* QCMDEXC.                                                         */

/* THIS REPORT IS PRINTS AT STANDARD 10 CPI FOR 132 CHARACTERS.     */
/* SHARE THE OPEN DATA PATH SO ERROR MESSAGES WILL PRINT CORRECTLY. */
/* THE PAGE BREAKS MAY BE WEIRD ON SOME LASER PRINTERS SINCE WE     */
/* COUNT LINES, PAGE BREAK ON LINE 62, AND EXPECT PAPER THAT IS     */
/* 68 LINES LONG.                                                   */

             SBDPRTOVR  CMDVAR(&CMD) FILE(QPRINT) OUTQ(&OUTQ) +
                          WIDTH(132) COPIES(&COPYS) HOLD(&HOLD) +
                          SHARE(*YES)

             CALL       PGM(QCMDEXC) PARM(&CMD 512)

/*------------------------------------------------------------------*/
/*  CALL BATCH EDIT LISTING                                         */
/*------------------------------------------------------------------*/

             CALL       PGM(HP232)
             DLTOVR QPRINT


/*------------------------------------------------------------------*/
/* RETRIEVE THE 'ERROR FLAG' VALUE FROM THE LDA                     */
/*------------------------------------------------------------------*/

             CHGVAR     VAR(&ERFL) VALUE(%SST(*LDA 80 1))

/*------------------------------------------------------------------*/
/*  IF THERE WERE ERRORS FROM THE BATCH EDIT,                       */
/*        SET THE INTERFACE STATUS BACK TO 'EDITING'                */
/*-------------------------------------------------------------------*/

             IF         COND(&ERFL *EQ 'Y') THEN(DO)
             CHGVAR     VAR(&COCD) VALUE(%SST(*LDA 23 3))
             CHGVAR     VAR(&CCYY) VALUE(%SST(*LDA 19 4))
             CHGVAR     VAR(&PER)  VALUE(%SST(*LDA 17 2))
             CHGVAR     VAR(&ISCD) VALUE('E')
             CALL       PGM(HP229) PARM(&COCD &CCYY &PER &ISCD)
             ENDDO

/*------------------------------------------------------------------*/
/*  IF THERE WERE NO ERRORS FROM THE BATCH EDIT LISTING,            */
/*     1) CALL THE POST PROGRAM TO WRITE THE RECORDS FROM           */
/*        THE EDITING FILES TO THE HPS EXPENSE TICKET FILES.        */
/*     2) CLEAR THE INTERFACE RECEIVING AND EDITING FILES           */
/*     3) SET THE INTERFACE STATUS TO COMPLETED                     */
/*-------------------------------------------------------------------*/

             IF         COND(&ERFL *EQ 'N') THEN(DO)

/*  WRITE HPS EXPENSE TICKETS                                       */

             CALL       PGM(HP233)
             MONMSG     MSGID(CPF0000)

/* CALL PROGRAM TO UPDATE EXPENSE INTERFACE STATUS TO 'COMPLETED'   */

             CHGVAR     VAR(&COCD) VALUE(%SST(*LDA 23 3))
             CHGVAR     VAR(&CCYY) VALUE(%SST(*LDA 19 4))
             CHGVAR     VAR(&PER)  VALUE(%SST(*LDA 17 2))
             CHGVAR     VAR(&ISCD) VALUE('C')
             CALL       PGM(HP229) PARM(&COCD &CCYY &PER &ISCD)

/*  CLEAR INTERFACE RECEIVING AND EDITING FILES                     */

             CLRPFM     FILE(HSP230)
             MONMSG     MSGID(CPF0000)

             CLRPFM     FILE(HSP231)
             MONMSG     MSGID(CPF0000)

             ENDDO

 EXIT:

/*------------------------------------------------------------------*/
/* IF THERE WAS AN ERROR IN ALLOCATING THE FILES, THEN RESET        */
/* THE INTERFACE CONTROL STATUS BACK TO E=EDIT INSTEAD IF P=POST    */
/*------------------------------------------------------------------*/

             IF         COND(&ALLOCERR *EQ 'Y') THEN(DO)
             CHGVAR     VAR(&COCD) VALUE(%SST(*LDA 23 3))
             CHGVAR     VAR(&CCYY) VALUE(%SST(*LDA 19 4))
             CHGVAR     VAR(&PER)  VALUE(%SST(*LDA 17 2))
             CHGVAR     VAR(&ISCD) VALUE('E')
             CALL       PGM(HP229) PARM(&COCD &CCYY &PER &ISCD)
             ENDDO

/*------------------------------------------------------------------*/
/*  DEALLOCATE THE INTERFACE FILES                                  */
/*------------------------------------------------------------------*/

             DLCOBJ     OBJ((*LIBL/HSP230 *FILE *EXCL))
             MONMSG     MSGID(CPF0000)

             DLCOBJ     OBJ((*LIBL/HSP231 *FILE *EXCL))
             MONMSG     MSGID(CPF0000)

             ENDPGM

