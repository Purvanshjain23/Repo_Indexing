      *****************  RPG PROGRAM HEADING  ************************
      *
      * ENVIRONMENT: Pork Division
      * SYSTEM:      Datamart--Sales
      * PROGRAM:     SH214 - Datamart Sales: Claims
      * PROGRAMMER:  LeAnne Ramsey
      * CREATED:     01/15/13
      *
      * FUNCTION: This program creates Claim data that Bret Getzel requested
      *           for use in his new "claim" KPI...which he is going to use to
      *           "measure" his salesmen.
      *
      *           For each load of the Datamart, we will read each record in the
      *           Claim header. If it already exists in the Datamart file we will
      *           delete/rewrite its records...so that we get the latest "status"
      *           and info on the claim. We will never have more than 2 years of
      *           data to process...since the OMS purges only keep 2 years of
      *           this claim data. So, the processing time should not be too long.
      *
      *           For our Datamart Claim Header file, we will do a 1-to-1 read/
      *           write from the AR Claim file (PDMKCPP).
      *           In our Datamart Claim Detail file, we will do a 1-to-1 read/write
      *           of each record in the Claim Product file (PDMOCPP).
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * Date      Programmer
      *
      * 01/24/13  LeAnne Ramsey  (E2401)
      *           Added 2 fields: Claim Amount and Claim Net Loss
      *           These come from the Claim Header; I will only print them on
      *           the first record that I write for each Claim.
      *           (Doug had me add this part trying to get stuff for Jill Gates.)
      *
      * 01/28/13  LeAnne Ramsey  (E2401)
      *           Complete rewrite; we are now writing Datamart Header/Detail files.
      *
      * 02/14/13  LeAnne Ramsey  (E2450)
      *           Recompile only.
      *           Adam Gross had Rose C. add 'sales channel' to the order entry screen. Now,
      *           the users will have to enter a valid Sales Channel (aka: Customer Type) on
      *           each Order!!  They were not happy with having it at the Customer level!!
      *           Rose will hold/store this new value in PMD0CPP-Sales History Extension.
      *           I have added 3 fields to our Datamart Sales History file:
      *             SHORCHCD-Order Sales Channel Code
      *             SHORCHDS-Order Sales Channel Description
      *             SHORCHCDDS-Order Sales Channel Code/Description
      *
      * 02/25/13  LeAnne Ramsey  (E2401)
      *           Add new field 'WeekEnding Date' to SHP214-Claim Header; we will
      *           pull this from SHP204-Datamart Sales History. It is the WeekEnding
      *           Date associated with the Actual Ship Date on the Order.
      *
      * 02/26/13  LeAnne Ramsey  (E2401)
      *           Doug E. reported that the Actual Ship Date was not populating
      *           correctly when a Claim created a Credit Memo against a Debit Memo.
      *           So, I removed the selection on SHL204C that limited it only to records
      *           where Order Type was 'OR'. In Doug's example we needed to retrieve
      *           a 'DB' record.
      *
      * 03/06/13  LeAnne Ramsey  (E2401)
      *           When Cognos rolls up the detail records it is doubling up on the
      *           Claim Amount from the Claim Header. So, we need to add the Claim
      *           Amount to our Claim Detail file.
      *
      * 07/16/13  LeAnne Ramsey  (E2682)
      *           Recompile only.
      *           Adam Gross had us add Gate/Final Price and Gate/Final Price Adjustment to
      *           Sales History.  These will come from the Sales History Extension file (PMD0CPP).
      *
      * 07/08/14  LeAnne Ramsey  (E3174)
      *           Recompile only.
      *           Added a new field to SHP204-Sales History:
      *               Spot/Buy Flag    (SHSPBYFL)
      *
      * 08/14/14  LeAnne Ramsey  (E3320)
      *           Recompile only.
      *           Added 3 new fields to SHP204-Sales History:
      *               Last Quantity Change Date  (SHLQCDT)
      *               Last Quantity Change Date  (SH80LQCDT)
      *               Last Quantity Change Time  (SHLQCTM)
      *
      * 09/23/14  Rose Centonze  (E3329)
      *           Recompile only.
      *           Added 7 new fields to SHP204-Sales History:
      *               DP Location      (SHDPLOC)
      *               DP Region        (SHDPREGION)
      *               DP Channel Type  (SHDPCHTYP)
      *               Req Shp Date     (SHRQSHDT)
      *               Req Shp w/end    (SHRQWEDT)
      *               Req Dlv Date     (SHRQDLDT)
      *               Req Dlv w/end    (SHDLWEDT)
      *
      * 10/07/14  LeAnne Ramsey  (E3499)
      *           Recompile only.
      *           Added 1 new field to SHP204-Sales History:
      *               Stop             (SHSTOP)
      *
      * 11/24/14  LeAnne Ramsey  (E3640)
      *           Recompile only.
      *           Added 3 new fields to SHP204-Sales History:
      *               DP Salesperson Code        (SHDPSPCD)
      *               DP Salesperson Name        (SHDPSPNM)
      *               DP Salesperson Code/Name   (SHDPSPCDNM)
      *
      * 10/28/16  Danny Nguyen   (E7720)
      *           Recompile only.
      *           Added 2 new fields to SHP204-Sales History:
      *               Requested Delivery Time      (SHRQDLTM)
      *               Requested Delivery Timestamp (SHRQDLSTMP)
      *
      * 04/13/17  Danny Nguyen   (R9970)
      *           Added 1 non-key field to SHP215-Claim Detail:
      *               Company Number   (CDCONO)
      *           Populate Company Number field with Claim Company Number from
      *           A/R Claim file.
      *
      * 05/18/17  Danny Nguyen   (T10560)
      *           Added 1 non-key field to SHP214-Claim Header:
      *               FPR ID           (CHFPRID)
      *           Populate FPR ID field with FPR ID from A/R Claim file.
      *
      * 05/23/18  Danny Nguyen  (R12926 - Absorbed Freight Rate Override)
      *           Recompile only.
      *           Added 3 new fields to SHP204-Sales History:
      *               ABSORBED FREIGHT RATE        (SHABFRRT)
      *               ORIG ORD ABS FREIGHT RATE    (SHORABFRRT)
      *               INVOICED MODE OF TRANSPTN    (SHINMOTCD)
      *
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fcavllsl0  if   e           k disk
      *  Synon values list
      *
      *
     Fpdmkcpl1  if   e           k disk
      *  A/R Claim
      *
      *
     Fpdmocpl1  if   e           k disk
      *  A/R Claim product
      *
      *
     Fopcxrel1  if   e           k disk
      *  Reason codes
      *
      *
     Fppatrel1  if   e           k disk
      *  Claim department
      *
      *
     Fshl204c   if   e           k disk
      *    Datamart: Sales history
      *
      *
     Fshp214    uf a e           k disk
      *  Datamart: Claim Header
      *
      *
     Fshp215    uf a e           k disk
      *  Datamart: Claim Detail
      *
      /EJECT
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      *---------------------------------------------------------------
      *  Named Constants
      *---------------------------------------------------------------
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      *---------------------------------------------------------------
      * Standalone fields
      *---------------------------------------------------------------
      *
      * Workfields for date manipulation
      *
     D wknulldt        s                   like(shwedt)   inz(D'0001-01-01')
      *
      * The fields below are used to retrieve the Claim Status Desc from
      * the Synon Values file..Purva gave me the 1523237 value to hardcode.
      *
     D wklistno        s                   like(y2lsno) inz(1523237)
     D wkextval        s                   like(y2exvl)
      *
      * Parms
     D xxcorg          s                   like(cdcorg)
      *
      ****************************************************************
      * Data Structures
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * Mainline
      ****************************************************************
      *
      * Process each record in the A/R Claim file.
      *
     C     *loval        setll     pdmkcpl1
     C                   dou       *in90 = *on                                  Main do loop
     C                   read      pdmkcpl1                               90
     C                   if        *in90 = *off                                 If not EOF
      *
      * Delete any/all records for this claim that you may already
      * have in the Datamart files.
      *
     C                   exsr      $delete
      *
      * Write Datamart header record and then detail records.
     C                   exsr      $wrt214
     C                   exsr      $wrt215
      *
     C                   endif                                                  If not eof
     C                   enddo                                                  Main do loop
      *
     C                   seton                                        lr
      /EJECT
      *--------------------------------------------------------------------
      * Delete all Datamart records for a Claim Number
      *--------------------------------------------------------------------
      *
     C     $delete       begsr
      *
      * Delete the Datamart Claim Header record.
      *
     C     key01         chain     shp214                             92
     C                   if        *in92 = *off
     C                   delete    chrec
     C                   endif
      *
      * Delete all Datamart Claim Detail records for this claim.
      *
     C     key01         setll     shp215
     C                   dou       *in91 = *on
     C     key01         reade     shp215                                 91
     C                   if        *in91 = *off
     C                   delete    cdrec
     C                   endif
     C                   enddo
      *
     C                   endsr
      /EJECT
      *--------------------------------------------------------------------
      * Write a Datamart Header record for this claim.
      *--------------------------------------------------------------------
      *
     C     $wrt214       begsr
      *
     C                   move      xxcorg        chcorg
     C                   z-add     mkc0c7        chclno
     C                   move      mkchsx        chcltype
     C                   z-add     mkayvl        chclam
     C                   z-add     mkazvl        chclnlam
     C                   z-add     mkbic3        chclcono
     C                   z-add     mkalnx        chclorno
     C                   z-add     mkc3c7        chclshcuno
     C                   move      mkrftx        chclshcunm
     C                   z-add     mkrmnb        chloadno
10560C                   z-add     mkeqvl        chfprid
      *
      * Format dates
     C                   exsr      $dates
      *
      * Populate Claim Status and get Description from Synon values file
      *
     C                   move      mkcjsx        chclst
     C                   eval      wkextval = mkcjsx
     C     key02         chain     @y2exin                            92
     C                   if        *in92 = *off
     C                   eval      chclstds = y2cntx
     C                   endif
      *
      * Retrieve Order info from the Datamart Sales History file.
      *
     C     key03         chain     shl204c                            92
     C                   if        *in92 = *off
     C                   move      shwhcd        chwhcd
     C                   z-add     shinno        chinno
     C                   move      shorspcd      chorspcd
     C                   move      shorspnm      chorspnm
     C                   move      shordt        chordt
     C                   z-add     sh80ordt      ch80ordt
     C                   move      shacshdt      chacshdt
     C                   z-add     sh80acshdt    ch80acshdt
     C                   move      shwedt        chwedt
     C                   z-add     sh80wedt      ch80wedt
     C                   else
     C                   move      wknulldt      chordt
     C                   move      wknulldt      chacshdt
     C                   move      wknulldt      chwedt
     C                   endif
      *
     C                   write     chrec
     C                   clear                   chrec
      *
     C                   endsr
      /EJECT
      *--------------------------------------------------------------------
      * Write Datamart Claim detail records for this claim.
      *--------------------------------------------------------------------
      *
     C     $wrt215       begsr
      *
      * Now, read all the Claim Product records (aka: claim detail) for this
      * claim and do a 1-to-1 write to the Datamart file.
      *
     C     mkc0c7        setll     pdmocpl1
     C                   dou       *in91 = *on                                  Do detail loop
     C     mkc0c7        reade     pdmocpl1                               91
     C                   if        *in91 = *off                                 If not EOF
      *
     C                   move      xxcorg        cdcorg
     C                   z-add     mkc0c7        cdclno
     C                   z-add     moc1c7        cdclitcd
     C                   z-add     mobnvl        cdclam
     C                   move      mocqcd        cdclrscd
     C                   move      mofuaa        cdcldpcd
R9970C                   z-add     mkbic3        cdcono
      *
      * Retrieve Reason Code description
      *
     C     cdclrscd      chain     opcxrel1                           92
     C                   if        *in92 = *off
     C                   move      cxauna        cdclrsds
     C                   endif
      *
      * Retrieve Claim Department description
      *
     C     cdcldpcd      chain     ppatrel1                           92
     C                   if        *in92 = *off
     C                   move      attxtx        cdcldpds
     C                   endif
      *
     C                   write     cdrec
     C                   clear                   cdrec
     C                   endif                                                  If not EOF
     C                   enddo                                                  Do detail loop
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Format dates from the Claim Header
      *---------------------------------------------------------------
      *
     C     $dates        begsr
      *
      * Claim Called in by Date (in 2 formats)
      *
     C     *cymd         test(d)                 mkmmdt                 92
     C                   if        *in92 = *off
     C     *cymd         move      mkmmdt        chclcidt
     C     *iso          move      chclcidt      ch80clcidt
     C                   else
     C                   move      wknulldt      chclcidt
     C                   endif
      *
      * Claim Entry Date (in 2 formats)
      *
     C     *cymd         test(d)                 mkmndt                 92
     C                   if        *in92 = *off
     C     *cymd         move      mkmndt        chclendt
     C     *iso          move      chclendt      ch80clendt
     C                   else
     C                   move      wknulldt      chclendt
     C                   endif
      *
      * Claim Approved/Denied Date (in 2 formats)
      *
     C     *cymd         test(d)                 mkmodt                 92
     C                   if        *in92 = *off
     C     *cymd         move      mkmodt        chcladdt
     C     *iso          move      chcladdt      ch80claddt
     C                   else
     C                   move      wknulldt      chcladdt
     C                   endif
      *
      * Actual Delivery Date
      *
     C     *cymd         test(d)                 mkejdt                 92
     C                   if        *in92 = *off
     C     *cymd         move      mkejdt        chacdldt
     C     *iso          move      chacdldt      ch80acdldt
     C                   else
     C                   move      wknulldt      chacdldt
     C                   endif
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Parms
     C     *entry        plist
     C                   parm                    xxcorg
      *
      *
      * Key lists
      *
     C     key01         klist
     C                   kfld                    xxcorg
     C                   kfld                    mkc0c7                         Claim number
      *
     C     key02         klist
     C                   kfld                    wklistno
     C                   kfld                    wkextval
      *
     C     key03         klist
     C                   kfld                    chclcono
     C                   kfld                    chclorno
      *
     C                   endsr
      /EJECT
