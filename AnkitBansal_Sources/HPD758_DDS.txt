     A*%%TS  SD  19950530  125301  ISLRAMS     REL-V3R0M5  5763-PW1
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD758
     A*  TITLE:         VIEW INVENTORY ADJUSTMENTS
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  CREATED:       05/30/95
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A                                      CF04(04 'PROMPT')
     A                                      CF05(05 'REFRESH')
     A                                      CA12(12 'PREVIOUS')
     A*
     A*---------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP758S1                   SFL
     A*%%TS  SD  19950510  110445  ISLRAMS     REL-V3R0M5  5763-PW1
     A  85                                  SFLNXTCHG
     A            HDRRN1         4Y 0H
     A            DFMDY     R        O 11  9REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A            DFIAHD    R        O 11 22REFFLD(IAIAHD)
     A                                      EDTCDE(K)
     A            DFMSCD    R        O 11 33REFFLD(IAMSCD)
     A            DFMVSN    R     Y  O 11 41REFFLD(IAMVSN)
     A                                      EDTCDE(M)
     A*
     A*---------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP758C1                   SFLCTL(HP758S1)
     A*%%TS  SD  19950530  125301  ISLRAMS     REL-V3R0M5  5763-PW1
     A                                      SETOF(56)
     A N82                                  ROLLUP(25 'PAGE FORWARD')
     A                                      SFLSIZ(0100)
     A                                      SFLPAG(0011)
     A                                      SFLCSRRRN(&HDRR1)
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A                                      OVERLAY
     A                                      RTNCSRLOC(&RCD &FLD)
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            HDRR1          5S 0H
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 28'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  2 26'VIEW INVENTORY ADJUSTMENTS'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  4  2'Farm site ..:'
     A            C1FSCD    R        O  4 17REFFLD(FSFSCD)
     A                                      EDTCDE(M)
     A            C1FSNM    R        O  4 25REFFLD(FSFSNM)
     A                                  5  2'Hog group ..:'
     A            C1HGCD    R        O  5 17REFFLD(HGHGCD)
     A                                  7  8'Adjustment'
     A                                      DSPATR(HI)
     A                                  7 33'?'
     A                                      DSPATR(HI)
     A                                  8 11'Date'
     A                                      DSPATR(HI)
     A                                  8 24'Head'
     A                                      DSPATR(HI)
     A                                  8 31'Status'
     A                                      DSPATR(HI)
     A                                  8 41'Movement'
     A                                      DSPATR(HI)
     A            C1MDY     R        B  9  9REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  -')
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A            C1MSCD    R        B  9 33REFFLD(IAMSCD)
     A  56                                  DSPATR(PC)
     A                                      DSPATR(HI)
     A            C1MVSN    R        B  9 41REFFLD(IAMVSN)
     A                                      DSPATR(HI)
     A*
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R HP758K1
     A*%%TS  SD  19950530  125301  ISLRAMS     REL-V3R0M5  5763-PW1
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A                                      DSPATR(HI)
     A                                 23  2'F3=Exit'
     A                                 23 11'F4=Prompt'
     A                                 23 22'F5=Refresh'
     A                                 23 34'F12=Previous'
     A*
     A*--------------------------------------------------------------*
     A* ERROR MESSAGE SUBFILE FORMAT
     A*---------------------------------------------------------------
     A          R HP758ES                   SFL SFLMSGRCD(24)
     A*
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
     A*
     A*---------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*---------------------------------------------------------------
     A          R HP758EC                   SFLCTL(HP758ES)
     A*
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
