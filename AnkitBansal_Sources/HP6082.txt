      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Hog Production System
      * PROGRAM:     HP6082
      * TITLE:       G/L Account/HPS expense Code Exception Report
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     11/11/03
      *
      * FUNCTION:  Batch program to print G/L account objects that do not have a
      *            corresponding HPS Expense Code.
      *
      *            This program can be generated on-demand. It is also run as part
      *            of the EOP function.
      *
      ****************************************************************
      * MODIFICATIONS:
      *
      * 11/18/03  Barb Gutierrez
      *           Omit processing of cost centers that are obsolete and
      *           account balances that have no $$ associated with it.
      *           Also only want to process BGF farms in this program.
      *
      * 12/28/04  LeAnne Fedor
      *           Recompile only. New field 'Farm Size' added to Datamart Farm Site
      *           file.
      *
      * 06/16/09  LeAnne Ramsey
      *           Replace Datamart file HPPB018 with Production file HSP018.
      *
      * 10/16/13  LeAnne Ramsey (E2831)
      *           Recompile only. Added field 'MTech Reference'.
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Ff0901lk   if   e           k disk
      *  JDE: Account master
      *
      *
     Ff0902cb   if   e           k disk
      *  JDE: Account balances   (records selected with open query)
      *
      *
     Ff0006     if   e           k disk
      *  JDE: Cost center master
      *
      *
     Fhsp052    if   e           k disk
      *  Expense codes
      *
      *
     Fhsp018    if   e           k disk
      *  Farm site
      *
      *
     Fqprint    o    f  132        printer oflind(*inof)
      *
      /EJECT
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Standard fields
      *
     D dash            s            132    inz(*all'-')
     d rtime           s              6  0
      *
      *
      * Control fields
      *
     D prtfl           s              1    inz('N')
     D svobj           s                   like(gbobj)
      *
      *
      * Workfields
      *
     D wkobj           s                   like(exexcd)
     D wkfrm5          s                   like(fsfscd)
      *
      *
      * Print fields
      *
     d h1ef            s              9
     d l1dl01          s                   like(gmdl01)
      *
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *---------------------------------------------------------------
      * Local data area.
      *---------------------------------------------------------------
      *
     Dlda             uds                  dtaara(*lda)
     D  ldfcymd                1     08  0
     D  ldtcymd                9     16  0
     D  ldper                 17     18  0
     D  ldeffl                19     19
     D  ldccyy                20     23  0
     D  ldfmdy                60     66  0
     D  ldtmdy                70     76  0
     D  lderfl                80     80
     D  ldppacyr              81     84  0
     D  ldppacpe              85     86  0
      *
     D  ldcell               102    103  0
     D  ldfscd               109    113  0
      *
     D wkmcu           ds                  inz
     D   wkfrm3                       3  0 overlay(wkmcu:10)
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * Mainline
      ****************************************************************
      *
      * Print headings
     C                   except    h1hdr
      *
      * Read each JDE Account Balance record selected by the open query.
      * Only process the record if you have $$ amount in any of the period buckets.
      *
     C     *loval        setll     f0902cb
      *
     C                   dou       *in90 = *on                                  Main do
     C                   read      f0902cb                                90
     C                   if        *in90 = *off and                             If not EOF
     C                             (gban01 <> 0 or
     C                             gban02 <> 0 or
     C                             gban03 <> 0 or
     C                             gban04 <> 0 or
     C                             gban05 <> 0 or
     C                             gban06 <> 0 or
     C                             gban07 <> 0 or
     C                             gban08 <> 0 or
     C                             gban09 <> 0 or
     C                             gban10 <> 0 or
     C                             gban11 <> 0 or
     C                             gban12 <> 0)
      *
      * You can have multiple records with the SAME cost center/object--
      * just different sub codes. We only want to process each Object 1 time.
      *
     C                   if        gbobj <> svobj                               If different
     C                   move      gbobj         svobj
      *
      * Only want to process BGF farms.
      *
     C                   move      gbmcu         wkmcu
     C                   move      wkfrm3        wkfrm5
     C     wkfrm5        chain     hsp018                             92
     C                   if        *in92 = *off and fsppcd = 'BGF  '            If BGF farm
      *
      * Only process Posting Accounts and accounts with a Category Code 10
      * between 01 and 21...
      *
     C     gbmcu         chain     f0006                              92
     C                   if        *in92 = *off and                             If continue
     C                             mcpecc <> 'N' and
     C                             mcrp10 >= '01 ' and
     C                             mcrp10 <= '21 ' and
     C                             mcstyl <> 'OB'
      *
      * Now, check to see if this Object is set up as an HPS Expense code.
      * If there is no Expense Code, print a line on the Exception report.
      *
     C                   movel     gbobj         wkobj
     C     wkobj         chain     hsp052                             92
     C                   if        *in92 = *on
     C                   exsr      $l1dtl
     C                   endif
      *
     C                   endif                                                  If continue
     C                   endif                                                  If BGF farm
     C                   endif                                                  If different
     C                   endif                                                  If not EOF
     C                   enddo                                                  Main do
      *
      *---------------------------------------------------------------
      * EOF processing
      *---------------------------------------------------------------
      *
      * If nothing was printed, print the standard 'no data...' message
      *
     C                   if        prtfl = no
     C                   except    nodata
     C                   endif
      *
     C                   seton                                        lr
      /eject
      *---------------------------------------------------------------
      * Print detail
      *---------------------------------------------------------------
      *
     C     $l1dtl        begsr
      *
      * Retrieve Account Description
      *
     C     key01         chain     f0901lk                            92
     C                   if        *in92 = *off
     C                   move      gmdl01        l1dl01
     C                   else
     C                   eval      l1dl01 = 'Unknown'
     C                   endif
      *
      * Print headings
     C                   if        *inof = *on
     C                   exsr      $h1hdr
     C                   endif
      *
      * Print detail
     C                   except    l1dtl
      *
     C                   move      yes           prtfl
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Print header
      *---------------------------------------------------------------
      *
     C     $h1hdr        begsr
      *
     C                   except    h1hdr
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Key lists
      *
     C     key01         klist
     C                   kfld                    gbco
     C                   kfld                    gbobj
      *
      *
      * Retrieve time for report heading.
      *
     C                   time                    rtime
      *
      *
      * Edit/Final/On-demand
      *
     C                   select
     C                   when      ldeffl = 'D'
     C                   movel(p)  'On-Demand'   h1ef
      *
     C                   when      ldeffl = 'E'
     C                   movel(p)  '  Edit   '   h1ef
      *
     C                   when      ldeffl = 'F'
     C                   movel(p)  '  Final  '   h1ef
     C                   endsl
      *
     C                   endsr
      /EJECT
      *-------------------------------------------------------------
      * Report heading lines
      *-------------------------------------------------------------
      *
     Oqprint    e            h1hdr          1 01
     O                       sdpgm               10
     O                                           77 'HOG PRODUCTION SYSTEM'
     O                                          122 'DATE'
     O                       udate         y    132
      *
     O          e            h1hdr          1
     O                       sdusr               10
     O                                           67 'G/L ACCOUNT/HPS EXPENSE'
     O                                           89 'CODE EXCEPTION REPORT'
     O                                          122 'TIME'
     O                       rtime              132 '  :  :  '
      *
     O          e            h1hdr          2
     O                       h1ef                70
     O                                          122 'PAGE'
     O                       page          z    132
      *
      *
     O          e            h1hdr          1
     O                                            5 'Year:'
     O                       ldccyy        z     10
      *
      *
      *-------------------------------------------------------------
      * Column headings
      *-------------------------------------------------------------
      *
     O          e            h1hdr       1  1
     O                                            6 'OBJECT'
      *
     O          e            h1hdr          1
     O                                            7 'ACCOUNT'
     O                                           20 'DESCRIPTION'
      *
     O          e            h1hdr          0
     O                       dash               132
      *
      *-------------------------------------------------------------
      * Detail line
      *-------------------------------------------------------------
      *
     O          e            l1dtl       1
     O                       gbobj                6
     O                       l1dl01              40
      *
      *-------------------------------------------------------------
      * No data message line
      *-------------------------------------------------------------
      *
     O          e            nodata      1
     O                                           25 'There are no exceptions.'
