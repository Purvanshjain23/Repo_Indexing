     A*%%TS  SD  20080107  162340  ISLRAMS     REL-V5R4M0  5722-WDS
     A*****************************************************************
     A*
     A*  ENVIRONMENT:   PORK DIVISION
     A*  SYSTEM:        RESOURCE OPTIMIZATION
     A*  DISPLAY FILE:  ROD408
     A*  TITLE:         WORK WITH MEAT COST
     A*  PROGRAMMER:    LEANNE RAMSEY
     A*  CREATED:       02/13/07
     A*
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/ROPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A                                      CF04(04 'PROMPT')
     A                                      CF05(05 'REFRESH')
     A                                      CA07(07 'REPORT')
     A                                      CF13(13 'FOLD/DROP')
     A                                      CF17(17 'PRINT OPTIONS')
     A                                      CF20(20 'WRKSPLF')
     A*
     A*
     A*---------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*---------------------------------------------------------------
     A          R RO408S1                   SFL
     A*%%TS  SD  20071114  124135  ISLRAMS     REL-V5R4M0  5722-WDS
     A  85                                  SFLNXTCHG
     A            HDWEDT    R        H      REFFLD(@@WEDT)
     A            HDRRN1         4Y 0H
     A            D1OPT          2A  B 11  2CHGINPDFT(UL)
     A  30                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A N30                                  DSPATR(HI)
     A            D1ITCD    R        O 11  6REFFLD(ROREF/@@ITCD *LIBL/ROPREF)
     A                                      EDTCDE(M)
     A            D1CONO    R        O 11 15REFFLD(ROREF/@@CONO *LIBL/ROPREF)
     A                                      EDTCDE(K)
     A            D1WEMDY   R        O 11 20REFFLD(@@DT06)
     A                                      EDTWRD('  /  /  ')
     A            D1PGCD    R        O 11 30REFFLD(ROREF/@@PGCD *LIBL/ROPREF)
     A            D1PLCD    R        O 11 37REFFLD(ROREF/@@PLCD *LIBL/ROPREF)
     A            D1YMCO    R        O 11 44REFFLD(ROREF/@@YMCO *LIBL/ROPREF)
     A                                      EDTCDE(K)
     A            D1BSPR    R        O 11 56REFFLD(ROREF/@@BSPR *LIBL/ROPREF)
     A                                      EDTCDE(K)
     A            D1CRVL    R        O 11 68REFFLD(ROREF/@@CRVL *LIBL/ROPREF)
     A                                      EDTCDE(K)
     A            D1ITDS    R        O 12 11REFFLD(ROREF/@@ITDS *LIBL/ROPREF)
     A                                      COLOR(PNK)
     A                                 12 43'Base:'
     A                                      COLOR(BLU)
     A            D1BIDS    R        O 12 49REFFLD(ROREF/@@BIDS *LIBL/ROPREF)
     A                                      COLOR(PNK)
     A*
     A*---------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*---------------------------------------------------------------
     A          R RO408C1                   SFLCTL(RO408S1)
     A*%%TS  SD  20080107  162340  ISLRAMS     REL-V5R4M0  5722-WDS
     A                                      SFLSIZ(0100)
     A                                      SFLPAG(0005)
     A N82                                  ROLLUP(25 'PAGE FORWARD')
     A                                      SETOF(52)
     A                                      SETOF(53)
     A                                      RTNCSRLOC(&RCD &FLD)
     A                                      OVERLAY
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A  78                                  SFLFOLD(CF13)
     A  79                                  SFLDROP(CF13)
     A                                      SFLMODE(&FOLD)
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            FOLD           1A  H
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 25'RESOURCE OPTIMIZATION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USR)
     A                                      DSPATR(HI)
     A                                  2 30'WORK WITH MEAT COST'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@JOB)
     A                                      DSPATR(HI)
     A                                  3 30'                  '
     A                                  4  3'5=View Component Items'
     A                                  4 38'6=Print Meat Cost Report'
     A                                  5  2'15=View Ham Group Yield Breakdown'
     A                                  5 37'16=Print Ham Group Yield Breakdown'
     A                                  6 32'?'
     A                                      DSPATR(HI)
     A                                  7 11'?'
     A                                      DSPATR(HI)
     A                                  7 22'Week'
     A                                      DSPATR(HI)
     A                                  7 30'Primal'
     A                                      DSPATR(HI)
     A                                  7 38'Prod'
     A                                      DSPATR(HI)
     A                                  7 47'Yielded'
     A                                      DSPATR(HI)
     A                                  8  9'Item'
     A                                      DSPATR(HI)
     A                                  8 15'Comp'
     A                                      DSPATR(HI)
     A                                  8 21'Ending'
     A                                      DSPATR(HI)
     A                                  8 30'Group'
     A                                      DSPATR(HI)
     A                                  8 38'Line'
     A                                      DSPATR(HI)
     A                                  8 50'Meat'
     A                                      DSPATR(HI)
     A                                  8 62'Base'
     A                                      DSPATR(HI)
     A                                  8 72'Credit'
     A                                      DSPATR(HI)
     A                                  9  2'Opt'
     A                                      DSPATR(HI)
     A                                  9  9'Code'
     A                                      DSPATR(HI)
     A                                  9 15'Nbr'
     A                                      DSPATR(HI)
     A                                  9 22'Date'
     A                                      DSPATR(HI)
     A                                  9 30'Code'
     A                                      DSPATR(HI)
     A                                  9 38'Code'
     A                                      DSPATR(HI)
     A                                  9 50'Cost'
     A                                      DSPATR(HI)
     A                                  9 61'Price'
     A                                      DSPATR(HI)
     A                                  9 73'Value'
     A                                      DSPATR(HI)
     A            C1ITCD    R        B 10  6REFFLD(ROREF/@@ITCD *LIBL/ROPREF)
     A                                      DSPATR(HI)
     A                                      EDTCDE(M)
     A  53                                  DSPATR(PC)
     A            C1CONO    R        B 10 15REFFLD(ROREF/@@CONO *LIBL/ROPREF)
     A                                      DSPATR(HI)
     A                                      EDTCDE(K)
     A            C1WEMDY   R        B 10 20REFFLD(@@DT06)
     A                                      DSPATR(HI)
     A                                      EDTWRD('  /  /  -')
     A            C1PGCD    R        B 10 30REFFLD(ROREF/@@PGCD *LIBL/ROPREF)
     A                                      DSPATR(HI)
     A  52                                  DSPATR(PC)
     A            C1PLCD    R        B 10 37REFFLD(ROREF/@@PLCD *LIBL/ROPREF)
     A                                      DSPATR(HI)
     A*
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R RO408K1
     A*%%TS  SD  20080103  162921  ISLRAMS     REL-V5R4M0  5722-WDS
     A  43                             21  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 22  2'F3=Exit'
     A                                 22 11'F4=Prompt'
     A                                 22 22'F5=Refresh'
     A                                 22 34'F7=Reports'
     A                                 22 46'F13=Fold/Drop'
     A                                 22 61'F17=Print Options'
     A                                 23 61'F20=Spool Files'
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *---------------------------------------------------------------
     A          R RO408ES                   SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE CONTROL FORMAT
      *---------------------------------------------------------------
     A          R RO408EC                   SFLCTL(RO408ES)
      *
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
