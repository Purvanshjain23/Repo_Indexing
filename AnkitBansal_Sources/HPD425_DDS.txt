     A*%%TS  SD  19970617  131933  ISLRAMS     REL-V3R6M0  5716-PW1
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION
     A*  DISPLAY FILE:  HPD425
     A*  TITLE:         WORK WITH FUNCTIONS
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  CREATED:       02/09/95
     A*
     A*****************************************************************
     A* MODIFICATIONS:
     A* 06/17/97  LEANNE FEDOR
     A*           ADDED A FUNCTION KEY FOR PRINT OPTIONS.
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A                                      CF04(04 'PROMPT')
     A                                      CF05(05 'REFRESH')
     A                                      CF06(06 'CREATE')
     A                                      CF07(07 'LISTING')
     A                                      CF09(09 'LISTING')
     A                                      CF17(17 'PRINT OPTIONS')
     A                                      CF20(20 'WRKSPLF')
     A*
     A*---------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP425S1                   SFL
     A*%%TS  SD  19950227  083629  ISLRAMS     REL-V3R0M5  5763-PW1
     A  85                                  SFLNXTCHG
     A            HDRRN1         4Y 0H
     A            DFOPT          2A  B 10  2CHGINPDFT(UL)
     A  30                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A N30                                  DSPATR(HI)
     A            DFFNCD    R        O 10  7REFFLD(FNFNCD)
     A            DFFNDS    R        O 10 19REFFLD(FNFNDS)
     A            DFSFFL    R        O 10 53REFFLD(FNSFFL)
     A            DFFNTY    R        O 10 60REFFLD(FNFNTY)
     A            DFBOFL    R        O 10 67REFFLD(FNBOFL)
     A            DFDVCD    R        O 10 74REFFLD(FNDVCD)
     A*
     A*---------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP425C1                   SFLCTL(HP425S1)
     A*%%TS  SD  19950227  084623  ISLRAMS     REL-V3R0M5  5763-PW1
     A                                      SFLSIZ(0050)
     A                                      SFLPAG(0011)
     A N82                                  ROLLUP(25 'PAGE FORWARD')
     A                                      SETOF(52)
     A                                      SETOF(53)
     A                                      SETOF(54)
     A                                      SETOF(55)
     A                                      RTNCSRLOC(&RCD &FLD)
     A                                      OVERLAY
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            RCD           10A  H
     A            FLD           10A  H
     A                                  1  2'HP425'
     A                                      DSPATR(HI)
     A                                  1 26'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  2 27'WORK WITH FUNCTIONS'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  4  2'2=Revise'
     A                                  4 12'3=Replace'
     A                                  4 23'4=Delete'
     A                                  4 33'6=Print Authorized Users'
     A                                  5  2'7=Maintain Authorized Users'
     A                                  5 33'8=View Authorized Users'
     A                                  6 52'?'
     A                                      DSPATR(HI)
     A                                  6 60'?'
     A                                      DSPATR(HI)
     A                                  6 67'?'
     A                                      DSPATR(HI)
     A                                  6 76'?'
     A                                      DSPATR(HI)
     A                                  7 49'Secured'
     A                                      DSPATR(HI)
     A                                  7 66'B/O'
     A                                      DSPATR(HI)
     A                                  7 72'Division'
     A                                      DSPATR(HI)
     A                                  8  2'Opt'
     A                                      DSPATR(HI)
     A                                  8  7'Code'
     A                                      DSPATR(HI)
     A                                  8 19'Description'
     A                                      DSPATR(HI)
     A                                  8 49'Function'
     A                                      DSPATR(HI)
     A                                  8 59'Type'
     A                                      DSPATR(HI)
     A                                  8 66'Reqd'
     A                                      DSPATR(HI)
     A                                  8 74'Code'
     A                                      DSPATR(HI)
     A            C1FNCD    R        B  9  7REFFLD(FNFNCD)
     A                                      DSPATR(HI)
     A            C1SFFL    R        B  9 53REFFLD(FNSFFL)
     A  52                                  DSPATR(PC)
     A                                      DSPATR(HI)
     A            C1FNTY    R        B  9 60REFFLD(FNFNTY)
     A                                      DSPATR(HI)
     A  53                                  DSPATR(PC)
     A            C1BOFL    R        B  9 67REFFLD(FNBOFL)
     A                                      DSPATR(HI)
     A  54                                  DSPATR(PC)
     A            C1DVCD    R        B  9 74REFFLD(FNDVCD)
     A                                      DSPATR(HI)
     A  55                                  DSPATR(PC)
     A*
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R HP425K1
     A*%%TS  SD  19970617  131933  ISLRAMS     REL-V3R6M0  5716-PW1
     A  43                             21  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 22  2'F3=Exit'
     A                                 22 11'F4=Prompt'
     A                                 22 22'F5=Refresh'
     A                                 22 34'F6=Create'
     A                                 22 53'F7=List Functions'
     A                                 23  2'F9=List Secured Functions'
     A                                 23 34'F17=Print Options'
     A                                 23 53'F20=Spool Files'
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *---------------------------------------------------------------
     A          R HP425ES                   SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE CONTROL FORMAT
      *---------------------------------------------------------------
     A          R HP425EC                   SFLCTL(HP425ES)
      *
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
