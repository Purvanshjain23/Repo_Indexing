/***************************************************************** */
/*                                                                 */
/*  SYSTEM:          TRIUMPH FOODS                                 */
/*  PROGRAM NUMBER:  TF403CL                                       */
/*  PROGRAM NAME:    CASH DISTRIBUTION BALANCE LISTING/DOWNLOAD    */
/*  PROGRAMMER:      LEANNE RAMSEY                                 */
/*  CREATE DATE:     09/03/09                                      */
/*                                                                 */
/*  FUNCTION:        PRINT/DOWNLOAD TFP011 FILE DATA               */
/*                                                                 */
/*******************************************************************/

             PGM

             DCL        VAR(&QDATA) TYPE(*CHAR) LEN(100)
             DCL        VAR(&RCDCNT) TYPE(*DEC) LEN(10 0)

             DCL        VAR(&OUTQ) TYPE(*CHAR) LEN(10)
             DCL        VAR(&HOLD) TYPE(*CHAR) LEN(4)
             DCL        VAR(&SAVE) TYPE(*CHAR) LEN(4)
             DCL        VAR(&COPY) TYPE(*DEC)  LEN(1)

             DCL        VAR(&FLAG)     TYPE(*CHAR) LEN(1)
             DCL        VAR(&LDEMAIL)  TYPE(*CHAR) LEN(50)

             DCL        VAR(&LDFCYMD) TYPE(*DEC) LEN(8)
             DCL        VAR(&LDTCYMD) TYPE(*DEC) LEN(8)
             DCL        VAR(&LDRDFL)  TYPE(*CHAR) LEN(1)
             DCL        VAR(&LDIECD)  TYPE(*CHAR) LEN(1)

             CHGVAR     VAR(&LDFCYMD)   VALUE(%SST(*LDA 13 8))
             CHGVAR     VAR(&LDTCYMD)   VALUE(%SST(*LDA 21 8))
             CHGVAR     VAR(&LDRDFL)    VALUE(%SST(*LDA 29 1))
             CHGVAR     VAR(&LDIECD)    VALUE(%SST(*LDA 49 1))
             CHGVAR     VAR(&LDEMAIL)   VALUE(%SST(*LDA 76 50))


/*---------------------------------------------------------------------*/
/* IF PRINTING THE REPORT, SET UP THE PRINT FILE                       */
/*---------------------------------------------------------------------*/

             IF         COND(&LDRDFL *EQ 'R') THEN(DO)

 OUTQ:       CHGVAR     VAR(&OUTQ) VALUE(%SST(*LDA 401 10))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(*JOB))

 HOLD:       CHGVAR     VAR(&HOLD) VALUE(%SST(*LDA 411 4))
             IF         COND(&HOLD *NE '*YES' *AND &HOLD *NE '*NO ') +
                          THEN(CHGVAR VAR(&HOLD) VALUE(*NO))

 COPY:       CHGVAR     VAR(&COPY) VALUE(%SST(*LDA 419 1))
             IF         COND(&COPY = 0) THEN(CHGVAR VAR(&COPY) +
                          VALUE(1))

 SAVE:       CHGVAR     VAR(&SAVE) VALUE(%SST(*LDA 415 4))
             IF         COND(&SAVE *NE '*YES' *AND &SAVE *NE '*NO ') +
                          THEN(CHGVAR VAR(&SAVE) VALUE(*NO))

             OVRPRTF    FILE(PRINT198) OUTQ(&OUTQ) COPIES(&COPY) +
                          HOLD(&HOLD) SAVE(&SAVE)
             ENDDO


/***********************************************************************/
/* EXTRACT THE DATA FROM THE CASH DISTRIBUTION BALANCE FILE            */
/***********************************************************************/

             CHGVAR     VAR(&QDATA) VALUE(' ')

/* FROM/TO DATE                                                           */

             CHGVAR     VAR(%SST(&QDATA 1 10)) VALUE('CBWEDT *GE')
             CHGVAR     VAR(%SST(&QDATA 12 8)) VALUE(&LDFCYMD)
             CHGVAR     VAR(%SST(&QDATA 21 15)) VALUE('*AND CBWEDT +
                          *LE')
             CHGVAR     VAR(%SST(&QDATA 37 8)) VALUE(&LDTCYMD)


/* IF ONLY THE 'EXEMPT' RECORDS                       */

             IF         COND(&LDIECD *EQ 'E') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 46 21)) VALUE('*AND CBRCEXFL +
                          *EQ "Y"')
             ENDDO


/* IF ONLY THE 'NON-EXEMPT' RECORDS                   */

             IF         COND(&LDIECD *EQ 'N') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 68 21)) VALUE('*AND CBRCEXFL +
                          *EQ "N"')
             ENDDO

/*---------------------------------------------------------------------*/
/* EXTRACT DATA                                                        */
/*---------------------------------------------------------------------*/

             OVRDBF     FILE(TFP011) SHARE(*YES)
             OPNQRYF    FILE((TFP011)) QRYSLT(&QDATA) KEYFLD(*FILE) +
                          SEQONLY(*YES)

/* REPORT                                                               */
             IF         COND(&LDRDFL *EQ 'R') THEN(DO)
             CRTDUPOBJL OBJ(TFP303) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)
             CALL       PGM(TF303)
             CALL       PGM(TF603)
             ENDDO

/* DOWNLOAD                                                             */

             IF         COND(&LDRDFL *EQ 'D') THEN(DO)

             CRTDUPOBJ  OBJ(TFP011) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP) NEWOBJ(TFP011DWL) DATA(*NO)
             CPYFRMQRYF FROMOPNID(TFP011) TOFILE(QTEMP/TFP011DWL) +
                          MBROPT(*REPLACE)

/* RETRIEVE THE NUMBER OF RECORDS IN THE DOWNLOAD FILE                  */

             RTVMBRD    FILE(TFP011DWL) NBRCURRCD(&RCDCNT)

             IF         COND(&RCDCNT = 0) THEN(DO)
             ESNDMAIL   RECIPIENT(&LDEMAIL) SUBJECT('Cash +
                          Distribution Balance Download') MSG('Your +
                          selections on TF403-Specify Options for +
                          Cash Distribution Balance +
                          Listing/Download did not extract any data +
                          for the spreadsheet download. Please +
                          change your selections and submit the +
                          Download again.')
             MONMSG     MSGID(CPF0000)
             ENDDO

             IF         COND(&RCDCNT >= 66000) THEN(DO)
             ESNDMAIL   RECIPIENT(&LDEMAIL) SUBJECT('Cash +
                          Distribution Balance Download') MSG('Your +
                          selections on TF403-Specify Options for +
                          Cash Distribution Balance +
                          Listing/Download extracted over 66,000 +
                          records--which is too many records for +
                          the spreadsheet to handle. Change your +
                          selections to extract fewer records and +
                          submit your Download request again.')
             MONMSG     MSGID(CPF0000)
             ENDDO

             IF         COND(&RCDCNT > 0 *AND &RCDCNT < 66000) THEN(DO)
             EXECUTE    SQL('SELECT * FROM TFP011DWL') NBRRCDS(*ALL) +
                          PCFMT(*XLS) RECIPIENT(&LDEMAIL) +
                          EMLMSG('Cash Distribution Balance +
                          Download') TEXT('Cash Distribution +
                          Balance Download')
             MONMSG     MSGID(CPF0000)
             ENDDO

             ENDDO

/*---------------------------------------------------------------------*/
/* END OF PROCESSING                                                   */
/*---------------------------------------------------------------------*/

             CLOF       OPNID(TFP011)
             DLTOVR     FILE(*ALL)

             ENDPGM

