      *         *****************  RPG PROGRAM HEADING  ************************
      * SYSTEM:      HOG PRODUCTION
      * PROGRAM:     HP392
      * TITLE:       REOPEN A CLOSED PERIOD
      * PROGRAMMER:  LEANNE FEDOR
      * CREATED:     10/20/94
      *
      *  FUNCTION:   THIS BATCH PROGRAM IS CALLED FROM HP492 THROUGH
      *              QCMDEXC TO REOPEN A CLOSED PERIOD.
      *
      *              THERE ARE 4 STEPS FOR REOPENING A CLOSED PERIOD:
      *
      *              1) DELETE ALL RECORDS FOR THE PERIOD FROM
      *                 THE CLOSED PERIOD FILE.
      *
      *              2) UPDATE RECORDS IN THE FEED TICKET HEADER
      *                 FILE. SET THE TICKET STATUS TO COSTED AND THE
      *                 PERIOD END DATE TO ZERO.
      *
      *              3) UPDATE RECORDS IN THE EXPENSE TICKET FILE.
      *                 SET THE TICKET STATUS TO ENTERED AND THE
      *                 PERIOD END DATE TO ZERO.
      *
      *              4) UPDATE RECORDS IN THE MOVEMENT HEADER FILE.
      *                 SET THE PERIOD END DATE TO ZERO AND THE
      *                 MOVEMENT STATUS AS FOLLOWS:
      *                   TRANSFERS       RESET TO SHIPPED
      *                   ADJUSTMENTS     RESET TO SHIPPED
      *                   SALES           RESET TO FULLY PAID
      *
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 07/14/03  LeAnne Fedor
      *           Removed all reference to file HSP047-EOP Farm Site file; this
      *           file no longer exists in the system.
      *
      * 04/23/09  LeAnne Ramsey
      *           Recompile only. Costed Time/User added to Feed Ticket Header.
      *
      * 05/12/09  LeAnne Ramsey
      *           Recompile only. PickUp Ticket Type added to Feed Ticket Header.
      *
      * 09/21/09  LeAnne Ramsey
      *           Recompile only. Added new field 'Upload Date' to Feed Ticket Header.
      *
      * 05/10/22 Eric L SDN736 Recompiled ticket nbr increase (TKNO & RTNO 7.0 TO 9.0)
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     FHSP037    UF   E           K DISK
      *    FEED TICKET HEADER
      *    (RECORDS SELECTED IN OPNQRY)
      *
      *
     FHSP048    UF   E           K DISK
      *    EXPENSE TICKET FILE
      *    (RECORDS SELECTED IN OPNQRY)
      *
      *
     FHSP068    UF   E           K DISK
      *    KILLED/DEAD HOGS
      *    (RECORDS SELECTED IN OPNQRY)
      *
      *
     FHSP069    UF   E           K DISK
      *    INVENTORY ADJUSTMENTS
      *    (RECORDS SELECTED IN OPNQRY)
      *
      *
     FHSP074    UF   E           K DISK
      *    TRANSFER MOVEMENT HEADER
      *    (RECORDS SELECTED IN OPNQRY)
      *
      *
     FHSP084    UF   E           K DISK
      *    SALES MOVEMENT HEADER
      *    (RECORDS SELECTED IN OPNQRY)
      *
      /EJECT
      ****************************************************************
      * Definition specs
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      *
      /EJECT
      ****************************************************************
      * MAIN LINE
      ****************************************************************
      *
      * UPDATE ALL FEED TICKET HEADER RECORDS FOR THE SPECIFIED PERIOD WITH:
      *   1) A STATUS OF 'CO'-COSTED
      *   2) A PERIOD END DATE OF ZERO
      *
      *
     C     *IN92         DOUEQ     *ON                                          DO FEED
     C                   READ      HSP037                                 92
     C     *IN92         IFEQ      *OFF                                         IF NOT EOF
     C                   MOVEL     'CO'          FHTSCD
     C                   Z-ADD     0             FHEPDT
     C                   UPDATE    FHREC
     C                   ENDIF                                                  IF NOT EOF
     C                   ENDDO                                                  DO FEED
      *
      *
      * UPDATE ALL EXPENSE TICKET RECORDS FOR THE SPECIFIED PERIOD WITH:
      *   1) A STATUS OF 'EN'-ENTERED
      *   2) A PERIOD END DATE OF ZERO
      *
      *
     C     *IN92         DOUEQ     *ON                                          DO FEED
     C                   READ      HSP048                                 92
     C     *IN92         IFEQ      *OFF                                         IF NOT EOF
     C                   MOVEL     'EN'          XTXSCD
     C                   Z-ADD     0             XTEPDT
     C                   UPDATE    XTREC
     C                   ENDIF                                                  IF NOT EOF
     C                   ENDDO                                                  DO FEED
      *
      *
      * MOVEMENTS:
      * RESET THE DATE TO ZERO AND THE MOVEMENT STATUS AS FOLLOWS:
      *      DAILY DEADS   RESET TO 'EN' ENTERED
      *      ADJUSTMENTS   RESET TO 'EN' ENTERED
      *      TRANSFERS     RESET TO 'SH' SHIPPED
      *      SALES         RESET TO 'FP' FULLY PAID
      *
      *
      * DAILY DEADS
      *
     C     *IN92         DOUEQ     *ON                                          DO DEAD
     C                   READ      HSP068                                 92
     C     *IN92         IFEQ      *OFF                                         IF NOT EOF
     C                   Z-ADD     0             KDEPDT
     C                   MOVEL     'EN'          KDMSCD
     C                   UPDATE    KDREC
     C                   ENDIF                                                  IF NOT EOF
     C                   ENDDO                                                  DO DEAD
      *
      * INVENTORY ADJUSTMENTS
      *
     C     *IN92         DOUEQ     *ON                                          DO ADJUST
     C                   READ      HSP069                                 92
     C     *IN92         IFEQ      *OFF                                         IF NOT EOF
     C                   Z-ADD     0             IAEPDT
     C                   MOVEL     'EN'          IAMSCD
     C                   UPDATE    IAREC
     C                   ENDIF                                                  IF NOT EOF
     C                   ENDDO                                                  DO ADJUST
      *
      * SALES MOVEMENTS
      *
     C     *IN92         DOUEQ     *ON                                          DO SALES
     C                   READ      HSP084                                 92
     C     *IN92         IFEQ      *OFF                                         IF NOT EOF
     C                   Z-ADD     0             SHEPDT
     C                   MOVEL     'FP'          SHMSCD
     C                   UPDATE    SHREC
     C                   ENDIF                                                  IF NOT EOF
     C                   ENDDO                                                  DO SALES
      *
      * TRANSFER MOVEMENTS
      *
     C     *IN92         DOUEQ     *ON                                          DO TRANSFERS
     C                   READ      HSP074                                 92
     C     *IN92         IFEQ      *OFF                                         IF NOT EOF
     C                   Z-ADD     0             MHEPDT
     C                   MOVEL     'SH'          MHMSCD
     C                   UPDATE    MHREC
     C                   ENDIF                                                  IF NOT EOF
     C                   ENDDO                                                  DO TRANSFERS
      *
     C                   SETON                                        LR
      /EJECT
