      *****************  RPG PROGRAM HEADING  ************************
     h option(*SRCSTMT:*NODEBUGIO)
      ****************************************************************
      *
      * SYSTEM:      Resource Optimization
      * PROGRAM:     RO211 - Update Item Standard Cost file
      * PROGRAMMER:  LeAnne Ramsey
      * CREATED:     01/18/07
      *
      * Function:    When a Final of the Weekly Meat Cost function runs, we call this program
      *              to update the Std Meat Cost in the Synon Item Standard Cost file for
      *              Company/Week-ending Date/Item.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 04/11/07  LeAnne Ramsey
      *           Recompile only. New fields added to Item Meat Cost file.
      *
      * 08/29/07  LeAnne Ramsey
      *           Program was abending on the Standard Meat Cost calc with
      *           "target for numeric operation is too small to hold the result".
      *           This happens when we have bad data. So, we will now calc
      *           into a 15,4 field and only move into the Standard Meat Cost
      *           when the result is LE 999.
      *
      * 11/02/07  LeAnne Ramsey
      *           Recompile only. Added "sales yield" to Item Meat Cost file.
      *
      * 11/15/07  Alice Brownfield
      *           Changed to update the Yield % as well as the Meat Cost.
      *
      * 03/18/08  LeAnne Ramsey
      *           Recompile only. Synon file POAXCPP was recompiled.
      *
      * 11/21/08  LeAnne Ramsey
      *           As part of synchronizing the LDAs between the TFS Margin Adjustment Close
      *           and the Meat Costing, we changed the LDA positions.
      *
      * 06/17/09  LeAnne Ramsey
      *           Recompile only. Three fields were added to ROP103-Item Meat Cost:
      *               TF Classification Code, TF Class Group Code, Item Structure Type Code
      /eject
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fpoaxcpl1  uf a e           k disk
      *  Item standard cost
      *
      *
     Frop103    if   e           k disk
      *  Item meat cost
      *
      /eject
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Workfields for date manipulation
      *
     D wkisodate       s               D   datfmt(*iso)
     D wkjobdt         s              7  0
      *
      *
      * Work fields
      *
     D wkpr            s             15  4
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *---------------------------------------------------------------
      *  Local data area
      *---------------------------------------------------------------
      *
     d lda            uds                  dtaara(*lda)
      *
     D  ldwedt                29     36  0
     D  ldwesyn               37     43  0
      *
     D  ldcono                82     84  0
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * Mainline
      ****************************************************************
      *
      * Process each record for the Company/Week in the Item Meat Cost file.
      * For each record, retrieve/update the corresponding Synon Item Standard
      * Cost record. If no Synon record exists, write one.
      *
      *
     C     key01         setll     rop103
      *
     C                   dou       *in90 = *on                                  Main do
     C     key01         reade     rop103                                 90
     C                   if        *in90 = *off                                 If not EOF
      *
      * Retrieve Item Standard Cost record.
      * Flip Yielded Meat Cost/LB to Std Meat Cost/CWT.
      * Setup the Yield %
      * Update record.
      *
     C                   eval      wkpr = imymco * 100
      *
     C     key02         chain     poaxcpl1                           92
     C                   if        *in92 = *off                                 If hit
      * Meat Cost
     C                   if        wkpr <= 999
     C                   z-add     wkpr          axfrpr
     C                   endif
      * Yield %
     C                   if        imsypc <> 0
     C                   eval      axfcpc = imsypc/100
     C                   endif
      * Update
     C                   update    @axcphi
     C                   else
      *
      * Write a record when you don't get a hit.
      *
     C                   clear                   @axcphi
     C                   z-add     ldcono        axaic3
     C                   z-add     imitcd        axhgcd
     C                   z-add     ldwesyn       axaudt
     C                   eval      axvsst = 'A'
     C                   eval      axayna = sdusr
     C                   eval      axa0na = sdjob
     C                   eval      axaatm = sdstm
     C                   eval      axaxdt = wkjobdt
      * Meat Cost
     C                   if        wkpr <= 999
     C                   z-add     wkpr          axfrpr
     C                   endif
      * Yield %
     C                   eval      axfcpc = imsypc/100
      * Write
     C                   write     @axcphi
     C                   endif
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Main do
      *
     C                   seton                                        lr
      /eject
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Key lists
      *
     C     key01         klist
     C                   kfld                    ldcono
     C                   kfld                    ldwedt
      *
     C     key02         klist
     C                   kfld                    ldcono
     C                   kfld                    imitcd
     C                   kfld                    ldwesyn
      *
      * When writing Item Standard Cost records, you will need the Job Date
      * in Synon format.
      *
     C     *mdy          move      sdsdt         wkisodate
     C     *cymd         move      wkisodate     wkjobdt
      *
     C                   endsr
      /eject
