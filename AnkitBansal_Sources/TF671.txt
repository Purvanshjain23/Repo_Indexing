      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Hog Production
      * PROGRAM:     TF671 - Claims: Listing of OMS Credit/Debit Memos by Product
      * PROGRAMMER:  LeAnne Ramsey
      * CREATED:     08/26/09
      *
      * FUNCTION:    Prints records from the Sales History file.
      *              This report was written for Tim Woods to use when he is
      *              trying to reconcile his Cash Distribution.
      *
      *              This 'report' is generated:
      *                 1) on-demand
      *
      **********************************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 09/14/09  LeAnne Ramsey
      *           Changed logic to print the "A/R" customer number/name from Sales History
      *           instead of the "ShipTo" customer number/name.
      /eject
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fomhstplb  if   e           k disk
      *    Sales history (records selected/keyed in Open Query)
      *
      *
     Ftfp007    if   e           k disk
      *  Exempt codes
      *
      *
     Fprint198  o    f  198        printer oflind(*inof)
      *
      /eject
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      *
      ****************************************************************
      * ARRAYS AND TABLES
      ****************************************************************
      *
     D aragam          s             11  2 dim(3)
     D aranam          s             11  2 dim(3)
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Standard fields
      *
     D dash            s            198    inz(*all'-')
     d rtime           s              6  0
      *
      *
      * Control fields
      *
     D first           s              1    inz('Y')
     d cntlnno         s              3  0
     D svcono          s                   like(f4aic3)
     D svprcd          s                   like(f4s5nb)
      *
      *
      * Workfields for date manipulation
      *
     D wkisodate       s               D   datfmt(*iso)
      *
      *
      * Print fields
      *
     D l1rcexfl        s                   like(exrcexfl)
     d l1wemdy         s              6  0
     d l1asmdy         s              6  0
     d l1acllb         s                   like(f4bywg)
     d l1afslfl        s                   like(f4vast)
     d l1anclam        s                   like(f4kdva)
      *
     d t1desc          s             18
     D t1cono          s                   like(f4aic3)
      *
     D t2eagam         s             11  2
     D t2eanam         s             11  2
      *
     D t2nagam         s             11  2
     D t2nanam         s             11  2
      *
      * Parms
      *
     D xxdt            s              8  0
     D xxsatdt         s              8  0
     D xxyr            s              4  0
     D xxpe            s              2  0
     D xxwk            s              2  0
      *
      * Array index
      *
     D x               s              3  0
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      *---------------------------------------------------------------
      * Local data area.
      *---------------------------------------------------------------
      *
     Dlda             uds                  dtaara(*lda)
      *
     D  ldfsyndt              25     31  0
     D  ldtsyndt              32     38  0
     D  ldfmdy                39     44  0
     D  ldtmdy                45     50  0
     D  ldfcymd               52     59  0
     D  ldtcymd               60     67  0
      *
     D  ldcono                68     70  0
     D  ldclno                71     77  0
     D  ldmmno                78     84  0
     D  ldprcd                85     91  0
     D  ldmmty                92     93
      *
     D  ldclorno              94    100  0
     D  lddscd               108    108
     D  ldcuno               109    115  0
     D  ldcunm               116    145
     D  ldprds               146    175
     D  lddsds               176    195
      *
     D  ldrpt                299    299
      *
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /eject
      ****************************************************************************************
      * Mainline
      ****************************************************************************************
      *
      * Read all Sales History records selected by the Open Query.
      * Print each record and accumulate total values.
      *
     C                   exsr      $h1hdr
      *
     C                   dou       *in90 = *on                                  Main do
     C                   read      omhstplb                               90
     C                   if        *in90 = *off                                 If not EOF
      *
      * Control break logic
      *
     C                   select
     C                   when      first = yes
     C                   move      no            first
     C                   z-add     f4s5nb        svprcd
     C                   z-add     f4aic3        svcono
     C                   exsr      $h2hdr
      * Company
     C                   when      f4aic3 <> svcono
     C                   exsr      $totprcd
     C                   exsr      $totcono
     C                   exsr      $h2hdr
      * Product
     C                   when      f4s5nb <> svprcd
     C                   exsr      $totprcd
     C                   endsl
      *
      * Detail Processing:
      *
      * If Affect Sales LBS Flag is Yes, populate
      *   Set Aggregate Claim LBS equal to Weight Billed
      *
     C                   if        f4vast = yes
     C                   move      yes           l1afslfl
     C                   z-add     f4bywg        l1acllb
     C                   else
     C                   move      no            l1afslfl
     C                   endif
      *
      * Get Aggregate and Net Claim Amounts into workfields
      *
     C                   eval      l1anclam = f4kdva
     C                                      - f4ulva - f4uova - f4unva
     C                                      - f4upva - f4uqva
      *
      * Accumulate Aggregate and Net Amounts into total arrays
      *
     C                   add       f4kdva        aragam
     C                   add       l1anclam      aranam
      *
      * Accumulate totals into Exempt/Non-Exempt totals for the
      *      "summary section" at the end of the report
      *
     C                   move      no            l1rcexfl
     C     f4iusx        chain     tfp007                             92
     C                   if        *in92 = *off
     C                   move      exrcexfl      l1rcexfl
     C                   endif
      *
     C                   select
     C                   when      l1rcexfl = 'Y'
     C                   add       f4kdva        t2eagam
     C                   add       l1anclam      t2eanam
     C                   other
     C                   add       f4kdva        t2nagam
     C                   add       l1anclam      t2nanam
     C                   endsl
      *
      * Count the lines processed for a Claim.
      *
     C                   add       1             cntlnno
      *
      * Print detail line
     C                   exsr      $l1dtl
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Main do
      *
      *-------------------
      * EOF PROCESSING
      *-------------------
      *
     C                   select
     C                   when      first = yes
     C                   except    nodata
     C                   other
      *
      * Print total lines:
      *  1) Last product
      *  2) Last company
      *  3) Report
     C                   exsr      $totprcd
     C                   exsr      $totcono
     C                   exsr      $totrpt
      *
      *  Print Summary Section (Exempt/Non-Exempt) total lines
      *
     C                   exsr      $t2tot
     C                   endsl
      *
     C                   seton                                        lr
      /eject
      *---------------------------------------------------------------
      * Print detail line
      *---------------------------------------------------------------
      *
     C     $l1dtl        begsr
      *
      * Flip Actual Ship Date from CCYYMMDD to MMDDYY format and retrieve the
      * Saturday WeekEnding Date.
      *
     C                   if        f4gndt <> 0                                  If actual ship date
     C     *cymd         test(d)                 f4gndt                 92
     C                   if        *in92 = *off                                 If OK ship date
     C     *cymd         move      f4gndt        wkisodate
     C     *mdy          move      wkisodate     l1asmdy
      *
      * Find subsequent Saturday using the generic program.
      *
     C     *iso          move      wkisodate     xxdt
     C                   call      'TF811'
     C                   parm                    xxdt
     C                   parm      0             xxsatdt
     C                   parm      0             xxyr
     C                   parm      0             xxpe
     C                   parm      0             xxwk
      *
      * Flip Saturday to MMDDYY format for printing.
      *
     C     *iso          test(d)                 xxsatdt                92
     C                   if        *in92 = *off                                 If OK Saturday
     C     *iso          move      xxsatdt       wkisodate
     C     *mdy          move      wkisodate     l1wemdy
     C                   endif                                                  If OK Saturday
     C                   endif                                                  If OK Ship Date
     C                   endif                                                  If actual ship date
      *
      *
      * Print headings if necessary
      *
     C                   if        *inof = *on
     C                   exsr      $h1hdr
     C                   exsr      $h2hdr
     C                   endif
      *
     C                   except    l1dtl
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Print Product Total Line
      *---------------------------------------------------------------
      *
     C     $totprcd      begsr
      *
     C                   z-add     1             x
     C                   move      *blank        t1desc
     C                   z-add     0             t1cono
      *
      * If you printed more than 1 detail line for this Product,
      *    print a Product Total line
      * Otherwise,
      *    clear the array totals for this claim.
      *
     C                   if        cntlnno > 1
     C                   exsr      $t1tot
     C                   else
     C                   exsr      $clear
     C                   except    blank
     C                   endif
      *
      * Save Product Code and reset count of number of lines processed for a Product
      *
     C                   z-add     f4s5nb        svprcd
     C                   z-add     0             cntlnno
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Print Company Total
      *---------------------------------------------------------------
      *
     C     $totcono      begsr
      *
     C                   z-add     2             x
     C                   z-add     svcono        t1cono
     C                   move      *blank        t1desc
      *
     C                   eval      t1desc = 'Total for Company:'
     C                   exsr      $t1tot
      *
     C                   z-add     f4aic3        svcono
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Print Report Total line
      *---------------------------------------------------------------
      *
     C     $totrpt       begsr
      *
     C                   move      *blank        t1desc
     C                   z-add     0             t1cono
     C                   seton                                        88
      *
     C                   eval      %subst(t1desc:6:13) = 'Report Total:'
      *
     C                   z-add     3             x
     C                   exsr      $t1tot
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Print Total line
      *---------------------------------------------------------------
      *
     C     $t1tot        begsr
      *
     C                   if        *inof = *on
     C                   exsr      $h1hdr
     C                   exsr      $h2hdr
     C                   endif
      *
     C                   except    t1tot
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Print Exempt/Non-Exempt Total lines at the end of the report
      *---------------------------------------------------------------
      *
     C     $t2tot        begsr
      *
     C                   if        *inof = *on
     C                   exsr      $h1hdr
     C                   endif
      *
     C                   except    t2tot
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Print report headings
      *---------------------------------------------------------------
      *
     C     $h1hdr        begsr
      *
     C                   except    h1hdr
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Print Company heading
      *---------------------------------------------------------------
      *
     C     $h2hdr        begsr
      *
     C                   if        *inof = *on
     C                   exsr      $h1hdr
     C                   endif
      *
      * Print it twice to make it "Bold".
      *
     C                   except    h2hdr
     C                   except    h2hdr
      *
      * Space twice
     C                   except    blank
     C                   except    blank
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Clear elements in arrays
      *---------------------------------------------------------------
      *
     C     $clear        begsr
      *
     C                   z-add     0             aragam (x)
     C                   z-add     0             aranam (x)
      *
     C                   endsr
      /eject
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Set up report headings
      *
     C                   time                    rtime
      *
     C                   if        ldfmdy = 0
     C                   seton                                        80
     C                   endif
      *
     C                   if        ldtmdy = 0
     C                   seton                                        81
     C                   endif
      *
     C                   if        ldcono = 0
     C                   seton                                        82
     C                   endif
      *
     C                   if        ldclno = 0
     C                   seton                                        83
     C                   endif
      *
     C                   if        ldclorno = 0
     C                   seton                                        89
     C                   endif
      *
     C                   if        ldmmty = *blank
     C                   seton                                        84
     C                   endif
      *
     C                   if        ldmmno = 0
     C                   seton                                        85
     C                   endif
      *
     C                   if        ldprcd = 0
     C                   seton                                        86
     C                   endif
      *
      *
     C                   if        lddscd = *blank
     C                   seton                                        98
     C                   endif
      *
     C                   if        ldcuno = 0
     C                   seton                                        87
     C                   endif
      *
     C                   endsr
      /eject
      *-------------------------------------------------------------
      * Report heading lines
      *-------------------------------------------------------------
      *
     Oprint198  e            h1hdr          1 01
     O                       sdpgm               10
     O                                           95 'LISTING OF CREDIT/DEBIT'
     O                                          + 1 'MEMOS FROM THE OMS SYSTEM'
     O                                          188 'DATE'
     O                       udate         y    198
      *
     O          e            h1hdr          1
     O                       sdusr               10
     O                                          188 'TIME'
     O                                          103 'By Product'
     O                       rtime              198 '  :  :  '
      *
     O          e            h1hdr          1
     O                                          188 'PAGE'
     O                       page          Z    198
      *
      * Line 1
     O          e            h1hdr       1  1
     O                                           15 'From date:'
     O              n80      ldfmdy        y     25
     O               80                          20 'All'
      *
     O                                           66 'Memo type:'
     O              n84      ldmmty              70
     O               84                          71 'All'
      *
     O                                          105 'Product code:'
     O              n86      ldprcd        z    114
     O              n85      ldprds             146
     O               86                         110 'All'
      * Line 2
     O          e            h1hdr          1
     O                                           15 'To date:'
     O              n81      ldtmdy        y     25
     O               81                          20 'All'
      *
     O                                           66 'Credit/debit memo number:'
     O              n85      ldmmno        z     75
     O               85                          71 'All'
      *
     O                                          105 'A/R customer:'
     O              n87      ldcuno        z    114
     O              n87      ldcunm             146
     O               87                         110 'All'
      *
      * Line 3
     O          e            h1hdr          1
     O                                           15 'Company number:'
     O              n82      ldcono        z     20
     O               82                          20 'All'
      *
     O                                           66 'Claim order number:'
     O              n89      ldclorno      z     75
     O               89                          71 'All'
      *
     O                                          105 'Data source:'
     O              n98      lddsds             127
     O               98                         110 'All'
      *
      * Line 4
     O          e            h1hdr          2
     O                                           15 'Claim number:'
     O              n83      ldclno        z     24
     O               83                          20 'All'
      *
      *-------------------------------------------------------------
      * Column headings
      *-------------------------------------------------------------
      *
     O          e            h1hdr          1
     O                                           26 'Actual'
     O                                           36 'Week'
     O                                           50 'CR/DB'
     O                                           65 'Claim'
     O                                           80 'Affect'
     O                                          139 'A/R'
      *
     O          e            h1hdr          1
     O                                            7 'Product'
     O                                           16 'Claim'
     O                                           26 'Ship'
     O                                           36 'Ending'
     O                                           42 'Memo'
     O                                           50 'Memo'
     O                                           56 'Line'
     O                                           65 'Order'
     O                                           73 'Exempt'
     O                                           80 'Lbs'
      *
     O                                           96 'Total Claim'
     O                                          112 'Total Gross'
     O                                          128 'Total Net'
     O                                          139 'Customer'
      *
     O          e            h1hdr          1
     O                                            7 'Code'
     O                                           16 'Number'
     O                                           26 'Date'
     O                                           36 'Date'
     O                                           42 'Type'
     O                                           50 'Number'
     O                                           56 'Nbr'
     O                                           65 'Number'
     O                                           73 'Record'
     O                                           80 'Flag'
      *
     O                                           96 'Pounds'
     O                                          112 'Claim Amount'
     O                                          128 'Claim Amount'
     O                                          139 'Number'
     O                                          158 'A/R Customer Name'
      *
     O          e            h1hdr          1
     O                       dash               198
      *
      *-------------------------------------------------------------
      * Company heading
      *-------------------------------------------------------------
      *
     O          e            h2hdr       0  0
     O                                            8 'Company:'
     O                       svcono        z     12
      *
      *-------------------------------------------------------------
      * Detail line
      *-------------------------------------------------------------
      *
     O          e            l1dtl          1
     O                       f4s5nb        m      8
     O                       f4c0c7        m     17
     O                       l1asmdy        b    26 '  /  /  '
     O                       l1wemdy        b    36 '  /  /  '
     O                       f4jncd              41
     O                       f4c4nb        m     51
     O                       f4dpnb        m     56
     O                       f4t3nb        m     66
     O                       l1rcexfl       b    70
     O                       l1afslfl       b    78
      *
     O                       l1acllb       kb    97
     O                       f4kdva        k    112
     O                       l1anclam      kb   129
      *
     O                       f4anc7        m    140
     O                       f4aqtx             171
      *
      *-------------------------------------------------------------
      * Total line
      *-------------------------------------------------------------
      *
     O          e            t1tot          1
     O              n88                         111 '--------------'
     O              n88                         128 '--------------'
      *
     O          e            t1tot          0
     O                       t1cono        k     62
     O                       t1desc              81
      *
     O          e            t1tot          2
     O                       t1cono        k     62
     O                       t1desc              81
     O                       aragam(x)     kb   112
     O                       aranam(x)     kb   129
      *
      *
      *--------------------------------------------------------------------------------
      * Total lines for the Exempt/Non-Exempt summary totals at the end of the report
      *--------------------------------------------------------------------------------
      *
     O          e            t2tot       1  0
     O                                           67 'EXEMPT/NON-EXEMPT SUMMARY:'
     O                                           81 'Exempt:'
      *
     O          e            t2tot       0  0
     O                                           67 'EXEMPT/NON-EXEMPT SUMMARY:'
     O                                           81 'Exempt:'
     O                       t2eagam       kb   112
     O                       t2eanam       kb   129
      *
     O          e            t2tot       1  0
     O                                           81 'Non-Exempt:'
      *
     O          e            t2tot       0  0
     O                                           81 'Non-Exempt:'
     O                       t2nagam       kb   112
     O                       t2nanam       kb   129
      *
      *-------------------------------------------------------------
      * Blank line
      *-------------------------------------------------------------
      *
     O          e            blank          1
      *
      *-------------------------------------------------------------
      * No data message line
      *-------------------------------------------------------------
      *
     O          e            nodata         1
     O                                           19 'No data meets your'
     O                                           39 'selection criteria.'
