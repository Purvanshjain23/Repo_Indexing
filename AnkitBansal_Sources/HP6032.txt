      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Hog Production System
      * PROGRAM:     HP6032
      * TITLE:       Listing of Production Variables
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     01/29/02
      *
      * FUNCTION:  Batch program to list the records in the file.
      *            This listing is submitted from HP4048-Work with Production
      *            Variables.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     Fhsp032    if   e           k disk
      *    Production variables
      *
      *
     Fqprint    o    f   80        printer oflind(*inof)
      *
      /EJECT
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      ****************************************************************
      * CONSTANTS
      ****************************************************************
      *
     D yes             c                   'Y'
     D no              c                   'N'
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Standard fields
      *
     D prtfl           s              1    inz('N')
     D dash            s             80
     d rtime           s              6  0
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * MAINLINE
      ****************************************************************
      *
      * Prepare to read the file and print headings the first
      * time through.
      *
     C     *loval        setll     hsp032
     C                   except    h1hdr
      *
      * Read and print all records in the file
      *
     C                   dou       *in90 = *on                                  Main do
     C                   read      hsp032                                 90
     C                   if        *in90 = *off                                 If not EOF
      *
      * Print detail line
      *
     C                   if        *inof = *on
     C                   except    h1hdr
     C                   endif
      *
     C                   except    l1dtl
     C                   movel     yes           prtfl
      *
     C                   endif                                                  If not EOF
     C                   enddo                                                  Main do
      *
      *---------------------------------------------------------------
      * EOF processing
      *---------------------------------------------------------------
      * If the file was empty, print the standard 'no data...'
      * error message
      *
     C                   if        prtfl = no
     C                   except    nodata
     C                   endif
      *
     C                   seton                                        lr
      /EJECT
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
      * Set up a line of underscores and retrieve time for report heading.
      *
     C                   movel     *all'-'       dash
     C                   time                    rtime
      *
     C                   endsr
      /EJECT
      *-------------------------------------------------------------
      * Report heading lines
      *-------------------------------------------------------------
      *
     Oqprint    e            h1hdr          1 01
     O                       sdpgm               10
     O                                           52 'HOG PRODUCTION SYSTEM'
     O                                           70 'DATE'
     O                       udate         y     80
      *
     O          e            h1hdr          1
     O                       sdusr               10
     O                                           47 'LISTING OF PRODUCTION '
     O                                           56 'VARIABLES'
     O                                           70 'TIME'
     O                       rtime               80 '  :  :  '
      *
     O          e            h1hdr          2
     O                                           70 'PAGE'
     O                       page          z     80
      *
      *
     O          e            h1hdr          1
     O                                            4 'CODE'
     O                                           17 'DESCRIPTION'
     O                                           68 'VALUE'
     O                                           80 'PERCENT'
      *
     O          e            h1hdr          1
     O                       dash                80
      *
      *-------------------------------------------------------------
      * Detail line
      *-------------------------------------------------------------
      *
     O          e            l1dtl       1
     O                       pvpvcd        k      4
     O                       pvpvds              56
     O                       pvpvval       k     69
     O                       pvperfl             77
      *
      *-------------------------------------------------------------
      * No data message line
      *-------------------------------------------------------------
      *
     O          e            nodata      1
     O                                           19 'NO DATA MEETS YOUR'
     O                                           38 'SELECTION CRITERIA'
