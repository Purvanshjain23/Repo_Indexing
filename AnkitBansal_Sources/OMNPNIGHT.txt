/********************************************************************/
/*                                                                  */
/*                                                                  */
/*  SYNOPSIS - PRINT SALES REPORT BY CO/DATE RANGE/ITEM CLASS/      */
/*             ITEM GROUP/ITEM TYPE.  THIS WILL PRINT THE SALES     */
/*             BY ITEM STRUCTURE FROM THE AS/400 SCHEDULER.         */
/*                                                                  */
/*                                                                  */
/*  COMPANY   - SEABOARD COPRORATION                                */
/*  SYSTEM    - ORDER MANAGEMENT                                    */
/*  USER NAME - MARK ARENSBERG                                      */
/*  DATE      - 07/19/95                                            */
/*                                                                  */
/*      PARAMETERS USED:                                            */
/*                                                                  */
/*          &OUTQ       - OUTPUT QUEUE                              */
/*          &COPIES     - NUMBER OF COPIES                          */
/*          &NIGHTQ     - NIGHT QUEUE                               */
/*          &HOLD       - HOLD PRINT FLAG                           */
/*          &SAVE       - SAVE PRINT FLAG                           */
/*          &PGM        - PROGRAM NAME                              */
/*          &COMP       - COMPANY NUMBER - BLANK FOR ALL            */
/*          &FDATE      - FROM DATE                                 */
/*          &TDATE      - TO DATE                                   */
/*          &TYPE       - SALES TYPE                                */
/*          &GRUP       - SALES GROUP                               */
/*          &CLSS       - SALES CLASS                               */
/*          &DTLSUM     - DETAIL OR SUMMARY                         */
/*          &EXPSLS     - EXPORT SALES                              */
/*          &FLAG1      - INCLUDE ALL PRODUCTS                      */
/*          &FLAG2      - INCLUDE CM/DM                             */
/********************************************************************/
/*  JRB DLY029 06/01/07 Call PMD4XFR to set the dfault Company Alpha*/
/********************************************************************/

 OMMPPCLP:   PGM

        DCL    &COMP      *DEC    (3 0)           /*  COMPANY #     */
        DCL    &FDATE     *DEC    (7 0)          /*  FROM DATE      */
        DCL    &TDATE     *DEC    (7 0)          /*  THROUGH DATE   */
        DCL    &COPIES    *DEC    (3 0)           /*  # OF COPIES   */
        DCL    &TYPE      *DEC    (3 0)          /*  SALES TYPE     */
        DCL    &GRUP      *DEC    (3 0)          /*  SALES GROUP    */
        DCL    &CLSS      *DEC    (3 0)          /*  SALES CLASS    */
        DCL    &OUTQ      *CHAR   10             /*  OUTPUT QUEUE   */
        DCL    &NIGHTQ    *CHAR   10             /*  NIGHT JOB QUEUE*/
        DCL    &SAVE      *CHAR   4              /*  SAVE SPOOL     */
        DCL    &HOLD      *CHAR   4              /*  HOLD SPOOL     */
        DCL    &PGM       *CHAR   10              /*  PGM NAME      */
        DCL    &COMPA     *CHAR   3               /*  COMPANY ALPHA */
        DCL    &FDATEA    *CHAR   7               /*  FROM DT ALPHA */
        DCL    &TDATEA    *CHAR   7               /*  TO   DT ALPHA */
        DCL    &DTLSUM    *CHAR   1               /*  DETAIL/SUMMARY*/
        DCL    &EXPSLS    *CHAR   1               /*  EXPORT SALES  */
        DCL    &FLAG1     *CHAR   1               /*  INCL. CM/DM   */
        DCL    &FLAG2     *CHAR   1               /*  INCL. ALL PRD */
        DCL    &SALRT     *CHAR   3
        DCL    &COMMAND    *CHAR   (512)

/* SEND PROGRAM MESSAGE PARAMETERS  */

        DCL    &USER      *CHAR   10              /*  USER ID       */
        DCL    &MSGRTN    *CHAR   10              /*  MSG QUEUE     */
        DCL    &PGMO      *CHAR   10              /*  PRINTER FILE  */
        DCL     &A        *CHAR   20              /* QRY FROM DATE  */
        DCL     &B        *CHAR   25              /* QRY TO DATE    */
        DCL     &C        *CHAR   25              /* QRY COMPANY    */
        DCL     &D        *CHAR   25              /* QRY EXPORT     */
        DCL     &E        *CHAR   95              /* QRY ALL        */
        DCL     &DATA     *CHAR   10              /* DATA FILES     */
        DCL     &DATAH    *CHAR   10              /* HIST FILES     */

/* SETUP LIBRARY LIST AND PARAMETERS */

             CALL      SETPRK

             CHGVAR    VAR(&PGM) VALUE('OMNPPFR')
             CHGVAR    VAR(&DTLSUM) VALUE('D')
             CHGVAR    VAR(&EXPSLS) VALUE('A')
             CHGVAR    VAR(&FLAG1) VALUE('N')
             CHGVAR    VAR(&FLAG2) VALUE('Y')
             CHGVAR    VAR(&COPIES) VALUE(1)
             CHGVAR    VAR(&OUTQ) VALUE('PRKPRT06')
             CHGVAR    VAR(&HOLD) VALUE('*NO')
             CHGVAR    VAR(&SAVE) VALUE('*NO')

/* CALL PGM TO RTV 2 PRIOR SUNDAY AND PRIOR SATURDAY DATE FROM UDATE */
             CALL       PGM(PDVMXFR) PARM('' &FDATE &TDATE)
/* CALL PGM TO RTV DEFAULT COMPANY NUMBER */
             CALL       PGM(PMD4XFR) PARM('' &COMPA)

             CHGVAR    VAR(&FDATEA) VALUE(&FDATE) /* Change to alpha +
                          for query data */
             CHGVAR    VAR(&TDATEA) VALUE(&TDATE) /* Change to alpha +
                          for query data */

/********************************************************************/
/* APPEND $ TO THE PROGRAM NAME                                     */
/********************************************************************/

             CHGVAR     VAR(&PGMO) VALUE(&PGM *TCAT '$')

/*********************************************************************/
/*   CREATE OPEN QUERY FILE TO BE USED FOR AREA/BRK WORK FILE BUILD  */
/*********************************************************************/

             CHGVAR     VAR(&A) VALUE('F4GNDT *GE ' *CAT +
                         &FDATEA)

             CHGVAR     VAR(&B) VALUE('*AND F4GNDT *LE ' *CAT +
                         &TDATEA)

             CHGVAR     VAR(&C) VALUE('*AND F4AIC3 *EQ ' *CAT +
                         &COMPA)

             IF         COND(&EXPSLS *EQ 'N') THEN(DO)
             CHGVAR     VAR(&D) VALUE('*AND F4U2ST *NE ' *CAT +
                         '"Y"')
             ENDDO
             IF         COND(&EXPSLS *EQ 'E') THEN(DO)
             CHGVAR     VAR(&D) VALUE('*AND F4U2ST *EQ ' *CAT +
                         '"Y"')
             ENDDO


             CHGVAR     VAR(&E) VALUE(&A *BCAT &B *BCAT &C *BCAT &D)

             OVRDBF     FILE(OMHSTPLC) SHARE(*YES)

             OPNQRYF    FILE((OMHSTPLC)) OPTION(*ALL) +
                          QRYSLT(&E) KEYFLD(*FILE)

/********************************************************************/
/* Override printer file with user specified values                 */
/********************************************************************/

/*  THIS USED TO SPECIFY DRAWER(2)                                */
             SBDPRTOVR  CMDVAR(&COMMAND) FILE(&PGMO) OUTQ(&OUTQ) +
                          COPIES(&COPIES) HOLD(&HOLD) SAVE(&SAVE)

             CALL       PGM(QCMDEXC) PARM(&COMMAND 512)
/*********************************************************************/
/*   CALL LISTING PROGRAM                                            */
/*********************************************************************/

             CALL       PGM(&PGM) PARM('' &FDATE &TDATE &TYPE &GRUP +
                          &CLSS &FLAG1 &FLAG2 &DTLSUM &EXPSLS &SALRT)

             CLOF       OPNID(OMHSTPLC)
             DLTOVR     FILE(OMHSTPLC)

 END:        ENDPGM
