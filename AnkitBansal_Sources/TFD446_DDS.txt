     A*%%TS  SD  20080827  094045  ISLRAMS     REL-V5R4M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        TRIUMPH FOODS
     A*  DISPLAY FILE:  TFD446
     A*  TITLE:         SPECIFY MARGIN ADJUSTMENT RECAP REPORT
     A*  PROGRAMMER:    LEANNE RAMSEY
     A*  CREATED:       12/11/06
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/TFPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A                                      CF08(08 'SUBMIT')
     A                                      CF17(17 'PRINT OPTIONS')
     A                                      CF20(20 'WRKSPLF')
     A*
     A*---------------------------------------------------------------
     A* RECORD FORMAT ONE
     A*---------------------------------------------------------------
     A          R TF446R1
     A*%%TS  SD  20070705  155355  ISLRAMS     REL-V5R4M0  5722-WDS
     A                                      SETOF(30)
     A                                      SETOF(31)
     A                                      OVERLAY
     A                                      RTNCSRLOC(&RCD &FLD)
     A            RCD       R        H      REFFLD(@@RCD)
     A            FLD       R        H      REFFLD(@@FLD)
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 15'SPECIFY OPTIONS FOR MARGIN ADJUSTM-
     A                                      ENT RECAP REPORT'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USR)
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@JOB)
     A                                      DSPATR(HI)
     A                                  4 28'Week-ending date'
     A                                      DSPATR(UL)
     A                                      COLOR(BLU)
     A                                  4 48'Year'
     A                                      DSPATR(UL)
     A                                      COLOR(BLU)
     A                                  4 55'Week'
     A                                      DSPATR(UL)
     A                                      COLOR(BLU)
     A                                  4 62'Period'
     A                                      DSPATR(UL)
     A                                      COLOR(BLU)
     A                                  5  2'From date ..'
     A            D1FMDY    R        B  5 15REFFLD(TFREF/@@DT06 *LIBL/TFPREF)
     A                                      EDTWRD('  /  /  -')
     A  30                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A N30                                  DSPATR(HI)
     A            D1FWEMDY  R        O  5 32REFFLD(TFREF/@@DT06 *LIBL/TFPREF)
     A                                      EDTWRD('  /  /  ')
     A            D1FYR     R        O  5 48REFFLD(TFREF/@@YR *LIBL/TFPREF)
     A                                      EDTCDE(Z)
     A            D1FWK     R        O  5 56REFFLD(TFREF/@@WK *LIBL/TFPREF)
     A                                      EDTCDE(Z)
     A            D1FPE     R        O  5 64REFFLD(TFREF/@@PE *LIBL/TFPREF)
     A                                      EDTCDE(Z)
     A                                  6  2'To date ....'
     A            D1TMDY    R        B  6 15REFFLD(TFREF/@@DT06 *LIBL/TFPREF)
     A                                      EDTWRD('  /  /  -')
     A  31                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A N31                                  DSPATR(HI)
     A            D1TWEMDY  R        O  6 32REFFLD(TFREF/@@DT06 *LIBL/TFPREF)
     A                                      EDTWRD('  /  /  ')
     A            D1TYR     R        O  6 48REFFLD(TFREF/@@YR *LIBL/TFPREF)
     A                                      EDTCDE(Z)
     A            D1TWK     R        O  6 56REFFLD(TFREF/@@WK *LIBL/TFPREF)
     A                                      EDTCDE(Z)
     A            D1TPE     R        O  6 64REFFLD(TFREF/@@PE *LIBL/TFPREF)
     A                                      EDTCDE(Z)
     A*
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R TF446K1
     A*%%TS  SD  20080827  094045  ISLRAMS     REL-V5R4M0  5722-WDS
     A  43                             21  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A                                 23 11'F8=Submit'
     A                                 23 22'F17=Print Options'
     A                                 23 41'F20=Spool Files'
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *---------------------------------------------------------------
     A          R TF446ES                   SFL SFLMSGRCD(24)
      *
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
     A*---------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT
     A*---------------------------------------------------------------
     A*
     A          R TF446EC                   SFLCTL(TF446ES)
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
