/***************************************************************** */
/*                                                                 */
/*  SYSTEM:          TRIUMPH FOODS                                 */
/*  PROGRAM NUMBER:  TF476CL                                       */
/*  PROGRAM NAME:    CONVERTING % REPORT                           */
/*  PROGRAMMER:      LEANNE RAMSEY                                 */
/*  CREATE DATE:     12/03/08                                      */
/*                                                                 */
/*                                                                 */
/*******************************************************************/
/* MODIFICATIONS                                                   */
/*                                                                 */
/* 03/06/09  LeAnne Ramsey                                         */
/*           Up until now, "476" stuff included this Converting    */
/*           % Report and the Weekly Yield Reports.                */
/*           We are now splitting them apart. SO, "476" will now   */
/*           be just the Converting % Report; the Weekly Yield     */
/*           stuff is now "477" screen/CL.                         */
/*                                                                 */
/* 05/06/09  LeAnne Ramsey                                         */
/*           Changed print logic to match Meat Costing.            */
/*******************************************************************/

             PGM        PARM(&CLPARM)

             DCL        VAR(&CLPARM)  TYPE(*CHAR) LEN(1)
             DCL        VAR(&QDATA)   TYPE(*CHAR) LEN(113)

             DCL        VAR(&OUTQ) TYPE(*CHAR) LEN(10)
             DCL        VAR(&HOLD) TYPE(*CHAR) LEN(4)
             DCL        VAR(&SAVE) TYPE(*CHAR) LEN(4)
             DCL        VAR(&COPY) TYPE(*DEC)  LEN(1)

             DCL        VAR(&LDFWEDT) TYPE(*DEC) LEN(8)
             DCL        VAR(&LDTWEDT) TYPE(*DEC) LEN(8)

             DCL        VAR(&LDISTYCD) TYPE(*DEC)  LEN(3)
             DCL        VAR(&LDTFCLCD) TYPE(*CHAR) LEN(3)
             DCL        VAR(&LDTFCGCD) TYPE(*CHAR) LEN(2)

             CHGVAR     VAR(&LDFWEDT) VALUE(%SST(*LDA 85 8))
             CHGVAR     VAR(&LDTWEDT) VALUE(%SST(*LDA 99 8))

/*----------------------------------------------------------------*/
/* SET PARAMETERS FROM LDA FOR LISTINGS                           */
/*----------------------------------------------------------------*/

 OUTQ:       CHGVAR     VAR(&OUTQ) VALUE(%SST(*LDA 401 10))
             IF         COND(&OUTQ = '          ') THEN(CHGVAR +
                          VAR(&OUTQ) VALUE(*JOB))

 HOLD:       CHGVAR     VAR(&HOLD) VALUE(%SST(*LDA 411 4))
             IF         COND(&HOLD *NE '*YES' *AND &HOLD *NE '*NO ') +
                          THEN(CHGVAR VAR(&HOLD) VALUE(*NO))

 SAVE:       CHGVAR     VAR(&SAVE) VALUE(%SST(*LDA 415 4))
             IF         COND(&SAVE *NE '*YES' *AND &SAVE *NE '*NO ') +
                          THEN(CHGVAR VAR(&SAVE) VALUE(*NO))

 COPY:       CHGVAR     VAR(&COPY) VALUE(%SST(*LDA 419 1))
             IF         COND(&COPY = 0) THEN(CHGVAR VAR(&COPY) +
                          VALUE(1))

             OVRPRTF    FILE(PRINT198) OUTQ(&OUTQ) COPIES(&COPY) +
                          HOLD(&HOLD) SAVE(&SAVE)

/*---------------------------------------------------------------------*/
/* CONVERTING % REPORT                                                 */
/*---------------------------------------------------------------------*/

/* DATE RANGE                                                          */

             CHGVAR     VAR(&QDATA) VALUE(' ')

             CHGVAR     VAR(%SST(&QDATA 1 10)) VALUE('VWWEDT *GE')
             CHGVAR     VAR(%SST(&QDATA 12 8)) VALUE(&LDFWEDT)

             CHGVAR     VAR(%SST(&QDATA 21 15)) VALUE('*AND VWWEDT *LE')
             CHGVAR     VAR(%SST(&QDATA 37 8)) VALUE(&LDTWEDT)


/* ITEM STRUCTURE TYPE                                           */

             IF         COND(&LDISTYCD *NE 0) THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 46 17)) VALUE('*AND VWISTYCD +
                          *EQ')
             CHGVAR     VAR(%SST(&QDATA 64 3)) VALUE(&LDISTYCD)
             ENDDO

/*  TF CLASS GROUP                                            */

             IF         COND(&LDTFCGCD *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 68 19)) VALUE('*AND VWTFCGCD +
                          *EQ "')
             CHGVAR     VAR(%SST(&QDATA 87 2)) VALUE(&LDTFCGCD)
             CHGVAR     VAR(%SST(&QDATA 89 1)) VALUE('"')
             ENDDO

/* TF CLASSIFICATION                                         */

             IF         COND(&LDTFCLCD *NE ' ') THEN(DO)
             CHGVAR     VAR(%SST(&QDATA 91 19)) VALUE('*AND VWTFCLCD +
                          *EQ "')
             CHGVAR     VAR(%SST(&QDATA 110 3)) VALUE(&LDTFCLCD)
             CHGVAR     VAR(%SST(&QDATA 113 1)) VALUE('"')
             ENDDO

             OVRDBF     FILE(TFL018C) SHARE(*YES)
             OPNQRYF    FILE((TFL018C)) QRYSLT(&QDATA) KEYFLD(*FILE) +
                          SEQONLY(*YES)

             CALL       PGM(TF676) PARM(&CLPARM) /* Converting % Report */

             CLOF       OPNID(TFL018C)
             DLTOVR     FILE(TFL018C)

             ENDPGM

