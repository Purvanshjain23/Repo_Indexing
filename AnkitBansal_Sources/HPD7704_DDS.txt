     A*%%TS  SD  20040322  133554  ISLRAMS     REL-V5R2M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION SYSTEM
     A*  DISPLAY FILE:  HPD7704
     A*  TITLE:         VIEW BUDGET ITEM ON TEMPLATES
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  DATE:          02/09/04
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CA03(03 'EXIT')
     A                                      CF04(04 'PROMPT')
     A                                      CF05(05 'REFRESH')
     A*
     A*
     A*---------------------------------------------------------------
     A* SUBFILE FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP7704S1                  SFL
     A*%%TS  SD  20040202  141408  ISLRAMS     REL-V5R2M0  5722-WDS
     A  85                                  SFLNXTCHG
     A            HDRRN1         4Y 0H
     A            D1BTQT    R        O 12 28REFFLD(HSRREF/@@BTQT *LIBL/HSPREF)
     A                                      EDTCDE(K)
     A            D1BTCD    R        O 12 44REFFLD(@@BTCD)
     A            D1BTDS    R        O 12 51REFFLD(HSRREF/@@BTDS *LIBL/HSPREF)
     A            D1AIST    R        O 12 78REFFLD(HSRREF/@@AIST *LIBL/HSPREF)
     A*
     A*
     A*---------------------------------------------------------------
     A* SUBFILE CONTROL FORMAT ONE
     A*---------------------------------------------------------------
     A          R HP7704C1                  SFLCTL(HP7704S1)
     A*%%TS  SD  20040322  133554  ISLRAMS     REL-V5R2M0  5722-WDS
     A                                      SFLSIZ(0050)
     A                                      SFLPAG(0010)
     A N82                                  ROLLUP(25 'PAGE FORWARD')
     A                                      SETOF(52)
     A                                      SETOF(53)
     A                                      RTNCSRLOC(&RCD &FLD)
     A                                      OVERLAY
     A  81                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  82                                  SFLEND
     A            HDPAG1         4S 0H      SFLRCDNBR
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  2REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 26'HOG PRODUCTION SYSTEM'
     A                                      DSPATR(HI)
     A            SDUSR     R        O  1 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  2 22'VIEW BUDGET ITEM ON TEMPLATES'
     A                                      DSPATR(HI)
     A            SDJOB     R        O  2 70REFFLD(@@USCD)
     A                                      DSPATR(HI)
     A                                  4  2'Budget item:'
     A                                      DSPATR(HI)
     A            C1BGIT    R        O  4 15REFFLD(HSRREF/@@BGIT *LIBL/HSPREF)
     A                                  5  2'Description:'
     A                                      DSPATR(HI)
     A            C1DESC    R        O  5 15REFFLD(HSRREF/@@ITDS *LIBL/HSPREF)
     A                                  6  2'UOM .......:'
     A                                      DSPATR(HI)
     A            C1UOM     R        O  6 15REFFLD(HSRREF/@@UOM *LIBL/HSPREF)
     A                                  7  2'UOW .......:'
     A                                      DSPATR(HI)
     A            C1UOW     R        O  7 15REFFLD(HSRREF/@@UOW *LIBL/HSPREF)
     A                                  8 46'?'
     A                                      DSPATR(HI)
     A                                  8 78'?'
     A                                      DSPATR(HI)
     A                                  9 28'------------------ Budget Template-
     A                                       -----------------'
     A                                      DSPATR(HI)
     A                                 10 34'Quantity'
     A                                      DSPATR(HI)
     A                                 10 44'Code'
     A                                      DSPATR(HI)
     A                                 10 51'Description'
     A                                      DSPATR(HI)
     A                                 10 77'A/I'
     A                                      DSPATR(HI)
     A            C1BTCD    R        B 11 44REFFLD(@@BTCD)
     A                                      DSPATR(HI)
     A  52                                  DSPATR(PC)
     A            C1BTDS    R        B 11 51REFFLD(HSRREF/@@BTDS *LIBL/HSPREF)
     A                                      CHGINPDFT(UL LC FE)
     A                                      DSPATR(HI)
     A            C1AIST    R        B 11 78REFFLD(HSRREF/@@AIST *LIBL/HSPREF)
     A                                      DSPATR(HI)
     A  53                                  DSPATR(PC)
     A*
     A*---------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*---------------------------------------------------------------
     A          R HP7704K1
     A  43                             22  2'Message program failed.  Call MIS -
     A                                      user support.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                 23  2'F3=Exit'
     A                                 23 11'F4=Prompt'
     A                                 23 22'F5=Refresh'
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *---------------------------------------------------------------
     A          R HP7704ES                  SFL SFLMSGRCD(24)
      *
     A                                      TEXT('Error message subfile')
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
      *
      *---------------------------------------------------------------
      * ERROR MESSAGE SUBFILE CONTROL FORMAT
      *---------------------------------------------------------------
     A          R HP7704EC                  SFLCTL(HP7704ES)
      *
     A                                      TEXT('Error message subfile')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            MSGPGM                    SFLPGMQ
