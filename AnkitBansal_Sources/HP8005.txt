      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      Hog Production System
      * PROGRAM:     HP8005 - Retrieve Day of Week Name and Abbreviation
      * PROGRAMMER:  LeAnne Fedor
      * CREATED:     01/28/02
      *
      * Function:  This function receives a date in the format CCYYMMDD and returns:
      *                1) the name of the day
      *                2) the abbreviation of the name of the day
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      /eject
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
      *
      ****************************************************************
      * DEFINITION SPECIFICATIONS
      ****************************************************************
      *
      *
      ****************************************************************
      * STANDALONE FIELDS
      ****************************************************************
      *
      * Workfields
      *
     D wkfld           s             15  0
     D wkdiff          s             15  0
     D wkrmdr          s              1  0
     D wkiso           s               D   datfmt(*iso)
     D wkisodec7       s               D   inz(D'1941-12-07')
      *
      *
      * Parm fields
      *
     D xxccyymmdd      s              8  0
     D xxname          s              9
     D xxabbrev        s              4
      *
      *
      ****************************************************************
      * DATA STRUCTURES
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standard program status data structure
      *---------------------------------------------------------------
      *    externally defined as UTPGFR (record format: PGMDSR)
     D pgmds         esds                  extname(utpgfr)
      *
      *
      * The following is a dummy C-spec to keep the D-specs from
      * running over into the C-specs on the compile listing. It
      * serves no other purpose and can be deleted at any time.
      *
     C                   if        sdpgm = *blank
     C                   endif
      /EJECT
      ****************************************************************
      * Mainline
      ****************************************************************
      *
      * Move your incoming date to a date format field
      *
     C                   movel     xxccyymmdd    wkiso
      *
      *
      *  Subtract December 7, 1942, from your incoming date...getting the
      *  difference in days.
      *
     C     wkiso         subdur    wkisodec7     wkdiff:*d
      *
      *
      * Divide the difference calculated above by 7...the remainder will be the
      * day of the week.
      *
     C     wkdiff        div       7             wkfld
     C                   mvr                     wkrmdr
      *
      *
      * Hardcode the name/abbreviation of the day to pass back to the calling program.
      *
     C                   select
     C                   when      wkrmdr = 0
     C                   eval      xxname = 'Sunday'
     C                   eval      xxabbrev = 'Sun'
      *
     C                   when      wkrmdr = 1
     C                   eval      xxname = 'Monday'
     C                   eval      xxabbrev = 'Mon'
      *
     C                   when      wkrmdr = 2
     C                   eval      xxname = 'Tuesday'
     C                   eval      xxabbrev = 'Tues'
      *
     C                   when      wkrmdr = 3
     C                   eval      xxname = 'Wednesday'
     C                   eval      xxabbrev = 'Wed'
      *
     C                   when      wkrmdr = 4
     C                   eval      xxname = 'Thursday'
     C                   eval      xxabbrev = 'Thur'
      *
     C                   when      wkrmdr = 5
     C                   eval      xxname = 'Friday'
     C                   eval      xxabbrev = 'Fri'
      *
     C                   when      wkrmdr = 6
     C                   eval      xxname = 'Saturday'
     C                   eval      xxabbrev = 'Sat'
     C                   endsl
      *
     C                   seton                                        lr
      /eject
      *---------------------------------------------------------------
      * Initialization subroutine
      *---------------------------------------------------------------
      *
     C     *inzsr        begsr
      *
     C     *entry        plist
     C                   parm                    xxccyymmdd
     C                   parm                    xxname
     C                   parm                    xxabbrev
      *
     C                   endsr
      /eject
