     A*%%TS  SD  20030624  153600  ISLRAMS     REL-V5R2M0  5722-WDS
     A*****************************************************************
     A*  SYSTEM:        HOG PRODUCTION
     A*  DISPLAY FILE:  HPD4410
     A*  TITLE:         SPECIFY EXPENSE LISTING OPTIONS
     A*  PROGRAMMER:    LEANNE FEDOR
     A*  DATE:          06/24/03
     A*
     A*  NOTE:  THIS IS A POP-UP WINDOW. THE WINDOW DEFINITION IS IN
     A*         RECORD FORMAT HP4410EC
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/HSPREF)
     A                                      CHGINPDFT(UL FE)
     A                                      PRINT
     A                                      CF03(03 'EXIT')
     A                                      CF04(04 'PROMPT')
     A                                      CF08(08 'SUBMIT')
     A*
     A*----------------------------------------------------------------
     A* RECORD FORMAT ONE
     A*----------------------------------------------------------------
     A          R HP4410R1
     A*%%TS  SD  20030624  153600  ISLRAMS     REL-V5R2M0  5722-WDS
     A                                      WINDOW(HP4410EC)
     A                                      TEXT('INPUT SCREEN')
     A                                      SETOF(37)
     A                                      SETOF(57)
     A                                      OVERLAY
     A                                      RTNCSRLOC(&RCD &FLD)
     A            RCD           10A  H
     A            FLD           10A  H
     A            SDPGM     R        O  1  1REFFLD(@@PGM)
     A                                      DSPATR(HI)
     A                                  1 14'SPECIFY EXPENSE LISTING OPTIONS'
     A                                      DSPATR(HI)
     A                                  3  1'Listing ...'
     A                                  3 13'?'
     A            D1LISTCD  R        B  3 15REFFLD(HSRREF/@@FLAG *LIBL/HSPREF)
     A  37
     AO 57                                  DSPATR(PC)
     A  37                                  DSPATR(RI)
     A N37                                  DSPATR(HI)
     A            D1LISTDS      15A  O  3 18
     A*
     A*----------------------------------------------------------------
     A* COMMAND KEY RECORD 1
     A*----------------------------------------------------------------
     A          R HP4410K1
     A*%%TS  SD  20020131  142842  ISLRAMS     REL-V4R4M0  5769-PW1
     A                                      WINDOW(HP4410EC)
     A                                      TEXT('COMMAND KEYS')
     A                                      OVERLAY
     A  43                              7  2'Message program failed. Call IS.'
     A  43                                  DSPATR(HI)
     A N43                                  DSPATR(ND)
     A                                  8  1'F3=Exit'
     A                                  8 10'F4=Prompt'
     A                                  8 21'F8=Submit'
      *
      *----------------------------------------------------------------
      * ERROR MESSAGE SUBFILE FORMAT
      *----------------------------------------------------------------
     A          R HP4410ES
     A                                      TEXT('Error message subfile')
     A                                      SFL SFLMSGRCD(9)
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGM                    SFLPGMQ
     A*
     A*----------------------------------------------------------------
     A* ERROR MESSAGE SUBFILE CONTROL FORMAT WITH WINDOW DEFINITION
     A*----------------------------------------------------------------
     A          R HP4410EC                  SFLCTL(HP4410ES)
     A*%%TS  SD  20000628  103649  ISLRAMS     REL-V4R2M0  5769-PW1
     A                                      TEXT('Error message subfile')
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N86                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A                                      WINDOW(*DFT 10 50)
     A                                      WDWBORDER((*DSPATR RI) (*CHAR '    -
     A                                          '))
     A            MSGPGM                    SFLPGMQ(10)
     A*
     A*----------------------------------------------------------------
     A* ASSUME FORMAT - REQUIRED FOR WINDOWS
     A*----------------------------------------------------------------
     A*
     A          R ASSUME
     A*%%TS  SD  20000627  095625  ISLRAMS     REL-V4R2M0  5769-PW1
     A                                      TEXT('ASSUME FORMAT FOR WINDOWS')
     A                                      ASSUME
     A                                      OVERLAY
     A                                 24 79' '
