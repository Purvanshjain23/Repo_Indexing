/********************************************************************/
/*                                                                  */
/*  SYNOPSIS - PRINT ORDER FILL DETAIL FROM HISTORY FILE.           */
/*                                                                  */
/*                                                                  */
/*                                                                  */
/*  COMPANY   - SEABOARD COPRORATION                                */
/*  SYSTEM    - ORDER MANAGEMENT                                    */
/*  USER NAME - SUSAN MASON (CLONE FROM OMNMPCLP)                   */
/*  DATE      - 02/21/05                                            */
/*                                                                  */
/*      PARAMETERS RECEIVED:                                        */
/*                                                                  */
/*          &OUTQ       - OUTPUT QUEUE                              */
/*          &COPEIS     - NUMBER OF COPIES                          */
/*          &NIGHTQ     - NIGHT QUEUE                               */
/*          &HOLD       - HOLD PRINT FLAG                           */
/*          &SAVE       - SAVE PRINT FLAG                           */
/*          &PGM        - PROGRAM NAME                              */
/*          &COMPL      - COMPANY NUMBER                            */
/*          &FDATEL     - FROM DATE                                 */
/*          &TDATEL     - TO DATE                                   */
/*          &FLAG       - EXCEPT FLAG                               */
/*          &SHIP TO    - SHIP TO                                   */
/********************************************************************/
/********************************************************************/

 OMNMPCLP:   PGM        PARM(&PGM &COMPL &FDATEL &TDATEL &SHIPTOL &FLAG +
          &STRTYP   &OUTQ &HOLD &SAVE &COPIESL &NIGHTQ)

        DCL    &COMP      *DEC    (3 0)           /*  COMPANY #     */
        DCL    &COMPL     *DEC                    /*  FOR PASSING   */
        DCL    &FDATE     *DEC    (7 0)           /*  FROM DATE     */
        DCL    &FDATEL    *DEC                    /*  FOR PASSING   */
        DCL    &TDATE     *DEC    (7 0)           /*  THROUGH DATE  */
        DCL    &TDATEL    *DEC                    /*  FOR PASSING   */
        DCL    &COPIES    *DEC    (3 0)           /*  # OF COPIES   */
        DCL    &COPIESL   *DEC                    /*  FOR PASSING   */
        DCL    &SHIPTO    *DEC    (7 0)           /*  SHIP TO       */
        DCL    &SHIPTOL   *DEC                    /*  FOR PASSING   */
        DCL    &OUTQ      *CHAR   10              /*  OUTPUT QUEUE  */
        DCL    &NIGHTQ    *CHAR   10              /*  NIGHT JOB QUE.*/
        DCL    &HOLD       *CHAR   (4)            /* Hold Status    */
        DCL    &SAVE       *CHAR   (4)            /* Save Status    */
        DCL    &PGM       *CHAR   10              /*  PGM NAME      */
        DCL    &COMPA     *CHAR   3               /*  COMPANY ALPHA */
        DCL    &TDATEA    *CHAR   7               /*  TO   DT ALPHA */
        DCL    &FDATEA    *CHAR   7               /*  TO   DT ALPHA */
        DCL    &SHIPTOA   *CHAR   7               /*  SHIP TO ALPHA */
        DCL    &FLAG       *CHAR   (1)            /* EXCEPT FLAG    */
        DCL    &STRTYP     *CHAR   (3)            /* ITEM STR TYPE  */
        DCL    &COMMAND    *CHAR   (512)

/* SEND PROGRAM MESSAGE PARAMETERS  */

        DCL    &PGMO      *CHAR   10              /*  PRINTER FILE  */
        DCL     &A        *CHAR   20              /* QRY YTD DATE   */
        DCL     &B        *CHAR   25              /* QRY TO DATE    */
        DCL     &C        *CHAR   25              /* QRY COMPANY    */
        DCL     &E        *CHAR   25              /* QRY SHIP TO    */
        DCL     &D        *CHAR   100             /* QRY ALL        */
        DCL     &DATA     *CHAR   10              /* DATA FILES     */
        DCL     &DATAH    *CHAR   10              /* HIST FILES     */

             CHGVAR    VAR(&COMP) VALUE(&COMPL) /* Change to length +
                          for print program */
             CHGVAR    VAR(&COMPA) VALUE(&COMP) /* Change to alpha +
                          for message data */
             CHGVAR    VAR(&FDATE) VALUE(&FDATEL) /* Change to length +
                          for print program */
             CHGVAR    VAR(&FDATEA) VALUE(&FDATE) /* Change to alpha +
                          for query data */
             CHGVAR    VAR(&TDATE) VALUE(&TDATEL) /* Change to length +
                          for print program */
             CHGVAR    VAR(&TDATEA) VALUE(&TDATE) /* Change to alpha +
                          for query data */
             CHGVAR    VAR(&COPIES) VALUE(&COPIESL) /* change to +
                          length for print program */
             CHGVAR    VAR(&SHIPTO) VALUE(&SHIPTOL) /* Change to length +
                          for print program */
             CHGVAR    VAR(&SHIPTOA) VALUE(&SHIPTO) /* Change to alpha +
                          for query data */

/********************************************************************/
/* APPEND $ TO THE PROGRAM NAME                                     */
/********************************************************************/

             CHGVAR     VAR(&PGMO) VALUE(&PGM *TCAT '$')

/*********************************************************************/
/*   CREATE OPEN QUERY FILE TO BE USED FOR REPORT                    */
/*********************************************************************/

             CHGVAR     VAR(&A) VALUE('F4GNDT *GE ' *CAT +
                         &FDATEA)

             CHGVAR     VAR(&B) VALUE('*AND F4GNDT *LE ' *CAT +
                         &TDATEA)

             CHGVAR     VAR(&C) VALUE('*AND F4AIC3 *EQ ' *CAT +
                         &COMPA)

             IF         COND(&SHIPTOA *NE '0000000') THEN(DO)
             CHGVAR     VAR(&E) VALUE('*AND F4BKC7 *EQ ' *CAT +
                         &SHIPTOA)
             CHGVAR     VAR(&D) VALUE(&A *BCAT &B *BCAT &C *BCAT &E)
             ENDDO
             ELSE       CMD(CHGVAR VAR(&D) VALUE(&A *BCAT &B *BCAT &C))

/*********************************************************************/
/*   SELECT QUERY BY REPORT BEING RUN                                */
/*********************************************************************/

             OVRDBF     FILE(OMHSTPLC) SHARE(*YES)
             OPNQRYF    FILE(OMHSTPLC) +
                        QRYSLT(&D) +
                        KEYFLD(*FILE)

/********************************************************************/
/* Override printer file with user specified values                 */
/********************************************************************/

             SBDPRTOVR  CMDVAR(&COMMAND) FILE(&PGMO) OUTQ(&OUTQ) +
                          COPIES(&COPIES) HOLD(&HOLD) +
                          SAVE(&SAVE)

             CALL       PGM(QCMDEXC) PARM(&COMMAND 512)

/*********************************************************************/
/*   CALL LISTING PROGRAM                                            */
/*********************************************************************/

    CALL      PGM(&PGM) PARM('' &FDATE &TDATE &FLAG &STRTYP &SHIPTO)

             CLOF       OPNID(OMHSTPLC)
             DLTOVR     FILE(OMHSTPLC)


 END:        ENDPGM
