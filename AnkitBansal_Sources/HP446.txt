      *****************  RPG PROGRAM HEADING  ************************
      *
      * SYSTEM:      HOG PRODUCTION
      * PROGRAM:     HP446
      * TITLE:       WORK WITH SALES
      * PROGRAMMER:  LEANNE FEDOR
      * CREATED:     06/08/95
      *
      * FUNCTION: THIS IS A WORK WITH THAT DISPLAYS THE SALES
      *           MOVEMENTS.
      *
      *           RESTRICTERS:
      *               CURRENT MOVEMENTS ONLY FLAG
      *
      *           POSITIONERS:
      *               MOVEMENT NUMBER
      *
      *           SELECTORS:
      *               MOVEMENT STATUS
      *               SCHEDULED SHIP DATE/SHIP DATE
      *               SCHEDULED KILL DATE
      *               FARM SITE
      *               BUILDING
      *               GROUP
      *               PHASE
      *               SALES TYPE
      *               CUSTOMER
      *
      *           IT HAS F4-PROMPT LOGIC.
      *           IT LOADS THE SUBFILE A PAGE AT A TIME.
      *           IT USES THE STANDARD MESSAGE HANDLING PROGRAM.
      *           IT HAS STANDARD SUBFILE OPTION EDITING.
      *
      *
      * IMPORTANT:
      *           As of February 2003, we have 2 versions of this program. The 'version'
      *           that a user gets is controlled by an incoming parm.  We went to this
      *           approach because there will now be WEB/SYNON/batch edits and posting
      *           into HPS for Seaboard market sales movements.  Alice hopes to force
      *           the Synon users to create/maintain Seaboard market sales thru the WEB
      *           and Synon. But, as a backup, Alice wants the HPS users to be able to
      *           'get the job' done by creating/maintaining thru the HPS functions.
      *
      *           So, the plan is to have the main access into this program 'restricted';
      *           but, there will also be full, unrestricted version. The incoming
      *           parms will control which 'version' a user is getting:
      *           1) R = restricted
      *              The Restricted Version restricts the functions that a user can
      *              perform on the 'Market' sales that are for Seaboard (customer 360516)
      *           2) ' '= full, not restricted
      *              The Full Version has all the functionality that existed prior
      *              to 2003.
      *
      ****************************************************************
      * MODIFICATIONS:
      ****************************************************************
      * DATE      PROGRAMMER
      *
      * 10/17/00  LeAnne Fedor
      *           Added new field 'load type' in sales movement header
      *           file to subfile and as a selector on the screen.
      *
      * 04/25/02  LeAnne Fedor
      *           Added new 'Show current movements only' restrictor field. (We hope
      *           this will allow the users to access data more quickly.)
      *
      * 12/17/02  LeAnne Fedor
      *           Removed Production Phase/Production Type from the Sales Movement
      *           Header.
      *
      * 02/27/03  LeAnne Fedor
      *           Added logic to have 2 versions:  restricted, not restricted
      *           Removed Business Office from screen.
      *           Added Scheduled Kill Date to screen.
      *
      * 12/13/04  LeAnne Fedor
      *           Per AMB, we want to call the new HP4070 with a reprint value of NO. Thus,
      *           thru this Work With, you will only be allowed to print BOLs that have
      *           never been printed before. If you wish to reprint a movement, you
      *           will have to do it thru the Reprint Menu Option.
      *
      * 10/08/08  LeAnne Ramsey
      *           Added a new Option 9=Change Status to FP.
      *
      * 07/06/09  LeAnne Ramsey
      *           Recompile only. Added new field 'Continuous Flow Flag' to Hog Group file.
      *
      * 06/l7/10  LeAnne Ramsey  (E817)
      *           In some scenarios, a Movement is not updated thru Delivery Tracking but does
      *           hit HPE. So, when the HPE-to-HPS interface runs, the Movement is still in a
      *           SC=Scheduled status and the HPS users cannot get to it to change it.
      *           So, I will now open up SC movements to HPS users if the Movement is in the
      *           Tattoo header.  (see $restricted subroutine)
      *
      * 02/15/16  Barb Gutierrez (E5190)
      *           With the new Iowa Plant, we will have a new live office (IFLIV).  We need to
      *           allow user to be able to select movements by live office.
      *
      * 02/27/18  Brad Baden     (E010812 - Market Hogs to OTC)
      *           Data area DACOPY has been changed to contain separate
      *           flags for Guymon 'OKLIV' and Seaboard Triumph Foods
      *           'IFLIV'.  Position 6 in the data area is for Guymon,
      *           and position 14 is for Seaboard Triumph Foods.  If
      *           the appropriate flag is yes, the user cannot create or
      *           update a record.
      * 06/04/18  Brad Baden  E13137 - Update Scheduled Load Plan dates
      *           Pass the Business Office code into the Maintain Sales
      *           program HP146.
      * 06/29/18  Brad Baden  E13476 - Correct Parameter on call to HP146
      *           Change the value of the Business Office parameter on
      *           the call to program HP146 to use the value on the
      *           screen instead of the value from the security routine.
      * 01/30/20  Brad Baden  E16108 - [ME] Delivery Tracking Split for HPS
      *           Change subfile option 7 from Edit Delivery Tracking
      *           to Display Delivery Tracking.
      * 08/03/20  Danny Nguyen    R16686 - Optima BOL Premise ID
      *           Renamed F17 from 'F17=Print BOL' to 'F17=Prt/Email BOL'.
      *           No logic changed.
      * 11/03/20  ISE           (H16853)
      *           Recompile only. Increased Buy order field length from 5 to 7.
      *  2/19/21  JBB           (H16853)
      *           Chnage length of field XXSYNBUY field length from 5 to 7.
      /EJECT
      ****************************************************************
      * FILE SPECIFICATIONS
      ****************************************************************
      *
     FHPD446    CF   E             WORKSTN SFILE(HP446S1:HDRRN1)
     F                                     INFDS(IOFEED)
      *
     FHSP009    IF   E           K DISK
      *    BUSINESS OFFICE
      *
      *
     FHSL034D   IF   E           K DISK
      *    HOG GROUP
      *
      *
     FHSP053    IF   E           K DISK
      *    MOVEMENT STATUS CODES
      *
      *
     FHSJ085A   IF   E           K DISK    INFDS(DBFEED)
      *  SALES DETAIL + HEADER
      *
      *
     FHSJ085F   IF   E           K DISK    RENAME(SHREC:SHRECF)
      *  SALES DETAIL + HEADER
      *
      *
     Fplafcpl2  if   e           k disk
      *  Synon: Delivery Tracking by Co/Buy Order/Load/Kill Date
      *
      *
     Fpka1cpk2  if   e           k disk
      *  Synon: Tattoo header
      *
      /EJECT
      ****************************************************************
      * Definition specs
      ****************************************************************
      *
      *---------------------------------------------------------------
      * Standalone fields
      *---------------------------------------------------------------
      *
     D editfl          s              1
     D dafl            s              1
      *
      * Fields for date manipulation
      *
     D wkmdy           s              6  0
     D wkscdt          s                   like(shscdt)
     D wksckldt        s                   like(shsckldt)
     D wkisodate       s               D   datfmt(*iso)
     D wkccyymmdd      s              8  0
      *
      * Parm fields for SYNON calls
      *
     D xxsynrtn        s              7
     D xxsyncono       s              3  0
     D*xxsynbuy        s              5  0
     D xxsynbuy        s              7  0
     D xxsynload       s              5  0
     D xxsyndt         s              7  0
     D xxsynkldt       s              7  0
     D xxsynsts        s              1
     D xxsynast        s              1
     D xxsyndlvdt      s              7  0
     D xxsyndlvtm      s              6  0
     D xxsyndlvsq      s              3  0
      *
     D xxbocd          s                   like(oubobo)
      *
      *---------------------------------------------------------------
      * STANDARD ERROR MESSAGE HANDLING DATA STRUCTURES
      *---------------------------------------------------------------
      *
      * FOR ERROR MESSAGE HANDLING, A PACKED INDEX (E) IS REQUIRED.
      * FOR PROGRAM READABILITY, DEFINE A CORRESPONDING ERROR COUNT
      * FIELD CALLED 'ERROR'
      *
     D                 DS                  INZ
     D  ERROR                  1      2P 0
     D  E                      1      2P 0
      *
      * THIS DATA STRUCTURE SUPPLIES THE NAME OF THE MESSAGE FILE TO
      * THE MESSAGE HANDLING CL PROGRAM.  THE FIELD NAME MSGFIL MUST BE
      * CONSTANT. THE VALUE IN QUOTES IS THE NAME OF THE SPECIFIC
      * MESSAGE FILE CONTAINING THE USER DEFINED MESSAGES.
      *
     D                 DS                  INZ
     D  MSGFIL                 1     10    INZ('HSMSGF    ')
      *
      * THE FOLLOWING 3 DATA STRUCTURES ARE USED TO SPEED MESSAGE
      * HANDLING SINCE IT IS FASTER TO CLEAR DATA STRUCTURES THAN
      * ARRAYS.  EACH IS ASSOCIATED WITH A STANDARD MESSAGE ARRAY.
      *
     D MGI             DS           140    INZ
     D  MGID                   1    140
     D                                     DIM(20)                              MSG ID ARRAY
      *
     D MGD             DS          1000    INZ
     D  MGDT                   1   1000
     D                                     DIM(20)                              MSG PARMS
      *
     D MGWK            DS            50    INZ
     D  MGW                    1     50
     D                                     DIM(50)                              MSG WORK PARMS
      *
      *---------------------------------------------------------------
      * STANDARD PROGRAM STATUS DATA STRUCTURE
      *---------------------------------------------------------------
      *    EXTERNALLY DEFINED AS UTPGFR (RECORD FORMAT: PGMDSR)
     D PGMDS         ESDS                  EXTNAME(UTPGFR)
      *
      *---------------------------------------------------------------
      * STANDARD WORKSTATION INFORMATION DATA STRUCTURE
      *---------------------------------------------------------------
      *    EXTERNALLY DEFINED AS UTWSFR (RECORD FORMAT: UTIDFRR)
     D IOFEED        E DS                  EXTNAME(UTWSFR)
      *
      *---------------------------------------------------------------
      * STANDARD DATABASE FILE INFORMATION DATA STRUCTURE
      *---------------------------------------------------------------
      *    EXTERNALLY DEFINED AS UTDBFR (RECORD FORMAT: FDBCKD)
     D DBFEED        E DS                  EXTNAME(UTDBFR)
      *
      *---------------------------------------------------------------
      *STANDARD SUBFILE SELECTOR CONVERSION
      *---------------------------------------------------------------
     D                 DS
     D  DFOPT                  1      2
     D  WKOPT1                 1      1
     D  WKOPT2                 2      2
      *
      *---------------------------------------------------------------
      *  DEFINITION FOR EXTERNAL DATA AREA 'DACOPY' WHICH HOLDS
      *  A FLAG VALUE INDICATING WHETHER THE 'COPY' IS CURRENTLY BEING
      *  RUN.
      *---------------------------------------------------------------
     D                 DS
     D  DACOPY                 1     14
     D  DAFLGUY                6      6
     D  DAFLSTP               14     14
      *
      *---------------------------------------------------------------
      *  NAMED CONSTANTS
      *---------------------------------------------------------------
      *
     D CREATE          C                   CONST('CREATE')
     D REVISE          C                   CONST('REVISE')
     D DELETE          C                   CONST('DELETE')
     D VIEW            C                   CONST('VIEW  ')
     D YES             C                   CONST('Y')
     D NO              C                   CONST('N')
     D SET1            C                   CONST('SET1  ')
     D EDIT1           C                   CONST('EDIT1 ')
     D SCRN1           C                   CONST('SCRN1 ')
     D EXIT            C                   CONST('EXIT  ')
     D UPDATE          C                   CONST('UPDATE')
      *
      *
      /EJECT
      ****************************************************************
      * MAINLINE
      ****************************************************************
      *
     C                   MOVEL     SET1          ROUTNE
      *
     C     *INLR         DOWEQ     *OFF
      *
     C     ROUTNE        CASEQ     SET1          $SET1
     C     ROUTNE        CASEQ     SCRN1         $SCRN1
     C     ROUTNE        CASEQ     EDIT1         $EDIT1
     C     ROUTNE        CASEQ     EXIT          $EXIT
     C                   ENDCS
     C                   ENDDO
      /EJECT
      *----------------------------------------------------------------
      * $SET1 - SUBROUTINE TO SET ENVIRONMENT FOR SCREEN 1
      *----------------------------------------------------------------
      *
     C     $SET1         BEGSR
      *
      *  FILL THE SUBFILE
      *
     C     TERMFL        IFEQ      NO
     C                   EXSR      $CLR1
     C                   EXSR      $FRSH1
     C                   ENDIF
      *
      *  SET ROUTINE TO DISPLAY SCREEN
      *
     C                   MOVEL     SCRN1         ROUTNE
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $SCRN1 - SUBROUTINE TO PERFORM OPERATOR I/O FOR SCREEN 1
      *----------------------------------------------------------------
      *
     C     $SCRN1        BEGSR
      *
      * IF THE USER FAILED THE INITIAL SECURITY CHECKS, SETON 99
      * TO PROTECT INPUT FIELDS AND INVALIDATE FUNCTION KEYS.
      *
     C     TERMFL        IFEQ      YES
     C                   SETON                                        99
     C                   ENDIF
      *
      * WRITE THE ERROR MESSAGES FROM THE ERROR ARRAYS TO THE
      * ERROR MESSAGE SUBFILE
      *
     C                   EXSR      $WRMSG
      *
      * WRITE SCREEN 1 TO CRT
      *
     C                   WRITE     HP446K1
     C                   WRITE     HP446EC
     C                   EXFMT     HP446C1
      *
      * SAVE THE VALUE OF THE LOWEST SUBFILE RRN ON THE SCREEN FOR
      * LATER REPOSITIONING THE SUBFILE
      *
     C                   Z-ADD     IDFLOW        WKFLOW
      *
      * CLEAR THE MESSAGES FROM THE MESSAGE DATA STRUCTURES AND PROGRAM
      * MESSAGE QUEUE.
      *
     C                   EXSR      $CLMSG
      *
      * THE USER IS NOT ALLOWED TO CHANGE AN EXISTING SALES MOVEMENT OR
      * CREATE A NEW MOVEMENT IF THE BATCH 'COPY' OF KILL DATA IS
      * RUNNING TO COPY KILL DATA FROM THE HOG PROCUREMENT AND
      * EVALUATION SYSTEM TO THE HOG PRODUCTION SYSTEM. SO, BRING IN
      * THE DATA AREA THAT CONTAINS THE FLAG WHICH INDICATES WHETHER
      * THE BATCH COPY IS CURRENTLY BEING PERFORMED.
      *
     C     *DTAARA       DEFINE                  DACOPY
     C                   IN        *DTAARA
      *
     C                   select
     C                   when      c1fsbo = 'OKLIV'
     C                   eval      dafl = daflguy
      *
     C                   when      c1fsbo = 'IFLIV'
     C                   eval      dafl = daflstp
      *
     C                   other
     C                   eval      dafl = ' '
     C                   endsl
      *
      *
      * GET USER'S ENTRY AND SET THE NEXT ROUTINE
      *
     C                   SELECT
     C     *IN03         WHENEQ    *ON                                          F3-EXIT
     C     TERMFL        OREQ      YES
     C                   MOVEL     EXIT          ROUTNE
      *
     C     *IN04         WHENEQ    *ON                                          F4-PROMPT
     C                   EXSR      $F4S1
      *
     C     FLD           IFEQ      'C1FSBO'
     C     C1FSBO        ANDNE     SVFSBO
     C                   MOVEL(P)  'HP446'       WKFNCD
     C                   EXSR      $CHKBO
     C                   ENDIF
      *
     C                   if        error = 0
      *
     C                   if        fld = 'C1CURFL' and
     C                             c1curfl <> svcurfl
     C                   exsr      $curfl
     C                   endif
      *
     C                   if        error = 0
     C                   exsr      $chksv
     C                   else
     C                   eval      routne = scrn1
     C                   endif
     C                   else
     C                   eval      routne = scrn1
     C                   endif
      *
     C     *IN05         WHENEQ    *ON                                          F5-REFRESH
     C                   MOVEL     YES           RESET
     C                   MOVEL     SVFSBO        C1FSBO
     C                   MOVEL     SVBODS        C1BODS
     C                   MOVEL     SET1          ROUTNE
      *
      *
     C     *IN06         WHENEQ    *ON                                          F6-CREATE
     C     C1FSBO        IFNE      SVFSBO
     C                   MOVEL(P)  'HP446'       WKFNCD
     C                   EXSR      $CHKBO
     C                   ENDIF
      *
      *  CHECK USER AUTHORITY TO THE FUNCTION
      *
     C     ERROR         IFEQ      0
     C                   MOVEL     C1FSBO        SVFSBO
     C                   MOVEL     C1BODS        SVBODS
     C                   MOVEL(P)  'HP146'       WKFNCD
     C                   EXSR      $SEC
      *
     C                   ENDIF
      *
     C                   IF        DAFL = YES                                   IF COPYING
     C                   ADD       1             ERROR
     C                   IF        ERROR < MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS00707'     MGID(E)
     C                   ENDIF
     C                   ENDIF                                                  IF COPYING
      *
     C                   IF        ERROR = 0
     C                   EXSR      $CREAT
     C                   ENDIF
      *
     C                   Z-ADD     WKFLOW        HDPAG1
     C                   MOVEL     SCRN1         ROUTNE
      *
     C     *IN17         WHENEQ    *ON                                          F17-BOL
     C     C1FSBO        IFNE      SVFSBO
     C                   MOVEL(P)  'HP446'       WKFNCD
     C                   EXSR      $CHKBO
     C                   ENDIF
     C                   IF        ERROR = 0
     C                   MOVEL     C1FSBO        SVFSBO                           E5190
     C                   MOVEL     C1BODS        SVBODS                           E5190
     C                   MOVEL(P)  'HP4070'      WKFNCD
     C                   EXSR      $SEC
     C                   IF        ERROR = 0
     C                   CALL      'HP4070'
     C                   parm      'N'           XXREPRINT
     C                   PARM                    VERSION
     C                   PARM                    C1VERSION
     C                   ENDIF
     C                   ENDIF
     C                   MOVEL     SCRN1         ROUTNE
      *
     C     *IN20         WHENEQ    *ON                                          F20-WRKSPLF
     C                   CALL      'QEZOUTPT'                                       API
     C                   Z-ADD     WKFLOW        HDPAG1
     C                   MOVEL     SCRN1         ROUTNE
      *
      *
      * REPOSITION THE SUBFILE IF THE USER HAS CHANGED ANY OF THE
      * POSITIONERS OR SELECTORS IN THE CONTROL RECORD.
      *
     C     SVFSBO        WHENNE    C1FSBO                                       IF RESTRICT  E5190
     C     SVCURFL       orne      C1CURFL                                      WH POSITION
     C     SVMVSN        ORNE      C1MVSN
     C     SVMSCD        ORNE      C1MSCD
     C     SVSMDY        ORNE      C1SMDY
     C     SVKMDY        ORNE      C1KMDY
     C     SVHGCD        ORNE      C1HGCD
     C     SVFSCD        ORNE      C1FSCD
     C     SVBLCD        ORNE      C1BLCD
     C     SVPPCD        ORNE      C1PPCD
     C     SVSTCD        ORNE      C1STCD
     C     SVCVNO        ORNE      C1CVNO
      *
      *
     C     SVFSBO        IFNE      C1FSBO                                       E5190
     C                   MOVE      *BLANK        C1BODS                            "
     C                   MOVEL(P)  'HP446'       WKFNCD                            "
     C                   EXSR      $CHKBO                                          "
     C                   ENDIF                                                     "
      *
     C     ERROR         IFEQ      0                                               "
     C                   MOVEL     C1FSBO        SVFSBO                           E5190
     C                   MOVEL     C1BODS        SVBODS                           E5190
     C                   EXSR      $CHKSV
     c                   else
     C                   MOVEL     SCRN1         ROUTNE
     C                   ENDIF                                                     "
      *
     C                   if        c1curfl <> svcurfl                                           nge
     C                   exsr      $curfl
     C                   endif                                                                   nge
      *
     C                   if        error = 0
     C                   exsr      $chksv
     C                   else
     C                   eval      routne = scrn1
     C                   endif
      *
     C     *IN25         WHENEQ    *ON                                          F25-ROLLUP
     C                   EXSR      $LOAD1
     C                   OTHER
      *
      * USER HAS PRESSED ENTER
      *
     C     NORECS        IFEQ      YES                                          IF EMPTY SFL
     C                   MOVEL     SET1          ROUTNE
     C                   ELSE
     C                   MOVEL     EDIT1         ROUTNE
     C                   ENDIF                                                  IF EMPTY SFL
      *
     C                   ENDSL
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * SAVE THE CONTROL RECORD POSITIONERS/SELECTORS
      *----------------------------------------------------------------
      *
     C     $CHKSV        BEGSR
      *
     C                   MOVEL     NO            RESET
     C                   MOVEL     SET1          ROUTNE
      *
      * IF THE 'SHIP DATE' SELECTOR HAS CHANGED, FLIP IT TO THE
      * CCYYMMDD FORMAT NEEDED FOR DATA COMPARISON FOR RETRIEVAL.
      *
     C                   if        svsmdy <> c1smdy
     C                   z-add     0             wkscdt
     C                   z-add     c1smdy        wkmdy
     C                   exsr      $tocymd
     C                   z-add     wkccyymmdd    wkscdt
     C                   endif                                                  IF DIFF DATE
      *
      * IF THE 'SCHEDULED KILL DATE' SELECTOR HAS CHANGED, FLIP IT TO THE
      * CCYYMMDD FORMAT NEEDED FOR DATA COMPARISON FOR RETRIEVAL.
      *
     C                   if        svkmdy <> c1kmdy
     C                   z-add     0             wksckldt
     C                   z-add     c1kmdy        wkmdy
     C                   exsr      $tocymd
     C                   z-add     wkccyymmdd    wksckldt
     C                   endif
      *
     C                   MOVEL     C1CURFL       SVCURFL
     C                   Z-ADD     C1MVSN        SVMVSN
     C                   MOVEL     C1MSCD        SVMSCD
      *
     C                   MOVEL     C1FSBO        SVFSBO                          E5190
     C                   MOVEL     C1BODS        SVBODS                             "
      *                                                                             "
     C                   MOVEL     C1HGCD        SVHGCD
     C                   Z-ADD     C1FSCD        SVFSCD
     C                   MOVEL     C1BLCD        SVBLCD
     C                   MOVEL     C1PPCD        SVPPCD
     C                   MOVEL     C1STCD        SVSTCD
     C                   Z-ADD     C1CVNO        SVCVNO
     C                   Z-ADD     C1SMDY        SVSMDY
     C                   Z-ADD     C1KMDY        SVKMDY
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $EDIT1 - SUBROUTINE TO EDIT INPUT FIELDS FOR SCREEN 1
      *----------------------------------------------------------------
      *
     C     $EDIT1        BEGSR
      *
      * FIRST, EDIT ALL THE SELECTIONS ENTERED BY THE USER IN THE
      * SUBFILE OPTION FIELD TO SEE THAT VALID SELECTIONS WERE INPUT
      *
     C                   EXSR      $EDOP1
      *
      * IF THERE ARE NO ERRORS IN THE SUBFILE OPTIONS ENTERED
      * BY THE USER, THEN EXECUTE THE SUBROUTINE TO READ AND PROCESS
      * THE SUBFILE RECORDS SELECTED BY THE USER.  AFTER COMPLETING THE
      * SUBROUTINE, SET THE ROUTINE TO START OVER.
      *
     C     ERROR         IFEQ      0                                            IF NO ERROR
     C                   EXSR      $PROC1
      *
     C     RTNCD         IFEQ      '00'
     C                   MOVEL     SET1          ROUTNE
     C                   ELSE
     C                   Z-ADD     WKFLOW        HDPAG1
     C                   MOVEL     SCRN1         ROUTNE
     C                   ENDIF
     C                   ELSE
      *
      * ELSE THERE WERE ERRORS IN THE SUBFILE OPTIONS ENTERED, SO SET
      * TO REDISPLAY SCREEN WITH ERRORS TO USER
      *
     C                   MOVEL     SCRN1         ROUTNE
     C                   ENDIF                                                  IF NO ERROR
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * Validate 'show current movements only-yes/no' flag
      *---------------------------------------------------------------
      *
     C     $curfl        begsr
      *
     C                   if        c1curfl <> yes and
     C                             c1curfl <> no
     C                   seton                                        39
     C                   if        error < maxmsg
     C                   add       1             error
     C                   eval      mgid(e) = 'HS09106'
     C                   endif
     C                   endif
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * $CLR1  - SUBROUTINE TO CLEAR THE SUBFILE ON SCREEN 1
      *---------------------------------------------------------------
      *
     C     $CLR1         BEGSR
      *
     C                   Z-ADD     0             SVRRN1
      *
     C                   CLEAR                   HP446S1
     C                   SETON                                        83        SFLCLR
     C                   WRITE     HP446C1
     C                   SETOFF                                       818283
      *                                                    81=SFLDSP
      *                                                    82=SFLEND
      *                                                    83=SFLCLR
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * $FRSH1 - SUBROUTINE TO REFRESH/FILL THE SUBFILE ON SCREEN 1
      *---------------------------------------------------------------
      *
     C     $FRSH1        BEGSR
      *
      * SET POSITION IN SALES DETAIL FILE
      *
     C                   select
     C                   when      c1curfl = yes
     C                   if        c1mvsn = 0
     C     *hival        setll     hsj085f
     C                   else
     C     c1mvsn        setll     hsj085f
     C                   endif
     C                   other
      *
     C                   if        c1mvsn = 0
     C     *hival        setll     hsj085a
     C                   else
     C     c1mvsn        setll     hsj085a
     C                   endif
     C                   endsl
      *
      *
      * THE FIELD 'RESET' IS A FLAG FIELD TO INDICATE WHETHER YOU ARE
      * ATTEMPTING TO REPOSITION THE USER CLOSE TO WHERE HE WAS IN THE
      * SUBFILE.
      * THE LOWEST SUBFILE RRN ON THE SCREEN FROM THE DSPF
      * FEEDBACK AREA (IDFLOW) WAS RETRIEVED EARLIER AND PUT IN WKFLOW.
      * ADD TO WKFLOW THE NUMBER OF LINES ON YOUR SUBFILE PAGE OR THE
      * NUMBER LESS 1 TO DETERMINE A SUBFILE RRN TO REPOSITION TO.
      * IF RESET IS 'NO', JUST START THE SUBFILE OVER
      *
     C     RESET         IFEQ      YES
     C     WKFLOW        ADD       WKRLIN        MAXRRN
     C                   ELSE
     C                   Z-ADD     WKPLIN        MAXRRN
     C                   ENDIF
      *
      * LOAD THE SUBFILE UNTIL YOU HAVE THE REQUIRED NUMBER OF RECORDS.
      * THE NUMBER WILL DEPEND ON WHETHER YOU ARE REPOSITIONING THE
      * SUBFILE OR NOT. STOP IF YOU GET TO THE END OF THE SUBFILE.
      *
     C     SVRRN1        DOUGE     MAXRRN
     C     *IN82         OREQ      *ON
     C                   EXSR      $LOAD1
     C                   ENDDO
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * $LOAD1- SUBROUTINE TO LOAD THE SUBFILE FOR SCREEN 1
      *---------------------------------------------------------------
      *
     C     $LOAD1        BEGSR
      *
     C                   Z-ADD     SVRRN1        HDRRN1
      *
      * SET THE COUNT OF RECORDS ON THE SUBFILE PAGE TO ZERO.
      *
     C                   Z-ADD     0             COUNT
      *
      * FILL THE SUBFILE UNTIL THE END OF THE DATABASE FILE OR YOU
      * HAVE FILLED ALL THE LINES ON THE SUBFILE PAGE
      *
     C                   SETOFF                                       82
     C     *IN82         DOUEQ     *ON                                          DO FILL LOOP
     C     COUNT         ORGE      WKPLIN
      *
     C                   select
     C                   when      c1curfl = yes
     C                   read      hsj085f                                82
     C                   other
     C                   read      hsj085a                                82
     C                   endsl
      *
     C     *IN82         IFEQ      *OFF                                          IF READ
     c     shfsbo        ifeq      c1fsbo                                        E5190
      *
      * RETRIEVE HOG GROUP ALPHA CODE SO IT CAN BE USED
      * IN MATCHING AGAINST THE USER'S SELECTORS
      *
     C     SGHGSN        CHAIN     HSL034D                            90
     C     *IN90         IFEQ      *OFF                                         IF GROUP
     C                   MOVEL     HGHGCD        DFHGCD
     C                   MOVEL     HGBLCD        DFBLCD
     C                   MOVEL     HGPPCD        DFPPCD
     C                   ELSE
     C                   MOVE      *BLANK        DFHGCD
     C                   MOVE      *BLANK        DFBLCD
     C                   MOVEL     *BLANK        DFPPCD
     C                   ENDIF                                                  IF GROUP
      *
      * ONLY INCLUDE RECORDS THAT MATCH THE USER'S SELECTORS
      *
     C     C1MSCD        IFNE      *BLANK                                       IF STATUS
     C     C1MSCD        ANDNE     SHMSCD
     C                   ELSE
      *
     C     C1SMDY        IFNE      0                                            IF SCHED
     C     SHMSCD        ANDEQ     'SC'
     C     WKSCDT        ANDNE     SHSCDT
     C                   ELSE
      *
     C     C1SMDY        IFNE      0                                            IF SHIPPED
     C     SHMSCD        ANDNE     'SC'
     C     WKSCDT        ANDNE     SHSHDT
     C                   ELSE
      *
     C     C1KMDY        IFNE      0                                            IF SCHED KILL
     C     WKSCKLDT      ANDNE     SHSCKLDT
     C                   ELSE
      *
     C     C1PPCD        IFNE      *BLANK                                       IF PHASE
     C     C1PPCD        ANDNE     DFPPCD
     C                   ELSE
      *
     C     C1FSCD        IFNE      0                                            IF FARM
     C     C1FSCD        ANDNE     SHFSCD
     C                   ELSE
      *
     C     C1HGCD        IFNE      *BLANK                                       IF GROUP
     C     C1HGCD        ANDNE     DFHGCD
     C                   ELSE
      *
     C     C1BLCD        IFNE      *BLANK                                       IF BLDG
     C     C1BLCD        ANDNE     DFBLCD
     C                   ELSE
      *
     C     C1STCD        IFNE      *BLANK                                       IF SALES TYP
     C     C1STCD        ANDNE     SHSTCD
     C                   ELSE
      *
     C     C1CVNO        IFNE      0                                            IF CUSTOMER
     C     C1CVNO        ANDNE     SHCVNO
     C                   ELSE
      *
     C                   ADD       1             COUNT
     C                   ADD       1             HDRRN1
     C                   MOVE      *BLANK        DFOPT
     C                   Z-ADD     SHSCKLDT      HDSCKLDT
     C                   Z-ADD     SHMVSN        DFMVSN
     C                   MOVEL     SHMSCD        DFMSCD
     C                   Z-ADD     SHFSCD        DFFSCD
     C                   MOVEL     SHSTCD        DFSTCD
     C                   Z-ADD     SHCVNO        DFCVNO
     C                   Z-ADD     SGSCHD        DFSCHD
      *
      *  FLIP EITHER 'SCHEDULED SHIP DATE' OR 'SHIPPED DATE'
      *  DEPENDING ON STATUS AND PUT IN THE 'SHIP DATE' COLUMN
      *
     C     SHMSCD        IFEQ      'SC'
     C                   Z-ADD     SHSCDT        wkccyymmdd
     C                   ELSE
     C                   Z-ADD     SHSHDT        wkccyymmdd
     C                   ENDIF
      *
     C                   exsr      $tomdy
     C                   z-add     wkmdy         dfsmdy
      *
      *  FLIP SCHEDULED KILL DATE FROM CCYYMMDD TO MMDDYY
      *
     C                   z-add     0             dfkmdy
     C                   if        shsckldt <> 0
     C                   z-add     shsckldt      wkccyymmdd
     C                   exsr      $tomdy
     C                   z-add     wkmdy         dfkmdy
     C                   endif
      *
     C                   WRITE     HP446S1
     C                   SETON                                        81
      *
      * FOR POSITIONING, PLACE THE SUBFILE RELATIVE RECORD NUMBER INTO
      * THE SFLRCDNBR FIELD (HDPAG1)
      *
     C                   Z-ADD     HDRRN1        HDPAG1
      *
     C                   ENDIF                                                  IF CUSTOMER
     C                   ENDIF                                                  IF SALES TYP
     C                   ENDIF                                                  IF BLDG
     C                   ENDIF                                                  IF GROUP
     C                   ENDIF                                                  IF FARM
     C                   ENDIF                                                  IF PHASE
     C                   ENDIF                                                  IF SCHED KILL
     C                   ENDIF                                                  IF SHIPPED
     C                   ENDIF                                                  IF SCHED
     C                   ENDIF                                                  IF STATUS
     C                   ENDIF                                                  IF BusOff
     C                   ENDIF                                                  IF READ
     C                   ENDDO                                                  DO FILL LOOP
      *
      * IF NO RECORDS WERE WRITTEN TO THE SUBFILE, THEN ISSUE
      * THE STANDARD MESSAGE 'NO DATA TO DISPLAY'.
      *
     C     HDRRN1        IFEQ      0
     C                   MOVEL     YES           NORECS
      *
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS09002'     MGID(E)
     C                   ENDIF
     C                   ELSE
     C                   MOVEL     NO            NORECS
     C                   ENDIF
      *
     C                   Z-ADD     HDRRN1        SVRRN1
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $CREAT - SUBROUTINE TO CALL MAINTENANCE PROGRAM WHEN THE USER
      *          HAS TAKEN F6=CREATE
      *----------------------------------------------------------------
      *
     C     $CREAT        BEGSR
      *
     C                   MOVEL     '00'          RTNCD
     C                   MOVEL     CREATE        MODE
     C                   Z-ADD     0             DFMVSN
     C                   MOVE      *BLANK        DFMSCD
     C                   MOVEL     BOCOCD        XXCOCD
     C                   CALL      'HP146'
     C                   PARM                    MODE
     C                   PARM                    XXCOCD
     C                   PARM                    DFMVSN
     C                   PARM                    DFMSCD
     C*                  PARM                    xxbocd
     C                   PARM                    c1fsbo
     C                   PARM                    RTNCD
     C                   PARM                    VERSION
     C                   PARM                    C1VERSION
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $CHKBO - CHECK THE BUSINESS OFFICE ENTERED AS A RESTRICTOR    subr added E5190
      *----------------------------------------------------------------
      *
      * THE BUSINESS OFFICE ENTERED MUST:
      *    BE A VALID OFFICE IN THE BUSINESS OFFICE MASTER
      *    BE A 'LIVE PRODUCTION' DIVISION OFFICE
      *    HAVE A SECURITY LOCATION TO WHICH THE USER IS AUTHORIZED
      *
     C     $CHKBO        BEGSR
      *
      * CLEAR SUBFILE
      *
     C                   EXSR      $CLR1
      *
      * CHECK THE USER'S SECURITY TO THIS BUSINESS OFFICE.
      * SET INDVFL TO YES TO TELL SECURITY PROGRAM TO COMPARE THE
      * FUNCTION DIVISION CODE TO THE B.O. DIVISION CODE.
      *
     C                   MOVEL     YES           INDVFL
     C                   EXSR      $SEC
     C     OUBOFL        IFEQ      YES
     C                   SETON                                        31
     C                   ENDIF
     C                   MOVEL     NO            INDVFL                          RESET FL
      *
     C                   MOVEL     OUBODS        C1BODS
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * $EDOP1 - SUBROUTINE TO READ CHANGED RECORDS IN SUBFILE AND
      *          EDIT FOR VALID SUBFILE OPTIONS FOR SCREEN 1
      *---------------------------------------------------------------
      *
     C     $EDOP1        BEGSR
      *
     C                   Z-ADD     0             ERROR
      *
      * READ ALL THE CHANGED SUBFILE RECORDS EDITING FOR VALID SUBFILE
      * OPTIONS
      *
     C                   SETOFF                                       80
     C     *IN80         DOUEQ     *ON                                          DO READC
     C                   READC     HP446S1                                80
     C     *IN80         IFEQ      *OFF                                         IF REC READ
      *
      * SET OFF ERROR INDICATOR FOR SUBFILE SELECTOR
      *
     C                   SETOFF                                       30
      *
      * CONVERT OPTION USER ENTERED TO STANDARD FORMAT. FOR EXAMPLE,
      * A ' 1', '01', AND '1 ' MUST BE STANDARDIZED TO ' 1'.
      *
     C     WKOPT2        IFEQ      *BLANK                                       IF 2 = BLANK
     C                   MOVE      WKOPT1        WKOPT2
     C                   MOVE      *BLANK        WKOPT1
     C                   ENDIF                                                  IF 2 = BLANK
      *
     C     WKOPT1        IFEQ      *ZERO                                        IF 1 IS ZERO
     C                   MOVE      *BLANK        WKOPT1
     C                   ENDIF                                                  IF 1 IS ZERO
      *
      * TEST FOR VALID SUBFILE OPTION ENTRY ON ALL CHANGED RECORDS
      *
     C     DFOPT         IFNE      ' 2'                                         IF BAD OPT
     C     DFOPT         ANDNE     ' 5'
     C     DFOPT         ANDNE     ' 7'
     C     DFOPT         ANDNE     ' 9'
     C     DFOPT         ANDNE     '  '
     C                   SETON                                        30
      *
     C                   IF        ERROR < MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS09006'     MGID(E)
     C                   EXSR      $ERR1
     C                   ENDIF
     C                   ELSE
      *
      * For Options 2, 7 and 9, make additional edits when:
      *   1) the Version is restricted
      *   2) the customer is Seaboard
      *   3) the sale type is a market sale
      *
      * Note: Option 7 now calls a display only, so no no need to check E16108
     C                   if        version = 'R' and
     C*****************            (dfopt = ' 2' or dfopt = ' 7' or dfopt =' 9')
     C                             (dfopt = ' 2' or dfopt =' 9')
     C                             and dfcvno = 360516
     C                             and dfstcd = 'MRKTS'
     C                   exsr      $restricted
     C                   endif
      *
      *
      * Option 7 is only valid for Seaboard Market Sales
      *
     C                   if        dfopt = ' 7' and                             If opt 7
     C                             (dfcvno <> 360516 or dfstcd <> 'MRKTS')
     C                   seton                                        30
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS00886'     mgid(e)
     C                   exsr      $err1
     C                   endif
     C                   endif                                                  If opt 7
      *
      * Make additional validations for Option 9-Change Status to FP
      *
     C                   if        dfopt = ' 9' and                             If opt 9
     C                             dfmscd <> 'NC'
     C                   seton                                        30
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS00888'     mgid(e)
     C                   exsr      $err1
     C                   endif
     C                   endif                                                  If opt 9
      *
      *
      * CHECK THE USER'S AUTHORITY TO THE FUNCTION
      *
     C     DFOPT         IFEQ      ' 2'                                         IF OPTION
     C     DFOPT         OREQ      ' 5'
      *
     C                   SELECT
     C     DFOPT         WHENEQ    ' 2'
     C                   MOVEL(P)  'HP146'       WKFNCD
      *
     C     DFOPT         WHENEQ    ' 5'
     C                   MOVEL(P)  'HP746'       WKFNCD
     C                   ENDSL
      *
     C                   EXSR      $SEC
      *
     C     SECFL         IFEQ      YES                                          IF PROBLEM
     C                   SETON                                        30
     C                   EXSR      $ERR1
     C                   ELSE
      *
      * PERFORM ADDITIONAL VALIDATIONS FOR OPTION 2=REVISE
      * THE USER CANNOT REVISE IF:
      *     1) THE SALES MOVEMENT IS POSTED
      *     2) THE BATCH COPY OF DATA FROM THE HOG PROCUREMENT
      *        AND EVALUATION SYSTEM TO THE HOG PRODUCTION
      *        SYSTEM IS IN PROGRESS.
      *
     C     DFOPT         IFEQ      ' 2'                                         IF REVISE
     C     DFMSCD        CHAIN     HSP053                             90
      *
     C     *IN90         IFEQ      *OFF                                         IF POSTED
     C     MSPOFL        ANDEQ     YES
     C                   SETON                                        30
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS00628'     MGID(E)
     C                   CLEAR                   MGW
     C                   MOVEA     DFMSCD        MGW(1)
     C                   MOVEA     MGW           MGDT(E)
     C                   EXSR      $ERR1
     C                   ENDIF
     C                   ELSE
      *
     C     DAFL          IFEQ      YES                                          IF COPYING
     C                   SETON                                        30
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR
     C                   MOVEL     'HS00707'     MGID(E)
     C                   EXSR      $ERR1
     C                   ENDIF
     C                   ENDIF                                                  IF COPYING
      *
     C                   ENDIF                                                  IF POSTED
     C                   ENDIF                                                  IF REVISE
      *
      *
     C                   ENDIF                                                  IF PROBLEM
     C                   ENDIF                                                  IF OPTION
     C                   ENDIF                                                  IF BAD OPT
      *
      * REWRITE THE SUBFILE RECORD WITH THE MODIFIED DATA TAG ON SO
      * IT WILL BE REREAD AND PROCESSED IN THE SUBROUTINE $PROC1.
      *
     C     ERROR         IFEQ      0
     C     DFOPT         ANDEQ     *BLANK
     C                   SETOFF                                       85
     C                   ELSE
     C                   SETON                                        85
     C                   ENDIF
      *
     C                   UPDATE    HP446S1
     C                   SETOFF                                       8530
      *
     C                   ENDIF                                                  IF REC READ
     C                   ENDDO                                                  DO READC
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------------
      * Validations exclusive to the 'restricted' version of the function
      *----------------------------------------------------------------------
      *
      * These edits only apply when:
      *   1) the customer is Seaboard
      *   2) it is a market sale
      *
     C     $restricted   begsr
      *
      * If REVISE, check to see if a Scheduled Movement is in the Tattoo Header.
      * If it is, then we will let the user Revise it.
      *
     C                   move      yes           editfl
      *
     C                   if        dfopt=' 2' and dfmscd='SC'
     C     dfmvsn        chain     pka1cpk2                           92
     C                   if        *in92 = *off
     C                   move      no            editfl
     C                   endif
     C                   endif
      *
      * Check Delivery Tracking
      *
     C                   if        editfl = yes                                 If edit more
     C                   call      'HP279'
     C                   parm                    hdsckldt
     C                   parm      *blank        xxokforhps
      *
     C                   if        xxokforhps = no
     C                   seton                                        30
     C                   if        error < maxmsg
     C                   add       1             error
     C                   movel     'HS00884'     mgid(e)
     C                   exsr      $err1
     C                   endif
     C                   endif
     C                   endif                                                  If edit more
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * $PROC1 - SUBROUTINE TO READ SUBFILE RECORDS AND PROCESS
      *          USER SELECTIONS FOR SCREEN 1
      *---------------------------------------------------------------
      *
     C     $PROC1        BEGSR
      *
     C                   Z-ADD     0             ERROR
     C                   MOVEL     '00'          RTNCD
      *
      * READ ALL THE CHANGED SUBFILE RECORDS AND PROCESS
      *
     C                   SETOFF                                       80
     C     *IN80         DOUEQ     *ON                                          DO PROCESS
     C                   READC     HP446S1                                80
     C     *IN80         IFEQ      *OFF                                         IF REC READ
      *
     C     RTNCD         IFEQ      '00'                                         IF ENTER
      *
      * IF USER SELECTED OPTION 2=REVISE, SET PARM FOR REVISE MODE
      * AND SET THE FLAG 'RESET' TO YES SO THAT WHEN YOU RETURN TO THIS
      * WORK WITH YOU WILL REPOSITION TO THE SAME PLACE IN THE SUBFILE.
      *
     C     DFOPT         IFEQ      ' 2'
     C                   MOVEL     YES           RESET
     C                   MOVEL     REVISE        MODE
     C                   MOVEL     BOCOCD        XXCOCD
     C                   MOVEL     DFMSCD        XXMSCD
     C                   CALL      'HP146'
     C                   PARM                    MODE
     C                   PARM                    XXCOCD
     C                   PARM                    DFMVSN
     C                   PARM                    XXMSCD
     C*                  PARM                    xxbocd
     C                   PARM                    c1fsbo
     C                   PARM                    RTNCD
     C                   PARM                    VERSION
     C                   PARM                    C1VERSION
     C                   ENDIF
      *
      * IF USER SELECTED OPTION 5=VIEW SALES,
      * SET THE FLAG 'RESET' TO YES SO THAT WHEN YOU RETURN TO THIS
      * WORK WITH YOU WILL REPOSITION TO THE SAME PLACE IN THE SUBFILE.
      *
     C     DFOPT         IFEQ      ' 5'
     C                   MOVEL     YES           RESET
     C                   CALL      'HP746'
     C                   PARM                    DFMVSN
     C                   PARM                    DFMSCD
     C                   PARM                    RTNCD
     C                   ENDIF
      *
      * IF USER SELECTED OPTION 7=EDIT DELIVERY TRACKING, CALL A SYNON PROGRAM.
      * SET THE FLAG 'RESET' TO YES SO THAT WHEN YOU RETURN TO THIS
      * WORK WITH YOU WILL REPOSITION TO THE SAME PLACE IN THE SUBFILE.
      *
     C                   goto      newopt7
     C     DFOPT         IFEQ      ' 7'
     C                   MOVEL     YES           RESET
     C                   CALL      'PLA0XFR'
     C                   PARM      *blank        xxsynrtn
     C                   PARM                    dfmvsn
     C                   PARM      360           xxsyncono
     C                   PARM      0             xxsynbuy
     C                   PARM      0             xxsynload
     C                   parm      'Y'           xxsynsts
     C                   parm      ' '           xxsynast
      *
     C                   if        xxsynrtn <> *blank
     C                   call      'HP705'
     C                   endif
      *
     C                   ENDIF
      *
      ** New option 7 replaces Edit Delivery Tracking with Display Delivery Tracking
      *
     C     newopt7       tag
     C     dfopt         ifeq      ' 7'
      *
      * Get delivery tracking key by Movement#
     C                   call      'PWKFXFR'
     C                   parm      *blank        xxsynrtn
     C                   parm                    dfmvsn
     C                   parm      360           xxsyncono
     C                   parm      0             xxsynbuy
     C                   parm      0             xxsynload
      *
     C                   if        xxsynrtn <> *blank
     C                   call      'HP705'
     C                   endif
     C*    dfmvsn        chain     pka1cpk2
     C*                  if        %found(pka1cpk2)                             if %found(plafcpl3)
     C*                  eval      xxsyncono  = a1hmnb
     C*                  eval      xxsyndlvdt = a1e9dt
     C*                  eval      xxsyndlvtm = a1ajth
     C*                  eval      xxsyndlvsq = a1h4a1
      *
     C*                  exsr      $tosyndt
     C*                  eval      xxsynkldt = xxsyndt
      *
     C*                  movel     yes           reset
     C*                  call      'PWKCD1R'
     C*                  parm      *blank        xxsynrtn
     C*                  parm      360           xxsyncono
     C*                  parm                    xxsynkldt
     C*                  parm                    xxsyndlvdt
     C*                  parm                    xxsyndlvtm
     C*                  parm                    xxsyndlvsq
     C*                  endif                                                  fi %found(plafcpl3)
     C                   endif                                                  fi dfopt = 7
      *
      * Option 9=Change Status to FP
      *
     C                   if        dfopt = ' 9'                                 If Opt 9
     C                   movel     yes           reset
     C                   call      'HP147'
     C                   parm                    dfmvsn
     C                   parm                    rtncd
     C                   endif                                                  If Opt 9
      *
      * UPON RETURNING FROM THE CALLED PROGRAM,
      * CLEAR THE SUBFILE OPTION SELECTOR ENTERED BY THE USER AND
      * REWRITE THE SUBFILE RECORD
      *
     C                   MOVE      *BLANK        DFOPT
     C                   SETOFF                                       85
     C                   ELSE
     C                   SETON                                        85
     C                   ENDIF                                                  IF ENTER
      *
     C                   UPDATE    HP446S1
      *
     C                   ENDIF                                                  IF REC READ
     C                   ENDDO                                                  DO PROCESS
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * $F4S1 - F4 PROMPT ON SCREEN 1
      *---------------------------------------------------------------
      *
     C     $F4S1         BEGSR
      *
     C                   SELECT
      *
      *
      * IF BUSINESS OFFICE CODE                               E5190
      *
     C     FLD           WHENEQ    'C1FSBO'
     C                   MOVE      *BLANK        XXFSBO
     C                   MOVE      *BLANK        XXBODS
     C                   CALL      'HP503'
     C                   PARM                    XXFSBO
     C                   PARM                    XXBODS
     C                   PARM                    SVDVCD
     C                   SETON                                        51
     C     XXFSBO        IFNE      *BLANK                                       IF SELECTED
     C                   MOVEL     XXFSBO        C1FSBO
     C                   MOVEL     XXBODS        C1BODS
     C                   ENDIF                                                  IF SELECTED
      *
      * Show current tickets only yes/no flag
     C                   when      fld = 'C1CURFL'
     C                   call      'HP522'
     C                   parm                    xxcurfl
     C                   seton                                        59
     C                   if        xxcurfl <> *blank
     C                   movel     xxcurfl       c1curfl
     C                   endif
      *
      * MOVEMENT STATUS
      *
     C     FLD           WHENEQ    'C1MSCD'
     C                   MOVEL     'S'           XXMTCD
     C                   MOVE      *BLANK        XXMSCD
     C                   MOVE      *BLANK        XXMSDS
     C                   CALL      'HP542'
     C                   PARM                    XXMTCD
     C                   PARM                    XXMSCD
     C                   PARM                    XXMSDS
     C                   SETON                                        51
     C     XXMSCD        IFNE      *BLANK                                       IF SELECTED
     C                   MOVEL     XXMSCD        C1MSCD
     C                   ENDIF                                                  IF SELECTED
      *
      * HOG GROUP
      *
     C     FLD           WHENEQ    'C1HGCD'
     C****               MOVE      *BLANK        XXFSBO
     C                   MOVE      C1fsbo        XXFSBO                          E5190
     C                   Z-ADD     0             WKFSCD
     C                   MOVE      *BLANK        XXHGCD
     C                   CALL      'HP541'
     C                   PARM                    XXFSBO
     C                   PARM                    WKFSCD
     C                   PARM                    XXHGCD
     C                   SETON                                        52
      *
     C     XXHGCD        IFNE      *BLANK                                       IF SELECTED
     C                   MOVEL     XXHGCD        C1HGCD
     C                   ENDIF                                                  IF SELECTED
      *
      * FARM SITE
      *
     C     FLD           WHENEQ    'C1FSCD'
     C                   CALL      'HP5040'
     C                   PARM      *blank        xxaist
     C                   PARM      0             WKFSCD
     C                   PARM      *blank        XXFSNM
     C                   SETON                                        53
     C     WKFSCD        IFNE      0                                            IF SELECTED
     C                   Z-ADD     WKFSCD        C1FSCD
     C                   ENDIF                                                  IF SELECTED
      *
      * PRODUCTION PHASE
      *
     C     FLD           WHENEQ    'C1PPCD'
     C                   MOVE      *BLANK        XXPPCD
     C                   MOVE      *BLANK        XXPPDS
     C                   CALL      'HP507'
     C                   PARM                    XXPPCD
     C                   PARM                    XXPPDS
     C                   SETON                                        54
     C     XXPPCD        IFNE      *BLANK                                       IF SELECTED
     C                   MOVEL     XXPPCD        C1PPCD
     C                   ENDIF                                                  IF SELECTED
      *
      * SALES TYPE
      *
     C     FLD           WHENEQ    'C1STCD'
     C                   MOVE      *BLANK        XXSTCD
     C                   MOVE      *BLANK        XXSTDS
     C                   CALL      'HP544'
     C                   PARM                    XXSTCD
     C                   PARM                    XXSTDS
     C                   SETON                                        56
     C     XXSTCD        IFNE      *BLANK                                       IF SELECTED
     C                   MOVEL     XXSTCD        C1STCD
     C                   ENDIF                                                  IF SELECTED
      *
      *
      * JDE CUSTOMER NUMBER
      *
     C     FLD           WHENEQ    'C1CVNO'
     C                   MOVEL(P)  'C'           INAT1                          SEARCH TYPE
     C                   CALL      'HP513'
     C                   PARM      BOCOCD        XXCOCD                         COMPANY
     C                   PARM                    INAT1                          SEARCH TYPE
     C                   PARM      *ZERO         OUAJD                          JDE NUMBER
     C                   PARM      *BLANK        OUALPH                         JDE NAME
      *
     C                   SETON                                        55
     C     OUAJD         IFNE      0                                            IF SELECTED
     C                   Z-ADD     OUAJD         C1CVNO
     C                   ENDIF                                                  IF SELECTED
     C                   OTHER
      *
      * F4 NOT VALID FOR THIS FIELD SO ISSUE MESSAGE
      *
     C     ERROR         IFLT      MAXMSG
     C                   ADD       1             ERROR                                        013
     C                   MOVEL     'HS09011'     MGID(E)
     C                   ENDIF
      *
     C                   ENDSL
      *
     C                   ENDSR
      /EJECT
      *----------------------------------------------------------------
      * $ERR1  - SUBROUTINE TO SET THE SUBFILE TO REDISPLAY AT THE
      *          FIRST PAGE THAT HAS ERRORS ON SCREEN 1
      *----------------------------------------------------------------
      *
     C     $ERR1         BEGSR
      *
     C     ERROR         IFEQ      1
     C                   Z-ADD     HDRRN1        HDPAG1
     C                   ENDIF
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * Flip date from MMDDYY to CCYYMMDD
      *---------------------------------------------------------------
      *
      * Test the field for a valid date; if it is valid, flip it from
      * MMDDYY to CCYYMMDD.
      *
     C     $tocymd       begsr
      *
     C     *mdy          test(d)                 wkmdy                  92
     C                   if        *in92 = *off                                 If good date
     C     *mdy          move      wkmdy         wkisodate
     C                   move      wkisodate     wkccyymmdd
     C                   else
     C                   z-add     wkmdy         wkccyymmdd
     C                   endif                                                  If good date
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Flip date from CCYYMMDD to MMDDYY
      *---------------------------------------------------------------
      *
      * Test the field for a valid date; if it is valid, flip it from
      * CCYYMMDD to MMDDYY.
      *
     C     $tomdy        begsr
      *
     C                   z-add     0             wkmdy
      *
     C     *iso          test(d)                 wkccyymmdd             92
     C                   if        *in92 = *off                                 If OK date
     C                   move      wkccyymmdd    wkisodate
     C     *mdy          move      wkisodate     wkmdy
     C                   endif                                                  If OK date
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * Flip date from MMDDYY to CYYMMDD
      *---------------------------------------------------------------
      *
      * Test the field for a valid date; if it is valid, flip it from
      * MMDDYY to CYYMMDD.
      *
     C     $tosyndt      begsr
      *
     C     *mdy          test(d)                 wkmdy                  92
     C                   if        *in92 = *off                                 If good date
           eval xxsyndt = %dec(%char(%date(wkmdy:*mdy):*cymd0):7:0);
     C     *mdy          move      wkmdy         wkisodate
     C                   move      wkisodate     wkccyymmdd
     C                   else
     C                   z-add     wkmdy         wkccyymmdd
     C                   endif                                                  If good date
      *
     C                   endsr
      /EJECT
      *---------------------------------------------------------------
      * SUBROUTINE TO SET ON LR AND END JOB
      *---------------------------------------------------------------
      *
     C     $EXIT         BEGSR
     C                   SETON                                        LR
     C                   ENDSR
      *
      *---------------------------------------------------------------
      * $WRMSG - SUBROUTINE TO WRITE ERROR MESSAGES
      *---------------------------------------------------------------
      *
     C     $WRMSG        BEGSR
      *
     C                   CALL      'UT80045J'                           43
     C                   PARM                    MGID
     C                   PARM                    MGDT
     C                   PARM                    ERROR
     C                   PARM      SDPGM         PGM
     C                   PARM      MSGFIL        MSGFL
      *
      * IF CALL TO UT80045J FAILED, A MESSAGE HARDCODED IN THE DSPF
      * COMMAND KEY RECORD WILL BE SET ON. SO, REDISPLAY SCREEN TO USER
      *
     C     *IN43         IFEQ      *ON
     C                   MOVEL     SET1          ROUTNE
     C                   ENDIF
      *
     C                   ENDSR
      *
      *----------------------------------------------------------------
      * $CLMSG - SUBROUTINE TO CLEAR MESSAGES
      *----------------------------------------------------------------
      *
     C     $CLMSG        BEGSR
      *
     C                   CALL      'UT80045J'                           43
     C                   PARM      *BLANK        MGI
     C                   PARM      *BLANK        MGD
     C                   PARM      *ZERO         ERROR
     C                   PARM      SDPGM         PGM
     C                   PARM      MSGFIL        MSGFL
      *
      * IF CALL TO UT80045J FAILED, A MESSAGE HARDCODED IN THE DSPF
      * COMMAND KEY RECORD WILL BE SET ON. SO, REDISPLAY SCREEN TO USER
      *
     C     *IN43         IFEQ      *ON
     C                   MOVEL     SET1          ROUTNE
     C                   ENDIF
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * $SEC   - CALL SECURITY PROGRAM
      *---------------------------------------------------------------
      *
     C     $SEC          BEGSR
      *
      * SETUP THE FIELDS TO SEND INTO THE SECURITY PROGRAM
      *
     C                   MOVEL     WKFNCD        INFNCD                         PGM NAME
     C                   MOVEL     C1FSBO        INBOBO                         E5190
     C                   MOVE      *BLANK        OUBOBO                         BO CODE   E5190
     C                   MOVE      *BLANK        OUBODS                         BO DESC   E5190
     C                   MOVEL     NO            TERMFL            1            TERMINAL FLG
      *
     C                   CALL      'HPSEC  '
      **** IN
     C                   PARM                    MGID                           MSGID ARRAY
     C                   PARM                    MGDT                           MSGDTA DATA
     C                   PARM                    ERROR                          # OF ERRORS
     C                   PARM                    INFNCD           10            FUNC CODE
     C                   PARM                    INBOBO            5            BO CODE
     C                   PARM                    INDVFL            1            CHECK DIV?
     C                   PARM                    INBOFL            1            CHECK B.O.?
      **** OUT
     C                   PARM                    OUBOBO            5            BO CODE
     C                   PARM                    OUBODS           30            BO DESC.
     C                   PARM                    OUDVCD            5            DIV CODE
     C                   PARM                    SECFL             1            SEC FLAG
     C                   PARM                    OUBOFL            1            B.O. ERR FLG
     C                   PARM                    OURMFL            1
      *
     C                   ENDSR
      /EJECT
      *---------------------------------------------------------------
      * INITIALIZATION SUBROUTINE
      *---------------------------------------------------------------
      *
     C     *INZSR        BEGSR
      *
      * Parms
     C     *entry        plist
     C                   parm                    version           1
      *
     C                   select
     C                   when      version = 'R'
     C                   eval      c1version = 'Restricted Version'
     C                   other
     C                   eval      c1version = 'Full Version'
     C                   endsl
      *
      * Delivery Tracking key list
     c     key01         klist
     c                   kfld                    xxsyncono
     c                   kfld                    xxsynbuy
     c                   kfld                    xxsynload
     c                   kfld                    xxsynkldt
      *
      *
      * GLOBAL DEFINES
      *
     C                   MOVE      *BLANK        MODE              7            PARM
     C                   MOVE      *BLANK        PGM              10            USED IN MSG
     C                   MOVE      *BLANK        MSGFL            10            USED IN MSG
     C                   MOVE      *BLANK        ROUTNE            6            ROUTINE
     C                   Z-ADD     0             MAXRRN            4 0          REPOSITION
     C                   MOVEL     NO            RESET             1            REPOSITION
     C                   MOVEL     NO            NORECS            1            NO RECORDS
     C     *LIKE         DEFINE    IDFLOW        WKFLOW
     C                   MOVEL     '00'          RTNCD             2            RETURN CODE
     C     *LIKE         DEFINE    HDRRN1        SVRRN1
     C                   Z-ADD     0             COUNT             2 0
      *
      * DEFINE SAVE FIELDS
      *
     C     *LIKE         DEFINE    C1FSBO        SVFSBO                            E5190
     C     *LIKE         DEFINE    C1BODS        SVBODS                            E5190
     C     *LIKE         DEFINE    C1MVSN        SVMVSN
     C     *LIKE         DEFINE    C1MSCD        SVMSCD
     C     *LIKE         DEFINE    C1HGCD        SVHGCD
     C     *LIKE         DEFINE    C1FSCD        SVFSCD
     C     *LIKE         DEFINE    C1BLCD        SVBLCD
     C     *LIKE         DEFINE    C1PPCD        SVPPCD
     C     *LIKE         DEFINE    C1STCD        SVSTCD
     C     *LIKE         DEFINE    C1CVNO        SVCVNO
     C     *LIKE         DEFINE    C1SMDY        SVSMDY
     C     *LIKE         DEFINE    C1KMDY        SVKMDY
     C     *LIKE         DEFINE    C1CURFL       SVCURFL
      *
      * DEFINE FIELDS FOR USE AS PARMS WHEN CALLING PROMPT PROGRAMS
      *
     C                   MOVE      *BLANK        XXOKFORHPS        1
     C     *LIKE         DEFINE    C1BODS        XXBODS                          e5190
     C     *LIKE         DEFINE    C1FSCD        xxFSCD                          e5190
     C     *LIKE         DEFINE    SHMVSN        XXMVSN
     C     *LIKE         DEFINE    BOCOCD        XXCOCD
     C     *LIKE         DEFINE    C1MSCD        XXMSCD
     C                   MOVE      *BLANK        XXMSDS           20
     C                   MOVE      *BLANK        XXMTCD            1
     C     *LIKE         DEFINE    SHFSBO        XXFSBO
     C     *LIKE         DEFINE    SHFSCD        WKFSCD
     C                   MOVE      *BLANK        XXFSNM           30
     C     *LIKE         DEFINE    HGPPCD        XXPPCD
     C                   MOVE      *BLANK        XXPPDS           30
     C     *LIKE         DEFINE    HGHGCD        XXHGCD
     C     *LIKE         DEFINE    HGDVCD        XXDVCD
     C                   MOVEL     'LIVE '       XXDVCD
     C                   MOVE      *BLANK        XXBODS           30
     C     *LIKE         DEFINE    C1STCD        XXSTCD
     C                   MOVE      *BLANK        XXSTDS           20
     C     *LIKE         DEFINE    C1CURFL       XXCURFL
     C                   MOVE      *BLANK        XXAIST            1
     C                   MOVE      *BLANK        xxreprint         1
      *
      * JDE FIELDS
      *
     C                   MOVE      *BLANK        INAT1             3            JDE ADDR TYP
     C                   MOVE      *BLANK        OUALPH           40            JDE NAME
     C     *LIKE         DEFINE    SHCVNO        OUAJD                          JDE NUMBER
      *
      * TO SUPPORT F5-REFRESH, LOGIC REQUIRES THE NUMBER OF LINES ON
      * THE SUBFILE PAGE AND ALSO THE NUMBER OF LINES LESS 1.  BY
      * SETTING THESE VALUES UP AS CONSTANT WORK FIELDS, THE PROCESSING
      * LOGIC CAN BE STANDARDIZED BETWEEN PROGRAMS.
      *
     C                   Z-ADD     10            WKPLIN            2 0          SFLPAG
     C     WKPLIN        SUB       1             WKRLIN            2 0          SFLPAG - 1
      *
      * THE FOLLOWING STANDARD CODE MUST BE INCLUDED TO MAKE THE
      * STANDARD ERROR MESSAGE HANDLING PROGRAM FUNCTION PROPERLY.
      * THIS CODE SETS AND CLEARS THE PROGRAM MESSAGE QUEUE.
      *
     C                   Z-ADD     20            MAXMSG            2 0
     C                   MOVEL     '*'           MSGPGM
     C                   EXSR      $CLMSG
      *
     C     *LIKE         DEFINE    OUDVCD        SVDVCD                          e5190
      *
      * EXECUTE THE SECURITY SUBROUTINE TO DETERMINE USER
      * AUTHORIZATIONS.
      *
     C                   MOVEL(P)  SDPGM         WKFNCD           10
     C                   MOVEL     NO            INBOFL
     C                   EXSR      $SEC
     C                   MOVEL     YES           INBOFL                              e5190
      *
      * IF ERRORS ON INITIAL CALL, SET TERMINAL FLAG TO YES.
      *
     C     SECFL         IFEQ      YES
     C                   MOVEL     YES           TERMFL
     C                   ENDIF
      *
      * MOVE RETURNED FIELDS TO SCREEN (AND SAVE IN WORK                       E5190
      * FIELDS FOR USE WITH F5=REFRESH LOGIC)                                 "
      *                                                                      "
     C                   MOVEL     OUBOBO        C1FSBO                      "
     C                   MOVEL     OUBOBO        SVFSBO                      "
     C                   MOVEL     OUBOBO        xxbocd                      "
     C                   MOVEL     OUBODS        C1BODS                      "
     C                   MOVEL     OUBODS        SVBODS                       E5190
      *
      * SAVE THIS FUNCTION'S DIVISION CODE
      *
     C                   MOVEL     OUDVCD        SVDVCD
      *
      *
      * GET THE COMPANY CODE/COST CENTER FROM THE USER'S DEFAULT B/O
      * TO USE WITH RETRIEVING JDE CUSTOMER INFO
      *
     C     OUBOBO        CHAIN     HSP009                             90
     C     *IN90         IFEQ      *OFF
     C                   MOVEL     BOCOCD        XXCOCD
     C                   ELSE
     C                   MOVE      *BLANK        XXCOCD
     C                   ENDIF
      *
      * Default 'show current movements only' flag to YES
      *
     C                   move      yes           c1curfl
     C                   move      yes           svcurfl
      *
     C                   ENDSR
      /EJECT
